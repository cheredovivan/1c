//@strict-types

#Область ОписаниеПеременных

&НаКлиенте
Перем КешированныеЗначения; // см. ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения

&НаКлиенте
Перем АналитикаТекущейСтрокиТоваров; // см. РетроБонусыКлиентСервер.СтруктураСохраняемойСтрокиТоваров

&НаКлиенте
Перем АналитикаИзменяемойСтрокиТоваров; // см. РетроБонусыКлиентСервер.СтруктураСохраняемойСтрокиТоваров

&НаКлиенте
Перем ТекущееЗначениеСвойства; // ОпределяемыйТип.ТипыСвойствНоменклатурыРетроБонусов, Неопределено

&НаКлиенте
Перем ИзменяемоеЗначениеСвойства; // ОпределяемыйТип.ТипыСвойствНоменклатурыРетроБонусов, Неопределено

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ТолькоПросмотр = Ложь;
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ИсправлениеДокументов.ПриЧтенииНаСервере(ЭтотОбъект, Элементы.СтрокаИсправление);
	
	ЕстьПравоИзмененияУРБ = УправлениеДоступом.ИзменениеРазрешено(ТекущийОбъект);
	
	ПриСозданииЧтенииНаСервере();
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	Если Объект.НачислитьСразу
	   И Элементы.ДекорацияОтменитьСогласование.Видимость Тогда
		
		Элементы.ДекорацияОтменитьСогласование.Видимость = НЕ ДатыЗапретаИзменения.ИзменениеЗапрещено(ТекущийОбъект);
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		
		ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	ИсправлениеДокументов.ПриСозданииНаСервере(ЭтотОбъект, Элементы.СтрокаИсправление);
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ПриСозданииЧтенииНаСервере();
		
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	УстановитьЗаголовок();
	
	// СтандартныеПодсистемы.РаботаСФайлами
	ПараметрыГиперссылки = РаботаСФайлами.ГиперссылкаФайлов();
	ПараметрыГиперссылки.Размещение = "КоманднаяПанель";
	РаботаСФайлами.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыГиперссылки);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ИнтеграцияС1СДокументооборотом
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ИнтеграцияС1СДокументооборотом
	
	ПропуститьПроверкуИзмененияПоСтатусу = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ПараметрыЗаписи,
		"ПропуститьПроверкуИзмененияПоСтатусу",
		Ложь);
	ТекущийОбъект.ДополнительныеСвойства.Вставить(
		"ПропуститьПроверкуИзмененияПоСтатусу",
		ПропуститьПроверкуИзмененияПоСтатусу);
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить(
		"ПропуститьПроверкуНаОграничениеПоНачислениямПриОтсутствииИзменений",
		Ложь);
	
	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОбщегоНазначенияУТКлиент.ВыполнитьДействияПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	
	ИсправлениеДокументовКлиент.ПослеЗаписи(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ЕстьПравоИзмененияУРБ = УправлениеДоступом.ИзменениеРазрешено(ТекущийОбъект);
	
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре();
	ЗаполнитьСлужебныеРеквизитыСоглашенийДоговоров();
	УстановитьЗаголовок();
	ЗаполнитьРеквизитыСоставаПоУсловию();
	УстановитьНастройкиПоСтатусу();
	РетроБонусыКлиентСервер.СформироватьНаименованияДиапазоновШкалРасчета(ЭтотОбъект);
	РетроБонусыСервер.ОбновитьДиапазоныСтрокВсехТоваров(Объект, ПоказательТоваров);
	РетроБонусыСервер.ОбновитьДиапазоныСтрокВсехСвойств(Объект, ПоказательТоваров);
	
	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
		
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	//@skip-check invocation-parameter-type-intersect
	ИсправлениеДокументовКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.РаботаСФайлами
	
	РаботаСФайламиКлиент.ПоказатьПодтверждениеЗакрытияФормыСФайлами(ЭтотОбъект, Отказ, ЗавершениеРаботы, Объект.Ссылка);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтрокаИсправлениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ИсправлениеДокументовКлиент.СтрокаИсправлениеОбработкаНавигационныйСсылки(
		ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура СтатусПриИзменении(Элемент)
	
	ОчиститьСообщения();
	
	ЗаполнитьПризнакНаличияНачислений();
	Если ТекущийСтатус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовРетроБонусов.Согласован")
	   И ЕстьНачисления Тогда
		
		УстановитьЗаголовкиДекорацийСГиперссылками();
		
		ТекстСообщения = НСтр("ru = 'Созданы документы начисления. Изменение согласования запрещено.';
								|en = 'Accrual documents are created. Cannot edit the approval.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "Объект", "Статус");
		Объект.Статус = ТекущийСтатус;
			
	ИначеЕсли Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовРетроБонусов.Согласован") Тогда
		
		ТекстВопроса = НСтр("ru = 'Документ будет проведен, продолжить?';
							|en = 'The document will be posted, continue?'");
		Результат = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
		
		Если Результат = КодВозвратаДиалога.Да Тогда
			
			ОчиститьСообщения();
			
			Если Объект.ИсправляемыйДокумент.Пустая() Тогда
				Объект.ИдентификаторЦепочки = Строка(Новый УникальныйИдентификатор);
			КонецЕсли;
			
			ПараметрыЗаписи = Новый Структура;
			ПараметрыЗаписи.Вставить("ПропуститьПроверкуИзмененияПоСтатусу", Истина);
			ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Проведение);
			ПараметрыЗаписи.Вставить("РежимПроведения", РежимПроведенияДокумента.Неоперативный);
			
			Попытка
				
				УспешноПроведен = Записать(ПараметрыЗаписи);
				Если НЕ УспешноПроведен Тогда
					
					Объект.Статус = ТекущийСтатус;
					
				КонецЕсли;
				
			Исключение
				
				Объект.Статус = ТекущийСтатус;
				ТекстОшибки = НСтр("ru = 'При проведении документа возникли ошибки, статус не установлен';
									|en = 'Errors occurred when posting the document. The status is not set'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки,, "Объект.Статус");
				ВызватьИсключение;
				
			КонецПопытки;
			
			ТекущийСтатус = Объект.Статус;
			
			// СтандартныеПодсистемы.ПодключаемыеКоманды
			ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
			// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
			
		Иначе
			
			Объект.Статус = ТекущийСтатус;
			
		КонецЕсли;
		
	Иначе
		
		ТекущийСтатус = Объект.Статус;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидРетроБонусаПриИзменении(Элемент)
	
	ОчиститьСообщения();
	ВидРетроБонусаПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура НачислитьСразуПриИзменении(Элемент)
	
	Объект.ДетализацияРасчетаУчастников = ПредопределенноеЗначение("Перечисление.ДетализацияРасчетаУчастниковРетроБонусов.ПоКонтрагентуКлиенту");
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	НастроитьЗависимыеЭлементыФормы("НачислитьСразу");
	
КонецПроцедуры

&НаКлиенте
Процедура ДетализацияРасчетаУчастниковПриИзменении(Элемент)
	
	ДетализацияРасчетаУчастниковПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоДействияПриИзменении(Элемент)
	
	НачалоДействияСтрокой = Формат(Объект.НачалоДействия, "ДЛФ=D;");
	
КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеДействияПриИзменении(Элемент)
	
	ОкончаниеДействияСтрокой = Формат(Объект.ОкончаниеДействия, "ДЛФ=D;");
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодичностьУсловийПриИзменении(Элемент)
	
	УстановитьДоступныйСписокПериодичностьНачислений(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборУчастниковПриИзменении(Элемент)
	
	ОтборУчастниковПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборТоваровПриИзменении(Элемент)
	
	ОтборТоваровПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПланПриИзменении(Элемент)
	
	СформироватьСуммыПлановСтрокой(ЭтотОбъект); 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапретНачисленияСверхПланаПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы("ЗапретНачисленияСверхПлана");
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаПриИзменении(Элемент)
	
	РетроБонусыКлиентСервер.СформироватьНаименованияДиапазоновШкалРасчета(ЭтотОбъект);
	УстановитьЗаголовкиПоВалюте();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЗафиксироватьСоставОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбрабатываемаяСсылка = "Зафиксировать";
	
	Если НавигационнаяСсылкаФорматированнойСтроки = ОбрабатываемаяСсылка Тогда
		ЗафиксироватьСоставДокумента();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОтменитьФиксациюСоставаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбрабатываемаяСсылка = "ОтменитьФиксацию";
	
	Если НавигационнаяСсылкаФорматированнойСтроки = ОбрабатываемаяСсылка Тогда
		ОтменитьФиксациюСоставаДокумента();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОтменитьСогласованиеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбрабатываемаяСсылка = "ОтменитьСогласование";
	
	Если НавигационнаяСсылкаФорматированнойСтроки = ОбрабатываемаяСсылка Тогда
		ОтменитьСогласование();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидШкалыРасчетаПриИзменении(Элемент)
	
	ВидШкалыРасчетаПриИзмененииСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКонтрагенты

&НаКлиенте
Процедура КонтрагентыПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Контрагенты.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ИсходнаяСтрока Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыПослеУдаления(Элемент)
	
	Если НЕ ДетализацияПоКонтрагентуКлиенту(Объект.ДетализацияРасчетаУчастников) Тогда
		
		УстановитьПризнакиРасчетаПланаУчастников();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.Контрагенты.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Копирование Тогда
		
		ТекущиеДанные.СуммаПланДоИзменений = 0;
		ТекущиеДанные.ПланНеУказываетсяДоИзменений = Ложь;
		ТекущиеДанные.НачалоДействияДоИзменений = Дата(1, 1, 1);
		ТекущиеДанные.ОкончаниеДействияДоИзменений = Дата(1, 1, 1);
		ТекущиеДанные.Отменено = Ложь;
		ТекущиеДанные.ИсходнаяСтрока = Ложь;
		
		Если НЕ ДетализацияПоКонтрагентуКлиенту(Объект.ДетализацияРасчетаУчастников) Тогда
		
			УстановитьПризнакиРасчетаПланаУчастников();
		
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыКонтрагентПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Контрагенты.ТекущиеДанные;
	
	Если (Объект.НачислитьСразу
		  И СоставУчастников = ПредопределенноеЗначение("Перечисление.СоставыУчастниковРетроБонусов.Контрагенты")
		  И ТекущиеДанные.Партнер.Пустая())
	 ИЛИ НЕ ДетализацияПоКонтрагентуКлиенту(Объект.ДетализацияРасчетаУчастников) Тогда
		
		ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
		КонтрагентыКонтрагентПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыПартнерПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Контрагенты.ТекущиеДанные;
	
	Если (Объект.НачислитьСразу
		  И СоставУчастников = ПредопределенноеЗначение("Перечисление.СоставыУчастниковРетроБонусов.Клиенты")
		  И ТекущиеДанные.Контрагент.Пустая())
	 ИЛИ НЕ ДетализацияПоКонтрагентуКлиенту(Объект.ДетализацияРасчетаУчастников) Тогда
		
		ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
		КонтрагентыПартнерПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыОтмененоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Контрагенты.ТекущиеДанные;
	
	Если ТекущиеДанные.Отменено Тогда
		
		ТекущиеДанные.СуммаПлан = 0;
		ТекущиеДанные.ПланНеУказывается = Ложь;
		ТекущиеДанные.НачалоДействия = Дата(1, 1, 1);
		ТекущиеДанные.ОкончаниеДействия = Дата(1, 1, 1);
		
	КонецЕсли;
	
	Если НЕ ДетализацияПоКонтрагентуКлиенту(Объект.ДетализацияРасчетаУчастников) Тогда
		
		УстановитьПризнакиРасчетаПланаУчастников();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИННКонтрагентов

&НаКлиенте
Процедура ИННКонтрагентовПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.ИННКонтрагентов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ИсходнаяСтрока Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИННКонтрагентовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.ИННКонтрагентов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Копирование Тогда
		
		ТекущиеДанные.СуммаПланДоИзменений = 0;
		ТекущиеДанные.НачалоДействияДоИзменений = Дата(1, 1, 1);
		ТекущиеДанные.ОкончаниеДействияДоИзменений = Дата(1, 1, 1);
		ТекущиеДанные.Отменено = Ложь;
		ТекущиеДанные.ИсходнаяСтрока = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИННКонтрагентовОтмененоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ИННКонтрагентов.ТекущиеДанные;
	
	Если ТекущиеДанные.Отменено Тогда
		
		ТекущиеДанные.СуммаПлан = 0;
		ТекущиеДанные.НачалоДействия = Дата(1, 1, 1);
		ТекущиеДанные.ОкончаниеДействия = Дата(1, 1, 1);
		
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСегментыПартнеров

&НаКлиенте
Процедура СегментыПартнеровПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.СегментыПартнеров.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ИсходнаяСтрока Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СегментыПартнеровПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.СегментыПартнеров.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Копирование Тогда
		
		ТекущиеДанные.Отменено = Ложь;
		ТекущиеДанные.ИсходнаяСтрока = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДоговорыСоглашения

&НаКлиенте
Процедура ДоговорыСоглашенияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.ДоговорыСоглашения.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Копирование Тогда
		
		ТекущиеДанные.СуммаПланДоИзменений = 0;
		ТекущиеДанные.ПланНеУказываетсяДоИзменений = Ложь;
		ТекущиеДанные.НачалоДействияДоИзменений = Дата(1, 1, 1);
		ТекущиеДанные.ОкончаниеДействияДоИзменений = Дата(1, 1, 1);
		ТекущиеДанные.Отменено = Ложь;
		ТекущиеДанные.ИсходнаяСтрока = Ложь;
		
		Если ДетализацияПоКонтрагенту(Объект.ДетализацияРасчетаУчастников)
		   И СоставУчастников = ПредопределенноеЗначение("Перечисление.СоставыУчастниковРетроБонусов.Договоры") Тогда
			
			УстановитьПризнакиРасчетаПланаУчастников();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорыСоглашенияПартнерПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДоговорыСоглашения.ТекущиеДанные;
	
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ДоговорыСоглашенияПартнерПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорыСоглашенияКонтрагентПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДоговорыСоглашения.ТекущиеДанные;
	
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ДоговорыСоглашенияКонтрагентПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорыСоглашенияДоговорНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДоговорыСоглашения.ТекущиеДанные; // ДанныеФормыЭлементКоллекции: см. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента.Элементы.ДоговорыСоглашения
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ТипДоговора", ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПокупателем"));
	СтруктураОтбора.Вставить("Организация", Объект.Организация);
	Если НЕ ТекущиеДанные.Контрагент.Пустая() Тогда
		СтруктураОтбора.Вставить("Контрагент", ТекущиеДанные.Контрагент);
	КонецЕсли;
	Если НЕ ТекущиеДанные.Партнер.Пустая() Тогда
		СтруктураОтбора.Вставить("Партнер", ТекущиеДанные.Партнер);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);
	
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорыСоглашенияДоговорПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДоговорыСоглашения.ТекущиеДанные;
	
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ДоговорыСоглашенияДоговорПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорыСоглашенияСоглашениеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДоговорыСоглашения.ТекущиеДанные; // ДанныеФормыЭлементКоллекции: см. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента.Элементы.ДоговорыСоглашения
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ДатаДокумента", Дата(1, 1, 1)); // Не ограничиваем выбор соглашений по периоду
	ПараметрыФормы.Вставить("ТолькоТиповые", Ложь);
	ПараметрыФормы.Вставить("ТолькоИспользуемыеВРаботеТП", Ложь);
	ПараметрыФормы.Вставить("РазрешитьВыборНедействующих", Истина);
	ПараметрыФормы.Вставить("ТекущаяСтрока", ТекущиеДанные.Соглашение);
	ПараметрыФормы.Вставить(
		"ХозяйственнаяОперация",
		ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияКлиенту"));
	
	Если НЕ ТекущиеДанные.Партнер.Пустая() Тогда
	
		ПараметрыФормы.Вставить("Партнер", ТекущиеДанные.Партнер);
	
	КонецЕсли;
	
	ДополнительныйОтбор = Новый Структура;
	ДополнительныйОтбор.Вставить("Организация", Объект.Организация);
	
	Если НЕ ТекущиеДанные.Контрагент.Пустая() Тогда
	
		МассивКонтрагентов = Новый Массив; // Массив Из СправочникСсылка.Контрагенты
		МассивКонтрагентов.Добавить(ТекущиеДанные.Контрагент);
		МассивКонтрагентов.Добавить(ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));
		ДополнительныйОтбор.Вставить("Контрагент", МассивКонтрагентов);
	
	КонецЕсли;
	
	ПараметрыФормы.Вставить("Отбор", ДополнительныйОтбор);
	
	ОткрытьФорму("Справочник.СоглашенияСКлиентами.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорыСоглашенияСоглашениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДоговорыСоглашения.ТекущиеДанные;
	
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ДоговорыСоглашенияСоглашениеПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорыСоглашенияОтмененоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДоговорыСоглашения.ТекущиеДанные;
	
	Если ТекущиеДанные.Отменено Тогда
		
		ТекущиеДанные.СуммаПлан = 0;
		ТекущиеДанные.ПланНеУказывается = Ложь;
		ТекущиеДанные.НачалоДействия = Дата(1, 1, 1);
		ТекущиеДанные.ОкончаниеДействия = Дата(1, 1, 1);
		
	КонецЕсли;
	
	Если ДетализацияПоКонтрагенту(Объект.ДетализацияРасчетаУчастников)
	   И СоставУчастников = ПредопределенноеЗначение("Перечисление.СоставыУчастниковРетроБонусов.Договоры") Тогда
		
		УстановитьПризнакиРасчетаПланаУчастников(ТекущиеДанные.Контрагент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорыСоглашенияПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.ДоговорыСоглашения.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ИсходнаяСтрока Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорыСоглашенияПослеУдаления(Элемент)
	
	Если ДетализацияПоКонтрагенту(Объект.ДетализацияРасчетаУчастников)
	   И СоставУчастников = ПредопределенноеЗначение("Перечисление.СоставыУчастниковРетроБонусов.Договоры") Тогда
		
		УстановитьПризнакиРасчетаПланаУчастников();
		
	КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьАналитикуСтрокиТоваров(АналитикаТекущейСтрокиТоваров, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные.ИсходнаяСтрока Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		СохранитьАналитикуСтрокиТоваров(АналитикаИзменяемойСтрокиТоваров, ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)
	
	Если Объект.Товары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров)
	   И НЕ АналитикаИзменяемойСтрокиТоваров.Номенклатура.Пустая() Тогда
		
		ОбновитьДиапазоныСтрокТоваровПоТовару(АналитикаИзменяемойСтрокиТоваров);
		
	КонецЕсли;
	
	// Сбрасываем параметры
	АналитикаТекущейСтрокиТоваров = РетроБонусыКлиентСервер.СтруктураСохраняемойСтрокиТоваров();
	АналитикаИзменяемойСтрокиТоваров = РетроБонусыКлиентСервер.СтруктураСохраняемойСтрокиТоваров();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если Копирование Тогда
		
		ТекущиеДанные.НачалоДействияДоИзменений = Дата(1, 1, 1);
		ТекущиеДанные.ОкончаниеДействияДоИзменений = Дата(1, 1, 1);
		ТекущиеДанные.Отменено = Ложь;
		ТекущиеДанные.ИсходнаяСтрока = Ложь;
		
		ТекущиеДанные.КоличествоПланГраницаДиапазона = 0;
		ТекущиеДанные.ОбозначениеДиапазона = "";
		
	КонецЕсли;
	
	СохранитьАналитикуСтрокиТоваров(АналитикаИзменяемойСтрокиТоваров, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока
	   И ОтменаРедактирования
	   И ИзменилиДанныеТовара Тогда
		
		Если РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров)
		   И НЕ АналитикаИзменяемойСтрокиТоваров.Номенклатура.Пустая() Тогда
			
			ОбновитьДиапазоныСтрокТоваровПоТовару(АналитикаИзменяемойСтрокиТоваров);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Сбрасываем параметры
	АналитикаИзменяемойСтрокиТоваров = РетроБонусыКлиентСервер.СтруктураСохраняемойСтрокиТоваров();
	ИзменилиДанныеТовара = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ТоварыНоменклатураПриИзмененииСервер(
		ТекущаяСтрокаИдентификатор,
		АналитикаТекущейСтрокиТоваров,
		АналитикаИзменяемойСтрокиТоваров,
		КешированныеЗначения);
	
	СохранитьАналитикуСтрокиТоваров(АналитикаТекущейСтрокиТоваров, ТекущиеДанные);
	ИзменилиДанныеТовара = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ТоварыХарактеристикаПриИзмененииСервер(
		ТекущаяСтрокаИдентификатор,
		АналитикаТекущейСтрокиТоваров,
		АналитикаИзменяемойСтрокиТоваров);
		
	СохранитьАналитикуСтрокиТоваров(АналитикаТекущейСтрокиТоваров, ТекущиеДанные);
	ИзменилиДанныеТовара = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыОтмененоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ТоварыОтмененоПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПланПриИзменении(Элемент)
	
	Если РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров) Тогда
		
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
		
		ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
		ТоварыКоличествоПланПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
		
	КонецЕсли;
	
	ИзменилиДанныеТовара = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНачалоДействияПриИзменении(Элемент)
	
	Если РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров) Тогда
		
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
		
		ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
		ТоварыНачалоДействияПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыОкончаниеДействияПриИзменении(Элемент)
	
	Если РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров) Тогда
		
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
		
		ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
		ТоварыОкончаниеДействияПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыБазоваяЦенаПриИзменении(Элемент)
	
	Если РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров) Тогда
		
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
		
		ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
		ТоварыБазоваяЦенаПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСегментыТоваров

&НаКлиенте
Процедура СегментыТоваровПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.СегментыТоваров.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ИсходнаяСтрока Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СегментыТоваровПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.СегментыТоваров.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Копирование Тогда
		
		ТекущиеДанные.Отменено = Ложь;
		ТекущиеДанные.ИсходнаяСтрока = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыШкалыРасчета

&НаКлиенте
Процедура ШкалыРасчетаПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ПроверкаГраницыДиапазонаВыполнена
	 ИЛИ (НоваяСтрока
	      И ОтменаРедактирования) Тогда
		
		ПроверкаГраницыДиапазонаВыполнена = Ложь;
		Возврат;
		
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ШкалыРасчета.ТекущиеДанные;
	ЗначениеГраницы = ТекущиеДанные.ДиапазонОт;
	ЭтоПерваяСтрокаПоДиапазонам =
		(Объект.ВидШкалыРасчета = 2
		 И ТекущиеДанные.НомерСтроки = 1);
	
	Если ЗначениеГраницы = 0
	   И НЕ ЭтоПерваяСтрокаПоДиапазонам Тогда
		
		Отказ = Истина;
		ПредупреждениеНеобходимостиЗаполненияГраницы();
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ДиапазонОт", ЗначениеГраницы);
		
		НайденныеСтроки = Объект.ШкалыРасчета.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() > 1 Тогда
			
			Отказ = Истина;
			ПредупреждениеДубльГраницы(ЗначениеГраницы);
		
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ШкалыРасчетаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ПроверкаГраницыДиапазонаВыполнена = Ложь;
	РетроБонусыКлиентСервер.УстановитьЗаголовокГруппыШкалРасчета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ШкалыРасчетаДиапазонОтПриИзменении(Элемент)
	
	Отказ = Ложь;
	ТекущиеДанные = Элементы.ШкалыРасчета.ТекущиеДанные;
	ЗначениеГраницы = ТекущиеДанные.ДиапазонОт;
	
	Если ЗначениеГраницы = 0 Тогда
		
		Отказ = Истина;
		ПредупреждениеНеобходимостиЗаполненияГраницы();
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ДиапазонОт", ЗначениеГраницы);
		
		НайденныеСтроки = Объект.ШкалыРасчета.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() > 1 Тогда
			
			Отказ = Истина;
			ПредупреждениеДубльГраницы(ЗначениеГраницы);
		
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		Объект.ШкалыРасчета.Сортировать("ДиапазонОт Возр");
		РетроБонусыКлиентСервер.СформироватьНаименованияДиапазоновШкалРасчета(ЭтотОбъект);
		
	КонецЕсли;
	
	ПроверкаГраницыДиапазонаВыполнена = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ШкалыРасчетаПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.ШкалыРасчета.ТекущиеДанные;
	
	Если ТекущиеДанные.ДиапазонОт = 0
	   И Объект.ВидШкалыРасчета = 2 Тогда // Для показателя суммы это по диапазонам
		
		// Для диапазонов нельзя удалять начальную строку
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ШкалыРасчетаПослеУдаления(Элемент)
	
	РетроБонусыКлиентСервер.СформироватьНаименованияДиапазоновШкалРасчета(ЭтотОбъект);
	РетроБонусыКлиентСервер.УстановитьЗаголовокГруппыШкалРасчета(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСвойстваНоменклатуры

&НаКлиенте
Процедура СвойстваНоменклатурыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.СвойстваНоменклатуры.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущееЗначениеСвойства = ТекущиеДанные.ЗначениеСвойства;
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваНоменклатурыПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.СвойстваНоменклатуры.ТекущиеДанные;
	
	Если ТекущиеДанные.ИсходнаяСтрока Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		ИзменяемоеЗначениеСвойства = ТекущиеДанные.ЗначениеСвойства;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваНоменклатурыПослеУдаления(Элемент)
	
	Если Объект.СвойстваНоменклатуры.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров)
	   И ИзменяемоеЗначениеСвойства <> Неопределено Тогда
		
		ОбновитьДиапазоныСтрокСвойствПоСвойству(ИзменяемоеЗначениеСвойства);
		
	КонецЕсли;
	
	// Сбрасываем параметры
	ТекущееЗначениеСвойства = Неопределено;
	ИзменяемоеЗначениеСвойства = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваНоменклатурыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.СвойстваНоменклатуры.ТекущиеДанные;
	
	Если Копирование Тогда
		
		ТекущиеДанные.НачалоДействияДоИзменений = Дата(1, 1, 1);
		ТекущиеДанные.ОкончаниеДействияДоИзменений = Дата(1, 1, 1);
		ТекущиеДанные.Отменено = Ложь;
		ТекущиеДанные.ИсходнаяСтрока = Ложь;
		
		ТекущиеДанные.КоличествоПланГраницаДиапазона = 0;
		ТекущиеДанные.ОбозначениеДиапазона = "";
		
	КонецЕсли;
	
	ИзменяемоеЗначениеСвойства = ТекущиеДанные.ЗначениеСвойства;
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваНоменклатурыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока
	   И ОтменаРедактирования
	   И ИзменилиДанныеСвойств Тогда
		
		Если РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров)
		   И ИзменяемоеЗначениеСвойства <> Неопределено Тогда
			
			ОбновитьДиапазоныСтрокСвойствПоСвойству(ИзменяемоеЗначениеСвойства);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Сбрасываем параметры
	ИзменяемоеЗначениеСвойства = Неопределено;
	ИзменилиДанныеСвойств = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваНоменклатурыЗначениеСвойстваПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СвойстваНоменклатуры.ТекущиеДанные;
	
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	СвойстваНоменклатурыЗначениеСвойстваПриИзмененииСервер(
		ТекущаяСтрокаИдентификатор,
		ТекущееЗначениеСвойства,
		ИзменяемоеЗначениеСвойства);
	
	ТекущееЗначениеСвойства = ТекущиеДанные.ЗначениеСвойства;
	ИзменилиДанныеСвойств = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваНоменклатурыОтмененоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СвойстваНоменклатуры.ТекущиеДанные;
	
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	СвойстваНоменклатурыОтмененоПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваНоменклатурыКоличествоПланПриИзменении(Элемент)
	
	Если РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров) Тогда
		
		ТекущиеДанные = Элементы.СвойстваНоменклатуры.ТекущиеДанные;
		
		ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
		СвойстваНоменклатурыКоличествоПланПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
		
	КонецЕсли;
	
	ИзменилиДанныеСвойств = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваНоменклатурыНачалоДействияПриИзменении(Элемент)
	
	Если РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров) Тогда
		
		ТекущиеДанные = Элементы.СвойстваНоменклатуры.ТекущиеДанные;
		
		ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
		СвойстваНоменклатурыНачалоДействияПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваНоменклатурыОкончаниеДействияПриИзменении(Элемент)
	
	Если РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров) Тогда
		
		ТекущиеДанные = Элементы.СвойстваНоменклатуры.ТекущиеДанные;
		
		ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
		СвойстваНоменклатурыОкончаниеДействияПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваНоменклатурыБазоваяЦенаПриИзменении(Элемент)
	
	Если РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров) Тогда
		
		ТекущиеДанные = Элементы.СвойстваНоменклатуры.ТекущиеДанные;
		
		ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
		СвойстваНоменклатурыБазоваяЦенаПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваНоменклатурыЗначениеСвойстваНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.СвойстваНоменклатуры.ТекущиеДанные;
	Элементы.СвойстваНоменклатурыЗначениеСвойства.ВыбиратьТип = НЕ ЗначениеЗаполнено(ТекущиеДанные.ЗначениеСвойства);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваНоменклатурыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Элементы.СвойстваНоменклатурыЗначениеСвойства.ВыбиратьТип = НЕ Копирование;
	РетроБонусыКлиентСервер.УстановитьФорматЗначенияСвойстваПоУмолчанию(
		Элементы.СвойстваНоменклатурыЗначениеСвойства,
		ДопустимыеТипыСвойства);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваНоменклатурыЗначениеСвойстваОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = Тип("Число")
	 ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("Число") Тогда
		
		РетроБонусыКлиентСервер.УстановитьФорматЧислоСвойства(Элемент, ДопустимыеТипыСвойства);
		
	ИначеЕсли ВыбранноеЗначение = Тип("Дата")
	 ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("Дата") Тогда
		
		РетроБонусыКлиентСервер.УстановитьФорматЗначенияСвойстваПоУмолчанию(Элемент, ДопустимыеТипыСвойства);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваНоменклатурыПриАктивизацииЯчейки(Элемент)
	
	УстановитьФорматЧисла = Ложь;
	Если Элемент.ТекущийЭлемент = Элементы.СвойстваНоменклатурыЗначениеСвойства Тогда
		
		ТекущиеДанные = Элементы.СвойстваНоменклатуры.ТекущиеДанные;
		
		Если ТекущиеДанные <> Неопределено
		   И ТипЗнч(ТекущиеДанные.ЗначениеСвойства) = Тип("Число") Тогда
			
			УстановитьФорматЧисла = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если УстановитьФорматЧисла Тогда
		
		РетроБонусыКлиентСервер.УстановитьФорматЧислоСвойства(
			Элементы.СвойстваНоменклатурыЗначениеСвойства,
			ДопустимыеТипыСвойства);
		
	Иначе
		
		РетроБонусыКлиентСервер.УстановитьФорматЗначенияСвойстваПоУмолчанию(
			Элементы.СвойстваНоменклатурыЗначениеСвойства,
			ДопустимыеТипыСвойства);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОбщегоНазначенияУТКлиент.ПровестиИЗакрыть(ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Записать(ЭтотОбъект, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Провести(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкуУдаленияДокумента(Команда)
	
	ОбщегоНазначенияУТКлиент.УстановитьПометкуУдаленияДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервал(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьИнтервалПослеЗавершения", ЭтотОбъект);
	
	ОбщегоНазначенияУТКлиент.РедактироватьПериод(
		Объект, 
		Новый Структура("ДатаНачала, ДатаОкончания", "НачалоДействия", "ОкончаниеДействия"),
		ОписаниеОповещения);
		
КонецПроцедуры

// СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.ПодключаемыеКоманды

// Параметры:
//   Команда - КомандаФормы - выполняемая команда
//
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.РаботаСФайлами

&НаКлиенте
Процедура Подключаемый_КомандаПанелиПрисоединенныхФайлов(Команда)
	
	РаботаСФайламиКлиент.КомандаУправленияПрисоединеннымиФайлами(ЭтотОбъект, Команда);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.РаботаСФайлами

// ИнтеграцияС1СДокументооборотом

// Параметры:
//   Команда - КомандаФормы - выполняемая команда
//
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Асинх Процедура ЗаполнитьТоварыПроцентБонуса(Команда)
	
	ВыделенныеСтроки = Элементы.Товары.ВыделенныеСтроки; // Массив из Число
	Если ВыделенныеСтроки.Количество() > 0 Тогда
		
		Подсказка = НСтр("ru = '% бонуса';
						|en = 'Rebate %'");
		Квалификаторы = Новый КвалификаторыЧисла(4, 2, ДопустимыйЗнак.Неотрицательный);
		ТипПроцент = Новый ОписаниеТипов("Число", Квалификаторы);
		НовыйПроцент = 0;
		Результат = Ждать ВвестиЗначениеАсинх(НовыйПроцент, Подсказка, ТипПроцент); // Число, Неопределено
		
		Если Результат <> Неопределено Тогда
			
			Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
				
				СтрокаТоваров = Объект.Товары.НайтиПоИдентификатору(ВыделеннаяСтрока);
				Если СтрокаТоваров.Отменено
				 ИЛИ СтрокаТоваров.ИсходнаяСтрока Тогда
					
					Продолжить;
					
				КонецЕсли;
				СтрокаТоваров.Процент = Результат;
				
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		
		ТекстПредупреждения = НСтр("ru = 'Не выбраны строки для заполнения.';
									|en = 'Lines for filling are not selected.'");
		Ждать ПредупреждениеАсинх(ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьКонтрагентовИзФайла(Команда)
	
	НастройкиОтображенияПолей = Новый Структура;
	НастройкиОтображенияПолей.Вставить("Контрагент", Элементы.КонтрагентыКонтрагент.Видимость);
	НастройкиОтображенияПолей.Вставить("Партнер", Элементы.КонтрагентыПартнер.Видимость);
	НастройкиОтображенияПолей.Вставить("СуммаПлан", Элементы.КонтрагентыСуммаПлан.Видимость);
	НастройкиОтображенияПолей.Вставить("СуммаБонус", Элементы.КонтрагентыСуммаБонус.Видимость);
	НастройкиОтображенияПолей.Вставить("Период", Элементы.КонтрагентыНачалоДействия.Видимость);
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзФайлаКлиент.ПараметрыЗагрузкиДанных();
	ПараметрыЗагрузки.ПолноеИмяТабличнойЧасти = "УсловияРетроБонусовКлиентов.Контрагенты";
	ПараметрыЗагрузки.Заголовок = ЗаголовокФормыЗагрузкиКонтрагентов();
	ПараметрыЗагрузки.КолонкиМакета = ОписаниеКолонокМакетаКонтрагенты(НастройкиОтображенияПолей);
	ДополнительныеПараметры = Новый Структура("НастройкиОтображенияПолей", НастройкиОтображенияПолей);
	ПараметрыЗагрузки.ДополнительныеПараметры = ДополнительныеПараметры;
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьКонтрагентовИзФайлаЗавершение", ЭтотОбъект);
	ЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗагрузки(ПараметрыЗагрузки, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИННКонтрагентовИзФайла(Команда)
	
	НастройкиОтображенияПолей = Новый Структура;
	НастройкиОтображенияПолей.Вставить("Период", Элементы.ИННКонтрагентовНачалоДействия.Видимость);
	НастройкиОтображенияПолей.Вставить("СуммаПлан", Элементы.ИННКонтрагентовСуммаПлан.Видимость);
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзФайлаКлиент.ПараметрыЗагрузкиДанных();
	ПараметрыЗагрузки.ПолноеИмяТабличнойЧасти = "УсловияРетроБонусовКлиентов.ИННКонтрагентов";
	ПараметрыЗагрузки.Заголовок = НСтр("ru = 'Загрузка ИНН контрагентов из файла';
										|en = 'Import counterparty TINs from the file'");
	ПараметрыЗагрузки.КолонкиМакета = ОписаниеКолонокМакетаКонтрагентыИНН(НастройкиОтображенияПолей);
	ДополнительныеПараметры = Новый Структура("НастройкиОтображенияПолей", НастройкиОтображенияПолей);
	ПараметрыЗагрузки.ДополнительныеПараметры = ДополнительныеПараметры;
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьИННКонтрагентовИзФайлаЗавершение", ЭтотОбъект);
	ЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗагрузки(ПараметрыЗагрузки, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСегментыТоваровИзФайла(Команда)
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзФайлаКлиент.ПараметрыЗагрузкиДанных();
	ПараметрыЗагрузки.ПолноеИмяТабличнойЧасти = "УсловияРетроБонусовКлиентов.СегментыТоваров";
	ПараметрыЗагрузки.Заголовок = НСтр("ru = 'Загрузка сегментов товаров из файла';
										|en = 'Import goods segments from the file'");
	ПараметрыЗагрузки.КолонкиМакета = ОписаниеКолонокМакетаСегментовТоваров();
	ПараметрыЗагрузки.ДополнительныеПараметры = Новый Структура;
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьСегментыТоваровИзФайлаЗавершение", ЭтотОбъект);
	ЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗагрузки(ПараметрыЗагрузки, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТоварыИзФайла(Команда)
	
	ОтображатьПериод = (Элементы.ТоварыНачалоДействия.Видимость
						ИЛИ Элементы.ТоварыОкончаниеДействия.Видимость);
	
	НастройкиОтображенияПолей = Новый Структура;
	НастройкиОтображенияПолей.Вставить("Характеристика", ИспользоватьХарактеристикиНоменклатуры);
	НастройкиОтображенияПолей.Вставить("Период", ОтображатьПериод);
	НастройкиОтображенияПолей.Вставить("КоличествоПлан", Элементы.ТоварыКоличествоПлан.Видимость);
	НастройкиОтображенияПолей.Вставить("БазоваяЦена", Элементы.ТоварыБазоваяЦена.Видимость);
	НастройкиОтображенияПолей.Вставить("Процент", Элементы.ТоварыПроцент.Видимость);
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзФайлаКлиент.ПараметрыЗагрузкиДанных();
	ПараметрыЗагрузки.ПолноеИмяТабличнойЧасти = "УсловияРетроБонусовКлиентов.Товары";
	ПараметрыЗагрузки.Заголовок = НСтр("ru = 'Загрузка товаров из файла';
										|en = 'Import goods from the file'");
	ПараметрыЗагрузки.КолонкиМакета = ОписаниеКолонокМакетаТовары(НастройкиОтображенияПолей);
	ДополнительныеПараметры = Новый Структура("НастройкиОтображенияПолей", НастройкиОтображенияПолей);
	ПараметрыЗагрузки.ДополнительныеПараметры = ДополнительныеПараметры;
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьТоварыИзФайлаЗавершение", ЭтотОбъект);
	ЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗагрузки(ПараметрыЗагрузки, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСегментыПартнеровИзФайла(Команда)
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзФайлаКлиент.ПараметрыЗагрузкиДанных();
	ПараметрыЗагрузки.ПолноеИмяТабличнойЧасти = "УсловияРетроБонусовКлиентов.СегментыПартнеров";
	ПараметрыЗагрузки.Заголовок = НСтр("ru = 'Загрузка сегментов клиентов из файла';
										|en = 'Import customer segments from the file'");
	ПараметрыЗагрузки.КолонкиМакета = ОписаниеКолонокМакетаСегментовПартнеров();
	ПараметрыЗагрузки.ДополнительныеПараметры = Новый Структура;
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьСегментыПартнеровИзФайлаЗавершение", ЭтотОбъект);
	ЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗагрузки(ПараметрыЗагрузки, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗаполнитьТоварыКоличествоПлан(Команда)
	
	ВыделенныеСтроки = Элементы.Товары.ВыделенныеСтроки; // Массив из Число
	Если ВыделенныеСтроки.Количество() > 0 Тогда
		
		Подсказка = НСтр("ru = 'Кол-во, план';
						|en = 'Quantity, plan'");
		Квалификаторы = Новый КвалификаторыЧисла(15, 3, ДопустимыйЗнак.Неотрицательный);
		ТипКоличествоПлан = Новый ОписаниеТипов("Число", Квалификаторы);
		НовыйПлан = 0;
		Результат = Ждать ВвестиЗначениеАсинх(НовыйПлан, Подсказка, ТипКоличествоПлан); // Число, Неопределено
		
		Если Результат <> Неопределено Тогда
			ЗаполнитьТоварыКоличествоПланСервер(Результат, ВыделенныеСтроки);
		КонецЕсли;
		
	Иначе
		
		ТекстПредупреждения = НСтр("ru = 'Не выбраны строки для заполнения.';
									|en = 'Lines to populate are not selected.'");
		Ждать ПредупреждениеАсинх(ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьУчастниковПоСегменту(Команда)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЗаполнитьУчастниковПоСегментуЗавершение", ЭтотОбъект);
	ОткрытьФорму(
		"Справочник.СегментыПартнеров.ФормаВыбора",
		,
		ЭтотОбъект,
		,
		,
		,
		ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТоварыПоСегменту(Команда)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЗаполнитьТоварыПоСегментуЗавершение", ЭтотОбъект);
	ОткрытьФорму(
		"Справочник.СегментыНоменклатуры.ФормаВыбора",
		,
		ЭтотОбъект,
		,
		,
		,
		ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура УпорядочитьТоварыПоДиапазону(Команда)
	
	УпорядочитьТоварыПоДиапазонуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УпорядочитьЗначенияСвойствПоДиапазону(Команда)
	
	УпорядочитьЗначенияСвойствПоДиапазонуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьЗначенияСвойствИзФайла(Команда)
	
	ОчиститьСообщения();
	МассивТипов = ДопустимыеТипыСвойства.Типы(); // Массив Из Тип
	
	Если МассивТипов.Количество() = 1 Тогда 
		
		ТипЗначения = МассивТипов[0];
		СведенияОбИсточнике = СведенияОбИсточникеЗначений(ТипЗначения);
		СведенияОбИсточнике.Вставить("Тип", ДопустимыеТипыСвойства);
		
		НастройкиОтображенияПолей = Новый Структура;
		НастройкиОтображенияПолей.Вставить("Период", Элементы.СвойстваНоменклатурыНачалоДействия.Видимость);
		НастройкиОтображенияПолей.Вставить("КоличествоПлан", Элементы.СвойстваНоменклатурыКоличествоПлан.Видимость);
		НастройкиОтображенияПолей.Вставить("БазоваяЦена", Элементы.СвойстваНоменклатурыБазоваяЦена.Видимость);
		НастройкиОтображенияПолей.Вставить("Процент", Элементы.СвойстваНоменклатурыПроцент.Видимость);
		
		ПараметрыЗагрузки = ЗагрузкаДанныхИзФайлаКлиент.ПараметрыЗагрузкиДанных();
		ПараметрыЗагрузки.ПолноеИмяТабличнойЧасти = "УсловияРетроБонусовКлиентов.СвойстваНоменклатуры";
		ПараметрыЗагрузки.Заголовок = НСтр("ru = 'Загрузка значений свойств из файла';
											|en = 'Import property values from file'");
		
		ПараметрыЗагрузки.КолонкиМакета = ОписаниеКолонокМакетаСвойстваНоменклатуры(
			НастройкиОтображенияПолей,
			СведенияОбИсточнике);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("НастройкиОтображенияПолей", НастройкиОтображенияПолей);
		ДополнительныеПараметры.Вставить("ЭтоЗначенияСвойств", Истина);
		ДополнительныеПараметры.Вставить("СведенияОбИсточнике", СведенияОбИсточнике);
		ПараметрыЗагрузки.ДополнительныеПараметры = ДополнительныеПараметры;
		
		Оповещение = Новый ОписаниеОповещения("ЗагрузитьЗначенияСвойствИзФайлаЗавершение", ЭтотОбъект);
		ЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗагрузки(ПараметрыЗагрузки, Оповещение);
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Загрузка возможна только для несоставных типов свойства номенклатуры';
								|en = 'Import is only available for non-flexible item property types'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗаполнитьСвойстваНоменклатурыПроцентБонуса(Команда)
	
	ВыделенныеСтроки = Элементы.СвойстваНоменклатуры.ВыделенныеСтроки; // Массив из Число
	Если ВыделенныеСтроки.Количество() > 0 Тогда
		
		Подсказка = НСтр("ru = '% бонуса';
						|en = 'Rebate %'");
		Квалификаторы = Новый КвалификаторыЧисла(4, 2, ДопустимыйЗнак.Неотрицательный);
		ТипПроцент = Новый ОписаниеТипов("Число", Квалификаторы);
		НовыйПроцент = 0;
		Результат = Ждать ВвестиЗначениеАсинх(НовыйПроцент, Подсказка, ТипПроцент); // Число, Неопределено
		
		Если Результат <> Неопределено Тогда
			
			Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
				
				СтрокаСвойствНоменклатуры = Объект.СвойстваНоменклатуры.НайтиПоИдентификатору(ВыделеннаяСтрока);
				Если НЕ СтрокаСвойствНоменклатуры.Отменено
				   И НЕ СтрокаСвойствНоменклатуры.ИсходнаяСтрока Тогда
					
					СтрокаСвойствНоменклатуры.Процент = Результат;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		
		ТекстПредупреждения = НСтр("ru = 'Не выбраны строки для заполнения.';
									|en = 'Lines for filling are not selected.'");
		Ждать ПредупреждениеАсинх(ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗаполнитьСвойстваНоменклатурыКоличествоПлан(Команда)
	
	ВыделенныеСтроки = Элементы.СвойстваНоменклатуры.ВыделенныеСтроки; // Массив из Число
	Если ВыделенныеСтроки.Количество() > 0 Тогда
		
		Подсказка = НСтр("ru = 'Кол-во, план';
						|en = 'Quantity, plan'");
		Квалификаторы = Новый КвалификаторыЧисла(15, 3, ДопустимыйЗнак.Неотрицательный);
		ТипКоличествоПлан = Новый ОписаниеТипов("Число", Квалификаторы);
		НовыйПлан = 0;
		Результат = Ждать ВвестиЗначениеАсинх(НовыйПлан, Подсказка, ТипКоличествоПлан); // Число, Неопределено
		
		Если Результат <> Неопределено Тогда
			
			Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
				
				СтрокаСвойствНоменклатуры = Объект.СвойстваНоменклатуры.НайтиПоИдентификатору(ВыделеннаяСтрока);
				Если НЕ СтрокаСвойствНоменклатуры.Отменено
				   И НЕ СтрокаСвойствНоменклатуры.ИсходнаяСтрока Тогда
					
					СтрокаСвойствНоменклатуры.КоличествоПлан = Результат;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		
		ТекстПредупреждения = НСтр("ru = 'Не выбраны строки для заполнения.';
									|en = 'Lines to populate are not selected.'");
		Ждать ПредупреждениеАсинх(ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	
	ТекущийСтатус = Объект.Статус;
	
	ЗаполнитьПризнакНаличияНачислений();
	ЗаполнитьДанныеПоследнейКорректировки();
	
	УстановитьЗаголовкиДекорацийСГиперссылками();
	
	ЗаполнитьПоВидуРетроБонуса(Истина);
	УстановитьЗаголовкиПоВалюте();
	РетроБонусыКлиентСервер.СформироватьНаименованияДиапазоновШкалРасчета(ЭтотОбъект);
	РетроБонусыКлиентСервер.УстановитьЗаголовокГруппыШкалРасчета(ЭтотОбъект);
	РетроБонусыСервер.ОбновитьДиапазоныСтрокВсехТоваров(Объект, ПоказательТоваров);
	РетроБонусыСервер.ОбновитьДиапазоныСтрокВсехСвойств(Объект, ПоказательТоваров);
	УстановитьНеизменнуюНастройкуПолейПоВиду();
	
	// Установка видимости по исправлению
	ИменаЭлементов = Новый Массив; // Массив из Строка
	ИменаЭлементов.Добавить(Элементы.КонтрагентыСуммаПланДоИзменений.Имя);
	ИменаЭлементов.Добавить(Элементы.КонтрагентыНачалоДействияДоИзменений.Имя);
	ИменаЭлементов.Добавить(Элементы.КонтрагентыОкончаниеДействияДоИзменений.Имя);
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(
		Элементы, ИменаЭлементов, "Видимость", Объект.Исправление);
	
	ДенежныеСредстваСервер.УправлениеЭлементамиФормыПриЧтенииСозданииНаСервере(ЭтотОбъект);
	
	МассивЭлементов = Новый Массив; // Массив из Строка
	МассивЭлементов.Добавить(Элементы.УстановитьИнтервал.Имя);
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(
		Элементы, МассивЭлементов, "Видимость", НЕ Объект.Исправление);
	
	МассивДопустимыхТипов = Новый ФиксированныйМассив(РетроБонусыСервер.ПоддерживаемыеТипыНоменклатуры());
	ПараметрВыбораНоменклатуры = Новый ПараметрВыбора("Отбор.ТипНоменклатуры", МассивДопустимыхТипов);
	ПараметрыВыбораНоменклатуры = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрВыбораНоменклатуры); // Массив из ПараметрВыбора
	Элементы.ТоварыНоменклатура.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораНоменклатуры);
	
	УстановитьДоступныйСписокПериодичностьНачислений(ЭтотОбъект);
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре();
	
	НачалоДействияСтрокой = Формат(Объект.НачалоДействия, "ДЛФ=D;");
	ОкончаниеДействияСтрокой = Формат(Объект.ОкончаниеДействия, "ДЛФ=D;");
	СформироватьСуммыПлановСтрокой(ЭтотОбъект);
	ИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	ИспользоватьТиповыеИИндивидуальныеСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьТиповыеИИндивидуальныеСоглашенияСКлиентами");
	
	УстановитьНастройкиСведенийУчастников();
	ЗаполнитьСлужебныеРеквизитыСоглашенийДоговоров();
	УстановитьНастройкиСведенийТоваров();
	ЗаполнитьРеквизитыСоставаПоУсловию();
	
	УстановитьНастройкиПоСтатусу();
	
	НастроитьЗависимыеЭлементыФормыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкиПоСтатусу()
	
	Если Объект.Ссылка.Пустая() Тогда
		ДоступноИзменениеПоПравам = Истина;
	Иначе
		ДоступноИзменениеПоПравам = УправлениеДоступом.ИзменениеРазрешено(Объект.Ссылка);
	КонецЕсли;
	
	РазрешеноИзменение = Документы.УсловияРетроБонусовКлиентов.РазрешеноИзменениеПоСтатусу(Объект.Статус);
	ВидимостьОтменыСогласования = Ложь;
	
	Если НЕ РазрешеноИзменение Тогда
		
		ТолькоПросмотр = Истина;
		
		ВидимостьОтменыСогласования = (ДоступноИзменениеПоПравам
									   И Объект.Статус = Перечисления.СтатусыДокументовРетроБонусов.Согласован);
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		Элементы.ДекорацияОтменитьСогласование.Имя,
		"Видимость",
		ВидимостьОтменыСогласования);
	
	УстановитьВидимостьКомандФиксацииСостава(ЭтотОбъект);
	
	Если ТолькоПросмотр Тогда
		
		Элементы.Статус.СписокВыбора.Очистить();
		Элементы.Статус.РежимВыбораИзСписка = Ложь;
		
	Иначе
		
		РазрешенныеСтатусы = Документы.УсловияРетроБонусовКлиентов.РазрешенныеСтатусыКУстановке();
		
		Если РазрешенныеСтатусы.Количество() > 0 Тогда
			
			Элементы.Статус.СписокВыбора.ЗагрузитьЗначения(РазрешенныеСтатусы);
			Элементы.Статус.РежимВыбораИзСписка = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыСоставаПоУсловию()
	
	Если Объект.Проведен Тогда
		
		ДанныеУсловий = Документы.УсловияРетроБонусовКлиентов.АктуальныеДанные(Объект.Ссылка);
		Если ДанныеУсловий.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры
		 ИЛИ ДанныеУсловий.СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.СегментыПартнеров
		 ИЛИ НЕ ПустаяСтрока(ДанныеУсловий.СвойствоНоменклатуры) Тогда
			
			ДоступнаФиксацияСостава = Истина;
			
		Иначе
			
			ДоступнаФиксацияСостава = Ложь;
			
		КонецЕсли;
		
		СоставСегментовЗафиксирован = ДанныеУсловий.ФиксацияВыполнена;
		
	Иначе
		
		ДоступнаФиксацияСостава = Ложь;
		СоставСегментовЗафиксирован = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьКомандФиксацииСостава(Форма)
	
	МассивВсехЭлементов = Новый Массив; // Массив из Строка
	МассивВсехЭлементов.Добавить(Форма.Элементы.ДекорацияЗафиксироватьСостав.Имя);
	МассивВсехЭлементов.Добавить(Форма.Элементы.ДекорацияОтменитьФиксациюСостава.Имя);
	
	МассивВидимых = Новый Массив; // Массив из Строка
	
	СтатусСогласован =
		(Форма.Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовРетроБонусов.Согласован"));
	
	Если СтатусСогласован
	   И Форма.ЕстьПравоИзмененияУРБ
	   И Форма.ДоступнаФиксацияСостава
	   И НЕ Форма.СоставСегментовЗафиксирован Тогда
		
		МассивВидимых.Добавить(Форма.Элементы.ДекорацияЗафиксироватьСостав.Имя);
		
	КонецЕсли;
	
	Если СтатусСогласован
	   И Форма.ЕстьПравоИзмененияУРБ
	   И Форма.ДоступнаФиксацияСостава
	   И Форма.СоставСегментовЗафиксирован Тогда
		
		МассивВидимых.Добавить(Форма.Элементы.ДекорацияОтменитьФиксациюСостава.Имя);
		
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьВидимостьЭлементовПоМассиву(
		Форма.Элементы, МассивВсехЭлементов, МассивВидимых);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		Форма.Элементы.ДекорацияЗафиксироватьСостав.Имя,
		"Доступность",
		Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		Форма.Элементы.ДекорацияОтменитьФиксациюСостава.Имя,
		"Доступность",
		Истина);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовкиДекорацийСГиперссылками()
	
	РетроБонусыСервер.УстановитьЗаголовкиДекорацийСГиперссылками(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПредупреждениеНеобходимостиЗаполненияГраницы()
	
	ТекстПредупреждения = НСтр("ru = 'Граница должна быть задана';
								|en = 'Specify the bound'");
	Ждать ПредупреждениеАсинх(ТекстПредупреждения);
	
	ТекущийЭлемент = Элементы.ШкалыРасчетаДиапазонОт;
	Элементы.ШкалыРасчета.ИзменитьСтроку();
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПредупреждениеДубльГраницы(ЗначениеГраницы)
	
	ШаблонПредупреждения = НСтр("ru = 'Значение границы %1 уже есть в списке';
								|en = 'The list already has the %1 bound value'");
	ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонПредупреждения,
		Формат(ЗначениеГраницы, "ЧДЦ=2; ЧН=0"));
	Ждать ПредупреждениеАсинх(ТекстПредупреждения);
	
	ТекущийЭлемент = Элементы.ШкалыРасчетаДиапазонОт;
	Элементы.ШкалыРасчета.ИзменитьСтроку();
	
КонецПроцедуры

#Область УсловноеОформление

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	УстановитьОформлениеТаблицыКонтрагенты();
	УстановитьОформлениеТаблицыИННКонтрагентов();
	УстановитьОформлениеТаблицыДоговорыСоглашения();
	УстановитьОформлениеТаблицыТовары();
	УстановитьОформлениеТаблицыСвойстваНоменклатуры();
	
	РетроБонусыСервер.УстановитьОформлениеТаблицыШкалаРасчета(УсловноеОформление);
	
	ЗаказыСервер.УстановитьОформлениеОтмененнойСтроки(
		УсловноеОформление,
		Элементы.СегментыПартнеров,
		Элементы.СегментыПартнеровОтменено.Имя);
	УстановитьОформлениеИсходныхСтрокСегментыПартнеров();
	
	ЗаказыСервер.УстановитьОформлениеОтмененнойСтроки(
		УсловноеОформление,
		Элементы.СегментыТоваров,
		Элементы.СегментыТоваровОтменено.Имя);
	
	УстановитьОформлениеИсходныхСтрокСегментыТоваров();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеПериода(ТаблицаФормы, ДатаНачалоСтрокой, ДатаОкончанияСтрокой)
	
	// Заполнение датой из шапки, если не указана в строках
	
	ПутьКТЧ = ТаблицаФормы.ПутьКДанным;
	ИмяТаблицы = ТаблицаФормы.Имя;
	ИмяНачалоДействия = ИмяТаблицы + "НачалоДействия";
	ПутьКНачалоДействия = ПутьКТЧ + ".НачалоДействия";
	ИмяОкончаниеДействия = ИмяТаблицы + "ОкончаниеДействия";
	ПутьКОкончаниеДействия = ПутьКТЧ + ".ОкончаниеДействия";
	ПутьКОтменено = ПутьКТЧ + ".Отменено";
	
	#Область НачалоДействия
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяНачалоДействия);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКНачалоДействия);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКОтменено);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ДатаНачалоСтрокой);
	Элемент.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение", ГоризонтальноеПоложение.Лево);
	#КонецОбласти
	
	#Область ОкончаниеДействия
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяОкончаниеДействия);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКОкончаниеДействия);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКОтменено);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ДатаОкончанияСтрокой);
	Элемент.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение", ГоризонтальноеПоложение.Лево);
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура ОформитьОтмененныеСтроки(ТаблицаФормы, ИменаЭлементов = Неопределено)
	
	ПутьКТЧ = ТаблицаФормы.ПутьКДанным;
	ПутьКОтменено = ПутьКТЧ + ".Отменено";
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Для Каждого ИмяПоля Из ИменаЭлементов Цикл
		
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
		
	КонецЦикла;
	
	ГруппаИЛИ = РетроБонусыСервер.НоваяГруппаОтборов(Элемент.Отбор);
		
	ОтборЭлемента = ГруппаИЛИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКОтменено);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", "-");
	Элемент.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение", ГоризонтальноеПоложение.Центр);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеИсходныхСтрокСегментыПартнеров()
	
	ПараметрыОформления = РетроБонусыСервер.НовыеПараметрыОформленияИсходныхСтрок();
	
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(Элементы.СегментыПартнеровСегмент.Имя);
	
	РетроБонусыСервер.ОформитьПоляПоИсходнымСтрокам(
		УсловноеОформление,
		Элементы.СегментыПартнеров,
		ПараметрыОформления);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеИсходныхСтрокСегментыТоваров()
	
	ПараметрыОформления = РетроБонусыСервер.НовыеПараметрыОформленияИсходныхСтрок();
	
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(Элементы.СегментыТоваровСегмент.Имя);
	
	РетроБонусыСервер.ОформитьПоляПоИсходнымСтрокам(
		УсловноеОформление,
		Элементы.СегментыТоваров,
		ПараметрыОформления);
	
КонецПроцедуры

#Область УсловноеОформлениеКонтрагенты

&НаСервере
Процедура УстановитьОформлениеТаблицыКонтрагенты()
	
	ПолеКомпоновкиДанныхДатаНачалаСтрокой = Новый ПолеКомпоновкиДанных("НачалоДействияСтрокой");
	ПолеКомпоновкиДанныхДатаОкончанияСтрокой = Новый ПолеКомпоновкиДанных("ОкончаниеДействияСтрокой");
	
	ЗаказыСервер.УстановитьОформлениеОтмененнойСтроки(
		УсловноеОформление,
		Элементы.Контрагенты,
		Элементы.КонтрагентыОтменено.Имя);
	
	ИменаЭлементов = Новый Массив; // Массив из Строка
	ИменаЭлементов.Добавить(Элементы.КонтрагентыСуммаПлан.Имя);
	ИменаЭлементов.Добавить(Элементы.КонтрагентыНачалоДействия.Имя);
	ИменаЭлементов.Добавить(Элементы.КонтрагентыОкончаниеДействия.Имя);
	ОформитьОтмененныеСтроки(Элементы.Контрагенты, ИменаЭлементов);
	
	УстановитьОформлениеИсходныхСтрокКонтрагенты();
	
	УстановитьУсловноеОформлениеПериода(
		Элементы.Контрагенты,
		ПолеКомпоновкиДанныхДатаНачалаСтрокой,
		ПолеКомпоновкиДанныхДатаОкончанияСтрокой);
	
	УстановитьОформлениеКонтрагентыКонтрагент();
	УстановитьОформлениеКонтрагентыПартнер();
	УстановитьОформлениеКонтрагентыСуммаПлан();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеКонтрагентыКонтрагент()
	
	#Область ОбязательностьЗаполнения
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КонтрагентыКонтрагент.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоставУчастников");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СоставыУчастниковРетроБонусов.Клиенты;
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НачислитьСразу");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	#КонецОбласти
	
	#Область ПустойКонтрагент
	СтрокаПустогоКонтрагента = НСтр("ru = '<любой>';
									|en = '<any>'");
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КонтрагентыКонтрагент.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоставУчастников");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СоставыУчастниковРетроБонусов.Клиенты;
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НачислитьСразу");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Контрагенты.Контрагент");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(
		"ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(
		"Текст", СтрокаПустогоКонтрагента);
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеКонтрагентыПартнер()
	
	#Область ОбязательностьЗаполнения
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КонтрагентыПартнер.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоставУчастников");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СоставыУчастниковРетроБонусов.Контрагенты;
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НачислитьСразу");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	#КонецОбласти
	
	#Область ПустойПартнер
	СтрокаПустогоПартнера = НСтр("ru = '<любой>';
								|en = '<any>'");
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КонтрагентыПартнер.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоставУчастников");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СоставыУчастниковРетроБонусов.Контрагенты;
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НачислитьСразу");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Контрагенты.Партнер");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(
		"ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(
		"Текст", СтрокаПустогоПартнера);
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеКонтрагентыСуммаПлан()
	
	ПолеКомпоновкиДанныхСуммаПланаСтрокой = Новый ПолеКомпоновкиДанных("СуммаПланСтрокой");
	ПолеКомпоновкиДанныхСуммаПланСтрокойНеизменный = Новый ПолеКомпоновкиДанных("СуммаПланСтрокойНеизменный");
	СоставыУчастников = Перечисления.СоставыУчастниковРетроБонусов;
	
	#Область ПустойПлан
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КонтрагентыСуммаПлан.Имя);
	
	Показатели = Новый СписокЗначений; // СписокЗначений из ПеречислениеСсылка.СоставыУчастниковРетроБонусов
	Показатели.Добавить(СоставыУчастников.Контрагенты);
	Показатели.Добавить(СоставыУчастников.Клиенты);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоставУчастников");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ОтборЭлемента.ПравоеЗначение = Показатели;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Контрагенты.СуммаПлан");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Контрагенты.ПланНеУказывается");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Контрагенты.Отменено");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ПолеКомпоновкиДанныхСуммаПланаСтрокой);
	#КонецОбласти
	
	#Область ПланНеУказывается
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КонтрагентыСуммаПлан.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Контрагенты.ПланНеУказывается");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ПолеКомпоновкиДанныхСуммаПланСтрокойНеизменный);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеИсходныхСтрокКонтрагенты()
	
	ПараметрыОформления = РетроБонусыСервер.НовыеПараметрыОформленияИсходныхСтрок();
	
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(Элементы.КонтрагентыКонтрагент.Имя);
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(Элементы.КонтрагентыПартнер.Имя);
	
	ПараметрыОформления.ПоляДоИзменений.Добавить(Элементы.КонтрагентыСуммаПланДоИзменений.Имя);
	ПараметрыОформления.ПоляДоИзменений.Добавить(Элементы.КонтрагентыНачалоДействияДоИзменений.Имя);
	ПараметрыОформления.ПоляДоИзменений.Добавить(Элементы.КонтрагентыОкончаниеДействияДоИзменений.Имя);
	
	РетроБонусыСервер.ОформитьПоляПоИсходнымСтрокам(
		УсловноеОформление,
		Элементы.Контрагенты,
		ПараметрыОформления);
	
КонецПроцедуры

#КонецОбласти

#Область УсловноеОформлениеИННКонтрагентов

&НаСервере
Процедура УстановитьОформлениеТаблицыИННКонтрагентов()
	
	ПолеКомпоновкиДанныхДатаНачалаСтрокой = Новый ПолеКомпоновкиДанных("НачалоДействияСтрокой");
	ПолеКомпоновкиДанныхДатаОкончанияСтрокой = Новый ПолеКомпоновкиДанных("ОкончаниеДействияСтрокой");
	
	ЗаказыСервер.УстановитьОформлениеОтмененнойСтроки(
		УсловноеОформление,
		Элементы.ИННКонтрагентов,
		Элементы.ИННКонтрагентовОтменено.Имя);
	
	ИменаЭлементов = Новый Массив; // Массив из Строка
	ИменаЭлементов.Добавить(Элементы.ИННКонтрагентовСуммаПлан.Имя);
	ИменаЭлементов.Добавить(Элементы.ИННКонтрагентовНачалоДействия.Имя);
	ИменаЭлементов.Добавить(Элементы.ИННКонтрагентовОкончаниеДействия.Имя);
	ОформитьОтмененныеСтроки(Элементы.ИННКонтрагентов, ИменаЭлементов);
	
	УстановитьОформлениеИсходныхСтрокИННКонтрагентов();
	
	УстановитьУсловноеОформлениеПериода(
		Элементы.ИННКонтрагентов,
		ПолеКомпоновкиДанныхДатаНачалаСтрокой,
		ПолеКомпоновкиДанныхДатаОкончанияСтрокой);
	
	УстановитьОформлениеИННКонтрагентовСуммаПлан();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеИННКонтрагентовСуммаПлан()
	
	ПолеКомпоновкиДанныхСуммаПланаСтрокой = Новый ПолеКомпоновкиДанных("СуммаПланСтрокой");
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИННКонтрагентовСуммаПлан.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоставУчастников");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СоставыУчастниковРетроБонусов.ИНН;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИННКонтрагентов.СуммаПлан");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИННКонтрагентов.Отменено");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ПолеКомпоновкиДанныхСуммаПланаСтрокой);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеИсходныхСтрокИННКонтрагентов()
	
	ПараметрыОформления = РетроБонусыСервер.НовыеПараметрыОформленияИсходныхСтрок();
	
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(Элементы.ИННКонтрагентовИНН.Имя);
	
	ПараметрыОформления.ПоляДоИзменений.Добавить(Элементы.ИННКонтрагентовСуммаПланДоИзменений.Имя);
	ПараметрыОформления.ПоляДоИзменений.Добавить(Элементы.ИННКонтрагентовНачалоДействияДоИзменений.Имя);
	ПараметрыОформления.ПоляДоИзменений.Добавить(Элементы.ИННКонтрагентовОкончаниеДействияДоИзменений.Имя);
	
	РетроБонусыСервер.ОформитьПоляПоИсходнымСтрокам(
		УсловноеОформление,
		Элементы.ИННКонтрагентов,
		ПараметрыОформления);
	
КонецПроцедуры

#КонецОбласти

#Область УсловноеОформлениеДоговорыСоглашения

&НаСервере
Процедура УстановитьОформлениеТаблицыДоговорыСоглашения()
	
	ПолеКомпоновкиДанныхДатаНачалаСтрокой = Новый ПолеКомпоновкиДанных("НачалоДействияСтрокой");
	ПолеКомпоновкиДанныхДатаОкончанияСтрокой = Новый ПолеКомпоновкиДанных("ОкончаниеДействияСтрокой");
	
	ЗаказыСервер.УстановитьОформлениеОтмененнойСтроки(
		УсловноеОформление,
		Элементы.ДоговорыСоглашения,
		Элементы.ДоговорыСоглашенияОтменено.Имя);
	
	ИменаЭлементов = Новый Массив; // Массив из Строка
	ИменаЭлементов.Добавить(Элементы.ДоговорыСоглашенияСуммаПлан.Имя);
	ИменаЭлементов.Добавить(Элементы.ДоговорыСоглашенияНачалоДействия.Имя);
	ИменаЭлементов.Добавить(Элементы.ДоговорыСоглашенияОкончаниеДействия.Имя);
	ОформитьОтмененныеСтроки(Элементы.ДоговорыСоглашения, ИменаЭлементов);
	
	УстановитьОформлениеИсходныхСтрокДоговорыСоглашения();
	
	УстановитьУсловноеОформлениеПериода(
		Элементы.ДоговорыСоглашения,
		ПолеКомпоновкиДанныхДатаНачалаСтрокой,
		ПолеКомпоновкиДанныхДатаОкончанияСтрокой);
	
	УстановитьОформлениеДоговорыСоглашенияСуммаПлан();
	УстановитьОформлениеДоговорыСоглашенияКонтрагент();
	УстановитьОформлениеДоговорыСоглашенияПартнер();
	УстановитьОформлениеДоговорыСоглашенияДоговор();
	УстановитьОформлениеДоговорыСоглашенияТипСоглашения();
	УстановитьОформлениеДоговорыСоглашенияПродажиИндивидуальныеПоТиповым();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеИсходныхСтрокДоговорыСоглашения()
	
	ПараметрыОформления = РетроБонусыСервер.НовыеПараметрыОформленияИсходныхСтрок();
	
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(Элементы.ДоговорыСоглашенияПартнер.Имя);
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(Элементы.ДоговорыСоглашенияКонтрагент.Имя);
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(Элементы.ДоговорыСоглашенияДоговор.Имя);
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(Элементы.ДоговорыСоглашенияСоглашение.Имя);
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(Элементы.ДоговорыСоглашенияПродажиИндивидуальныеПоТиповым.Имя);
	
	ПараметрыОформления.ПоляДоИзменений.Добавить(Элементы.ДоговорыСоглашенияСуммаПланДоИзменений.Имя);
	ПараметрыОформления.ПоляДоИзменений.Добавить(Элементы.ДоговорыСоглашенияНачалоДействияДоИзменений.Имя);
	ПараметрыОформления.ПоляДоИзменений.Добавить(Элементы.ДоговорыСоглашенияОкончаниеДействияДоИзменений.Имя);
	
	РетроБонусыСервер.ОформитьПоляПоИсходнымСтрокам(
		УсловноеОформление,
		Элементы.ДоговорыСоглашения,
		ПараметрыОформления);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеДоговорыСоглашенияСуммаПлан()
	
	ПолеКомпоновкиДанныхСуммаПланаСтрокой = Новый ПолеКомпоновкиДанных("СуммаПланСтрокой");
	ПолеКомпоновкиДанныхСуммаПланСтрокойНеизменный = Новый ПолеКомпоновкиДанных("СуммаПланСтрокойНеизменный");
	СоставыУчастников = Перечисления.СоставыУчастниковРетроБонусов;
	
	#Область ПустойПлан
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДоговорыСоглашенияСуммаПлан.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоставУчастников");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = СоставыУчастников.Договоры;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДоговорыСоглашения.СуммаПлан");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДоговорыСоглашения.ПланНеУказывается");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДоговорыСоглашения.Отменено");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ПолеКомпоновкиДанныхСуммаПланаСтрокой);
	#КонецОбласти
	
	#Область ПланНеУказывается
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДоговорыСоглашенияСуммаПлан.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДоговорыСоглашения.ПланНеУказывается");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ПолеКомпоновкиДанныхСуммаПланСтрокойНеизменный);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеДоговорыСоглашенияДоговор()
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДоговорыСоглашенияДоговор.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Организация");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	СтрокаУкажитеОрганизацию = НСтр("ru = '<укажите организацию>';
									|en = '<specify the company>'");
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", СтрокаУкажитеОрганизацию);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеДоговорыСоглашенияКонтрагент()
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДоговорыСоглашенияКонтрагент.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДоговорыСоглашения.Контрагент");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	СтрокаДляВсех = НСтр("ru = '<все>';
						|en = '<all>'");
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", СтрокаДляВсех);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеДоговорыСоглашенияПартнер()
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДоговорыСоглашенияПартнер.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДоговорыСоглашения.Партнер");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	СтрокаДляВсех = НСтр("ru = '<все>';
						|en = '<all>'");
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", СтрокаДляВсех);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеДоговорыСоглашенияТипСоглашения()
	
	#Область СоглашениеНеЗаполнено
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДоговорыСоглашенияТипСоглашения.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДоговорыСоглашения.Соглашение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ТекстНеЗаполнено = НСтр("ru = '<укажите соглашение>';
							|en = '<specify the terms of sales>'");
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ТекстНеЗаполнено);
	Элемент.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение", ГоризонтальноеПоложение.Центр);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	#КонецОбласти
	
	#Область ТиповоеСоглашение
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДоговорыСоглашенияТипСоглашения.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДоговорыСоглашения.Соглашение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДоговорыСоглашения.ЭтоТиповоеСоглашение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ТекстТиповое = НСтр("ru = 'Типовое';
						|en = 'Standard'");
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ТекстТиповое);
	#КонецОбласти
	
	#Область ИндивидуальноеСоглашение
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДоговорыСоглашенияТипСоглашения.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДоговорыСоглашения.Соглашение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДоговорыСоглашения.ЭтоТиповоеСоглашение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ТекстИндивидуальное = НСтр("ru = 'Индивидуальное';
								|en = 'Individual'");
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ТекстИндивидуальное);
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеДоговорыСоглашенияПродажиИндивидуальныеПоТиповым()
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДоговорыСоглашенияПродажиИндивидуальныеПоТиповым.Имя);
	
	ГруппаИЛИ = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИЛИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ОтборЭлемента = ГруппаИЛИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДоговорыСоглашения.Соглашение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ГруппаИ = ГруппаИЛИ.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДоговорыСоглашения.Соглашение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ОтборЭлемента = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДоговорыСоглашения.ЭтоТиповоеСоглашение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры
#КонецОбласти

#Область УсловноеОформлениеОформлениеТовары

&НаСервере
Процедура УстановитьОформлениеТаблицыТовары()
	
	ПолеКомпоновкиДанныхДатаНачалаСтрокой = Новый ПолеКомпоновкиДанных("НачалоДействияСтрокой");
	ПолеКомпоновкиДанныхДатаОкончанияСтрокой = Новый ПолеКомпоновкиДанных("ОкончаниеДействияСтрокой");
	
	РетроБонусыСервер.УстановитьОформлениеТаблицыТоварыХарактеристика(ЭтотОбъект);
	
	ЗаказыСервер.УстановитьОформлениеОтмененнойСтроки(
		УсловноеОформление,
		Элементы.Товары,
		Элементы.ТоварыОтменено.Имя);
	
	ИменаЭлементов = Новый Массив; // Массив из Строка
	ИменаЭлементов.Добавить(Элементы.ТоварыНачалоДействия.Имя);
	ИменаЭлементов.Добавить(Элементы.ТоварыОкончаниеДействия.Имя);
	ОформитьОтмененныеСтроки(Элементы.Товары, ИменаЭлементов);
	
	РетроБонусыСервер.УстановитьОформлениеТаблицыПолеКоличествоПлан(УсловноеОформление, "Товары");
	РетроБонусыСервер.УстановитьОформлениеТаблицыПолеПроцент(УсловноеОформление, "Товары");
	
	УстановитьОформлениеИсходныхСтрокТаблицыТовары();
	
	УстановитьУсловноеОформлениеПериода(
		Элементы.Товары,
		ПолеКомпоновкиДанныхДатаНачалаСтрокой,
		ПолеКомпоновкиДанныхДатаОкончанияСтрокой);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеИсходныхСтрокТаблицыТовары()
	
	ИмяТаблицы = "Товары";
	ПараметрыОформления = РетроБонусыСервер.НовыеПараметрыОформленияИсходныхСтрок();
	
	ПолеНоменклатура = ИмяТаблицы + "Номенклатура";
	ПолеХарактеристика = ИмяТаблицы + "Характеристика";
	ПолеКоличествоПлан = ИмяТаблицы + "КоличествоПлан";
	ПолеБазоваяЦена = ИмяТаблицы + "БазоваяЦена";
	ПолеПроцент = ИмяТаблицы + "Процент";
	
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(ПолеНоменклатура);
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(ПолеХарактеристика);
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(ПолеКоличествоПлан);
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(ПолеБазоваяЦена);
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(ПолеПроцент);
	
	ПолеНачалоДействияДоИзменений = ИмяТаблицы + "НачалоДействияДоИзменений";
	ПолеОкончаниеДействияДоИзменений = ИмяТаблицы + "ОкончаниеДействияДоИзменений";
	
	ПараметрыОформления.ПоляДоИзменений.Добавить(ПолеНачалоДействияДоИзменений);
	ПараметрыОформления.ПоляДоИзменений.Добавить(ПолеОкончаниеДействияДоИзменений);
	
	РетроБонусыСервер.ОформитьПоляПоИсходнымСтрокам(
		УсловноеОформление,
		Элементы[ИмяТаблицы],
		ПараметрыОформления);
	
КонецПроцедуры

#КонецОбласти

#Область УстановитьОформлениеСвойстваНоменклатуры

&НаСервере
Процедура УстановитьОформлениеТаблицыСвойстваНоменклатуры()
	
	ПолеКомпоновкиДанныхДатаНачалаСтрокой = Новый ПолеКомпоновкиДанных("НачалоДействияСтрокой");
	ПолеКомпоновкиДанныхДатаОкончанияСтрокой = Новый ПолеКомпоновкиДанных("ОкончаниеДействияСтрокой");
	
	ЗаказыСервер.УстановитьОформлениеОтмененнойСтроки(
		УсловноеОформление,
		Элементы.СвойстваНоменклатуры,
		Элементы.СвойстваНоменклатурыОтменено.Имя);
	
	ИменаЭлементов = Новый Массив; // Массив из Строка
	ИменаЭлементов.Добавить(Элементы.СвойстваНоменклатурыНачалоДействия.Имя);
	ИменаЭлементов.Добавить(Элементы.СвойстваНоменклатурыОкончаниеДействия.Имя);
	ОформитьОтмененныеСтроки(Элементы.СвойстваНоменклатуры, ИменаЭлементов);
	
	РетроБонусыСервер.УстановитьОформлениеТаблицыПолеКоличествоПлан(УсловноеОформление, "СвойстваНоменклатуры");
	РетроБонусыСервер.УстановитьОформлениеТаблицыПолеПроцент(УсловноеОформление, "СвойстваНоменклатуры");
	РетроБонусыСервер.УстановитьОформлениеИсходныхСтрокТаблицыСвойстваНоменклатуры(Элементы, УсловноеОформление);
	
	УстановитьУсловноеОформлениеПериода(
		Элементы.СвойстваНоменклатуры,
		ПолеКомпоновкиДанныхДатаНачалаСтрокой,
		ПолеКомпоновкиДанныхДатаОкончанияСтрокой);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормыВспомогательные

// Параметры:
//  Результат - Неопределено - Результат
//  ДополнительныеПараметры - Структура - Дополнительные параметры
// 
&НаКлиенте
Процедура УстановитьИнтервалПослеЗавершения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НачалоДействияСтрокой = Формат(Объект.НачалоДействия, "ДЛФ=D;");
	ОкончаниеДействияСтрокой = Формат(Объект.ОкончаниеДействия, "ДЛФ=D;");
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииСервер()
	
	ОпределитьВалютуДокумента();
	УстановитьЗаголовкиПоВалюте();
	
	Объект.ДоговорыСоглашения.Очистить();
	
	НастроитьЗависимыеЭлементыФормыНаСервере("Организация");
	
КонецПроцедуры

&НаСервере
Процедура ВидРетроБонусаПриИзмененииСервер()
	
	ЗаполнитьПоВидуРетроБонуса();
	УстановитьЗаголовкиПоВалюте();
	УстановитьПризнакиРасчетаПланаУчастников();
	УстановитьНеизменнуюНастройкуПолейПоВиду();
	
	ОтборУчастниковПриИзмененииСервер(Ложь);
	ОтборТоваровПриИзмененииСервер(Ложь);
	УстановитьДоступныйСписокПериодичностьНачислений(ЭтотОбъект);
	
	РасчетПоШкале =
		(ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма
		 И Объект.ВидШкалыРасчета <> 0);
	
	Если ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма
	 ИЛИ РасчетПоШкале Тогда
		
		Объект.СуммаПлан = 0;
		
	КонецЕсли;
	
	Если ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.НеИспользуется
	 ИЛИ РасчетПоШкале Тогда
		
		Объект.БонусПроцент = 0;
		Объект.ЗапретНачисленияСверхПлана = Ложь;
		
	КонецЕсли;
	
	Если ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма Тогда
		
		Объект.БонусПроцент = 0;
		
	КонецЕсли;
	
	Если Объект.БезРасчета Тогда
		
		Для Каждого СтрокаКонтрагенты Из Объект.Контрагенты Цикл
			
			СтрокаКонтрагенты.СуммаПлан = 0;
			СтрокаКонтрагенты.НачалоДействия = Дата(1, 1, 1);
			СтрокаКонтрагенты.ОкончаниеДействия = Дата(1, 1, 1);
			
		КонецЦикла;
		
		Объект.ИННКонтрагентов.Очистить();
		Объект.Товары.Очистить();
		
	Иначе
		
		Для Каждого СтрокаКонтрагенты Из Объект.Контрагенты Цикл
			
			СтрокаКонтрагенты.СуммаБонус = 0;
			
		КонецЦикла;
		
		Объект.НачислитьСразу = Ложь;
		
	КонецЕсли;
		
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	НастроитьЗависимыеЭлементыФормыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ДетализацияРасчетаУчастниковПриИзмененииСервер()
	
	УстановитьПризнакиРасчетаПланаУчастников();
	НастроитьЗависимыеЭлементыФормыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОтборУчастниковПриИзмененииСервер(Знач НастроитьЗависимыеЭлементы = Истина)
	
	УстановитьНастройкиСведенийУчастников();
	
	ПараметрыУчастников = Документы.УсловияРетроБонусовКлиентов.ПараметрыОчисткиРеквизитовУчастников();
	ПараметрыУчастников.СоставУчастников = СоставУчастников;
	ПараметрыУчастников.ОтборУчастников = Объект.ОтборУчастников;
	ПараметрыУчастников.ПоказательТоваров = ПоказательТоваров;
	Документы.УсловияРетроБонусовКлиентов.ОчиститьНеиспользуемыеРеквизитыУчастников(Объект, ПараметрыУчастников);
	
	Если НастроитьЗависимыеЭлементы Тогда
		
		НастроитьЗависимыеЭлементыФормыНаСервере("ОтборУчастников");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтборТоваровПриИзмененииСервер(Знач НастроитьЗависимыеЭлементы = Истина)
	
	УстановитьНастройкиСведенийТоваров();
	
	ПараметрыТоваров = Документы.УсловияРетроБонусовКлиентов.ПараметрыОчисткиРеквизитовТоваров();
	ПараметрыТоваров.СоставТоваров = СоставТоваров;
	ПараметрыТоваров.ОтборТоваров = Объект.ОтборТоваров;
	ПараметрыТоваров.ПоказательТоваров = ПоказательТоваров;
	ПараметрыТоваров.БазаРасчета = БазаРасчетаПродаж;
	Документы.УсловияРетроБонусовКлиентов.ОчиститьНеиспользуемыеРеквизитыТоваров(Объект, ПараметрыТоваров);
	
	Если НастроитьЗависимыеЭлементы Тогда
		
		НастроитьЗависимыеЭлементыФормыНаСервере("ОтборТоваров");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВидШкалыРасчетаПриИзмененииСервер()
	
	Если Объект.ВидШкалыРасчета = 0 Тогда
		
		Объект.ШкалыРасчета.Очистить();
		
	КонецЕсли;
	
	Если ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество Тогда
		
		Объект.ШкалыРасчета.Очистить();
		
		Если Объект.ВидШкалыРасчета = 0 Тогда // Прогрессивная шкала
			
			РетроБонусыСервер.ИнициализироватьПрогрессивнуюШкалу(ЭтотОбъект, "Товары");
			РетроБонусыСервер.ИнициализироватьПрогрессивнуюШкалу(ЭтотОбъект, "СвойстваНоменклатуры");
			
		КонецЕсли;
		
		Если Объект.ВидШкалыРасчета = 1 Тогда // По диапазонам
			
			Объект.ЗапретНачисленияСверхПлана = Ложь;
			РетроБонусыСервер.ИнициализироватьДиапазоныТоваров(ЭтотОбъект);
			РетроБонусыСервер.ИнициализироватьДиапазоныСвойств(ЭтотОбъект);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма Тогда
		
		Если Объект.ВидШкалыРасчета <> 0 Тогда // Не единственное значение
			
			Объект.БонусПроцент = 0;
			Объект.СуммаПлан = 0;
			
		КонецЕсли;
		
		Если Объект.ВидШкалыРасчета = 1 Тогда // Прогрессивная шкала
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ДиапазонОт", 0);
			
			НайденныеСтроки = Объект.ШкалыРасчета.НайтиСтроки(ПараметрыОтбора);
			Для Каждого СтрокаШкалы Из НайденныеСтроки Цикл
				Объект.ШкалыРасчета.Удалить(СтрокаШкалы);
			КонецЦикла;
			
		КонецЕсли;
		
		Если Объект.ВидШкалыРасчета = 2 Тогда // Диапазоны
			
			Объект.ЗапретНачисленияСверхПлана = Ложь;
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ДиапазонОт", 0);
			
			НайденныеСтроки = Объект.ШкалыРасчета.НайтиСтроки(ПараметрыОтбора);
			Если НайденныеСтроки.Количество() = 0 Тогда
				
				Объект.ШкалыРасчета.Вставить(0);
				РетроБонусыКлиентСервер.СформироватьНаименованияДиапазоновШкалРасчета(ЭтотОбъект);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормыНаСервере();
	РетроБонусыКлиентСервер.УстановитьЗаголовокГруппыШкалРасчета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьСогласование()
	
	ЗаполнитьПризнакНаличияНачислений();
	
	Если НЕ ЕстьНачисления Тогда
		
		Модифицированность = Истина;
		ТолькоПросмотр = Ложь;
		Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовРетроБонусов.Черновик");
		ТекущийСтатус = Объект.Статус;
		УстановитьНастройкиПоСтатусу();
		
		// СтандартныеПодсистемы.ПодключаемыеКоманды
		ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
		// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
	Иначе
		
		УстановитьЗаголовкиДекорацийСГиперссылками();
		
		ТекстСообщения = НСтр("ru = 'Созданы документы начисления. Отмена согласования запрещена.';
								|en = 'Accrual documents are created. Cannot cancel the approval.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "Объект", "Статус");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗафиксироватьСоставДокумента()
	
	ОбновитьДанныеПередФиксациейСостава();
	Если НЕ ЕстьКорректировкаНаСогласовании
	   И НЕ СоставСегментовЗафиксирован Тогда
		
		ТекстВопроса = НСтр("ru = 'Зафиксировать состав?';
							|en = 'Do you want to record the document?'");
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			
			ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗафиксироватьСоставДокументаЗавершение", ЭтотОбъект);
			ОповещениеОперацияВыполняется = Новый ОписаниеОповещения(
				"ВыполняютсяОперацииПоФиксацииСоставаДокумента",
				ЭтотОбъект);
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				Элементы.ДекорацияЗафиксироватьСостав.Имя,
				"Доступность",
				Ложь);
			
			РетроБонусыКлиент.ЗафиксироватьСоставДокументаУсловий(
				Объект.Ссылка,
				ЭтотОбъект,
				ОповещениеОЗавершении,
				ОповещениеОперацияВыполняется);
			
		КонецЕсли;
		
	Иначе
		
		Если ЕстьКорректировкаНаСогласовании Тогда
			
			ТекстСообщения = НСтр("ru = 'Невозможно зафиксировать состав документа: введена корректировка в статусе ""На согласовании"".';
									|en = 'Cannot record the document: an adjustment is registered in the ""Pending approval"" status.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
		
		УстановитьВидимостьКомандФиксацииСостава(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ОтменитьФиксациюСоставаДокумента()
	
	ОбновитьДанныеПередОтменойФиксацииСостава();
	Если НЕ ЕстьНачисления
	   И СоставСегментовЗафиксирован Тогда
		
		ТекстВопроса = НСтр("ru = 'Отменить фиксацию состава?';
							|en = 'Do you want to cancel recording?'");
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			
			ОповещениеОЗавершении = Новый ОписаниеОповещения("ОтменитьФиксациюСоставаДокументаЗавершение", ЭтотОбъект);
			ОповещениеОперацияВыполняется = Новый ОписаниеОповещения(
				"ВыполняютсяОперацииПоФиксацииСоставаДокумента",
				ЭтотОбъект);
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				Элементы.ДекорацияОтменитьФиксациюСостава.Имя,
				"Доступность",
				Ложь);
			
			РетроБонусыКлиент.ОтменитьФиксациюСоставаДокументаУсловий(
				Объект.Ссылка,
				ЭтотОбъект,
				ОповещениеОЗавершении,
				ОповещениеОперацияВыполняется);
			
		КонецЕсли;
		
	Иначе
		
		УстановитьВидимостьКомандФиксацииСостава(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  Результат - см. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
//  ДополнительныеПараметры - Неопределено
//
&НаКлиенте
Процедура ЗафиксироватьСоставДокументаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Если Результат.Статус = "Ошибка" Тогда
			
			СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(Результат.ИнформацияОбОшибке);
			
		Иначе
			
			СоставСегментовЗафиксирован = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьДанныеПередФиксациейСостава();
	УстановитьВидимостьКомандФиксацииСостава(ЭтотОбъект);
	
КонецПроцедуры

// Параметры:
//  Результат - см. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
//  ДополнительныеПараметры - Неопределено
//
&НаКлиенте
Процедура ОтменитьФиксациюСоставаДокументаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Если Результат.Статус = "Ошибка" Тогда
			
			СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(Результат.ИнформацияОбОшибке);
			
		Иначе
			
			СоставСегментовЗафиксирован = Ложь;
			
		КонецЕсли;
		
	Иначе
		ЗаполнитьРеквизитыСоставаПоУсловию();
	КонецЕсли;
	
	УстановитьВидимостьКомандФиксацииСостава(ЭтотОбъект);
	
КонецПроцедуры

// Параметры:
//  Результат - см. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
//  ДополнительныеПараметры - Неопределено
//
&НаКлиенте
Процедура ВыполняютсяОперацииПоФиксацииСоставаДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	ТекстСообщения = НСтр("ru = 'По данному документу уже выполняются операции по фиксации состава. Попробуйте позже.';
							|en = 'Recording operations for the document are already in process. Try again later.'");
	
	ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	УстановитьВидимостьКомандФиксацииСостава(ЭтотОбъект);
	
КонецПроцедуры

// Параметры:
//  Результат - СправочникСсылка.СегментыНоменклатуры, Неопределено -
//  ДополнительныеПараметры - Неопределено
//
&НаКлиенте
Процедура ЗаполнитьТоварыПоСегментуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		ВыбранныйСегмент = Результат;
		ЗаполнитьТоварыПоСегментуСервер(ВыбранныйСегмент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТоварыПоСегментуСервер(Знач Сегмент)
	
	СпособФормирования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сегмент, "СпособФормирования");
	Если СпособФормирования <> Перечисления.СпособыФормированияСегментов.ФормироватьДинамически Тогда
		
		ВыборкаНоменклатуры = ТоварыСтатическогоСегмента(Сегмент);
		
	Иначе
		
		ВыборкаНоменклатуры = ТоварыДинамическогоСегмента(Сегмент);
		
	КонецЕсли;
	
	Пока ВыборкаНоменклатуры.Следующий() Цикл
		
		СтрокаТовары = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТовары, ВыборкаНоменклатуры);
		
	КонецЦикла;
	
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыТоварыВспомогательные

&НаСервере
Процедура ТоварыКоличествоПланПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор)
	
	СтрокаТоваров = Объект.Товары.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	ИменаПолей = "Номенклатура, Характеристика, ХарактеристикиИспользуются";
	
	СтрокиКВыгрузке = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаТоваров);
	ТаблицаКОбработке = Объект.Товары.Выгрузить(СтрокиКВыгрузке, ИменаПолей);
	РетроБонусыСервер.ОбновитьДиапазоныСтрокТоваровПоТаблице(Объект, ТаблицаКОбработке);
	
КонецПроцедуры

&НаСервере
Процедура ТоварыОтмененоПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор)
	
	СтрокаТоваров = Объект.Товары.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	Если СтрокаТоваров.Отменено Тогда
		
		СтрокаТоваров.НачалоДействия = Дата(1, 1, 1);
		СтрокаТоваров.ОкончаниеДействия = Дата(1, 1, 1);
		СтрокаТоваров.ОбозначениеДиапазона = "";
		
	ИначеЕсли РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров) Тогда
		
		РетроБонусыСервер.ЗаполнитьЕдиныеДанныеДиапазонаСтрокиТовара(Объект, ТекущаяСтрокаИдентификатор, Истина);
		
	КонецЕсли;
	
	ИменаПолей = "Номенклатура, Характеристика, ХарактеристикиИспользуются";
	
	СтрокиКВыгрузке = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаТоваров);
	ТаблицаКОбработке = Объект.Товары.Выгрузить(СтрокиКВыгрузке, ИменаПолей);
	РетроБонусыСервер.ОбновитьДиапазоныСтрокТоваровПоТаблице(Объект, ТаблицаКОбработке);
	
КонецПроцедуры

&НаСервере
Процедура ТоварыБазоваяЦенаПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор)
	
	СтрокаТоваров = Объект.Товары.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура", СтрокаТоваров.Номенклатура);
	ПараметрыОтбора.Вставить("Характеристика", СтрокаТоваров.Характеристика);
	ПараметрыОтбора.Вставить("Отменено", Ложь);
	
	РетроБонусыСервер.ОбновитьПолеТаблицыПоОтбору(
		Объект.Товары,
		ПараметрыОтбора,
		СтрокаТоваров.БазоваяЦена,
		"БазоваяЦена",
		Истина);
	
КонецПроцедуры

&НаСервере
Процедура ТоварыНачалоДействияПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор)
	
	СтрокаТоваров = Объект.Товары.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура", СтрокаТоваров.Номенклатура);
	ПараметрыОтбора.Вставить("Характеристика", СтрокаТоваров.Характеристика);
	ПараметрыОтбора.Вставить("Отменено", Ложь);
	
	РетроБонусыСервер.ОбновитьПолеТаблицыПоОтбору(
		Объект.Товары,
		ПараметрыОтбора,
		СтрокаТоваров.НачалоДействия,
		"НачалоДействия");
	
КонецПроцедуры

&НаСервере
Процедура ТоварыОкончаниеДействияПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор)
	
	СтрокаТоваров = Объект.Товары.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура", СтрокаТоваров.Номенклатура);
	ПараметрыОтбора.Вставить("Характеристика", СтрокаТоваров.Характеристика);
	ПараметрыОтбора.Вставить("Отменено", Ложь);
	
	РетроБонусыСервер.ОбновитьПолеТаблицыПоОтбору(
		Объект.Товары,
		ПараметрыОтбора,
		СтрокаТоваров.ОкончаниеДействия,
		"ОкончаниеДействия");
	
КонецПроцедуры

// Параметры:
//  ТекущаяСтрокаИдентификатор - Число - Текущая строка идентификатор
//  АналитикаТекущейСтрокиТоваров - см. РетроБонусыКлиентСервер.СтруктураСохраняемойСтрокиТоваров
//  АналитикаИзменяемойСтрокиТоваров - см. РетроБонусыКлиентСервер.СтруктураСохраняемойСтрокиТоваров
//  КешированныеЗначения - см. ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения
//
&НаСервере
Процедура ТоварыНоменклатураПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор, АналитикаТекущейСтрокиТоваров, АналитикаИзменяемойСтрокиТоваров, КешированныеЗначения)
	
	СтрокаТоваров = Объект.Товары.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", СтрокаТоваров.Характеристика);
	СтруктураДействий.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
	
	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(
		СтрокаТоваров,
		СтруктураДействий,
		КешированныеЗначения);
	
	ПараметрыЗаполнения = РетроБонусыКлиентСервер.ПараметрыЗаполненияАналитикиИзменяемойСтроки();
	ПараметрыЗаполнения.ВидШкалыРасчета = Объект.ВидШкалыРасчета;
	ПараметрыЗаполнения.ПоказательТоваров = ПоказательТоваров;
	ПараметрыЗаполнения.Номенклатура = СтрокаТоваров.Номенклатура;
	ПараметрыЗаполнения.Характеристика = СтрокаТоваров.Характеристика;
	ПараметрыЗаполнения.ХарактеристикиИспользуются = СтрокаТоваров.ХарактеристикиИспользуются;
	
	РетроБонусыКлиентСервер.СохранитьАналитикуСтрокиТоваров(
		АналитикаИзменяемойСтрокиТоваров, ПараметрыЗаполнения);
	
	Если РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров)
	   И НЕ АналитикаИзменяемойСтрокиТоваров.Номенклатура.Пустая() Тогда
		
		РетроБонусыСервер.ЗаполнитьЕдиныеДанныеДиапазонаСтрокиТовара(Объект, ТекущаяСтрокаИдентификатор);
		ОбновитьДиапазоныСтрокТоваровПоТовару(АналитикаТекущейСтрокиТоваров);
		ОбновитьДиапазоныСтрокТоваровПоТовару(АналитикаИзменяемойСтрокиТоваров);
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ТекущаяСтрокаИдентификатор - Число - Текущая строка идентификатор
//  АналитикаТекущейСтрокиТоваров - см. РетроБонусыКлиентСервер.СтруктураСохраняемойСтрокиТоваров
//  АналитикаИзменяемойСтрокиТоваров - см. РетроБонусыКлиентСервер.СтруктураСохраняемойСтрокиТоваров
//
&НаСервере
Процедура ТоварыХарактеристикаПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор, АналитикаТекущейСтрокиТоваров, АналитикаИзменяемойСтрокиТоваров)
	
	СтрокаТоваров = Объект.Товары.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	ПараметрыЗаполнения = РетроБонусыКлиентСервер.ПараметрыЗаполненияАналитикиИзменяемойСтроки();
	ПараметрыЗаполнения.ВидШкалыРасчета = Объект.ВидШкалыРасчета;
	ПараметрыЗаполнения.ПоказательТоваров = ПоказательТоваров;
	ПараметрыЗаполнения.Номенклатура = СтрокаТоваров.Номенклатура;
	ПараметрыЗаполнения.Характеристика = СтрокаТоваров.Характеристика;
	ПараметрыЗаполнения.ХарактеристикиИспользуются = СтрокаТоваров.ХарактеристикиИспользуются;
	
	РетроБонусыКлиентСервер.СохранитьАналитикуСтрокиТоваров(
		АналитикаИзменяемойСтрокиТоваров, ПараметрыЗаполнения);
	
	Если РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров)
	   И НЕ АналитикаИзменяемойСтрокиТоваров.Номенклатура.Пустая() Тогда
		
		РетроБонусыСервер.ЗаполнитьЕдиныеДанныеДиапазонаСтрокиТовара(Объект, ТекущаяСтрокаИдентификатор);
		ОбновитьДиапазоныСтрокТоваровПоТовару(АналитикаТекущейСтрокиТоваров);
		ОбновитьДиапазоныСтрокТоваровПоТовару(АналитикаИзменяемойСтрокиТоваров);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыСвойстваНоменклатурыВспомогательные

&НаСервере
Процедура СвойстваНоменклатурыКоличествоПланПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор)
	
	СтрокаСвойства = Объект.СвойстваНоменклатуры.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	ИменаПолей = "ЗначениеСвойства";
	
	СтрокиКВыгрузке = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаСвойства);
	ТаблицаКОбработке = Объект.СвойстваНоменклатуры.Выгрузить(СтрокиКВыгрузке, ИменаПолей);
	РетроБонусыСервер.ОбновитьДиапазоныСтрокСвойствПоТаблице(Объект, ТаблицаКОбработке);
	
КонецПроцедуры

&НаСервере
Процедура СвойстваНоменклатурыОтмененоПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор)
	
	СтрокаСвойства = Объект.СвойстваНоменклатуры.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	Если СтрокаСвойства.Отменено Тогда
		
		СтрокаСвойства.НачалоДействия = Дата(1, 1, 1);
		СтрокаСвойства.ОкончаниеДействия = Дата(1, 1, 1);
		СтрокаСвойства.ОбозначениеДиапазона = "";
		
	ИначеЕсли РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров) Тогда
		
		РетроБонусыСервер.ЗаполнитьЕдиныеДанныеДиапазонаСтрокиСвойства(Объект, ТекущаяСтрокаИдентификатор, Истина);
		
	КонецЕсли;
	
	ИменаПолей = "ЗначениеСвойства";
	
	СтрокиКВыгрузке = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаСвойства);
	ТаблицаКОбработке = Объект.СвойстваНоменклатуры.Выгрузить(СтрокиКВыгрузке, ИменаПолей);
	РетроБонусыСервер.ОбновитьДиапазоныСтрокСвойствПоТаблице(Объект, ТаблицаКОбработке);
	
КонецПроцедуры

&НаСервере
Процедура СвойстваНоменклатурыБазоваяЦенаПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор)
	
	СтрокаСвойства = Объект.СвойстваНоменклатуры.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ЗначениеСвойства", СтрокаСвойства.ЗначениеСвойства);
	ПараметрыОтбора.Вставить("Отменено", Ложь);
	
	РетроБонусыСервер.ОбновитьПолеТаблицыПоОтбору(
		Объект.СвойстваНоменклатуры,
		ПараметрыОтбора,
		СтрокаСвойства.БазоваяЦена,
		"БазоваяЦена");
	
КонецПроцедуры

&НаСервере
Процедура СвойстваНоменклатурыНачалоДействияПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор)
	
	СтрокаСвойства = Объект.СвойстваНоменклатуры.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ЗначениеСвойства", СтрокаСвойства.ЗначениеСвойства);
	ПараметрыОтбора.Вставить("Отменено", Ложь);
	
	РетроБонусыСервер.ОбновитьПолеТаблицыПоОтбору(
		Объект.СвойстваНоменклатуры,
		ПараметрыОтбора,
		СтрокаСвойства.НачалоДействия,
		"НачалоДействия");
		
КонецПроцедуры

&НаСервере
Процедура СвойстваНоменклатурыОкончаниеДействияПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор)
	
	СтрокаСвойства = Объект.СвойстваНоменклатуры.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ЗначениеСвойства", СтрокаСвойства.ЗначениеСвойства);
	ПараметрыОтбора.Вставить("Отменено", Ложь);
	
	РетроБонусыСервер.ОбновитьПолеТаблицыПоОтбору(
		Объект.СвойстваНоменклатуры,
		ПараметрыОтбора,
		СтрокаСвойства.ОкончаниеДействия,
		"ОкончаниеДействия");
	
КонецПроцедуры

// Параметры:
//  ТекущаяСтрокаИдентификатор - Число - Текущая строка идентификатор
//  ТекущееЗначение - ОпределяемыйТип.ТипыСвойствНоменклатурыРетроБонусов
//  ИзменяемоеЗначение - ОпределяемыйТип.ТипыСвойствНоменклатурыРетроБонусов
//
&НаСервере
Процедура СвойстваНоменклатурыЗначениеСвойстваПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор, ТекущееЗначение, ИзменяемоеЗначение)
	
	СтрокаСвойства = Объект.СвойстваНоменклатуры.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	ИзменяемоеЗначение = СтрокаСвойства.ЗначениеСвойства;
	
	Если РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров)
	   И ИзменяемоеЗначение <> Неопределено Тогда
		
		РетроБонусыСервер.ЗаполнитьЕдиныеДанныеДиапазонаСтрокиСвойства(Объект, ТекущаяСтрокаИдентификатор);
		ОбновитьДиапазоныСтрокСвойствПоСвойству(ТекущееЗначение);
		ОбновитьДиапазоныСтрокСвойствПоСвойству(ИзменяемоеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыКонтрагентыВспомогательные

&НаСервере
Процедура КонтрагентыКонтрагентПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор)
	
	СтрокаУчастников = Объект.Контрагенты.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	Если Объект.НачислитьСразу
	   И СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Контрагенты
	   И СтрокаУчастников.Партнер.Пустая() Тогда
		
		НовыйПартнер = ПартнерПоКонтрагенту(СтрокаУчастников.Контрагент);
		СтрокаУчастников.Партнер = НовыйПартнер;
		
	КонецЕсли;
	
	ДетализацияРасчета = Перечисления.ДетализацияРасчетаУчастниковРетроБонусов;
	Если Объект.ДетализацияРасчетаУчастников <> ДетализацияРасчета.ПоКонтрагентуКлиенту Тогда
		
		УстановитьПризнакиРасчетаПланаУчастников();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КонтрагентыПартнерПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор)
	
	СтрокаУчастников = Объект.Контрагенты.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	Если Объект.НачислитьСразу
	   И СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Клиенты
	   И СтрокаУчастников.Контрагент.Пустая() Тогда
		
		НовыйКонтрагент = КонтрагентПоПартнеру(СтрокаУчастников.Партнер);
		СтрокаУчастников.Контрагент = НовыйКонтрагент;
		
	КонецЕсли;
	
	ДетализацияРасчета = Перечисления.ДетализацияРасчетаУчастниковРетроБонусов;
	Если Объект.ДетализацияРасчетаУчастников <> ДетализацияРасчета.ПоКонтрагентуКлиенту Тогда
		
		УстановитьПризнакиРасчетаПланаУчастников();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПартнерПоКонтрагенту(Знач Контрагент)
	
	Партнер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "Партнер");
	
	Возврат Партнер;
	
КонецФункции

&НаСервереБезКонтекста
Функция КонтрагентПоПартнеру(Знач Партнер)
	
	Контрагент = Справочники.Контрагенты.ПустаяСсылка();
	
	Выборка = ВыборкаКонтрагентов(Партнер);
	Если Выборка.Количество() = 1 Тогда
		
		Выборка.Следующий();
		Контрагент = Выборка.Ссылка;
		
	КонецЕсли;
	
	Возврат Контрагент;
	
КонецФункции

// Параметры:
//  Партнер - СправочникСсылка.Партнеры - Партнер
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//	* Ссылка - СправочникСсылка.Контрагенты
//
&НаСервереБезКонтекста
Функция ВыборкаКонтрагентов(Знач Партнер)
	
	Запрос = Новый	Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	Контрагенты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Партнер = &Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ЕСТЬNULL(Контрагенты.Ссылка, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
	|		ПО Контрагенты.Партнер = Партнеры.Родитель
	|ГДЕ
	|	Партнеры.Ссылка = &Партнер
	|	И Партнеры.Родитель <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Партнер", Партнер);
	
	Результат = Запрос.ВыполнитьПакет();
	РезультатПоПартнеру = Результат[0];
	РезультатПоРодителюПартнера = Результат[1];
	
	Если РезультатПоПартнеру.Пустой() Тогда
		Возврат РезультатПоРодителюПартнера.Выбрать();
	Иначе
		Возврат РезультатПоПартнеру.Выбрать();
	КонецЕсли;
	
КонецФункции

// Параметры:
//  Результат - СправочникСсылка.СегментыПартнеров, Неопределено -
//  ДополнительныеПараметры - Неопределено
//
&НаКлиенте
Процедура ЗаполнитьУчастниковПоСегментуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		ВыбранныйСегмент = Результат;
		ЗаполнитьУчастниковПоСегментуСервер(ВыбранныйСегмент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУчастниковПоСегментуСервер(Знач Сегмент)
	
	СпособФормирования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сегмент, "СпособФормирования");
	Если СпособФормирования <> Перечисления.СпособыФормированияСегментов.ФормироватьДинамически Тогда
		
		ВыборкаПартнеров = ПартнерыСтатическогоСегмента(Сегмент);
		
	Иначе
		
		ВыборкаПартнеров = ПартнерыДинамическогоСегмента(Сегмент);
		
	КонецЕсли;
	
	Пока ВыборкаПартнеров.Следующий() Цикл
		
		СтрокаКонтрагенты = Объект.Контрагенты.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКонтрагенты, ВыборкаПартнеров);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыДоговорыСоглашенияВспомогательные

&НаСервере
Процедура ДоговорыСоглашенияКонтрагентПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор)
	
	СтрокаУчастников = Объект.ДоговорыСоглашения.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	Если СоставУчастников = ПредопределенноеЗначение("Перечисление.СоставыУчастниковРетроБонусов.Договоры") Тогда
		
		ТекущийДоговор = СтрокаУчастников.Договор;
		ЗаполнитьОчиститьДоговор(СтрокаУчастников);
		Если ТекущийДоговор <> СтрокаУчастников.Договор Тогда
			
			ДоговорыСоглашенияДоговорПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
			
		КонецЕсли;
		УстановитьПризнакиРасчетаПланаУчастников(СтрокаУчастников.Контрагент);
		
	ИначеЕсли СоставУчастников = ПредопределенноеЗначение("Перечисление.СоставыУчастниковРетроБонусов.Соглашения") Тогда
		
		ТекущееСоглашение = СтрокаУчастников.Соглашение;
		ЗаполнитьОчиститьСоглашение(СтрокаУчастников);
		
		Если ТекущееСоглашение <> СтрокаУчастников.Соглашение Тогда
			
			ДоговорыСоглашенияСоглашениеПриИзмененииСерверОбработатьСтроку(ТекущаяСтрокаИдентификатор);
			
		КонецЕсли;
		УстановитьПризнакиРасчетаПланаУчастников(СтрокаУчастников.Контрагент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДоговорыСоглашенияПартнерПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор)
	
	СтрокаУчастников = Объект.ДоговорыСоглашения.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(
		СтрокаУчастников.Партнер, СтрокаУчастников.Контрагент);
	
	Если СоставУчастников = ПредопределенноеЗначение("Перечисление.СоставыУчастниковРетроБонусов.Договоры") Тогда
		
		ТекущийДоговор = СтрокаУчастников.Договор;
		ЗаполнитьОчиститьДоговор(СтрокаУчастников);
		Если ТекущийДоговор <> СтрокаУчастников.Договор Тогда
			
			ДоговорыСоглашенияДоговорПриИзмененииСервер(ТекущаяСтрокаИдентификатор);
			
		КонецЕсли;
		УстановитьПризнакиРасчетаПланаУчастников(СтрокаУчастников.Контрагент);
		
	ИначеЕсли СоставУчастников = ПредопределенноеЗначение("Перечисление.СоставыУчастниковРетроБонусов.Соглашения") Тогда
		
		ТекущееСоглашение = СтрокаУчастников.Соглашение;
		ЗаполнитьОчиститьСоглашение(СтрокаУчастников);
		
		Если ТекущееСоглашение <> СтрокаУчастников.Соглашение Тогда
			
			ДоговорыСоглашенияСоглашениеПриИзмененииСерверОбработатьСтроку(ТекущаяСтрокаИдентификатор);
			
		КонецЕсли;
		УстановитьПризнакиРасчетаПланаУчастников(СтрокаУчастников.Контрагент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОчиститьДоговор(СтрокаУчастников, ТолькоДействующие = Истина)
	
	Если СтрокаУчастников.Контрагент.Пустая()
	 ИЛИ СтрокаУчастников.Партнер.Пустая() Тогда
		
		СтрокаУчастников.Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		Возврат;
		
	КонецЕсли;
	
	Выборка = ЗапросДоговораПоАналитике(СтрокаУчастников, ТолькоДействующие);
	Если Выборка.Количество() = 0 Тогда
		
		СтрокаУчастников.Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		
	ИначеЕсли Выборка.Количество() = 1 Тогда
		
		Выборка.Следующий();
		СтрокаУчастников.Договор = Выборка.Договор;
		
	ИначеЕсли Выборка.Количество() > 2 Тогда
		
		СтрокаУчастников.Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		
	Иначе
		
		НовыйДоговор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		КоличествоПохожих = 0;
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.Приоритет = 1 Тогда
				
				НовыйДоговор = Выборка.Договор;
				Прервать;
				
			Иначе
				
				КоличествоПохожих = КоличествоПохожих + 1;
				НовыйДоговор = Выборка.Договор;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если КоличествоПохожих > 1 Тогда
			СтрокаУчастников.Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		Иначе
			СтрокаУчастников.Договор = НовыйДоговор;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  СтрокаУчастников - ДанныеФормыЭлементКоллекции: см. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента.Элементы.ДоговорыСоглашения - Строка участников
//  ТолькоДействующие - Булево - отбирать только действующие договора
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//  * Приоритет - Число
//  * Договор - СправочникСсылка.ДоговорыКонтрагентов
//
&НаСервере
Функция ЗапросДоговораПоАналитике(Знач СтрокаУчастников, ТолькоДействующие)
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Данные.Приоритет,
	|	Данные.Договор
	|ИЗ
	|	(ВЫБРАТЬ
	|		1 КАК Приоритет,
	|		Договоры.Ссылка КАК Договор
	|	ИЗ
	|		Справочник.ДоговорыКонтрагентов КАК Договоры
	|	ГДЕ
	|		Договоры.Организация = &Организация
	|		И Договоры.Контрагент = &Контрагент
	|		И Договоры.Партнер = &Партнер
	|		И НЕ Договоры.ПометкаУдаления
	|		И Договоры.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПокупателем)
	|		И Договоры.Ссылка = &Договор
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ ПЕРВЫЕ 2
	|		2 КАК Приоритет,
	|		Договоры.Ссылка КАК Договор
	|	ИЗ
	|		Справочник.ДоговорыКонтрагентов КАК Договоры
	|	ГДЕ
	|		Договоры.Организация = &Организация
	|		И Договоры.Контрагент = &Контрагент
	|		И Договоры.Партнер = &Партнер
	|		И НЕ Договоры.ПометкаУдаления
	|		И Договоры.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПокупателем)
	|		И &ТолькоДействующие
	|		И Договоры.Ссылка <> &Договор) КАК Данные
	|
	|УПОРЯДОЧИТЬ ПО
	|	Данные.Приоритет";
	
	Если ТолькоДействующие Тогда
		
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса,
			"&ТолькоДействующие",
			"Договоры.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)");
		
	Иначе
		
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса,
			"&ТолькоДействующие",
			"ИСТИНА");
		
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Контрагент", СтрокаУчастников.Контрагент);
	Запрос.УстановитьПараметр("Партнер", СтрокаУчастников.Партнер);
	Запрос.УстановитьПараметр("Договор", СтрокаУчастников.Договор);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

&НаСервере
Процедура ЗаполнитьОчиститьСоглашение(СтрокаУчастников, ТолькоДействующие = Истина)
	
	Если СтрокаУчастников.Партнер.Пустая() Тогда
		
		СтрокаУчастников.Соглашение = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
		Возврат;
		
	КонецЕсли;
	
	Выборка = ЗапросСоглашенияПоАналитике(СтрокаУчастников);
	Если Выборка.Количество() = 0 Тогда
		
		СтрокаУчастников.Соглашение = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
		
	ИначеЕсли Выборка.Количество() = 1 Тогда
		
		Выборка.Следующий();
		СтрокаУчастников.Соглашение = Выборка.Соглашение;
		СтрокаУчастников.Контрагент = Выборка.Контрагент;
		
	ИначеЕсли Выборка.Количество() > 2 Тогда
		
		СтрокаУчастников.Соглашение = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
		
	Иначе
		
		НовоеСоглашение = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
		КоличествоПохожих = 0;
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.Приоритет = 1 Тогда
				
				НовоеСоглашение = Выборка.Соглашение;
				Прервать;
				
			Иначе
				
				КоличествоПохожих = КоличествоПохожих + 1;
				НовоеСоглашение = Выборка.Соглашение;
				Контрагент = Выборка.Контрагент;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если КоличествоПохожих > 1 Тогда
			
			СтрокаУчастников.Соглашение = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
			
		Иначе
			
			СтрокаУчастников.Соглашение = НовоеСоглашение;
			СтрокаУчастников.Контрагент = Контрагент;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  СтрокаУчастников - ДанныеФормыЭлементКоллекции: см. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента.Элементы.ДоговорыСоглашения - Строка участников
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//  * Приоритет - Число -
//  * Соглашение - СправочникСсылка.СоглашенияСКлиентами -
//  * Контрагент - СправочникСсылка.Контрагенты - контрагент по соглашению 
//
&НаСервере
Функция ЗапросСоглашенияПоАналитике(Знач СтрокаУчастников)
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Данные.Приоритет КАК Приоритет,
	|	Данные.Соглашение КАК Соглашение,
	|	Данные.Соглашение.Контрагент КАК Контрагент
	|ИЗ
	|	(ВЫБРАТЬ
	|		1 КАК Приоритет,
	|		Соглашения.Ссылка КАК Соглашение
	|	ИЗ
	|		Справочник.СоглашенияСКлиентами КАК Соглашения
	|	ГДЕ
	|		Соглашения.Организация В (&Организации)
	|		И Соглашения.Контрагент = &Контрагент
	|		И Соглашения.Партнер = &Партнер
	|		И НЕ Соглашения.ПометкаУдаления
	|		И НЕ Соглашения.Типовое
	|		И Соглашения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
	|		И Соглашения.Ссылка = &Соглашение
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ ПЕРВЫЕ 2
	|		2 КАК Приоритет,
	|		Соглашения.Ссылка КАК Соглашение
	|	ИЗ
	|		Справочник.СоглашенияСКлиентами КАК Соглашения
	|	ГДЕ
	|		Соглашения.Организация В (&Организации)
	|		И Соглашения.Контрагент В (&Контрагенты)
	|		И Соглашения.Партнер В (&Партнеры)
	|		И Соглашения.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийСКлиентами.Действует)
	|		И НЕ Соглашения.ПометкаУдаления
	|		И НЕ Соглашения.Типовое
	|		И Соглашения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
	|		И Соглашения.Ссылка <> &Соглашение) КАК Данные
	|
	|УПОРЯДОЧИТЬ ПО
	|	Данные.Приоритет";
	
	Запрос.Текст = ТекстЗапроса;
	
	Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Организация); // Массив из СправочникСсылка.Организации
	Организации.Добавить(Справочники.Организации.ПустаяСсылка());
	
	Контрагенты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаУчастников.Контрагент); // Массив из СправочникСсылка.Контрагенты
	Контрагенты.Добавить(Справочники.Контрагенты.ПустаяСсылка());
	
	Партнеры = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаУчастников.Партнер); // Массив из СправочникСсылка.Партнеры
	Партнеры.Добавить(Справочники.Партнеры.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("Контрагенты", Контрагенты);
	Запрос.УстановитьПараметр("Контрагент", СтрокаУчастников.Контрагент);
	Запрос.УстановитьПараметр("Партнеры", Партнеры);
	Запрос.УстановитьПараметр("Партнер", СтрокаУчастников.Партнер);
	Запрос.УстановитьПараметр("Соглашение", СтрокаУчастников.Соглашение);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

&НаСервере	
Процедура ДоговорыСоглашенияДоговорПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор)
	
	СтрокаУчастников = Объект.ДоговорыСоглашения.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	Если СтрокаУчастников.Контрагент.Пустая()
	 ИЛИ СтрокаУчастников.Партнер.Пустая() Тогда
		
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			СтрокаУчастников.Договор, "Контрагент, Партнер");
		СтрокаУчастников.Контрагент = РеквизитыДоговора.Контрагент;
		СтрокаУчастников.Партнер = РеквизитыДоговора.Партнер;
		
	КонецЕсли;
	
	УстановитьПризнакиРасчетаПланаУчастников(СтрокаУчастников.Контрагент);
	
КонецПроцедуры

&НаСервере
Процедура ДоговорыСоглашенияСоглашениеПриИзмененииСервер(Знач ТекущаяСтрокаИдентификатор)
	
	ДоговорыСоглашенияСоглашениеПриИзмененииСерверОбработатьСтроку(ТекущаяСтрокаИдентификатор);
	
	СтрокаУчастников = Объект.ДоговорыСоглашения.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	Если СтрокаУчастников.ЭтоТиповоеСоглашение Тогда
		
		СтрокаУчастников.Партнер = Справочники.Партнеры.ПустаяСсылка();
		СтрокаУчастников.Контрагент = Справочники.Контрагенты.ПустаяСсылка();
		
	Иначе
		
		// Обновляем данные строки по соглашению (может быть не указан контрагент)
		
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			СтрокаУчастников.Соглашение, "Контрагент, Партнер");
		СтрокаУчастников.Контрагент = РеквизитыДоговора.Контрагент;
		СтрокаУчастников.Партнер = РеквизитыДоговора.Партнер;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДоговорыСоглашенияСоглашениеПриИзмененииСерверОбработатьСтроку(Знач ТекущаяСтрокаИдентификатор)
	
	СтрокаУчастников = Объект.ДоговорыСоглашения.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьПризнакСоглашенияВСтрокеТЧ", "Соглашение");
	
	КешЗначений = Неопределено;
	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(
		СтрокаУчастников,
		СтруктураДействий,
		КешЗначений,
		Объект.ДоговорыСоглашения);
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеЭлементамиФормы

&НаКлиенте
Процедура НастроитьЗависимыеЭлементыФормы(ИзмененныйРеквизит = "")
	
	ДенежныеСредстваКлиентСервер.НастроитьЭлементыФормы(ЭтотОбъект, ИзмененныйРеквизит, РеквизитыФормы(ЭтотОбъект));
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЗависимыеЭлементыФормыНаСервере(ИзмененныйРеквизит = "")
	
	ДенежныеСредстваКлиентСервер.НастроитьЭлементыФормы(ЭтотОбъект, ИзмененныйРеквизит, РеквизитыФормы(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РеквизитыФормы(Форма)
	
	РеквизитыФормы = Новый Структура;
	РеквизитыФормы.Вставить("ПоказательТоваров");
	РеквизитыФормы.Вставить("БазаРасчетаПродаж");
	РеквизитыФормы.Вставить("СоставТоваров");
	РеквизитыФормы.Вставить("СоставУчастников");
	РеквизитыФормы.Вставить("ЕстьНачисления");
	РеквизитыФормы.Вставить("ИспользоватьИндивидуальныеСоглашения");
	РеквизитыФормы.Вставить("ИспользоватьТиповыеИИндивидуальныеСоглашенияСКлиентами");
	
	ЗаполнитьЗначенияСвойств(РеквизитыФормы, Форма);
	
	Возврат РеквизитыФормы;
	
КонецФункции

&НаСервере
Процедура УстановитьНеизменнуюНастройкуПолейПоВиду()
	
	// Состав списка отбора товаров
	НовыеПараметры = РетроБонусыКлиентСервер.НовыеПараметрыДоступныхОтборовТоваров();
	НовыеПараметры.БезРасчета = Объект.БезРасчета;
	НовыеПараметры.ПоказательТоваров = ПоказательТоваров;
	НовыеПараметры.БазаРасчета = БазаРасчетаПродаж;
	НовыеПараметры.СоставТоваров = СоставТоваров;
	
	МассивВыбора = РетроБонусыКлиентСервер.ДоступныйСписокОтборТоваров(НовыеПараметры);
	Элементы.ОтборТоваров.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);
	РетроБонусыКлиентСервер.ПроверитьУстановитьЗначениеСпискаВыбора(Объект, "ОтборТоваров", МассивВыбора);
	
	// Состав списка отбора участников
	НовыеПараметры = РетроБонусыКлиентСервер.НовыеПараметрыДоступныхОтборовУчастников();
	НовыеПараметры.БезРасчета = Объект.БезРасчета;
	НовыеПараметры.СоставУчастников = СоставУчастников;
	
	МассивВыбора = РетроБонусыКлиентСервер.ДоступныйСписокОтбораУчастников(НовыеПараметры);
	Элементы.ОтборУчастников.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);
	РетроБонусыКлиентСервер.ПроверитьУстановитьЗначениеСпискаВыбора(Объект, "ОтборУчастников", МассивВыбора);
	
	// Установка видимости и только просмотр полей
	ИменаЭлементовВидимостьВсех = Новый Массив; // Массив из Строка
	ИменаЭлементовВидимостьВсех.Добавить("Контрагенты.СуммаБонус");
	ИменаЭлементовВидимостьВсех.Добавить("НачислитьСразу");
	
	ИменаЭлементовВидимых = Новый Массив; // Массив из Строка
	СвязиПараметровВыбора = Новый Массив(); // Массив из СвязьПараметраВыбора
	
	Если СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Контрагенты Тогда
		
		Элементы.Переместить(
			Элементы.КонтрагентыКонтрагент,
			Элементы.Контрагенты,
			Элементы.КонтрагентыПартнер);
		
	ИначеЕсли СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Клиенты Тогда
		
		Элементы.Переместить(
			Элементы.КонтрагентыПартнер,
			Элементы.Контрагенты,
			Элементы.КонтрагентыКонтрагент);
		
		НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Партнер", "Элементы.Контрагенты.ТекущиеДанные.Партнер");
		СвязиПараметровВыбора.Добавить(НоваяСвязь);
		
	КонецЕсли;
	
	Элементы.КонтрагентыКонтрагент.СвязиПараметровВыбора = Новый ФиксированныйМассив(СвязиПараметровВыбора);
	
	Если Объект.БезРасчета Тогда
		
		ЗаголовокВалюты =  НСтр("ru = 'Валюта
								|начисляемого бонуса';
								|en = 'Currency of the bonus
								|to charge'");
		
		ИменаЭлементовВидимых.Добавить("НачислитьСразу");
		ИменаЭлементовВидимых.Добавить("Контрагенты.СуммаБонус");
		
	ИначеЕсли БазаРасчетаПродаж = Перечисления.БазыРасчетаРетроБонусов.ВыручкаБазовыеЦены Тогда
		
		ЗаголовокВалюты = НСтр("ru = 'Валюта
							   |базовых цен';
							   |en = 'Base
							   |price currency'");
		
	Иначе
		
		ЗаголовокВалюты = НСтр("ru = 'Валюта';
								|en = 'Currency'");
		
	КонецЕсли;
	
	Элементы.Валюта.Заголовок = ЗаголовокВалюты;
	ОбщегоНазначенияУТКлиентСервер.УстановитьВидимостьЭлементовПоМассиву(
		Элементы, ИменаЭлементовВидимостьВсех, ИменаЭлементовВидимых);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкиСведенийУчастников()
	
	ЗаголовокПоля = "";
	Если СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Контрагенты Тогда
		
		ЗаголовокПоля = НСтр("ru = 'Контрагенты';
							|en = 'Counterparties'");
		
	ИначеЕсли СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Клиенты Тогда
		
		ЗаголовокПоля = НСтр("ru = 'Клиенты';
							|en = 'Customers'");
		
	ИначеЕсли СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.ИНН Тогда 
		
		ЗаголовокПоля = НСтр("ru = 'ИНН';
							|en = 'TIN'");
		
	ИначеЕсли СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.СегментыПартнеров Тогда
		
		ЗаголовокПоля = НСтр("ru = 'Сегменты
							 |клиентов';
							 |en = 'Customer
							 |segments'");
	
	ИначеЕсли СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Договоры Тогда
		
		ЗаголовокПоля = НСтр("ru = 'Договоры';
							|en = 'Contracts'");
		
	ИначеЕсли СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Соглашения Тогда
		
		ЗаголовокПоля = НСтр("ru = 'Соглашения';
							|en = 'Terms of sales'");
		
	Иначе
		
		ЗаголовокПоля = НСтр("ru = 'Участники';
							|en = 'Participants'");
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, Элементы.ОтборУчастников.Имя, "Заголовок", ЗаголовокПоля);
	
	УстановитьНастройкуСтраницыИННКонтрагентов();
	УстановитьНастройкуСтраницыУчастники();
	УстановитьНастройкуСтраницыСегментыПартнеров();
	УстановитьНастройкуСтраницыДоговорыСоглашения();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкуСтраницыУчастники()
	
	Если СоставУчастников <> Перечисления.СоставыУчастниковРетроБонусов.Контрагенты
	   И СоставУчастников <> Перечисления.СоставыУчастниковРетроБонусов.Клиенты Тогда
		Возврат;
	КонецЕсли;
	
	ЗаголовокСтраницы = РетроБонусыКлиентСервер.ЗаголовокСтраницыПоНастройкам(СоставУчастников, Объект.ОтборУчастников);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, Элементы.СтраницаУчастники.Имя, "Заголовок", ЗаголовокСтраницы);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкуСтраницыИННКонтрагентов()
	
	Если СоставУчастников <> Перечисления.СоставыУчастниковРетроБонусов.ИНН Тогда
		Возврат;
	КонецЕсли;
	
	ЗаголовокСтраницы = РетроБонусыКлиентСервер.ЗаголовокСтраницыПоНастройкам(СоставУчастников, Объект.ОтборУчастников);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, Элементы.СтраницаИННКонтрагентов.Имя, "Заголовок", ЗаголовокСтраницы);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкуСтраницыСегментыПартнеров()
	
	Если СоставУчастников <> Перечисления.СоставыУчастниковРетроБонусов.СегментыПартнеров Тогда
		Возврат;
	КонецЕсли;
	
	ЗаголовокСтраницы = РетроБонусыКлиентСервер.ЗаголовокСтраницыПоНастройкам(СоставУчастников, Объект.ОтборУчастников);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, Элементы.СтраницаСегментыПартнеров.Имя, "Заголовок", ЗаголовокСтраницы);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкуСтраницыДоговорыСоглашения()
	
	ВидимостьСтраницы = Ложь;
	
	Если СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Договоры Тогда
		
		ЗаголовокСтраницы = НСтр("ru = 'Выбранные договоры';
								|en = 'Selected contracts'");
		ВидимостьСтраницы = Истина;
		
	ИначеЕсли СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Соглашения Тогда
		
		ЗаголовокСтраницы = НСтр("ru = 'Выбранные соглашения';
								|en = 'Selected terms of sales'");
		ВидимостьСтраницы = Истина;
		
	КонецЕсли;
	
	Если ВидимостьСтраницы Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, Элементы.СтраницаДоговорыСоглашения.Имя, "Заголовок", ЗаголовокСтраницы);
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, Элементы.СтраницаДоговорыСоглашения.Имя, "Видимость", ВидимостьСтраницы);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкиСведенийТоваров()
	
	Если СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры Тогда
		
		Элементы.ОтборТоваров.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ОтборТоваров.МаксимальнаяШирина = 12;
		Элементы.ОтборТоваров.Ширина = 9;
		
	Иначе
		
		Элементы.ОтборТоваров.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
		Элементы.ОтборТоваров.МаксимальнаяШирина = 29;
		Элементы.ОтборТоваров.Ширина = 0;
		
	КонецЕсли;
	
	ЗаголовокПоля = "";
	Если СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры Тогда
		
		ЗаголовокПоля = НСтр("ru = 'Сегменты
							 |товаров';
							 |en = 'Goods
							 |segments'");
		
	Иначе
		
		ЗаголовокПоля = НСтр("ru = 'Товары';
							|en = 'Goods'");
		
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, Элементы.ОтборТоваров.Имя, "Заголовок", ЗаголовокПоля);
	
	Если СоставТоваров =  Перечисления.СоставыТоваровРетроБонусов.Номенклатура Тогда
		
		ЗаголовокСтраницы = РетроБонусыКлиентСервер.ЗаголовокСтраницыПоНастройкам(СоставТоваров, Объект.ОтборТоваров);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, Элементы.СтраницаТовары.Имя, "Заголовок", ЗаголовокСтраницы);
		
	ИначеЕсли СоставТоваров =  Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры Тогда
		
		ЗаголовокСтраницы = РетроБонусыКлиентСервер.ЗаголовокСтраницыПоНастройкам(СоставТоваров, Объект.ОтборТоваров);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, Элементы.СтраницаСвойстваНоменклатуры.Имя, "Заголовок", ЗаголовокСтраницы);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, Элементы.СвойстваНоменклатурыЗначениеСвойства.Имя, "Заголовок", СвойствоНоменклатурыПредставление);
		
	ИначеЕсли СоставТоваров =  Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры Тогда
		
		ЗаголовокСтраницы = РетроБонусыКлиентСервер.ЗаголовокСтраницыПоНастройкам(СоставТоваров, Объект.ОтборТоваров);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, Элементы.СтраницаСегментыТоваров.Имя, "Заголовок", ЗаголовокСтраницы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаголовокФормыЗагрузкиКонтрагентов()
	
	ЗаголовокФормы = "";
	
	Если СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Контрагенты Тогда
		ЗаголовокФормы = НСтр("ru = 'Загрузка контрагентов из файла';
								|en = 'Import counterparties from file'");
	ИначеЕсли СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Клиенты Тогда
		ЗаголовокФормы = НСтр("ru = 'Загрузка клиентов из файла';
								|en = 'Import customers from file'");
	КонецЕсли;
	
	Возврат ЗаголовокФормы;
	
КонецФункции

#КонецОбласти

#Область ЗагрузкаИзФайлов

// Параметры:
//  АдресЗагруженныхДанных - Строка
//  ДополнительныеПараметры - Структура
//
&НаКлиенте
Процедура ТоварыЗагрузитьИзВнешнегоФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(АдресЗагруженныхДанных) Тогда
		
		ОчиститьСообщения();
		ПолучитьЗагруженныеТоварыИзХранилища(АдресЗагруженныхДанных);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗагруженныеТоварыИзХранилища(Знач АдресТоваровВХранилище)

	ТоварыИзХранилища = ПолучитьИзВременногоХранилища(АдресТоваровВХранилище);
	
	Для Каждого СтрокаТоваров Из ТоварыИзХранилища Цикл
		
		СтрокаТЧТовары = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧТовары, СтрокаТоваров);
		
	КонецЦикла;
	
	УдалитьИзВременногоХранилища(АдресТоваровВХранилище);
	
КонецПроцедуры

// Параметры:
//  АдресЗагруженныхДанных - Строка
//  ДополнительныеПараметры - Структура
//
&НаКлиенте
Процедура СегментыПартнеровЗагрузитьИзВнешнегоФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(АдресЗагруженныхДанных) Тогда
		
		ОчиститьСообщения();
		ПолучитьЗагруженныеСегментыПартнеровИзХранилища(АдресЗагруженныхДанных);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗагруженныеСегментыПартнеровИзХранилища(Знач АдресТоваровВХранилище)

	СегментыИзХранилища = ПолучитьИзВременногоХранилища(АдресТоваровВХранилище);
	
	Для Каждого СтрокаСегментов Из СегментыИзХранилища Цикл
		СтрокаТЧСегментыПартнеров = Объект.СегментыПартнеров.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧСегментыПартнеров, СтрокаСегментов);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ОписаниеКолонокМакетаКонтрагенты(НастройкиОтображенияПолей)
	
	ИменаКолонок = Новый Массив; // Массив из Строка
	Если НастройкиОтображенияПолей.СуммаПлан Тогда
		ИменаКолонок.Добавить("СуммаПлан");
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.СуммаБонус Тогда
		ИменаКолонок.Добавить("СуммаБонус");
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.Период Тогда
		
		ИменаКолонок.Добавить("НачалоДействия");
		ИменаКолонок.Добавить("ОкончаниеДействия");
		
	КонецЕсли;
	
	Колонки = СтрСоединить(ИменаКолонок, ",");
	
	КолонкиМакета = ЗагрузкаДанныхИзФайла.СформироватьОписаниеКолонок(Объект.Контрагенты, Колонки);
	
	НомерКолонки = 0;
	
	Если СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Клиенты Тогда
		
		ДобавитьПоляПартнераВМакетЗагрузкиКонтрагентов(НастройкиОтображенияПолей, КолонкиМакета, НомерКолонки);
		ДобавитьПоляКонтрагентаВМакетЗагрузкиКонтрагентов(НастройкиОтображенияПолей, КолонкиМакета, НомерКолонки);
		
	ИначеЕсли СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Контрагенты Тогда
		
		ДобавитьПоляКонтрагентаВМакетЗагрузкиКонтрагентов(НастройкиОтображенияПолей, КолонкиМакета, НомерКолонки);
		ДобавитьПоляПартнераВМакетЗагрузкиКонтрагентов(НастройкиОтображенияПолей, КолонкиМакета, НомерКолонки);
		
	КонецЕсли;
	
	ПодсказкаСумма = НСтр("ru = 'Поддерживаются лидирующие нули, а также разделители целой и дробной части "","" и "".""';
							|en = 'Leading zeros, integer and fraction dividers, "","", and ""."" are supported'");
	Если НастройкиОтображенияПолей.СуммаПлан Тогда
		
		НомерКолонки = НомерКолонки + 1;
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("СуммаПлан", КолонкиМакета);
		Колонка.Позиция = НомерКолонки;
		Колонка.Заголовок = НСтр("ru = 'Сумма, план';
								|en = 'Amount, plan'");
		Колонка.Подсказка = ПодсказкаСумма;
		
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.СуммаБонус Тогда
		
		НомерКолонки = НомерКолонки + 1;
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("СуммаБонус", КолонкиМакета);
		Колонка.Позиция = НомерКолонки;
		Колонка.Заголовок = НСтр("ru = 'Сумма, бонус';
								|en = 'Amount, bonus'");
		Колонка.Подсказка = ПодсказкаСумма;
		
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.Период Тогда
		
		ПодсказкаДата = НСтр("ru = 'Поддерживается формат в виде ""ДД.ММ.ГГГГ"" или ""ДД/ММ/ГГ""';
							|en = '""MM.DD.YYYY"" and ""MM/DD/YY"" formats are supported'");
		
		НомерКолонки = НомерКолонки + 1;
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("НачалоДействия", КолонкиМакета);
		Колонка.Заголовок = НСтр("ru = 'Начало действия';
								|en = 'Valid from'");
		Колонка.Подсказка = ПодсказкаДата;
		Колонка.Позиция = НомерКолонки;
		
		НомерКолонки = НомерКолонки + 1;
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("ОкончаниеДействия", КолонкиМакета);
		Колонка.Заголовок = НСтр("ru = 'Окончание действия';
								|en = 'Valid to'");
		Колонка.Подсказка = ПодсказкаДата;
		Колонка.Позиция = НомерКолонки;
		
	КонецЕсли;
	
	Возврат КолонкиМакета;
	
КонецФункции

&НаСервере
Процедура ДобавитьПоляПартнераВМакетЗагрузкиКонтрагентов(НастройкиОтображенияПолей, КолонкиМакета, НомерКолонки)
	
	Если НастройкиОтображенияПолей.Партнер Тогда
		
		ИмяГруппы = НСтр("ru = 'Данные клиента';
						|en = 'Customer data'");
		
		НомерКолонки = НомерКолонки + 1;
		ТипКод = ОбщегоНазначения.ОписаниеТипаСтрока(11);
		ЗаголовокКолонки = НСтр("ru = 'Код клиента';
								|en = 'Customer code'");
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Код", ТипКод, ЗаголовокКолонки);
		Колонка.Позиция = НомерКолонки;
		Колонка.Родитель = ИмяГруппы;
		КолонкиМакета.Добавить(Колонка);
		
		НомерКолонки = НомерКолонки + 1;
		ТипНаименованиеПартнера = ОбщегоНазначения.ОписаниеТипаСтрока(100);
		ЗаголовокКолонки = НСтр("ru = 'Клиент';
								|en = 'Customer'");
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета(
			"ПартнерНаименование",
			ТипНаименованиеПартнера,
			ЗаголовокКолонки);
		Колонка.Позиция = НомерКолонки;
		Колонка.Родитель = ИмяГруппы;
		КолонкиМакета.Добавить(Колонка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПоляКонтрагентаВМакетЗагрузкиКонтрагентов(НастройкиОтображенияПолей, КолонкиМакета, НомерКолонки)
	
	Если НастройкиОтображенияПолей.Контрагент Тогда
		
		ИмяГруппы = "Контрагент";
		
		НомерКолонки = НомерКолонки + 1;
		ТипИНН = Метаданные.ОпределяемыеТипы.ИНН.Тип;
		ЗаголовокКолонки = НСтр("ru = 'ИНН';
								|en = 'TIN'");
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("ИНН", ТипИНН, ЗаголовокКолонки);
		Колонка.Позиция = НомерКолонки;
		Колонка.Родитель = ИмяГруппы;
		КолонкиМакета.Добавить(Колонка);
		
		НомерКолонки = НомерКолонки + 1;
		ТипКПП = ОбщегоНазначения.ОписаниеТипаСтрока(9);
		ЗаголовокКолонки = НСтр("ru = 'КПП';
								|en = 'KPP'");
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("КПП", ТипКПП, ЗаголовокКолонки);
		Колонка.Позиция = НомерКолонки;
		Колонка.Родитель = ИмяГруппы;
		КолонкиМакета.Добавить(Колонка);
		
		НомерКолонки = НомерКолонки + 1;
		ТипНаименованиеКонтрагента = ОбщегоНазначения.ОписаниеТипаСтрока(250);
		ЗаголовокКолонки = НСтр("ru = 'Контрагент';
								|en = 'Counterparty'");
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета(
			"КонтрагентНаименование",
			ТипНаименованиеКонтрагента,
			ЗаголовокКолонки);
		Колонка.Позиция = НомерКолонки;
		Колонка.Родитель = ИмяГруппы;
		КолонкиМакета.Добавить(Колонка);
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  АдресЗагруженныхДанных - Строка, Неопределено - Адрес загруженных данных
//  ДополнительныеПараметры - Произвольный - 
//
&НаКлиенте
Процедура ЗагрузитьКонтрагентовИзФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьКонтрагентовИзФайлаНаСервере(АдресЗагруженныхДанных);
	
КонецПроцедуры

// Параметры:
//  АдресЗагруженныхДанных - Строка - Адрес загруженных данных
//
&НаСервере
Процедура ЗагрузитьКонтрагентовИзФайлаНаСервере(АдресЗагруженныхДанных)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	СтрокиДобавлены = Ложь;
	Для Каждого СтрокаТаблицы Из ЗагруженныеДанные Цикл
		
		НоваяСтрока = Объект.Контрагенты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		СтрокиДобавлены = Истина;
		
	КонецЦикла;
	
	Если СтрокиДобавлены Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОписаниеКолонокМакетаКонтрагентыИНН(НастройкиОтображенияПолей)
	
	ИменаКолонок = Новый Массив; // Массив из Строка
	ИменаКолонок.Добавить("ИНН");
	Если НастройкиОтображенияПолей.СуммаПлан Тогда
		ИменаКолонок.Добавить("СуммаПлан");
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.Период Тогда
		
		ИменаКолонок.Добавить("НачалоДействия");
		ИменаКолонок.Добавить("ОкончаниеДействия");
		
	КонецЕсли;
	
	Колонки = СтрСоединить(ИменаКолонок, ",");
	
	КолонкиМакета = ЗагрузкаДанныхИзФайла.СформироватьОписаниеКолонок(Объект.ИННКонтрагентов, Колонки);
	
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("ИНН", КолонкиМакета);
	Колонка.ОбязательнаДляЗаполнения = Истина;
	
	Если НастройкиОтображенияПолей.СуммаПлан Тогда
		
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("СуммаПлан", КолонкиМакета);
		Колонка.Заголовок = НСтр("ru = 'Сумма, план';
								|en = 'Amount, plan'");
		Колонка.Подсказка = НСтр("ru = 'Поддерживаются лидирующие нули, а также разделители целой и дробной части "","" и "".""';
								|en = 'Leading zeros, integer and fraction dividers, "","", and ""."" are supported'");
		
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.Период Тогда
		
		ПодсказкаДата = НСтр("ru = 'Поддерживается формат в виде ""ДД.ММ.ГГГГ"" или ""ДД/ММ/ГГ""';
							|en = '""MM.DD.YYYY"" and ""MM/DD/YY"" formats are supported'");
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("НачалоДействия", КолонкиМакета);
		Колонка.Заголовок = НСтр("ru = 'Начало действия';
								|en = 'Valid from'");
		Колонка.Подсказка = ПодсказкаДата;
		
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("ОкончаниеДействия", КолонкиМакета);
		Колонка.Заголовок = НСтр("ru = 'Окончание действия';
								|en = 'Valid to'");
		Колонка.Подсказка = ПодсказкаДата;
		
	КонецЕсли;
	
	Возврат КолонкиМакета;
	
КонецФункции

// Параметры:
//  АдресЗагруженныхДанных - Строка, Неопределено - Адрес загруженных данных
//  ДополнительныеПараметры - Структура - Дополнительные параметры
//
&НаКлиенте
Процедура ЗагрузитьИННКонтрагентовИзФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьИННКонтрагентовИзФайлаНаСервере(АдресЗагруженныхДанных);
	
КонецПроцедуры

// Параметры:
//  АдресЗагруженныхДанных - Строка -
//
&НаСервере
Процедура ЗагрузитьИННКонтрагентовИзФайлаНаСервере(АдресЗагруженныхДанных)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	СтрокиДобавлены = Ложь;
	Для Каждого СтрокаТаблицы Из ЗагруженныеДанные Цикл
		
		НоваяСтрока = Объект.ИННКонтрагентов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		СтрокиДобавлены = Истина;
		
	КонецЦикла;
	
	Если СтрокиДобавлены Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОписаниеКолонокМакетаСегментовТоваров()
	
	КолонкиМакета = ЗагрузкаДанныхИзФайла.СформироватьОписаниеКолонок(Объект.СегментыТоваров, "Сегмент");
	
	ТипКод = ОбщегоНазначения.ОписаниеТипаСтрока(11);
	ЗаголовокКолонки = НСтр("ru = 'Код';
							|en = 'Code'");
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Код", ТипКод, ЗаголовокКолонки);
	Колонка.Родитель = "Сегмент";
	Колонка.Позиция = 1;
	КолонкиМакета.Добавить(Колонка);
	
	ТипНаименованиеСегмента = ОбщегоНазначения.ОписаниеТипаСтрока(50);
	ЗаголовокКолонки = НСтр("ru = 'Сегмент';
							|en = 'Segment'");
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета(
		"СегментНаименование",
		ТипНаименованиеСегмента,
		ЗаголовокКолонки);
	Колонка.Родитель = "Сегмент";
	Колонка.Позиция = 2;
	КолонкиМакета.Добавить(Колонка);
	
	ЗагрузкаДанныхИзФайлаКлиентСервер.УдалитьКолонкуМакета("Сегмент", КолонкиМакета);
	
	Возврат КолонкиМакета;
	
КонецФункции

// Параметры:
//  АдресЗагруженныхДанных - Строка, Неопределено - Адрес загруженных данных
//  ДополнительныеПараметры - Структура - Дополнительные параметры
//
&НаКлиенте
Процедура ЗагрузитьСегментыТоваровИзФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьСегментыТоваровИзФайлаНаСервере(АдресЗагруженныхДанных);
	
КонецПроцедуры

// Параметры:
//  АдресЗагруженныхДанных - Строка, Неопределено - Адрес загруженных данных
//
&НаСервере
Процедура ЗагрузитьСегментыТоваровИзФайлаНаСервере(АдресЗагруженныхДанных)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	СтрокиДобавлены = Ложь;
	Для Каждого СтрокаТаблицы Из ЗагруженныеДанные Цикл
		
		НоваяСтрока = Объект.СегментыТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		СтрокиДобавлены = Истина;
		
	КонецЦикла;
	
	Если СтрокиДобавлены Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОписаниеКолонокМакетаТовары(НастройкиОтображенияПолей)
	
	ИменаКолонок = Новый Массив; // Массив из Строка
	Если НастройкиОтображенияПолей.Период Тогда
		
		ИменаКолонок.Добавить("НачалоДействия");
		ИменаКолонок.Добавить("ОкончаниеДействия");
		
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.КоличествоПлан Тогда
		ИменаКолонок.Добавить("КоличествоПлан");
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.БазоваяЦена Тогда
		ИменаКолонок.Добавить("БазоваяЦена");
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.Процент Тогда
		ИменаКолонок.Добавить("Процент");
	КонецЕсли;
	
	Колонки = СтрСоединить(ИменаКолонок, ",");
	
	КолонкиМакета = ЗагрузкаДанныхИзФайла.СформироватьОписаниеКолонок(Объект.Товары, Колонки);
	
	НомерКолонки = 0;
	
	ИмяГруппыНоменклатура = НСтр("ru = 'Номенклатура';
								|en = 'Items'");
	
	НомерКолонки = НомерКолонки + 1;
	ТипШтрихКод = ОбщегоНазначения.ОписаниеТипаСтрока(200);
	ЗаголовокКолонки = НСтр("ru = 'Штрихкод';
							|en = 'Barcode'");
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Штрихкод", ТипШтрихКод, ЗаголовокКолонки);
	Колонка.Позиция = НомерКолонки;
	Колонка.Группа = ИмяГруппыНоменклатура;
	Колонка.Родитель = ИмяГруппыНоменклатура;
	КолонкиМакета.Добавить(Колонка);
	
	НомерКолонки = НомерКолонки + 1;
	ТипКод = ОбщегоНазначения.ОписаниеТипаСтрока(11);
	ЗаголовокКолонки = НСтр("ru = 'Код';
							|en = 'Code'");
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Код", ТипКод, ЗаголовокКолонки);
	Колонка.Позиция = НомерКолонки;
	Колонка.Группа = ИмяГруппыНоменклатура;
	Колонка.Родитель = ИмяГруппыНоменклатура;
	КолонкиМакета.Добавить(Колонка);
	
	НомерКолонки = НомерКолонки + 1;
	ТипАртикул = ОбщегоНазначения.ОписаниеТипаСтрока(50);
	ЗаголовокКолонки = НСтр("ru = 'Артикул';
							|en = 'Item ID'");
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Артикул", ТипАртикул, ЗаголовокКолонки);
	Колонка.Позиция = НомерКолонки;
	Колонка.Группа = ИмяГруппыНоменклатура;
	Колонка.Родитель = ИмяГруппыНоменклатура;
	КолонкиМакета.Добавить(Колонка);
	
	НомерКолонки = НомерКолонки + 1;
	ТипНаименованиеНоменклатуры = ОбщегоНазначения.ОписаниеТипаСтрока(100);
	ЗаголовокКолонки = НСтр("ru = 'Номенклатура';
							|en = 'Items'");
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета(
		"НоменклатураНаименование",
		ТипНаименованиеНоменклатуры,
		ЗаголовокКолонки);
	Колонка.Позиция = НомерКолонки;
	Колонка.Группа = ИмяГруппыНоменклатура;
	Колонка.Родитель = ИмяГруппыНоменклатура;
	КолонкиМакета.Добавить(Колонка);
	
	Если НастройкиОтображенияПолей.Характеристика Тогда
		
		НомерКолонки = НомерКолонки + 1;
		ТипНаименованиеХарактеристики = ОбщегоНазначения.ОписаниеТипаСтрока(150);
		ЗаголовокКолонки = НСтр("ru = 'Характеристика';
								|en = 'Variant'");
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета(
			"ХарактеристикаНаименование",
			ТипНаименованиеХарактеристики,
			ЗаголовокКолонки);
		Колонка.Позиция = НомерКолонки;
		Колонка.Родитель = "Характеристика";
		КолонкиМакета.Добавить(Колонка);
		
	КонецЕсли;
	
	ТекстПодсказкиЧисло =
		НСтр("ru = 'Поддерживаются лидирующие нули, а также разделители целой и дробной части "","" и "".""';
			|en = 'Leading zeros, integer and fraction dividers, "","", and ""."" are supported'");
	Если НастройкиОтображенияПолей.КоличествоПлан Тогда
		
		НомерКолонки = НомерКолонки + 1;
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("КоличествоПлан", КолонкиМакета);
		Колонка.Заголовок = НСтр("ru = 'Кол-во, план';
								|en = 'Quantity, plan'");
		Колонка.Подсказка = ТекстПодсказкиЧисло;
		Колонка.Позиция = НомерКолонки;
		
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.БазоваяЦена Тогда
		
		НомерКолонки = НомерКолонки + 1;
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("БазоваяЦена", КолонкиМакета);
		Колонка.Заголовок = НСтр("ru = 'Базовая цена';
								|en = 'Base price'");
		Колонка.Подсказка = ТекстПодсказкиЧисло;
		Колонка.Позиция = НомерКолонки;
		
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.Процент Тогда
		
		НомерКолонки = НомерКолонки + 1;
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("Процент", КолонкиМакета);
		Колонка.Заголовок = НСтр("ru = 'Бонус, %';
								|en = 'Bonus, %'");
		Колонка.Подсказка = ТекстПодсказкиЧисло;
		Колонка.Позиция = НомерКолонки;
		
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.Период Тогда
		
		ПодсказкаДата = НСтр("ru = 'Поддерживается формат в виде ""ДД.ММ.ГГГГ"" или ""ДД/ММ/ГГ""';
							|en = '""MM.DD.YYYY"" and ""MM/DD/YY"" formats are supported'");
		
		НомерКолонки = НомерКолонки + 1;
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("НачалоДействия", КолонкиМакета);
		Колонка.Заголовок = НСтр("ru = 'Начало действия';
								|en = 'Valid from'");
		Колонка.Подсказка = ПодсказкаДата;
		Колонка.Позиция = НомерКолонки;
		
		НомерКолонки = НомерКолонки + 1;
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("ОкончаниеДействия", КолонкиМакета);
		Колонка.Заголовок = НСтр("ru = 'Окончание действия';
								|en = 'Valid to'");
		Колонка.Подсказка = ПодсказкаДата;
		Колонка.Позиция = НомерКолонки;
		
	КонецЕсли;
	
	Возврат КолонкиМакета;
	
КонецФункции

&НаСервере
Функция ОписаниеКолонокМакетаСегментовПартнеров()
	
	КолонкиМакета = ЗагрузкаДанныхИзФайла.СформироватьОписаниеКолонок(Объект.СегментыПартнеров, "Сегмент");
	
	ТипКод = ОбщегоНазначения.ОписаниеТипаСтрока(11);
	ЗаголовокКолонки = НСтр("ru = 'Код';
							|en = 'Code'");
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Код", ТипКод, ЗаголовокКолонки);
	Колонка.Родитель = "Сегмент";
	Колонка.Позиция = 1;
	КолонкиМакета.Добавить(Колонка);
	
	ТипНаименованиеСегмента = ОбщегоНазначения.ОписаниеТипаСтрока(50);
	ЗаголовокКолонки = НСтр("ru = 'Сегмент';
							|en = 'Segment'");
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета(
		"СегментНаименование",
		ТипНаименованиеСегмента,
		ЗаголовокКолонки);
	Колонка.Родитель = "Сегмент";
	Колонка.Позиция = 2;
	КолонкиМакета.Добавить(Колонка);
	
	ЗагрузкаДанныхИзФайлаКлиентСервер.УдалитьКолонкуМакета("Сегмент", КолонкиМакета);
	
	Возврат КолонкиМакета;
	
КонецФункции

// Параметры:
//  АдресЗагруженныхДанных - Строка, Неопределено - Адрес загруженных данных
//  ДополнительныеПараметры - Структура - Дополнительные параметры
//
&НаКлиенте
Процедура ЗагрузитьТоварыИзФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьТоварыИзФайлаНаСервере(АдресЗагруженныхДанных);
	
КонецПроцедуры

// Параметры:
//  АдресЗагруженныхДанных - Строка -
//
&НаСервере
Процедура ЗагрузитьТоварыИзФайлаНаСервере(АдресЗагруженныхДанных)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	СтрокиДобавлены = Ложь;
	Для Каждого СтрокаТаблицы Из ЗагруженныеДанные Цикл
		
		НоваяСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		СтрокиДобавлены = Истина;
		
	КонецЦикла;
	
	Если СтрокиДобавлены Тогда
		
		ЗаполнитьСлужебныеРеквизитыПоНоменклатуре();
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  АдресЗагруженныхДанных - Строка, Неопределено -
//  ДополнительныеПараметры - Произвольный -
//
&НаКлиенте
Процедура ЗагрузитьСегментыПартнеровИзФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьСегментыПартнеровИзФайлаНаСервере(АдресЗагруженныхДанных);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСегментыПартнеровИзФайлаНаСервере(АдресЗагруженныхДанных)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	СтрокиДобавлены = Ложь;
	Для Каждого СтрокаТаблицы Из ЗагруженныеДанные Цикл
		
		НоваяСтрока = Объект.СегментыПартнеров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		СтрокиДобавлены = Истина;
		
	КонецЦикла;
	
	Если СтрокиДобавлены Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СведенияОбИсточникеЗначений(Тип)
	
	Сведения = Новый Структура;
	Сведения.Вставить("СвойствоНоменклатурыПредставление", СвойствоНоменклатурыПредставление);
	Сведения.Вставить("Имя", "");
	Сведения.Вставить("ПолноеИмя", "");
	Сведения.Вставить("Синоним", "");
	Сведения.Вставить("ДлинаНомера", 0);
	Сведения.Вставить("ЭтоЧисло", Ложь);
	Сведения.Вставить("ЭтоДата", Ложь);
	Сведения.Вставить("ЭтоСправочник", Ложь);
	Сведения.Вставить("ЭтоДокумент", Ложь);
	Сведения.Вставить("ЭтоПланВидовХарактеристик", Ложь);
	Сведения.Вставить("ЭтоПеречисление", Ложь);
	Сведения.Вставить("ДлинаКода", 0);
	Сведения.Вставить("ДлинаНаименования", 0);
	Сведения.Вставить("СодержитГруппы", Ложь);
	Сведения.Вставить("ДлинаНомера", 0);
	
	Если Тип = Тип("Число") Тогда
		
		Сведения.ЭтоЧисло = Истина;
		
	ИначеЕсли Тип = Тип("Дата") Тогда
		
		Сведения.ЭтоДата = Истина;
		
	Иначе
		
		ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип);
		
		Сведения.Имя = ОбъектМетаданных.Имя;
		Сведения.ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
		Сведения.Синоним = ОбъектМетаданных.Синоним;
		Сведения.ЭтоСправочник = Справочники.ТипВсеСсылки().СодержитТип(Тип);
		Сведения.ЭтоДокумент = Документы.ТипВсеСсылки().СодержитТип(Тип);
		Сведения.ЭтоПланВидовХарактеристик = ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(Тип);
		Сведения.ЭтоПеречисление = Перечисления.ТипВсеСсылки().СодержитТип(Тип);
		
		Если Сведения.ЭтоСправочник Тогда
			
			ЗаполнитьЗначенияСвойств(Сведения, ОбъектМетаданных, "ДлинаКода, ДлинаНаименования");
			Сведения.СодержитГруппы = ОбъектМетаданных.Иерархический
				И (ОбъектМетаданных.ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов);
			
		КонецЕсли;
		
		Если Сведения.ЭтоДокумент Тогда
			
			ЗаполнитьЗначенияСвойств(Сведения, ОбъектМетаданных, "ДлинаНомера");
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Сведения;
	
КонецФункции

// Параметры:
//  АдресЗагруженныхДанных - Строка, Неопределено - Адрес загруженных данных
//  ДополнительныеПараметры - Структура - Дополнительные параметры
//
&НаКлиенте
Процедура ЗагрузитьЗначенияСвойствИзФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьЗначенияСвойствИзФайлаНаСервере(АдресЗагруженныхДанных);
	
КонецПроцедуры

// Параметры:
//  АдресЗагруженныхДанных - Строка -
//
&НаСервере
Процедура ЗагрузитьЗначенияСвойствИзФайлаНаСервере(АдресЗагруженныхДанных)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	СтрокиДобавлены = Ложь;
	Для Каждого СтрокаТаблицы Из ЗагруженныеДанные Цикл
		
		НоваяСтрока = Объект.СвойстваНоменклатуры.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		СтрокиДобавлены = Истина;
		
	КонецЦикла;
	
	Если СтрокиДобавлены Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОписаниеКолонокМакетаСвойстваНоменклатуры(НастройкиОтображенияПолей, СведенияОбИсточнике)
	
	ИменаКолонок = Новый Массив; // Массив из Строка
	Если НастройкиОтображенияПолей.Период Тогда
		
		ИменаКолонок.Добавить("НачалоДействия");
		ИменаКолонок.Добавить("ОкончаниеДействия");
		
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.КоличествоПлан Тогда
		ИменаКолонок.Добавить("КоличествоПлан");
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.БазоваяЦена Тогда
		ИменаКолонок.Добавить("БазоваяЦена");
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.Процент Тогда
		ИменаКолонок.Добавить("Процент");
	КонецЕсли;
	
	Колонки = СтрСоединить(ИменаКолонок, ",");
	
	КолонкиМакета = ЗагрузкаДанныхИзФайла.СформироватьОписаниеКолонок(Объект.Товары, Колонки);
	ПодсказкаЧисло = НСтр("ru = 'Поддерживаются лидирующие нули, а также разделители целой и дробной части "","" и "".""';
							|en = 'Leading zeros, integer and fraction dividers, "","", and ""."" are supported'");
	ПодсказкаДата = НСтр("ru = 'Поддерживается формат в виде ""ДД.ММ.ГГГГ"" или ""ДД/ММ/ГГ""';
						|en = '""MM.DD.YYYY"" and ""MM/DD/YY"" formats are supported'");
	ЗаголовокКолонкиЗначенияСвойств = СведенияОбИсточнике.СвойствоНоменклатурыПредставление;
	ИмяГруппы = ЗаголовокКолонкиЗначенияСвойств;
	ИмяРодителя = "ЗначениеСвойства";
	
	НомерКолонки = 0;
	ДлинаНаименования = 100;
	
	ВыводитьГруппу = Ложь;
	
	Если (СведенияОбИсточнике.ЭтоСправочник
	      ИЛИ СведенияОбИсточнике.ЭтоПланВидовХарактеристик)
	     И СведенияОбИсточнике.ДлинаКода > 0 Тогда
		
		ДлинаКода = СведенияОбИсточнике.ДлинаКода;
		НомерКолонки = НомерКолонки + 1;
		ТипКод = ОбщегоНазначения.ОписаниеТипаСтрока(ДлинаКода);
		ЗаголовокКолонки = НСтр("ru = 'Код';
								|en = 'Code'");
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Код", ТипКод, ЗаголовокКолонки);
		Колонка.Позиция = НомерКолонки;
		Колонка.Группа = ИмяГруппы;
		Колонка.Родитель = ИмяРодителя;
		Колонка.Ширина = 20;
		КолонкиМакета.Добавить(Колонка);
		ДлинаНаименования = СведенияОбИсточнике.ДлинаНаименования;
		ВыводитьГруппу = Истина;
		
	ИначеЕсли СведенияОбИсточнике.ЭтоДокумент Тогда
		
		НомерКолонки = НомерКолонки + 1;
		Тип = ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя);
		ЗаголовокКолонки = НСтр("ru = 'Дата документа';
								|en = 'Document date'");
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Дата", Тип, ЗаголовокКолонки);
		Колонка.Позиция = НомерКолонки;
		Колонка.ОбязательнаДляЗаполнения = Истина;
		Колонка.Подсказка = ПодсказкаДата;
		Колонка.Группа = ИмяГруппы;
		Колонка.Родитель = ИмяРодителя;
		Колонка.Ширина = 20;
		КолонкиМакета.Добавить(Колонка);
		
		ЗаголовокКолонкиЗначенияСвойств = НСтр("ru = 'Номер документа';
												|en = 'Document number'");
		ДлинаНаименования = СведенияОбИсточнике.ДлинаНомера;
		ВыводитьГруппу = Истина;
		
	КонецЕсли;
	
	НомерКолонки = НомерКолонки + 1;
	
	Если СведенияОбИсточнике.ЭтоЧисло Тогда
		ТипСвойства = СведенияОбИсточнике.Тип;
	ИначеЕсли СведенияОбИсточнике.ЭтоДата Тогда
		ТипСвойства = СведенияОбИсточнике.Тип;
	Иначе
		ТипСвойства = ОбщегоНазначения.ОписаниеТипаСтрока(ДлинаНаименования);
	КонецЕсли;
	
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета(
		ИмяРодителя,
		ТипСвойства,
		ЗаголовокКолонкиЗначенияСвойств);
	Колонка.Позиция = НомерКолонки;
	Если СведенияОбИсточнике.ЭтоДокумент Тогда
		Колонка.ОбязательнаДляЗаполнения = Истина;
	КонецЕсли; 
	Если ВыводитьГруппу Тогда
		Колонка.Группа = ИмяГруппы;
	КонецЕсли;
	Колонка.Родитель = ИмяРодителя;
	Колонка.Ширина = 35;
	КолонкиМакета.Добавить(Колонка);
	
	Если НастройкиОтображенияПолей.КоличествоПлан Тогда
		
		НомерКолонки = НомерКолонки + 1;
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("КоличествоПлан", КолонкиМакета);
		Колонка.Заголовок = НСтр("ru = 'Кол-во, план';
								|en = 'Quantity, plan'");
		Колонка.Подсказка = ПодсказкаЧисло;
		Колонка.Позиция = НомерКолонки;
		
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.БазоваяЦена Тогда
		
		НомерКолонки = НомерКолонки + 1;
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("БазоваяЦена", КолонкиМакета);
		Колонка.Заголовок = НСтр("ru = 'Базовая цена';
								|en = 'Base price'");
		Колонка.Подсказка = ПодсказкаЧисло;
		Колонка.Позиция = НомерКолонки;
		
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.Процент Тогда
		
		НомерКолонки = НомерКолонки + 1;
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("Процент", КолонкиМакета);
		Колонка.Заголовок = НСтр("ru = 'Бонус, %';
								|en = 'Bonus, %'");
		Колонка.Подсказка = ПодсказкаЧисло;
		Колонка.Позиция = НомерКолонки;
		
	КонецЕсли;
	
	Если НастройкиОтображенияПолей.Период Тогда
		
		НомерКолонки = НомерКолонки + 1;
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("НачалоДействия", КолонкиМакета);
		Колонка.Заголовок = НСтр("ru = 'Начало действия';
								|en = 'Start date'");
		Колонка.Подсказка = ПодсказкаДата;
		Колонка.Позиция = НомерКолонки;
		
		НомерКолонки = НомерКолонки + 1;
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.КолонкаМакета("ОкончаниеДействия", КолонкиМакета);
		Колонка.Заголовок = НСтр("ru = 'Окончание действия';
								|en = 'End date'");
		Колонка.Подсказка = ПодсказкаДата;
		Колонка.Позиция = НомерКолонки;
		
	КонецЕсли; 
	
	Возврат КолонкиМакета;
	
КонецФункции

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура Подключаемый_ЗакрытьФорму()
	
	Закрыть();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДетализацияПоКонтрагентуКлиенту(ДетализацияРасчетаУчастников)
	
	Возврат ДетализацияРасчетаУчастников = ПредопределенноеЗначение("Перечисление.ДетализацияРасчетаУчастниковРетроБонусов.ПоКонтрагентуКлиенту");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДетализацияПоКонтрагенту(ДетализацияРасчетаУчастников)
	
	Возврат ДетализацияРасчетаУчастников = ПредопределенноеЗначение("Перечисление.ДетализацияРасчетаУчастниковРетроБонусов.ПоКонтрагенту");
	
КонецФункции

&НаСервере
Процедура УстановитьЗаголовок()
	
	АвтоЗаголовок = Ложь;
	Заголовок = РетроБонусыСервер.ЗаголовокДокумента(
		Объект.Ссылка, Объект.Номер, Объект.Дата, Объект.Исправление);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоВидуРетроБонуса(ТолькоКеш = Ложь)
	
	МассивРеквизитов = Новый Массив; // Массив Из Строка
	
	Если НЕ ТолькоКеш Тогда
		
		МассивРеквизитов.Добавить("БезРасчета");
		МассивРеквизитов.Добавить("ПериодичностьУсловий");
		МассивРеквизитов.Добавить("ПериодичностьНачислений");
		МассивРеквизитов.Добавить("ДетализацияРасчетаУчастников");
		МассивРеквизитов.Добавить("ОтборТоваров");
		МассивРеквизитов.Добавить("ОтборУчастников");
		
	КонецЕсли;
	
	МассивРеквизитов.Добавить("ПоказательТоваров");
	МассивРеквизитов.Добавить("БазаРасчетаПродаж");
	МассивРеквизитов.Добавить("СоставТоваров");
	МассивРеквизитов.Добавить("СоставУчастников");
	МассивРеквизитов.Добавить("СвойствоНоменклатуры");
	
	РеквизитыВида = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ВидРетроБонуса, МассивРеквизитов);
	
	Если НЕ ТолькоКеш Тогда
		
		Объект.ШкалыРасчета.Очистить();
		Объект.БезРасчета = РеквизитыВида.БезРасчета;
		Если НЕ Объект.БезРасчета Тогда
			
			Объект.НачислитьСразу = Ложь;
			
		КонецЕсли;
		
		Объект.ПериодичностьУсловий = РеквизитыВида.ПериодичностьУсловий;
		Объект.ПериодичностьНачислений = РеквизитыВида.ПериодичностьНачислений;
		Объект.ДетализацияРасчетаУчастников = РеквизитыВида.ДетализацияРасчетаУчастников;
		Объект.ОтборУчастников = РеквизитыВида.ОтборУчастников;
		Объект.ОтборТоваров = РеквизитыВида.ОтборТоваров;
		Объект.ВидШкалыРасчета = 0;
		
	КонецЕсли;
	
	ПоказательТоваров = РеквизитыВида.ПоказательТоваров;
	БазаРасчетаПродаж = РеквизитыВида.БазаРасчетаПродаж;
	СоставТоваров = РеквизитыВида.СоставТоваров;
	СоставУчастников = РеквизитыВида.СоставУчастников;
	
	РетроБонусыКлиентСервер.ЗаполнитьДопустимыеВариантыШкалРасчета(Элементы.ВидШкалыРасчета, ПоказательТоваров);
	РетроБонусыКлиентСервер.УстановитьЗаголовокГруппыШкалРасчета(ЭтотОбъект);
	
	#Область СвойствоНоменклатуры
	Если СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры Тогда
		
		РезультатПроверки = РетроБонусыСервер.ДанныеПроверкиСвойстваНоменклатуры(РеквизитыВида.СвойствоНоменклатуры);
		СвойствоНоменклатуры = РезультатПроверки.Свойство;
		СвойствоНоменклатурыПредставление = РезультатПроверки.Заголовок;
		
		ПараметрыВыбораЗначения = Новый Массив; // Массив из ПараметрВыбора
		ДопустимыеТипыСвойства = Новый ОписаниеТипов();
		
		Если РезультатПроверки.РазрешеноИспользовать Тогда
			
			ТипЗначения = РезультатПроверки.ТипЗначения;
			ДопустимыеТипыСвойства = РетроБонусыСервер.РазрешенныеТипыЗначенийСвойстваНоменклатуры(ТипЗначения);
			Элементы.СвойстваНоменклатурыЗначениеСвойства.ОграничениеТипа = ДопустимыеТипыСвойства;
			Элементы.СвойстваНоменклатурыЗначениеСвойства.ВыбиратьТип = Истина;
			РетроБонусыКлиентСервер.УстановитьФорматЗначенияСвойстваПоУмолчанию(
				Элементы.СвойстваНоменклатурыЗначениеСвойства,
				ДопустимыеТипыСвойства);
			
			Если ЗначениеЗаполнено(СвойствоНоменклатуры) Тогда
				
				НовыйОтбор = Новый ПараметрВыбора("Отбор.Владелец", СвойствоНоменклатуры);
				ПараметрыВыбораЗначения.Добавить(НовыйОтбор);
				
			КонецЕсли;
			
			Если НЕ ТолькоКеш
			   И СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры Тогда
				
				Для каждого СтрокаТЧ Из Объект.СвойстваНоменклатуры Цикл
					
					//@skip-check statement-type-change
					СтрокаТЧ.ЗначениеСвойства = ДопустимыеТипыСвойства.ПривестиЗначение(СтрокаТЧ.ЗначениеСвойства);
					
				КонецЦикла;
				РетроБонусыСервер.ОбновитьДиапазоныСтрокВсехСвойств(Объект, ПоказательТоваров);
				
			КонецЕсли;
			
		Иначе
			
			РетроБонусыСервер.СообщитьОбОшибкеСвойстваНоменклатуры(РезультатПроверки, "ВидРетроБонуса");
			
		КонецЕсли;
		
		Элементы.СвойстваНоменклатурыЗначениеСвойства.ПараметрыВыбора =
			Новый ФиксированныйМассив(ПараметрыВыбораЗначения);
		
	КонецЕсли;
	#КонецОбласти
	
	Если НЕ ТолькоКеш Тогда
		
		ОпределитьВалютуДокумента();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПризнакиРасчетаПланаУчастников(ИзмененныйУчастник = Неопределено)
	
	ПараметрыУчастников = Документы.УсловияРетроБонусовКлиентов.ПараметрыОчисткиРеквизитовУчастников();
	ПараметрыУчастников.СоставУчастников = СоставУчастников;
	ПараметрыУчастников.ОтборУчастников = Объект.ОтборУчастников;
	ПараметрыУчастников.ПоказательТоваров = ПоказательТоваров;
	Документы.УсловияРетроБонусовКлиентов.УстановитьПризнакиРасчетаПланаУчастников(
		Объект, ПараметрыУчастников, ИзмененныйУчастник);
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьВалютуДокумента()
	
	Если БазаРасчетаПродаж = Перечисления.БазыРасчетаРетроБонусов.ВыручкаРеглУчет
	   И НЕ Объект.Организация.Пустая() Тогда
		
		Объект.Валюта = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Объект.Организация);
		
	ИначеЕсли БазаРасчетаПродаж = Перечисления.БазыРасчетаРетроБонусов.ВыручкаУпрУчет Тогда
		
		Объект.Валюта = Константы.ВалютаУправленческогоУчета.Получить();
		
	ИначеЕсли БазаРасчетаПродаж = Перечисления.БазыРасчетаРетроБонусов.ВыручкаБазовыеЦены Тогда
		
		Объект.Валюта = Справочники.Валюты.ПолучитьВалютуПоУмолчанию();
		
	ИначеЕсли Объект.БезРасчета
			И ПолучитьФункциональнуюОпцию("НеИспользоватьНесколькоВалют") Тогда
		
		Объект.Валюта = Справочники.Валюты.ПолучитьВалютуПоУмолчанию();
		
	Иначе
		
		Объект.Валюта = Справочники.Валюты.ПустаяСсылка();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовкиПоВалюте()
	
	#Область СуммаПлан
	Если БазаРасчетаПродаж = Перечисления.БазыРасчетаРетроБонусов.ВыручкаВзаиморасчеты
	 ИЛИ Объект.Валюта.Пустая() Тогда
		
		ЗаголовокСуммыПлан = НСтр("ru = 'Сумма, план';
									|en = 'Amount, plan'");
		
	Иначе
		
		ШаблонСуммыПлан = НСтр("ru = 'Сумма, план (%1)';
								|en = 'Amount, plan (%1)'");
		ЗаголовокСуммыПлан = СтрШаблон(ШаблонСуммыПлан, Объект.Валюта);
		
	КонецЕсли;
	
	Элементы.ИННКонтрагентовСуммаПлан.Заголовок = ЗаголовокСуммыПлан;
	Элементы.КонтрагентыСуммаПлан.Заголовок = ЗаголовокСуммыПлан;
	#КонецОбласти
	
	#Область СуммаБонус
	Если Объект.Валюта.Пустая() Тогда
		
		ЗаголовокСуммыПлан = НСтр("ru = 'Сумма, бонус';
									|en = 'Amount, bonus'");
		
	Иначе
		
		ШаблонСуммыПлан = НСтр("ru = 'Сумма, бонус (%1)';
								|en = 'Amount, bonus (%1)'");
		ЗаголовокСуммыПлан = СтрШаблон(ШаблонСуммыПлан, Объект.Валюта);
		
	КонецЕсли;
	
	Элементы.КонтрагентыСуммаБонус.Заголовок = ЗаголовокСуммыПлан;
	#КонецОбласти
	
	#Область БазоваяЦена
	Если БазаРасчетаПродаж <> Перечисления.БазыРасчетаРетроБонусов.ВыручкаБазовыеЦены
	 ИЛИ Объект.Валюта.Пустая() Тогда
		
		ЗаголовокБазоваяЦена = НСтр("ru = 'Базовая цена';
									|en = 'Base price'");
		
	Иначе
		
		ШаблонБазоваяЦена = НСтр("ru = 'Базовая цена (%1)';
								|en = 'Base price (%1)'");
		ЗаголовокБазоваяЦена = СтрШаблон(ШаблонБазоваяЦена, Объект.Валюта);
		
	КонецЕсли;
	
	Элементы.ТоварыБазоваяЦена.Заголовок = ЗаголовокБазоваяЦена;
	#КонецОбласти
	
КонецПроцедуры

// Установить доступный список периодичности начислений
// 
// Параметры:
//  Форма - см. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента
//
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступныйСписокПериодичностьНачислений(Форма)
	
	Объект = Форма.Объект;
	
	ПараметрыОтбора = РетроБонусыКлиентСервер.НовыеПараметрыДоступныхПериодичностейНачислений();
	ПараметрыОтбора.ПериодичностьУсловий = Объект.ПериодичностьУсловий;
	ПараметрыОтбора.ПоказательТоваров = Форма.ПоказательТоваров;
	
	МассивВыбора = РетроБонусыКлиентСервер.ДоступныйСписокПериодичностьНачислений(ПараметрыОтбора);
	Форма.Элементы.ПериодичностьНачислений.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);
	РетроБонусыКлиентСервер.ПроверитьУстановитьЗначениеСпискаВыбора(Объект, "ПериодичностьНачислений", МассивВыбора);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуре()
	
	ПараметрыЗаполненияРеквизитов = Новый Структура;
	ПараметрыЗаполненияРеквизитов.Вставить(
		"ЗаполнитьПризнакХарактеристикиИспользуются",
		Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
	ПараметрыЗаполненияРеквизитов.Вставить(
		"ЗаполнитьПризнакТипНоменклатуры",
		Новый Структура("Номенклатура", "ТипНоменклатуры"));
	ПараметрыЗаполненияРеквизитов.Вставить(
		"ЗаполнитьПризнакАртикул",
		Новый Структура("Номенклатура", "Артикул"));
	
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(
		Объект.Товары, ПараметрыЗаполненияРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыСоглашенийДоговоров()
	
	Если СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Соглашения Тогда
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ЗаполнитьПризнакСоглашенияВСтрокеТЧ", "Соглашение");
		
		ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Объект.ДоговорыСоглашения, СтруктураДействий);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПризнакНаличияНачислений()
	
	Если Объект.ИсправляемыйДокумент.Пустая() Тогда
		
		Если Объект.Ссылка.Пустая()
		 ИЛИ Объект.НачислитьСразу Тогда
			
			ЕстьНачисления = Ложь;
			
		Иначе
			
			МаксимальнаяДата = РетроБонусыСервер.ГраницаНачисленийКлиентов(Объект.Ссылка);
			ЕстьНачисления = (МаксимальнаяДата <> Дата(1, 1, 1));
			
		КонецЕсли;
		
	Иначе
		
		МаксимальнаяДата = РетроБонусыСервер.ГраницаНачисленийКлиентов(Объект.ИсправляемыйДокумент);
		ЕстьНачисления = (МаксимальнаяДата <> Дата(1, 1, 1));
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеПоследнейКорректировки()
	
	ЕстьКорректировкаНаСогласовании = Ложь;
	Если НЕ Объект.Ссылка.Пустая() Тогда
		
		ЕстьКорректировкаНаСогласовании = РетроБонусыСервер.ЕстьКорректировкаНаСогласовании(Объект.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПередФиксациейСостава()
	
	ЗаполнитьДанныеПоследнейКорректировки();
	Если ЕстьКорректировкаНаСогласовании Тогда
		
		УстановитьЗаголовкиДекорацийСГиперссылками();
		
	КонецЕсли;
	ЗаполнитьРеквизитыСоставаПоУсловию();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПередОтменойФиксацииСостава()
	
	ЗаполнитьПризнакНаличияНачислений();
	Если ЕстьНачисления Тогда
		УстановитьЗаголовкиДекорацийСГиперссылками();
	КонецЕсли;
	ЗаполнитьРеквизитыСоставаПоУсловию();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьСуммыПлановСтрокой(Форма)
	
	Форма.СуммаПланСтрокой = Формат(Форма.Объект.СуммаПлан, "ЧДЦ=2; ЧН=-;");
	
	ШаблонСуммы = "<%1>";
	Форма.СуммаПланСтрокойНеизменный = СтрШаблон(ШаблонСуммы, Форма.СуммаПланСтрокой);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПартнерыСтатическогоСегмента(Сегмент)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПартнерыСегмента.Партнер КАК Партнер
	|ИЗ
	|	РегистрСведений.ПартнерыСегмента КАК ПартнерыСегмента
	|ГДЕ
	|	ПартнерыСегмента.Сегмент = &Сегмент
	|	И ПартнерыСегмента.Партнер.Клиент
	|
	|УПОРЯДОЧИТЬ ПО
	|	Партнер";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Сегмент", Сегмент);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПартнерыДинамическогоСегмента(Сегмент)
	
	ДанныеСегмента = ТаблицаДинамическогоСегментаПартнеров(Сегмент);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ДанныеСегмента.ЭлементСписка КАК Справочник.Партнеры) КАК Партнер
	|ПОМЕСТИТЬ ВТ_ПартнерыСегментов
	|ИЗ
	|	&ДанныеСегмента КАК ДанныеСегмента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартнерыСегментов.Партнер КАК Партнер
	|ИЗ
	|	ВТ_ПартнерыСегментов КАК ПартнерыСегментов
	|ГДЕ
	|	ПартнерыСегментов.Партнер.Клиент
	|
	|УПОРЯДОЧИТЬ ПО
	|	Партнер";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДанныеСегмента", ДанныеСегмента);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции

// Параметры:
//  Сегмент - СправочникСсылка.СегментыПартнеров
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
//  * ЭлементСписка - СправочникСсылка.Партнеры
//
&НаСервереБезКонтекста
Функция ТаблицаДинамическогоСегментаПартнеров(Сегмент)
	
	ТаблицаСегмента = СегментыСервер.СписокЭлементовСКД(Сегмент);
	Возврат ТаблицаСегмента;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТоварыСтатическогоСегмента(Сегмент)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НоменклатураСегмента.Номенклатура КАК Номенклатура,
	|	НоменклатураСегмента.Характеристика КАК Характеристика
	|ИЗ
	|	РегистрСведений.НоменклатураСегмента КАК НоменклатураСегмента
	|ГДЕ
	|	НоменклатураСегмента.Сегмент = &Сегмент
	|	И НоменклатураСегмента.Номенклатура.ТипНоменклатуры В (&ПоддерживаемыеТипыНоменклатуры)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	Характеристика";
	
	ТипыНоменклатуры = РетроБонусыСервер.ПоддерживаемыеТипыНоменклатуры();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Сегмент", Сегмент);
	Запрос.УстановитьПараметр("ПоддерживаемыеТипыНоменклатуры", ТипыНоменклатуры);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТоварыДинамическогоСегмента(Сегмент)
	
	ДанныеСегмента = ТаблицаДинамическогоСегментаНоменклатуры(Сегмент);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ДанныеСегмента.ЭлементСписка КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ДанныеСегмента.ХарактеристикаЭлемента КАК Характеристика
	|ПОМЕСТИТЬ ВТ_НоменклатураСегментов
	|ИЗ
	|	&ДанныеСегмента КАК ДанныеСегмента
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НоменклатураСегментов.Номенклатура КАК Номенклатура,
	|	ВТ_НоменклатураСегментов.Характеристика КАК Характеристика
	|ИЗ
	|	ВТ_НоменклатураСегментов КАК ВТ_НоменклатураСегментов
	|ГДЕ
	|	ВТ_НоменклатураСегментов.Номенклатура.ТипНоменклатуры В (&ПоддерживаемыеТипыНоменклатуры)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	Характеристика";
	
	ТипыНоменклатуры = РетроБонусыСервер.ПоддерживаемыеТипыНоменклатуры();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДанныеСегмента", ДанныеСегмента);
	Запрос.УстановитьПараметр("ПоддерживаемыеТипыНоменклатуры", ТипыНоменклатуры);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции

// Параметры:
//  Сегмент - СправочникСсылка.СегментыНоменклатуры
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
//  * ЭлементСписка - СправочникСсылка.Номенклатура
//  * ХарактеристикаЭлемента - СправочникСсылка.ХарактеристикиНоменклатуры
//
&НаСервереБезКонтекста
Функция ТаблицаДинамическогоСегментаНоменклатуры(Сегмент)
	
	ТаблицаСегмента = СегментыСервер.СписокЭлементовСКД(Сегмент);
	Возврат ТаблицаСегмента;
	
КонецФункции

&наСервере
Процедура ЗаполнитьТоварыКоличествоПланСервер(Знач Значение, Знач ВыделенныеСтроки)
	
	ЭтоКоличествоПоДиапазонам = РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров);
	МассивСтрокКОбработке = Новый Массив; // Массив Из ДанныеФормыЭлементКоллекции
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		
		СтрокаТоваров = Объект.Товары.НайтиПоИдентификатору(ВыделеннаяСтрока);
		Если СтрокаТоваров.ИсходнаяСтрока Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТоваров.КоличествоПлан = Значение;
		
		Если ЭтоКоличествоПоДиапазонам Тогда
			МассивСтрокКОбработке.Добавить(СтрокаТоваров);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЭтоКоличествоПоДиапазонам Тогда
		
		СтрокиКОбработке = Объект.Товары.Выгрузить(МассивСтрокКОбработке);
		РетроБонусыСервер.ОбновитьДиапазоныСтрокТоваровПоТаблице(Объект, СтрокиКОбработке);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСДиапазонамиСвойств

// Параметры:
//  ЗначениеСвойства - ОпределяемыйТип.ТипыСвойствНоменклатурыРетроБонусов
//
&НаСервере
Процедура ОбновитьДиапазоныСтрокСвойствПоСвойству(ЗначениеСвойства)
	
	РетроБонусыСервер.ОбновитьДиапазоныСтрокСвойствПоСвойству(Объект, ЗначениеСвойства);
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСДиапазонамиТоваров

&НаКлиенте
Процедура СохранитьАналитикуСтрокиТоваров(АналитикаСтрокиТоваров, ТекущиеДанные)
	
	ПараметрыЗаполнения = РетроБонусыКлиентСервер.ПараметрыЗаполненияАналитикиИзменяемойСтроки();
	ПараметрыЗаполнения.ВидШкалыРасчета = Объект.ВидШкалыРасчета;
	ПараметрыЗаполнения.ПоказательТоваров = ПоказательТоваров;
	ПараметрыЗаполнения.Номенклатура = ТекущиеДанные.Номенклатура;
	ПараметрыЗаполнения.Характеристика = ТекущиеДанные.Характеристика;
	ПараметрыЗаполнения.ХарактеристикиИспользуются = ТекущиеДанные.ХарактеристикиИспользуются;
	
	РетроБонусыКлиентСервер.СохранитьАналитикуСтрокиТоваров(
		АналитикаСтрокиТоваров, ПараметрыЗаполнения);
	
КонецПроцедуры

// Параметры:
//  АналитикаИзменяемойСтрокиТоваров - см. РетроБонусыКлиентСервер.СтруктураСохраняемойСтрокиТоваров
//
&НаСервере
Процедура ОбновитьДиапазоныСтрокТоваровПоТовару(АналитикаИзменяемойСтрокиТоваров)
	
	РетроБонусыСервер.ОбновитьДиапазоныСтрокТоваровПоТовару(
		Объект,
		АналитикаИзменяемойСтрокиТоваров.Номенклатура,
		АналитикаИзменяемойСтрокиТоваров.Характеристика);
	
КонецПроцедуры

&НаСервере
Процедура УпорядочитьТоварыПоДиапазонуНаСервере()
	
	Если НЕ РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров)
	 ИЛИ Объект.Товары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Колонки = "Номенклатура, Характеристика, КоличествоПлан, ИсходнаяСтрока Убыв";
	Объект.Товары.Сортировать(Колонки, Новый СравнениеЗначений);
	
КонецПроцедуры

&НаСервере
Процедура УпорядочитьЗначенияСвойствПоДиапазонуНаСервере()

	Если НЕ РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров)
	 ИЛИ Объект.СвойстваНоменклатуры.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Колонки = "ЗначениеСвойства, КоличествоПлан, ИсходнаяСтрока Убыв";
	Объект.СвойстваНоменклатуры.Сортировать(Колонки, Новый СравнениеЗначений);
	
КонецПроцедуры

#КонецОбласти

#Область СтандартныеПодсистемы_ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды

// Подключаемый продолжить выполнение команды на сервере.
// 
// Параметры:
//  ПараметрыВыполнения - Структура -
//  ДополнительныеПараметры - Структура -
//
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СтандартныеПодсистемы_Свойства

// СтандартныеПодсистемы.Свойства
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#КонецОбласти