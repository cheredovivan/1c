#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОперативныйУчетТоваровОрганизаций");
	//++ НЕ УТ
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- НЕ УТ
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("УчетНДС");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.КорректировкаНалогообложенияНДСПартийТоваров") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры);
		
		РасчетСебестоимостиПроведениеДокументов.ОтразитьВМеханизмеУчетаЗатратИСебестоимости(ДокументСсылка, Запрос, ТекстыЗапроса, Регистры);
		
		//++ НЕ УТ
		РеглУчетПроведениеСервер.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
		//-- НЕ УТ
		
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
	
КонецПроцедуры

// Добавляет команду создания документа "Корректировка налогообложения НДС".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
// Возвращаемое значение:
//  СтрокаТаблицыЗначений
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.КорректировкаНалогообложенияНДСПартийТоваров) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.КорректировкаНалогообложенияНДСПартийТоваров.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.КорректировкаНалогообложенияНДСПартийТоваров);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
	
КонецПроцедуры

#Область ЗакрытиеМесяца

// Процедура отменяет проведение документов "Корректировка налогообложения НДС товаров".
//
// Параметры:
// 	ПериодРегистрации - Дата - Месяц регистрации документа корректировки.
//
Процедура ОтменитьКорректировкуНалогообложенияНДС(ПериодРегистрации) Экспорт
	
	ПартионныйУчетВключен  = РасчетСебестоимостиПовтИсп.ПартионныйУчетВключен(НачалоМесяца(ПериодРегистрации));
	ПоследнийМесяцКвартала = (КонецКвартала(ПериодРегистрации) = КонецМесяца(ПериодРегистрации));
	Если НЕ (ПартионныйУчетВключен И ПоследнийМесяцКвартала) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Операция.Ссылка
	|ИЗ
	|	Документ.КорректировкаНалогообложенияНДСПартийТоваров КАК Операция
	|ГДЕ
	|	Операция.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И Операция.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносОстатковПодНовыйВидДеятельности)
	|	И Операция.Проведен
	|");
	Запрос.УстановитьПараметр("ДатаНачала",    НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(ПериодРегистрации));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОбъект.ДополнительныеСвойства.Вставить(РасчетСебестоимостиПрикладныеАлгоритмы.ИмяСлужебногоДополнительногоСвойстваОбъекта());
		ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
	КонецЦикла;
	
КонецПроцедуры

// Процедура анализирует необходимость формирования документов по корректировке налогообложения НДС
// для партий товаров с истекающим периодом принятия НДС к вычету
// (для налогообложения по фактическому использованию).
//
// Параметры:
//	ПериодРегистрации - Дата - закрываемый месяц
//	СозданаКорректировка - Булево - В параметр устанавливается признак, что корректировка выполнена.
//
Процедура СкорректироватьНалогообложениеНДС(ПериодРегистрации, СозданаКорректировка) Экспорт
	
	ПартионныйУчетВключен  = РасчетСебестоимостиПовтИсп.ПартионныйУчетВключен(НачалоМесяца(ПериодРегистрации));
	ПоследнийМесяцКвартала = (КонецКвартала(ПериодРегистрации) = КонецМесяца(ПериодРегистрации));
	
	// Формируем документ в последнем месяце отчетного периода и только при партионном учете.
	Если НЕ (ПартионныйУчетВключен И ПоследнийМесяцКвартала) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	СпрОрганизации.Ссылка КАК Организация
	|ПОМЕСТИТЬ НДСПоФактическомуИспользованию
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДС.СрезПоследних(&ДатаОкончания) КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК СпрОрганизации
	|		ПО СпрОрганизации.ГоловнаяОрганизация = ДанныеРегистра.Организация
	|ГДЕ
	|	ДанныеРегистра.ПрименяетсяУчетНДСПоФактическомуИспользованию
	|;
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	тПартии.Организация	КАК Организация,
	|	ИСТИНА				КАК ФормироватьДвижения
	|ПОМЕСТИТЬ ВтПартии
	|	
	|ИЗ (
	|	ВЫБРАТЬ
	|		Остатки.Организация						КАК Организация,
	|		Остатки.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|		Остатки.ДокументПоступления				КАК ДокументПоступления,
	|		Остатки.ВидЗапасов						КАК ВидЗапасов,
	|		Остатки.АналитикаУчетаПартий			КАК АналитикаУчетаПартий,
	|		Остатки.КоличествоОстаток				КАК Количество
	|	ИЗ
	|		РегистрНакопления.ПартииТоваровОрганизаций.Остатки(&НачалоСледКвартала,
	|			Организация В (
	|			ВЫБРАТЬ
	|				НДСПоФактическомуИспользованию.Организация
	|			ИЗ
	|				НДСПоФактическомуИспользованию КАК НДСПоФактическомуИспользованию
	|			)
	|		) КАК Остатки
	|	
	|		// Дата документа поступления должны быть в определенном диапазоне
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрНакопления.ПартииТоваровОрганизаций КАК Партии
	|		ПО
	|			Партии.Период МЕЖДУ &НачПериода И &КонПериода
	|			И Партии.Регистратор = Остатки.ДокументПоступления
	|			И Партии.Регистратор = Партии.ДокументПоступления
	|			И Партии.АналитикаУчетаПартий <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
	|	
	|		// Аналитика учета партий должны быть с налогообложением по фактическому использованию
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаПартий КАК АУП
	|		ПО
	|			АУП.КлючАналитики = Остатки.АналитикаУчетаПартий
	|			И АУП.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуИспользованию)
	|	ГДЕ
	|		Остатки.КоличествоОстаток > 0
	|
	|) КАК тПартии
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.Ссылка							КАК Организация,
	|	ЕСТЬNULL(Партии.ФормироватьДвижения, ЛОЖЬ)	КАК ФормироватьДвижения,
	|	ЕСТЬNULL(КорНДС.Проведен, ЛОЖЬ)				КАК Проведен,
	|	ЕСТЬNULL(КорНДС.Ссылка, НЕОПРЕДЕЛЕНО)		КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтПартии КАК Партии
	|	ПО
	|		Организации.Ссылка = Партии.Организация
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаНалогообложенияНДСПартийТоваров КАК КорНДС
	|	ПО
	|		Организации.Ссылка = КорНДС.Организация
	|		И КорНДС.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносОстатковПодНовыйВидДеятельности)
	|		И КорНДС.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		И НЕ КорНДС.ПометкаУдаления
	|
	|ГДЕ
	|	НЕ (Партии.Организация ЕСТЬ NULL И КорНДС.Организация ЕСТЬ NULL)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Партии.Организация ВОЗР,
	|	КорНДС.Проведен УБЫВ
	|");
	
	НачалоСледНалПериода = (КонецМесяца(ПериодРегистрации) + 1);
	
	Запрос.УстановитьПараметр("НачалоСледКвартала",	КонецМесяца(ПериодРегистрации) + 1);
	Запрос.УстановитьПараметр("НачПериода",			ДобавитьМесяц(НачалоСледНалПериода, -36));
	Запрос.УстановитьПараметр("КонПериода",			ДобавитьМесяц(КонецКвартала(НачалоСледНалПериода), -36));
	Запрос.УстановитьПараметр("ДатаНачала",			НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("ДатаОкончания",		КонецМесяца(ПериодРегистрации));
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	ТекОрганизация = Неопределено;
	МассивОрганизаций = Новый Массив;
	СозданаКорректировка = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		Если Не ТекОрганизация = Выборка.Организация Тогда
			ТекОрганизация = Выборка.Организация;
			МассивОрганизаций.Добавить(ТекОрганизация);
		Иначе
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.Ссылка) Тогда
			Корректировка = Выборка.Ссылка.ПолучитьОбъект();
		Иначе
			Корректировка = Документы.КорректировкаНалогообложенияНДСПартийТоваров.СоздатьДокумент();
		КонецЕсли;
		
		Корректировка.ДополнительныеСвойства.Вставить(РасчетСебестоимостиПрикладныеАлгоритмы.ИмяСлужебногоДополнительногоСвойстваОбъекта());
		
		Если Выборка.ФормироватьДвижения Тогда
			
			Корректировка.Дата = КонецКвартала(ПериодРегистрации);
			Корректировка.Организация = Выборка.Организация;
			Корректировка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КорректировкаНалогообложенияНДСПартийТоваров;
			Корректировка.ПеремещениеПодДеятельность = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
			Корректировка.Записать(РежимЗаписиДокумента.Проведение);
			
			СозданаКорректировка = Истина;
			
		ИначеЕсли Выборка.Проведен Тогда
			
			Корректировка.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
// 
// Параметры:
// 	ТаблицаЭтапов - см. Обработки.ОперацииЗакрытияМесяца.ЗаполнитьОписаниеЭтаповЗакрытияМесяца
// 	ТекущийРодитель - Строка - идентификатор группы.
//
Процедура ДобавитьЭтап_ПереносОстатковПодНовыйВидДеятельности(ТаблицаЭтапов, ТекущийРодитель) Экспорт
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Справочники.ОперацииЗакрытияМесяца.ПереносОстатковПодНовыйВидДеятельности);
	
	НоваяСтрока.МультиязычноеНаименование = "ru = 'Перенос остатков под новый вид деятельности';
											|en = 'Transfer balances to new activity category'"; // @НСтр-1
	НоваяСтрока.ТекстВыполнить = НСтр("ru = 'Выполнить';
										|en = 'Perform'");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"Документы.КорректировкаНалогообложенияНДСПартийТоваров.Использование_ПереносОстатковПодНовыйВидДеятельности");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"Документы.КорректировкаНалогообложенияНДСПартийТоваров.Выполнить_ПереносОстатковПодНовыйВидДеятельности");
	НоваяСтрока.ДействиеПодробнее = ЗакрытиеМесяцаСервер.ОписаниеДействия_ОткрытьФорму(
		Метаданные.Документы.КорректировкаНалогообложенияНДСПартийТоваров.Формы.ФормаСписка.ПолноеИмя());
	НоваяСтрока.ДействиеПодробнее.ОткрыватьВместоЖР = Истина;
	
КонецПроцедуры

// Процедура определяет необходимость выполнения этапа.
//
// Параметры:
//  ПараметрыОбработчика - Структура - параметры обработчика события этапа.
//
Процедура Использование_ПереносОстатковПодНовыйВидДеятельности(ПараметрыОбработчика) Экспорт
	
	ОрганизацииСОстаткамиПоСебестоимости = РасчетСебестоимостиПрикладныеАлгоритмы.ОрганизацииСОстаткамиПоСебестоимости(
		ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации, ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОрганизаций", ОрганизацииСОстаткамиПоСебестоимости);
	//Запрос.УстановитьПараметр("ДатаАнализаНалогообложения", НачалоМесяца(ДобавитьМесяц(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации, 1)));
	Запрос.УстановитьПараметр("Период", НачалоМесяца(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации));
	МассивТекстовЗапроса = Новый Массив;
	МассивТекстовЗапроса.Добавить(ТекстЗапросаОрганизацииСоСменойНалогообложения());
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Данные.Организация КАК Организация,
	|	Данные.ТекущееНалогообложение КАК ТекущееНалогообложение,
	|	Данные.СтароеНалогообложение КАК СтароеНалогообложение,
	|	Данные.НовоеНалогообложение КАК НовоеНалогообложение,
	|	Данные.СтатьяРасходовНеНДС КАК СтатьяРасходовНеНДС,
	|	ЕСТЬNULL(КорНДС.Проведен, ЛОЖЬ) КАК ЕстьПроведенныйДокументПереноса
	|ИЗ
	|	ОрганизацииНаОблагаемойДеятельности КАК Данные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаНалогообложенияНДСПартийТоваров КАК КорНДС
	|		ПО Данные.Организация = КорНДС.Организация
	|			И (КорНДС.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносОстатковПодНовыйВидДеятельности))
	|			И (НАЧАЛОПЕРИОДА(КорНДС.Дата, МЕСЯЦ) = &Период)
	|ГДЕ
	|	Данные.ТекущееНалогообложение <> Данные.СтароеНалогообложение
	|	И Данные.НовоеНалогообложение <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|	ИЛИ Данные.ТекущееНалогообложение <> Данные.НовоеНалогообложение
	|	И Данные.НовоеНалогообложение = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)";
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ТекстПодробнее = НСтр("ru = 'Подробнее';
							|en = 'Details'", ОбщегоНазначения.КодОсновногоЯзыка());
	ДействиеПодробнее = ЗакрытиеМесяцаСервер.ОписаниеДействия_ОткрытьФорму(
			Метаданные.Документы.КорректировкаНалогообложенияНДСПартийТоваров.Формы.ФормаСписка.ПолноеИмя());
	ШаблонПояснения = НСтр("ru = 'Смена налогообложения организации ""%1"" с ""%2"" на ""%3""';
							|en = 'Change of taxation of the %1 company from %2 to %3'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	Пока Выборка.Следующий() Цикл
		
		ТекстПояснения = "";
		
		Если Выборка.СтароеНалогообложение <> Выборка.ТекущееНалогообложение Тогда
			
			ТекстПояснения = СтрШаблон(ШаблонПояснения, Выборка.Организация, Выборка.СтароеНалогообложение, Выборка.ТекущееНалогообложение);
				
		КонецЕсли;
			
		Если Выборка.НовоеНалогообложение <> Выборка.ТекущееНалогообложение Тогда
			
			ТекстПояснения = СтрШаблон(ШаблонПояснения, Выборка.Организация, Выборка.ТекущееНалогообложение, Выборка.НовоеНалогообложение);
				
		КонецЕсли;
			
		Если Выборка.НовоеНалогообложение = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС И
			Не ЗначениеЗаполнено(Выборка.СтатьяРасходовНеНДС) И Выборка.ЕстьПроведенныйДокументПереноса Тогда
			
			ТекстПодробнееОшибки = НСтр("ru = 'Установить';
										|en = 'Set'", ОбщегоНазначения.КодОсновногоЯзыка());
		
			ТекстПоясненияОшибки = СтрШаблон(
				НСтр("ru = 'Организация ""%1"" в следующем периоде не будет являться плательщиком НДС. Необходимо указать статью списания НДС на необлагаемую деятельность.';
					|en = 'The %1 company will not be a VAT payer in the next period. Specify the VAT write-off item for non-taxable activities.'",
				ОбщегоНазначения.КодОсновногоЯзыка()), Выборка.Организация);
			
			ДействиеПодробнееОшибки = ЗакрытиеМесяцаСервер.ОписаниеДействия_ОткрытьФорму(
				Метаданные.РегистрыСведений.НастройкиУчетаНДС.Формы.ФормаЗаписиРФ.ПолноеИмя());
			ДействиеПодробнееОшибки.ПараметрыФормы.Вставить("Организация", Выборка.Организация);
			ЗакрытиеМесяцаСервер.УстановитьСостояниеВыполненСОшибками(ПараметрыОбработчика, ТекстПоясненияОшибки, ТекстПодробнееОшибки, ДействиеПодробнееОшибки);
				
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекстПояснения) И Не Выборка.ЕстьПроведенныйДокументПереноса Тогда
			
			ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(ПараметрыОбработчика, ТекстПояснения, ТекстПодробнее, ДействиеПодробнее);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТекстПояснения = Неопределено Тогда
		
		ПараметрыОбработчика.ДанныеЭтапа.Состояние = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура определяет действия выполнения этапа.
//
// Параметры:
//  ПараметрыОбработчика - Структура - параметры обработчика события этапа.
//
Процедура Выполнить_ПереносОстатковПодНовыйВидДеятельности(ПараметрыОбработчика) Экспорт
	
	ОрганизацииСОстаткамиПоСебестоимости = РасчетСебестоимостиПрикладныеАлгоритмы.ОрганизацииСОстаткамиПоСебестоимости(
		ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации, ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОрганизаций", ОрганизацииСОстаткамиПоСебестоимости);
	Запрос.УстановитьПараметр("Период", НачалоМесяца(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации));
	МассивТекстовЗапроса = Новый Массив;
	МассивТекстовЗапроса.Добавить(ТекстЗапросаОрганизацииСоСменойНалогообложения());
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Данные.Организация КАК Организация,
	|	Данные.ТекущееНалогообложение,
	|	Данные.СтароеНалогообложение,
	|	Данные.НовоеНалогообложение,
	|	КорНДС.Ссылка КАК Ссылка
	|ИЗ
	|	ОрганизацииНаОблагаемойДеятельности КАК Данные
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаНалогообложенияНДСПартийТоваров КАК КорНДС
	|	ПО
	|		Данные.Организация = КорНДС.Организация
	|		И КорНДС.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносОстатковПодНовыйВидДеятельности)
	|		И НАЧАЛОПЕРИОДА(КорНДС.Дата, Месяц) = &Период
	|		И НЕ КорНДС.ПометкаУдаления
	|ГДЕ
	|	Данные.ТекущееНалогообложение <> Данные.СтароеНалогообложение
	|	И Данные.НовоеНалогообложение <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|	ИЛИ Данные.ТекущееНалогообложение <> Данные.НовоеНалогообложение
	|	И Данные.НовоеНалогообложение = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)";
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Корректировка = ?(ЗначениеЗаполнено(Выборка.Ссылка), Выборка.Ссылка.ПолучитьОбъект(),
				Документы.КорректировкаНалогообложенияНДСПартийТоваров.СоздатьДокумент());
		
		ДатаДокумента = ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации;
		Корректировка.Дата = ?(Выборка.НовоеНалогообложение = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС,
			КонецМесяца(ДатаДокумента), НачалоМесяца(ДатаДокумента));
		Корректировка.Организация = Выборка.Организация;
		Корректировка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПереносОстатковПодНовыйВидДеятельности;
		Корректировка.ПеремещениеПодДеятельность = ?(Выборка.ТекущееНалогообложение <> Выборка.СтароеНалогообложение,
			Выборка.ТекущееНалогообложение, Выборка.НовоеНалогообложение);
		Корректировка.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПроводкиРеглУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстыОтражения = Новый Массив;
	
	//++ НЕ УТ
	ТекстыОтражения.Добавить(ТекстВключениеИсключениеНДСВСтоимостьТовара());
	//-- НЕ УТ
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса создания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат ""
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область ПереносОстатковПодНовыйВидДеятельности

// Возвращает исходное налогообложение организации на основании того, на какое налогообложение осуществляется перенос.
//
//	Параметры:
//		Организация - СправочникСсылка.Организации - организация по которой осуществляется перенос;
//		Дата - Дата - дата переноса;
//		ПеремещениеПодДеятельность - ПеречислениеСсылка.ТипыНалогообложенияНДС - налогообложение на которое осуществляется перенос.
//
//	Возвращаемое значение:
//		ПеречислениеСсылка.ТипыНалогообложенияНДС - исходное налогообложение, с которого будет осуществляться перенос.
//
Функция ИсходноеОсновноеНалогообложениеОрганизации(Организация, Дата, ПеремещениеПодДеятельность) Экспорт
	
	// Определим старый вид деятельности:
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаОрганизацииСоСменойНалогообложения();
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПОМЕСТИТЬ ОрганизацииНаОблагаемойДеятельности", "");
	Запрос.УстановитьПараметр("МассивОрганизаций", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация));
	Запрос.УстановитьПараметр("Период", НачалоМесяца(Дата));
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Если ПеремещениеПодДеятельность = Выборка.ТекущееНалогообложение Тогда
		// Налогообложение изменилось в период ввода документа, значит старое налогообложение определяется в прошлом периоде.
		ИсходныйВидДеятельности = Выборка.СтароеНалогообложение;
	Иначе
		// Налогообложение меняется в следующем периоде, значит старое налогообложение - текущее налогообложение организации.
		ИсходныйВидДеятельности = Выборка.ТекущееНалогообложение;
	КонецЕсли;
	
	Возврат ИсходныйВидДеятельности;

КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Дата");
	Поля.Добавить("Организация");
	Поля.Добавить("ХозяйственнаяОперация");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если Данные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПереносОстатковПодНовыйВидДеятельности Тогда
	
		СтандартнаяОбработка = Ложь;
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 ""%2"" за %3';
				|en = '%1 ""%2"" for %3'"),
			Данные.ХозяйственнаяОперация,
			Данные.Организация,
			Формат(Данные.Дата, "ДФ='ММММ гггг ""г.""'"));
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Корректировка.Ссылка,
	|	Корректировка.Дата,
	|	Корректировка.Организация,
	|	Корректировка.ПеремещениеПодДеятельность,
	|	Корректировка.Номер КАК Номер,
	|	Корректировка.Комментарий КАК Комментарий,
	|	Корректировка.Проведен КАК Проведен,
	|	Корректировка.ПометкаУдаления КАК ПометкаУдаления,
	|	ВЫБОР
	|		КОГДА Корректировка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносОстатковПодНовыйВидДеятельности)
	|		ТОГДА Корректировка.ХозяйственнаяОперация
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеТоваров)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	Корректировка.ХозяйственнаяОперация КАК ХозОперацияДляРеестра
	|ИЗ
	|	Документ.КорректировкаНалогообложенияНДСПартийТоваров КАК Корректировка
	|ГДЕ
	|	Корректировка.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	НачалоСледНалПериода = (КонецКвартала(Реквизиты.Дата) + 1);
	
	Запрос.УстановитьПараметр("Период",						Реквизиты.Дата);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",		Реквизиты.ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("ПоФактИспользованию",		Перечисления.ТипыНалогообложенияНДС.ПоФактическомуИспользованию);
	Запрос.УстановитьПараметр("Организация",				Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПеремещениеПодДеятельность",	Реквизиты.ПеремещениеПодДеятельность);
	Запрос.УстановитьПараметр("НачПериода",					ДобавитьМесяц(НачалоСледНалПериода, -36));
	Запрос.УстановитьПараметр("КонПериода",					КонецКвартала(ДобавитьМесяц(НачалоСледНалПериода, -36)));
	Запрос.УстановитьПараметр("ИдентификаторМетаданных",	ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.КорректировкаНалогообложенияНДСПартийТоваров"));
	Запрос.УстановитьПараметр("Номер",						Реквизиты.Номер);
	Запрос.УстановитьПараметр("НомерНаПечать",				ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	Запрос.УстановитьПараметр("Комментарий",				Реквизиты.Комментарий);
	Запрос.УстановитьПараметр("Проведен",					Реквизиты.Проведен);
	Запрос.УстановитьПараметр("ПометкаУдаления",			Реквизиты.ПометкаУдаления);
	Запрос.УстановитьПараметр("ХозОперацияДляРеестра",		Реквизиты.ХозОперацияДляРеестра);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
КонецПроцедуры

Функция ТекстЗапросаВтВидыЗапасов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтВидыЗапасов";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			ТОГДА ВидыЗапасов.ВидЗапасов
	|		ИНАЧЕ ВидыЗапасов.ВидЗапасовПолучателя
	|	КОНЕЦ КАК ВидЗапасовПолучателя,
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад,
	|	ВидыЗапасов.ДокументПоступления КАК ДокументПоступления,
	|	ВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ВидыЗапасов.Стоимость КАК Стоимость,
	|	ВидыЗапасов.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|	ВидыЗапасов.СтоимостьРегл КАК СтоимостьРегл,
	|	ВидыЗапасов.НДСРегл КАК НДСРегл,
	|	ВидыЗапасов.ПостояннаяРазница КАК ПостояннаяРазница,
	|	ВидыЗапасов.ВременнаяРазница КАК ВременнаяРазница,
	|	ВидыЗапасов.Количество КАК Количество,
	|	ВидыЗапасов.Количество КАК КоличествоПоРНПТ
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	Документ.КорректировкаНалогообложенияНДСПартийТоваров.ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ТоварыОрганизаций";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтВидыЗапасов", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВидыЗапасов(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	1													КАК Порядок,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)				КАК ВидДвижения,
	|	&Период												КАК Период,
	|	&Организация										КАК Организация,
	|	&Организация										КАК ОрганизацияОтгрузки,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.Номенклатура						КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика					КАК Характеристика,
	|	ТаблицаВидыЗапасов.ВидЗапасов						КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.Количество						КАК Количество,
	|	ТаблицаВидыЗапасов.КоличествоПоРНПТ					КАК КоличествоПоРНПТ,
	|	ТаблицаВидыЗапасов.НомерГТД							КАК НомерГТД,
	|	&ХозяйственнаяОперация								КАК ХозяйственнаяОперация,
	|	&ПеремещениеПодДеятельность							КАК НалогообложениеНДС,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры		КАК КорАналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасовПолучателя				КАК КорВидЗапасов
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	НЕ &ПартионныйУчетВерсии22
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2													КАК Порядок,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)				КАК ВидДвижения,
	|	&Период												КАК Период,
	|	&Организация										КАК Организация,
	|	&Организация										КАК ОрганизацияОтгрузки,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.Номенклатура						КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика					КАК Характеристика,
	|	ТаблицаВидыЗапасов.ВидЗапасовПолучателя				КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.Количество						КАК Количество,
	|	ТаблицаВидыЗапасов.КоличествоПоРНПТ					КАК КоличествоПоРНПТ,
	|	ТаблицаВидыЗапасов.НомерГТД							КАК НомерГТД,
	|	&ХозяйственнаяОперация								КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО										КАК НалогообложениеНДС,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры		КАК КорАналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов						КАК КорВидЗапасов
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	НЕ &ПартионныйУчетВерсии22
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&ИдентификаторМетаданных КАК ТипСсылки,
	|	&ХозОперацияДляРеестра КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Партнер,
	|	НЕОПРЕДЕЛЕНО КАК МестоХранения,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	&Период КАК ДатаДокументаИБ,
	|	&Ссылка КАК Ссылка,
	|	&Номер КАК НомерДокументаИБ,
	|	НЕОПРЕДЕЛЕНО КАК Статус,
	|	НЕОПРЕДЕЛЕНО КАК Ответственный,
	|	НЕОПРЕДЕЛЕНО КАК Автор,
	|	ЛОЖЬ КАК ДополнительнаяЗапись,
	|	НЕОПРЕДЕЛЕНО КАК Дополнительно,
	|	&Комментарий КАК Комментарий,
	|	&Проведен КАК Проведен,
	|	&ПометкаУдаления КАК ПометкаУдаления,
	|	&Период КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать КАК НомерПервичногоДокумента,
	|	0 КАК Сумма,
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ЛОЖЬ  КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО КАК ИсправляемыйДокумент,
	|	&Период КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО КАК Приоритет";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.КорректировкаНалогообложенияНДСПартийТоваров";
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.КорректировкаНалогообложенияНДСПартийТоваров"));
	ЗначенияПараметров.Вставить("ХозОперацияДляРеестра", Перечисления.ХозяйственныеОперации.КорректировкаНалогообложенияНДСПартийТоваров);
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента, Ложь);
	Иначе
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента);
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

#Область ПартионныйУчет

Функция ОписаниеРегистровУчетаЗатратИСебестоимости(Документ) Экспорт
	
	ОписаниеРегистров = Новый Массив;
	ОписаниеРегистров.Добавить(Метаданные.РегистрыНакопления.СебестоимостьТоваров);
	
	Возврат ОписаниеРегистров;
	
КонецФункции

Функция УстановитьДополнительныеПараметрыОперацийУчетаЗатратИСебестоимости(Документ, Запрос = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Массив;
	
	Если Запрос <> Неопределено Тогда
		// Нет дополнительных параметров.
	КонецЕсли;
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

Функция СформироватьДополнительныеТаблицыОперацийУчетаЗатратИСебестоимости(Документ, Запрос = Неопределено, ТекстыЗапроса = Неопределено) Экспорт
	
	ДополнительныеТаблицы = Новый Массив;
	ДополнительныеТаблицы.Добавить("ВтВидыЗапасов");
	
	Если Запрос <> Неопределено Тогда
	
		Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса(ДополнительныеТаблицы[0], ТекстыЗапроса) Тогда
			ТекстЗапросаВтВидыЗапасов(Запрос, ТекстыЗапроса);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДополнительныеТаблицы;
	
КонецФункции

Функция ОписаниеОперацийУчетаСебестоимости(Документ) Экспорт
	
	ОписаниеОпераций = Новый Массив;
	
	#Область Перемещение_Товар
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	ТаблицаДокумента.Дата 	КАК Период,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО 			КАК КорректируемыйДокумент,
	|
	// Поля учета номенклатуры
	|	ТаблицаДокумента.Организация 					 КАК Организация,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры 	 КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов					 КАК ВидЗапасов,
	|	ТаблицаДокумента.ПеремещениеПодДеятельность		 КАК ВидДеятельностиНДС,
	|	ТаблицаДокумента.ПеремещениеПодДеятельность	 	 КАК ВидДеятельностиНДСДокумента,
	|
	// Корреспондирующие поля
	|	НЕОПРЕДЕЛЕНО                                     КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО 									 КАК КорПартия,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры	 КАК КорАналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасовПолучателя          КАК КорВидЗапасов,
	|
	// Поля аналитики финансового учета
	|	НЕОПРЕДЕЛЕНО 						КАК Сделка,
	|	НЕОПРЕДЕЛЕНО 						КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО						КАК Менеджер,
	|	НЕОПРЕДЕЛЕНО 						КАК ГруппаПродукции,
	|
	// Количественные и суммовые показатели
	|	ТаблицаВидыЗапасов.Количество 			КАК Количество,
	|	НЕОПРЕДЕЛЕНО 							КАК ИдентификаторСтроки,
	|
	// Прочие поля
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносОстатковПодНовыйВидДеятельности)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеТоваров)
	|		ИНАЧЕ ТаблицаДокумента.ХозяйственнаяОперация
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносОстатковПодНовыйВидДеятельности)
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ &ИдентификаторНеиспользуемойФинЗаписи
	|	КОНЕЦ КАК ИдентификаторФинЗаписи,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносОстатковПодНовыйВидДеятельности)
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПереносОстатковПодНовыйВидДеятельности)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.КорректировкаНалогообложенияНДСПартийТоваров КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ПО ИСТИНА
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|";
	
	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.Перемещение,
		ТекстЗапроса);
		
	#КонецОбласти
	
	Возврат ОписаниеОпераций;
	
КонецФункции

#КонецОбласти

Функция ТекстЗапросаОрганизацииСоСменойНалогообложения()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НастройкиУчетаНДС.ПрименяетсяОсвобождениеОтУплатыНДС, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		КОГДА НЕ НастройкиУчетаНДСПриУСН.Организация ЕСТЬ NULL
	|			ТОГДА НастройкиУчетаНДСПриУСН.ОсновноеНалогообложение
	|		КОГДА ЕСТЬNULL(НастройкиСистемыНалогообложения.СистемаНалогообложения, ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		КОГДА ЕСТЬNULL(НастройкиСистемыНалогообложения.СистемаНалогообложения, ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		КОГДА ЕСТЬNULL(НастройкиУчетаНДС.ПлательщикНДС, НЕОПРЕДЕЛЕНО) = ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		КОГДА ЕСТЬNULL(НастройкиУчетаНДС.ПлательщикНДС, НЕОПРЕДЕЛЕНО) = ЛОЖЬ
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		КОГДА ИспользоватьУчетНДС.Значение = ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|	КОНЕЦ КАК ТекущееНалогообложение,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НастройкиУчетаНДС_Старые.ПрименяетсяОсвобождениеОтУплатыНДС, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		КОГДА НЕ НастройкиУчетаНДСПриУСН_Старые.Организация ЕСТЬ NULL
	|			ТОГДА НастройкиУчетаНДСПриУСН_Старые.ОсновноеНалогообложение
	|		КОГДА ЕСТЬNULL(НастройкиСистемыНалогообложения_Старые.СистемаНалогообложения, ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		КОГДА ЕСТЬNULL(НастройкиСистемыНалогообложения_Старые.СистемаНалогообложения, ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		КОГДА ЕСТЬNULL(НастройкиУчетаНДС_Старые.ПлательщикНДС, НЕОПРЕДЕЛЕНО) = ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		КОГДА ЕСТЬNULL(НастройкиУчетаНДС_Старые.ПлательщикНДС, НЕОПРЕДЕЛЕНО) = ЛОЖЬ
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		КОГДА ИспользоватьУчетНДС.Значение = ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|	КОНЕЦ КАК СтароеНалогообложение,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НастройкиУчетаНДС_Новые.ПрименяетсяОсвобождениеОтУплатыНДС, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		КОГДА НЕ НастройкиУчетаНДСПриУСН_Новые.Организация ЕСТЬ NULL
	|			ТОГДА НастройкиУчетаНДСПриУСН_Новые.ОсновноеНалогообложение
	|		КОГДА ЕСТЬNULL(НастройкиСистемыНалогообложения_Новые.СистемаНалогообложения, ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		КОГДА ЕСТЬNULL(НастройкиСистемыНалогообложения_Новые.СистемаНалогообложения, ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		КОГДА ЕСТЬNULL(НастройкиУчетаНДС_Новые.ПлательщикНДС, НЕОПРЕДЕЛЕНО) = ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		КОГДА ЕСТЬNULL(НастройкиУчетаНДС_Новые.ПлательщикНДС, НЕОПРЕДЕЛЕНО) = ЛОЖЬ
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		КОГДА ИспользоватьУчетНДС.Значение = ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|	КОНЕЦ КАК НовоеНалогообложение,
	|	НастройкиУчетаНДС.СтатьяРасходовНеНДС КАК СтатьяРасходовНеНДС
	|ПОМЕСТИТЬ ОрганизацииНаОблагаемойДеятельности
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.ИспользоватьУчетНДС КАК ИспользоватьУчетНДС
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНДС.СрезПоследних(ДОБАВИТЬКДАТЕ(&Период, МЕСЯЦ, -1), Организация В (&МассивОрганизаций)) КАК НастройкиУчетаНДС_Старые
	|		ПО Организации.Ссылка = НастройкиУчетаНДС_Старые.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(ДОБАВИТЬКДАТЕ(&Период, МЕСЯЦ, -1), Организация В (&МассивОрганизаций)) КАК НастройкиСистемыНалогообложения_Старые
	|		ПО Организации.Ссылка = НастройкиСистемыНалогообложения_Старые.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНДСПриУСН.СрезПоследних(ДОБАВИТЬКДАТЕ(&Период, МЕСЯЦ, -1), Организация В (&МассивОрганизаций)) КАК НастройкиУчетаНДСПриУСН_Старые
	|		ПО Организации.Ссылка = НастройкиУчетаНДСПриУСН_Старые.Организация
	|			И (ЕСТЬNULL(НастройкиСистемыНалогообложения_Старые.СистемаНалогообложения, ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНДС.СрезПоследних(&Период, Организация В (&МассивОрганизаций)) КАК НастройкиУчетаНДС
	|		ПО Организации.Ссылка = НастройкиУчетаНДС.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&Период, Организация В (&МассивОрганизаций)) КАК НастройкиСистемыНалогообложения
	|		ПО Организации.Ссылка = НастройкиСистемыНалогообложения.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНДСПриУСН.СрезПоследних(&Период, Организация В (&МассивОрганизаций)) КАК НастройкиУчетаНДСПриУСН
	|		ПО Организации.Ссылка = НастройкиУчетаНДСПриУСН.Организация
	|			И (ЕСТЬNULL(НастройкиСистемыНалогообложения.СистемаНалогообложения, ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНДС.СрезПоследних(ДОБАВИТЬКДАТЕ(&Период, МЕСЯЦ, 1), Организация В (&МассивОрганизаций)) КАК НастройкиУчетаНДС_Новые
	|		ПО Организации.Ссылка = НастройкиУчетаНДС_Новые.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(ДОБАВИТЬКДАТЕ(&Период, МЕСЯЦ, 1), Организация В (&МассивОрганизаций)) КАК НастройкиСистемыНалогообложения_Новые
	|		ПО Организации.Ссылка = НастройкиСистемыНалогообложения_Новые.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНДСПриУСН.СрезПоследних(ДОБАВИТЬКДАТЕ(&Период, МЕСЯЦ, 1), Организация В (&МассивОрганизаций)) КАК НастройкиУчетаНДСПриУСН_Новые
	|		ПО Организации.Ссылка = НастройкиУчетаНДСПриУСН_Новые.Организация
	|			И (ЕСТЬNULL(НастройкиСистемыНалогообложения_Новые.СистемаНалогообложения, ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная))
	|ГДЕ
	|	Организации.Ссылка В(&МассивОрганизаций)
	|	И ВЫБОР
	|		КОГДА ЕСТЬNULL(НастройкиУчетаНДС.ПрименяетсяОсвобождениеОтУплатыНДС, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		КОГДА НЕ НастройкиУчетаНДСПриУСН.Организация ЕСТЬ NULL
	|			ТОГДА НастройкиУчетаНДСПриУСН.ОсновноеНалогообложение
	|		КОГДА ЕСТЬNULL(НастройкиСистемыНалогообложения.СистемаНалогообложения, ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		КОГДА ЕСТЬNULL(НастройкиСистемыНалогообложения.СистемаНалогообложения, ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		КОГДА ЕСТЬNULL(НастройкиУчетаНДС.ПлательщикНДС, НЕОПРЕДЕЛЕНО) = ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		КОГДА ЕСТЬNULL(НастройкиУчетаНДС.ПлательщикНДС, НЕОПРЕДЕЛЕНО) = ЛОЖЬ
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		КОГДА ИспользоватьУчетНДС.Значение = ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|	КОНЕЦ = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

//++ НЕ УТ
#Область ПроводкиРеглУчета

Функция ТекстВключениеИсключениеНДСВСтоимостьТовара()
	
	ТекстЗапроса =
	"ВЫБРАТЬ // Включение/исключение НДС в стоимость товара на складе - получателе (Дт 41.01 :: Кт 19.03) @НДСПредъявленный
	|	Партии.Ссылка КАК Ссылка,
	|	Партии.Период КАК Период,
	|	Партии.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.НДСРегл
	|	ИНАЧЕ
	|		-Партии.НДСРегл
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.НДСУпр
	|	ИНАЧЕ
	|		-Партии.НДСУпр
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ВЫБОР
	|		КОГДА Партии.Склад.ЦеховаяКладовая
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	ВЫБОР
	|		КОГДА Партии.Склад.ЦеховаяКладовая
	|		ТОГДА Партии.ГруппаПродукции
	|		ИНАЧЕ Партии.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК АналитикаУчетаДт,
	|	ВЫБОР
	|		КОГДА Партии.Склад.ЦеховаяКладовая
	|		ТОГДА Партии.Подразделение
	|		ИНАЧЕ Партии.Склад
	|	КОНЕЦ КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Партии.Подразделение КАК ПодразделениеДт,
	|	Партии.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Партии.Номенклатура КАК СубконтоДт1,
	|	ВЫБОР
	|		КОГДА Партии.Склад.ЦеховаяКладовая
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты)
	|		ИНАЧЕ Партии.Склад
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА Партии.Склад.ЦеховаяКладовая
	|		ТОГДА Партии.ГруппаПродукции
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСпоПриобретеннымЦенностям) КАК ВидСчетаКт,
	|	Партии.ВидДеятельностиНДС                                              КАК АналитикаУчетаКт,
	|	Партии.ВидЦенности                                                     КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ВЫБОР
	|		КОГДА Партии.ВключениеНДСВСтоимость
	|		ТОГДА ЕСТЬNULL(ВводСобственныхСредств.НаправлениеДеятельности, Партии.НаправлениеДеятельности)
	|		ИНАЧЕ ЕСТЬNULL(КорВводСобственныхСредств.НаправлениеДеятельности, Партии.КорНаправлениеДеятельности)
	|	КОНЕЦ КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Партии.Контрагент КАК СубконтоКт1,
	|	Партии.ДокументПоступления КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Включение/исключение НДС в стоимость товара"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаНалогообложенияНДСПартийТоваров КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|		И Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносОстатковПодНовыйВидДеятельности)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Партии КАК Партии
	|	ПО
	|		Партии.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТВводСобственныхСредств КАК ВводСобственныхСредств
	|	ПО Партии.Ссылка = ВводСобственныхСредств.Ссылка
	|		И Партии.ДокументПоступления = ВводСобственныхСредств.СчетФактура
	|		И Партии.НаправлениеДеятельности = ВводСобственныхСредств.ИсходноеНаправлениеДеятельности
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТВводСобственныхСредств КАК КорВводСобственныхСредств
	|	ПО Партии.Ссылка = КорВводСобственныхСредств.Ссылка
	|		И Партии.ДокументПоступления = КорВводСобственныхСредств.СчетФактура
	|		И Партии.КорНаправлениеДеятельности = КорВводСобственныхСредств.ИсходноеНаправлениеДеятельности
	|
	|ГДЕ
	|	(Партии.ВключениеНДСВСтоимость ИЛИ Партии.ИсключениеНДСИзСтоимости)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

#КонецОбласти

#КонецОбласти

#КонецЕсли
