#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ДляВсехСтрок( ЗначениеРазрешено(Сотрудники.ФизическоеЛицо, NULL КАК ИСТИНА)
	|	) И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	ВоинскийУчет.ДляОтбораПоОрганизации(Настройки);
	ВоинскийУчет.ДляОбъектаСПрисоединеннымиФайлами(Настройки);
КонецПроцедуры

#КонецОбласти

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.СведенияОПодлежащихВоинскомуУчетуГражданах;
	ОписаниеСоставаОбъекта = ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	ОписаниеСоставаОбъекта.ЗаполнятьФизическиеЛицаПоСотрудникам = Ложь;
	Возврат ОписаниеСоставаОбъекта;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Идентификатор = "ПФ_MXL_СписокСостоящихНаВоинскомУчетеГраждан";
	КомандаПечати.Представление = НСтр("ru = 'Список состоящих на воинском учете граждан';
										|en = 'Список состоящих на воинском учете граждан'");
	КомандаПечати.Порядок = 10;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	
КонецПроцедуры

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ОшибкиПечати          - Список значений  - Ошибки печати  (значение - ссылка на объект, представление - текст
//                           ошибки).
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя
//                           области в которой был выведен объект).
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_СписокСостоящихНаВоинскомУчетеГраждан") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ПФ_MXL_СписокСостоящихНаВоинскомУчетеГраждан", 
			НСтр("ru = 'Список состоящих на воинском учете граждан';
				|en = 'Список состоящих на воинском учете граждан'"), 
			ПечатнаяФормаСпискаСостоящихНаВоинскомУчетеГраждан(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;					
						
КонецПроцедуры								

Функция ПечатнаяФормаСпискаСостоящихНаВоинскомУчетеГраждан(МассивОбъектов, ОбъектыПечати)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СведенияОПодлежащихВоинскомуУчетуГражданах";
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ВыборкаПоШапкеДокумента = ЗапросПоШапкеДляПечати(МассивОбъектов).Выбрать();
	ВыборкаПоРаботникам = ЗапросПоСотрудникамДляПечати(МассивОбъектов).Выбрать();
	
	Пока ВыборкаПоШапкеДокумента.СледующийПоЗначениюПоля("Ссылка") Цикл 
		
		Макет 		  = УправлениеПечатью.МакетПечатнойФормы("Отчет.ВоинскийУчетОбщий.ПФ_MXL_СписокГражданПодлежащихПостановкеНаВоинскийУчет2017");
		Заголовок 	  = Макет.ПолучитьОбласть("Заголовок");
		Шапка 		  = Макет.ПолучитьОбласть("Шапка");
		СтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
		Подвал 		  = Макет.ПолучитьОбласть("Подвал");
		
		ВыборкаПоРаботникам.Сбросить();
		
		Если ВыборкаПоРаботникам.НайтиСледующий(Новый Структура("Ссылка", ВыборкаПоШапкеДокумента.Ссылка)) Тогда
			
			ВыборкаПоРаботникам.СледующийПоЗначениюПоля("Ссылка");
			
			Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("Военкомат") Цикл 
		
				НачалоФормы = ДокументРезультат.ВысотаТаблицы + 1;
				
				ЗаполнитьЗначенияСвойств(Заголовок.Параметры, ВыборкаПоРаботникам);
				ЗаполнитьЗначенияСвойств(Заголовок.Параметры, ВыборкаПоШапкеДокумента);
				
				ДокументРезультат.Вывести(Заголовок);
				ДокументРезультат.Вывести(Шапка);
				
				НомерСтроки = 1;
				
				Пока ВыборкаПоРаботникам.Следующий() Цикл
					
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы.Параметры, ВыборкаПоРаботникам);
					СтрокаТаблицы.Параметры.ФИОПолные = ВыборкаПоРаботникам.Фамилия + " " + ВыборкаПоРаботникам.Имя + " " + ВыборкаПоРаботникам.Отчество;
					СтрокаТаблицы.Параметры.ВидОбразования = ВыборкаПоРаботникам.Образование;
					СтрокаТаблицы.Параметры.СостояниеВБраке = ВыборкаПоРаботникам.СемейноеПоложение;
					СтрокаТаблицы.Параметры.ДатаМестоРождения = Формат(ВыборкаПоРаботникам.ДатаРождения, "ДЛФ=Д") + ?(ЗначениеЗаполнено(ВыборкаПоРаботникам.МестоРождения), ", " + ВыборкаПоРаботникам.МестоРождения, "");
					СтрокаТаблицы.Параметры.СистемныеПоляНомерПоПорядкуВГруппировке = НомерСтроки;
					
					Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, СтрокаТаблицы) Тогда
						ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					
					ДокументРезультат.Вывести(СтрокаТаблицы);
					НомерСтроки = НомерСтроки + 1;
					
				КонецЦикла;
			
				Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, Подвал) Тогда
					ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(Подвал.Параметры, ВыборкаПоШапкеДокумента);
				ДокументРезультат.Вывести(Подвал);
				
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
				УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, НачалоФормы, ОбъектыПечати, ВыборкаПоШапкеДокумента.Ссылка);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДокументРезультат;
	
КонецФункции

Функция ЗапросПоШапкеДляПечати(МассивСсылок)
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СведенияОПодлежащихВоинскомуУчетуГражданах.Дата КАК Дата,
		|	СведенияОПодлежащихВоинскомуУчетуГражданах.Номер КАК Номер,
		|	СведенияОПодлежащихВоинскомуУчетуГражданах.НомерПриказа КАК НомерПриказа,
		|	СведенияОПодлежащихВоинскомуУчетуГражданах.Организация КАК Организация,
		|	СведенияОПодлежащихВоинскомуУчетуГражданах.ФактическийАдресОрганизации КАК ФактическийАдресОрганизации,
		|	СведенияОПодлежащихВоинскомуУчетуГражданах.Ссылка КАК Ссылка,
		|	СведенияОПодлежащихВоинскомуУчетуГражданах.Руководитель КАК Руководитель,
		|	СведенияОПодлежащихВоинскомуУчетуГражданах.ДолжностьРуководителя КАК ДолжностьРуководителя,
		|	СведенияОПодлежащихВоинскомуУчетуГражданах.ОтветственныйЗаВУР КАК ОтветственныйЗаВУР,
		|	СведенияОПодлежащихВоинскомуУчетуГражданах.ДолжностьОтветственногоЗаВУР КАК ДолжностьОтветственногоЗаВУР
		|ПОМЕСТИТЬ ВТДанныеДокументов
		|ИЗ
		|	Документ.СведенияОПодлежащихВоинскомуУчетуГражданах КАК СведенияОПодлежащихВоинскомуУчетуГражданах
		|ГДЕ
		|	СведенияОПодлежащихВоинскомуУчетуГражданах.Ссылка В(&МассивСсылок)";
	
	Запрос.Выполнить();
	
	ИменаПолейОтветственныхЛиц = Новый Массив;
	ИменаПолейОтветственныхЛиц.Добавить("Руководитель");
	ИменаПолейОтветственныхЛиц.Добавить("ОтветственныйЗаВУР");
	ЗарплатаКадры.СоздатьВТФИООтветственныхЛиц(Запрос.МенеджерВременныхТаблиц, Истина, ИменаПолейОтветственныхЛиц, "ВТДанныеДокументов");
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДокументов.ОтветственныйЗаВУР КАК ОтветственныйЗаВУР
		|ИЗ
		|	ВТДанныеДокументов КАК ДанныеДокументов";
	
	МассивФизическихЛиц = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ОтветственныйЗаВУР");
	
	РабочиеТелефоныПодписантов = КонтактнаяИнформацияБЗК.КонтактнаяИнформацияОбъектов(
		МассивФизическихЛиц, , Справочники.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица);
		
	РабочиеТелефоны = Новый ТаблицаЗначений;
	РабочиеТелефоны.Колонки.Добавить("Объект", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	РабочиеТелефоны.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(20)));
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РабочиеТелефоныПодписантов, РабочиеТелефоны);
	
	Запрос.УстановитьПараметр("РабочиеТелефоны", РабочиеТелефоны);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РабочиеТелефоны.Объект КАК ОтветственныйЗаВУР,
		|	РабочиеТелефоны.Представление КАК Телефон
		|ПОМЕСТИТЬ ВТРабочиеТелефоны
		|ИЗ
		|	&РабочиеТелефоны КАК РабочиеТелефоны
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеДокументов.Ссылка КАК Ссылка,
		|	ДанныеДокументов.Дата КАК Дата,
		|	ДанныеДокументов.Номер КАК Номер,
		|	ДанныеДокументов.НомерПриказа КАК НомерПриказа,
		|	ДанныеДокументов.Организация КАК Организация,
		|	ДанныеДокументов.ФактическийАдресОрганизации КАК ФактАдресОрганизации,
		|	ДанныеДокументов.Руководитель КАК Руководитель,
		|	ДанныеДокументов.ДолжностьРуководителя КАК ДолжностьРуководителя,
		|	ДанныеДокументов.ОтветственныйЗаВУР КАК ОтветственныйЗаВУР,
		|	ДанныеДокументов.ДолжностьОтветственногоЗаВУР КАК ДолжностьОтветственногоЗаВУР,
		|	ВЫБОР
		|		КОГДА ПОДСТРОКА(Организации.НаименованиеПолное, 1, 10) <> """"
		|			ТОГДА Организации.НаименованиеПолное
		|		ИНАЧЕ Организации.Наименование
		|	КОНЕЦ КАК ПолноеНаименованиеОрганизации,
		|	Организации.НаименованиеПолное КАК НаименованиеПолное,
		|	Организации.НаименованиеСокращенное КАК НаименованиеСокращенное,
		|	ФИОРуководителя.РасшифровкаПодписи КАК РуководительРасшифровкаПодписи,
		|	ФИООтветственногоЗаВУР.РасшифровкаПодписи КАК ОтветственныйЗаВУРРасшифровкаПодписи,
		|	РабочиеТелефоны.Телефон КАК ТелефонОтветственногоЗаВУР
		|ИЗ
		|	ВТДанныеДокументов КАК ДанныеДокументов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО ДанныеДокументов.Организация = Организации.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИОРуководителя
		|		ПО ДанныеДокументов.Руководитель = ФИОРуководителя.ФизическоеЛицо
		|			И ДанныеДокументов.Ссылка = ФИОРуководителя.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИООтветственногоЗаВУР
		|		ПО ДанныеДокументов.ОтветственныйЗаВУР = ФИООтветственногоЗаВУР.ФизическоеЛицо
		|			И ДанныеДокументов.Ссылка = ФИООтветственногоЗаВУР.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРабочиеТелефоны КАК РабочиеТелефоны
		|		ПО ДанныеДокументов.ОтветственныйЗаВУР = РабочиеТелефоны.ОтветственныйЗаВУР
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДанныеДокументов.Дата,
		|	Ссылка";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапросПоСотрудникамДляПечати(МассивСсылок)
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Сотрудники.Ссылка КАК Ссылка,
		|	Сотрудники.НомерСтроки КАК НомерСтроки,
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Сотрудники.Военкомат КАК Военкомат,
		|	Сотрудники.Военкомат.Наименование КАК НаименованиеВоенкомата,
		|	ВЫБОР
		|		КОГДА Сотрудники.Военкомат.НаименованиеДляЭлектронногоОбмена = """"
		|			ТОГДА Сотрудники.Военкомат.Наименование
		|		ИНАЧЕ Сотрудники.Военкомат.НаименованиеДляЭлектронногоОбмена
		|	КОНЕЦ КАК ВоенкоматНаименованиеДляЭлектронногоОбмена,
		|	Сотрудники.Военкомат.Адрес КАК АдресВоенкомата,
		|	Сотрудники.Фамилия КАК Фамилия,
		|	Сотрудники.Имя КАК Имя,
		|	Сотрудники.Отчество КАК Отчество,
		|	Сотрудники.СтраховойНомерСФР КАК СтраховойНомерСФР,
		|	Сотрудники.ДатаРождения КАК ДатаРождения,
		|	Сотрудники.Пол КАК Пол,
		|	Сотрудники.СерияПаспорта КАК СерияПаспорта,
		|	Сотрудники.НомерПаспорта КАК НомерПаспорта,
		|	Сотрудники.ДатаВыдачиПаспорта КАК ДатаВыдачиПаспорта,
		|	Сотрудники.ДатаРегистрацииПоМестуЖительства КАК ДатаРегистрацииПоМестуЖительства,
		|	Сотрудники.АдресРегистрацииПоМестуЖительства КАК АдресРегистрацииПоМестуЖительства,
		|	Сотрудники.ДатаРегистрацииПоМестуПребывания КАК ДатаРегистрацииПоМестуПребывания,
		|	Сотрудники.АдресРегистрацииПоМестуПребывания КАК АдресРегистрацииПоМестуПребывания,
		|	Сотрудники.ДатаФактическогоПроживания КАК ДатаФактическогоПроживания,
		|	Сотрудники.АдресФактическогоПроживания КАК АдресФактическогоПроживания,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(Сотрудники.АдресРегистрацииПоМестуПребывания, """") = """"
		|			ТОГДА Сотрудники.АдресРегистрацииПоМестуЖительства
		|		ИНАЧЕ Сотрудники.АдресРегистрацииПоМестуПребывания
		|	КОНЕЦ КАК АдресМестаПроживанияПредставление,
		|	Сотрудники.Должность КАК Должность,
		|	Сотрудники.Звание КАК Звание,
		|	Сотрудники.НомерКомандыПартии КАК НомерКомандыПартии,
		|	Сотрудники.Забронирован КАК Забронирован,
		|	Сотрудники.КатегорияЗапаса КАК КатегорияЗапаса,
		|	Сотрудники.Состав КАК Состав,
		|	Сотрудники.ВУС КАК ВУС,
		|	Сотрудники.Годность КАК Годность,
		|	ВЫБОР
		|		КОГДА НЕ Сотрудники.Забронирован
		|			ТОГДА ""общий"" + ВЫБОР
		|					КОГДА Сотрудники.НомерКомандыПартии <> """"
		|						ТОГДА "" ("" + Сотрудники.НомерКомандыПартии + "")""
		|					ИНАЧЕ """"
		|				КОНЕЦ
		|		ИНАЧЕ ""спец. учет""
		|	КОНЕЦ КАК ВидВоинскогоУчета,
		|	Сотрудники.Подразделение КАК Подразделение,
		|	Сотрудники.Образование КАК Образование,
		|	Сотрудники.СемейноеПоложение КАК СемейноеПоложение,
		|	Сотрудники.МестоРождения КАК МестоРождения
		|ИЗ
		|	Документ.СведенияОПодлежащихВоинскомуУчетуГражданах.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&МассивСсылок)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	Военкомат,
		|	НомерСтроки";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапрашиваемыеКадровыеДанныеСотрудников() Экспорт

	ЗапрашиваемыеДанные = "Фамилия, Имя, Отчество, СтраховойНомерПФР, Пол, ДатаРождения, Должность,
		|ДокументСерия, ДокументНомер, ДокументДатаВыдачи, ВоинскийУчетВоенкомат, 
		|ВоинскийУчетЗвание, ВоинскийУчетНомерКомандыПартии, ВоинскийУчетЗабронированОрганизацией,
		|ВоинскийУчетКатегорияЗапаса, ВоинскийУчетСостав, ВоинскийУчетВУС, ВоинскийУчетГодность,
		|АдресПоПропискеПредставление, АдресМестаПроживанияПредставление, АдресДляИнформированияПредставление, 
		|ДатаРегистрации, ДатаВременнойРегистрации, ДатаФактическогоПроживания,
		|Подразделение, ВидОбразования, СостояниеВБраке, МестоРождения";
	
	Возврат ЗапрашиваемыеДанные;
	
КонецФункции

Функция КадровыеДанныеСотрудников(ДанныеСотрудников) Экспорт
	
	ЗапрашиваемыеДанные = ЗапрашиваемыеКадровыеДанныеСотрудников();
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудниковПоТаблицеЗначений(Истина, ДанныеСотрудников, ЗапрашиваемыеДанные);
	
	КадровыеДанные.Колонки.Добавить("Образование", Новый ОписаниеТипов("ПеречислениеСсылка.ОбразованиеГражданДляВоенкомата"));
	КадровыеДанные.Колонки.Добавить("СемейноеПоложение", Новый ОписаниеТипов("ПеречислениеСсылка.СемейноеПоложениеГражданДляВоенкомата"));
	
	ОбразованиеГраждан = ВоинскийУчет.ОбразованиеГражданДляВоенкомата();
	СемейноеПоложениеГраждан = ВоинскийУчет.СемейноеПоложениеГражданДляВоенкомата();
	
	Для Каждого КадровыеДанныеСотрудника Из КадровыеДанные Цикл
		КадровыеДанныеСотрудника.Образование = ОбразованиеГраждан[КадровыеДанныеСотрудника.ВидОбразования];
		КадровыеДанныеСотрудника.СемейноеПоложение = СемейноеПоложениеГраждан[КадровыеДанныеСотрудника.СостояниеВБраке];
		Если Не ЗначениеЗаполнено(КадровыеДанныеСотрудника.СемейноеПоложение) Тогда 
			КадровыеДанныеСотрудника.СемейноеПоложение = Перечисления.СемейноеПоложениеГражданДляВоенкомата.ОтсутствуютСведенияОСемейномПоложении;
		КонецЕсли;
	КонецЦикла;
	
	Возврат КадровыеДанные;
	
КонецФункции

Процедура ЗаполнитьДанныеСтрокиСотрудника(СтрокаСотрудника, КадровыеДанные, ДатаДокумента) Экспорт 
	
	ЗаполнитьЗначенияСвойств(СтрокаСотрудника, КадровыеДанные, , "ДатаФактическогоПроживания");
	
	СтрокаСотрудника.Военкомат = КадровыеДанные.ВоинскийУчетВоенкомат; 
	СтрокаСотрудника.СтраховойНомерСФР = КадровыеДанные.СтраховойНомерПФР;
	СтрокаСотрудника.СерияПаспорта = СтрЗаменить(КадровыеДанные.ДокументСерия, " ", "");
	СтрокаСотрудника.НомерПаспорта = СтрЗаменить(КадровыеДанные.ДокументНомер, " ", "");
	СтрокаСотрудника.ДатаВыдачиПаспорта = КадровыеДанные.ДокументДатаВыдачи;
	СтрокаСотрудника.ДатаРегистрацииПоМестуЖительства = КадровыеДанные.ДатаРегистрации;
	СтрокаСотрудника.АдресРегистрацииПоМестуЖительства = КадровыеДанные.АдресПоПропискеПредставление;
	
	Если Не ПолучитьФункциональнуюОпцию("ЗаполнятьТолькоАдресПоПропискеВСведенияхДляВоенкоматов") Тогда 
		СтрокаСотрудника.ДатаРегистрацииПоМестуПребывания = КадровыеДанные.ДатаВременнойРегистрации;
		СтрокаСотрудника.АдресРегистрацииПоМестуПребывания = КадровыеДанные.АдресМестаПроживанияПредставление;
		СтрокаСотрудника.ДатаФактическогоПроживания = КадровыеДанные.ДатаФактическогоПроживания;
		СтрокаСотрудника.АдресФактическогоПроживания = КадровыеДанные.АдресДляИнформированияПредставление;
	КонецЕсли;
	
	СтрокаСотрудника.Звание = КадровыеДанные.ВоинскийУчетЗвание;
	СтрокаСотрудника.НомерКомандыПартии = КадровыеДанные.ВоинскийУчетНомерКомандыПартии;
	СтрокаСотрудника.Забронирован = ЗначениеЗаполнено(КадровыеДанные.ВоинскийУчетЗабронированОрганизацией);
	СтрокаСотрудника.КатегорияЗапаса = КадровыеДанные.ВоинскийУчетКатегорияЗапаса;
	СтрокаСотрудника.Состав = КадровыеДанные.ВоинскийУчетСостав;
	СтрокаСотрудника.ВУС = КадровыеДанные.ВоинскийУчетВУС;
	СтрокаСотрудника.Годность = КадровыеДанные.ВоинскийУчетГодность;
	СтрокаСотрудника.МестоРождения = ВоинскийУчет.ПредставлениеМестаРождения(КадровыеДанные.МестоРождения);
	
КонецПроцедуры

Процедура ОбработкаФормированияФайла(Объект) Экспорт
	
	ТекстФайла = СформироватьВыходнойФайл(Объект.Ссылка);
	ВоинскийУчет.ЗаписатьФайлОтчетности(Объект.Ссылка, Объект.ИмяФайлаОтчетности, ТекстФайла, "UTF-8");
	
КонецПроцедуры

Функция СформироватьВыходнойФайл(Ссылка)
	
	СписокДокументов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка);
	ВыборкаПоШапкеДокумента = ЗапросПоШапкеДляПечати(СписокДокументов).Выбрать();
	ВыборкаПоРаботникам = ЗапросПоСотрудникамДляПечати(СписокДокументов).Выбрать();
	
	ВыборкаПоШапкеДокумента.Следующий();
	
	ТекстФайла = "Наименование военного комиссариата;Фамилия;Имя;Отчество (при наличии);Дата рождения;Пол;Серия паспорта;Номер паспорта;Дата выдачи паспорта;СНИЛС;Образование;Дата регистрации по месту жительства;Адрес регистрации по месту жительства;Дата регистрации по месту пребывания;Адрес регистрации по месту пребывания;Дата фактического проживания;Адрес фактического проживания;Семейное положение;Структурное подразделение организации;Должность (профессия)" + Символы.ПС;
	
	Пока ВыборкаПоРаботникам.Следующий() Цикл 
		ДанныеСтроки = Новый Массив;
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.ВоенкоматНаименованиеДляЭлектронногоОбмена);
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.Фамилия);
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.Имя);
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.Отчество);
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаРождения, "ДЛФ=Д"));
		ДанныеСтроки.Добавить(Строка(ВыборкаПоРаботникам.Пол));
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.СерияПаспорта);
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.НомерПаспорта);
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаВыдачиПаспорта, "ДЛФ=Д"));
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.СтраховойНомерСФР);
		ДанныеСтроки.Добавить(Строка(ВыборкаПоРаботникам.Образование)); 
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаРегистрацииПоМестуЖительства, "ДЛФ=Д"));
		ДанныеСтроки.Добавить(ВоинскийУчет.ФорматАдресаДляЭлектронногоОбмена(ВыборкаПоРаботникам.АдресРегистрацииПоМестуЖительства));
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаРегистрацииПоМестуПребывания, "ДЛФ=Д"));
		ДанныеСтроки.Добавить(ВоинскийУчет.ФорматАдресаДляЭлектронногоОбмена(ВыборкаПоРаботникам.АдресРегистрацииПоМестуПребывания));
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаФактическогоПроживания, "ДЛФ=Д"));
		ДанныеСтроки.Добавить(ВоинскийУчет.ФорматАдресаДляЭлектронногоОбмена(ВыборкаПоРаботникам.АдресФактическогоПроживания));
		ДанныеСтроки.Добавить(Строка(ВыборкаПоРаботникам.СемейноеПоложение));
		ДанныеСтроки.Добавить(Строка(ВыборкаПоРаботникам.Подразделение));
		ДанныеСтроки.Добавить(Строка(ВыборкаПоРаботникам.Должность));
		ТекстФайла = ТекстФайла + СтрСоединить(ДанныеСтроки, ";") + Символы.ПС;
	КонецЦикла;
	
	Возврат ТекстФайла;
		
КонецФункции

#КонецОбласти

#КонецЕсли