#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ИнтеграцияВЕТИСПереопределяемый.ОбработкаЗаполненияДокумента(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
	МассивПродукции = Товары.Выгрузить(,"Продукция").ВыгрузитьКолонку("Продукция");
	СоответствиеТипЖивыеЖивотные = ИнтеграцияВЕТИСВызовСервера.ПродукцияПринадлежитТипуЖивыеЖивотные(МассивПродукции);
	Для каждого СтрокаТабличнойЧасти Из Товары Цикл
		Если СоответствиеТипЖивыеЖивотные[СтрокаТабличнойЧасти.Продукция] = Истина Тогда
			ИнтеграцияВЕТИСКлиентСервер.СброситьЗначенияПоСтрокеСЖивымиЖивотными(СтрокаТабличнойЧасти);
		КонецЕсли;
	КонецЦикла;
	
	Документы.ПроизводственнаяОперацияВЕТИС.ЗаполнитьДанныеЭкспертизыПоСтатистикеПользователя(ЭтотОбъект);
	Документы.ПроизводственнаяОперацияВЕТИС.ЗаполнитьСрокиГодностиПоСтатистикеПродукции(ЭтотОбъект);
	Документы.ПроизводственнаяОперацияВЕТИС.ЗаполнитьУпаковкиПоСтатистикеПродукции(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа
	ИнтеграцияИС.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	// Инициализация данных документа
	Документы.ПроизводственнаяОперацияВЕТИС.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ИнтеграцияИС.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ИнтеграцияИСПереопределяемый.ОтразитьДвиженияСерийТоваров(ДополнительныеСвойства, Движения, Отказ);
	ИнтеграцияВЕТИС.ЗагрузитьТаблицыДвижений(ДополнительныеСвойства, Движения, "ДвиженияСерийТоваров");
	
	ИнтеграцияИС.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ИнтеграцияИСПереопределяемый.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);
	
	ИнтеграцияИС.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ВыполнятьПроверкиРеквизитовВетИС = Истина;
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ТекущееСостояние = РегистрыСведений.СтатусыДокументовВЕТИС.ТекущееСостояние(Ссылка);
		КонечныеСтатусы = Документы.ПроизводственнаяОперацияВЕТИС.КонечныеСтатусы(Ложь);
		Если КонечныеСтатусы.Найти(ТекущееСостояние.Статус) <> Неопределено Тогда
			ВыполнятьПроверкиРеквизитовВетИС = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ВыполнятьПроверкиРеквизитовВетИС Тогда
		
		ПроверитьЗаполнениеТабличнойЧастиТовары(Отказ);
		
		ПроверитьСоответствиеПродукцииСырья(Отказ);
		
		ПроверитьЗаполнениеТабличнойЧастиТехнологическийПроцесс(Отказ);
		
		Если Товары.Итог("КоличествоВЕТИС") = 0
			И Сырье.Итог("КоличествоВЕТИС") = 0
			И Не ЗавершениеПроизводственнойТранзакции Тогда
			
			ТекстСообщения = НСтр("ru = 'Количество продукции и сырья нулевое, нет данных для передачи.';
									|en = 'Количество продукции и сырья нулевое, нет данных для передачи.'");
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,,,
				Отказ);
			
		КонецЕсли;
		
	Иначе
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.ИдентификаторПартии");
		МассивНепроверяемыхРеквизитов.Добавить("Сырье.КоличествоВЕТИС");
		
	КонецЕсли;
	
	ИнтеграцияВЕТИСПереопределяемый.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Идентификатор) Тогда
		Идентификатор = Новый УникальныйИдентификатор;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ИнтеграцияВЕТИС.ЗаписатьСоответствиеНоменклатуры(ЭтотОбъект, РежимЗаписи);
	ИнтеграцияВЕТИС.ЗаписатьСоответствиеНоменклатуры(ЭтотОбъект, РежимЗаписи, "Сырье");
	
	ИнтеграцияВЕТИС.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияВЕТИС.ЗаписатьСтатусДокументаВЕТИСПоУмолчанию(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДокументОснование = Неопределено;
	Идентификатор     = "";
	
	Если Товары.Количество() > 0 Тогда
		Товары.ЗагрузитьКолонку(Новый Массив(Товары.Количество()), "ЗаписьСкладскогоЖурнала");
		Товары.ЗагрузитьКолонку(Новый Массив(Товары.Количество()), "ВетеринарноСопроводительныйДокумент");
		
		ПереносИдентификаторовСтрок = Новый Соответствие;
		Для Каждого СтрокаТовары Из Товары Цикл
			Ид = Строка(Новый УникальныйИдентификатор);
			ПереносИдентификаторовСтрок.Вставить(СтрокаТовары.ИдентификаторСтроки, Ид);
			СтрокаТовары.ИдентификаторСтроки = Ид;
		КонецЦикла;
		Для Каждого СтрокаУпаковка Из УпаковкиВЕТИС Цикл
			СтрокаУпаковка.ИдентификаторСтрокиТовары = ПереносИдентификаторовСтрок.Получить(СтрокаУпаковка.ИдентификаторСтрокиТовары);
		КонецЦикла;
		
	КонецЕсли;
	
	Если Сырье.Количество() > 0 Тогда
		Сырье.ЗагрузитьКолонку(Новый Массив(Сырье.Количество()), "ЗаписьСкладскогоЖурнала");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьЗаполнениеТабличнойЧастиТовары(Отказ)
	
	ЗаполнениеДокументовВЕТИС.ПроверитьЗаполнениеДатыПроизводстваСрокаГодности(ЭтотОбъект, "Товары", Отказ);
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеТабличнойЧастиТехнологическийПроцесс(Отказ)
	
	ШаблонСообщения = НСтр("ru = 'Дата окончания операции не должна быть меньше даты начала (строка %1, список ""Технологический процесс"")';
							|en = 'Дата окончания операции не должна быть меньше даты начала (строка %1, список ""Технологический процесс"")'");
	
	Для каждого Строка Из ТехнологическийПроцесс Цикл
		Если ЗначениеЗаполнено(Строка.ДатаОперацииНачалоПериода)
			И ЗначениеЗаполнено(Строка.ДатаОперацииКонецПериода)
			И Строка.ДатаОперацииНачалоПериода > Строка.ДатаОперацииКонецПериода Тогда
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТехнологическийПроцесс", Строка.НомерСтроки, "ДатаОперацииКонецПериода");
			
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(ШаблонСообщения, Строка.НомерСтроки),
				ЭтотОбъект, 
				Поле,,
				Отказ);
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьСоответствиеПродукцииСырья(Отказ)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Т.Продукция КАК Продукция,
	|	Т.Сырье КАК Сырье
	|ПОМЕСТИТЬ ВТТаблицаСоответствия
	|ИЗ
	|	&ТаблицаСоответствия КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.НомерСтроки КАК НомерСтроки,
	|	Т.Продукция КАК Продукция
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	&Товары КАК Т
	|ГДЕ
	|	НЕ Т.Продукция = ЗНАЧЕНИЕ(Справочник.ПродукцияВЕТИС.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПродукцияВЕТИС.ТипПродукции КАК ТипПродукции,
	|	ПродукцияВЕТИС.ВидПродукции КАК ВидПродукции
	|ПОМЕСТИТЬ ВТСырье
	|ИЗ
	|	Справочник.ПродукцияВЕТИС КАК ПродукцияВЕТИС
	|ГДЕ
	|	ПродукцияВЕТИС.Ссылка В(&Сырье)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТТовары.НомерСтроки КАК НомерСтроки,
	|	ПродукцияВЕТИС.ТипПродукции.Представление КАК ТипПродукцииПредставление,
	|	ВТСырье.ТипПродукции.Представление КАК ТипСырьяПредставление
	|ИЗ
	|	ВТТовары КАК ВТТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСырье КАК ВТСырье
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПродукцияВЕТИС КАК ПродукцияВЕТИС
	|		ПО ВТТовары.Продукция = ПродукцияВЕТИС.Ссылка
	|ГДЕ
	|	НЕ((ПродукцияВЕТИС.ТипПродукции, ВТСырье.ТипПродукции) В
	|					(ВЫБРАТЬ
	|						ВТТаблицаСоответствия.Продукция КАК Продукция,
	|						ВТТаблицаСоответствия.Сырье КАК Сырье
	|					ИЗ
	|						ВТТаблицаСоответствия КАК ВТТаблицаСоответствия)
	|				ИЛИ (ПродукцияВЕТИС.ВидПродукции, ВТСырье.ВидПродукции) В
	|					(ВЫБРАТЬ
	|						ВТТаблицаСоответствия.Продукция КАК Продукция,
	|						ВТТаблицаСоответствия.Сырье КАК Сырье
	|					ИЗ
	|						ВТТаблицаСоответствия КАК ВТТаблицаСоответствия)
	|				ИЛИ (ПродукцияВЕТИС.ВидПродукции, ВТСырье.ТипПродукции) В
	|					(ВЫБРАТЬ
	|						ВТТаблицаСоответствия.Продукция КАК Продукция,
	|						ВТТаблицаСоответствия.Сырье КАК Сырье
	|					ИЗ
	|						ВТТаблицаСоответствия КАК ВТТаблицаСоответствия))
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Товары", Товары);
	Запрос.УстановитьПараметр("Сырье" , Сырье.ВыгрузитьКолонку("Продукция"));
	Запрос.УстановитьПараметр("ТаблицаСоответствия", ТаблицаСоответствияПродукцииСырья());
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		ШаблонСообщения = НСтр("ru = 'Продукция с типом ""%1"" не может производиться из сырья с типом ""%2"" (Строка %3)';
								|en = 'Продукция с типом ""%1"" не может производиться из сырья с типом ""%2"" (Строка %3)'");
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки, "Продукция");
			
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(ШаблонСообщения, Выборка.ТипПродукцииПредставление, Выборка.ТипСырьяПредставление, Выборка.НомерСтроки),
				ЭтотОбъект,
				Поле,,
				Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ТаблицаСоответствияПродукцииСырья()
	
	ТипПродукцияВЕТИС = Новый ОписаниеТипов("СправочникСсылка.ПродукцияВЕТИС");
	ТипИдентификатор = Метаданные.ОпределяемыеТипы.УникальныйИдентификаторИС.Тип;
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Продукция",	ТипПродукцияВЕТИС);
	Результат.Колонки.Добавить("Сырье",		ТипПродукцияВЕТИС);
	
	ТаблицаСоответствияПоИдентификаторам = Новый ТаблицаЗначений;
	ТаблицаСоответствияПоИдентификаторам.Колонки.Добавить("ИдентификаторПродукции",	ТипИдентификатор);
	ТаблицаСоответствияПоИдентификаторам.Колонки.Добавить("ИдентификаторСырья",		ТипИдентификатор);
	
	Макет = Документы.ПроизводственнаяОперацияВЕТИС.ПолучитьМакет("СоответствияПродукцииСырья");
	
	КоличествоСтрок = Макет.ВысотаТаблицы;
	
	Для НомерСтроки = 2 По КоличествоСтрок Цикл
		ИдентификаторПродукции	= СокрЛП(Макет.Область("R" + НомерСтроки + "C2").Текст);
		ИдентификаторСырья		= СокрЛП(Макет.Область("R" + НомерСтроки + "C4").Текст);
		
		НоваяСтрока = ТаблицаСоответствияПоИдентификаторам.Добавить();
		НоваяСтрока.ИдентификаторПродукции	= ИдентификаторПродукции;
		НоваяСтрока.ИдентификаторСырья		= ИдентификаторСырья;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Соответствия.ИдентификаторПродукции КАК ИдентификаторПродукции,
	|	Соответствия.ИдентификаторСырья КАК ИдентификаторСырья
	|ПОМЕСТИТЬ ВТСоответствияПоИдентификаторам
	|ИЗ
	|	&ТаблицаСоответствияПоИдентификаторам КАК Соответствия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторПродукции,
	|	ИдентификаторСырья
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТипыПродукции.Продукция КАК Продукция,
	|	ТипыПродукции.КодТипа КАК КодТипа
	|ПОМЕСТИТЬ ВТТипыПродукции
	|ИЗ
	|	&ТаблицаТипыПродукции КАК ТипыПродукции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСоответствияПоИдентификаторам.ИдентификаторПродукции КАК ИдентификаторПродукции,
	|	ВТСоответствияПоИдентификаторам.ИдентификаторСырья КАК ИдентификаторСырья,
	|	ЕСТЬNULL(ВТТипыПродукции.Продукция, ПродукцияВЕТИС.Ссылка) КАК Продукция,
	|	ЕСТЬNULL(ВТТипыСырья.Продукция, СырьеВЕТИС.Ссылка) КАК Сырье
	|ИЗ
	|	ВТСоответствияПоИдентификаторам КАК ВТСоответствияПоИдентификаторам
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПродукцияВЕТИС КАК ПродукцияВЕТИС
	|		ПО ВТСоответствияПоИдентификаторам.ИдентификаторПродукции = ПродукцияВЕТИС.Идентификатор
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПродукцияВЕТИС КАК СырьеВЕТИС
	|		ПО ВТСоответствияПоИдентификаторам.ИдентификаторСырья = СырьеВЕТИС.Идентификатор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТипыПродукции КАК ВТТипыПродукции
	|		ПО ВТСоответствияПоИдентификаторам.ИдентификаторПродукции = ВТТипыПродукции.КодТипа
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТипыПродукции КАК ВТТипыСырья
	|		ПО ВТСоответствияПоИдентификаторам.ИдентификаторСырья = ВТТипыСырья.КодТипа
	|ГДЕ
	|	НЕ ЕСТЬNULL(ВТТипыПродукции.Продукция, ПродукцияВЕТИС.Ссылка) ЕСТЬ NULL
	|	И НЕ ЕСТЬNULL(ВТТипыСырья.Продукция, СырьеВЕТИС.Ссылка) ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ТаблицаСоответствияПоИдентификаторам", ТаблицаСоответствияПоИдентификаторам);
	Запрос.УстановитьПараметр("ТаблицаТипыПродукции", ТаблицаТипыПродукции());
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрокаПродукцияСырье(Результат, Выборка.Продукция, Выборка.Сырье);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ТаблицаТипыПродукции()
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Продукция", Новый ОписаниеТипов("СправочникСсылка.ПродукцияВЕТИС"));
	Результат.Колонки.Добавить("КодТипа", Метаданные.ОпределяемыеТипы.УникальныйИдентификаторИС.Тип);
	
	ТипыПродукции = ПродукцияВЕТИСВызовСервера.ТипыПродукции();
	Для Каждого ТипПродукции Из ТипыПродукции Цикл
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.КодТипа = ТипПродукции.Ключ;
		НоваяСтрока.Продукция = ТипПродукции.Значение;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции


Процедура НоваяСтрокаПродукцияСырье(Таблица, Продукция, Сырье)
	
	НоваяСтрока = Таблица.Добавить();
	
	НоваяСтрока.Продукция = Продукция;
	НоваяСтрока.Сырье     = Сырье;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
