
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ВызватьИсключение НСтр("ru = 'Документ не предназначен для интерактивного ввода.';
								|en = 'The document is not designed for interactive input.'");
	КонецЕсли;
	
	ЕстьПравоАдминистрирования = ОбщегоНазначенияБЭД.ЕстьПравоАдминистрирования();
	
	Если РаботаСПрочтениямиВызовСервера.УстановитьСвойствоПрочтен(Объект.Ссылка, Истина) Тогда
		ОповеститьОПрочтении = Истина;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаПанельКоманд;
	ПараметрыРазмещения.Источники = Новый Массив;
	Для Каждого ДанныеОбъектаУчета Из ОбъектыУчетаДокумента Цикл
		ПараметрыРазмещения.Источники.Добавить(ДанныеОбъектаУчета.ОбъектУчета.Метаданные());
	КонецЦикла;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОбменСКонтрагентамиПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Параметры, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект);

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	ТипДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидДокумента, "ТипДокумента");

	ОбновитьДанныеДляПросмотраДокумента();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОбменСКонтрагентамиПереопределяемый.ПриЧтенииНаСервереФормыПросмотраЭлектронногоДокумента(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОжидатьПолученияАсинхронныхДанных();

	Если ОповеститьОПрочтении Тогда
		Оповестить("ПрочтениеОбновитьСписок", Объект.Ссылка);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияСостоянияЭДО()
		И Параметр <> Неопределено Тогда
		
		ОбработатьОповещение = Истина;
		
		ДанныеПроверки = ИнтерфейсДокументовЭДОКлиент.НовыеДанныеПроверкиОповещения();
		ДанныеПроверки.ЭлектронныйДокумент = Объект.Ссылка;
		
		ИнтерфейсДокументовЭДОКлиент.ПриОбработкеОповещенияФормыПросмотраЭД(ДанныеПроверки, Параметр, 
			ОбработатьОповещение);
		
		Если ОбработатьОповещение Тогда
			ОбновитьДанныеПослеВыполненияДействийПоЭДО();
		КонецЕсли;
		
		Если Не ОбработатьОповещение
			И ЛегкийИнтерфейсДокументовЭДОКлиент.ТребуетсяОбновлениеСвязанныхДокументовПриИзмененииСостоянияЭДО(
			ЭтотОбъект, Параметр) Тогда
			ОбновитьДанныеПослеИзмененияСвязанныхДокументов();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияСостоянияЭДО()
		И ЛегкийИнтерфейсДокументовЭДОКлиент.ТребуетсяОбновлениеСвязанныхДокументовПриИзмененииСостоянияЭДО(
			ЭтотОбъект, Параметр) Тогда
		
		ОбновитьДанныеПослеИзмененияСвязанныхДокументов();
		
	ИначеЕсли ИмяСобытия = "ЗакрытьФормуДлительнойОперации" Тогда
			
		ЛегкийИнтерфейсДокументовЭДОКлиент.НастроитьБлокировкуПанелиКомандКарточкиДокумента(ЭтотОбъект, Ложь);
		
	ИначеЕсли ИмяСобытия = МашиночитаемыеДоверенностиКлиент.ИмяСобытияИсправлениеОшибкиПроверкиПолномочий() Тогда
		
		Для Каждого ИсправленнаяОшибка Из Параметр Цикл
			Если ИсправленнаяОшибка.СсылкаНаОбъект = Объект.Ссылка Тогда
				ОбновитьДанныеПослеИзмененияПодписейДокумента();
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ИмяСобытия = ЛегкийИнтерфейсДокументовЭДОКлиент.ИмяСобытияИзменениеОбщегоСостоянияДокумента()
		И Параметр = Объект.Ссылка Тогда
			
		ОбновитьДанныеПослеИзмененияОбщегоСостояния();
	
	ИначеЕсли ИмяСобытия = ЛегкийИнтерфейсДокументовЭДОКлиент.ИмяСобытияИзменениеСвязанныхДокументов()
		И ЛегкийИнтерфейсДокументовЭДОКлиент.ТребуетсяОбновлениеСвязанныхДокументовПриИзмененииСвязанныхДокументов(
			ЭтотОбъект, Параметр) Тогда
		
		ОбновитьДанныеПослеИзмененияСвязанныхДокументов();
		
	КонецЕсли;
	
	ОбменСКонтрагентамиКлиентПереопределяемый.ОбработкаОповещения(
		ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЗаписи, "ИзменениеВидаДокумента", Ложь) Тогда
		ЭлектронныеДокументыЭДО.ПриИзмененииВидаДокумента(ТекущийОбъект.Ссылка,
			ТекущийОбъект.ВидДокумента, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ОбменСКонтрагентамиПереопределяемый.ПослеЗаписиНаСервереФормыПросмотраЭлектронногоДокумента(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ОбменСКонтрагентамиКлиентПереопределяемый.ОбработкаНавигационнойСсылкиФормыПросмотраЭлектронногоДокумента(
		ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НадписьОшибкаФормированияПредставленияОбработкаНавигационнойСсылки(Элемент,
	НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьЖурналРегистрации" Тогда
		ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьЖурналРегистрацииПоОшибкамПредставленияИзКарточкиДокумента(
			ЭтотОбъект, СтандартнаяОбработка);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовШапкиФормы

#Область ОбработчикиСобытийЭлементовТаблицыФормыСоставПакета

&НаКлиенте
Процедура СоставПакетаПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("СоставПакетаПослеАктивизацииСтроки", 0.1, Истина);
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыСоставПакета

#Область ОбработчикиСобытийЭлементовТаблицыФормыПодписи

&НаКлиенте
Процедура ПодписиПриАктивизацииСтроки(Элемент)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОбработатьАктивациюСтрокиВТаблицеПодписейКарточкиДокумента(ЭтотОбъект, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПодписиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗакрытияРезультатаПроверкиПодписи", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОбработатьВыборВТаблицеПодписейИзКарточкиДокумента(ЭтотОбъект, Элемент, Поле, 
		СтандартнаяОбработка, ОповещениеОЗавершении);
		
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыПодписи

#Область ОбработчикиКомандФормы

#Область ОбработчикиКомандФормыОсновные

#Область ВыполнениеДействийПоЭДО

&НаКлиенте
Процедура Утвердить(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеУтвердитьИзКарточкиДокумента(ЭтотОбъект, ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьОтправить(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействияПодписатьОтправитьИзКарточкиДокумента(ЭтотОбъект, 
		ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура Подписать(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеПодписатьИзКарточкиДокумента(ЭтотОбъект, ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура Отправить(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтправитьИзКарточкиДокумента(ЭтотОбъект, ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура Отклонить(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьИзКарточкиДокумента(ЭтотОбъект, ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПовторно(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтправитьПовторноИзКарточкиДокумента(ЭтотОбъект,
		ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура ПринятьАннулирование(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеПринятьАннулированиеИзКарточкиДокумента(ЭтотОбъект, 
		ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьАннулирование(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьАннулированиеИзКарточкиДокумента(ЭтотОбъект, 
		ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура Аннулировать(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеАннулироватьИзКарточкиДокумента(ЭтотОбъект, ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура Завершить(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеЗакрытьПринудительноИзКарточкиДокумента(ЭтотОбъект,
		ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура ВернутьВРаботу(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеВернутьВРаботуИзКарточкиДокумента(ЭтотОбъект, 
		ЭтоПакетДокументов);
КонецПроцедуры

#КонецОбласти // ВыполнениеДействийПоЭДО

#Область ВыгрузкаИПечать

&НаКлиенте
Процедура НапечататьЭлектронныеДокументы(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьФормуПечатиИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДокументооборотЦеликом(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыгрузитьДокументооборотЦеликомИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЭлектронныеДокументыВФорматеPDF(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыгрузитьДокументыВФорматеPDFИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЭлектронныеДокументыДляФНС(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыгрузитьДокументыДляФНСИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПротоколОбмена(Команда)
	ИнтерфейсДокументовЭДОКлиент.ПоказатьПротоколОбмена(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка));
КонецПроцедуры

#КонецОбласти // ВыгрузкаИПечать

#КонецОбласти // ОбработчикиКомандФормыОсновные

#Область ОбработчикиКомандФормыЕще

#Область ВыполнениеДействийПоЭДОСДокументом

&НаКлиенте
Процедура УтвердитьДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеУтвердитьИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьОтправитьДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействияПодписатьОтправитьИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеПодписатьИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтправитьИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПринятьАннулированиеДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеПринятьАннулированиеИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьАннулированиеДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьАннулированиеИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура АннулироватьДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеАннулироватьИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеЗакрытьПринудительноИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВернутьВРаботуДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеВернутьВРаботуИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти // ВыполнениеДействийПоЭДОСДокументом

#Область НастройкиОтображения

&НаКлиенте
Процедура ОтображатьБанковскиеРеквизиты(Кнопка)

	ВыводитьБанковскиеРеквизиты = Не ВыводитьБанковскиеРеквизиты;
	Элементы.КнопкаОтображатьБанковскиеРеквизиты.Пометка = ВыводитьБанковскиеРеквизиты;

	ОбновитьВизуализациюДокумента();

КонецПроцедуры

&НаКлиенте
Процедура ОтображатьДополнительнуюИнформацию(Команда)

	ОтключитьВыводДопДанных = Не ОтключитьВыводДопДанных;
	Элементы.КнопкаОтображатьДополнительнуюИнформацию.Пометка = Не ОтключитьВыводДопДанных;

	ОбновитьВизуализациюДокумента();

КонецПроцедуры

&НаКлиенте
Процедура ОтображатьОбластьКопияВерна(Команда)

	ОтключитьВыводКопияВерна = Не ОтключитьВыводКопияВерна;
	Элементы.КнопкаОтображатьОбластьКопияВерна.Пометка = Не ОтключитьВыводКопияВерна;

	ОбновитьВизуализациюДокумента();

КонецПроцедуры

#КонецОбласти // НастройкиОтображения

&НаКлиенте
Процедура СменитьОтветственного(Команда)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеСменыОтветственного", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.СменитьОтветственногоИзКарточкиДокумента(ЭтотОбъект, ОповещениеОЗавершении);
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьОтветственногоПакет(Команда)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеСменыОтветственного", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.СменитьОтветственногоИзКарточкиДокумента(ЭтотОбъект, ОповещениеОЗавершении,
		Истина);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьПроизвольныйДокумент(Команда)
	ИнтерфейсДокументовЭДОКлиент.ОткрытьНовыйПроизвольныйЭлектронныйДокументНаОсновании(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодписи(Команда)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеПроверкиПодписейДокумента", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьПроверкуПодписейИзКарточкиДокумента(ЭтотОбъект, ОповещениеОЗавершении);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСертификат(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ПоказатьСертификатИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодписьПоДоверенностиВручную(Команда)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеПроверкиПодписейДокумента", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.ПроверитьПодписьПоДоверенностиВручнуюИзКарточкиДокумента(ЭтотОбъект,
		ОповещениеОЗавершении);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуЭлектронногоДокумента(Команда)
	ИнтерфейсДокументовЭДОКлиент.ОткрытьКарточкуЭлектронногоДокумента(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКонтейнерДокумента(Команда)
	
	Если Не ЗначениеЗаполнено(СообщениеЭДО) Тогда
		СообщениеЭДО = СообщениеЭДОИнформацииОтправителя(Объект.Ссылка);
	КонецЕсли;
	СинхронизацияЭДОКлиент.ОткрытьТранспортныйКонтейнерСообщения(СообщениеЭДО);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЖурналСобытийЭДО(Команда)
	ЭлектронныеДокументыЭДОКлиент.ОткрытьЖурналДействийПоЭДО(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкуЭДО(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьНастройкиОтраженияВУчетеИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеДокумент(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПометитьНаУдалениеДокументПослеОтветаНаВопрос", ЭтотОбъект);
	ОбщегоНазначенияБЭДКлиент.ЗадатьВопросНаУстановкуПометкиУдаления(Объект.ПометкаУдаления, ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормыЕще

#Область ОбработчикиКомандФормыСодержимое

&НаКлиенте
Процедура СостояниеОбработкаНажатия(Команда)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(КомментарийСостояния, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьФайлИнформацииОтправителяДокумента(
		Объект.Ссылка, Объект.ЭтоОблачныйЭДО, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВизуализацию(Команда)
	ПеречитатьКэшВизуализации = Истина;
	ОбновитьДанныеЭлектронногоДокумента();
	ПеречитатьКэшВизуализации = Ложь;
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормыСодержимое

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	ОбменСКонтрагентамиКлиентПереопределяемый.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти // ПодключаемыеКоманды

&НаКлиенте
Процедура ДополнительноеСостояниеОбработкаНажатия(Команда)
	
	Если Не ЗначениеЗаполнено(КомандыДополнительногоСостояния) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработчикКоманды = Неопределено;
	Значение          = Неопределено;
	ИдентификаторКоманды = СтрЗаменить(ТекущийЭлемент.Имя, "ДополнительноеСостояние_", "");
	Для Каждого СвязаннаяКоманда Из КомандыДополнительногоСостояния Цикл
		Если СвязаннаяКоманда.Идентификатор = ИдентификаторКоманды Тогда
			ОбработчикКоманды = СвязаннаяКоманда.Обработчик;
			Значение          = СвязаннаяКоманда.Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если Не ЗначениеЗаполнено(ОбработчикКоманды) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура("Форма", ЭтотОбъект);
	Оповещение = ОбщегоНазначенияБЭДКлиент.ОписаниеОповещенияПоПутиКОбработчику(ОбработчикКоманды, ПараметрыОповещения);
	ВыполнитьОбработкуОповещения(Оповещение, Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСвязанныйДокумент1(Команда)
	ПоказатьЗначение(, СвязанныеДокументы[0].Значение);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСвязанныйДокумент2(Команда)
	ПоказатьЗначение(, СвязанныеДокументы[1].Значение);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокСвязанныхДокументов(Команда)
	ПараметрыФормы = Новый Структура("СвязанныеДокументы", СвязанныеДокументы);
	ОткрытьФорму("ОбщаяФорма.СвязанныеДокументыБЭД", ПараметрыФормы);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСвязанныйДокумент(Команда)

	Если Не ЗначениеЗаполнено(КомандыСозданияСвязанныхДокументов) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработчикКоманды = Неопределено;
	ИдентификаторКоманды = СтрЗаменить(ТекущийЭлемент.Имя, "СоздатьСвязанныйДокумент_", "");
	Для Каждого СвязаннаяКоманда Из КомандыСозданияСвязанныхДокументов Цикл
		Если СвязаннаяКоманда.Идентификатор = ИдентификаторКоманды Тогда
			ОбработчикКоманды = СвязаннаяКоманда.Обработчик;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если Не ЗначениеЗаполнено(ОбработчикКоманды) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура("Форма", ЭтотОбъект);
	Оповещение = ОбщегоНазначенияБЭДКлиент.ОписаниеОповещенияПоПутиКОбработчику(ОбработчикКоманды, ПараметрыОповещения);
	ВыполнитьОбработкуОповещения(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьПолеПредпросмотраPDFФайлов(Команда)
	Элементы.ВключитьПолеПредпросмотраPDFФайлов.Пометка = Не Элементы.ВключитьПолеПредпросмотраPDFФайлов.Пометка;
	ВключитьПолеПредпросмотраPDFФайловНаСервере(Элементы.ВключитьПолеПредпросмотраPDFФайлов.Пометка);
	ОбновитьВизуализациюДокумента();
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеДанных

&НаСервереБезКонтекста
Функция СообщениеЭДОИнформацииОтправителя(Знач ЭлектронныйДокумент)
	Возврат ЭлектронныеДокументыЭДО.СообщениеОтправителя(ЭлектронныйДокумент);
КонецФункции

#КонецОбласти // ЗаполнениеДанных

#Область ОбновлениеДанных

&НаКлиенте
Процедура ОбновитьДанныеЭлектронногоДокумента()
	
	Прочитать();
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеДляПросмотраДокумента()
	
	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельКарточки, ПанельПометкиНаУдаление, ПанельКомандВходящегоДокументаЛегкийКлиентЭДО, ПанельПакета,
		|ПанельСостоянияЛегкийКлиентЭДО, ПанельЗакладокВходящегоДокумента, ПанельВизуализации,
		|ПанельКомандНастроекВизуализации, ПанельПредставленияФайла, ПанельПодписей,
		|ПанельСопроводительнойЗапискиВходящегоДокумента, ПанельРеквизитов, ПанельРеквизитовПакета,
		|ПанельСвязанныхДокументовЛегкийКлиентЭДО");
	
	ОбновитьДанныеКомпонентов(ДанныеКомпонентов);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеКомпонентов(ДанныеКомпонентов)

	Если ОбщегоНазначения.ЭтоВебКлиент() Тогда
		ИнтерфейсДокументовЭДО.УдалитьРеквизитыВыводаPDFДокумента(ЭтотОбъект, СведенияОPdfДокументе);
	КонецЕсли;

	ПараметрыЗаполнения = 
		ЛегкийИнтерфейсДокументовЭДО.ПараметрыЗаполненияДанныхКомпонентовПросмотраДокумента(ЭтотОбъект);
	ПараметрыЗаполнения.НастройкиФормы.ПеречитатьКэшВизуализации = ПеречитатьКэшВизуализации;
	ОперацияПолученияДанных = ЛегкийИнтерфейсДокументовЭДО.ЗаполнитьДанныеКомпонентовПросмотраДокумента(
		ДанныеКомпонентов, ПараметрыЗаполнения);
	
	ЛегкийИнтерфейсДокументовЭДО.НастроитьПанелиПросмотраКарточкиДокумента(ЭтотОбъект, ДанныеКомпонентов);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВизуализациюДокумента()

	ОбновитьВизуализациюДокументаНаСервере();

	ОжидатьПолученияАсинхронныхДанных();

КонецПроцедуры

&НаСервере
Процедура ОбновитьВизуализациюДокументаНаСервере()

	ЛегкийИнтерфейсДокументовЭДО.СохранитьНастройкиОтображенияВизуализацииКарточкиДокумента(ЭтотОбъект);

	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельКомандВходящегоДокументаЛегкийКлиентЭДО, ПанельВизуализации, ПанельКомандНастроекВизуализации, 
		|ПанельПредставленияФайла");
	
	ОбновитьДанныеКомпонентов(ДанныеКомпонентов);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПослеВыполненияДействийПоЭДО()
	
	Прочитать();
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПослеИзмененияПодписейДокумента()
	
	ОбновитьДанныеПослеИзмененияПодписейДокументаНаСервере();
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеИзмененияПодписейДокументаНаСервере()
	
	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельКомандВходящегоДокументаЛегкийКлиентЭДО, ПанельСостоянияЛегкийКлиентЭДО,
		|ПанельЗакладокВходящегоДокумента, ПанельВизуализации, ПанельКомандНастроекВизуализации,
		|ПанельПредставленияФайла, ПанельПодписей");
	
	ОбновитьДанныеКомпонентов(ДанныеКомпонентов);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПослеИзмененияОбщегоСостояния()

	ОбновитьДанныеПослеИзмененияОбщегоСостоянияНаСервере();
	
	ОжидатьПолученияАсинхронныхДанных();

КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеИзмененияОбщегоСостоянияНаСервере()

	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельСостоянияЛегкийКлиентЭДО");
	
	ОбновитьДанныеКомпонентов(ДанныеКомпонентов);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПослеИзмененияСвязанныхДокументов()

	ОбновитьДанныеПослеИзмененияСвязанныхДокументовНаСервере();
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеИзмененияСвязанныхДокументовНаСервере()

	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельСвязанныхДокументовЛегкийКлиентЭДО");
	
	ОбновитьДанныеКомпонентов(ДанныеКомпонентов);

КонецПроцедуры

#Область АсинхронноеОбновлениеДанных

&НаКлиенте
Процедура ОжидатьПолученияАсинхронныхДанных()

	ДлительнаяОперация = ОперацияПолученияДанных; // См. ДлительныеОперации.ВыполнитьФункцию

	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаполнитьАсинхронныеДанныеПослеОжидания", ЭтотОбъект);

	Оповещение = Новый ОписаниеОповещения("ОжидатьПолученияДанныхКомпонентовПросмотраПослеДлительнойОперации",
		ЛегкийИнтерфейсДокументовЭДОКлиент, ОповещениеОЗавершении);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАсинхронныеДанныеПослеОжидания(Результат, ДополнительныеПараметры) Экспорт

	Если ОперацияПолученияДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Результат.Успех Тогда
		ОперацияПолученияДанных = Результат.ОперацияПолученияДанных;
		ОбновитьДанныеПослеЗаполненияАсинхронныхДанных();
		ОжидатьПолученияАсинхронныхДанных();
		
		Если ЗначениеЗаполнено(АдресХранилищаPdf) Тогда
			ПредставлениеДокумента = ПолучитьИзВременногоХранилища(АдресХранилищаPdf);
			ИнтерфейсДокументовЭДОКлиент.ВывестиPDFДокументНаФорму(ЭтотОбъект, СведенияОPdfДокументе,
				ПредставлениеДокумента);
			УдалитьИзВременногоХранилища(АдресХранилищаPdf);
			АдресХранилищаPdf = "";
		КонецЕсли;
	Иначе
		ОперацияПолученияДанных = Неопределено;
		ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(Результат.КонтекстДиагностики);
	КонецЕсли;
	
	Если ОповеститьОбИзмененииСвязанныхДокументов Тогда
		ЛегкийИнтерфейсДокументовЭДОКлиент.ОповеститьОбИзмененииСвязанныхДокументовИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеЗаполненияАсинхронныхДанных()

	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.РезультатЗаполненияДанныхКомпонентовПросмотраДокументаАсинхронноВФоне(
		ОперацияПолученияДанных);
	
	ОперацияПолученияДанных = Неопределено;
	
	Если ДанныеКомпонентов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоПакетДокументов И ТекущийДокументПакета <> Неопределено
		И ДанныеКомпонентов.ЭлектронныйДокумент <> ТекущийДокументПакета Тогда
		ОбновитьДанныеДляПросмотраДокумента();
		Возврат;
	КонецЕсли;
	
	ЛегкийИнтерфейсДокументовЭДО.НастроитьПанелиПросмотраКарточкиДокумента(ЭтотОбъект, ДанныеКомпонентов);
	Если ЗначениеЗаполнено(ДанныеКомпонентов.ПанельПредставленияФайла) Тогда
		АдресХранилищаPdf = ПоместитьВоВременноеХранилище(ДанныеКомпонентов.ПанельПредставленияФайла.Данные);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // АсинхронноеОбновлениеДанных

#КонецОбласти // ОбновлениеДанных

#Область РаботаСПакетом

&НаКлиенте
Процедура СоставПакетаПослеАктивизацииСтроки()
	
	ТекущиеДанные = Элементы.СоставПакета.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
		Или Не ЗначениеЗаполнено(ТекущиеДанные.ЭлектронныйДокумент)
		Или ТекущиеДанные.ЭлектронныйДокумент = Объект.Ссылка Тогда
		Возврат;
	КонецЕсли;

	ТекущийДокументПакета = ТекущиеДанные.ЭлектронныйДокумент;

	Если ОповеститьОПрочтении Тогда
		Оповестить("ПрочтениеОбновитьСписок", ТекущийДокументПакета);
	КонецЕсли;
	
	ПоказатьДокументПакета();

	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьДокументПакета()
	
	ЛегкийИнтерфейсДокументовЭДО.ПрекратитьОжиданиеПолученияДанныхКомпонентовПросмотраДокумента(ОперацияПолученияДанных);
	
	ЛегкийИнтерфейсДокументовЭДО.ПоказатьДокументПакетаКарточкиДокумента(ЭтотОбъект, ТекущийДокументПакета);

КонецПроцедуры

#КонецОбласти // РаботаСПакетом

#Область ОбработчикиОповещений

&НаКлиенте
Процедура ПослеСменыОтветственного(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Или Не Результат.Успех Тогда
		Возврат;
	КонецЕсли;

	ОбновитьДанныеЭлектронногоДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПроверкиПодписейДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) И Результат.Успех Тогда
		ОбновитьДанныеПослеИзмененияПодписейДокумента();
	КонецЕсли;
	
	ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(Результат.КонтекстДиагностики);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияРезультатаПроверкиПодписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьДанныеПослеИзмененияПодписейДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеДокументПослеОтветаНаВопрос(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено
		Или Ответ.Значение <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьПометкуУдаления();
	
	ОжидатьПолученияАсинхронныхДанных();
	
	Оповестить(ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияТекущихДелЭДО(),, ВладелецФормы);
	
	ОповеститьОбИзмененииСвязанныхДокументов = Истина;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиОповещений

#Область ОбработчикиКоманд

&НаСервере
Процедура ИзменитьПометкуУдаления()
	
	Объект.ПометкаУдаления = Не Объект.ПометкаУдаления;
	Записать();
	Прочитать();
	
	СвязанныеОбъектыУчета = ОбъектыУчетаДокумента.Выгрузить().ВыгрузитьКолонку("ОбъектУчета");
	ИзменитьПометкуУдаленияСвязанныхОбъектовУчета(СвязанныеОбъектыУчета, Объект.ПометкаУдаления);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьПометкуУдаленияСвязанныхОбъектовУчета(ОбъектыУчета, ПометкаУдаления)
	
	Для Каждого ОбъектУчета Из ОбъектыУчета Цикл 
		ИнтеграцияЭДО.ИзменитьПометкуУдаленияЧерновикаЭлектронногоДокумента(ОбъектУчета, ПометкаУдаления, Ложь);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВключитьПолеПредпросмотраPDFФайловНаСервере(ВключитьПоле)
	НастройкиЭДО.УстановитьИспользованиеПросмотраPDFДокументовЭДО(ВключитьПоле);
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти // ПодключаемыеКоманды

#КонецОбласти // ОбработчикиКоманд

#КонецОбласти // СлужебныеПроцедурыИФункции
