
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем ЗначениеОтбора;
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокРаспоряжений();
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Закрыть(Новый Структура("ЗаказКлиента, ПорядокРасчетов", 
			                Элементы.СписокРаспоряжений.ТекущиеДанные.Ссылка,
			                Элементы.СписокРаспоряжений.ТекущиеДанные.ПорядокРасчетов));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ОбщегоНазначенияУТКлиент.ПроверитьНаличиеВыделенныхВСпискеСтрок(Элементы.СписокРаспоряжений) Тогда
		Закрыть(
			Новый Структура("ЗаказКлиента, ПорядокРасчетов", 
			                Элементы.СписокРаспоряжений.ТекущиеДанные.Ссылка,
			                Элементы.СписокРаспоряжений.ТекущиеДанные.ПорядокРасчетов));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокРаспоряжений()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаЗаказы.ЗаказКлиента КАК ЗаказКлиента,
	|	СУММА(ТаблицаЗаказы.КОформлению) КАК КОформлению
	|ПОМЕСТИТЬ ТаблицаЗаказы1
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫРАЗИТЬ(РаспоряженияОбороты.Распоряжение КАК Документ.ЗаказКлиента) КАК ЗаказКлиента,
	|		ВЫБОР
	|			КОГДА &ХозяйственнаяОперацияРеализации <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереходПраваСобственности)
	|				ТОГДА РаспоряженияОбороты.КОтгрузкеКоличествоОборот - РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|			ИНАЧЕ
	|				РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|					+ РаспоряженияОбороты.ОприходованоДоППСКоличествоОборот
	|					- РаспоряженияОбороты.СписаноДоППСКоличествоОборот
	|					- РаспоряженияОбороты.ПринятоКоличествоОборот
	|					- РаспоряженияОбороты.ОформленоКоличествоОборот
	|		КОНЕЦ КАК КОформлению
	|	ИЗ
	|		#РаспоряженияНаОтгрузкуОборотыРазвернутая1 КАК РаспоряженияОбороты
	|	ГДЕ
	|		ВЫБОР
	|			КОГДА &ХозяйственнаяОперацияРеализации <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереходПраваСобственности)
	|				ТОГДА РаспоряженияОбороты.КОтгрузкеКоличествоОборот - РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|			ИНАЧЕ
	|				РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|					+ РаспоряженияОбороты.ОприходованоДоППСКоличествоОборот
	|					- РаспоряженияОбороты.СписаноДоППСКоличествоОборот
	|					- РаспоряженияОбороты.ПринятоКоличествоОборот
	|					- РаспоряженияОбороты.ОформленоКоличествоОборот
	|		КОНЕЦ <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ЗаказКлиента.Ссылка КАК ЗаказКлиента,
	|		ЗаказКлиентаТовары.Количество КАК КОформлению
	|	ИЗ
	|		Документ.ЗаказКлиента КАК ЗаказКлиента
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|			ПО ЗаказКлиента.Ссылка = ЗаказКлиентаТовары.Ссылка
	|	ГДЕ
	|		ЗаказКлиента.Организация = &Организация
	|		И ЗаказКлиента.Валюта = &Валюта
	|		И ЗаказКлиента.Контрагент = &Контрагент
	|		И ЗаказКлиента.Договор = &Договор
	|		И ВЫБОР
	|			КОГДА ЗаказКлиента.НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|			ИНАЧЕ ЗаказКлиента.НалогообложениеНДС
	|		КОНЕЦ = &НалогообложениеНДС
	|		И ЗаказКлиента.Партнер = &Партнер
	|		И ВЫБОР
	|			КОГДА НЕ &ИспользоватьСоглашенияСКлиентами
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЗаказКлиента.Соглашение = &Соглашение
	|		КОНЕЦ
	|		И ЗаказКлиента.ЦенаВключаетНДС = &ЦенаВключаетНДС
	|		И ЗаказКлиента.Сделка = &Сделка
	|		И ЗаказКлиента.ВернутьМногооборотнуюТару = &ВернутьМногооборотнуюТару
	|		И ЗаказКлиента.ТребуетсяЗалогЗаТару = &ТребуетсяЗалогЗаТару
	|		И ЗаказКлиентаТовары.Номенклатура.ВариантОформленияПродажи = ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияПродажи.РеализацияТоваровУслуг)
	|		И (ЗаказКлиента.Склад = &Склад
	|		ИЛИ ЗаказКлиента.Склад В ИЕРАРХИИ (&Склад)
	|		ИЛИ ЗаказКлиента.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|		И ВЫБОР
	|			КОГДА НЕ &ИспользоватьНаправленияДеятельности
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЗаказКлиента.НаправлениеДеятельности = &НаправлениеДеятельности
	|		КОНЕЦ
	|		И НЕ ЗаказКлиентаТовары.Отменено
	|		И ЗаказКлиента.ЭтоЗаказКакСчет
	|		И &ХозяйственнаяОперацияРеализации <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереходПраваСобственности)
	|		И НЕ ЗаказКлиента.ПометкаУдаления
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВЫРАЗИТЬ(РаспоряженияДвижения.Распоряжение КАК Документ.ЗаказКлиента) КАК ЗаказКлиента,
	|		РаспоряженияДвижения.Количество КАК КОформлению
	|	ИЗ
	|		РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК РаспоряженияДвижения
	|	ГДЕ
	|		РаспоряженияДвижения.Регистратор = &Регистратор
	|		И РаспоряженияДвижения.Показатель = ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Оформлено)
	|		И РаспоряженияДвижения.Активность
	|		И РаспоряженияДвижения.Распоряжение ССЫЛКА Документ.ЗаказКлиента) КАК ТаблицаЗаказы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗаказы.ЗаказКлиента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗаказы.ЗаказКлиента КАК ЗаказКлиента,
	|	СУММА(ТаблицаЗаказы.КОформлению) КАК КОформлению
	|ПОМЕСТИТЬ ТаблицаЗаказы2
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫРАЗИТЬ(РаспоряженияОбороты.Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента) КАК ЗаказКлиента,
	|		ВЫБОР
	|			КОГДА &ХозяйственнаяОперацияРеализации <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереходПраваСобственности)
	|				ТОГДА РаспоряженияОбороты.КОтгрузкеКоличествоОборот - РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|			ИНАЧЕ
	|				РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|					+ РаспоряженияОбороты.ОприходованоДоППСКоличествоОборот
	|					- РаспоряженияОбороты.СписаноДоППСКоличествоОборот
	|					- РаспоряженияОбороты.ПринятоКоличествоОборот
	|					- РаспоряженияОбороты.ОформленоКоличествоОборот
	|		КОНЕЦ КАК КОформлению
	|	ИЗ
	|		#РаспоряженияНаОтгрузкуОборотыРазвернутая2 КАК РаспоряженияОбороты
	|	ГДЕ
	|		ВЫБОР
	|			КОГДА &ХозяйственнаяОперацияРеализации <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереходПраваСобственности)
	|				ТОГДА РаспоряженияОбороты.КОтгрузкеКоличествоОборот - РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|			ИНАЧЕ
	|				РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|					+ РаспоряженияОбороты.ОприходованоДоППСКоличествоОборот
	|					- РаспоряженияОбороты.СписаноДоППСКоличествоОборот
	|					- РаспоряженияОбороты.ПринятоКоличествоОборот
	|					- РаспоряженияОбороты.ОформленоКоличествоОборот
	|		КОНЕЦ <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ЗаказКлиента.Ссылка КАК ЗаказКлиента,
	|		ЗаказКлиентаТовары.Количество КАК КОформлению
	|	ИЗ
	|		Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК ЗаказКлиента
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаВозвратТоваровОтКлиента.ЗаменяющиеТовары КАК ЗаказКлиентаТовары
	|			ПО ЗаказКлиента.Ссылка = ЗаказКлиентаТовары.Ссылка
	|	ГДЕ
	|		ЗаказКлиента.Организация = &Организация
	|		И ЗаказКлиента.Валюта = &Валюта
	|		И ЗаказКлиента.Контрагент = &Контрагент
	|		И ЗаказКлиента.Договор = &Договор
	|		И ВЫБОР
	|			КОГДА ЗаказКлиента.НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|			ИНАЧЕ ЗаказКлиента.НалогообложениеНДС
	|		КОНЕЦ = &НалогообложениеНДС
	|		И ЗаказКлиента.Партнер = &Партнер
	|		И ВЫБОР
	|			КОГДА НЕ &ИспользоватьСоглашенияСКлиентами
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЗаказКлиента.Соглашение = &Соглашение
	|		КОНЕЦ
	|		И ЗаказКлиента.ЦенаВключаетНДС = &ЦенаВключаетНДС
	|		И ЗаказКлиента.Сделка = &Сделка
	|		И ЗаказКлиента.ВернутьМногооборотнуюТару = &ВернутьМногооборотнуюТару
	|		И ЗаказКлиентаТовары.Номенклатура.ВариантОформленияПродажи = ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияПродажи.РеализацияТоваровУслуг)
	|		И (ЗаказКлиента.Склад = &Склад
	|		ИЛИ ЗаказКлиента.Склад В ИЕРАРХИИ (&Склад)
	|		ИЛИ ЗаказКлиента.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|		И ВЫБОР
	|			КОГДА НЕ &ИспользоватьНаправленияДеятельности
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЗаказКлиента.НаправлениеДеятельности = &НаправлениеДеятельности
	|		КОНЕЦ
	|		И НЕ ЗаказКлиентаТовары.Отменено
	|		И ЗаказКлиента.ЭтоЗаказКакСчет
	|		И &ХозяйственнаяОперацияРеализации <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереходПраваСобственности)
	|		И НЕ ЗаказКлиента.ПометкаУдаления
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВЫРАЗИТЬ(РаспоряженияДвижения.Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента) КАК ЗаказКлиента,
	|		ВЫБОР
	|			КОГДА РаспоряженияДвижения.Показатель = ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлению)
	|				ТОГДА -РаспоряженияДвижения.Количество
	|			ИНАЧЕ РаспоряженияДвижения.Количество
	|		КОНЕЦ КАК КОформлению
	|	ИЗ
	|		РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК РаспоряженияДвижения
	|	ГДЕ
	|		РаспоряженияДвижения.Регистратор = &Регистратор
	|		И РаспоряженияДвижения.Показатель В(
	|					ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлению),
	|					ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Оформлено)
	|					)
	|		И РаспоряженияДвижения.Активность
	|		И РаспоряженияДвижения.Распоряжение ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента) КАК ТаблицаЗаказы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗаказы.ЗаказКлиента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗаказы.ЗаказКлиента КАК ОтгрузкаТоваровКлиенту,
	|	СУММА(ТаблицаЗаказы.КОформлению) КАК КОформлению
	|ПОМЕСТИТЬ ТаблицаОтгрузкиТоваров
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫРАЗИТЬ(РаспоряженияОбороты.Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту) КАК ЗаказКлиента,
	|		РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|			+ РаспоряженияОбороты.ОприходованоДоППСКоличествоОборот
	|			- РаспоряженияОбороты.СписаноДоППСКоличествоОборот
	|			- РаспоряженияОбороты.ПринятоКоличествоОборот
	|			- РаспоряженияОбороты.ОформленоКоличествоОборот КАК КОформлению
	|	ИЗ
	|		#РаспоряженияНаОтгрузкуОборотыРазвернутая3 КАК РаспоряженияОбороты
	|	ГДЕ
	|		РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|			+ РаспоряженияОбороты.ОприходованоДоППСКоличествоОборот
	|			- РаспоряженияОбороты.СписаноДоППСКоличествоОборот
	|			- РаспоряженияОбороты.ПринятоКоличествоОборот
	|			- РаспоряженияОбороты.ОформленоКоличествоОборот <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВЫРАЗИТЬ(РаспоряженияДвижения.Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту) КАК ЗаказКлиента,
	|		ВЫБОР
	|			КОГДА РаспоряженияДвижения.Показатель = ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлению)
	|				ТОГДА -РаспоряженияДвижения.Количество
	|			ИНАЧЕ 
	|				РаспоряженияДвижения.Количество
	|		КОНЕЦ КАК КОформлению
	|	ИЗ
	|		РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК РаспоряженияДвижения
	|	ГДЕ
	|		&ХозяйственнаяОперацияРеализации = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереходПраваСобственности)
	|		И РаспоряженияДвижения.Активность
	|		И РаспоряженияДвижения.Распоряжение ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|		И РаспоряженияДвижения.Регистратор = &Регистратор
	|		И РаспоряженияДвижения.Показатель В(
	|					ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлению),
	|					ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Оформлено)
	|					)) КАК ТаблицаЗаказы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗаказы.ЗаказКлиента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗаказы.ЗаказКлиента КАК ПервичныйДокумент,
	|	СУММА(ТаблицаЗаказы.КОформлению) КАК КОформлению
	|ПОМЕСТИТЬ ТаблицаПервичныеДокументы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫРАЗИТЬ(РаспоряженияОбороты.Распоряжение КАК Документ.ПервичныйДокумент) КАК ЗаказКлиента,
	|		РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|			+ РаспоряженияОбороты.ОприходованоДоППСКоличествоОборот
	|			- РаспоряженияОбороты.СписаноДоППСКоличествоОборот
	|			- РаспоряженияОбороты.ПринятоКоличествоОборот
	|			- РаспоряженияОбороты.ОформленоКоличествоОборот КАК КОформлению
	|	ИЗ
	|		#РаспоряженияНаОтгрузкуОборотыРазвернутая4 КАК РаспоряженияОбороты
	|	ГДЕ
	|		РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|			+ РаспоряженияОбороты.ОприходованоДоППСКоличествоОборот
	|			- РаспоряженияОбороты.СписаноДоППСКоличествоОборот
	|			- РаспоряженияОбороты.ПринятоКоличествоОборот
	|			- РаспоряженияОбороты.ОформленоКоличествоОборот <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВЫРАЗИТЬ(РаспоряженияДвижения.Распоряжение КАК Документ.ПервичныйДокумент) КАК ЗаказКлиента,
	|		ВЫБОР
	|			КОГДА РаспоряженияДвижения.Показатель = ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлению)
	|				ТОГДА -РаспоряженияДвижения.Количество
	|			ИНАЧЕ 
	|				РаспоряженияДвижения.Количество
	|		КОНЕЦ КАК КОформлению
	|	ИЗ
	|		РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК РаспоряженияДвижения
	|	ГДЕ
	|		&ХозяйственнаяОперацияРеализации = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереходПраваСобственности)
	|		И РаспоряженияДвижения.Активность
	|		И РаспоряженияДвижения.Распоряжение ССЫЛКА Документ.ПервичныйДокумент
	|		И РаспоряженияДвижения.Регистратор = &Регистратор
	|		И РаспоряженияДвижения.Показатель В(
	|					ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлению),
	|					ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Оформлено)
	|					)) КАК ТаблицаЗаказы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗаказы.ЗаказКлиента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Заказы.Ссылка КАК Ссылка,
	|	ТИПЗНАЧЕНИЯ(Заказы.Ссылка) КАК ТипРаспоряжения,
	|	Заказы.Дата КАК Дата,
	|	Заказы.Номер КАК Номер,
	|	Заказы.Партнер КАК Партнер,
	|	Заказы.Контрагент КАК Контрагент,
	|	Заказы.Договор КАК Договор,
	|	Заказы.Соглашение КАК Соглашение,
	|	Заказы.Организация КАК Организация,
	|	Заказы.Склад КАК Склад,
	|	Заказы.Валюта КАК Валюта,
	|	Заказы.Менеджер КАК Менеджер,
	|	Заказы.Статус КАК Статус,
	|	Заказы.СуммаДокумента КАК СуммаДокумента,
	|	Заказы.Приоритет КАК Приоритет,
	|	Заказы.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Заказы.НалогообложениеНДС КАК НалогообложениеНДС,
	|	Заказы.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	Заказы.ПорядокРасчетов КАК ПорядокРасчетов,
	|	ВЫРАЗИТЬ(Заказы.Комментарий КАК СТРОКА(100)),
	|	ВЫБОР
	|		КОГДА Заказы.Приоритет В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				Приоритеты.Ссылка КАК Приоритет
	|			ИЗ
	|				Справочник.Приоритеты КАК Приоритеты
	|
	|			УПОРЯДОЧИТЬ ПО
	|				Приоритеты.РеквизитДопУпорядочивания)
	|			ТОГДА 0
	|		КОГДА Заказы.Приоритет В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				Приоритеты.Ссылка КАК Приоритет
	|			ИЗ
	|				Справочник.Приоритеты КАК Приоритеты
	|
	|			УПОРЯДОЧИТЬ ПО
	|				Приоритеты.РеквизитДопУпорядочивания УБЫВ)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КартинкаПриоритета
	|ИЗ
	|	Документ.ЗаказКлиента КАК Заказы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЗаказы1 КАК ТаблицаЗаказы1
	|		ПО Заказы.Ссылка = ТаблицаЗаказы1.ЗаказКлиента
	|ГДЕ
	|	ТаблицаЗаказы1.КОформлению > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Заявки.Ссылка,
	|	ТИПЗНАЧЕНИЯ(Заявки.Ссылка),
	|	Заявки.Дата,
	|	Заявки.Номер,
	|	Заявки.Партнер,
	|	Заявки.Контрагент,
	|	Заявки.Договор,
	|	Заявки.Соглашение,
	|	Заявки.Организация,
	|	Заявки.Склад,
	|	Заявки.Валюта,
	|	Заявки.Менеджер,
	|	Заявки.Статус,
	|	Заявки.СуммаДокумента,
	|	Заявки.Приоритет,
	|	Заявки.ХозяйственнаяОперация,
	|	Заявки.НалогообложениеНДС,
	|	Заявки.ЦенаВключаетНДС,
	|	Заявки.ПорядокРасчетов,
	|	ВЫРАЗИТЬ(Заявки.Комментарий КАК СТРОКА(100)),
	|	ВЫБОР
	|		КОГДА Заявки.Приоритет В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				Приоритеты.Ссылка КАК Приоритет
	|			ИЗ
	|				Справочник.Приоритеты КАК Приоритеты
	|
	|			УПОРЯДОЧИТЬ ПО
	|				Приоритеты.РеквизитДопУпорядочивания)
	|			ТОГДА 0
	|		КОГДА Заявки.Приоритет В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				Приоритеты.Ссылка КАК Приоритет
	|			ИЗ
	|				Справочник.Приоритеты КАК Приоритеты
	|
	|			УПОРЯДОЧИТЬ ПО
	|				Приоритеты.РеквизитДопУпорядочивания УБЫВ)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК Заявки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЗаказы2 КАК ТаблицаЗаказы2
	|		ПО Заявки.Ссылка = ТаблицаЗаказы2.ЗаказКлиента
	|ГДЕ
	|	ТаблицаЗаказы2.КОформлению > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                       КАК Ссылка,
	|	ТИПЗНАЧЕНИЯ(ДанныеДокумента.Ссылка)          КАК ТипРаспоряжения,
	|	ДанныеДокумента.Дата                         КАК Дата,
	|	ДанныеДокумента.Номер                        КАК Номер,
	|	ДанныеДокумента.Партнер                      КАК Партнер,
	|	ДанныеДокумента.Контрагент                   КАК Контрагент,
	|	ДанныеДокумента.Договор                      КАК Договор,
	|	ДанныеДокумента.Соглашение                   КАК Соглашение,
	|	ДанныеДокумента.Организация                  КАК Организация,
	|	ДанныеДокумента.Склад                        КАК Склад,
	|	ДанныеДокумента.Валюта                       КАК Валюта,
	|	ДанныеДокумента.Менеджер                     КАК Менеджер,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовКлиентов.ПустаяСсылка) КАК Статус,
	|	ДанныеДокумента.СуммаДокумента               КАК СуммаДокумента,
	|	ЗНАЧЕНИЕ(Справочник.Приоритеты.ПустаяСсылка) КАК Приоритет,
	|	ДанныеДокумента.ХозяйственнаяОперация        КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.НалогообложениеНДС           КАК НалогообложениеНДС,
	|	ИСТИНА                                       КАК ЦенаВключаетНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка) КАК ПорядокРасчетов,
	|	ВЫРАЗИТЬ(ДанныеДокумента.Комментарий КАК СТРОКА(100)) КАК Комментарий,
	|	1                                            КАК КартинкаПриоритета
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОтгрузкиТоваров КАК ТаблицаОтгрузкиТоваров
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОтгрузкиТоваров.ОтгрузкаТоваровКлиенту
	|ГДЕ
	|	ТаблицаОтгрузкиТоваров.КОформлению > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент                    КАК Ссылка,
	|	ТИПЗНАЧЕНИЯ(ТаблицаПервичныеДокументы.ПервичныйДокумент)       КАК ТипРаспоряжения,
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент.Дата               КАК Дата,
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент.Номер              КАК Номер,
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент.Партнер            КАК Партнер,
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент.Контрагент         КАК Контрагент,
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент.Договор            КАК Договор,
	|	ТаблицаТовары.Ссылка.СоглашениеСКлиентом                       КАК Соглашение,
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент.Организация        КАК Организация,
	|	ТаблицаТовары.Ссылка.Склад                                     КАК Склад,
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент.Валюта             КАК Валюта,
	|	ТаблицаТовары.Ссылка.Менеджер                                  КАК Менеджер,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовКлиентов.ПустаяСсылка)     КАК Статус,
	|	СУММА(ТаблицаТовары.СуммаСНДС)                                 КАК СуммаДокумента,
	|	ЗНАЧЕНИЕ(Справочник.Приоритеты.ПустаяСсылка)                   КАК Приоритет,
	|	ТаблицаТовары.Ссылка.ХозяйственнаяОперация                     КАК ХозяйственнаяОперация,
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ТаблицаТовары.Ссылка.ЦенаВключаетНДС                           КАК ЦенаВключаетНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)            КАК ПорядокРасчетов,
	|	ВЫРАЗИТЬ(ТаблицаПервичныеДокументы.ПервичныйДокумент.Комментарий КАК СТРОКА(100)) КАК Комментарий,
	|	1                                                              КАК КартинкаПриоритета
	|ИЗ
	|	Документ.ВводОстатковТоваров.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПервичныеДокументы КАК ТаблицаПервичныеДокументы
	|		ПО ТаблицаТовары.Ссылка.Партия = ТаблицаПервичныеДокументы.ПервичныйДокумент
	|ГДЕ
	|	ТаблицаТовары.Ссылка.Проведен
	|	И ТаблицаТовары.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковОтгруженныхТоваров)
	|	И ТаблицаПервичныеДокументы.КОформлению > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент,
	|	ТИПЗНАЧЕНИЯ(ТаблицаПервичныеДокументы.ПервичныйДокумент),
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент.Дата,
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент.Номер,
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент.Партнер,
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент.Контрагент,
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент.Договор,
	|	ТаблицаТовары.Ссылка.СоглашениеСКлиентом,
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент.Организация,
	|	ТаблицаТовары.Ссылка.Склад,
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент.Валюта,
	|	ТаблицаТовары.Ссылка.Менеджер,
	|	ТаблицаТовары.Ссылка.ХозяйственнаяОперация,
	|	ТаблицаПервичныеДокументы.ПервичныйДокумент.НалогообложениеНДС,
	|	ТаблицаТовары.Ссылка.ЦенаВключаетНДС,
	|	ВЫРАЗИТЬ(ТаблицаПервичныеДокументы.ПервичныйДокумент.Комментарий КАК СТРОКА(100))
	|";
	
	// 1. Для заказов клиента
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"ТИПЗНАЧЕНИЯ(Распоряжение) = ТИП(Документ.ЗаказКлиента)
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Организация = &Организация
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Валюта = &Валюта
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Контрагент = &Контрагент
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Договор = &Договор
		|	И ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).НалогообложениеНДС В(
		|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
		|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
		|					)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
		|		ИНАЧЕ
		|			ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).НалогообложениеНДС
		|	КОНЕЦ = &НалогообложениеНДС
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Партнер = &Партнер
		|	И ВЫБОР
		|		КОГДА НЕ &ИспользоватьСоглашенияСКлиентами
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ
		|			ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Соглашение = &Соглашение
		|	КОНЕЦ
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).ЦенаВключаетНДС = &ЦенаВключаетНДС
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Сделка = &Сделка
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).ВернутьМногооборотнуюТару = &ВернутьМногооборотнуюТару
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).ТребуетсяЗалогЗаТару = &ТребуетсяЗалогЗаТару
		|	И Номенклатура.ВариантОформленияПродажи = ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияПродажи.РеализацияТоваровУслуг)
		|	И ВЫБОР
		|		КОГДА НЕ &ИспользоватьНаправленияДеятельности
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ
		|			ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).НаправлениеДеятельности = &НаправлениеДеятельности
		|	КОНЕЦ
		|	И (Склад = &Склад
		|		ИЛИ Склад В ИЕРАРХИИ (&Склад)
		|		ИЛИ Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|		ИЛИ &ХозяйственнаяОперацияРеализации <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереходПраваСобственности))
		|";
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение";
	ПараметрыФормированияТаблицы.Показатели =
		"КОтгрузке, Отгружено, ОприходованоДоППС, СписаноДоППС, Принято, Оформлено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая1 =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая1",
		РаспоряженияНаОтгрузкуОборотыРазвернутая1);
	
	// 2. Для заявок на возврат
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"ТИПЗНАЧЕНИЯ(Распоряжение) = ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента)
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Организация = &Организация
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Валюта = &Валюта
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Контрагент = &Контрагент
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Договор = &Договор
		|	И ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).НалогообложениеНДС В(
		|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
		|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
		|					)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
		|		ИНАЧЕ
		|			ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).НалогообложениеНДС
		|	КОНЕЦ = &НалогообложениеНДС
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Партнер = &Партнер
		|	И ВЫБОР
		|		КОГДА НЕ &ИспользоватьСоглашенияСКлиентами
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ
		|			ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Соглашение = &Соглашение
		|	КОНЕЦ
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ЦенаВключаетНДС = &ЦенаВключаетНДС
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Сделка = &Сделка
		|	И ВЫРАЗИТЬ(Распоряжение КАК
		|		Документ.ЗаявкаНаВозвратТоваровОтКлиента).ВернутьМногооборотнуюТару = &ВернутьМногооборотнуюТару
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ТребуетсяЗалогЗаТару = &ТребуетсяЗалогЗаТару
		|	И Номенклатура.ВариантОформленияПродажи = ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияПродажи.РеализацияТоваровУслуг)
		|	И ВЫБОР
		|		КОГДА НЕ &ИспользоватьНаправленияДеятельности
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ВЫРАЗИТЬ(Распоряжение КАК
		|			Документ.ЗаявкаНаВозвратТоваровОтКлиента).НаправлениеДеятельности = &НаправлениеДеятельности
		|	КОНЕЦ
		|	И (Склад = &Склад
		|		ИЛИ Склад В ИЕРАРХИИ (&Склад)
		|		ИЛИ Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|		ИЛИ &ХозяйственнаяОперацияРеализации <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереходПраваСобственности))
		|";
		
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение";
	ПараметрыФормированияТаблицы.Показатели =
		"КОтгрузке, Отгружено, ОприходованоДоППС, СписаноДоППС, Принято, Оформлено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая2 =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая2",
		РаспоряженияНаОтгрузкуОборотыРазвернутая2);

	// 3. Для отгрузки товаров клиенту без распоряжений
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"ТИПЗНАЧЕНИЯ(Распоряжение) = ТИП(Документ.ОтгрузкаТоваровКлиенту)
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).Организация = &Организация
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).Валюта = &Валюта
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).Контрагент = &Контрагент
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).Договор = &Договор
		|	И ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).НалогообложениеНДС В (
		|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
		|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
		|					)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
		|		ИНАЧЕ 
		|			ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).НалогообложениеНДС
		|	КОНЕЦ = &НалогообложениеНДС
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).Партнер = &Партнер
		|	И ВЫБОР
		|		КОГДА НЕ &ИспользоватьСоглашенияСКлиентами
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ 
		|			ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).Соглашение = &Соглашение
		|	КОНЕЦ
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).Сделка = &Сделка
		|	И ВЫБОР
		|		КОГДА НЕ &ИспользоватьНаправленияДеятельности
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ 
		|			ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).НаправлениеДеятельности = &НаправлениеДеятельности
		|	КОНЕЦ
		|	И &ХозяйственнаяОперацияРеализации = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереходПраваСобственности)
		|";
		
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение";
	ПараметрыФормированияТаблицы.Показатели =
		"Отгружено, ОприходованоДоППС, СписаноДоППС, Принято, Оформлено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая3 =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая3",
		РаспоряженияНаОтгрузкуОборотыРазвернутая3);

	// 4. Для первичных документов
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"ТИПЗНАЧЕНИЯ(Распоряжение) = ТИП(Документ.ПервичныйДокумент)
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ПервичныйДокумент).Организация = &Организация
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ПервичныйДокумент).Валюта = &Валюта
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ПервичныйДокумент).Контрагент = &Контрагент
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ПервичныйДокумент).Договор = &Договор
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ПервичныйДокумент).Партнер = &Партнер
		|	И ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(Распоряжение КАК Документ.ПервичныйДокумент).НалогообложениеНДС В (
		|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
		|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
		|					)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
		|		ИНАЧЕ 
		|			ВЫРАЗИТЬ(Распоряжение КАК Документ.ПервичныйДокумент).НалогообложениеНДС
		|	КОНЕЦ = &НалогообложениеНДС
		|	И &ХозяйственнаяОперацияРеализации = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереходПраваСобственности)
		|";
		
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение";
	ПараметрыФормированияТаблицы.Показатели =
		"Отгружено, ОприходованоДоППС, СписаноДоППС, Принято, Оформлено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая4 =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая4",
		РаспоряженияНаОтгрузкуОборотыРазвернутая4);

	Запрос.УстановитьПараметр("Организация",               Параметры.Отбор.Организация);
	Запрос.УстановитьПараметр("Контрагент",                Параметры.Отбор.Контрагент);
	Запрос.УстановитьПараметр("Валюта",                    Параметры.Отбор.Валюта);
	Запрос.УстановитьПараметр("Партнер",                   Параметры.Отбор.Партнер);
	Запрос.УстановитьПараметр("НалогообложениеНДС",        Параметры.Отбор.НалогообложениеНДС);
	Запрос.УстановитьПараметр("Соглашение",                Параметры.Отбор.Соглашение);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС",           Параметры.Отбор.ЦенаВключаетНДС);
	Запрос.УстановитьПараметр("Сделка",                    Параметры.Отбор.Сделка);
	Запрос.УстановитьПараметр("ВернутьМногооборотнуюТару", Параметры.Отбор.ВернутьМногооборотнуюТару);
	Запрос.УстановитьПараметр("ТребуетсяЗалогЗаТару",      Параметры.Отбор.ТребуетсяЗалогЗаТару);
	Запрос.УстановитьПараметр("Договор",                   Параметры.Отбор.Договор);
	Запрос.УстановитьПараметр("НаправлениеДеятельности",   Параметры.Отбор.НаправлениеДеятельности);
		
	Запрос.УстановитьПараметр("Регистратор", Параметры.Регистратор);
	Запрос.УстановитьПараметр("Склад",       Параметры.Склад);
	
	Запрос.УстановитьПараметр("ИспользоватьСоглашенияСКлиентами",
								ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами"));
	Запрос.УстановитьПараметр("ИспользоватьНаправленияДеятельности",
								ПолучитьФункциональнуюОпцию("ИспользоватьУчетДоходовПоНаправлениямДеятельности"));
	
	Если Параметры.Отбор.Свойство("ХозяйственнаяОперация") Тогда
		
		Если ЗначениеЗаполнено(Параметры.Отбор.ХозяйственнаяОперация) Тогда
			
			СписокОпераций = Документы.РеализацияТоваровУслуг.ДопустимыеОперацииДокументовОснований(Параметры.Отбор.ХозяйственнаяОперация);
			
			Условие = "	И ";
			Запрос.УстановитьПараметр("СписокХозяйственныхОпераций", СписокОпераций);
			Запрос.УстановитьПараметр("ХозяйственнаяОперацияРеализации", Параметры.Отбор.ХозяйственнаяОперация);
			
		Иначе
			
			СписокОпераций = Новый СписокЗначений;
			СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию);
			СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ВозвратОтКомиссионера);
			СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ВозвратОтРозничногоПокупателя);
			
			Условие = "	И НЕ ";
			Запрос.УстановитьПараметр("СписокХозяйственныхОпераций", СписокОпераций);
			
		КонецЕсли;
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
									"//УсловиеХозяйственнойОперацииЗаказы",
									Условие + "Заказы.ХозяйственнаяОперация В (&СписокХозяйственныхОпераций)");
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
									"//УсловиеХозяйственнойОперацииЗаявки",
									Условие + "Заявки.ХозяйственнаяОперация В (&СписокХозяйственныхОпераций)");
		
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	СписокРаспоряжений.Загрузить(Результат.Выгрузить());
	
КонецПроцедуры

#КонецОбласти

