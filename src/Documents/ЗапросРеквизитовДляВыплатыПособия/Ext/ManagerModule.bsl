#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.Печать

// Заполняет список команд печати.
// 
// Параметры:
//  КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной
//                                                            параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной
//                                            параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ФизическоеЛицо)
	|	И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ТекущиеДела

// См. ТекущиеДелаПереопределяемый.ПриОпределенииОбработчиковТекущихДел.
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.ЗапросРеквизитовДляВыплатыПособия;
	Если Не СЭДОФСС.ДоступенОбменЧерезСЭДО()
		Или Не ПравоДоступа("Изменение", МетаданныеДокумента) Тогда
		Возврат; // Нет прав.
	КонецЕсли;
	
	МодульТекущиеДелаСервер = ОбщегоНазначения.ОбщийМодуль("ТекущиеДелаСервер");
	Разделы = МодульТекущиеДелаСервер.РазделыДляОбъекта(МетаданныеДокумента.ПолноеИмя());
	Если Разделы.Количество() = 0 Тогда
		Возврат; // Некорректное внедрение.
	КонецЕсли;
	
	Требования = ТребованияПоОтправке();
	ВРаботе                  = Требования.Количество();
	ТребуетсяЗагрузить       = Требования.НайтиСтроки(Новый Структура("ТребуетсяЗагрузить", Истина)).Количество();
	ТребуетсяОтветитьВсего   = Требования.НайтиСтроки(Новый Структура("ТребуетсяОтветить", Истина)).Количество();
	ТребуетсяОтветитьСегодня = Требования.НайтиСтроки(Новый Структура("ТребуетсяОтветитьСегодня", Истина)).Количество();
	ТребуетсяОтветитьЗавтра  = ТребуетсяОтветитьВсего - ТребуетсяОтветитьСегодня;
	ТребуетсяПодтвердить     = Требования.НайтиСтроки(Новый Структура("ТребуетсяПодтвердить", Истина)).Количество();
	ИмяДокумента             = МетаданныеДокумента.Имя;
	ПредставлениеСписка      = МетаданныеДокумента.ПредставлениеСписка;
	
	Для Каждого Раздел Из Разделы Цикл
		
		ПолноеИмяРаздела = СтрЗаменить(Раздел.ПолноеИмя(), ".", "_");
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = ИмяДокумента + "_ВРаботе_" + ПолноеИмяРаздела;
		Дело.ЕстьДела       = (ВРаботе > 0);
		Дело.Важное         = (ТребуетсяОтветитьСегодня > 0);
		Дело.Владелец       = Раздел;
		Дело.Представление  = ПредставлениеСписка;
		Дело.Количество     = ВРаботе;
		Дело.Подсказка      = НСтр("ru = 'Извещения о представлении недостающих документов для выплаты пособий СФР.';
									|en = 'Notifications about submission of the necessary documents to receive benefits from the Social Insurance Fund of Russia.'");
		Дело.ПараметрыФормы = Новый Структура("ТолькоВРаботе", Истина);
		Дело.Форма          = "Документ.ЗапросРеквизитовДляВыплатыПособия.ФормаСписка";
		
		ИдентификаторРодителя = Дело.Идентификатор;
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = ИмяДокумента + "_Загрузить_" + ПолноеИмяРаздела;
		Дело.ЕстьДела       = (ТребуетсяЗагрузить > 0);
		Дело.Важное         = Истина;
		Дело.Владелец       = ИдентификаторРодителя;
		Дело.Представление  = НСтр("ru = 'Получить новые извещения из СФР';
									|en = 'Get new notifications from the Social Insurance Fund of Russia'");
		Дело.Количество     = ТребуетсяЗагрузить;
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = ИмяДокумента + "_ОтветитьСегодня_" + ПолноеИмяРаздела;
		Дело.ЕстьДела       = (ТребуетсяОтветитьСегодня > 0);
		Дело.Важное         = Истина;
		Дело.Владелец       = ИдентификаторРодителя;
		Дело.Представление  = НСтр("ru = 'Отправить недостающие сведения сегодня';
									|en = 'Send the missing information today'");
		Дело.Количество     = ТребуетсяОтветитьСегодня;
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = ИмяДокумента + "_Ответить_" + ПолноеИмяРаздела;
		Дело.ЕстьДела       = (ТребуетсяОтветитьЗавтра > 0);
		Дело.Важное         = Ложь;
		Дело.Владелец       = ИдентификаторРодителя;
		Дело.Представление  = НСтр("ru = 'Отправить недостающие сведения';
									|en = 'Send the missing information'");
		Дело.Количество     = ТребуетсяОтветитьЗавтра;
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = ИмяДокумента + "_Подтвердить_" + ПолноеИмяРаздела;
		Дело.ЕстьДела       = (ТребуетсяПодтвердить > 0);
		Дело.Важное         = Ложь;
		Дело.Владелец       = ИдентификаторРодителя;
		Дело.Представление  = НСтр("ru = 'Подтвердить получение';
									|en = 'Confirm receipt'");
		Дело.Количество     = ТребуетсяПодтвердить;
		
	КонецЦикла;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ТекущиеДела

#КонецОбласти

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс


#Область ОбновлениеИнформационнойБазы

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.34.3";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор   = Новый УникальныйИдентификатор("ed06b277-2bea-11f0-8154-a842a16ef6f5");
	Обработчик.Процедура       = "Документы.ЗапросРеквизитовДляВыплатыПособия.ПовторноОбработатьСообщения32";
	Обработчик.Комментарий     = НСтр("ru = 'Создание документов по сообщениям СЭДО СФР 32.';
										|en = 'Create documents based on Social Insurance Fund EDI 32 messages.'");
	
КонецПроцедуры

// Обработка об изменении расчета пособий.
//
// Параметры:
//   ПараметрыОбновления - Структура - Параметры отложенного обновления.
//
Процедура ПовторноОбработатьСообщения32(ПараметрыОбновления = Неопределено) Экспорт
	СЭДОФСС.ПовторноОбработатьВходящиеСообщенияСЭДО(ПараметрыОбновления, 32);
КонецПроцедуры

#КонецОбласти

#Область СЭДО

Функция ТипСообщения(Документ = Неопределено) Экспорт
	Возврат 32;
КонецФункции

Функция Метаданные() Экспорт
	Возврат Метаданные.Документы.ЗапросРеквизитовДляВыплатыПособия;
КонецФункции

Процедура ЗагрузитьУведомлениеОНаличииСообщения(ТипСообщения, Страхователь, Уведомление, ЕстьИзменения) Экспорт
	ИдентификаторСообщения = Уведомление.Идентификатор;
	ТребуетсяПодтверждение = Уведомление.ТребуетсяПодтверждение;
	
	СуществующиеДокументы = НайтиПоИдентификаторуСообщения(ИдентификаторСообщения);
	Если СуществующиеДокументы.Количество() > 0 Тогда
		Фильтр = Новый Структура;
		Фильтр.Вставить("Страхователь", Страхователь);
		Фильтр.Вставить("ПометкаУдаления", Ложь);
		Фильтр.Вставить("ТребуетсяПодтверждение", ТребуетсяПодтверждение);
		Если СуществующиеДокументы.НайтиСтроки(Фильтр).Количество() = СуществующиеДокументы.Количество() Тогда
			Возврат;
		КонецЕсли;
	Иначе
		СуществующиеДокументы.Добавить(); // Все реквизиты = Неопределено.
	КонецЕсли;
	
	Для Каждого СуществующийДокумент Из СуществующиеДокументы Цикл
		Если СуществующийДокумент.Загружен = Истина Или СуществующийДокумент.Обработан = Истина Тогда
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(СуществующийДокумент.Ссылка) Тогда
			ДокументОбъект = СуществующийДокумент.Ссылка.ПолучитьОбъект();
		Иначе
			ДокументОбъект = СоздатьДокумент();
		КонецЕсли;
		ДокументОбъект.Страхователь           = Страхователь;
		ДокументОбъект.ИдентификаторСообщения = ИдентификаторСообщения;
		ДокументОбъект.ТребуетсяПодтверждение = ТребуетсяПодтверждение;
		ДокументОбъект.Организация            = Страхователь;
		ДокументОбъект.ЗаполнитьДату();
		ДокументОбъект.ОбновитьВторичныеДанные();
		СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, "БЗК", РежимЗаписиДокумента.Запись);
		
		ЕстьИзменения = Истина;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗагрузитьСообщение32(Страхователь, ИдентификаторСообщения, ТекстXML, ТребуетсяПодтверждение, Результат, Кэш) Экспорт
	СтруктураДокумента = СтруктураДокументаИзXML(ТекстXML, Результат);
	Если СтруктураДокумента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИзменяемыеРеквизиты = Новый Массив;
	Для Каждого КлючИЗначение Из СтруктураДокумента Цикл
		Если КлючИЗначение.Значение = Неопределено Тогда
			СтруктураДокумента.Удалить(КлючИЗначение.Ключ);
		Иначе
			ИзменяемыеРеквизиты.Добавить(КлючИЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;
	ИзменяемыеРеквизиты.Добавить("НомерПроцесса");
	ИзменяемыеРеквизиты.Добавить("Загружен");
	ИзменяемыеРеквизиты.Добавить("Страхователь");
	ИзменяемыеРеквизиты.Добавить("ИдентификаторСообщения");
	ИзменяемыеРеквизиты.Добавить("ТребуетсяПодтверждение");
	ИзменяемыеРеквизиты.Добавить("Дата");
	
	СуществующиеДокументы = НайтиПоИдентификаторуСообщения(ИдентификаторСообщения);
	
	Если СуществующиеДокументы.Количество() = 0 Тогда
		ДокументОбъект = СоздатьДокумент();
	Иначе
		ДокументОбъект = СуществующиеДокументы[0].Ссылка.ПолучитьОбъект();
		ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюРеквизитовШапки(ДокументОбъект, ИзменяемыеРеквизиты);
		ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюТабличнойЧасти(ДокументОбъект, "НедостающиеСведения");
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, СтруктураДокумента);
	
	Если СтруктураДокумента.Свойство("НедостающиеСведения")
		И СтруктураДокумента.НедостающиеСведения <> Неопределено
		И СтруктураДокумента.НедостающиеСведения.Количество() > 0 Тогда
		ДокументОбъект.НедостающиеСведения.Загрузить(СтруктураДокумента.НедостающиеСведения);
	КонецЕсли;
	
	ДокументОбъект.Загружен               = Истина;
	ДокументОбъект.Страхователь           = Страхователь;
	ДокументОбъект.ИдентификаторСообщения = ИдентификаторСообщения;
	ДокументОбъект.ТребуетсяПодтверждение = ТребуетсяПодтверждение;
	ДокументОбъект.ЗаполнитьДату(Кэш);
	ДокументОбъект.ОбновитьВторичныеДанные();
	
	СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, "БЗК", РежимЗаписиДокумента.Проведение);
	
	Результат.Обработано = Истина;
КонецПроцедуры

// См. ЭлектронныйДокументооборотСФССПереопределяемый.ПослеПолученияОтветаНаПодтверждениеОПрочтении.
Процедура ЗагрузитьОтветНаПодтверждениеПолученияСообщения(Страхователь, ИсходноеСообщение, Результат, ЕстьИзменения) Экспорт
	// Вызывается при получении сообщения с типом 11 в ответ на подтверждение о прочтении входящего сообщения.
	//   Страхователь - СправочникСсылка.Организации - организация, получатель сообщения.
	//   ИсходноеСообщение - Структура:
	//     * ИдентификаторСообщения - Строка - идентификатор исходного сообщения СЭДО, по которому отправлялось подтверждение.
	//     * Тип                    - Число  - тип исходного сообщения СЭДО, по которому отправлялось подтверждение.
	//     * ТекстОшибки            - Строка - ошибка приема подтверждения.
	//     * ТекстПредупреждения    - Строка - предупреждение приема подтверждения.
	//   Результат - Структура - результат обработки сообщения:
	//     * Обработано      - Булево - признак того, что сообщение было успешно обработано.
	//     * ОшибкаОбработки - Булево - признак того, что при обработке сообщения возникла ошибка.
	//     * ОписаниеОшибки  - Строка - описание ошибки обработки.
	
	// Поиск документа по идентификатору сообщения.
	СуществующиеДокументы = НайтиПоИдентификаторуСообщения(ИсходноеСообщение.ИдентификаторСообщения);
	
	// Заполнение подтверждения получения.
	Для Каждого СуществующийДокумент Из СуществующиеДокументы Цикл
		ДокументОбъект = СуществующийДокумент.Ссылка.ПолучитьОбъект();
		Если ДокументОбъект.ЗаполнитьПодтверждениеПолучения(Неопределено) Тогда
			СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, "БЗК", РежимЗаписиДокумента.Проведение);
			ЕстьИзменения = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СоставДокументов

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
//
Функция ОписаниеСоставаОбъекта() Экспорт
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаФизическоеЛицоВШапке("ФизическоеЛицо", "Сотрудник");
КонецФункции

#КонецОбласти

#Область ФиксацияВторичныхДанныхВДокументах

Функция ПараметрыФиксацииВторичныхДанных() Экспорт
	ФиксируемыеРеквизиты = ФиксируемыеРеквизиты();
	ФиксируемыеТаблицы = Новый Структура("НедостающиеСведения", СтрРазделить("Поле", ",", Ложь));
	Возврат ФиксацияВторичныхДанныхВДокументах.ПараметрыФиксации(ФиксируемыеРеквизиты, ФиксируемыеТаблицы);
КонецФункции

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область ФиксацияВторичныхДанныхВДокументах

Функция ФиксируемыеРеквизиты()
	ФиксируемыеРеквизиты = Новый Соответствие;
	
	// Входящие ссылки, исходящие ссылки, результат обработки.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения = "ЭтотОбъект";
	Группа.ИмяГруппы           = "СсылкиИВторичныеДанные";
	
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ВходящееЗаявление");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ВходящийЗапрос");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ВходящийОтветНаЗапрос");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ВходящийПервичныйДокумент");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ВходящийРеестр");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Обработан", Ложь);
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Организация");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ОтветНаЗапрос");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Ребенок");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Сотрудник");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ФизическоеЛицо");
	
	// Реквизиты XML.
	РеквизитыРучногоЗаполнения = Новый Структура("Ответственный, Комментарий");
	ОбъектМетаданных = Метаданные();
	Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Шаблон.ИмяГруппы           = "ДанныеXML";
	Шаблон.ОснованиеЗаполнения = "ДанныеXML";
	Для Каждого Реквизит Из ОбъектМетаданных.Реквизиты Цикл
		Если РеквизитыРучногоЗаполнения.Свойство(Реквизит.Имя)
			Или ФиксируемыеРеквизиты[Реквизит.Имя] <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, Реквизит.Имя);
	КонецЦикла;
	Шаблон.Путь = "НедостающиеСведения";
	Для Каждого Реквизит Из ОбъектМетаданных.ТабличныеЧасти.НедостающиеСведения.Реквизиты Цикл
		Если РеквизитыРучногоЗаполнения.Свойство(Шаблон.Путь + Реквизит.Имя)
			Или ФиксируемыеРеквизиты[Шаблон.Путь + Реквизит.Имя] <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, Реквизит.Имя);
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(ФиксируемыеРеквизиты);
КонецФункции

#КонецОбласти

#Область XML

Функция СтруктураДокументаИзXML(ТекстXML, Результат) Экспорт
	// Поиск нужного узла.
	СтруктураDOM = СериализацияБЗК.СтруктураDOM(ТекстXML);
	КореньDOM = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "//*[local-name() = 'noticeRequest']");
	Если КореньDOM = Неопределено Тогда
		СЭДОФСС.ЗаписатьОшибкуПоискаУзла(Результат, "noticeRequest");
		Возврат Неопределено;
	КонецЕсли;
	
	// Подготовка структуры документа.
	СтруктураДокумента = Новый Структура(
	"ВерсияСпецификации,
	|ИдентификаторСообщения101,
	|ДатаСообщения,
	|ВходящийНомер,
	|ВходящаяДата,
	|КодТОФ,
	|НаименованиеТОФ,
	|ВходящийНомерРеестра,
	|НомерСтрокиРеестра,
	|КодВидаПособия,
	|НомерЛН,
	|РебенокСНИЛС,
	|РебенокДатаРождения,
	|ОтпускПоУходуДатаНачала,
	|ОтпускПоУходуДатаОкончания,
	|НачалоПериода,
	|СотрудникСНИЛС,
	|СотрудникФамилия,
	|СотрудникИмя,
	|СотрудникОтчество,
	|НедостающиеСведения,
	|НомерПроцесса,
	|НомерПроцессаСтрокой,
	|РегистрационныйНомерСФР,
	|Пособия,
	|ОтветственныйСФРФИО,
	|ОтветственныйСФРДолжность,
	|ОтветственныйСФРТелефон,
	|ПодписантСФРФИО,
	|ПодписантСФРДолжность,
	|НеобработанныеУзлы");
	
	// Чтение реквизитов узла.
	ПрочитатьАтрибутыКорняXML(КореньDOM, СтруктураДокумента);
	ПрочитатьРеквизитыXML(КореньDOM, СтруктураДокумента);
	
	Если ЗначениеЗаполнено(СтруктураДокумента.НомерПроцессаСтрокой) Тогда
		СтруктураДокумента.НомерПроцесса = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтруктураДокумента.НомерПроцессаСтрокой);
	КонецЕсли;
	
	Возврат СтруктураДокумента;
КонецФункции

Процедура ПрочитатьАтрибутыКорняXML(КореньDOM, СтруктураДокумента)
	// Пример корневого узла XML-сообщения:
	//<n1:noticeRequest xmlns:com="urn:ru:ecp:integration:types:common:v3.0"
	//                  xmlns:p="urn:ru:ecp:integration:types:person:v3.0"
	//                  xmlns:n1="urn:ru:ecp:integration:vnim:notice:request:v3.0"
	//                  specVersion="3.0.1"
	//                  messageDateTime="2024-04-19T09:30:47Z">
	АтрибутыКорня = СериализацияБЗК.АтрибутыЭлементаDOM(КореньDOM, "specVersion,messageUuid101,messageDateTime");
	СтруктураДокумента.ВерсияСпецификации = АтрибутыКорня.specVersion;
	СтруктураДокумента.ИдентификаторСообщения101 = АтрибутыКорня.messageUuid101;
	СтруктураДокумента.ДатаСообщения = СериализацияБЗК.ДатаИзXML(АтрибутыКорня.messageDateTime);
КонецПроцедуры

Процедура ПрочитатьРеквизитыXML(КореньDOM, СтруктураДокумента)
	// Пример реквизитов XML-сообщения:
	//	<noticeNumber>5862</noticeNumber>
	//	<noticeDate>2024-04-19</noticeDate>
	//	<tof>
	//		<com:code>086</com:code>
	//		<com:name>ОСФР ПО ЯРОСЛАВСКОЙ ОБЛАСТИ</com:name>
	//	</tof>
	//	<registryNum>2025-0228-1024-403-3104201008</registryNum>
	//	<insuredInfo>
	//		<snils>13700328015</snils>
	//		<fullName>
	//			<p:firstName>Иван</p:firstName>
	//			<p:lastName>Иванов</p:lastName>
	//			<p:middleName>Иванович</p:middleName>
	//		</fullName>
	//	</insuredInfo>
	//	<insurerInfo>
	//		<regNumSFR>1000260750</regNumSFR>
	//	</insurerInfo>
	//	<benefitInfo>
	//		<rowNumber>1</rowNumber>
	//		<benefit88>
	//			<dt1>2024-03-01</dt1>
	//		</benefit88>
	//	</benefitInfo>
	//	<informationRequest>
	//		<item>
	//			<requestText>Банковские реквизиты некорректны. Требуется уточнить реквизиты</requestText>
	//			<requestFields>
	//				<field>
	//					<fieldName>БИК</fieldName>
	//					<fieldCode>noticeResponse/informationResponse/information/
	//receivePayment/com:bankInfo/com:bik</fieldCode>
	//				</field>
	//				<field>
	//					<fieldName>Номер счета</fieldName>
	//					<fieldCode>noticeResponse/informationResponse/information/
	//receivePayment/com:bankInfo/com:accountNum</fieldCode>
	//				</field>
	//			</requestFields>
	//		</item>
	//	</informationRequest>
	//	<senderInfo>
	//		<fullName>Петров Петр Петрович</fullName>
	//		<role>Заместитель начальника отдела назначения</role>
	//		<phone>+7 495 123 45 78 (1234)</phone>
	//	</senderInfo>
	//</n1:noticeRequest>
	
	УзлыDOM = СериализацияБЗК.УзлыЭлементаDOM(КореньDOM);
	НеобработанныеУзлы = Новый Массив;
	
	Если УзлыDOM.Свойство("noticeNumber") Тогда
		СтруктураДокумента.ВходящийНомер = СериализацияБЗК.СтрокаИзXML(УзлыDOM.noticeNumber);
		УзлыDOM.Удалить("noticeNumber");
	КонецЕсли;
	
	Если УзлыDOM.Свойство("noticeDate") Тогда
		СтруктураДокумента.ВходящаяДата = СериализацияБЗК.ДатаИзXML(УзлыDOM.noticeDate);
		УзлыDOM.Удалить("noticeDate");
	КонецЕсли;
	
	Если УзлыDOM.Свойство("tof") Тогда
		УзлыТОФ = СериализацияБЗК.УзлыЭлементаDOM(УзлыDOM.tof);
		Если УзлыТОФ.Свойство("code") Тогда
			СтруктураДокумента.КодТОФ = СериализацияБЗК.СтрокаИзXML(УзлыТОФ.code);
		КонецЕсли;
		Если УзлыТОФ.Свойство("name") Тогда
			СтруктураДокумента.НаименованиеТОФ = СериализацияБЗК.СтрокаИзXML(УзлыТОФ.name);
		КонецЕсли;
		УзлыDOM.Удалить("tof");
	КонецЕсли;
	
	Если УзлыDOM.Свойство("registryNum") Тогда
		СтруктураДокумента.ВходящийНомерРеестра = СериализацияБЗК.СтрокаИзXML(УзлыDOM.registryNum);
		УзлыDOM.Удалить("registryNum");
	КонецЕсли;
	
	Если УзлыDOM.Свойство("insuredInfo") Тогда
		ЗагрузитьРеквизитыЗастрахованногоЛица(СтруктураДокумента, НеобработанныеУзлы, УзлыDOM.insuredInfo);
		УзлыDOM.Удалить("insuredInfo");
	КонецЕсли;
	
	Если УзлыDOM.Свойство("insurerInfo") Тогда
		УзлыСтрахователя = СериализацияБЗК.УзлыЭлементаDOM(УзлыDOM.insurerInfo);
		Если УзлыСтрахователя.Свойство("regNumSFR") Тогда
			СтруктураДокумента.РегистрационныйНомерСФР = СериализацияБЗК.СтрокаИзXML(УзлыСтрахователя.regNumSFR);
			УзлыСтрахователя.Удалить("regNumSFR");
		КонецЕсли;
		Для Каждого КлючИЗначение Из УзлыСтрахователя Цикл
			Ключ = "insurerInfo." + КлючИЗначение.Ключ;
			НеобработанныеУзлы.Добавить(Ключ + " = " + СериализацияБЗК.СтрокаИзXML(КлючИЗначение.Значение));
		КонецЦикла;
		УзлыDOM.Удалить("insurerInfo");
	КонецЕсли;
	
	Если УзлыDOM.Свойство("benefitInfo") Тогда
		ЗагрузитьРеквизитыПособия(СтруктураДокумента, НеобработанныеУзлы, УзлыDOM.benefitInfo);
		УзлыDOM.Удалить("benefitInfo");
	КонецЕсли;
	
	Если УзлыDOM.Свойство("informationRequest") Тогда
		ЗагрузитьЗапрашиваемыеРеквизиты(СтруктураДокумента, НеобработанныеУзлы, УзлыDOM.informationRequest);
		УзлыDOM.Удалить("informationRequest");
	КонецЕсли;
	
	// Данные работника СФР направившего извещение.
	Если УзлыDOM.Свойство("senderInfo") Тогда
		УзлыСФР = СериализацияБЗК.УзлыЭлементаDOM(УзлыDOM.senderInfo);
		Если УзлыСФР.Свойство("fullName") Тогда
			СтруктураДокумента.ОтветственныйСФРФИО = СериализацияБЗК.СтрокаИзXML(УзлыСФР.fullName);
		КонецЕсли;
		Если УзлыСФР.Свойство("role") Тогда
			СтруктураДокумента.ОтветственныйСФРДолжность = СериализацияБЗК.СтрокаИзXML(УзлыСФР.role);
		КонецЕсли;
		Если УзлыСФР.Свойство("phone") Тогда
			СтруктураДокумента.ОтветственныйСФРТелефон = СериализацияБЗК.СтрокаИзXML(УзлыСФР.phone);
		КонецЕсли;
		УзлыDOM.Удалить("senderInfo");
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из УзлыDOM Цикл
		НеобработанныеУзлы.Добавить(КлючИЗначение.Ключ + " = " + СериализацияБЗК.СтрокаИзXML(КлючИЗначение.Значение));
	КонецЦикла;
	
	СтруктураДокумента.НеобработанныеУзлы = СтрСоединить(НеобработанныеУзлы, ", ");
КонецПроцедуры

Процедура ЗагрузитьРеквизитыЗастрахованногоЛица(СтруктураДокумента, НеобработанныеУзлы, СотрудникDOM)
	Узлы = СериализацияБЗК.УзлыЭлементаDOM(СотрудникDOM);
	
	Если Узлы.Свойство("snils") Тогда
		СтруктураДокумента.СотрудникСНИЛС = СЭДОФСС.СНИЛСИзXML(Узлы.snils);
		Узлы.Удалить("snils");
	КонецЕсли;
	Если Узлы.Свойство("fullName") Тогда
		ФИО = СЭДОФСС.ФИОИзXML(Узлы.fullName);
		СтруктураДокумента.СотрудникФамилия = ФИО.Фамилия;
		СтруктураДокумента.СотрудникИмя = ФИО.Имя;
		СтруктураДокумента.СотрудникОтчество = ФИО.Отчество;
		Узлы.Удалить("fullName");
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из Узлы Цикл
		Ключ = "insuredInfo." + КлючИЗначение.Ключ;
		НеобработанныеУзлы.Добавить(Ключ + " = " + СериализацияБЗК.СтрокаИзXML(КлючИЗначение.Значение));
	КонецЦикла;
КонецПроцедуры

Процедура ЗагрузитьРеквизитыПособия(СтруктураДокумента, НеобработанныеУзлы, ПособиеDOM) Экспорт
	
	УзлыПособия = СериализацияБЗК.УзлыЭлементаDOM(ПособиеDOM);
	Если УзлыПособия.Свойство("proactiveProcessNum") Тогда
		СтруктураДокумента.НомерПроцессаСтрокой = СериализацияБЗК.СтрокаИзXML(УзлыПособия.proactiveProcessNum);
		УзлыПособия.Удалить("proactiveProcessNum");
	КонецЕсли;
	Если УзлыПособия.Свойство("rowNumber") Тогда
		СтруктураДокумента.НомерСтрокиРеестра = СериализацияБЗК.ЧислоИзXML(УзлыПособия.rowNumber);
		УзлыПособия.Удалить("rowNumber");
	КонецЕсли;
	Если УзлыПособия.Свойство("benefit1") Тогда
		СтруктураДокумента.КодВидаПособия = "1";
		ЗагрузитьРеквизитыВидаПособия(СтруктураДокумента, НеобработанныеУзлы, УзлыПособия.benefit1);
		УзлыПособия.Удалить("benefit1");
	КонецЕсли;
	Если УзлыПособия.Свойство("benefit2") Тогда
		СтруктураДокумента.КодВидаПособия = "2";
		ЗагрузитьРеквизитыВидаПособия(СтруктураДокумента, НеобработанныеУзлы, УзлыПособия.benefit2);
		УзлыПособия.Удалить("benefit2");
	КонецЕсли;
	Если УзлыПособия.Свойство("benefit4") Тогда
		СтруктураДокумента.КодВидаПособия = "4";
		ЗагрузитьРеквизитыВидаПособия(СтруктураДокумента, НеобработанныеУзлы, УзлыПособия.benefit4);
		УзлыПособия.Удалить("benefit4");
	КонецЕсли;
	Если УзлыПособия.Свойство("benefit5") Тогда
		СтруктураДокумента.КодВидаПособия = "5";
		ЗагрузитьРеквизитыВидаПособия(СтруктураДокумента, НеобработанныеУзлы, УзлыПособия.benefit5);
		УзлыПособия.Удалить("benefit5");
	КонецЕсли;
	Если УзлыПособия.Свойство("benefit6") Тогда
		СтруктураДокумента.КодВидаПособия = "6";
		ЗагрузитьРеквизитыВидаПособия(СтруктураДокумента, НеобработанныеУзлы, УзлыПособия.benefit6);
		УзлыПособия.Удалить("benefit6");
	КонецЕсли;
	// Ежемесячная выплата федеральным государственным гражданским служащим (ПП № 1652).
	Если УзлыПособия.Свойство("benefit87") Тогда
		СтруктураДокумента.КодВидаПособия = "87";
		ЗагрузитьРеквизитыВидаПособия(СтруктураДокумента, НеобработанныеУзлы, УзлыПособия.benefit87);
		УзлыПособия.Удалить("benefit87");
	КонецЕсли;
	// Специальная социальная выплата отдельным категориям медицинских работников (ПП РФ № 2568).
	Если УзлыПособия.Свойство("benefit88") Тогда
		СтруктураДокумента.КодВидаПособия = "88";
		ЗагрузитьРеквизитыВидаПособия(СтруктураДокумента, НеобработанныеУзлы, УзлыПособия.benefit88);
		УзлыПособия.Удалить("benefit88");
	КонецЕсли;
	// Необработанные узлы.
	Для Каждого КлючИЗначение Из УзлыПособия Цикл
		Ключ = "benefitInfo." + КлючИЗначение.Ключ;
		НеобработанныеУзлы.Добавить(Ключ + " = " + СериализацияБЗК.СтрокаИзXML(КлючИЗначение.Значение));
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗагрузитьРеквизитыВидаПособия(СтруктураДокумента, НеобработанныеУзлы, ВидПособияDOM)
	УзлыВида = СериализацияБЗК.УзлыЭлементаDOM(ВидПособияDOM);
	
	Если УзлыВида.Свойство("lnNumber") Тогда
		СтруктураДокумента.НомерЛН = СериализацияБЗК.СтрокаИзXML(УзлыВида.lnNumber);
		УзлыВида.Удалить("lnNumber");
	КонецЕсли;
	Если УзлыВида.Свойство("childSnils") Тогда
		СтруктураДокумента.РебенокСНИЛС = СЭДОФСС.СНИЛСИзXML(УзлыВида.childSnils);
		УзлыВида.Удалить("childSnils");
	КонецЕсли;
	Если УзлыВида.Свойство("childBirthDate") Тогда
		СтруктураДокумента.РебенокДатаРождения = СериализацияБЗК.ДатаИзXML(УзлыВида.childBirthDate);
		УзлыВида.Удалить("childBirthDate");
	КонецЕсли;
	Если УзлыВида.Свойство("vacationPeriod") Тогда
		УзлыОтпуска = СериализацияБЗК.УзлыЭлементаDOM(УзлыВида.vacationPeriod, "begin, end");
		СтруктураДокумента.ОтпускПоУходуДатаНачала    = СериализацияБЗК.ДатаИзXML(УзлыОтпуска.begin);
		СтруктураДокумента.ОтпускПоУходуДатаОкончания = СериализацияБЗК.ДатаИзXML(УзлыОтпуска.end);
		УзлыВида.Удалить("vacationPeriod");
	КонецЕсли;
	Если УзлыВида.Свойство("dt1") Тогда
		СтруктураДокумента.НачалоПериода = СериализацияБЗК.ДатаИзXML(УзлыВида.dt1);
		УзлыВида.Удалить("dt1");
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из УзлыВида Цикл
		Ключ = "benefitInfo.benefit" + СтруктураДокумента.КодВидаПособия + "." + КлючИЗначение.Ключ;
		НеобработанныеУзлы.Добавить(Ключ + " = " + СериализацияБЗК.СтрокаИзXML(КлючИЗначение.Значение));
	КонецЦикла;
КонецПроцедуры

Процедура ЗагрузитьЗапрашиваемыеРеквизиты(СтрокаТаблицы, НеобработанныеУзлы, ЗапрашиваемыеРеквизитыDOM)
	ВложенныеУзлы = СериализацияБЗК.СписокDOM(ЗапрашиваемыеРеквизитыDOM, "item");
	
	НедостающиеСведения = Новый ТаблицаЗначений;
	НедостающиеСведения.Колонки.Добавить("Поле");
	НедостающиеСведения.Колонки.Добавить("Наименование");
	НедостающиеСведения.Колонки.Добавить("Описание");
	СтрокаТаблицы.НедостающиеСведения = НедостающиеСведения;
	
	Для Каждого ВложенныйУзелDOM Из ВложенныеУзлы Цикл
		РеквизитыУзлаDOM = СериализацияБЗК.УзлыЭлементаDOM(ВложенныйУзелDOM, "requestText, requestFields");
		Описание = СериализацияБЗК.СтрокаИзXML(РеквизитыУзлаDOM.requestText);
		Если РеквизитыУзлаDOM.requestFields = Неопределено И Не ЗначениеЗаполнено(Описание) Тогда
			Продолжить;
		КонецЕсли;
		ПоляDOM = СериализацияБЗК.СписокDOM(РеквизитыУзлаDOM.requestFields, "field");
		Если ПоляDOM.Количество() = 0 Тогда
			НедостающийРеквизит = НедостающиеСведения.Добавить();
			НедостающийРеквизит.Описание = Описание;
		КонецЕсли;
		Для Каждого ПолеDOM Из ПоляDOM Цикл
			РеквизитыВложенногоУзлаDOM = СериализацияБЗК.УзлыЭлементаDOM(ПолеDOM, "fieldCode, fieldName");
			НедостающийРеквизит = НедостающиеСведения.Добавить();
			НедостающийРеквизит.Поле = СериализацияБЗК.СтрокаИзXML(РеквизитыВложенногоУзлаDOM.fieldCode);
			НедостающийРеквизит.Наименование = СериализацияБЗК.СтрокаИзXML(РеквизитыВложенногоУзлаDOM.fieldName);
			НедостающийРеквизит.Описание = Описание;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Регламенты

Функция МаксимальнаяДатаПодтвержденияПолучения(Объект) Экспорт
	Возврат СЭДОФСС.СледующийРабочийДень(Объект.ВходящаяДата, РабочихДнейНаПодтверждениеПолучения());
КонецФункции

Функция МаксимальнаяДатаОбработки(Объект) Экспорт
	Если ЗначениеЗаполнено(Объект.ДатаОтправкиПодтверждения)
		И Объект.ДатаОтправкиПодтверждения < Объект.МаксимальнаяДатаПодтверждения Тогда
		ДатаОтсчета = Объект.ДатаОтправкиПодтверждения;
	Иначе
		ДатаОтсчета = Объект.МаксимальнаяДатаПодтверждения;
	КонецЕсли;
	Возврат СЭДОФСС.СледующийРабочийДень(ДатаОтсчета, РабочихДнейНаОтвет());
КонецФункции

// См. п.7 Положения № 1 утвержденного Постановлением Правительства РФ от 21.04.2011 N 294.
Функция РабочихДнейНаПодтверждениеПолучения()
	Возврат 1;
КонецФункции

// См. п.7 Положения № 1 утвержденного Постановлением Правительства РФ от 21.04.2011 N 294.
Функция РабочихДнейНаОтвет() Экспорт
	Возврат 5;
КонецФункции

#КонецОбласти

#Область ТекущиеДела

Функция ТребованияПоОтправке()
	НачалоТекущегоДня = НачалоДня(ТекущаяДатаСеанса());
	НачалоРабочегоДня = НачалоДня(СЭДОФСС.БлижайшийРабочийДень(НачалоТекущегоДня));
	// Бумагу отправляют через 2-3 дня, а на подтверждение получения дается 1 день.
	НачалоТекущегоДняМинусДваДня = НачалоТекущегоДня - 86400 * 2;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Извещение.Ссылка КАК Ссылка,
	|	НЕ Извещение.Загружен КАК ТребуетсяЗагрузить,
	|	Извещение.Загружен КАК ТребуетсяОтветить,
	|	ВЫБОР
	|		КОГДА Извещение.Загружен
	|				И Извещение.МаксимальнаяДатаОбработки <= &НачалоРабочегоДня
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТребуетсяОтветитьСегодня,
	|	ВЫБОР
	|		КОГДА Извещение.Загружен
	|				И НЕ Извещение.ПодтверждениеДоставлено
	|				И Извещение.ДатаОтправкиПодтверждения = &ПустаяДата
	|				И Извещение.МаксимальнаяДатаПодтверждения >= &НачалоТекущегоДняМинусДваДня
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТребуетсяПодтвердить
	|ИЗ
	|	Документ.ЗапросРеквизитовДляВыплатыПособия КАК Извещение
	|ГДЕ
	|	НЕ Извещение.Обработан
	|	И НЕ Извещение.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("НачалоРабочегоДня", НачалоРабочегоДня);
	Запрос.УстановитьПараметр("НачалоТекущегоДняМинусДваДня", НачалоТекущегоДняМинусДваДня);
	Запрос.УстановитьПараметр("ПустаяДата", '00010101');
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

#КонецОбласти

#Область Заполнение

// Поиск документа по идентификатору сообщения.
Функция НайтиПоИдентификаторуСообщения(ИдентификаторСообщения, Количество = Неопределено) Экспорт
	Настройки = ЗапросыБЗК.НастройкиЗапросаКТаблице();
	Если Количество <> Неопределено Тогда
		Настройки.Количество = Количество;
	КонецЕсли;
	ЗапросыБЗК.ДобавитьОтбор(Настройки.Отбор, "ИдентификаторСообщения", "=", ИдентификаторСообщения);
	Поля = "Ссылка, Дата, ПометкаУдаления, Организация, Страхователь, Загружен, Обработан, ТребуетсяПодтверждение";
	Запрос = ЗапросыБЗК.ЗапросКТаблице(Метаданные(), Поля, Настройки);
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Процедура ПриЗаписиОтветаНаЗапрос(ОтветНаЗапрос) Экспорт
	ЗначенияДоЗаписи = ОтветНаЗапрос.ДополнительныеСвойства.ЗначенияРеквизитовДоЗаписи;
	
	ИдентификаторыЗапросов = Новый Массив;
	КоллекцииБЗК.ДобавитьНеПустоеЗначениеВМассив(ИдентификаторыЗапросов, ОтветНаЗапрос.ВходящийЗапросИдентификатор);
	КоллекцииБЗК.ДобавитьНеПустоеЗначениеВМассив(ИдентификаторыЗапросов, ЗначенияДоЗаписи.ВходящийЗапросИдентификатор);
	
	// Обновление вторичных данных по ссылкам входящих запросов.
	ВходящиеЗапросы = Новый Массив;
	КоллекцииБЗК.ДобавитьНеПустоеЗначениеВМассив(ВходящиеЗапросы, ОтветНаЗапрос.ВходящийЗапрос);
	КоллекцииБЗК.ДобавитьНеПустоеЗначениеВМассив(ВходящиеЗапросы, ЗначенияДоЗаписи.ВходящийЗапрос);
	
	Для Каждого Ссылка Из ВходящиеЗапросы Цикл
		ДокументОбъект = Ссылка.ПолучитьОбъект();
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ИдентификаторыЗапросов, ДокументОбъект.ИдентификаторСообщения);
		Если ДокументОбъект.ОбновитьВторичныеДанные() Тогда
			СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, "БЗК", РежимЗаписиДокумента.Запись);
		КонецЕсли;
	КонецЦикла;
	
	// Обновление вторичных данных по идентификаторам входящих запросов.
	Если ВходящиеЗапросы.Количество() > 0 Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Запрос.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗапросРеквизитовДляВыплатыПособия КАК Запрос
		|ГДЕ
		|	Запрос.ИдентификаторСообщения В(&ИдентификаторыЗапросов)";
		Запрос.УстановитьПараметр("ИдентификаторыЗапросов", ИдентификаторыЗапросов);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			Если ДокументОбъект.ОбновитьВторичныеДанные() Тогда
				СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, "БЗК", РежимЗаписиДокумента.Запись);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ФизическиеЛица

Процедура ПриИзмененииСНИЛСФизическогоЛица(ФизическоеЛицо, СтарыйСНИЛС, НовыйСНИЛС) Экспорт
	// В сообщениях 32 Фонд явно указывает СНИЛС.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Шапка.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗапросРеквизитовДляВыплатыПособия КАК Шапка
	|ГДЕ
	|	Шапка.СотрудникСНИЛС = &НовыйСНИЛС
	|	И Шапка.ФизическоеЛицо <> &ФизическоеЛицо
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Шапка.Ссылка
	|ИЗ
	|	Документ.ЗапросРеквизитовДляВыплатыПособия КАК Шапка
	|ГДЕ
	|	Шапка.ФизическоеЛицо = &ФизическоеЛицо
	|	И Шапка.СотрудникСНИЛС <> &НовыйСНИЛС";
	Если ЗначениеЗаполнено(НовыйСНИЛС) Тогда
		Запрос.УстановитьПараметр("НовыйСНИЛС", НовыйСНИЛС);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Шапка.СотрудникСНИЛС = &НовыйСНИЛС", "ЛОЖЬ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Шапка.СотрудникСНИЛС <> &НовыйСНИЛС", "ИСТИНА");
	КонецЕсли;
	Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаВыборки Из Таблица Цикл
		Если Не ЗначениеЗаполнено(СтрокаВыборки.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		ДокументОбъект = СтрокаВыборки.Ссылка.ПолучитьОбъект();
		Если ДокументОбъект.СотрудникСНИЛС = НовыйСНИЛС И ЗначениеЗаполнено(НовыйСНИЛС) Тогда
			ДокументОбъект.ФизическоеЛицо = ФизическоеЛицо;
		Иначе
			ДокументОбъект.ФизическоеЛицо = Неопределено;
		КонецЕсли;
		ИменаРеквизитов = "ФизическоеЛицо, Организация, Сотрудник";
		ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюРеквизитовШапки(ДокументОбъект, ИменаРеквизитов);
		ДокументОбъект.ОбновитьВторичныеДанные();
		СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, "БЗК", РежимЗаписиДокумента.Запись);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

Функция ИсходящиеДокументы(Документ) Экспорт
	Если Не ЗначениеЗаполнено(Документ.ВходящийНомер) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтветНаЗапрос.Ссылка КАК ОтветНаЗапрос,
	|	РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий.Состояние ЕСТЬ НЕ NULL  КАК Обработан,
	|	ОтветНаЗапрос.ДатаОтправки КАК ДатаОтправки
	|ИЗ
	|	Документ.ОтветНаЗапросРеквизитовДляВыплатыПособия КАК ОтветНаЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий КАК РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий
	|		ПО ОтветНаЗапрос.Ссылка = РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий.ИсходящийДокумент
	|			И (РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДокументаСЭДОФСС.Принят))
	|ГДЕ
	|	ОтветНаЗапрос.ВходящийЗапросНомер = &ВходящийЗапросНомер
	|	И ОтветНаЗапрос.ВходящийЗапрос = &ВходящийЗапрос
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтветНаЗапрос.Ссылка,
	|	РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий.Состояние ЕСТЬ НЕ NULL ,
	|	ОтветНаЗапрос.ДатаОтправки
	|ИЗ
	|	Документ.ОтветНаЗапросРеквизитовДляВыплатыПособия КАК ОтветНаЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий КАК РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий
	|		ПО ОтветНаЗапрос.Ссылка = РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий.ИсходящийДокумент
	|			И (РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДокументаСЭДОФСС.Принят))
	|ГДЕ
	|	ОтветНаЗапрос.ВходящийЗапросНомер = &ВходящийЗапросНомер
	|	И ОтветНаЗапрос.ФизическоеЛицо = &ФизическоеЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОтправки УБЫВ";
	Запрос.УстановитьПараметр("ВходящийЗапросНомер", Документ.ВходящийНомер);
	Запрос.УстановитьПараметр("ВходящийЗапрос", Документ.Ссылка);
	Запрос.УстановитьПараметр("ФизическоеЛицо", Документ.ФизическоеЛицо);
	
	Результат = Новый Структура;
	Результат.Вставить("ОтветНаЗапрос", Неопределено);
	Результат.Вставить("Обработан", Ложь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция СведенияОбЭЛН(Документ) Экспорт
	СведенияОбЭЛН = Неопределено;
	Если Документ.ДополнительныеСвойства.Свойство("СведенияОбЭЛН", СведенияОбЭЛН) Тогда
		Возврат СведенияОбЭЛН;
	КонецЕсли;
	Если ЗначениеЗаполнено(Документ.НомерЛН) Тогда
		СведенияОбЭЛН = РегистрыСведений.СведенияОбЭЛН.СведенияОбЭЛН(Документ.НомерЛН, Документ.ГоловнаяОрганизация);
	КонецЕсли;
	Документ.ДополнительныеСвойства.Вставить("СведенияОбЭЛН", СведенияОбЭЛН);
	Возврат СведенияОбЭЛН;
КонецФункции

#КонецОбласти

#КонецЕсли