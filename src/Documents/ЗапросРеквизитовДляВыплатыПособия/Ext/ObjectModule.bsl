#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаСоздания) Тогда
		ДатаСоздания = ТекущаяДата(); // АПК:143 Для фильтрации событий в журнале регистрации требуется дата сервера.
	КонецЕсли;
	ЗаполнитьГоловнуюОрганизацию();
	Если ЗначениеЗаполнено(Сотрудник) Тогда
		ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сотрудник, "ФизическоеЛицо");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ВходящийРеестр) И ВходящийРеестр <> Неопределено Тогда
		ВходящийРеестр = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПервичноеЗаполнение

Процедура ЗаполнитьДату(Кэш = Неопределено) Экспорт
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	Если ЗначениеЗаполнено(ИдентификаторСообщения) Тогда
		СЭДОФСС.ОбновитьДатуСообщения(Дата, НачалоДня(Дата), СЭДОФСС.ДатаСообщения(ИдентификаторСообщения, Кэш));
	КонецЕсли;
КонецПроцедуры

Функция ЗаполнитьГоловнуюОрганизацию() Экспорт
	Если ЗначениеЗаполнено(Страхователь) Тогда
		НоваяСсылка = ЗарплатаКадры.ГоловнаяОрганизация(Страхователь);
	Иначе
		НоваяСсылка = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
	Возврат ФиксацияВторичныхДанныхВДокументах.ИзменитьЗначениеРеквизита(ГоловнаяОрганизация, НоваяСсылка);
КонецФункции

#КонецОбласти

#Область ФиксацияВторичныхДанныхВДокументах

Функция Менеджер()
	Возврат Документы.ЗапросРеквизитовДляВыплатыПособия;
КонецФункции

Функция ОбновитьВторичныеДанные(ПараметрыФиксации = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Модифицирован = Ложь;
	
	Если ОбъектЗафиксирован() Тогда
		Возврат Модифицирован;
	КонецЕсли;
	
	Если ПараметрыФиксации = Неопределено Тогда
		ПараметрыФиксации = Менеджер().ПараметрыФиксацииВторичныхДанных();
	КонецЕсли;
	
	Если ЗаполнитьГоловнуюОрганизацию() Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьФизическоеЛицо() Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьВидПособия() Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьРебенка() Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьВходящиеДокументы(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьПодтверждениеПолучения(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьМаксимальнуюДатуПодтвержденияПолучения(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьМаксимальнуюДатуОтправкиРеестра(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьВходящийРеестр(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьНедостающиеСведенияКратко() Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	ИсходящиеДокументы = Менеджер().ИсходящиеДокументы(ЭтотОбъект);
	Если ЗаполнитьИсходящиеДокументы(ИсходящиеДокументы) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	Если ЗаполнитьФлажокОбработан(ИсходящиеДокументы) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Возврат Модифицирован;
КонецФункции

Функция ОбъектЗафиксирован() Экспорт
	Возврат Обработан;
КонецФункции

Функция ЗаполнитьФизическоеЛицо() Экспорт
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "ФизическоеЛицо") Тогда
		Возврат Ложь;
	КонецЕсли;
	// Быстрая проверка на СНИЛС выбранного физлица.
	Если ЗначениеЗаполнено(ФизическоеЛицо)
		И ЗначениеЗаполнено(СотрудникСНИЛС)
		И СотрудникСНИЛС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФизическоеЛицо, "СтраховойНомерПФР") Тогда
		Возврат Ложь;
	КонецЕсли;
	НоваяСсылка = Неопределено;
	Если ЗначениеЗаполнено(СотрудникСНИЛС) Тогда
		РезультатПоиска = ФизическиеЛицаЗарплатаКадры.ФизическоеЛицоПоСНИЛСИлиФИО(
			СотрудникСНИЛС,
			СотрудникФамилия,
			СотрудникИмя,
			СотрудникОтчество);
		Если ЗначениеЗаполнено(РезультатПоиска.ФизическоеЛицо) Тогда
			НоваяСсылка = РезультатПоиска.ФизическоеЛицо;
		КонецЕсли;
	КонецЕсли;
	Возврат ФиксацияВторичныхДанныхВДокументах.ИзменитьЗначениеРеквизита(ФизическоеЛицо, НоваяСсылка);
КонецФункции

Функция ЗаполнитьВидПособия()
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "ВидПособия") Тогда
		Возврат Ложь;
	КонецЕсли;
	НовоеЗначение = Перечисления.ПособияНазначаемыеФСС.НайтиПоКоду(КодВидаПособия);
	Возврат ФиксацияВторичныхДанныхВДокументах.ИзменитьЗначениеРеквизита(ВидПособия, НовоеЗначение);
КонецФункции

Функция ЗаполнитьРебенка()
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "Ребенок") Тогда
		Возврат Ложь;
	КонецЕсли;
	// Быстрая проверка на СНИЛС выбранного ребенка.
	Если ЗначениеЗаполнено(Ребенок)
		И ЗначениеЗаполнено(РебенокСНИЛС)
		И РебенокСНИЛС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ребенок, "СНИЛС") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ФизическоеЛицо)
		Или Не ЗначениеЗаполнено(РебенокСНИЛС) Тогда
		НоваяСсылка = Неопределено;
	Иначе
		Родственники = СЭДОФСС.ПустаяТаблицаРодственников();
		
		Родственник = Родственники.Добавить();
		Родственник.ДатаРождения = РебенокДатаРождения;
		Родственник.СНИЛС        = РебенокСНИЛС;
		
		СЭДОФСС.ЗаполнитьСсылкиРодственников(ФизическоеЛицо, Родственники);
		НоваяСсылка = Родственник.Ссылка;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ИзменитьЗначениеРеквизита(Ребенок, НоваяСсылка);
КонецФункции

Функция ЗаполнитьВходящийРеестр(ПараметрыФиксации) Экспорт
	Если Не ЗначениеЗаполнено(ВходящийНомерРеестра) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Реестр.Ссылка КАК Ссылка,
	|	Реестр.Номер КАК Номер,
	|	Реестр.Дата КАК Дата,
	|	ВЫБОР
	|		КОГДА Реестр.СтатусДокумента = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявленийИРеестровНаВыплатуПособий.ПринятФСС)
	|			ТОГДА 6
	|		КОГДА Реестр.СтатусДокумента = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявленийИРеестровНаВыплатуПособий.ПереданВФСС)
	|			ТОГДА 5
	|		КОГДА Реестр.СтатусДокумента = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявленийИРеестровНаВыплатуПособий.Подготовлен)
	|			ТОГДА 4
	|		КОГДА Реестр.СтатусДокумента = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявленийИРеестровНаВыплатуПособий.НеПринятФСС)
	|			ТОГДА 3
	|		КОГДА Реестр.СтатусДокумента = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявленийИРеестровНаВыплатуПособий.Аннулирован)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ВесСостояния,
	|	Реестр.ПометкаУдаления КАК ПометкаУдаления,
	|	Реестр.Проведен КАК Проведен
	|ИЗ
	|	Документ.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий КАК Реестр
	|ГДЕ
	|	Реестр.ВходящийНомерРеестра = &ВходящийНомерРеестра
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПометкаУдаления,
	|	Проведен УБЫВ,
	|	ВесСостояния УБЫВ,
	|	Дата УБЫВ";
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&ВходящийНомерРеестра", "&ВходящийНомерРеестра
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Реестр.Ссылка,
		|	Реестр.Номер,
		|	Реестр.Дата,
		|	7,
		|	Реестр.ПометкаУдаления,
		|	Реестр.Проведен
		|ИЗ
		|	Документ.РеестрСтимулирующихВыплатМедицинскимИСоциальнымРаботникам КАК Реестр
		|ГДЕ
		|	Реестр.ВходящийНомерРеестра = &ВходящийНомерРеестра");
	КонецЕсли;
	Запрос.УстановитьПараметр("ВходящийНомерРеестра", ВходящийНомерРеестра);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Реквизиты = Новый Структура("ВходящийРеестр");
	Если Выборка.Следующий() Тогда
		Реквизиты.ВходящийРеестр = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьВходящиеДокументы(ПараметрыФиксации)
	Реквизиты = Новый Структура("Организация, Сотрудник,
	|ВходящийЗапрос, ВходящийОтветНаЗапрос, ВходящееЗаявление, ВходящийПервичныйДокумент");
	
	// Заполнение по данным исходного реестра.
	Если ЗначениеЗаполнено(ВходящийРеестр) И ЗначениеЗаполнено(НомерСтрокиРеестра) Тогда
		Запрос = Новый Запрос;
		Если ТипЗнч(ВходящийРеестр) = Тип("ДокументСсылка.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий") Тогда
			Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Реестр.Организация КАК Организация,
			|	ЕСТЬNULL(Таблица.Сотрудник, НЕОПРЕДЕЛЕНО) КАК Сотрудник,
			|	ЕСТЬNULL(Таблица.Заявление, НЕОПРЕДЕЛЕНО) КАК ВходящееЗаявление,
			|	ЕСТЬNULL(Таблица.ПервичныйДокумент, НЕОПРЕДЕЛЕНО) КАК ВходящийПервичныйДокумент
			|ИЗ
			|	Документ.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий КАК Реестр
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий.СведенияНеобходимыеДляНазначенияПособий КАК Таблица
			|		ПО (Таблица.Ссылка = &Реестр)
			|			И (Таблица.НомерСтроки = &НомерСтрокиРеестра)
			|			И (Таблица.ФизическоеЛицо = &ФизическоеЛицо)
			|ГДЕ
			|	Реестр.Ссылка = &Реестр";
		ИначеЕсли ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
			Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Реестр.Организация КАК Организация,
			|	ЕСТЬNULL(Таблица.Сотрудник, НЕОПРЕДЕЛЕНО) КАК Сотрудник,
			|	НЕОПРЕДЕЛЕНО КАК ВходящееЗаявление,
			|	НЕОПРЕДЕЛЕНО КАК ВходящийПервичныйДокумент
			|ИЗ
			|	Документ.РеестрСтимулирующихВыплатМедицинскимИСоциальнымРаботникам КАК Реестр
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеестрСтимулирующихВыплатМедицинскимИСоциальнымРаботникам.Сотрудники КАК Таблица
			|		ПО (Таблица.Ссылка = &Реестр)
			|			И (Таблица.НомерСтроки = &НомерСтрокиРеестра)
			|			И (Таблица.ФизическоеЛицо = &ФизическоеЛицо)
			|ГДЕ
			|	Реестр.Ссылка = &Реестр";
		КонецЕсли;
		Запрос.УстановитьПараметр("Реестр", ВходящийРеестр);
		Запрос.УстановитьПараметр("НомерСтрокиРеестра", НомерСтрокиРеестра);
		Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
			Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "И (Таблица.ФизическоеЛицо = &ФизическоеЛицо)", "");
		КонецЕсли;
		Таблица = Запрос.Выполнить().Выгрузить();
		Если Таблица.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(Реквизиты, Таблица[0]);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторСообщения101) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОтветНаЗапросФССДляРасчетаПособия.Организация КАК Организация,
		|	ОтветНаЗапросФССДляРасчетаПособия.Сотрудник КАК Сотрудник,
		|	ОтветНаЗапросФССДляРасчетаПособия.Ссылка КАК ВходящийОтветНаЗапрос,
		|	ОтветНаЗапросФССДляРасчетаПособия.ВходящийЗапрос КАК ВходящийЗапрос,
		|	ОтветНаЗапросФССДляРасчетаПособия.ПервичныйДокумент КАК ВходящийПервичныйДокумент
		|ИЗ
		|	Документ.ОтветНаЗапросФССДляРасчетаПособия КАК ОтветНаЗапросФССДляРасчетаПособия
		|ГДЕ
		|	ОтветНаЗапросФССДляРасчетаПособия.ИдентификаторСообщения = &ИдентификаторСообщения101";
		Запрос.УстановитьПараметр("ИдентификаторСообщения101", ИдентификаторСообщения101);
		Таблица = Запрос.Выполнить().Выгрузить();
		Если Таблица.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(Реквизиты, Таблица[0]);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НомерПроцесса) И Не ЗначениеЗаполнено(Реквизиты.ВходящийОтветНаЗапрос) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Шапка.Организация КАК Организация,
		|	Шапка.Сотрудник КАК Сотрудник,
		|	Шапка.Ссылка КАК ВходящийОтветНаЗапрос,
		|	Шапка.ВходящийЗапрос КАК ВходящийЗапрос,
		|	Шапка.ПервичныйДокумент КАК ВходящийПервичныйДокумент
		|ИЗ
		|	Документ.ОтветНаЗапросФССДляРасчетаПособия КАК Шапка
		|ГДЕ
		|	Шапка.ИдентификаторСообщения = &НомерПроцесса";
		Запрос.УстановитьПараметр("НомерПроцесса", НомерПроцесса);
		Таблица = Запрос.Выполнить().Выгрузить();
		Если Таблица.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(Реквизиты, Таблица[0]);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НомерПроцесса) И Не ЗначениеЗаполнено(Реквизиты.ВходящийЗапрос) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Шапка.Организация КАК Организация,
		|	Шапка.Сотрудник КАК Сотрудник,
		|	Шапка.ОтветНаЗапрос КАК ВходящийОтветНаЗапрос,
		|	Шапка.Ссылка КАК ВходящийЗапрос,
		|	Шапка.ПервичныйДокумент КАК ВходящийПервичныйДокумент
		|ИЗ
		|	Документ.ВходящийЗапросФССДляРасчетаПособия КАК Шапка
		|ГДЕ
		|	Шапка.ИдентификаторСообщения = &НомерПроцесса";
		Запрос.УстановитьПараметр("НомерПроцесса", НомерПроцесса);
		Таблица = Запрос.Выполнить().Выгрузить();
		Если Таблица.Количество() > 0 Тогда
			СтрокаТаблицы = Таблица[0];
			Если Не ЗначениеЗаполнено(Реквизиты.Организация) Тогда
				Реквизиты.Организация = СтрокаТаблицы.Организация;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Реквизиты.Сотрудник) Тогда
				Реквизиты.Сотрудник = СтрокаТаблицы.Сотрудник;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Реквизиты.ВходящийОтветНаЗапрос) Тогда
				Реквизиты.ВходящийОтветНаЗапрос = СтрокаТаблицы.ВходящийОтветНаЗапрос;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Реквизиты.ВходящийЗапрос) Тогда
				Реквизиты.ВходящийЗапрос = СтрокаТаблицы.ВходящийЗапрос;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Реквизиты.ВходящийПервичныйДокумент) Тогда
				Реквизиты.ВходящийПервичныйДокумент = СтрокаТаблицы.ВходящийПервичныйДокумент;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Реквизиты.ВходящийПервичныйДокумент)
		И ЗначениеЗаполнено(ФизическоеЛицо)
		И ЗначениеЗаполнено(НомерЛН) Тогда
		СведенияОбЭЛН = Менеджер().СведенияОбЭЛН(ЭтотОбъект);
		Если СведенияОбЭЛН <> Неопределено Тогда
			Если Не ЗначениеЗаполнено(Реквизиты.Организация) Тогда
				Реквизиты.Организация = СведенияОбЭЛН.Организация;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Реквизиты.Сотрудник) Тогда
				Реквизиты.Сотрудник = СведенияОбЭЛН.Сотрудник;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Реквизиты.ВходящийЗапрос) Тогда
				Реквизиты.ВходящийЗапрос = СведенияОбЭЛН.ВходящийЗапрос;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Реквизиты.ВходящийОтветНаЗапрос) Тогда
				Реквизиты.ВходящийОтветНаЗапрос = СведенияОбЭЛН.ОтветНаЗапрос;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Реквизиты.ВходящийПервичныйДокумент)
				И ЗначениеЗаполнено(СведенияОбЭЛН.Больничный)
				И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СведенияОбЭЛН.Больничный, "Дата") <= ВходящаяДата Тогда
				Реквизиты.ВходящийПервичныйДокумент = СведенияОбЭЛН.Больничный;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Заполнение по кадровым данным.
	Если Не ЗначениеЗаполнено(Реквизиты.Сотрудник) Тогда
		ТекстОшибки = "";
		ДатаНачалаСобытия = ?(ЗначениеЗаполнено(ОтпускПоУходуДатаНачала), ОтпускПоУходуДатаНачала,
			?(ЗначениеЗаполнено(НачалоПериода), НачалоПериода,
			?(ЗначениеЗаполнено(ВходящаяДата), ВходящаяДата,
			Дата)));
		КадровыеДанные = КадровыйУчет.КадровыеДанныеОсновногоСотрудникаФизическогоЛица(
			Страхователь,
			ФизическоеЛицо,
			"Организация",
			ДатаНачалаСобытия,
			Ложь,
			ТекстОшибки);
		Если КадровыеДанные <> Неопределено Тогда
			Реквизиты.Организация = КадровыеДанные.Организация;
			Реквизиты.Сотрудник   = КадровыеДанные.Сотрудник;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Реквизиты.Организация) Тогда
		Реквизиты.Организация = Страхователь;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьПодтверждениеПолучения(ПараметрыФиксации) Экспорт
	Если Не ЗначениеЗаполнено(ИдентификаторСообщения) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Реквизиты = Новый Структура("ДатаОтправкиПодтверждения, ПодтверждениеДоставлено");
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитыШапкиЗафиксированы(ЭтотОбъект, Реквизиты) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Подтверждение = СЭДОФСС.СостояниеПодтверждения(Страхователь, ИдентификаторСообщения, ДатаОтправкиПодтверждения);
	Если Подтверждение = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Реквизиты.ДатаОтправкиПодтверждения = Подтверждение.ДатаОтправки;
	Реквизиты.ПодтверждениеДоставлено   = Подтверждение.Доставлено;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьМаксимальнуюДатуПодтвержденияПолучения(ПараметрыФиксации)
	Если Не ЗначениеЗаполнено(ВходящаяДата) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Реквизиты = Новый Структура("МаксимальнаяДатаПодтверждения");
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитыШапкиЗафиксированы(ЭтотОбъект, Реквизиты) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Реквизиты.МаксимальнаяДатаПодтверждения = Менеджер().МаксимальнаяДатаПодтвержденияПолучения(ЭтотОбъект);
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьМаксимальнуюДатуОтправкиРеестра(ПараметрыФиксации)
	Если Не ЗначениеЗаполнено(ВходящаяДата) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Реквизиты = Новый Структура("МаксимальнаяДатаОбработки");
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитыШапкиЗафиксированы(ЭтотОбъект, Реквизиты) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Реквизиты.МаксимальнаяДатаОбработки = Менеджер().МаксимальнаяДатаОбработки(ЭтотОбъект);
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьНедостающиеСведенияКратко()
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "НедостающиеСведенияКратко") Тогда
		Возврат Ложь;
	КонецЕсли;
	НовоеЗначение = Документы.ЗапросНедостающихСведенийПособияСФР.КраткоеПредставлениеНедостающихСведений(ЭтотОбъект);
	Возврат ФиксацияВторичныхДанныхВДокументах.ИзменитьЗначениеРеквизита(НедостающиеСведенияКратко, НовоеЗначение);
КонецФункции

Функция ЗаполнитьИсходящиеДокументы(ИсходящиеДокументы)
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "ОтветНаЗапрос") Тогда
		Возврат Ложь;
	КонецЕсли;
	Если ИсходящиеДокументы = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат ФиксацияВторичныхДанныхВДокументах.ИзменитьЗначениеРеквизита(ОтветНаЗапрос, ИсходящиеДокументы.ОтветНаЗапрос);
КонецФункции

Функция ЗаполнитьФлажокОбработан(ИсходящиеДокументы)
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "Обработан") Тогда
		Возврат Ложь;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ОтветНаЗапрос) Тогда
		НовоеЗначениеФлажка = Ложь;
	ИначеЕсли ИсходящиеДокументы <> Неопределено И ОтветНаЗапрос = ИсходящиеДокументы.ОтветНаЗапрос Тогда
		НовоеЗначениеФлажка = ИсходящиеДокументы.Обработан;
	Иначе
		ДатаОтправкиОтветаНаЗапрос = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтветНаЗапрос, "ДатаОтправки");
		НовоеЗначениеФлажка = ЗначениеЗаполнено(ДатаОтправкиОтветаНаЗапрос);
	КонецЕсли;
	Возврат ФиксацияВторичныхДанныхВДокументах.ИзменитьЗначениеРеквизита(Обработан, НовоеЗначениеФлажка);
КонецФункции

#КонецОбласти

#КонецОбласти
F = 1;
#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли