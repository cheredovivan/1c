
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Параметры.Свойство("ЗаполнениеИзПомощника") Тогда
		
		Если Параметры.Свойство("Организация") Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список, "Организация", Параметры.Организация, ВидСравненияКомпоновкиДанных.Равно, , Истина, 
				РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
		КонецЕсли;
		
		Если Параметры.Свойство("РазделУчета") Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список, "РазделУчета", Параметры.РазделУчета, ВидСравненияКомпоновкиДанных.Равно, , Истина, 
				РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
		КонецЕсли;

	Иначе
		ОбщегоНазначенияБПВызовСервера.УстановитьОтборПоОсновнойОрганизации(ЭтаФорма);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "РазделУчета", , ВидСравненияКомпоновкиДанных.Равно, , Ложь, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
	КонецЕсли;

	АдресХранилищаНастройкиДинСпискаДляРеестра = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		ОбщегоНазначенияБПКлиент.ИзменитьОтборПоОсновнойОрганизации(Список, , Параметр); 
	ИначеЕсли ИмяСобытия = "СозданыДокументыИнвентаризации" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ОбщегоНазначенияБП.ВосстановитьОтборСписка(Список, Настройки, "Организация");
	ОбщегоНазначенияБП.ВосстановитьОтборСписка(Список, Настройки, "РазделУчета");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура ПомощникСозданияДокументов(Команда)

	ПараметрыФормыНастроек = Новый Структура();
	ПараметрыФормыНастроек.Вставить("Организация","");
	ПараметрыФормыНастроек.Вставить("ПодразделениеОрганизации","");
	ОтборыСписка = Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
	
	Для Каждого Отбор Из ОтборыСписка Цикл
		Если ТипЗнч(Отбор) = Тип("ОтборКомпоновкиДанных") Тогда
			Для Каждого ПользовательскийОтбор Из Отбор.Элементы Цикл
				Если ПользовательскийОтбор.Использование Тогда
					ПараметрыФормыНастроек.Вставить(ПользовательскийОтбор.ЛевоеЗначение, ПользовательскийОтбор.ПравоеЗначение);
				КонецЕсли;
			КонецЦикла
		ИначеЕсли ТипЗнч(Отбор) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Если Отбор.Использование Тогда
				ПараметрыФормыНастроек.Вставить(Отбор.ИдентификаторПользовательскойНастройки, Отбор.ПравоеЗначение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
	ОповещениеНастроитьРазделыУчетаЗавершение = Новый ОписаниеОповещения("НастроитьРазделыУчетаЗавершение", ЭтотОбъект);
	ОткрытьФорму("Документ.ИнвентаризацияОбъектовУчета.Форма.ФормаНастроекРазделовУчета", ПараметрыФормыНастроек, ЭтотОбъект,
		УникальныйИдентификатор,,, ОповещениеНастроитьРазделыУчетаЗавершение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

#Область СтандартныеПодсистемы_ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура НастроитьРазделыУчетаЗавершение(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти
