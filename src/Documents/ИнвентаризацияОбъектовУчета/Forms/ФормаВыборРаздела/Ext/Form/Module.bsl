
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ЗначенияЗаполнения") Тогда
		ЗначенияЗаполнения = Параметры.ЗначенияЗаполнения;
		Если ЗначенияЗаполнения.Свойство("Организация") Тогда
			ОсновнаяОрганизация = ЗначенияЗаполнения.Организация;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ОсновнаяОрганизация) Тогда
		ОсновнаяОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	СписокРазделов = Перечисления.РазделыУчетаДляИнвентаризации.ДанныеВыбораПоФункциональнымОпциям(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьРаздел(Команда)

	СтрокаТаблицы = Элементы.СписокРазделов.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		
		ОткрытьДокументПоРазделу(СтрокаТаблицы.Значение);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокРазделовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = СписокРазделов.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ОткрытьДокументПоРазделу(СтрокаТаблицы.Значение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьДокументПоРазделу(ВыбранныйРазделУчета)
	
	Если ТипЗнч(ЗначенияЗаполнения) <> Тип("Структура") Тогда
		ЗначенияЗаполнения = Новый Структура();
	КонецЕсли;
	
	ЗначенияЗаполнения.Вставить("РазделУчета", ВыбранныйРазделУчета);
	
	Модифицированность = Ложь;
	Закрыть();
	
	ОткрытьФорму("Документ.ИнвентаризацияОбъектовУчета.Форма.ФормаДокумента", Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения),, Истина);
	
КонецПроцедуры

#КонецОбласти


