#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, "Организация, ПодразделениеОрганизации");
	
	Если Не ЗначениеЗаполнено(ДатаОкончанияИнвентаризации) Тогда
		ДатаОкончанияИнвентаризации = КонецМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаНачалаИнвентаризации) Тогда
		ДатаНачалаИнвентаризации = НачалоКвартала(ДатаОкончанияИнвентаризации);
	КонецЕсли;
	
	Элементы.ПоПодразделениям.Видимость = НЕ ЗначениеЗаполнено(ПодразделениеОрганизации);
	ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
	ДеревоРазделовУчета();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокументы()
	
	СоздаватьДокументы = Истина;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Организация';
																					|en = 'Company'"));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Организация");
		СоздаватьДокументы = Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаОкончанияИнвентаризации) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Дата';
																					|en = 'Date'"));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ДатаОкончанияИнвентаризации");
		СоздаватьДокументы = Ложь;
	КонецЕсли; 
	
	МассивВыбранныхРазделов = ПолучитьРазделыУчета(ДеревоРазделовУчета.ПолучитьЭлементы());
	Если МассивВыбранныхРазделов.Количество() = 0 Тогда 
		ТекстСообщения = НСтр("ru = 'Нет выбранных разделов учета';
								|en = 'There are no accounting sections selected'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		СоздаватьДокументы = Ложь;
	КонецЕсли;
	
	Если ДатаНачалаИнвентаризации > ДатаОкончанияИнвентаризации
		И ДатаОкончанияИнвентаризации <> '000101010000' Тогда
		
		ТекстСообщения = НСтр("ru = 'Начало инвентаризации должно быть не позже окончания';
								|en = 'The physical inventory count start must not be later than the end'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ДатаНачалаИнвентаризации");
		СоздаватьДокументы = Ложь;
	КонецЕсли;

	Если Не СоздаватьДокументы Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("Дата",                            ДатаОкончанияИнвентаризации);
	ПараметрыОперации.Вставить("ДатаНачалаИнвентаризации",        ДатаНачалаИнвентаризации);
	ПараметрыОперации.Вставить("ДатаОкончанияИнвентаризации",     ДатаОкончанияИнвентаризации);
	ПараметрыОперации.Вставить("Организация",                     Организация);
	ПараметрыОперации.Вставить("РазделыУчета",                    МассивВыбранныхРазделов);
	ПараметрыОперации.Вставить("ПричинаПроведенияИнвентаризации", ПричинаПроведенияИнвентаризации);
	ПараметрыОперации.Вставить("ДокументОснованиеВид",            ДокументОснованиеВид);
	ПараметрыОперации.Вставить("ДокументОснованиеНомер",          ДокументОснованиеНомер);
	ПараметрыОперации.Вставить("ДокументОснованиеДата",           ДокументОснованиеДата);
	ПараметрыОперации.Вставить("Ответственный",                   Ответственный);
	ПараметрыОперации.Вставить("ИнвентаризационнаяКомиссия",      ПолучитьИнвентаризационнуюКомиссию());
	ПараметрыОперации.Вставить("ПодразделениеОрганизации",        ПодразделениеОрганизации);
	ПараметрыОперации.Вставить("ПоПодразделениям",                ПоПодразделениям);
	
	ДлительнаяОперация = НачатьЗаполнение(ПараметрыОперации, УникальныйИдентификатор);
	ОжидатьЗавершениеЗаполнения(ДлительнаяОперация);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьВыполнить()
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтметкиПоРазделам(Команда)
	
	Для каждого ВложенныйЭлемент Из ДеревоРазделовУчета.ПолучитьЭлементы() Цикл
		ВложенныйЭлемент.Пометка = 1;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	
	Для каждого ВложенныйЭлемент Из ДеревоРазделовУчета.ПолучитьЭлементы() Цикл
		ВложенныйЭлемент.Пометка = 0;
	КонецЦикла;
		
КонецПроцедуры 

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	Элементы.ПоПодразделениям.Видимость = НЕ ЗначениеЗаполнено(ПодразделениеОрганизации);
	Если НЕ ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
		ПоПодразделениям = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИнвентаризационнаяКомиссия

&НаКлиенте
Процедура ПодборФизическихЛиц(Команда)
	
	ПараметрыФормы = Новый Структура("РежимВыбора, МножественныйВыбор, ЗакрыватьПриВыборе", Истина, Ложь, Ложь);
	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаВыбора", 
		ПараметрыФормы, 
		Элементы.ИнвентаризационнаяКомиссия, 
		УникальныйИдентификатор,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияПередУдалением(Элемент, Отказ)
	
	ТекущаяСтрокаТЧ = Элементы.ИнвентаризационнаяКомиссия.ТекущиеДанные;
	Если ТекущаяСтрокаТЧ.Председатель Тогда
		ИндексУдаляемойСтроки = ИнвентаризационнаяКомиссия.Индекс(ТекущаяСтрокаТЧ);
		КоличествоСтрок = ИнвентаризационнаяКомиссия.Количество() - 1;
		
		Если КоличествоСтрок > 0 Тогда
			Если ИндексУдаляемойСтроки <= КоличествоСтрок - 1 Тогда
				ИндексНовогоПредседателя = ИндексУдаляемойСтроки + 1;
			Иначе
				ИндексНовогоПредседателя = КоличествоСтрок - 1;
			КонецЕсли;
			ИнвентаризационнаяКомиссия[ИндексНовогоПредседателя].Председатель = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Копирование Тогда
		ТекущаяСтрокаТЧ = Элементы.ИнвентаризационнаяКомиссия.ТекущиеДанные;
		ТекущаяСтрокаТЧ.ФизЛицо = Неопределено;
		ТекущаяСтрокаТЧ.Председатель = Ложь;
	Иначе // Создание заново
		Если ИнвентаризационнаяКомиссия.Количество() = 1 Тогда
			ИнвентаризационнаяКомиссия[0].Председатель = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Строки = ИнвентаризационнаяКомиссия.НайтиСтроки(Новый Структура("ФизЛицо", ВыбранноеЗначение));
	
	Если Строки.Количество() > 0 Тогда
		ШаблонСообщения = НСтр("ru = 'Физическое лицо <%1> уже подобрано';
								|en = 'Person <%1> is already picked'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ВыбранноеЗначение);
		ПоказатьПредупреждение(Неопределено, ТекстСообщения, 60);
	Иначе
		НоваяСтрока = ИнвентаризационнаяКомиссия.Добавить();
		НоваяСтрока.ФизЛицо = ВыбранноеЗначение;
		Если ИнвентаризационнаяКомиссия.Количество() = 1 Тогда
			НоваяСтрока.Председатель = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияПредседательПриИзменении(Элемент)
	
	ТекущаяСтрокаТЧ = Элементы.ИнвентаризационнаяКомиссия.ТекущиеДанные;
	
	Если НЕ ТекущаяСтрокаТЧ.Председатель Тогда
		// Снимать флажок нельзя
		ТекущаяСтрокаТЧ.Председатель = Истина;
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТЧ Из ИнвентаризационнаяКомиссия Цикл
		Если СтрокаТЧ.ФизЛицо <> ТекущаяСтрокаТЧ.ФизЛицо Тогда
			СтрокаТЧ.Председатель = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияФизЛицоПриИзменении(Элемент)
	
	Если ИнвентаризационнаяКомиссия.Количество() = 1 Тогда
		ИнвентаризационнаяКомиссия[0].Председатель = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияФизЛицоОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекущаяСтрокаТЧ = Элементы.ИнвентаризационнаяКомиссия.ТекущиеДанные;
	Если ТекущаяСтрокаТЧ.ФизЛицо <> ВыбранноеЗначение Тогда
		СтрокиТабличнойЧасти = ИнвентаризационнаяКомиссия.НайтиСтроки(Новый Структура("ФизЛицо", ВыбранноеЗначение));
		Если СтрокиТабличнойЧасти.Количество() > 0 Тогда
			СтандартнаяОбработка = Ложь;
			ШаблонСообщения = НСтр("ru = 'Физическое лицо <%1> уже включено в состав комиссии';
									|en = 'Person <%1> is already included in the commission'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ВыбранноеЗначение);
			ПоказатьПредупреждение(Неопределено, ТекстСообщения, 60);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекущаяСтрокаТЧ = Элементы.ИнвентаризационнаяКомиссия.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(ТекущаяСтрокаТЧ.ФизЛицо) Тогда
		Отказ = Истина; 
		ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Члены комиссии"" в текущей строке';
								|en = 'The ""Commission members"" column is not filled in the current row'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, Элемент);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьРазделыУчета(ТекущиеЭлементыДерева)
	
	МассивВозврата = Новый Массив;
	
	Для каждого СтрокаДерева Из ТекущиеЭлементыДерева Цикл
		Если СтрокаДерева.Пометка Тогда
			МассивВозврата.Добавить(СтрокаДерева.РазделУчета);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивВозврата;
	
КонецФункции

&НаКлиенте
Функция ПолучитьИнвентаризационнуюКомиссию()
	
	МассивВозврата = Новый Массив;
	Для каждого Строка Из ИнвентаризационнаяКомиссия Цикл   
		СтруктураСтрокиКомиссии = Новый Структура();
		СтруктураСтрокиКомиссии.Вставить("ФизЛицо", Строка.ФизЛицо);
		СтруктураСтрокиКомиссии.Вставить("Председатель", Строка.Председатель);
		МассивВозврата.Добавить(СтруктураСтрокиКомиссии);
	КонецЦикла;
	
	Возврат МассивВозврата;
	
КонецФункции

&НаСервереБезКонтекста
Функция НачатьЗаполнение(ПараметрыОперации, УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыВыполнения,
		"Документы.ИнвентаризацияОбъектовУчета.СоздатьДокументы",
		ПараметрыОперации);
	
КонецФункции

&НаКлиенте
Процедура ОжидатьЗавершениеЗаполнения(ДлительнаяОперация)
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗавершениеОперации = Новый ОписаниеОповещения("ЗавершитьЗаполнение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ЗавершениеОперации);
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗавершитьЗаполнение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение НСтр("ru = 'Не удалось создать документы. Подробная информация в журнале регистрации.';
								|en = 'Cannot create the document. For details, see the Event log.'"); 
	Иначе
		Оповестить("СозданыДокументыИнвентаризации");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДеревоРазделовУчета()
	
	ВыбранныеРазделыУчета = Перечисления.РазделыУчетаДляИнвентаризации.ДанныеВыбораПоФункциональнымОпциям();
	
	ДеревоРазделов = РеквизитФормыВЗначение("ДеревоРазделовУчета");
	
	Для каждого РазделУчета Из ВыбранныеРазделыУчета Цикл
		
		СтрокаРаздела = ДеревоРазделов.Строки.Добавить();
		СтрокаРаздела.РазделУчета = РазделУчета.Значение;
		
	КонецЦикла;
		
	ЗначениеВРеквизитФормы(ДеревоРазделов, "ДеревоРазделовУчета");
		
КонецПроцедуры

#КонецОбласти