#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	//
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ЗаполнитьПриОткрытии = Ложь;
	Если Параметры.Ключ.Пустая() Тогда
		Параметры.Свойство("ЗаполнениеИзПомощника", ЗаполнениеИзПомощника);
		Если ЗаполнениеИзПомощника Тогда
			ЗаполнитьЗначенияСвойств(Объект, Параметры, "Организация, РазделУчета, Дата");
			СчетУчетаПодраздела = Параметры.СчетУчетаПодраздела;
		КонецЕсли;
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ЗаполнениеИзПомощника Тогда
		ЗаполнитьНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	УстановитьСостояниеДокумента();
	ЗаполнитьДобавленныеКолонки();
	УстановитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Организация", Объект.Организация);
	ПараметрыОповещения.Вставить("РазделУчета", Объект.РазделУчета);
	ПараметрыОповещения.Вставить("СчетУчетаПодраздела", СчетУчетаПодраздела);
	Оповестить("ОбновитьФормуПомощникаИнвентаризации", ПараметрыОповещения);
	
	ОбщегоНазначенияУТКлиент.ОповеститьОЗаписиДокументаВЖурнал(); 
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОбщегоНазначенияУТКлиент.ПровестиИЗакрыть(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Записать(ЭтаФорма, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Провести(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаУстановитьФлажки(Команда)
	
	СчетаУчетаУстановитьСнятьФлажки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаСнятьФлажки(Команда)
	
	СчетаУчетаУстановитьСнятьФлажки(Ложь);
	
КонецПроцедуры

#Область СтандартныеПодсистемы_ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбъектыУчета

&НаКлиенте
Процедура ОбъектыУчетаПередНачаломИзменения(Элемент, Отказ)
	
	СтрокаТаблицы = Элементы.ОбъектыУчета.ТекущиеДанные;
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма);
	УстановитьЗаголовкиИДоступностьСубконто(ЭтаФорма, СтрокаТаблицы.СчетУчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыУчетаПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыУчетаСчетУчетаПриИзменении(Элемент)

	СтрокаТаблицы = Элементы.ОбъектыУчета.ТекущиеДанные;
	
	УстановитьЗаголовкиИДоступностьСубконто(ЭтаФорма, СтрокаТаблицы.СчетУчета);
	
	ПоляОбъекта = Новый Структура();
	ПоляОбъекта.Вставить("Организация", Объект.Организация);
	ПоляОбъекта.Вставить("Субконто1", "Субконто1");
	ПоляОбъекта.Вставить("Субконто2", "Субконто2");
	ПоляОбъекта.Вставить("Субконто3", "Субконто3");
		
	БухгалтерскийУчетКлиентСервер.ПриИзмененииСчета(СтрокаТаблицы.СчетУчета, СтрокаТаблицы, ПоляОбъекта, Истина,, Ложь);
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма);
	ЗаполнитьДобавленныеКолонки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыУчетаСубконто1ПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыУчетаСубконто2ПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыУчетаСубконто3ПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыУчетаСубконтоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтрокаТаблицы = Элементы.ОбъектыУчета.ТекущиеДанные;

	ПараметрыДокумента = ПолучитПараметрыВыбора(ЭтаФорма, СтрокаТаблицы, "Субконто%Индекс%");
	ПараметрыДокумента.Вставить("СчетУчета", СтрокаТаблицы.СчетУчета);
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтаФорма, Элемент, СтандартнаяОбработка, ПараметрыДокумента);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИнвентаризационнаяКомиссия

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияПередУдалением(Элемент, Отказ)
	
	ТекущаяСтрокаТЧ = Элементы.ИнвентаризационнаяКомиссия.ТекущиеДанные;
	Если ТекущаяСтрокаТЧ.Председатель Тогда
		ИндексУдаляемойСтроки = Объект.ИнвентаризационнаяКомиссия.Индекс(ТекущаяСтрокаТЧ);
		КоличествоСтрок = Объект.ИнвентаризационнаяКомиссия.Количество() - 1;

		Если КоличествоСтрок > 0 Тогда
			Если ИндексУдаляемойСтроки <= КоличествоСтрок - 1 Тогда
				ИндексНовогоПредседателя = ИндексУдаляемойСтроки + 1;
			Иначе
				ИндексНовогоПредседателя = КоличествоСтрок - 1;
			КонецЕсли;
			Объект.ИнвентаризационнаяКомиссия[ИндексНовогоПредседателя].Председатель = Истина;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	Строки = Объект.ИнвентаризационнаяКомиссия.НайтиСтроки(Новый Структура("ФизЛицо", ВыбранноеЗначение));

	Если Строки.Количество() > 0 Тогда
		ШаблонСообщения = ШаблонТекстаФизЛицоВключеноВКомиссию();
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ВыбранноеЗначение);
		ПоказатьПредупреждение( , ТекстСообщения, 60);
	Иначе
		НоваяСтрока = Объект.ИнвентаризационнаяКомиссия.Добавить();
		НоваяСтрока.ФизЛицо = ВыбранноеЗначение;
		Если Объект.ИнвентаризационнаяКомиссия.Количество() = 1 Тогда
			НоваяСтрока.Председатель = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование) 
	
	Если Копирование Тогда
		ТекущаяСтрокаТЧ = Элементы.ИнвентаризационнаяКомиссия.ТекущиеДанные;
		ТекущаяСтрокаТЧ.ФизЛицо = Неопределено;
		ТекущаяСтрокаТЧ.Председатель = Ложь;
	Иначе // Создание заново
		Если Объект.ИнвентаризационнаяКомиссия.Количество() = 1 Тогда
			Объект.ИнвентаризационнаяКомиссия[0].Председатель = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияФизЛицоПриИзменении(Элемент)
	
	Если Объект.ИнвентаризационнаяКомиссия.Количество() = 1 Тогда
		Объект.ИнвентаризационнаяКомиссия[0].Председатель = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияФизЛицоОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	ИнвентаризационнаяКомиссияИсключитьДублиФизическихЛиц(
		Объект.ИнвентаризационнаяКомиссия,
		Элементы.ИнвентаризационнаяКомиссия,
		ВыбранноеЗначение,
		СтандартнаяОбработка);
		
КонецПроцедуры

// Обработчик выбора в поле ФизЛицо списка ИнвентаризационнаяКомиссия документов, обеспечивающий уникальность физических лиц в списке.
//
// Параметры:
//  ТабличнаяЧасть - ДанныеФормыКоллекция - представление табличной части ИнвентаризационнаяКомиссия
//  ПолеСписка - ПолеФормы - поле формы для отображения табличной части ИнвентаризационнаяКомиссия
//  ВыбранноеЗначение    - СправочникСсылка.ФизическиеЛица - выбранное пользователем физлицо
//  СтандартнаяОбработка - Булево - изменяемый параметр обработчика выбора
//
&НаКлиенте
Процедура ИнвентаризационнаяКомиссияИсключитьДублиФизическихЛиц(ТабличнаяЧасть, ПолеСписка, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекущаяСтрокаТЧ = ПолеСписка.ТекущиеДанные;
	
	Если ТекущаяСтрокаТЧ.ФизЛицо = ВыбранноеЗначение Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиТабличнойЧасти = ТабличнаяЧасть.НайтиСтроки(Новый Структура("ФизЛицо", ВыбранноеЗначение));
	
	Если СтрокиТабличнойЧасти.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонСообщения = ШаблонТекстаФизЛицоВключеноВКомиссию();
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ВыбранноеЗначение);
	
	Если ЗначениеЗаполнено(ТекущаяСтрокаТЧ.ФизЛицо) Тогда
		ПоказатьПредупреждение(, ТекстСообщения, 60);
	Иначе
		
		ОписаниеСтрок = Новый Структура;
		ОписаниеСтрок.Вставить("Найденные",       СтрокиТабличнойЧасти);
		ОписаниеСтрок.Вставить("Лишняя",          ПолеСписка.ТекущаяСтрока);
		ОписаниеСтрок.Вставить("ТабличнаяЧасть",  ТабличнаяЧасть);
		ОписаниеСтрок.Вставить("ПолеСписка",      ПолеСписка);
		
		ОповещениеУдалитьДублирующуюСтроку = Новый ОписаниеОповещения(
			"Подключаемый_ИнвентаризационнаяКомиссияУдалитьДублирующуюСтроку",
			ЭтотОбъект,
			ОписаниеСтрок);
		ПоказатьПредупреждение(ОповещениеУдалитьДублирующуюСтроку, ТекстСообщения, 60);
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияПредседательПриИзменении(Элемент)
	
	ТекущаяСтрокаТЧ = Элементы.ИнвентаризационнаяКомиссия.ТекущиеДанные;

	Если Не ТекущаяСтрокаТЧ.Председатель Тогда
		// Снимать флажок нельзя
		ТекущаяСтрокаТЧ.Председатель = Истина;
		Возврат;
	КонецЕсли;

	Для Каждого СтрокаТЧ Из Объект.ИнвентаризационнаяКомиссия Цикл
		Если СтрокаТЧ.НомерСтроки <> ТекущаяСтрокаТЧ.НомерСтроки Тогда
			СтрокаТЧ.Председатель = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура Заполнить(Команда)
	
	ТекстВопросаУдалениеСтрок = НСтр("ru = 'Перед заполнением все строки таблицы ""Объекты учета"" будут удалены. Продолжить?';
									|en = 'All rows in the ""Accounting objects"" table will be deleted before filling. Continue?'");
		
	Если Не ЗначениеЗаполнено(Объект.ОбъектыУчета)
		Или Ждать ВопросАсинх(ТекстВопросаУдалениеСтрок, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
		
		ЗаполнитьНаКлиенте();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборФизическихЛиц(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.Элементы);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);

	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаСписка",
		ПараметрыФормы,
		Элементы.ИнвентаризационнаяКомиссия,
		,
		,
		,
		,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКомиссиюИзПредыдущегоДокумента(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Организация';
																					|en = 'Company'"));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Объект.Организация");
		Возврат;
	КонецЕсли;
	
	ТекстПредупреждения = ЗаполнитьИнвентаризационнуюКомиссию();
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(,ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	// Ограничение выбора счета учета
	СчетаДляИнвентаризации = Документы.ИнвентаризацияОбъектовУчета.СчетаДляИнвентаризации(Объект.РазделУчета, Объект.Дата, Объект.Организация);
	СчетаУчета = СчетаДляИнвентаризации.СчетаУчета;
	
	Если Объект.СчетаУчета.Количество() = 0 Тогда
		Для Каждого Счет Из СчетаУчета Цикл
			НоваяСтрокаСчетаУчета = Объект.СчетаУчета.Добавить();
			НоваяСтрокаСчетаУчета.СчетУчета = Счет;
			НоваяСтрокаСчетаУчета.УчаствуетВРасчетах = Истина;
			Если Объект.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ПрочиеСчетаБухгалтерскогоУчета
				Или Объект.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета
				Или Объект.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.НезавершенноеПроизводство Тогда
				Если ЗначениеЗаполнено(СчетУчетаПодраздела) И Счет <> СчетУчетаПодраздела Тогда
					НоваяСтрокаСчетаУчета.УчаствуетВРасчетах = Ложь;
				КонецЕсли;
			КонецЕсли;
			Если Объект.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета
				И СчетаДляИнвентаризации.ИсключаемыеСчета.Найти(Счет) <> Неопределено Тогда
				НоваяСтрокаСчетаУчета.УчаствуетВРасчетах = Ложь;
			КонецЕсли;
		КонецЦикла;
	Иначе
		СчетаУчета = Объект.СчетаУчета.Выгрузить().ВыгрузитьКолонку("СчетУчета");
	КонецЕсли;
	
	УчетПоПодразделениям = Ложь; 
	УчетПоНаправлениямДеятельности = Ложь;
	Элементы.ОбъектыУчетаКоличество.Видимость = Ложь;
	Элементы.ОбъектыУчетаВалюта.Видимость = Ложь;
	Элементы.ОбъектыУчетаВалютнаяСумма.Видимость = Ложь;
	
	УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
	УсловиеОтбора.ИспользоватьВПроводках = Истина;
	УсловиеОтбора.СчетаИсключения = СчетаДляИнвентаризации.ИсключаемыеСчета;
	Для Каждого СтрокаСчетУчета Из Объект.СчетаУчета Цикл
		Если Не СтрокаСчетУчета.УчаствуетВРасчетах Тогда
			Продолжить;
		КонецЕсли;
		МассивСчетов = Новый Массив;
		МассивСчетов.Добавить(СтрокаСчетУчета.СчетУчета);
		СчетУчетаСубсчета = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(МассивСчетов, УсловиеОтбора);
		УстановитьВидимостьОбъектыУчета(СчетУчетаСубсчета, УчетПоПодразделениям, УчетПоНаправлениямДеятельности)
	КонецЦикла;
	Элементы.ПодразделениеОрганизации.Видимость = УчетПоПодразделениям;
	Элементы.ОбъектыУчетаПодразделение.Видимость = УчетПоПодразделениям; 
	
	Элементы.ОбъектыУчетаНаправлениеДеятельности.Видимость = УчетПоНаправлениямДеятельности;
	
	УсловияОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
	УсловияОтбора.ИспользоватьВПроводках = Истина;
	УсловияОтбора.СчетаИсключения = СчетаДляИнвентаризации.ИсключаемыеСчета;
	СчетаДляОтбора = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаУчета, УсловияОтбора);
	УсловияОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
	УсловияОтбора.СчетаИсключения = СчетаДляИнвентаризации.ИсключаемыеСчета;
	СчетаДляОтбораСРодителями = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаУчета, УсловияОтбора);
	
	ОтборПоПризнакуЗабалансовый = ?(Объект.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета , Истина, Ложь);
	
	Если Объект.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ТМЦВЭксплуатации Тогда 
		БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораСчета(Элементы.ОбъектыУчетаСчетУчета, СчетаДляОтбора);
	Иначе
		БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораСчета(Элементы.ОбъектыУчетаСчетУчета, СчетаДляОтбора,, ОтборПоПризнакуЗабалансовый);
	КонецЕсли;
	
	Если Объект.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ПрочиеСчетаБухгалтерскогоУчета Тогда
		СчетаДругихРазделов = Документы.ИнвентаризацияОбъектовУчета.СчетаДляИнвентаризации(Неопределено, Объект.Дата, Объект.Организация).СчетаУчета;
		УсловияОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
		СчетаДругихРазделовССубсчетами = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаДругихРазделов, УсловияОтбора);
		УсловияОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
		УсловияОтбора.СчетаИсключения = СчетаДругихРазделовССубсчетами;
		СчетаДляОтбора = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(Неопределено, УсловияОтбора);
		БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораСчета(Элементы.СчетаУчетаСчетУчета, СчетаДляОтбора,, ОтборПоПризнакуЗабалансовый, Неопределено);
	ИначеЕсли Объект.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ТМЦВЭксплуатации Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаДляОтбора,СчетаУчета);
		БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораСчета(Элементы.СчетаУчетаСчетУчета, СчетаДляОтбораСРодителями,,, Неопределено);
	Иначе
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаДляОтбора,СчетаУчета);
		БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораСчета(Элементы.СчетаУчетаСчетУчета, СчетаДляОтбораСРодителями,, ОтборПоПризнакуЗабалансовый, Неопределено);
	КонецЕсли;
	
	УстановитьФункциональныеОпцииФормы();
	ЗаполнитьДобавленныеКолонки();
	УстановитьЗаголовокФормы();
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаКлиенте()
	
	ДлительнаяОперация = НачатьЗаполнение();
	ОжидатьЗавершениеЗаполнения(ДлительнаяОперация);
	
КонецПроцедуры

&НаСервере
Функция НачатьЗаполнение()
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("Дата",              Объект.Дата);
	ПараметрыОперации.Вставить("Организация",       Объект.Организация);
	ПараметрыОперации.Вставить("РазделУчета",       Объект.РазделУчета);
	ПараметрыОперации.Вставить("Подразделение",     Объект.ПодразделениеОрганизации);
	ПараметрыОперации.Вставить("ОтветственноеЛицо", Объект.ОтветственноеЛицо);
	ВыбранныеСчета = Новый Массив;
	Для Каждого СтрокаСчета Из Объект.СчетаУчета Цикл
		Если ЗначениеЗаполнено(СтрокаСчета.СчетУчета) И СтрокаСчета.УчаствуетВРасчетах Тогда
			ВыбранныеСчета.Добавить(СтрокаСчета.СчетУчета);
		КонецЕсли;
	КонецЦикла;
	ПараметрыОперации.Вставить("СчетаУчета", ВыбранныеСчета);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"Документы.ИнвентаризацияОбъектовУчета.ДанныеПоУчету",
		ПараметрыОперации);
	
КонецФункции

&НаКлиенте
Процедура ОжидатьЗавершениеЗаполнения(ДлительнаяОперация)
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗавершениеОперации = Новый ОписаниеОповещения("ЗавершитьЗаполнение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ЗавершениеОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьЗаполнение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение НСтр("ru = 'Не удалось заполнить документ. Подробная информация в журнале регистрации.';
								|en = 'Cannot fill in the document. For details, see the Event log.'");
	КонецЕсли;
	
	ЗавершитьЗаполнениеНаСервере(Результат.АдресРезультата);
	
КонецПроцедуры

&НаСервере
Процедура ЗавершитьЗаполнениеНаСервере(АдресРезультата)
	
	ДанныеЗаполнения = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ОбъектыУчета.Загрузить(ДанныеЗаполнения.ОбъектыУчета);
	ЗаполнитьДобавленныеКолонки();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонки()
	
	// Установим еще раз видимость от конкретного списка счетов
	МассивСчетов = Объект.ОбъектыУчета.Выгрузить().ВыгрузитьКолонку("СчетУчета");
	СчетаУчета = ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(МассивСчетов);
	
	Если СчетаУчета.Количество() <> 0 Тогда
		УчетПоПодразделениям = Ложь;
		УчетПоНаправлениямДеятельности = Ложь;
		
		Элементы.ОбъектыУчетаКоличество.Видимость = Ложь;
		Элементы.ОбъектыУчетаВалюта.Видимость = Ложь;
		Элементы.ОбъектыУчетаВалютнаяСумма.Видимость = Ложь;
		
		УстановитьВидимостьОбъектыУчета(СчетаУчета, УчетПоПодразделениям, УчетПоНаправлениямДеятельности);
		Элементы.ПодразделениеОрганизации.Видимость = УчетПоПодразделениям;
		Элементы.ОбъектыУчетаПодразделение.Видимость = УчетПоПодразделениям;
		Элементы.ОбъектыУчетаНаправлениеДеятельности.Видимость = УчетПоНаправлениямДеятельности;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из Объект.ОбъектыУчета Цикл
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.СчетУчета) Тогда
			
			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетУчета);
			
			КоличествоСубконто = СвойстваСчета.КоличествоСубконто;
			Для Индекс = 1 По 3 Цикл
				СтрокаТаблицы["Субконто" + Индекс + "Доступность"] = (Индекс <= КоличествоСубконто) И Не СвойстваСчета["ВидСубконто" + Индекс + "ТолькоОбороты"];
			КонецЦикла;
			СтрокаТаблицы.СчетУчетаВалютный       = СвойстваСчета.Валютный;
			СтрокаТаблицы.СчетУчетаКоличественный = СвойстваСчета.Количественный; 
			
			Если СвойстваСчета.Вид = ВидСчета.Активный И СтрокаТаблицы.Сумма < 0 Тогда
				СтрокаТаблицы.Сумма = 0;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	//Субконто
	Для Сч = 1 По 3 Цикл
		
		// Если субконто нет на счете, то делаем его недоступным,
		// а для 2-го и 3-его субконто скрываем его полностью, чтобы не занимать лишнюю строку.
		// Первое субконто не скрываем, чтобы колонка не "мигала" для разных строк.
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОбъектыУчетаСубконто" + Сч);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"Объект.ОбъектыУчета.Субконто" + Сч + "Доступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
		Если Сч = 1 Тогда
			ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		Иначе
			ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
		КонецЕсли;
		
		// Пустое субконто в виде <...>
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОбъектыУчетаСубконто" + Сч);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"Объект.ОбъектыУчета.Субконто" + Сч + "Доступность", ВидСравненияКомпоновкиДанных.Равно, Истина);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"Объект.ОбъектыУчета.Субконто" + Сч, ВидСравненияКомпоновкиДанных.НеЗаполнено);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<...>';
																		|en = '<...>'"));
		
	КонецЦикла;
	
	// Количество
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОбъектыУчетаКоличество");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ОбъектыУчета.СчетУчетаКоличественный", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// ВалютнаяСумма
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОбъектыУчетаВалютнаяСумма");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ОбъектыУчета.СчетУчетаВалютный", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>';
																	|en = '<not required>'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// Валюта
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОбъектыУчетаВалюта");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ОбъектыУчета.СчетУчетаВалютный", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>';
																	|en = '<not required>'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовкиИДоступностьСубконто(Форма, Счет)
	
	ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
		"ОбъектыУчетаСубконто1",
		"ОбъектыУчетаСубконто2",
		"ОбъектыУчетаСубконто3");
	
	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(Счет, Форма, ПоляФормы, , Истина);
	
	ДанныеСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
	
	Форма.Элементы.ОбъектыУчетаПодразделение.Доступность           = ДанныеСчета.УчетПоПодразделениям;
	Форма.Элементы.ОбъектыУчетаНаправлениеДеятельности.Доступность = ДанныеСчета.УчетПоНаправлениямДеятельности;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ИзменитьПараметрыВыбораПолейСубконто(Форма)
	
	Если Форма.Элементы.ОбъектыУчета.ТекущаяСтрока <> Неопределено Тогда
		СтрокаТаблицы = Форма.Объект.ОбъектыУчета.НайтиПоИдентификатору(Форма.Элементы.ОбъектыУчета.ТекущаяСтрока);
		ПараметрыДокумента = ПолучитПараметрыВыбора(Форма, СтрокаТаблицы, "Субконто%Индекс%");
		БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораПолейСубконто(Форма, СтрокаТаблицы, "Субконто%Индекс%", "ОбъектыУчетаСубконто%Индекс%", ПараметрыДокумента);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитПараметрыВыбора(Форма, Объект, ШаблонИмяПоляОбъекта)
	
	СписокПараметров = Новый Структура;
	БухгалтерскийУчетКлиентСервер.ДополнитьСписокПараметровСубконто(СписокПараметров, Объект, ШаблонИмяПоляОбъекта);
	СписокПараметров.Вставить("Организация" , Форма.Объект.Организация);
	
	Возврат СписокПараметров;
	
КонецФункции

&НаСервере
Процедура СчетаУчетаПриОкончанииРедактированияНаСервере()
	ПодготовитьФормуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	СчетаУчетаПриОкончанииРедактированияНаСервере();
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ШаблонЗаголовка = НСтр("ru = 'Инвентаризация объектов учета %1 от %2';
								|en = 'Physical inventory count of accounting objects %1 dated %2'");
		ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонЗаголовка, Объект.Номер, Формат(Объект.Дата, "ДЛФ=D"));
	Иначе
		ТекстЗаголовка = НСтр("ru = 'Инвентаризация объектов учета (создание)';
								|en = 'Physical inventory count of accounting objects (create)'");;
	КонецЕсли;
	
	Заголовок = ТекстЗаголовка + " (" + Строка(Объект.РазделУчета) + ")";

КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьОбъектыУчета(СчетаУчета, УчетПоПодразделениям, УчетПоНаправлениямДеятельности)
	
	ФормироватьФинансовыйРезультат = ПолучитьФункциональнуюОпцию("ФормироватьФинансовыйРезультат");
	Для Каждого Счет Из СчетаУчета Цикл
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
		Если Не УчетПоПодразделениям И СвойстваСчета.УчетПоПодразделениям Тогда
			УчетПоПодразделениям = Истина;
		КонецЕсли; 
		Если ФормироватьФинансовыйРезультат И Не УчетПоНаправлениямДеятельности И СвойстваСчета.УчетПоНаправлениямДеятельности Тогда
			УчетПоНаправлениямДеятельности = Истина;
		КонецЕсли;
		Если Не Элементы.ОбъектыУчетаКоличество.Видимость И СвойстваСчета.Количественный Тогда
			Элементы.ОбъектыУчетаКоличество.Видимость = Истина;
		КонецЕсли;
		Если Не Элементы.ОбъектыУчетаВалюта.Видимость И СвойстваСчета.Валютный Тогда
			Элементы.ОбъектыУчетаВалюта.Видимость = Истина;
			Элементы.ОбъектыУчетаВалютнаяСумма.Видимость = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаУстановитьСнятьФлажки(Флаг)
	
	Для Каждого ТекущаяСтрока Из Объект.СчетаУчета Цикл
		
		ТекущаяСтрока.УчаствуетВРасчетах = Флаг;
		
	КонецЦикла;
	
КонецПроцедуры

#Область ИнвентаризационныеКомиссии

&НаСервере
Функция ЗаполнитьИнвентаризационнуюКомиссию()
	
	// Очищаем вне зависимости от результата, для того, чтобы пользователь увидел реальный результат работы функции
	Объект.ИнвентаризационнаяКомиссия.Очистить();
	
	ДанныеПредыдущегоДокумента = ДанныеИнвентаризационнойКомиссииПредыдущегоДокумента(
		Объект.Ссылка,
		Объект.Дата,
		Объект.Организация,
		Неопределено);
	
	Если ДанныеПредыдущегоДокумента.Комиссия.Количество() = 0 Тогда
		Возврат ДанныеПредыдущегоДокумента.ТекстПредупреждения;
	КонецЕсли;
	
	Для Каждого Элемент Из ДанныеПредыдущегоДокумента.Комиссия Цикл
		НоваяСтрокаКомиссии = Объект.ИнвентаризационнаяКомиссия.Добавить();
		НоваяСтрокаКомиссии.ФизЛицо = Элемент;
	КонецЦикла;
	
	Объект.ИнвентаризационнаяКомиссия[0].Председатель = Истина;
	
	Возврат ДанныеПредыдущегоДокумента.ТекстПредупреждения;
	
КонецФункции 

&НаКлиенте
Процедура Подключаемый_ИнвентаризационнаяКомиссияУдалитьДублирующуюСтроку(ОписаниеСтрок) Экспорт
	
	ТекущаяСтрокаТЧ = ОписаниеСтрок.ТабличнаяЧасть.НайтиПоИдентификатору(ОписаниеСтрок.Лишняя);
	
	Если ТекущаяСтрокаТЧ.Председатель Тогда
		ИндексУдаляемойСтроки = Объект.ИнвентаризационнаяКомиссия.Индекс(ТекущаяСтрокаТЧ);
		КоличествоСтрок = Объект.ИнвентаризационнаяКомиссия.Количество() - 1;

		Если КоличествоСтрок > 0 Тогда
			Если ИндексУдаляемойСтроки <= КоличествоСтрок - 1 Тогда
				ИндексНовогоПредседателя = ИндексУдаляемойСтроки + 1;
			Иначе
				ИндексНовогоПредседателя = КоличествоСтрок - 1;
			КонецЕсли;
			Объект.ИнвентаризационнаяКомиссия[ИндексНовогоПредседателя].Председатель = Истина;
		КонецЕсли;

	КонецЕсли;

	Если ТекущаяСтрокаТЧ <> Неопределено Тогда
		ОписаниеСтрок.ТабличнаяЧасть.Удалить(ТекущаяСтрокаТЧ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ШаблонТекстаФизЛицоВключеноВКомиссию()
	Возврат НСтр("ru = 'Физическое лицо %1 уже включено в состав комиссии';
				|en = 'Person %1 is already included in the commission'");
КонецФункции 

&НаСервереБезКонтекста
Функция ДанныеИнвентаризационнойКомиссииПредыдущегоДокумента(СсылкаНаДокумент, ДатаДокумента, Организация, Отбор)

	ШаблонТекстаЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ИнвентаризационнаяКомиссия.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПредыдущаяИнвентаризация
	|ИЗ
	|	Документ.ИнвентаризацияОбъектовУчета.ИнвентаризационнаяКомиссия КАК ИнвентаризационнаяКомиссия
	|ГДЕ
	|	ИнвентаризационнаяКомиссия.Ссылка.Проведен
	|	И ИнвентаризационнаяКомиссия.Ссылка.Организация = &Организация
	|	И ИнвентаризационнаяКомиссия.Ссылка.Дата <= &Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИнвентаризационнаяКомиссия.Ссылка.МоментВремени УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставКомиссии.Ссылка КАК Ссылка,
	|	СоставКомиссии.ФизЛицо КАК ФизЛицо,
	|	СоставКомиссии.Председатель КАК Председатель
	|ИЗ
	|	Документ.ИнвентаризацияОбъектовУчета.ИнвентаризационнаяКомиссия КАК СоставКомиссии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПредыдущаяИнвентаризация КАК ПредыдущаяИнвентаризация
	|		ПО СоставКомиссии.Ссылка = ПредыдущаяИнвентаризация.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Председатель УБЫВ,
	|	СоставКомиссии.НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
		
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ШаблонТекстаЗапроса);
	
	МетаданныеДокумента = СсылкаНаДокумент.Метаданные();
	
	ПсевдонимТаблицыСсылкиДокумента = "ИнвентаризационнаяКомиссия";
	ПсевдонимТаблицыСоставаКомиссии = "СоставКомиссии";
	
	ИмяТаблицыСТабличнойЧастью = ИмяТаблицыИнвентаризационнаяКомиссия(МетаданныеДокумента);
	ТаблицаСсылкиДокумента = НайтиТаблицуПоПсевдониму(СхемаЗапроса, ПсевдонимТаблицыСсылкиДокумента);
	ЗаменитьТаблицуВСхемеЗапроса(ТаблицаСсылкиДокумента, ИмяТаблицыСТабличнойЧастью, ПсевдонимТаблицыСсылкиДокумента);
	ТаблицаСоставаКомиссии = НайтиТаблицуПоПсевдониму(СхемаЗапроса, ПсевдонимТаблицыСоставаКомиссии);
	ЗаменитьТаблицуВСхемеЗапроса(ТаблицаСоставаКомиссии, ИмяТаблицыСТабличнойЧастью, ПсевдонимТаблицыСоставаКомиссии);
	
	Запрос = Новый Запрос;
	
	Если Не СсылкаНаДокумент.Пустая() Тогда
		УстановитьОтбор(Запрос,
		ТаблицаСсылкиДокумента,
		ПсевдонимТаблицыСсылкиДокумента,
		"<>",
		"Ссылка",
		СсылкаНаДокумент);
	КонецЕсли;
	
	ТекстЗапросаДоОтбора = СхемаЗапроса.ПолучитьТекстЗапроса(); // на случай, если поиск с отбором не даст результата
	
	Если ЗначениеЗаполнено(Отбор) Тогда
		ПсевдонимТаблицыДляОтбора = СтрШаблон("%1.Ссылка", ПсевдонимТаблицыСсылкиДокумента);
		Для Каждого Элемент Из Отбор Цикл
			УстановитьОтбор(
				Запрос,
				ТаблицаСсылкиДокумента,
				ПсевдонимТаблицыДляОтбора,
				"=",
				Элемент.Ключ,
				Элемент.Значение);
		КонецЦикла;
	КонецЕсли;
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Дата", КонецДня(ДатаДокумента));
	РезультатЗапроса = Запрос.Выполнить();
	
	// Часто на небольших предприятиях, в простых случаях инвентаризационная комиссия одна.
	// На более сложных может быть несколько комиссий: территориально удаленных, занимающихся разными классами активов.
	//
	// Поэтому, последовательно ищем комиссию от сложных случаев к простым
	// - с заданным отбором в документах того же вида
	// - без отбора в документах того же вида
	// - без отбора в документах других видов
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Возврат РезультатПоискаИнвентаризационнойКомиссии(МетаданныеДокумента, Организация, РезультатЗапроса);
	КонецЕсли;
	
	СхемаЗапроса = Создать(ТекстЗапросаДоОтбора);
	
	Если ЗначениеЗаполнено(Отбор) Тогда
		
		// Поля отбора добавим в результирующий запрос, чтобы была возможность описать результат поиска
		ЗапросРезультата = НайтиТаблицуПоПсевдониму(СхемаЗапроса, ПсевдонимТаблицыСоставаКомиссии);
		Для Каждого Элемент Из Отбор Цикл
			Выражение = СтрШаблон("%1.Ссылка.%2", ПсевдонимТаблицыСоставаКомиссии, Элемент.Ключ);
			УстановитьВыражение(ЗапросРезультата, Элемент.Ключ, Выражение);
		КонецЦикла;
		
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Возврат РезультатПоискаИнвентаризационнойКомиссии(МетаданныеДокумента, Организация, РезультатЗапроса, Отбор);
		КонецЕсли;
		
	КонецЕсли;
	
	ВидыДокументов = ВидыДокументовИнвентаризационнаяКомиссия();
	Для Каждого ВидДокумента Из ВидыДокументов Цикл
		
		Если ВидДокумента = МетаданныеДокумента Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ПравоДоступа("Просмотр", ВидДокумента) Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяТаблицыСТабличнойЧастью = ИмяТаблицыИнвентаризационнаяКомиссия(ВидДокумента);
		ТаблицаСсылкиДокумента = НайтиТаблицуПоПсевдониму(СхемаЗапроса, ПсевдонимТаблицыСсылкиДокумента);
		ЗаменитьТаблицуВСхемеЗапроса(ТаблицаСсылкиДокумента, ИмяТаблицыСТабличнойЧастью, ПсевдонимТаблицыСсылкиДокумента);
		ТаблицаСоставаКомиссии = НайтиТаблицуПоПсевдониму(СхемаЗапроса, ПсевдонимТаблицыСоставаКомиссии);
		ЗаменитьТаблицуВСхемеЗапроса(ТаблицаСоставаКомиссии, ИмяТаблицыСТабличнойЧастью, ПсевдонимТаблицыСоставаКомиссии);
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Возврат РезультатПоискаИнвентаризационнойКомиссии(МетаданныеДокумента, Организация, РезультатЗапроса, ВидДокумента);
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат РезультатПоискаИнвентаризационнойКомиссии(МетаданныеДокумента, Организация);
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяТаблицыИнвентаризационнаяКомиссия(МетаданныеДокумента)
	
	Возврат СтрШаблон("%1.ИнвентаризационнаяКомиссия", МетаданныеДокумента.ПолноеИмя());
	
КонецФункции

&НаСервереБезКонтекста
Функция РезультатПоискаИнвентаризационнойКомиссии(МетаданныеДокумента, Организация, РезультатПоиска = Неопределено, УсловияНеточногоПоиска = Неопределено)
	
	Данные = Новый Структура;
	Данные.Вставить("Комиссия",            Новый Массив);
	Данные.Вставить("ТекстПредупреждения", "");
	
	СведенияДокумента = Неопределено;
	
	Если РезультатПоиска <> Неопределено Тогда
		ВыборкаДокумент = РезультатПоиска.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаДокумент.Следующий() Тогда
			СведенияДокумента = ВыборкаДокумент;
			ВыборкаКомиссия = ВыборкаДокумент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаКомиссия.Следующий() Цикл
				Данные.Комиссия.Добавить(ВыборкаКомиссия.ФизЛицо);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Данные.Комиссия) Тогда
		
		Если Справочники.Организации.ИспользуетсяНесколькоОрганизаций() Тогда
			Данные.ТекстПредупреждения = НСтр("ru = 'Состав комиссии не был заполнен, так как по этой организации нет предыдущих документов инвентаризации';
												|en = 'The commission is not filled, as there are no previous inventory count documents for this company'");
		Иначе
			Данные.ТекстПредупреждения = НСтр("ru = 'Состав комиссии не был заполнен, так как нет предыдущих документов инвентаризации';
												|en = 'The commission is not filled, as there are no previous inventory count documents'");
		КонецЕсли;
		
		Возврат Данные;
		
	КонецЕсли;
	
	Если УсловияНеточногоПоиска = Неопределено Тогда
		// найдено точно
		Возврат Данные;
	КонецЕсли;
	
	// опишем, в чем именно отступили от заданных условий поиска
	ТипНеточногоПоиска = ТипЗнч(УсловияНеточногоПоиска);
	Если ТипНеточногоПоиска = Тип("Структура") И СведенияДокумента <> Неопределено Тогда
		
		ПоляПоиска = Новый Структура;
		ПоляПоиска.Вставить("Искали", Новый Структура); // все поля отбора
		ПоляПоиска.Искали.Вставить("Заполненные", Новый Массив); // разделяем на заполненные и пустые в связи с принятым в русском языке построением фразы
		ПоляПоиска.Искали.Вставить("Пустые",      Новый Массив);
		ПоляПоиска.Вставить("Нашли", ОбщегоНазначения.СкопироватьРекурсивно(ПоляПоиска.Искали)); // только отличающиеся от тех, по которым искали
		
		Для Каждого ПолеПоиска Из УсловияНеточногоПоиска Цикл
			ДобавитьПредставлениеПоляПоиска(ПоляПоиска.Искали, МетаданныеДокумента, ПолеПоиска.Ключ, ПолеПоиска.Значение);
			НайденноеЗначение = СведенияДокумента[ПолеПоиска.Ключ];
			Если НайденноеЗначение <> ПолеПоиска.Значение Тогда
				ДобавитьПредставлениеПоляПоиска(ПоляПоиска.Нашли, МетаданныеДокумента, ПолеПоиска.Ключ, НайденноеЗначение);
			КонецЕсли;
		КонецЦикла;
		
		Шаблон = НСтр("ru = 'Состав комиссии заполнен на основании данных предыдущего документа %1, так как нет документов с заполненной комиссией %2';
						|en = 'The commission is filled based on the data of the previous %1 document, since there are no documents with the filled %2 commission '");
		Данные.ТекстПредупреждения = СтрШаблон(Шаблон, ПредставлениеПолейПоиска(ПоляПоиска.Нашли), ПредставлениеПолейПоиска(ПоляПоиска.Искали));
		
	ИначеЕсли ТипНеточногоПоиска = Тип("ОбъектМетаданных") Тогда
		
		Если Справочники.Организации.ИспользуетсяНесколькоОрганизаций() Тогда
			Шаблон = НСтр("ru = 'Состав комиссии заполнен на основании данных документа ""%1"", так как по этой организации нет документов ""%2"" с заполненной комиссией';
							|en = 'The commission is filled based on the data of the %1 document, since there are no %2 documents with the filled commission for this company'");
		Иначе
			Шаблон = НСтр("ru = 'Состав комиссии заполнен на основании данных документа ""%1"", так как нет документов ""%2"" с заполненной комиссией';
							|en = 'The commission is filled based on the data of the %1 document, since there are no %2 documents with the filled commission'");
		КонецЕсли;
		
		Данные.ТекстПредупреждения = СтрШаблон(Шаблон, УсловияНеточногоПоиска, МетаданныеДокумента);
		
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

&НаСервереБезКонтекста
Функция ВидыДокументовИнвентаризационнаяКомиссия()
	
	ВидыДокументов = Новый Массив;
	ВидыДокументов.Добавить(Метаданные.Документы.ИнвентаризацияОбъектовУчета);
	Возврат ВидыДокументов;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ДобавитьПредставлениеПоляПоиска(НаборыПредставлений, МетаданныеДокумента, ИмяРеквизита, Значение)
	
	СинонимРеквизита = ОбщегоНазначенияБПКлиентСервер.ДекапитализироватьСтроку(МетаданныеДокумента.Реквизиты[ИмяРеквизита].Синоним);
	
	ПредставлениеПоляПоиска = Строка(Значение);
	Если ЗначениеЗаполнено(Значение) Тогда
		
		Набор = НаборыПредставлений.Заполненные;
		
		Падеж = 3; // Дательный, "по чему?"
		ПредставлениеРеквизита = СклонениеПредставленийОбъектов.ПросклонятьПредставление(СинонимРеквизита, Падеж);
		Если Не ПустаяСтрока(ПредставлениеРеквизита) Тогда
			ПредставлениеПоляПоиска = СтрШаблон(НСтр("ru = '%1 ""%2""';
													|en = '%1 ""%2""'"), ПредставлениеРеквизита, Значение);
		КонецЕсли;
		
	Иначе
		
		Набор = НаборыПредставлений.Пустые;
		
		Падеж = 2; // родительный, "без указания чего?"
		ПредставлениеРеквизита = СклонениеПредставленийОбъектов.ПросклонятьПредставление(СинонимРеквизита, Падеж);
		Если ПустаяСтрока(ПредставлениеРеквизита) Тогда
			ПредставлениеПоляПоиска = ОбщегоНазначенияБПКлиентСервер.ПредставлениеНезаполненногоЗначения();
		Иначе
			ПредставлениеПоляПоиска = ПредставлениеРеквизита;
		КонецЕсли;
		
	КонецЕсли;
	
	Набор.Добавить(ПредставлениеПоляПоиска);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеПолейПоиска(НаборыПредставлений)
	
	// Пример: по подразделению "Офис Дмитровское шоссе, 9" и без указания склада
	
	ВсеПоля = ОбщегоНазначения.СкопироватьРекурсивно(НаборыПредставлений.Заполненные);
	Если ЗначениеЗаполнено(ВсеПоля) Тогда
		ВсеПоля[0] = СтрШаблон(НСтр("ru = 'по %1';
									|en = 'to %1'"), ВсеПоля[0]);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НаборыПредставлений.Пустые) Тогда
		ИндексПервогоПустогоПоля = ВсеПоля.Количество();
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеПоля, НаборыПредставлений.Пустые);
		ВсеПоля[ИндексПервогоПустогоПоля] = СтрШаблон(НСтр("ru = 'без указания %1';
															|en = 'without specifying %1'"), ВсеПоля[ИндексПервогоПустогоПоля]);
	КонецЕсли;
	
	Возврат ОбщегоНазначенияБПКлиентСервер.ПредставлениеСписка(ВсеПоля);
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаменитьТаблицуВСхемеЗапроса(ТочкаЗапроса, ИмяТаблицы, Псевдоним)
	
	Таблица = ТочкаЗапроса.Запрос.ДоступныеТаблицы.Найти(ИмяТаблицы);
	ИсточникиДанных = ТочкаЗапроса.Оператор.Источники;
	ИндексТаблицы = ИсточникиДанных.Индекс(ТочкаЗапроса.Источник);
	ИсточникиДанных.Заменить(ИндексТаблицы, Таблица);
	ТочкаЗапроса.Таблица.Псевдоним = Псевдоним;

	Возврат ТочкаЗапроса;
	
КонецФункции 

&НаСервереБезКонтекста
Функция НайтиТаблицуПоПсевдониму(Запрос, Псевдоним)
	
	СхемаЗапроса = СхемаЗапроса(Запрос);
	
	Для Каждого ОписаниеЗапроса Из СхемаЗапроса.ПакетЗапросов Цикл
		
		Если ТипЗнч(ОписаниеЗапроса) <> Тип("ЗапросВыбораСхемыЗапроса") Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ОператорЗапроса Из ОписаниеЗапроса.Операторы Цикл
			
			ОписаниеИсточника = ОператорЗапроса.Источники.НайтиПоПсевдониму(Псевдоним);
			Если ОписаниеИсточника = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Возврат НовыйТочкаЗапроса(
				СхемаЗапроса,
				ОписаниеЗапроса,
				ОператорЗапроса,
				ОписаниеИсточника,
				ОписаниеИсточника.Источник);
				
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервереБезКонтекста
Функция НовыйТочкаЗапроса(Схема, Запрос, Оператор = Неопределено, Источник = Неопределено, Таблица = Неопределено)
	
	Контекст = Новый Структура;
	
	Контекст.Вставить("Схема",  Схема);
	Контекст.Вставить("Запрос", Запрос);
	Если Оператор <> Неопределено Тогда
		Контекст.Вставить("Оператор", Оператор);
	КонецЕсли;
	Если Источник <> Неопределено Тогда
		Контекст.Вставить("Источник", Источник);
	КонецЕсли;
	Если Таблица <> Неопределено Тогда
		Контекст.Вставить("Таблица",  Таблица);
	КонецЕсли;
	
	Возврат Контекст;
	
КонецФункции

&НаСервереБезКонтекста
Функция СхемаЗапроса(Запрос)
	
	Если ТипЗнч(Запрос) = Тип("Строка") Тогда
		Возврат Создать(Запрос);
	Иначе
		Возврат Запрос;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция УстановитьВыражение(ТочкаЗапроса, ИмяПоля, Выражение, ЗаменятьУстановленное = Истина)
	
	Если ТипЗнч(Выражение) = Тип("ВыражениеСхемыЗапроса") Тогда
		ВыражениеСхемыЗапроса = Выражение;
	Иначе
		ВыражениеСхемыЗапроса = Новый ВыражениеСхемыЗапроса(Выражение);
	КонецЕсли;
	
	ОператорыДляУстановки = Новый Массив;
	Если ТочкаЗапроса.Свойство("Оператор") Тогда
		ОператорыДляУстановки.Добавить(ТочкаЗапроса.Оператор);
	Иначе
		ОператорыДляУстановки = ТочкаЗапроса.Запрос.Операторы;
	КонецЕсли;
	
	Колонка = ТочкаЗапроса.Запрос.Колонки.Найти(ИмяПоля);
	ВыражениеСтрокой = Строка(ВыражениеСхемыЗапроса);
	
	Для Каждого Оператор Из ОператорыДляУстановки Цикл
		
		ИндексОператора = ТочкаЗапроса.Запрос.Операторы.Индекс(Оператор);
		
		Если Колонка = Неопределено Тогда
			ИндексКолонки = ТочкаЗапроса.Запрос.Колонки.Количество();
			Оператор.ВыбираемыеПоля.Добавить(ВыражениеСтрокой);
			Колонка = ТочкаЗапроса.Запрос.Колонки[ИндексКолонки];
			Колонка.Псевдоним = ИмяПоля;
		ИначеЕсли Колонка.Поля[ИндексОператора] = Неопределено Или ЗаменятьУстановленное Тогда
			УстановитьВыражениеПоля(ТочкаЗапроса.Запрос, Колонка, Оператор, ВыражениеСхемыЗапроса);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Колонка;
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьВыражениеПоля(Запрос, Колонка, Оператор, Знач Выражение)
	
	ИндексОператора = Запрос.Операторы.Индекс(Оператор);
	
	Если Колонка.Поля[ИндексОператора] = Неопределено Тогда
		ИндексКолонки   = Запрос.Колонки.Индекс(Колонка);
		Оператор.ВыбираемыеПоля.Добавить(Строка(Выражение), ИндексКолонки);
	Иначе
		ИндексПоля = ИндексВыбираемогоПоля(Запрос, Колонка, Оператор);
		Если ТипЗнч(Выражение) = Тип("Строка") Тогда
			Выражение = Новый ВыражениеСхемыЗапроса(Выражение);
		КонецЕсли;
		Оператор.ВыбираемыеПоля.Установить(ИндексПоля, Выражение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИндексВыбираемогоПоля(Запрос, Колонка, Оператор)
	
	// Коллекция выбираемых полей хранит только заполненные поля,
	// поэтому индекс выбираемого поля может быть меньше индекса колонки
	
	Если ТипЗнч(Оператор) = Тип("Число") Тогда
		ИндексОператора = Оператор;
	Иначе
		ИндексОператора = Запрос.Операторы.Индекс(Оператор);
	КонецЕсли;
	
	ИндексПоля = -1;
	
	Для ИндексПредыдущейКолонки = 0 По Запрос.Колонки.Индекс(Колонка) Цикл
		ПредыдущаяКолонка = Запрос.Колонки[ИндексПредыдущейКолонки];
		Если ПредыдущаяКолонка.Поля[ИндексОператора] = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ИндексПоля = ИндексПоля + 1;
	КонецЦикла;
	
	Возврат ИндексПоля;

КонецФункции

&НаСервереБезКонтекста
Функция Создать(ТекстЗапроса) 
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	Возврат СхемаЗапроса;
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьОтбор(Запрос, ТочкаЗапроса, ТаблицаЗапроса, ВидСравнения, ПараметрОтбора, ЗначениеПараметра);

	ШаблонОтбора = СтрШаблон("%1.%2 %3 &%2", ТаблицаЗапроса, ПараметрОтбора, ВидСравнения);
	ТочкаЗапроса.Оператор.Отбор.Добавить(ШаблонОтбора);
	Запрос.УстановитьПараметр(ПараметрОтбора, ЗначениеПараметра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
