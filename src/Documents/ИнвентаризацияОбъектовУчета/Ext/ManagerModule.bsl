
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает таблицу остатков по объектам учета по переданному списку счетов.
//
// Параметры:
//  Параметры - Структура:
//    Дата - Дата
//    Организация - СправочникСсылка.Организации
//    ОтветственноеЛицо - СправочникСсылка.ФизическиеЛица
//    Подразделение - СправочникСсылка.ПодразделенияОрганизаций
//    РазделУчета - ПеречислениеСсылка.РазделыУчетаДляИнвентаризации
//    СчетаУчета - Массив
// 
// Возвращаемое значение:
//  Структура:
//    ОбъектыУчета - ТаблицаЗначений
//
Функция ДанныеПоУчету(Параметры) Экспорт
	
	РезультатЗаполнения = Новый Структура;
	РезультатЗаполнения.Вставить("ОбъектыУчета", Новый ТаблицаЗначений); 
	
	Если Параметры.СчетаУчета.Количество() = 0 Тогда
		Возврат РезультатЗаполнения; 
	КонецЕсли;
	
	УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
	УсловиеОтбора.ИспользоватьВПроводках = Истина;
	Если Параметры.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложения
		Или Параметры.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.Запасы
		Или Параметры.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСКонтрагентами 
		Тогда
		СчетаДляИнвентаризации = СчетаДляИнвентаризации(Параметры.РазделУчета, Параметры.Дата, Параметры.Организация);
		УсловиеОтбора.СчетаИсключения = СчетаДляИнвентаризации.ИсключаемыеСчета;
	КонецЕсли;
	СчетаУчета = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(Параметры.СчетаУчета, УсловиеОтбора);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК СчетУчета,
	|	ХозрасчетныйОстатки.Счет.Порядок КАК СчетУчетаПорядок,
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
	|	ХозрасчетныйОстатки.Субконто3 КАК Субконто3,
	|	ХозрасчетныйОстатки.КоличествоОстаток КАК Количество,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет.Вид <> ЗНАЧЕНИЕ(ВидСчета.Активный)
	|				И ХозрасчетныйОстатки.СуммаОстаток < 0
	|			ТОГДА -ХозрасчетныйОстатки.СуммаОстаток
	|		ИНАЧЕ ХозрасчетныйОстатки.СуммаОстаток
	|	КОНЕЦ КАК Сумма,
	|	ХозрасчетныйОстатки.Валюта КАК Валюта,
	|	ХозрасчетныйОстатки.ВалютнаяСуммаОстаток КАК ВалютнаяСумма,
	|	ХозрасчетныйОстатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ХозрасчетныйОстатки.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОстатки.Субконто1) = ТИП(Справочник.ФизическиеЛица)
	|			ТОГДА ХозрасчетныйОстатки.Субконто1
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОстатки.Субконто2) = ТИП(Справочник.ФизическиеЛица)
	|			ТОГДА ХозрасчетныйОстатки.Субконто2
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОстатки.Субконто3) = ТИП(Справочник.ФизическиеЛица)
	|			ТОГДА ХозрасчетныйОстатки.Субконто3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоМОЛ
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Дата, Счет В (&СчетаУчета), , Организация = &Организация) КАК ХозрасчетныйОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.СчетУчета КАК СчетУчета,
	|	Остатки.Субконто1 КАК Субконто1,
	|	Остатки.Субконто2 КАК Субконто2,
	|	Остатки.Субконто3 КАК Субконто3,
	|	Остатки.Количество КАК Количество,
	|	Остатки.Сумма КАК Сумма,
	|	Остатки.Валюта КАК Валюта,
	|	Остатки.ВалютнаяСумма КАК ВалютнаяСумма,
	|	Остатки.Подразделение КАК Подразделение,
	|	Остатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Остатки.СубконтоМОЛ КАК СубконтоМОЛ
	|ИЗ
	|	Остатки КАК Остатки
	|ГДЕ
	|	&УсловиеПодразделение
	|	И &УсловиеОтветственноеЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетУчетаПорядок,
	|	Подразделение,
	|	Субконто1,
	|	Субконто2,
	|	Субконто3
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Дата", КонецМесяца(Параметры.Дата)+1);
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Запрос.УстановитьПараметр("СчетаУчета", СчетаУчета);
	Если ЗначениеЗаполнено(Параметры.Подразделение) Тогда
		Запрос.УстановитьПараметр("Подразделение", Параметры.Подразделение);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПодразделение", "Подразделение = &Подразделение");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПодразделение", "Истина");
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.ОтветственноеЛицо) Тогда
		Запрос.УстановитьПараметр("ОтветственноеЛицо", Параметры.ОтветственноеЛицо);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОтветственноеЛицо", "(СубконтоМОЛ = &ОтветственноеЛицо Или СубконтоМОЛ = НЕОПРЕДЕЛЕНО)");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОтветственноеЛицо", "Истина");
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	РезультатЗаполнения.ОбъектыУчета = Результат.Выгрузить();
	Возврат РезультатЗаполнения; 
	
КонецФункции

// Возвращает описание выбранных счетов документа, участвующих в инвентаризации.
//
// Параметры:
//   СчетаУчета - ТабличнаяЧасть
//
// Возвращаемое значение:
//   Строка
//
Функция ПереченьСчетовДокумента(СчетаУчета) Экспорт
	
	СчетаУчаствующиеВРасчетах = Новый Массив;
	
	Для Каждого ТекущийСчет Из СчетаУчета Цикл
		
		Если ТекущийСчет.УчаствуетВРасчетах Тогда
			СчетаУчаствующиеВРасчетах.Добавить(ТекущийСчет.СчетУчета);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтрСоединить(СчетаУчаствующиеВРасчетах, ", ");
	
КонецФункции

// Возвращает список счетов, по которым требуется инвентаризация.
//
// Параметры:
//    РазделУчета - ПеречислениеСсылка.РазделыУчетаДляИнвентаризации
//                - Неопределено - для получения списка счетов по всем разделам кроме забалансовых и прочих счетов.
//    Дата - Дата
//    Организация - СправочникСсылка.Организации
// 
// Возвращаемое значение:
//  Структура:
//    СчетаУчета - Массив
//    ИсключаемыеСчета - Массив
//
Функция СчетаДляИнвентаризации(РазделУчета, Дата, Организация) Экспорт
	
	СчетаДляИнвентаризации = Новый Структура;
	СчетаУчета = Новый Массив;
	ИсключаемыеСчета = Новый Массив;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ОсновныеСредства
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ОсновныеСредства);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.АмортизацияОсновныхСредств);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ДоходныеВложенияВ_МЦ);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.НематериальныеАктивы
		Или РазделУчета = Неопределено Тогда 
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.НематериальныеАктивы);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.АмортизацияНематериальныхАктивов);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложения
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы);
		
		СубконтоИсключить = Новый Массив;
		СубконтоИсключить.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
		КоличествоСубконтоИсключить = СубконтоИсключить.Количество();
		УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
		УсловиеОтбора.ИспользоватьВПроводках = Истина;
		СчетаУчетаСубсчета = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаУчета, УсловиеОтбора);
		Для Каждого Счет Из СчетаУчетаСубсчета Цикл
			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
			НомерСубконто = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
			Если НомерСубконто <> Неопределено Тогда
				ИсключаемыеСчета.Добавить(Счет);
			КонецЕсли;
			НомерСубконто = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
			Если НомерСубконто <> Неопределено Тогда
				ИсключаемыеСчета.Добавить(Счет);
			КонецЕсли;
			НомерСубконто = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства);
			Если НомерСубконто <> Неопределено Тогда
				ИсключаемыеСчета.Добавить(Счет);
			КонецЕсли;
		КонецЦикла; 
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.ПриобретениеОбъектовОсновныхСредств);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.Запасы 
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.Материалы);                        // 10
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.Полуфабрикаты);                    // 21
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.Товары);                           // 41
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ГотоваяПродукция);                 // 43
			
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.ТорговаяНаценка);                            // 42
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.ГТД);                                        // ГТД   
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.СпецоснасткаИСпецодеждаВЭксплуатации);       // 10.11
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИсключаемыеСчета,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.СпецоснасткаИСпецодеждаВЭксплуатации));
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.МатериалыПереданныеВПереработку);            // 10.07 
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные);                          // 45
		// Исключим также забалансовые, в помощнике инвентаризации они в отдельном разделе.
		УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
		УсловиеОтбора.ИспользоватьВПроводках = Истина;
		УсловиеОтбора.Забалансовый = Истина;
		СчетаУчетаЗабалансовые = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(Новый Массив, УсловиеОтбора);
		Для Каждого Счет Из СчетаУчетаЗабалансовые Цикл
			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
			НомерСубконто = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
			Если НомерСубконто <> Неопределено Тогда
				ИсключаемыеСчета.Добавить(Счет);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.НезавершенноеПроизводство 
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ОсновноеПроизводство);        // 20
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ВспомогательныеПроизводства); //23
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.НДСПоПриобретеннымЦенностям 
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымЦенностям);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.Касса 
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.Касса);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.БанковскиеСчета 
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетныеСчета);   // 51
		ИспользоватьВалютныйУчет = БухгалтерскийУчетПереопределяемый.ИспользоватьВалютныйУчет();
		Если ИспользоватьВалютныйУчет Тогда
			СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ВалютныеСчета);// 52
		КонецЕсли;
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.СпециальныеСчета); // 55
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ФинансовыеВложения 
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ФинансовыеВложения);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСКонтрагентами 
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоКраткосрочнымКредитамИЗаймам);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоДолгосрочнымКредитамИЗаймам);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСУчредителями);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами);
		
		// 76.01.2 - Платежи (взносы) по добровольному страхованию на случай смерти и причинения вреда здоровью
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.ПлатежиПоДобровольномуСтрахованиюРаботников); 
		// 76.01.9 - Платежи (взносы) по прочим видам страхования
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИсключаемыеСчета,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПлатежиПоПрочимВидамСтрахования));
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.КорректировкаРасчетовПрошлогоПериода);
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоНДСОтложенномуДляУплатыВБюджет);
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыНДСНалоговогоАгента);
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.НДСНачисленныйПоОтгрузке);
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоДепонированнымСуммам);
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоБронированию);
		ИсключаемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоБронированиюВал); 
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ОтложенныеНалоговыеАктивыОбязательства 
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ОтложенныеНалоговыеАктивы);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ОтложенныеНалоговыеОбязательства);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ОценочныеОбязательства 
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РезервыПредстоящихРасходов);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасходыБудущихПериодов
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ПлатежиПоДобровольномуСтрахованиюРаботников); // 76.07.2
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ПлатежиПоПрочимВидамСтрахования);             // 76.07.9
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасходыБудущихПериодов);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ДоходыБудущихПериодов 
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ДоходыБудущихПериодов);
	КонецЕсли; 
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСПерсоналом
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами_);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоПрочимОперациям);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоДепонированнымСуммам);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоБронированию);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоБронированиюВал);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыПоНалогамИСоциальномуСтрахованию
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоНалогам);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоСоциальномуСтрахованию);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ПрочиеСчетаБухгалтерскогоУчета Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ЖивотныеНаВыращиванииИОткорме);    //11
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ЗаготовлениеИПриобретениеМЦ);      //15
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ОтклонениеВСтоимостиМЦ);           //16
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.БракВПроизводстве);                //28
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ОбслуживающиеПроизводства);        //29
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ПереводыВПути_);                   //57
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ВнутрихозяйственныеРасчеты);       //79
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ЦелевоеФинансирование);            //86
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.НедостачиИПотериОтПорчиЦенностей); //94
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета 
		Или РазделУчета = Неопределено Тогда
		УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
		УсловиеОтбора.Забалансовый = Истина;
		СчетаУчетаЗабалансовые = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(Новый Массив, УсловиеОтбора);
		Для Каждого Счет Из СчетаУчетаЗабалансовые Цикл
			КодСчета = Лев(Счет.Код, 3);
			Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(КодСчета) И Счет.Родитель = ПланыСчетов.Хозрасчетный.ПустаяСсылка() Тогда
				СчетаУчета.Добавить(Счет);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложенияПоОС
		Или РазделУчета = Неопределено Тогда
		СчетаУчетаОтбора = ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы;
		УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
		УсловиеОтбора.ИспользоватьВПроводках = Истина;
		СчетаУчетаСубсчета = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаУчетаОтбора, УсловиеОтбора);
		Для Каждого Счет Из СчетаУчетаСубсчета Цикл
			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
			НомерСубконто = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
			Если НомерСубконто <> Неопределено Тогда
				СчетаУчета.Добавить(Счет);
			КонецЕсли;
			НомерСубконто = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства);
			Если НомерСубконто <> Неопределено Тогда
				СчетаУчета.Добавить(Счет);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.КомпонентыОСИОборудованиеКУстановке
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке);
		СчетаУчетаОтбора = ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы;
		УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
		УсловиеОтбора.ИспользоватьВПроводках = Истина;
		СчетаУчетаСубсчета = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаУчетаОтбора, УсловиеОтбора);
		Для Каждого Счет Из СчетаУчетаСубсчета Цикл
			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
			НомерСубконто = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
			Если НомерСубконто <> Неопределено Тогда
				СчетаУчета.Добавить(Счет);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ТМЦВЭксплуатации
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.СпецоснасткаИСпецодеждаВЭксплуатации);
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.МатериальныеЦенностиВЭксплуатации);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.МатериалыПереданныеВПереработку
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.МатериалыПереданныеВПереработку);
	КонецЕсли; 
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ТоварыОтгруженные
		Или РазделУчета = Неопределено Тогда
		СчетаУчета.Добавить(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные); 
	КонецЕсли;
	
	СчетаДляИнвентаризации.Вставить("СчетаУчета", СчетаУчета);
	СчетаДляИнвентаризации.Вставить("ИсключаемыеСчета", ИсключаемыеСчета);
	Возврат СчетаДляИнвентаризации;
	
КонецФункции

// Групповое создание документов
// 
// Параметры:
//  Параметры - Структура параметров создания документов:
//				- Организация - СправочникСсылка.Организации - Организация документов.
//				- Дата - Период проведения инвентаризации.
//				- РазделыУчета - Массив выбранных разделов учета, по которым нужно создавать документы.
//
Процедура СоздатьДокументы(Параметры) Экспорт
	
	СчетаУчетаРазделов = Новый ТаблицаЗначений();
	СчетаУчетаРазделов.Колонки.Добавить("РазделУчета"       , Новый ОписаниеТипов("ПеречислениеСсылка.РазделыУчетаДляИнвентаризации"));
	СчетаУчетаРазделов.Колонки.Добавить("Счет"              , Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	
	МассивСчетов = Новый Массив;
	МассивСчетовПоРазделам = Новый Массив;
	
	Для Каждого РазделУчета Из Параметры.РазделыУчета Цикл
		
		СчетаДляИнвентаризации = Документы.ИнвентаризацияОбъектовУчета.СчетаДляИнвентаризации(РазделУчета, Параметры.Дата, Параметры.Организация);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСчетов, СчетаДляИнвентаризации.СчетаУчета);
		СчетаДляИнвентаризации.Вставить("РазделУчета", РазделУчета);
		МассивСчетовПоРазделам.Добавить(СчетаДляИнвентаризации);
		
	КонецЦикла;
	
	МассивСчетовССубСчетами = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(МассивСчетов);
	ТаблицаСчетовПоРазделам = ОбщегоНазначенияУТ.МассивВТаблицуЗначений(МассивСчетовПоРазделам);
	
	Для Каждого Счет Из МассивСчетовССубСчетами Цикл
		
		РазделНайден = Ложь;
		Для Каждого РазделУчета Из Параметры.РазделыУчета Цикл
			
			Если РазделНайден Тогда
				Прервать;
			КонецЕсли;
			
			СтрокаРазделУчета = ТаблицаСчетовПоРазделам.Найти(РазделУчета, "РазделУчета");
			Если СтрокаРазделУчета <> Неопределено Тогда
				
				Для Каждого СчетРодитель Из СтрокаРазделУчета.СчетаУчета Цикл
					Если (Счет.ПринадлежитЭлементу(СчетРодитель) И СтрокаРазделУчета.ИсключаемыеСчета.Найти(Счет) = Неопределено)
						ИЛИ Счет = СчетРодитель Тогда
						Строка = СчетаУчетаРазделов.Добавить();
						Строка.РазделУчета = РазделУчета;
						Строка.Счет        = Счет;
						РазделНайден       = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СчетаУчета" , СчетаУчетаРазделов);
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Запрос.УстановитьПараметр("Дата"       , Новый Граница(КонецДня(Параметры.Дата), ВидГраницы.Включая));
	Запрос.Текст = "ВЫБРАТЬ
	               |	СчетаУчета.Счет КАК Счет,
	               |	СчетаУчета.РазделУчета КАК РазделУчета
	               |ПОМЕСТИТЬ ВТ_СчетаУчета
	               |ИЗ
	               |	&СчетаУчета КАК СчетаУчета
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Счет
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ХозрасчетныйОстатки.Счет КАК Счет,
	               |	ХозрасчетныйОстатки.Счет.Порядок КАК СчетПорядок,
	               |	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	               |	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
	               |	ХозрасчетныйОстатки.Субконто3 КАК Субконто3,
	               |	ХозрасчетныйОстатки.КоличествоОстаток КАК Количество,
	               |	ВЫБОР
	               |		КОГДА ХозрасчетныйОстатки.Счет.Вид <> ЗНАЧЕНИЕ(ВидСчета.Активный)
	               |				И ХозрасчетныйОстатки.СуммаОстаток < 0
	               |			ТОГДА -ХозрасчетныйОстатки.СуммаОстаток
	               |		ИНАЧЕ ХозрасчетныйОстатки.СуммаОстаток
	               |	КОНЕЦ КАК Сумма,
	               |	ХозрасчетныйОстатки.Валюта КАК Валюта,
	               |	ХозрасчетныйОстатки.ВалютнаяСуммаОстаток КАК ВалютнаяСумма,
	               |	ХозрасчетныйОстатки.Подразделение КАК Подразделение, 
	               |	ВЫБОР
	               |		КОГДА ХозрасчетныйОстатки.Подразделение ЕСТЬ NULL
	               |			ТОГДА Значение(Справочник.СтруктураПредприятия.ПустаяСсылка)
	               |		ИНАЧЕ ХозрасчетныйОстатки.Подразделение
	               |	КОНЕЦ КАК ПодразделениеОрганизации,
	               |	ХозрасчетныйОстатки.НаправлениеДеятельности КАК НаправлениеДеятельности
	               |ПОМЕСТИТЬ Данные
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Остатки(
	               |			&Дата,
	               |			Счет В
	               |				(ВЫБРАТЬ
	               |					ВТ_СчетаУчета.Счет
	               |				ИЗ
	               |					ВТ_СчетаУчета),
	               |			,
	               |			Организация = &Организация) КАК ХозрасчетныйОстатки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СчетаУчета КАК ВТ_СчетаУчета
	               |		ПО ХозрасчетныйОстатки.Счет = ВТ_СчетаУчета.Счет
	               |ГДЕ
	               |	&УсловиеПодразделение
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Счет
	               |;
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_СчетаУчета.РазделУчета КАК РазделУчета,
	               |	Данные.Счет КАК СчетУчета,
	               |	Данные.Субконто1 КАК Субконто1,
	               |	Данные.Субконто2 КАК Субконто2,
	               |	Данные.Субконто3 КАК Субконто3,
	               |	Данные.Количество КАК Количество,
	               |	Данные.Сумма КАК Сумма,
	               |	Данные.Валюта КАК Валюта,
	               |	Данные.ВалютнаяСумма КАК ВалютнаяСумма,
	               |	Данные.Подразделение КАК Подразделение,
	               |	Данные.ПодразделениеОрганизации КАК ПодразделениеШапки,
	               |	Данные.НаправлениеДеятельности КАК НаправлениеДеятельности
	               |ИЗ
	               |	Данные КАК Данные
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СчетаУчета КАК ВТ_СчетаУчета
	               |		ПО Данные.Счет = ВТ_СчетаУчета.Счет
	               |ГДЕ
	               |	&УсловиеПодразделение
	               |УПОРЯДОЧИТЬ ПО 
	               |	СчетПорядок,
	               |	ПодразделениеШапки,
	               |	Субконто1,
	               |	Субконто2,
	               |	Субконто3";
	
	Если ЗначениеЗаполнено(Параметры.ПодразделениеОрганизации) Тогда
		Запрос.УстановитьПараметр("Подразделение", Параметры.ПодразделениеОрганизации);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПодразделение", "Подразделение = &Подразделение");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УПОРЯДОЧИТЬ ПО", "УПОРЯДОЧИТЬ ПО РазделУчета,");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПодразделение", "Истина");
		Если Параметры.ПоПодразделениям Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "УПОРЯДОЧИТЬ ПО", "УПОРЯДОЧИТЬ ПО РазделУчета, ПодразделениеШапки,");
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "УПОРЯДОЧИТЬ ПО", "УПОРЯДОЧИТЬ ПО РазделУчета,");
		КонецЕсли;
	КонецЕсли;
	ДанныеУчета = Запрос.Выполнить().Выбрать();
	Пока ДанныеУчета.СледующийПоЗначениюПоля("РазделУчета") Цикл
			
		Если Параметры.ПоПодразделениям Тогда
			Пока ДанныеУчета.СледующийПоЗначениюПоля("ПодразделениеШапки") Цикл
				ДокументИнвентаризация = СоздатьДокументИнвентаризации(Параметры, ДанныеУчета, ТаблицаСчетовПоРазделам);
				ЗаполнитьДокумент(ДокументИнвентаризация, ДанныеУчета);
				ДокументИнвентаризация.ПодразделениеОрганизации = ДанныеУчета.ПодразделениеШапки;
				Если ДокументИнвентаризация.ПроверитьЗаполнение() Тогда
					ДокументИнвентаризация.Записать(РежимЗаписиДокумента.Проведение);
				Иначе
					ДокументИнвентаризация.Записать();
				КонецЕсли;
			КонецЦикла;	
		Иначе
			ДокументИнвентаризация = СоздатьДокументИнвентаризации(Параметры, ДанныеУчета, ТаблицаСчетовПоРазделам);
			ЗаполнитьДокумент(ДокументИнвентаризация,ДанныеУчета);
				Если ДокументИнвентаризация.ПроверитьЗаполнение() Тогда
					ДокументИнвентаризация.Записать(РежимЗаписиДокумента.Проведение);
				Иначе
					ДокументИнвентаризация.Записать();
				КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("РеестрДокументов");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов - таблиц значений - данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.ИнвентаризацияОбъектовУчета") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем 

#Область СтандартныеПодсистемы_УправлениеДоступом

// СтандартныеПодсистемы.УправлениеДоступом

//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеОрганизации)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область СтандартныеПодсистемы_Свойства

// СтандартныеПодсистемы.Свойства
// 
// Получает описание предопределенных наборов свойств.
//
// Параметры:
//  Наборы - ДеревоЗначений - с колонками:
//     * Имя           - Строка - Имя набора свойств. Формируется из полного имени объекта
//                       метаданных заменой символа "." на "_".
//                       Например, "Документ_ЗаказПокупателя".
//     * Идентификатор - УникальныйИдентификатор - Идентификатор ссылки предопределенного элемента.
//     * Используется  - Неопределено, Булево - Признак того, что набор свойств используется.
//                       Например, можно использовать для скрытия набора по функциональным опциям.
//                       Значение по умолчанию - Неопределено, соответствует значению Истина.
//     * ЭтоГруппа     - Булево - Истина, если набор свойств является группой.
//
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	Набор = Наборы.Строки.Добавить();
	Набор.Имя = "Документ_ИнвентаризацияОбъектовУчета";
	Набор.Идентификатор = Новый УникальныйИдентификатор("ade6b723-2a8b-417b-9dad-b6d804ea66be");
	Набор.Используется  = Истина;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область СтандартныеПодсистемы_Печать

// СтандартныеПодсистемы.Печать
// 
// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт инвентаризации
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ИНВ";
	КомандаПечати.Представление = НСтр("ru = 'Акт инвентаризации';
										|en = 'Physical inventory count report'");
		
	// ИНВ-22 (Приказ о проведении инвентаризации)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ИНВ22";
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьИНВ22";
	КомандаПечати.Представление = НСтр("ru = 'ИНВ-22 (Приказ о проведении инвентаризации)';
										|en = 'INV-22 (Stocktaking order)'");
	
КонецПроцедуры

// Функция формирует данные для печатной формы ИНВ22
// 
// Параметры:
//  ПараметрыПечати - Структура - Параметры печати
//  МассивОбъектов - Массив из ДокументСсылка.ИнвентаризацияОС - Список документов
// 
// Возвращаемое значение:
//  РезультатЗапроса - Данные для печатной формы ИНВ22
//
Функция ДанныеДляПечатнойФормыИНВ22(ПараметрыПечати, МассивОбъектов) Экспорт 
	
	КС = Новый КвалификаторыСтроки();
	Массив = Новый Массив;
	Массив.Добавить(Тип("Строка"));
	ОписаниеТиповС = Новый ОписаниеТипов(Массив, , КС);
	
	ПредставлениеРазделовУчета = Новый ТаблицаЗначений();
	ПредставлениеРазделовУчета.Колонки.Добавить("Раздел", Новый ОписаниеТипов("ПеречислениеСсылка.РазделыУчетаДляИнвентаризации"));
	ПредставлениеРазделовУчета.Колонки.Добавить("Шапка", ОписаниеТиповС);
	ПредставлениеРазделовУчета.Колонки.Добавить("Подвал", ОписаниеТиповС);
	
	РазделыУчета = Перечисления.РазделыУчетаДляИнвентаризации.ДанныеВыбораПоФункциональнымОпциям();
	
	Для Каждого Раздел Из РазделыУчета Цикл
		СтрокаПредставленияРаздела = ПредставлениеРазделовУчета.Добавить();
		СтрокаПредставленияРаздела.Раздел = Раздел.Значение;
		Если Раздел.Значение = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСКонтрагентами Тогда 
			СтрокаПредставленияРаздела.Шапка = НСтр("ru = 'расчетов с покупателями, поставщиками и прочими дебиторами и кредиторами';
													|en = 'settlements with customers, suppliers and other debtors and creditors'");
			СтрокаПредставленияРаздела.Подвал = НСтр("ru = 'расчеты с покупателями, поставщиками и прочими дебиторами и кредиторами';
													|en = 'settlements with customers, suppliers and other debtors and creditors'");
		ИначеЕсли Раздел.Значение = Перечисления.РазделыУчетаДляИнвентаризации.ТМЦВЭксплуатации Тогда 
			СтрокаПредставленияРаздела.Шапка = НСтр("ru = 'товарно-материальных ценностей, находящихся в эксплуатации';
													|en = 'inventory in operation'");
			СтрокаПредставленияРаздела.Подвал = НСтр("ru = 'товарно-материальные ценности, находящиеся в эксплуатации';
													|en = 'inventory in operation'"); 
		ИначеЕсли Раздел.Значение = Перечисления.РазделыУчетаДляИнвентаризации.Запасы Тогда 
			СтрокаПредставленияРаздела.Шапка = НСтр("ru = 'товарно-материальных ценностей';
													|en = 'inventory'");
			СтрокаПредставленияРаздела.Подвал = НСтр("ru = 'товарно-материальные ценности';
													|en = 'Inventory'");
		ИначеЕсли Раздел.Значение = Перечисления.РазделыУчетаДляИнвентаризации.НДСПоПриобретеннымЦенностям Тогда
			СтрокаПредставленияРаздела.Шапка = НСтр("ru = 'НДС по приобретенным ценностям';
													|en = 'VAT on acquired assets'");
			СтрокаПредставленияРаздела.Подвал = НСтр("ru = 'НДС по приобретенным ценностям';
													|en = 'VAT on acquired assets'");
		ИначеЕсли Раздел.Значение = Перечисления.РазделыУчетаДляИнвентаризации.Касса Тогда
			СтрокаПредставленияРаздела.Шапка = НСтр("ru = 'денежных средств, разных ценностей и документов, находящихся в кассе';
													|en = 'cash, various assets, and documents in the cash account'");
			СтрокаПредставленияРаздела.Подвал = НСтр("ru = 'денежные средства, разные ценности и документы, находящиеся в кассе';
													|en = 'cash, various assets, and documents in the cash account'"); 
		ИначеЕсли Раздел.Значение = Перечисления.РазделыУчетаДляИнвентаризации.БанковскиеСчета Тогда
			СтрокаПредставленияРаздела.Шапка = НСтр("ru = 'денежных средств, находящихся на счетах';
													|en = 'cash on accounts'");
			СтрокаПредставленияРаздела.Подвал = НСтр("ru = 'денежные средства, находящиеся на счетах';
													|en = 'cash on accounts'");
		ИначеЕсли Раздел.Значение = Перечисления.РазделыУчетаДляИнвентаризации.НематериальныеАктивы Тогда
			СтрокаПредставленияРаздела.Шапка = НСтр("ru = 'нематериальных активов';
													|en = 'intangible assets'");
			СтрокаПредставленияРаздела.Подвал = НСтр("ru = 'нематериальные активы';
													|en = 'intangible assets'");
		ИначеЕсли Раздел.Значение = Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложенияПоОС Тогда
			СтрокаПредставленияРаздела.Шапка = НСтр("ru = 'капитальных вложений по основным средствам';
													|en = 'capital fixed asset expenditures'");
			СтрокаПредставленияРаздела.Подвал = НСтр("ru = 'капитальные вложения по основным средствам';
													|en = 'capital fixed asset expenditures'"); 
		ИначеЕсли Раздел.Значение = Перечисления.РазделыУчетаДляИнвентаризации.КомпонентыОСИОборудованиеКУстановке Тогда
			СтрокаПредставленияРаздела.Шапка = НСтр("ru = 'компонентов основных средств и оборудования к установке';
													|en = 'fixed asset components and equipment to install'");
			СтрокаПредставленияРаздела.Подвал = НСтр("ru = 'компоненты основных средств и оборудование к установке';
													|en = 'fixed asset components and equipment to install'");
		ИначеЕсли Раздел.Значение = Перечисления.РазделыУчетаДляИнвентаризации.ПрочиеСчетаБухгалтерскогоУчета Тогда
			СтрокаПредставленияРаздела.Шапка = НСтр("ru = 'прочих счетов бухгалтерского учета';
													|en = 'Other bookkeeping accounts'");
			СтрокаПредставленияРаздела.Подвал = НСтр("ru = 'прочие счета бухгалтерского учета';
													|en = 'other bookkeeping accounts'");
		ИначеЕсли Раздел.Значение = Перечисления.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета Тогда
			СтрокаПредставленияРаздела.Шапка = НСтр("ru = 'забалансовых счетов';
													|en = 'off-balance accounts'");
			СтрокаПредставленияРаздела.Подвал = НСтр("ru = 'забалансовые счета';
													|en = 'off-balance accounts'");
		Иначе
			СтрокаПредставленияРаздела.Шапка = НРег(СклонениеПредставленийОбъектов.ПросклонятьПредставление(Раздел.Значение, 2));
			СтрокаПредставленияРаздела.Подвал = НРег(Раздел); 
		КонецЕсли;
	КонецЦикла;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	Запрос.УстановитьПараметр("РазделыУчета", ПредставлениеРазделовУчета);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТЗРазделыУчета.Раздел КАК Раздел,
	|	ТЗРазделыУчета.Шапка КАК Шапка,
	|	ТЗРазделыУчета.Подвал КАК Подвал
	|	ПОМЕСТИТЬ РазделыУчета
	|ИЗ
	|	&РазделыУчета КАК ТЗРазделыУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнвентаризацияОбъектовУчета.Ссылка КАК Ссылка,
	|	ИнвентаризацияОбъектовУчета.Дата КАК Дата,
	|	ИнвентаризацияОбъектовУчета.Номер КАК Номер,
	|	РазделыУчета.Шапка КАК НаименованиеИмуществаОбязательствШапка,
	|	РазделыУчета.Подвал КАК НаименованиеИмуществаОбязательствПодвал,
	|	ИнвентаризацияОбъектовУчета.Организация КАК Организация,
	|	ИнвентаризацияОбъектовУчета.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ИнвентаризацияОбъектовУчета.ПодразделениеОрганизации.Наименование КАК Подразделение,
	|	ИнвентаризацияОбъектовУчета.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	ИнвентаризацияОбъектовУчета.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	ИнвентаризацияОбъектовУчета.ДокументОснованиеДата КАК ДокументОснованиеДата,
	|	ИнвентаризацияОбъектовУчета.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	ИнвентаризацияОбъектовУчета.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации,
	|	ИнвентаризацияОбъектовУчета.ИнвентаризационнаяКомиссия.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		ФизЛицо.Наименование КАК ФИОПолные,
	|		"""" КАК Должность,
	|		Председатель КАК Председатель
	|	) КАК ИнвентаризационнаяКомиссия
	|ИЗ
	|	Документ.ИнвентаризацияОбъектовУчета КАК ИнвентаризацияОбъектовУчета
	|	ЛЕВОЕ СОЕДИНЕНИЕ РазделыУчета КАК РазделыУчета
	|		ПО ИнвентаризацияОбъектовУчета.РазделУчета = РазделыУчета.Раздел
	|ГДЕ
	|	ИнвентаризацияОбъектовУчета.Ссылка В(&МассивСсылок)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - см. УправлениеПечатьюПереопределяемый.ПриПечати.МассивОбъектов
//  ПараметрыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыПечати
//  КоллекцияПечатныхФорм - см. УправлениеПечатьюПереопределяемый.ПриПечати.КоллекцияПечатныхФорм
//  ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
//  ПараметрыВывода - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыВывода
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Проверяем, нужно ли для макета ИНВ формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИНВ") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ИНВ", НСтр("ru = 'Акт инвентаризации';
																								|en = 'Physical inventory count report'"), 
			ПечатьИНВ(МассивОбъектов, ОбъектыПечати, ПараметрыПечати), , "Документ.ИнвентаризацияОбъектовУчета.ПФ_MXL_ИНВ");
		
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#Область СтандартныеПодсистемы_ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// @skip-check module-empty-method
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьИНВ(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы   = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентаризацияОбъектовУчета_ИНВ";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИнвентаризацияОбъектовУчета.ПФ_MXL_ИНВ");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Номер КАК Номер,
	|	Реквизиты.Организация КАК Организация, 
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Реквизиты.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	Реквизиты.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	ВЫБОР
	|		КОГДА Реквизиты.ДокументОснованиеДата = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА Реквизиты.Дата
	|		ИНАЧЕ Реквизиты.ДокументОснованиеДата
	|	КОНЕЦ КАК ДокументОснованиеДата,
	|	Реквизиты.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	Реквизиты.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации,
	|	Реквизиты.ДокументОснованиеВид КАК ДокументОснованиеВид,
	|	Реквизиты.РазделУчета КАК РазделУчета,
	|	Реквизиты.ОтветственноеЛицо КАК ОтветственноеЛицо
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.ИнвентаризацияОбъектовУчета КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Номер КАК Номер,
	|	ОбъектыУчета.Субконто1 КАК Субконто1,
	|	ОбъектыУчета.Субконто2 КАК Субконто2,
	|	ОбъектыУчета.Субконто3 КАК Субконто3,
	|	ОбъектыУчета.СчетУчета КАК НомерСчета,
	|	ОбъектыУчета.СчетУчета.Наименование КАК СчетНаименование,
	|	ЕСТЬNULL(ОбъектыУчета.Сумма, 0) КАК Стоимость,
	|	ЕСТЬNULL(ОбъектыУчета.ВалютнаяСумма, 0) КАК СтоимостьВал,
	|	ОбъектыУчета.Подразделение КАК Подразделение,
	|	ОбъектыУчета.Валюта КАК Валюта,
	|	ОбъектыУчета.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ОбъектыУчета.Количество КАК Количество,
	|	ОбъектыУчета.НомерСтроки КАК НомерСтроки,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Реквизиты.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	Реквизиты.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	Реквизиты.ДокументОснованиеДата КАК ДокументОснованиеДата,
	|	Реквизиты.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	Реквизиты.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации,
	|	Реквизиты.ДокументОснованиеВид КАК ДокументОснованиеВид,
	|	Реквизиты.ОтветственноеЛицо КАК ОтветственноеЛицо,
	|	Реквизиты.РазделУчета КАК РазделУчета
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияОбъектовУчета.ОбъектыУчета КАК ОбъектыУчета
	|		ПО Реквизиты.Ссылка = ОбъектыУчета.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Субконто1,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	ОбъектыУчета.СчетУчета КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА ОбъектыУчета.СчетУчета.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА ОбъектыУчета.СчетУчета
	|		КОГДА ОбъектыУчета.СчетУчета.Родитель.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА ОбъектыУчета.СчетУчета.Родитель
	|		ИНАЧЕ ОбъектыУчета.СчетУчета.Родитель.Родитель
	|	КОНЕЦ КАК СчетУчетаРодитель,
	|	Реквизиты.РазделУчета КАК РазделУчета,
	|	ОбъектыУчета.СчетУчета.Наименование КАК СчетУчетаНаименование,
	|	ОбъектыУчета.СчетУчета.Код КАК СчетУчетаКод,
	|	ОбъектыУчета.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ СчетаУчета
	|ИЗ
	|	Документ.ИнвентаризацияОбъектовУчета.ОбъектыУчета КАК ОбъектыУчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО ОбъектыУчета.Ссылка = Реквизиты.Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаУчета.Ссылка КАК Ссылка,
	|	СчетаУчета.Дата КАК Дата,
	|	СчетаУчета.СчетУчета КАК СчетУчета,
	|	СчетаУчета.СчетУчетаРодитель КАК СчетУчетаРодитель,
	|	СчетаУчета.РазделУчета КАК РазделУчета,
	|	СчетаУчета.СчетУчетаНаименование КАК СчетУчетаНаименование,
	|	СчетаУчета.СчетУчетаКод КАК СчетУчетаКод,
	|	МАКСИМУМ(Хозрасчетный.Количественный) КАК Количественный,
	|	МАКСИМУМ(Хозрасчетный.УчетПоПодразделениям) КАК УчетПоПодразделениям,
	|	МАКСИМУМ(Хозрасчетный.УчетПоНаправлениямДеятельности) КАК УчетПоНаправлениямДеятельности,
	|	МИНИМУМ(СчетаУчета.НомерСтроки) КАК НомерСтроки
	|ИЗ
	|	СчетаУчета КАК СчетаУчета
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|	ПО СчетаУчета.Ссылка = Хозрасчетный.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетаУчета.Ссылка,
	|	СчетаУчета.СчетУчетаНаименование,
	|	СчетаУчета.Дата,
	|	СчетаУчета.СчетУчетаКод,
	|	СчетаУчета.РазделУчета,
	|	СчетаУчета.СчетУчетаРодитель,
	|	СчетаУчета.СчетУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаУчета.Ссылка КАК Ссылка,
	|	СчетаУчета.Дата КАК Дата,
	|	МИНИМУМ(СчетаУчета.СчетУчета) КАК СчетУчета,
	|	СчетаУчета.СчетУчетаРодитель КАК СчетУчетаРодитель,
	|	СчетаУчета.РазделУчета КАК РазделУчета,
	|	МИНИМУМ(СчетаУчета.СчетУчетаНаименование) КАК СчетУчетаНаименование,
	|	МИНИМУМ(СчетаУчета.СчетУчетаКод) КАК СчетУчетаКод,
	|	МИНИМУМ(СчетаУчета.НомерСтроки) КАК НомерСтроки
	|ИЗ
	|	СчетаУчета КАК СчетаУчета
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетаУчета.Ссылка,
	|	СчетаУчета.Дата,
	|	СчетаУчета.СчетУчетаРодитель,
	|	СчетаУчета.РазделУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Ссылка КАК Ссылка,
	|	Данные.Дата КАК Дата,
	|	Данные.Номер КАК Номер,
	|	Данные.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА Данные.Организация.КодОКВЭД2 = """"
	|			ТОГДА Данные.Организация.КодОКВЭД
	|		ИНАЧЕ Данные.Организация.КодОКВЭД2
	|	КОНЕЦ КАК ВидДеятельности,
	|	Данные.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА Данные.НомерСчета.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА Данные.НомерСчета
	|		КОГДА Данные.НомерСчета.Родитель.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА Данные.НомерСчета.Родитель
	|		ИНАЧЕ Данные.НомерСчета.Родитель.Родитель
	|	КОНЕЦ КАК СчетУчетаРодитель,
	|	Данные.НомерСчета КАК НомерСчета,
	|	Данные.НомерСчета.Наименование КАК НомерСчетаНаименование,
	|	Данные.Подразделение КАК Подразделение,
	|	Данные.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Данные.Субконто1    КАК Субконто1,
	|	Данные.Субконто2    КАК Субконто2,
	|	Данные.Субконто3    КАК Субконто3,
	|	Данные.Стоимость    КАК Стоимость,
	|	Данные.Количество   КАК Количество,
	|	Данные.Валюта       КАК Валюта,
	|	Данные.НомерСтроки  КАК НомерСтр,
	|	Данные.СтоимостьВал КАК СтоимостьВал,
	|	Данные.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	Данные.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	Данные.ДокументОснованиеДата КАК ДокументОснованиеДата,
	|	Данные.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	Данные.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации,
	|	Данные.ДокументОснованиеВид КАК ДокументОснованиеВид,
	|	Данные.ОтветственноеЛицо КАК ОтветственноеЛицо,
	|	Данные.РазделУчета КАК РазделУчета,
	|	ВЫБОР
	|		КОГДА Данные.Субконто1 ССЫЛКА Справочник.БанковскиеСчетаОрганизаций
	|			ИЛИ Данные.Субконто1 ССЫЛКА Справочник.БанковскиеСчетаКонтрагентов
	|			ТОГДА Данные.Субконто1.Банк.Наименование
	|	КОНЕЦ КАК НаименованиеБанка,
	|	ВЫБОР
	|		КОГДА Данные.Субконто1 ССЫЛКА Справочник.БанковскиеСчетаОрганизаций
	|			ИЛИ Данные.Субконто1 ССЫЛКА Справочник.БанковскиеСчетаКонтрагентов
	|			ТОГДА Данные.Субконто1.НомерСчета
	|	КОНЕЦ КАК НомерБанковскогоСчета
	|ИЗ
	|	Данные КАК Данные
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Номер,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Номер КАК Номер,
	|	ЕСТЬNULL(ИнвентаризационнаяКомиссия.Председатель, ИСТИНА) КАК Председатель,
	|	ЕСТЬNULL(ИнвентаризационнаяКомиссия.НомерСтроки, 1) КАК НомерСтроки,
	|	ЕСТЬNULL(ИнвентаризационнаяКомиссия.ФизЛицо, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК ФизЛицо
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияОбъектовУчета.ИнвентаризационнаяКомиссия КАК ИнвентаризационнаяКомиссия
	|		ПО Реквизиты.Ссылка = ИнвентаризационнаяКомиссия.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Номер,
	|	Председатель УБЫВ,
	|	НомерСтроки";

	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ИндексПоследнегоРезультата = РезультатЗапроса.Количество() - 1;
	
	СчетаУчетаТаблица = РезультатЗапроса[ИндексПоследнегоРезультата - 3].Выгрузить();
	СчетаУчетаРодителиТаблица = РезультатЗапроса[ИндексПоследнегоРезультата - 2].Выгрузить();
	
	Док = РезультатЗапроса[ИндексПоследнегоРезультата - 1].Выбрать();
	ИнвентаризационнаяКомиссия = РезультатЗапроса[ИндексПоследнегоРезультата].Выбрать();
	
	ПервыйДокумент = Истина;
		
	СвойстваСчета = Новый Структура(); 
	СвойстваСчета.Вставить("УчетПоПодразделениюИНД", Ложь);
	СвойстваСчета.Вставить("Количественный",         Ложь);
	СвойстваСчета.Вставить("Валютный",               Ложь);
	
	// Варианты заголовков разделов с подписями печатной формы	
	ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей();
	КоличествоЧленовКомиссииУнифицированнойФормы = 3;
	
	Пока Док.СледующийПоЗначениюПоля("Ссылка") Цикл
		Комиссия = Новый Структура();
		Комиссия.Вставить("ПараметрыПодписиПредседатель",Новый Массив());
		Комиссия.Вставить("ПараметрыПодписи",Новый Массив());

		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь; 
		
		СчетаУчета = СчетаУчетаТаблица.НайтиСтроки(Новый Структура("Ссылка", Док.Ссылка));
		СчетаУчетаРодители = СчетаУчетаРодителиТаблица.НайтиСтроки(Новый Структура("Ссылка", Док.Ссылка));
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ПараметрыЗаголовкаТаблицыИСтроки = НовыйПараметрыЗаголовкаТаблицыИСтроки();
		ПервыйСчет = Истина;

		Если ИнвентаризационнаяКомиссия.СледующийПоЗначениюПоля("Ссылка") Тогда
			ИнвентаризационнаяКомиссия.Следующий();
			ПараметрыПодписиПредседатель = НовыйПараметрыПодписи();
			ПараметрыПодписиДок = Новый Массив();
			ПараметрыПодписиПредседатель.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ПредседательКомиссии;
			ДанныеПредседателя = УправлениеВнеоборотнымиАктивамиПереопределяемый.ДанныеФизЛица(Док.Организация, ИнвентаризационнаяКомиссия.ФизЛицо, ИнвентаризационнаяКомиссия.Дата);
			
			ПараметрыПодписиПредседатель.Должность = ДанныеПредседателя.Должность;
			ПараметрыПодписиПредседатель.РасшифровкаПодписи = ДанныеПредседателя.Представление;
			
			Для НомерЧленаКомиссии = 1 По КоличествоЧленовКомиссииУнифицированнойФормы Цикл
				ПараметрыПодписи = НовыйПараметрыПодписи();
				Если НомерЧленаКомиссии = 1 Тогда
					ПараметрыПодписи.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ЧленыКомиссии;
				КонецЕсли;
				Если ИнвентаризационнаяКомиссия.Следующий() Тогда
					ДанныеЧленаКомиссии = УправлениеВнеоборотнымиАктивамиПереопределяемый.ДанныеФизЛица(Док.Организация, ИнвентаризационнаяКомиссия.ФизЛицо, ИнвентаризационнаяКомиссия.Дата);
					ПараметрыПодписи.Должность = ДанныеЧленаКомиссии.Должность;
					ПараметрыПодписи.РасшифровкаПодписи = ДанныеЧленаКомиссии.Представление;
				КонецЕсли;
				ПараметрыПодписиДок.Добавить(ПараметрыПодписи);
			КонецЦикла;
			
			Пока ИнвентаризационнаяКомиссия.Следующий() Цикл
				ДанныеЧленаКомиссии = УправлениеВнеоборотнымиАктивамиПереопределяемый.ДанныеФизЛица(Док.Организация, ИнвентаризационнаяКомиссия.ФизЛицо, ИнвентаризационнаяКомиссия.Дата);
				ПараметрыПодписи.Должность = ДанныеЧленаКомиссии.Должность;
				ПараметрыПодписи.РасшифровкаПодписи = ДанныеЧленаКомиссии.Представление;
				ПараметрыПодписиДок.Добавить(ПараметрыПодписи);
			КонецЦикла;
			Комиссия.Вставить("ПараметрыПодписиПредседатель",ПараметрыПодписиПредседатель);
			Комиссия.Вставить("ПараметрыПодписи",ПараметрыПодписиДок);
		Конецесли; 
		
		Если СчетаУчета.Количество() = 0 Тогда
			// Печать для пустой ТЧ ОбъектыУчета
			ПечатьПоСчетуУчета(Док, Комиссия, ТабличныйДокумент, Макет, ПараметрыЗаголовкаТаблицыИСтроки, СвойстваСчета);
		Иначе
			// Если несколько разноплановых счетов в одном документе, то отдельные печатные формы на каждый такой счет.
			Если Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ПрочиеСчетаБухгалтерскогоУчета
				Или Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета Тогда
				ПараметрыПодписиДок = Новый Массив();
				Для Каждого СтрокаСчетаУчетаРодители Из СчетаУчетаРодители Цикл
					
					Если Не ПервыйСчет Тогда
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					ПервыйСчет = Ложь;
					СчетаУчетаТек = СчетаУчетаТаблица.НайтиСтроки(Новый Структура("Ссылка, СчетУчетаРодитель", Док.Ссылка, СтрокаСчетаУчетаРодители.СчетУчетаРодитель));
					ЗаполнитьПараметрыЗаголовкаТаблицыИСтроки(СчетаУчетаТек, ПараметрыЗаголовкаТаблицыИСтроки, СвойстваСчета);
					Если Док.СледующийПоЗначениюПоля("СчетУчетаРодитель") Тогда
						ПечатьПоСчетуУчета(Док, Комиссия, ТабличныйДокумент, Макет, ПараметрыЗаголовкаТаблицыИСтроки, СвойстваСчета, СтрокаСчетаУчетаРодители);
					КонецЕсли;
				КонецЦикла;
			Иначе
				ЗаполнитьПараметрыЗаголовкаТаблицыИСтроки(СчетаУчета, ПараметрыЗаголовкаТаблицыИСтроки, СвойстваСчета);
				Если ПараметрыЗаголовкаТаблицыИСтроки.ПараметрыСтрокиТаблица.Количество() > 0 Тогда
					ПечатьПоСчетуУчета(Док, Комиссия, ТабличныйДокумент, Макет, ПараметрыЗаголовкаТаблицыИСтроки, СвойстваСчета);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Док.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ПечатьПоСчетуУчета(Док, ИнвентаризационнаяКомиссия, ТабличныйДокумент, Макет, ПараметрыЗаголовкаТаблицыИСтроки, СвойстваСчета, СчетаУчета = Неопределено)
	
	// Варианты заголовков разделов с подписями печатной формы	
	ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей();
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	// Получаем области макета для вывода в табличный документ
	Шапка            = ОбластьМакета(Макет, "Шапка");
	Расписка         = ОбластьМакета(Макет, "Расписка");
	Подпись          = ОбластьМакета(Макет, "Подпись");
	ПолеДаты         = ОбластьМакета(Макет, "ПолеДаты");
	ПередТаблицей    = ОбластьМакета(Макет, "ПередТаблицей");
	Подсчеты         = ОбластьМакета(Макет, "Подсчеты");
	Претензии        = ОбластьМакета(Макет, "Претензии");
	
	// Выведем шапку документа
	СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Док.Организация, Док.Дата);
	
	Если Шапка <> Неопределено Тогда
		Шапка.Параметры.Заполнить(Док);
	КонецЕсли;
	
	ПараметрыПечатнойФормы = Новый Структура;
	ПараметрыПечатнойФормы.Вставить("Организация",          ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации));
	ПараметрыПечатнойФормы.Вставить("ОрганизацияКодПоОКПО", СведенияОбОрганизации.КодПоОКПО);
	
	ПараметрыПечатнойФормы.Вставить("НомерДокумента",       ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Док.Номер, Истина, Ложь));
	ПараметрыПечатнойФормы.Вставить("ДатаДокумента",        Док.Дата);

	ПараметрыСтрокиТаблица = ПараметрыЗаголовкаТаблицыИСтроки.ПараметрыСтрокиТаблица;
	ПараметрыЗаголовокТаблицы = ПараметрыЗаголовкаТаблицыИСтроки.ПараметрыЗаголовокТаблицы;
	КоличествоКолонок = ПараметрыЗаголовкаТаблицыИСтроки.КоличествоКолонок;
	
	ВидАктива = ВРег(СклонениеПредставленийОбъектов.ПросклонятьПредставление(Док.РазделУчета, 2));
	ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'Акт составлен комиссией о том, что по состоянию на %1 проведена инвентаризация %2.
		|При инвентаризации установлено следующее:';
		|en = 'The commission composed the certificate stating that as of %1, the physical inventory count of %2 was carried out.
		|The following is established during the inventory count:'"), формат(Док.Дата, "ДЛФ=dd.MM.yyyy"), НРег(ВидАктива));
	ЗаголовокРазделаПодписей.ОтветственноеЛицо = СтрШаблон(НСтр("ru = 'Лицо(а), ответственное(ые) за учет %1:';
																|en = 'Person(s) responsible for %1 accounting:'"), НРег(ВидАктива));
		
	// Для некоторых разделов расписки может и не быть
	ТекстРасписки = Неопределено;  
	Если ПараметрыСтрокиТаблица.Количество() = 0 Тогда
		ВидАктиваДательныйПадеж = НРег(СклонениеПредставленийОбъектов.ПросклонятьПредставление(Док.РазделУчета, 3));
		ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'При инвентаризации по состоянию на %1 комиссия не выявила наличие остатков по %2.';
													|en = 'During the physical inventory count as of %1, the commission did not reveal the balances of %2.'"),
			формат(Док.Дата, "ДЛФ=dd.MM.yyyy"), ВидАктиваДательныйПадеж);
	Иначе
		ЗаголовокТаблицы = "ЗаголовокТаблицыКолонки" + КоличествоКолонок;
		ИтогоПоСтранице  = "ИтогоПоСтраницеКолонки" + КоличествоКолонок;
		СтрокаТаблицы    = "СтрокаТаблицыКолонки" + КоличествоКолонок;
	
		Если КоличествоКолонок = 5 И СвойстваСчета.Количественный Тогда
			Если СвойстваСчета.УчетПоПодразделениюИНД Тогда
				ЗаголовокТаблицы = "ЗаголовокТаблицыКолонки" + КоличествоКолонок + "СКоличеством";
				ИтогоПоСтранице  = "ИтогоПоСтраницеКолонки" + КоличествоКолонок+ "СКоличеством";
				СтрокаТаблицы    = "СтрокаТаблицыКолонки" + КоличествоКолонок+ "СКоличеством";
			Иначе
				ЗаголовокТаблицы = "ЗаголовокТаблицыКолонки" + КоличествоКолонок + "БезНД";
				ИтогоПоСтранице  = "ИтогоПоСтраницеКолонки" + КоличествоКолонок+ "БезНД";
				СтрокаТаблицы    = "СтрокаТаблицыКолонки" + КоличествоКолонок+ "БезНД";
			КонецЕсли;
		ИначеЕсли КоличествоКолонок = 0 Тогда
			ЗаголовокТаблицы = "ЗаголовокТаблицыКолонки1";
			ИтогоПоСтранице  = "ИтогоПоСтраницеКолонки1";
			СтрокаТаблицы    = "СтрокаТаблицыКолонки1";
		КонецЕсли;
		
		Если СвойстваСчета.Валютный Тогда
			Если КоличествоКолонок = 4 И СвойстваСчета.Количественный Тогда
				ЗаголовокТаблицы = "ЗаголовокТаблицыКолонки4СКолВал";
				ИтогоПоСтранице  = "ИтогоПоСтраницеКолонки4СКолВал";
				СтрокаТаблицы    = "СтрокаТаблицыКолонки4СКолВал";
			Иначе
				ЗаголовокТаблицы = ЗаголовокТаблицы + "Вал";
				ИтогоПоСтранице  = ИтогоПоСтранице + "Вал";
				СтрокаТаблицы    = СтрокаТаблицы + "Вал";
			КонецЕсли;
		КонецЕсли;
		
		ЗаголовокТаблицы = ОбластьМакета(Макет, ЗаголовокТаблицы);
		ИтогоПоСтранице  = ОбластьМакета(Макет, ИтогоПоСтранице);
		СтрокаТаблицы    = ОбластьМакета(Макет, СтрокаТаблицы);
		
		ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'Акт составлен комиссией о том, что по состоянию на %1 проведена инвентаризация %2.
			|При инвентаризации установлено следующее:';
			|en = 'The commission composed the certificate stating that as of %1, the physical inventory count of %2 was carried out.
			|The following is established during the inventory count:'"), формат(Док.Дата, "ДЛФ=dd.MM.yyyy"), НРег(ВидАктива));
		Если Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложения Тогда
			ТекстРасписки = 
				НСтр("ru = 'все приходные и расходные документы по прочим капитальным вложениям получены бухгалтерией и введены в учетную систему.';
					|en = 'all disbursement and reimbursement documents on other capital expenditures are received by the accounting business unit and entered into the accounting system.'");
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.КомпонентыОСИОборудованиеКУстановке Тогда 
			ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'Акт составлен комиссией о том, что по состоянию на %1 проведена инвентаризация компонентов ОС и оборудования к установке.
				|При инвентаризации установлено следующее:';
				|en = 'The commission composed the certificate stating that as of %1, the physical inventory count of fixed asset components and equipment for installation was carried out.
				|The following is established during the inventory count:'"), формат(Док.Дата, "ДЛФ=dd.MM.yyyy"));
			ТекстРасписки = 
				НСтр("ru = 'все приходные и расходные документы по компонентам ОС и оборудованию к установке получены бухгалтерией и введены в учетную систему.';
					|en = 'all disbursement and reimbursement documents on fixed asset components and equipment for installation are received by the accounting business unit and entered into the accounting system.'"); 
			ЗаголовокРазделаПодписей.Вставить("ОтветственноеЛицо",
				НСтр("ru = 'Лицо(а), ответственное(ые) за учет компонентов ОС и оборудования к установке:';
					|en = 'Person(s) responsible for accounting of fixed asset components and equipment for installation:'"));
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложенияПоОС Тогда
			ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'Акт составлен комиссией о том, что по состоянию на %1 проведена инвентаризация капитальных вложений по ОС.
				|При инвентаризации установлено следующее:';
				|en = 'The commission composed the certificate stating that as of %1, the physical inventory count of capital fixed asset expenditures was carried out.
				|The following is established during the inventory count:'"), формат(Док.Дата, "ДЛФ=dd.MM.yyyy"));
			ТекстРасписки = 
				НСтр("ru = 'все приходные и расходные документы по капитальным вложениям по ОС получены бухгалтерией и введены в учетную систему.';
					|en = 'all disbursement and reimbursement documents on capital fixed asset expenditures are received by the accounting business unit and entered into the accounting system.'"); 
			ЗаголовокРазделаПодписей.Вставить("ОтветственноеЛицо",
				НСтр("ru = 'Лицо(а), ответственное(ые) за капитальных вложений по ОС:';
					|en = 'Person(s) responsible for accounting of capital fixed asset expenditures:'"));
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ОсновныеСредства Тогда
			ТекстРасписки = 
				НСтр("ru = 'все приходные и расходные документы по основным средствам получены бухгалтерией и введены в учетную систему.';
					|en = 'all disbursement and reimbursement documents on fixed assets are received by the accounting business unit and entered into the accounting system.'");
			ВидАктива =  НСтр("ru = 'ОСНОВНЫХ СРЕДСТВ';
								|en = 'FIXED ASSETS'");
			ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'Акт составлен комиссией о том, что по состоянию на %1 проведена инвентаризация основных средств.
				|При инвентаризации установлено следующее:';
				|en = 'The commission composed the certificate stating that as of %1, the physical inventory count of fixed assets was carried out.
				|The following is established during the inventory count:'"), формат(Док.Дата, "ДЛФ=dd.MM.yyyy"));
			ЗаголовокРазделаПодписей.Вставить("ОтветственноеЛицо",
				НСтр("ru = 'Лицо(а), ответственное(ые) за учет основных средств:';
					|en = 'Person(s) responsible for fixed asset accounting:'"));
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.НематериальныеАктивы Тогда
			ТекстРасписки = 
				НСтр("ru = 'все приходные и расходные документы по нематериальным активам получены бухгалтерией и введены в учетную систему.';
					|en = 'all disbursement and reimbursement documents on intangible assets are received by the accounting business unit and entered into the accounting system.'");
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.Запасы Тогда
			ТекстРасписки = 
				НСтр("ru = 'все приходные и расходные документы по запасам получены бухгалтерией и введены в учетную систему.';
					|en = 'all disbursement and reimbursement documents on inventory are received by the accounting business unit and entered into the accounting system.'");
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ТМЦВЭксплуатации Тогда
			ТекстРасписки = 
				НСтр("ru = 'все приходные и расходные документы по материальным ценностям в эксплуатации получены бухгалтерией и введены в учетную систему.';
					|en = 'all disbursement and reimbursement documents on inventory in operation are received by the accounting business unit and entered into the accounting system.'");
			ВидАктива =  НСтр("ru = 'МАТЕРИАЛЬНЫХ ЦЕННОСТЕЙ В ЭКСПЛУАТАЦИИ';
								|en = 'INVENTORY IN OPERATION'");
			ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'Акт составлен комиссией о том, что по состоянию на %1 проведена инвентаризация материальных ценностей в эксплуатации.
				|При инвентаризации установлено следующее:';
				|en = 'The commission composed the certificate stating that as of %1, the physical inventory count of inventory in operation was carried out.
				|The following is established during the inventory count:'"), формат(Док.Дата, "ДЛФ=dd.MM.yyyy"));
			ЗаголовокРазделаПодписей.Вставить("ОтветственноеЛицо",
				НСтр("ru = 'Лицо(а), ответственное(ые) за учет материальных ценностей в эксплуатации:';
					|en = 'Person(s) responsible for accounting of inventory in operation:'"));
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.НДСПоПриобретеннымЦенностям Тогда
			ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'Акт составлен комиссией о том, что по состоянию на %1 проведена инвентаризация по счету 19 НДС по приобретенным ценностям.
				|При инвентаризации установлено следующее:';
				|en = 'The commission composed the certificate stating that as of %1, the physical inventory count on account 19 of VAT on acquired assets was carried out.
				|The following is established during the inventory count:'"), формат(Док.Дата, "ДЛФ=dd.MM.yyyy"));
			ЗаголовокРазделаПодписей.ОтветственноеЛицо = НСтр("ru = 'Лицо(а), ответственное(ые) за учет НДС по приобретенным ценностям:';
																|en = 'Person(s) responsible for accounting of VAT on acquired assets:'");
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.Касса Тогда
			ТекстРасписки = 
				НСтр("ru = 'все расходные и приходные документы на денежные средства сданы в бухгалтерию, и все 
				|денежные средства, разные ценности и документы, поступившие на мою ответственность, оприходованы, а выбывшие списаны в расход.';
				|en = 'all disbursement and reimbursement documents for cash are passed to the accounting business unit, and all 
				|cash, various assets, and documents received in my charge are recorded as received, and the retired ones are written off as outflows.'");
			ВидАктива =  НСтр("ru = 'НАЛИЧНЫХ ДЕНЕЖНЫХ СРЕДСТВ';
								|en = 'CASH'");
			ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'Акт составлен комиссией о том, что по состоянию на %1 проведена инвентаризация наличных денежных средств,
				|разных ценностей и документов, находящихся в кассе. При инвентаризации установлено следующее:';
				|en = 'The commission composed the certificate stating that as of %1, the physical inventory count of cash, various assets, and documents in the cash account was carried out.
				|The following is established during the inventory count:'"), формат(Док.Дата, "ДЛФ=dd.MM.yyyy"));
			ЗаголовокРазделаПодписей.Вставить("ОтветственноеЛицо",
				НСтр("ru = 'Лицо(а), ответственное(ые) за ведение кассы:';
					|en = 'Person(s) responsible for keeping cash accounts:'")); 
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.БанковскиеСчета Тогда
			ТекстРасписки = НСтр("ru = 'все банковские выписки получены бухгалтерией и введены в учетную систему.';
								|en = 'all bank statements are received by the accounting business unit and entered into the accounting system.'");
			ВидАктива =  НСтр("ru = 'БЕЗНАЛИЧНЫХ ДЕНЕЖНЫХ СРЕДСТВ НА РАСЧЕТНОМ СЧЕТЕ';
								|en = 'CASH ON CURRENT ACCOUNT'");
			ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'При инвентаризации по состоянию на %1 комиссией установлено:';
														|en = 'During the physical inventory count as of %1, the commission established:'"), формат(Док.Дата, "ДЛФ=dd.MM.yyyy"));
			ПодсчетыДопТекст = НСтр("ru = 'Остатки денежных средств на расчетных счетах по данным бухгалтерского учета соответствуют остаткам, указанным в банковских выписках.';
									|en = 'According to accounting data, the cash balances on the current accounts correspond to the balances specified in the bank statements.'");
			ЗаголовокРазделаПодписей.ОтветственноеЛицо = НСтр("ru = 'Лицо(а), ответственное(ые) за ведение расчетного счета:';
																|en = 'Person(s) responsible for keeping current accounts:'");
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ФинансовыеВложения Тогда
			СтрокаПараметрыСтрокиКоличество = ПараметрыСтрокиТаблица.Найти("Количество");
			ТекстРасписки = 
				НСтр("ru = 'все приходные и расходные документы по финансовым вложениям получены бухгалтерией и введены в учетную систему.';
					|en = 'all disbursement and reimbursement documents on financial investments are received by the accounting business unit and entered into the accounting system.'");
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ПрочиеСчетаБухгалтерскогоУчета
			Или Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета Тогда
			Счет = СчетаУчета.СчетУчетаРодитель;
			Если СчетаУчета.СчетУчета = ПланыСчетов.Хозрасчетный.ПереданныеОбъектыНедвижимости Тогда
				Счет = СчетаУчета.СчетУчета;
			КонецЕсли;
			РеквизитыСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Счет, "Код, Наименование");
			Если Счет = ПланыСчетов.Хозрасчетный.СписаннаяВУбытокЗадолженностьДебиторов Тогда
				ВидАктива = НСтр("ru = 'СПИСАННОЙ В УБЫТОК ЗАДОЛЖЕННОСТИ НЕПЛАТЕЖЕСПОСОБНЫХ ДЕБИТОРОВ';
								|en = 'INDEBTEDNESS OF INSOLVENT DEBTOR WRITTEN OFF TO LOSSES'");
			Иначе
				ВидАктива = ВРег(СклонениеПредставленийОбъектов.ПросклонятьПредставление(РеквизитыСчета.Наименование, 2));
			КонецЕсли;
			ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'Акт составлен комиссией о том, что по состоянию на %1 проведена инвентаризация по счету %2 %3.
				|При инвентаризации установлено следующее:';
				|en = 'The commission composed the certificate stating that as of %1, the physical inventory count on account %2 %3 was carried out.
				|The following is established during the inventory count:'"), формат(Док.Дата, "ДЛФ=dd.MM.yyyy"), РеквизитыСчета.Код, РеквизитыСчета.Наименование);
		КонецЕсли;
		
	КонецЕсли;
	ПараметрыПечатнойФормы.Вставить("ТекстРасписки", ТекстРасписки);
	ПараметрыПечатнойФормы.Вставить("ВидАктива",     ВидАктива);
	
	ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Шапка, ПараметрыПечатнойФормы);
	
	ДанныеМОЛ = УправлениеВнеоборотнымиАктивамиПереопределяемый.ДанныеФизЛица(Док.Организация, Док.ОтветственноеЛицо, Док.Дата);
	ПараметрыПодписиОтветственноеЛицо = НовыйПараметрыПодписи();
	ПараметрыПодписиОтветственноеЛицо.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ОтветственноеЛицо;
	ПараметрыПодписиОтветственноеЛицо.Должность = ДанныеМОЛ.Должность;
	ПараметрыПодписиОтветственноеЛицо.РасшифровкаПодписи = ДанныеМОЛ.Представление;
	Если ЗначениеЗаполнено(ТекстРасписки) Тогда
		ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Расписка, ПараметрыПечатнойФормы);
		ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписиОтветственноеЛицо);
		
		ПараметрыПодписи = НовыйПараметрыПодписи();
		ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
	КонецЕсли;
	
	ПараметрыПередТаблицей = Новый Структура;
	ПараметрыПередТаблицей.Вставить("ТекстКомиссиейУстановлено", ТекстКомиссиейУстановлено);
	ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ПередТаблицей, ПараметрыПередТаблицей);
	
	Если ПараметрыСтрокиТаблица.Количество() > 0 Тогда
		
		ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ЗаголовокТаблицы, ПараметрыЗаголовокТаблицы);
		
		ОбластьПроверкиВывода = Новый Массив;
		Если СтрокаТаблицы <> Неопределено Тогда
			ОбластьПроверкиВывода.Добавить(СтрокаТаблицы);
		КонецЕсли;
		
		НомерСтроки = 0;
		КоллекцияИтоговСтраницы = НовыйИтогиПоказателей();
		КоллекцияИтоговДокумента = НовыйИтогиПоказателей();
		Пока Док.Следующий() Цикл
			НомерСтроки = НомерСтроки + 1;
			ПараметрыСтрокиПоСчету = ПараметрыСтрокиТаблица.Найти(Док.НомерСчета, "СчетУчета");
			ПараметрыСтроки = Новый Структура;
			Если ПараметрыСтрокиПоСчету <> Неопределено Тогда
				ПараметрыСтроки = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ПараметрыСтрокиПоСчету);
			КонецЕсли;
			Если СтрокаТаблицы <> Неопределено Тогда
				СтрокаТаблицы.Параметры.Заполнить(Новый Структура("НомерСтроки", НомерСтроки));
				Для Каждого ПараметрСтрока Из ПараметрыСтроки Цикл
					Если ПараметрСтрока.Ключ = "СчетУчета" Тогда
						Продолжить;
					КонецЕсли;
					Если Не ЗначениеЗаполнено(ПараметрСтрока.Значение) Тогда
						СтрокаТаблицы.Параметры.Заполнить(Новый Структура(ПараметрСтрока.Ключ, ""));
					Иначе
						СтрокаТаблицы.Параметры.Заполнить(Новый Структура(ПараметрСтрока.Ключ, Док[ПараметрСтрока.Значение]));
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			Если ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, ОбластьПроверкиВывода) Тогда
				ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, СтрокаТаблицы, Док);
			Иначе
				ЗаполнитьСтроковыеПоляИтогов(КоллекцияИтоговСтраницы, ВалютаРегламентированногоУчета);
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ЗаголовокТаблицы, ПараметрыЗаголовокТаблицы);
				ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, СтрокаТаблицы, Док);
			КонецЕсли;
			
			КоллекцияИтоговСтраницы.Стоимость = КоллекцияИтоговСтраницы.Стоимость + Док.Стоимость;
			КоллекцияИтоговСтраницы.КоличествоСтрок = КоллекцияИтоговСтраницы.КоличествоСтрок + 1;
			
			КоллекцияИтоговДокумента.Стоимость = КоллекцияИтоговДокумента.Стоимость + Док.Стоимость;
			КоллекцияИтоговДокумента.КоличествоСтрок = КоллекцияИтоговДокумента.КоличествоСтрок + 1;
			
		КонецЦикла;
		
		ЗаполнитьСтроковыеПоляИтогов(КоллекцияИтоговСтраницы, ВалютаРегламентированногоУчета);
		ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ИтогоПоСтранице, КоллекцияИтоговСтраницы);
		
		ПараметрыПодсчеты = Новый Структура;
		ПараметрыПодсчеты.Вставить("ПодсчетыДопТекст", ПодсчетыДопТекст);
		ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подсчеты, ПараметрыПодсчеты);
		
	КонецЕсли;
	
	ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ИнвентаризационнаяКомиссия.ПараметрыПодписиПредседатель);
	Если ИнвентаризационнаяКомиссия.ПараметрыПодписи.Количество() > 0 Тогда
		Для Каждого Строка Из ИнвентаризационнаяКомиссия.ПараметрыПодписи Цикл
			ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, Строка);
		КонецЦикла;
	Иначе 
		КоличествоЧленовКомиссииУнифицированнойФормы = 3;
		ПараметрыПодписиПредседатель = НовыйПараметрыПодписи();
		ПараметрыПодписиПредседатель.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ПредседательКомиссии;
		ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписиПредседатель);
		ПараметрыПодписи = НовыйПараметрыПодписи();
		ПараметрыПодписи.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ЧленыКомиссии;
		ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
		Для НомерЧленаКомиссии = 2 По КоличествоЧленовКомиссииУнифицированнойФормы Цикл
			ПараметрыПодписи = НовыйПараметрыПодписи();
			ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
		КонецЦикла;
	КонецЕсли;
	
	Если ПараметрыСтрокиТаблица.Количество() > 0 Тогда
		
		ПараметрыПретензии = Новый Структура("НомерСтрокиНачальный, НомерСтрокиКонечный, ВидАктива");
		Если НомерСтроки > 0 Тогда
			ПараметрыПретензии.НомерСтрокиНачальный = 1;
			ПараметрыПретензии.НомерСтрокиКонечный  = НомерСтроки;
		Иначе
			ПараметрыПретензии.НомерСтрокиНачальный = "______";
			ПараметрыПретензии.НомерСтрокиКонечный  = "______";
		КонецЕсли;
		Если Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.НДСПоПриобретеннымЦенностям Тогда
			ПараметрыПретензии.ВидАктива = НСтр("ru = 'НДС по приобретенным ценностям';
												|en = 'VAT on acquired assets'");
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.Касса Тогда
			ПараметрыПретензии.ВидАктива = НРег(ВидАктива) + НСтр("ru = ', разных ценностей и документов, находящихся в кассе';
																	|en = ', various assets and documents in the cash account'"); 
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложенияПоОС Тогда
			ПараметрыПретензии.ВидАктива = НСтр("ru = 'капитальных вложений по ОС';
												|en = 'capital fixed asset expenditures'");
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.КомпонентыОСИОборудованиеКУстановке Тогда
			ПараметрыПретензии.ВидАктива = НСтр("ru = 'компонентов ОС и оборудования к установке';
												|en = 'fixed asset components and equipment to install'");
		Иначе
			ПараметрыПретензии.ВидАктива = НРег(ВидАктива);
		КонецЕсли;		
		ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Претензии, ПараметрыПретензии);
	КонецЕсли;
	
	ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписиОтветственноеЛицо);
	
	ПараметрыПодписи = НовыйПараметрыПодписи();
	ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
	
	ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ПолеДаты, Док);
	
	ПараметрыПодписиПроверяющий = НовыйПараметрыПодписи();
	ПараметрыПодписиПроверяющий.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.Проверяющий;
	ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписиПроверяющий);
	
	ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ПолеДаты, Док);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыЗаголовкаТаблицыИСтроки(СчетаУчета, ПараметрыЗаголовкаТаблицыИСтроки, СвойстваСчетаДляПечати)
	
	ПараметрыСтрокиТаблица = ПараметрыЗаголовкаТаблицыИСтроки.ПараметрыСтрокиТаблица;
	ПараметрыЗаголовокТаблицы = ПараметрыЗаголовкаТаблицыИСтроки.ПараметрыЗаголовокТаблицы; 
	
	ПараметрыСчетовРаздела = Новый Структура();
	ПараметрыСчетовРаздела.Вставить("ДобавитьКолонкуПодразделение",Ложь);
	ПараметрыСчетовРаздела.Вставить("ДобавитьКолонкуНД",Ложь);
	ПараметрыСчетовРаздела.Вставить("ДобавитьКолонкуКоличество",Ложь);
	ПараметрыСчетовРаздела.Вставить("ДобавитьКолонкуВалюта",Ложь);

	ПараметрыСчетовРаздела.Вставить("КоличествоСубконто",0);
		
	УчетПоНаправлениямДеятельности = ПолучитьФункциональнуюОпцию("ФормироватьФинансовыйРезультат");
	
	ОсновнойСчет = ОсновнойСчет(СчетаУчета,ПараметрыСчетовРаздела); // счет с максимальным количеством колонок 
	
	СвойстваСчетаДляПечати.Вставить("УчетПоПодразделениюИНД", ПараметрыСчетовРаздела.ДобавитьКолонкуПодразделение
																И ПараметрыСчетовРаздела.ДобавитьКолонкуНД);
	СвойстваСчетаДляПечати.Вставить("Количественный",         ПараметрыСчетовРаздела.ДобавитьКолонкуКоличество);
	СвойстваСчетаДляПечати.Вставить("Валютный",               ПараметрыСчетовРаздела.ДобавитьКолонкуВалюта);
	
	НомерКолонки = 1;
	
	Если ПараметрыСчетовРаздела.ДобавитьКолонкуПодразделение Тогда
		НаименованиеКолонки = НСтр("ru = 'Подразделение';
									|en = 'Business unit'");
		ПараметрыЗаголовокТаблицы.НаименованиеКолонки1 = НаименованиеКолонки;
		НомерКолонки = НомерКолонки + 1;
	КонецЕсли; 
	
	Если ПараметрыСчетовРаздела.ДобавитьКолонкуНД Тогда
		ИмяКолонки = "НаименованиеКолонки" + НомерКолонки;
		ПараметрыЗаголовокТаблицы[ИмяКолонки] = НСтр("ru = 'Направление деятельности';
													|en = 'Line of business'");
	КонецЕсли;
	
	НомерПоследнейКолонки = ПараметрыСчетовРаздела.КоличествоСубконто + ?(ПараметрыСчетовРаздела.ДобавитьКолонкуПодразделение, 1, 0) +
							+ ?(ПараметрыСчетовРаздела.ДобавитьКолонкуНД, 1, 0);
	Если ПараметрыСчетовРаздела.ДобавитьКолонкуКоличество Тогда 
		НомерПоследнейКолонки = НомерПоследнейКолонки + 1;
		ИмяКолонки = "НаименованиеКолонки" + НомерПоследнейКолонки;
		ПараметрыЗаголовокТаблицы[ИмяКолонки] = НСтр("ru = 'Количество';
													|en = 'Quantity'");
	КонецЕсли;
	
	ПараметрыЗаголовкаТаблицыИСтроки.КоличествоКолонок = НомерПоследнейКолонки;
	ВидыСубконтоХозрасчетные = Новый Массив;
	ВидыСубконтоХозрасчетные.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций);
	ВидыСубконтоХозрасчетные.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	ВидыСубконтоХозрасчетные.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	ВидыСубконтоХозрасчетные.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РеализуемыеАктивы);
	ВидыСубконтоХозрасчетные.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СФПолученные);
	ВидыСубконтоХозрасчетные.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СФВыданные);
	ВидыСубконтоХозрасчетные.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
	
	НаименованиеСубконто = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ВидыСубконтоХозрасчетные,"Наименование");
	
	РаботникиОрганизаций = НаименованиеСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций).Наименование;
	Контрагенты          = НаименованиеСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты).Наименование;
	ОсновныеСредства     = НаименованиеСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства).Наименование;
	Договоры             = НаименованиеСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры).Наименование;
	СФПолученные         = НаименованиеСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СФПолученные).Наименование;
	СФВыданные           = НаименованиеСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СФВыданные).Наименование;
	РеализуемыеАктивы    = НаименованиеСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РеализуемыеАктивы).Наименование;

	Для Каждого СтрокаСчетаУчета Из СчетаУчета Цикл
		
		НоваяСтрокаПараметрыСтроки = ПараметрыСтрокиТаблица.Добавить();
		НоваяСтрокаПараметрыСтроки.СчетУчета = СтрокаСчетаУчета.СчетУчета;
		
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаСчетаУчета.СчетУчета);
		НомерСубконтоЦенныеБумаги = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ЦенныеБумаги);
		НомерСубконтоБанковскиеСчета = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.БанковскиеСчета); 
		Если НомерСубконтоЦенныеБумаги <> Неопределено Или НомерСубконтоБанковскиеСчета <> Неопределено Тогда
			ОсновнойСчет = СтрокаСчетаУчета.СчетУчета;
		КонецЕсли;
		ЭтоОсновнойСчет = СтрокаСчетаУчета.СчетУчета = ОсновнойСчет;
		КоличествоСубконто = СвойстваСчета.КоличествоСубконто;
		СубконтоНаСчете = Новый ТаблицаЗначений;
		СубконтоНаСчете.Колонки.Добавить("Субконто");
		СубконтоНаСчете.Колонки.Добавить("ВидСубконто");
		Для НомерСубконто = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если Не СвойстваСчета["ВидСубконто" + НомерСубконто + "ТолькоОбороты"] Тогда
				НоваяСтрокаСубконто = СубконтоНаСчете.Добавить();
				НоваяСтрокаСубконто.Субконто = "Субконто"+ НомерСубконто;
				НоваяСтрокаСубконто.ВидСубконто = "ВидСубконто" + НомерСубконто + "Наименование";
			Иначе
				КоличествоСубконто = КоличествоСубконто - 1;
			КонецЕсли;
		КонецЦикла; 
		
		КоличествоКолонок = ?(ПараметрыСчетовРаздела.ДобавитьКолонкуПодразделение, КоличествоСубконто + 1, КоличествоСубконто);
		КоличествоКолонок = ?(ПараметрыСчетовРаздела.ДобавитьКолонкуНД, КоличествоКолонок + 1, КоличествоКолонок);
		КоличествоКолонок = ?(ПараметрыСчетовРаздела.ДобавитьКолонкуКоличество, КоличествоКолонок + 1, КоличествоКолонок);
		
		Если НомерПоследнейКолонки = 0 Тогда 
			ПараметрыЗаголовокТаблицы.НаименованиеКолонки1 = НСтр("ru = 'Наименование';
																	|en = 'Description'");
			НоваяСтрокаПараметрыСтроки.Колонка1 = "НомерСчетаНаименование";
		ИначеЕсли КоличествоКолонок = 0 Тогда
			Если ЭтоОсновнойСчет Тогда
				ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки1", СтрокаСчетаУчета.СчетУчетаНаименование);
			КонецЕсли;
		Иначе
			НомерСубконто = 0;
			НомерКолонки = 1;
			Если СвойстваСчета.УчетПоПодразделениям Тогда
				НоваяСтрокаПараметрыСтроки.Колонка1 = "Подразделение";
				НомерКолонки = НомерКолонки + 1;
			ИначеЕсли ПараметрыСчетовРаздела.ДобавитьКолонкуПодразделение Тогда
				НомерКолонки = НомерКолонки + 1;
			КонецЕсли;
			Если УчетПоНаправлениямДеятельности И СвойстваСчета.УчетПоНаправлениямДеятельности И НомерКолонки <= КоличествоКолонок Тогда
				НоваяСтрокаПараметрыСтроки["Колонка" + НомерКолонки] = "НаправлениеДеятельности";
				НомерКолонки = НомерКолонки + 1;
			ИначеЕсли ПараметрыСчетовРаздела.ДобавитьКолонкуНД Тогда
				НомерКолонки = НомерКолонки + 1;
			КонецЕсли;
			Пока НомерКолонки <= КоличествоКолонок И НомерСубконто < КоличествоСубконто Цикл
				ИмяКолонки = "НаименованиеКолонки" + НомерКолонки;
				НаименованиеКолонки = СвойстваСчета[СубконтоНаСчете[НомерСубконто].ВидСубконто];
				Если ЭтоОсновнойСчет И СтрокаСчетаУчета.РазделУчета <> Перечисления.РазделыУчетаДляИнвентаризации.ОсновныеСредства
					И СтрокаСчетаУчета.РазделУчета <> Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСПерсоналом 
					И СтрокаСчетаУчета.РазделУчета <> Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСКонтрагентами Тогда
					ПараметрыЗаголовокТаблицы[ИмяКолонки] = НаименованиеКолонки;
				КонецЕсли;
				Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.Запасы
						И ПараметрыЗаголовокТаблицы[ИмяКолонки] <> НаименованиеКолонки
						И НЕ НаименованиеКолонки = РеализуемыеАктивы Тогда
					НомерКолонки = НомерКолонки + 1;
				ИначеЕсли (СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ФинансовыеВложения И НомерСубконто <> 0
					Или СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.НезавершенноеПроизводство)
					И ПараметрыЗаголовокТаблицы[ИмяКолонки] <> НаименованиеКолонки  Тогда
					НомерКолонки = НомерКолонки + 1;
					ИмяКолонки = "НаименованиеКолонки" + НомерКолонки;
					ПараметрыЗаголовокТаблицы[ИмяКолонки] = НаименованиеКолонки; 
					Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.НезавершенноеПроизводство 
						И ПараметрыСчетовРаздела.КоличествоСубконто < 4 Тогда 
						ПараметрыСчетовРаздела.Вставить("КоличествоСубконто",ПараметрыСчетовРаздела.КоличествоСубконто + 1);
						ПараметрыЗаголовкаТаблицыИСтроки.КоличествоКолонок = ПараметрыЗаголовкаТаблицыИСтроки.КоличествоКолонок + 1;
					КонецЕсли;
				ИначеЕсли СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыПоНалогамИСоциальномуСтрахованию Тогда
					ПараметрыЗаголовокТаблицы[ИмяКолонки] = "Аналитика учета" + (НомерСубконто + 1);
				КонецЕсли;
				Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ОсновныеСредства
					ИЛИ СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСПерсоналом 
					ИЛИ СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСКонтрагентами Тогда 
					Если НаименованиеКолонки = СФПолученные Или НаименованиеКолонки = СФВыданные Тогда 
						НаименованиеКолонки = "Счета-фактуры";
					КонецЕсли;
					НовыйНомерКолонки = НомерКолонки;
					Если (НаименованиеКолонки = ОсновныеСредства Или НаименованиеКолонки = РаботникиОрганизаций)
						И НомерСубконто <> 0 Тогда
						НовыйНомерКолонки = НомерКолонки - НомерСубконто;
					ИначеЕсли ((НаименованиеКолонки <> ОсновныеСредства И СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ОсновныеСредства)
						Или (НаименованиеКолонки <> РаботникиОрганизаций И СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСПерсоналом))
						И НомерСубконто = 0 Тогда 
						НовыйНомерКолонки = НомерКолонки + 1;
					ИначеЕсли СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСПерсоналом
						И НаименованиеКолонки <> Контрагенты И НомерСубконто > 0 И ПараметрыСчетовРаздела.КоличествоСубконто = 3 Тогда
						НовыйНомерКолонки = НомерКолонки + 1;
					ИначеЕсли СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСКонтрагентами
						И НаименованиеКолонки = Контрагенты И НомерСубконто > 0 Тогда
						НовыйНомерКолонки = НомерКолонки - НомерСубконто; 
					ИначеЕсли СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСКонтрагентами
						И НаименованиеКолонки = Договоры И НомерСубконто > 1 Тогда
						НовыйНомерКолонки = НомерКолонки - НомерСубконто + 1;
					ИначеЕсли СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСКонтрагентами
						И НаименованиеКолонки <> Договоры И НаименованиеКолонки <> Контрагенты 
						И ПараметрыСчетовРаздела.КоличествоСубконто = 3 Тогда
						НовыйНомерКолонки = НомерКолонки - НомерСубконто + 2;
					КонецЕсли;
					ИмяКолонки = "НаименованиеКолонки" + НовыйНомерКолонки;
					ПараметрыЗаголовокТаблицы[ИмяКолонки] = НаименованиеКолонки;
					НоваяСтрокаПараметрыСтроки["Колонка" + НовыйНомерКолонки] = СубконтоНаСчете[НомерСубконто].Субконто;
				Иначе
					НоваяСтрокаПараметрыСтроки["Колонка" + НомерКолонки] = СубконтоНаСчете[НомерСубконто].Субконто;
				КонецЕсли;
				НомерКолонки = НомерКолонки + 1;
				НомерСубконто = НомерСубконто + 1;
			КонецЦикла;
			Если НомерКолонки <= КоличествоКолонок Тогда
				НоваяСтрокаПараметрыСтроки["Колонка" + НомерПоследнейКолонки] = "Количество";
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.НДСПоПриобретеннымЦенностям Тогда
			ПараметрыЗаголовокТаблицы.НаименованиеСуммы = НСтр("ru = 'Сумма НДС, руб.коп.';
																|en = 'VAT amount, rub. cop.'");
		КонецЕсли;
		
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.БанковскиеСчета Тогда
			НомерСубконтоБанковскиеСчета = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.БанковскиеСчета);
			Если НомерСубконтоБанковскиеСчета <> Неопределено Тогда
				Для НомерКолонки = 1 По КоличествоКолонок Цикл
					Если НоваяСтрокаПараметрыСтроки["Колонка" + НомерКолонки] = "Субконто" + НомерСубконтоБанковскиеСчета Тогда
						НоваяСтрокаПараметрыСтроки["Колонка" + НомерКолонки] = "НаименованиеБанка";
						НоваяСтрокаПараметрыСтроки["Колонка" + (НомерКолонки + 1)] = "НомерБанковскогоСчета";
						Если ЭтоОсновнойСчет Тогда
							ПараметрыЗаголовкаТаблицыИСтроки.КоличествоКолонок = КоличествоКолонок + 1;
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + НомерКолонки] = НСтр("ru = 'Наименование банка';
																									|en = 'Bank name'");
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + (НомерКолонки + 1)] = НСтр("ru = 'Номер счета';
																										|en = 'Account number'");
						КонецЕсли;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ОтложенныеНалоговыеАктивыОбязательства Тогда
			НомерСубконтоВидыАктивовИОбязательств = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыАктивовИОбязательств);
			Если НомерСубконтоВидыАктивовИОбязательств <> Неопределено Тогда
				Для НомерКолонки = 1 По КоличествоКолонок Цикл
					Если НоваяСтрокаПараметрыСтроки["Колонка" + НомерКолонки] = "Субконто" + НомерСубконтоВидыАктивовИОбязательств Тогда
						Если ЭтоОсновнойСчет Тогда
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + НомерКолонки] = НСтр("ru = 'Вид активов и обязательств';
																									|en = 'Kind of assets and liabilities '");
						КонецЕсли;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ОценочныеОбязательства Тогда
			НомерСубконтоРезервы = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Резервы);
			Если НомерСубконтоРезервы <> Неопределено Тогда
				Для НомерКолонки = 1 По КоличествоКолонок Цикл
					Если НоваяСтрокаПараметрыСтроки["Колонка" + НомерКолонки] = "Субконто" + НомерСубконтоРезервы Тогда
						Если ЭтоОсновнойСчет Тогда
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + НомерКолонки] = НСтр("ru = 'Обязательство (резерв)';
																									|en = 'Liability (reserve)'");
						КонецЕсли;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложения И ПараметрыСчетовРаздела.КоличествоСубконто > 0 Тогда 
			НомерКолонки = ?(ПараметрыСчетовРаздела.ДобавитьКолонкуПодразделение, 1, 0) + ?(ПараметрыСчетовРаздела.ДобавитьКолонкуНД, 1, 0) + 1;
			ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + НомерКолонки] = НСтр("ru = 'НМА и расходы на НИОКР';
																					|en = 'Intangible assets and R&D expenses'");
		КонецЕсли;
		
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложенияПоОС И ПараметрыСчетовРаздела.КоличествоСубконто > 0 Тогда 
			НомерКолонки = ?(ПараметрыСчетовРаздела.ДобавитьКолонкуПодразделение, 1, 0) + ?(ПараметрыСчетовРаздела.ДобавитьКолонкуНД, 1, 0) + 1;
			ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + НомерКолонки] = НСтр("ru = 'Объекты строительства и ОС';
																					|en = 'Assets under construction and fixed assets'");
		КонецЕсли;
		
		Если (СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСПерсоналом
			Или СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСКонтрагентами)
			И ПараметрыСчетовРаздела.КоличествоСубконто = 3 Тогда
			НомерКолонки = ?(ПараметрыСчетовРаздела.ДобавитьКолонкуПодразделение, 1, 0) + ?(ПараметрыСчетовРаздела.ДобавитьКолонкуНД, 1, 0) + 3;
			ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + НомерКолонки] = НСтр("ru = 'Аналитика учета';
																					|en = 'Accounting dimension'");
		КонецЕсли;
		
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасходыБудущихПериодов Тогда
			НомерСубконтоРасходыБудущихПериодов = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов);
			НомерСубконтоРаботникиОрганизаций = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций);
			НомерСубконтоКонтрагенты = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
			Для НомерКолонки = 1 По КоличествоКолонок Цикл
				Если НомерСубконтоРасходыБудущихПериодов <> Неопределено И НоваяСтрокаПараметрыСтроки["Колонка" + НомерКолонки] <> "Подразделение" 
					И НоваяСтрокаПараметрыСтроки["Колонка" + НомерКолонки] <> "НаправлениеДеятельности" Тогда
					НомерТек = НомерКолонки;
					НоваяСтрокаПараметрыСтроки["Колонка" + НомерТек] = "Субконто" + НомерСубконтоРасходыБудущихПериодов;
					Если ЭтоОсновнойСчет Тогда
						ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + НомерТек] = НСтр("ru = 'Вид расходов';
																							|en = 'Expense kind'");
					КонецЕсли;
					Если НомерСубконтоРаботникиОрганизаций <> Неопределено Тогда
						НомерТек = НомерТек + 1;
						НоваяСтрокаПараметрыСтроки["Колонка" + НомерТек] = "Субконто" + НомерСубконтоРаботникиОрганизаций;
						Если ЭтоОсновнойСчет Тогда
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + НомерТек] = РаботникиОрганизаций;
						КонецЕсли;
					КонецЕсли;
					Если НомерСубконтоКонтрагенты <> Неопределено Тогда
						НомерТек = НомерТек + 1;
						НоваяСтрокаПараметрыСтроки["Колонка" + НомерТек] = "Субконто" + НомерСубконтоКонтрагенты;
						Если ЭтоОсновнойСчет Тогда
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + НомерТек] = Контрагенты;
						КонецЕсли;
					КонецЕсли;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ДоходыБудущихПериодов Тогда
			НомерСубконтоДоходыБудущихПериодов = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДоходыБудущихПериодов);
			Если КоличествоКолонок = 0 Тогда
				НоваяСтрокаПараметрыСтроки["Колонка1"] = "НомерСчетаНаименование";
				Если ЭтоОсновнойСчет Тогда
					ПараметрыЗаголовокТаблицы["НаименованиеКолонки1"] = НСтр("ru = 'Вид доходов';
																			|en = 'Income kind'");
				КонецЕсли;
			КонецЕсли;
			НомерСубконтоРаботникиОрганизаций = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций);
			Для НомерКолонки = 1 По КоличествоКолонок Цикл
				Если НомерСубконтоДоходыБудущихПериодов = Неопределено Тогда
					Если (КоличествоСубконто = 0 Или НомерСубконтоРаботникиОрганизаций <> Неопределено) И НоваяСтрокаПараметрыСтроки["Колонка" + НомерКолонки] <> "Подразделение" 
						И НоваяСтрокаПараметрыСтроки["Колонка" + НомерКолонки] <> "НаправлениеДеятельности" Тогда
						НоваяСтрокаПараметрыСтроки["Колонка" + НомерКолонки] = "НомерСчетаНаименование";
						Если НомерСубконтоРаботникиОрганизаций <> Неопределено Тогда
							НоваяСтрокаПараметрыСтроки["Колонка" + (НомерКолонки + 1)] = "Субконто" + НомерСубконтоРаботникиОрганизаций;
						КонецЕсли;
						Если ЭтоОсновнойСчет Тогда
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + НомерКолонки] = НСтр("ru = 'Вид доходов';
																									|en = 'Income kind'");
							Если НомерСубконтоРаботникиОрганизаций <> Неопределено Тогда
								ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + (НомерКолонки + 1)] = РаботникиОрганизаций;
							КонецЕсли;
						КонецЕсли;
						Прервать;
					КонецЕсли;
				Иначе
					Если НоваяСтрокаПараметрыСтроки["Колонка" + НомерКолонки] = "Субконто" + НомерСубконтоДоходыБудущихПериодов Тогда
						Если ЭтоОсновнойСчет Тогда
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + НомерКолонки] = НСтр("ru = 'Вид доходов';
																									|en = 'Income kind'");
						КонецЕсли;
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьСтроковыеПоляИтогов(КоллекцияИтоговСтраницы, ВалютаРегламентированногоУчета)
	КоллекцияИтоговСтраницы.КоличествоПорядковыхНомеровПрописью = ЧислоПрописью(КоллекцияИтоговСтраницы.КоличествоСтрок,, ",,,м,,,,,0");
	КоллекцияИтоговСтраницы.СтоимостьПрописью = РаботаСКурсамиВалют.СформироватьСуммуПрописью(
		КоллекцияИтоговСтраницы.Стоимость,
		ВалютаРегламентированногоУчета);
КонецПроцедуры
	
Функция НовыйИтогиПоказателей()

	ИтогиПоказателей = Новый Структура;
	ИтогиПоказателей.Вставить("Стоимость", 0);
	ИтогиПоказателей.Вставить("КоличествоСтрок", 0);
	ИтогиПоказателей.Вставить("КоличествоПорядковыхНомеровПрописью", "");
	ИтогиПоказателей.Вставить("СтоимостьПрописью", "");
	Возврат ИтогиПоказателей;
	
КонецФункции

Функция НовыйПараметрыПодписи()

	ПараметрыПодписи = Новый Структура;
	ПараметрыПодписи.Вставить("ЗаголовокРазделаПодписей");
	ПараметрыПодписи.Вставить("Должность");
	ПараметрыПодписи.Вставить("РасшифровкаПодписи");
	Возврат ПараметрыПодписи;
	
КонецФункции 

Функция ЗаголовокРазделаПодписей()

	ЗаголовокРазделаПодписей = Новый Структура();
	ЗаголовокРазделаПодписей.Вставить("ПредседательКомиссии", НСтр("ru = 'Председатель комиссии';
																	|en = 'Commission chairperson'"));
	ЗаголовокРазделаПодписей.Вставить("ЧленыКомиссии",        НСтр("ru = 'Члены комиссии:';
																	|en = 'Commission members:'"));
	ЗаголовокРазделаПодписей.Вставить("ОтветственноеЛицо",
		НСтр("ru = 'Материально ответственное(ые) лицо(а):';
			|en = 'Financially liable person(s):'"));
	ЗаголовокРазделаПодписей.Вставить("Проверяющий",
		НСтр("ru = 'Указанные в настоящем акте данные и расчеты проверил';
			|en = 'Data and calculations specified in the report are checked by'")); 

	Возврат ЗаголовокРазделаПодписей;
КонецФункции 
	
Функция НовыйПараметрыЗаголовкаТаблицыИСтроки()
	
	ПараметрыЗаголовокТаблицы = Новый Структура;
	ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки1", "");
	ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки2", "");
	ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки3", "");
	ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки4", "");
	ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки5", "");
	ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки6", "");
	ПараметрыЗаголовокТаблицы.Вставить("НаименованиеВалСуммы", НСтр("ru = 'Сумма по балансу в валюте';
																	|en = 'Balance amount in currency'"));
	ПараметрыЗаголовокТаблицы.Вставить("НаименованиеСуммы", НСтр("ru = 'Сумма по балансу, руб.коп.';
																|en = 'Balance amount, rub. cop.'"));
	ПараметрыСтрокиТаблица = Новый ТаблицаЗначений;
	ПараметрыСтрокиТаблица.Колонки.Добавить("СчетУчета", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ПараметрыСтрокиТаблица.Колонки.Добавить("Колонка1", Новый ОписаниеТипов("Строка"));
	ПараметрыСтрокиТаблица.Колонки.Добавить("Колонка2", Новый ОписаниеТипов("Строка"));
	ПараметрыСтрокиТаблица.Колонки.Добавить("Колонка3", Новый ОписаниеТипов("Строка"));
	ПараметрыСтрокиТаблица.Колонки.Добавить("Колонка4", Новый ОписаниеТипов("Строка")); 
	ПараметрыСтрокиТаблица.Колонки.Добавить("Колонка5", Новый ОписаниеТипов("Строка"));
	ПараметрыСтрокиТаблица.Колонки.Добавить("Колонка6", Новый ОписаниеТипов("Строка"));
	
	ПараметрыЗаголовкаТаблицыИСтроки = Новый Структура();
	ПараметрыЗаголовкаТаблицыИСтроки.Вставить("ПараметрыЗаголовокТаблицы", ПараметрыЗаголовокТаблицы);
	ПараметрыЗаголовкаТаблицыИСтроки.Вставить("ПараметрыСтрокиТаблица", ПараметрыСтрокиТаблица); 
	ПараметрыЗаголовкаТаблицыИСтроки.Вставить("КоличествоКолонок", 0);
	Возврат ПараметрыЗаголовкаТаблицыИСтроки;
	
КонецФункции

Функция ОсновнойСчет(СчетаУчета,ПараметрыСчетовРаздела)
	
	СчетаКоличествоКолонок = Новый ТаблицаЗначений;
	СчетаКоличествоКолонок.Колонки.Добавить("СчетУчета", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	СчетаКоличествоКолонок.Колонки.Добавить("КоличествоКолонок", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	УчетПоНаправлениямДеятельности = ПолучитьФункциональнуюОпцию("ФормироватьФинансовыйРезультат");
	ЕстьЦенныеБумаги = Ложь;
	ЕстьДоговора = Ложь;
	ДобавитьСубконто = Ложь;
	
	Для Каждого СтрокаСчетаУчета Из СчетаУчета Цикл
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаСчетаУчета.СчетУчета);
		КоличествоСубконто = СвойстваСчета.КоличествоСубконто;
		Для НомерСубконто = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если СвойстваСчета["ВидСубконто" + НомерСубконто + "ТолькоОбороты"] Тогда
				КоличествоСубконто = КоличествоСубконто - 1;
			КонецЕсли;
		КонецЦикла;
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ФинансовыеВложения И КоличествоСубконто > 1 Тогда
			НомерСубконтоЦенныеБумаги = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ЦенныеБумаги);
			Если НомерСубконтоЦенныеБумаги <> Неопределено Тогда
				ЕстьЦенныеБумаги = Истина;
			Иначе
				ЕстьДоговора = Истина;
			КонецЕсли;
			Если ЕстьЦенныеБумаги И ЕстьДоговора Тогда
				КоличествоСубконто = КоличествоСубконто + 1
			КонецЕсли;
		КонецЕсли; 
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСКонтрагентами И КоличествоСубконто > 1 Тогда
			НомерСубконтоДоговора = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
			Если НомерСубконтоДоговора <> Неопределено Тогда
				ЕстьДоговора = Истина;
			КонецЕсли;
			Если СвойстваСчета.ВидСубконто2Наименование <> "Договора" Тогда
				ДобавитьСубконто = Истина;
			КонецЕсли;
		КонецЕсли;
		Если УчетПоНаправлениямДеятельности И Не ПараметрыСчетовРаздела.ДобавитьКолонкуНД Тогда
			ПараметрыСчетовРаздела.ДобавитьКолонкуНД = СвойстваСчета.УчетПоНаправлениямДеятельности;
		КонецЕсли;
		Если Не ПараметрыСчетовРаздела.ДобавитьКолонкуПодразделение Тогда
			ПараметрыСчетовРаздела.ДобавитьКолонкуПодразделение = СвойстваСчета.УчетПоПодразделениям;
		КонецЕсли;
		Если Не ПараметрыСчетовРаздела.ДобавитьКолонкуКоличество Тогда
			ПараметрыСчетовРаздела.ДобавитьКолонкуКоличество = СвойстваСчета.Количественный;
		КонецЕсли; 
		Если Не ПараметрыСчетовРаздела.ДобавитьКолонкуВалюта Тогда
			ПараметрыСчетовРаздела.ДобавитьКолонкуВалюта = СвойстваСчета.Валютный;
		КонецЕсли;
		НоваяСтрокаСчетаКоличествоКолонок = СчетаКоличествоКолонок.Добавить();
		НоваяСтрокаСчетаКоличествоКолонок.СчетУчета = СтрокаСчетаУчета.СчетУчета;
		НоваяСтрокаСчетаКоличествоКолонок.КоличествоКолонок = КоличествоСубконто;
	КонецЦикла;
	
	СчетаКоличествоКолонок.Сортировать("КоличествоКолонок УБЫВ");
	
	Если СчетаКоличествоКолонок.Количество() > 0 Тогда
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСКонтрагентами 
			И ЕстьДоговора И ДобавитьСубконто И СчетаКоличествоКолонок[0].КоличествоКолонок < 3 Тогда
			ПараметрыСчетовРаздела.КоличествоСубконто = СчетаКоличествоКолонок[0].КоличествоКолонок + 1;
		Иначе 
			ПараметрыСчетовРаздела.КоличествоСубконто = СчетаКоличествоКолонок[0].КоличествоКолонок;
		КонецЕсли;
		Возврат СчетаКоличествоКолонок[0].СчетУчета;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Проверяет есть ли в макете область с заданным именем и возвращает ее.
//
// Параметры:
//  Макет      - ТабличныйДокумент - Макет, область которого необходимо получить.
//  ИмяОбласти - Строка - Получаемая область.
//
// Возвращаемое значение:
//  ТабличныйДокумент, Неопределено - Область макета, если она существует.
//
Функция ОбластьМакета(Макет, ИмяОбласти)

	Если Макет.Области.Найти(ИмяОбласти) = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат Макет.ПолучитьОбласть(ИмяОбласти);
	
КонецФункции

Процедура ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Область, СтруктураПараметров = Неопределено, ПроверитьВывод = Ложь)

	Если Область = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроверитьВывод И Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, Область) Тогда
		// Выведем разрыв страницы
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	
	Если СтруктураПараметров <> Неопределено Тогда
		Область.Параметры.Заполнить(СтруктураПараметров);
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(Область);

КонецПроцедуры

Функция СоздатьДокументИнвентаризации(Параметры,ДанныеУчета, ТаблицаСчетовПоРазделам)
	
	ДокументИнвентаризация = Документы.ИнвентаризацияОбъектовУчета.СоздатьДокумент();
	ЗаполнитьЗначенияСвойств(ДокументИнвентаризация,Параметры);
	ДокументИнвентаризация.РазделУчета = ДанныеУчета.РазделУчета; 
	
	Для Каждого ЧленКомиссии Из Параметры.ИнвентаризационнаяКомиссия Цикл
		СтрокаКомиссия = ДокументИнвентаризация.ИнвентаризационнаяКомиссия.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКомиссия, ЧленКомиссии);
	КонецЦикла;
	
	СтрокиСчетовРаздела = ТаблицаСчетовПоРазделам.Найти(ДанныеУчета.РазделУчета);
	Для каждого СчетУчетаРаздела Из СтрокиСчетовРаздела.СчетаУчета Цикл
		Строка = ДокументИнвентаризация.СчетаУчета.Добавить();
		Строка.СчетУчета = СчетУчетаРаздела;
		Строка.УчаствуетВРасчетах = Истина;
	КонецЦикла;
	
	Возврат ДокументИнвентаризация; 
	
КонецФункции

Процедура ЗаполнитьДокумент(ДокументИнвентаризация, ДанныеУчета)

	Пока ДанныеУчета.Следующий() Цикл
		СтрокаОбъектовУчета = ДокументИнвентаризация.ОбъектыУчета.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаОбъектовУчета,ДанныеУчета);
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаОбъектовУчета.СчетУчета);
		Если СвойстваСчета.Вид = ВидСчета.Активный И СтрокаОбъектовУчета.Сумма < 0 Тогда
			СтрокаОбъектовУчета.Сумма = 0;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	ВыбранныйРаздел = Неопределено;
	Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда
		ВыбранныйРаздел = Параметры.Ключ.РазделУчета;
	ИначеЕсли Параметры.Свойство("ЗначенияЗаполнения") Тогда
		Параметры.ЗначенияЗаполнения.Свойство("РазделУчета", ВыбранныйРаздел);
	ИначеЕсли Параметры.Свойство("ЗначениеКопирования")
		И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		ВыбранныйРаздел = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Параметры.ЗначениеКопирования, "РазделУчета");
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ВыбранныйРаздел) Тогда
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = "ФормаДокумента";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Проведение

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата                            КАК Период,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ДанныеДокумента.Ответственный                   КАК Ответственный,
	|	ДанныеДокумента.Ответственный                   КАК Автор,
	|	ДанныеДокумента.Номер                           КАК Номер,
	|	ДанныеДокумента.Комментарий                     КАК Комментарий,
	|	ДанныеДокумента.ПометкаУдаления                 КАК ПометкаУдаления,
	|	ДанныеДокумента.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации,
	|	ДанныеДокумента.ПодразделениеОрганизации        КАК ПодразделениеОрганизации,
	|	ДанныеДокумента.Проведен                        КАК Проведен
	|ИЗ
	|	Документ.ИнвентаризацияОбъектовУчета КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения(Реквизиты) Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
		
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)
	
	Значения = Новый Структура;
	Значения.Вставить("ИдентификаторМетаданных",           ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ИнвентаризацияОбъектовУчета"));
	Значения.Вставить("ВалютаУправленческогоУчета",        Константы.ВалютаУправленческогоУчета.Получить());
	Значения.Вставить("ХозяйственнаяОперация",             Перечисления.ХозяйственныеОперации.ИнвентаризацияОбъектовУчета);
	
	Если Реквизиты <> Неопределено Тогда
		Значения.Вставить("НомерНаПечать",                  ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
		Значения.Вставить("ВалютаРегламентированногоУчета", ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Реквизиты.Организация));
	КонецЕсли;
	
	Возврат Значения;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ИнвентаризацияОбъектовУчета";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """"""); 
		
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
		ВЗапросеЕстьИсточник = Ложь;
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Организация                            КАК Организация,
	|	НЕОПРЕДЕЛЕНО                            КАК Партнер,
	|	НЕОПРЕДЕЛЕНО                            КАК МестоХранения,
	|	НЕОПРЕДЕЛЕНО                            КАК Контрагент,
	|	&ПодразделениеОрганизации               КАК Подразделение,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Ссылка                                 КАК Ссылка,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	НЕОПРЕДЕЛЕНО                            КАК Статус,
	|	&Ответственный                          КАК Ответственный,
	|	&Ответственный                          КАК Автор,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&ПричинаПроведенияИнвентаризации        КАК Дополнительно,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	0                                       КАК Сумма,
	|	НЕОПРЕДЕЛЕНО                            КАК Валюта,
	|	НЕОПРЕДЕЛЕНО                            КАК Договор,
	|	НЕОПРЕДЕЛЕНО                            КАК НаправлениеДеятельности,
	|	ЛОЖЬ                                    КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО                            КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО                            КАК ИсправляемыйДокумент,
	|	&Период                                 КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                            КАК Приоритет
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
