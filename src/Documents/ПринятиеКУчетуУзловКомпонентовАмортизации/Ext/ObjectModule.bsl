
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокументПередЗаполнением();
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		ЗаполнитьДокументПоОтбору(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("СправочникСсылка.ОбъектыЭксплуатации") Тогда
		ЗаполнитьНаОснованииОбъектаЭксплуатации(ДанныеЗаполнения);
	КонецЕсли;
	
	ПараметрыВыбораСтатейИАналитик = Документы.ПринятиеКУчетуУзловКомпонентовАмортизации.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ОбработкаЗаполнения(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Комментарий = "";
	ДокументВДругомУчете = Неопределено;
	
	ИнициализироватьДокументПередЗаполнением();
	
	ОбщегоНазначенияУТ.ОчиститьИдентификаторыДокумента(ЭтотОбъект, "КомпонентыАмортизации");
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ВспомогательныеРеквизиты = ВспомогательныеРеквизиты();
	ПараметрыРеквизитовОбъекта = 
		ВнеоборотныеАктивыКлиентСервер.ЗначенияСвойствЗависимыхРеквизитов_ПринятиеКУчетуУзловКомпонентовАмортизации(ЭтотОбъект, ВспомогательныеРеквизиты);
	ОбщегоНазначенияУТ.ОтключитьПроверкуЗаполненияРеквизитовОбъекта(ПараметрыРеквизитовОбъекта, МассивНепроверяемыхРеквизитов);
	
	ПроверитьКомпонентыАмортизации(МассивНепроверяемыхРеквизитов, Отказ);
	ПроверитьОсновныеСредства(Отказ);
	
	ПараметрыВыбораСтатейИАналитик = Документы.ПринятиеКУчетуУзловКомпонентовАмортизации.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, ПараметрыВыбораСтатейИАналитик);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	Если Не Отказ И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ВнеоборотныеАктивыСлужебный.ПроверитьВозможностьПринятияКУчетуОС(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	ЗаполнитьРеквизитыПередЗаписью();
	
	ПараметрыВыбораСтатейИАналитик = Документы.ПринятиеКУчетуУзловКомпонентовАмортизации.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ПередЗаписью(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	ОбщегоНазначенияУТ.ЗаполнитьИдентификаторыДокумента(ЭтотОбъект, "КомпонентыАмортизации");
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Заполнение

Процедура ИнициализироватьДокументПередЗаполнением()
	
	Ответственный = Пользователи.ТекущийПользователь();
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
	Если НЕ ЗначениеЗаполнено(ГруппаФинансовогоУчета) Тогда
		ГруппаФинансовогоУчета = Справочники.ГруппыФинансовогоУчетаВнеоборотныхАктивов.ЗначениеПоУмолчанию(Перечисления.ВидыВнеоборотныхАктивов.ОсновноеСредство);
	КонецЕсли; 
	
	ОтражатьВУпрУчете = Истина;
	ОтражатьВРеглУчете = Истина;
	
	ВнеоборотныеАктивыКлиентСервер.ПриИзмененииРеквизитов_ПринятиеКУчетуУзловКомпонентовАмортизации(
		ЭтотОбъект, 
		ВспомогательныеРеквизиты(), 
		"Организация,ОтражатьВУпрУчете,ОтражатьВРеглУчете");
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоОтбору(Основание)

	Если Основание.Свойство("Дата") Тогда
		Дата = Основание.Дата;
	КонецЕсли;
	Если Основание.Свойство("Организация") Тогда
		Организация = Основание.Организация;
	КонецЕсли;

	Если Основание.Свойство("ОсновноеСредство") Тогда
		ЗаполнитьНаОснованииОбъектаЭксплуатации(Основание.ОсновноеСредство);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаполнитьНаОснованииОбъектаЭксплуатации(ОсновноеСредство)
	
	Если НЕ ЗначениеЗаполнено(ОсновноеСредство) Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоСписокОС = (ТипЗнч(ОсновноеСредство) = Тип("Массив"));
	
	Если НЕ ЭтоСписокОС Тогда
		
		РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОсновноеСредство, "ЭтоГруппа,ТипОС");
		
		Если РеквизитыОснования.ЭтоГруппа Тогда
			
			ТекстСообщения = НСтр("ru = 'Принятие к учету узлов и компонентов амортизации на основании группы ОС невозможен.
				|Выберите ОС. Для раскрытия группы используйте клавиши Ctrl и стрелку вниз.';
				|en = 'Cannot recognize sub-assets and components of depreciation based on fixed assets group.
				|Select fixed assets. To expand the group, press Ctrl+Down.'");
			ВызватьИсключение(ТекстСообщения);
			
		ИначеЕсли РеквизитыОснования.ТипОС <> Перечисления.ТипыОС.Узел
			И РеквизитыОснования.ТипОС <> Перечисления.ТипыОС.КомпонентАмортизации
			И РеквизитыОснования.ТипОС <> Перечисления.ТипыОС.ОбъектЭксплуатации Тогда
			
			ТекстСообщения = НСтр("ru = 'Принятие к учету узлов и компонентов амортизации доступно для создания на основания типов ОС ""Объект эксплуатации"", ""Компонент амортизации"", ""Узел"".
								  |Выберите доступный тип ОС.';
								  |en = 'You can create recognition of sub-assets and components of depreciation based on the ""Asset"", ""Depreciation component"", and ""Sub-asset"" fixed asset types.
								  |Select the available fixed asset type.'");
			ВызватьИсключение(ТекстСообщения);
			
		КонецЕсли;
		
		СтрокаТабличнойЧасти = КомпонентыАмортизации.Добавить();
		
		Если РеквизитыОснования.ТипОС = Перечисления.ТипыОС.Узел
			ИЛИ РеквизитыОснования.ТипОС = Перечисления.ТипыОС.КомпонентАмортизации Тогда
			СтрокаТабличнойЧасти.КомпонентАмортизации = ОсновноеСредство;
		Иначе
			СтрокаТабличнойЧасти.ОсновноеСредство = ОсновноеСредство;
		КонецЕсли;
		
	Иначе
		Для Каждого ЭлементМассива Из ОсновноеСредство Цикл
			СтрокаТабличнойЧасти = КомпонентыАмортизации.Добавить();
			СтрокаТабличнойЧасти.ОсновноеСредство = ЭлементМассива;
		КонецЦикла;
	КонецЕсли;
	
	Документы.ПринятиеКУчетуУзловКомпонентовАмортизации.ЗаполнитьСтоимость(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаЗаполнения

Процедура ПроверитьКомпонентыАмортизации(МассивНепроверяемыхРеквизитов, Отказ)

	ВедетсяРегламентированныйУчетВНА = ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА();
	
	ВнеоборотныеАктивы.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "КомпонентыАмортизации", "КомпонентАмортизации", Отказ);
	ВнеоборотныеАктивыСлужебный.ПроверитьЧтоВыбраныУзлыКомпоненты(ЭтотОбъект, "КомпонентыАмортизации", "КомпонентАмортизации", Отказ);
	
	Если ВедетсяРегламентированныйУчетВНА Тогда
		ШаблонСообщенияЛиквидационнаяСтоимость = НСтр("ru = 'Ликвидационная стоимость в строке %1 должна быть меньше стоимости по упр. учету';
														|en = 'Residual value in line %1 must be less than the cost in manag. accounting'");
	Иначе
		ШаблонСообщенияЛиквидационнаяСтоимость = НСтр("ru = 'Ликвидационная стоимость в строке %1 должна быть меньше стоимости в валюте упр. учета';
														|en = 'Residual value in line %1 must be less than the cost in the currency of manag. accounting'");
	КонецЕсли; 
	
	ПроверятьСтоимостьБУ = (МассивНепроверяемыхРеквизитов.Найти("КомпонентыАмортизации.СтоимостьБУ") = Неопределено);
	ПроверятьСтоимостьУУ = (МассивНепроверяемыхРеквизитов.Найти("КомпонентыАмортизации.СтоимостьУУ") = Неопределено);
	
	ПредставлениеРеквизитов = Документы.ПринятиеКУчетуУзловКомпонентовАмортизации.ПредставлениеРеквизитов(Организация);
	
	ШаблонСообщения = НСтр("ru = 'Не заполнена колонка ""%1"" в строке %2 списка ""Основные средства""';
							|en = 'Column ""%1"" in line %2 of the ""Fixed assets"" list is not filled in'");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КомпонентыАмортизации.НомерСтроки КАК НомерСтроки,
	|	ВЫРАЗИТЬ(КомпонентыАмортизации.КомпонентАмортизации КАК Справочник.ОбъектыЭксплуатации) КАК КомпонентАмортизации,
	|	КомпонентыАмортизации.СтоимостьБУ КАК СтоимостьБУ,
	|	КомпонентыАмортизации.СтоимостьУУ КАК СтоимостьУУ,
	|	КомпонентыАмортизации.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость
	|ПОМЕСТИТЬ КомпонентыАмортизации
	|ИЗ
	|	&КомпонентыАмортизации КАК КомпонентыАмортизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КомпонентыАмортизации.НомерСтроки КАК НомерСтроки,
	|	ПРЕДСТАВЛЕНИЕ(КомпонентыАмортизации.КомпонентАмортизации) КАК КомпонентАмортизации,
	|	ВЫБОР
	|		КОГДА КомпонентыАмортизации.КомпонентАмортизации.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.КомпонентАмортизации)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоТипКомпонентАмортизации,
	|	КомпонентыАмортизации.СтоимостьБУ КАК СтоимостьБУ,
	|	КомпонентыАмортизации.СтоимостьУУ КАК СтоимостьУУ,
	|	КомпонентыАмортизации.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость
	|ИЗ
	|	КомпонентыАмортизации КАК КомпонентыАмортизации";
	Запрос.УстановитьПараметр("КомпонентыАмортизации", КомпонентыАмортизации.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НомерСтроки = Формат(Выборка.НомерСтроки, "ЧГ=");
		
		Если Выборка.ЭтоТипКомпонентАмортизации
			И (МетодНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции
				Или МетодНачисленияАмортизацииУУ = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции) Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Для компонента амортизации %1 нельзя указывать метод начисления %2';
											|en = 'Cannot specify the %2 depreciation method for the %1 depreciation component'"),
				Выборка.КомпонентАмортизации, Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("КомпонентыАмортизации", Выборка.НомерСтроки, "КомпонентАмортизации");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
			
		КонецЕсли;
		
		Если ПроверятьСтоимостьБУ
			И Выборка.СтоимостьБУ = 0 Тогда
			
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПредставлениеРеквизитов.Получить("КомпонентыАмортизации.СтоимостьБУ"), НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("КомпонентыАмортизации", Выборка.НомерСтроки, "СтоимостьБУ");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
		КонецЕсли;
		
		Если ПроверятьСтоимостьУУ
			И Выборка.СтоимостьУУ = 0 Тогда
			
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПредставлениеРеквизитов.Получить("КомпонентыАмортизации.СтоимостьУУ"), НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("КомпонентыАмортизации", Выборка.НомерСтроки, "СтоимостьУУ");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
		КонецЕсли;
		
		Если Выборка.ЛиквидационнаяСтоимость >= Выборка.СтоимостьУУ 
			И Выборка.СтоимостьУУ <> 0
			И ОтражатьВУпрУчете Тогда
			
			ТекстСообщения = СтрШаблон(ШаблонСообщенияЛиквидационнаяСтоимость, НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("КомпонентыАмортизации", Выборка.НомерСтроки, "ЛиквидационнаяСтоимость");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
			
		КонецЕсли;
		
	КонецЦикла; 
	
	МассивНепроверяемыхРеквизитов.Добавить("КомпонентыАмортизации.СтоимостьБУ");
	МассивНепроверяемыхРеквизитов.Добавить("КомпонентыАмортизации.СтоимостьУУ");
	
КонецПроцедуры

Процедура ПроверитьОсновныеСредства(Отказ)

	ВнеоборотныеАктивыСлужебный.ПроверитьЧтоВыбраныОС(ЭтотОбъект, "КомпонентыАмортизации", "ОсновноеСредство", Отказ);

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОбъектыЭксплуатации.Ссылка КАК ОсновноеСредство,
	|	ОбъектыЭксплуатации.Наименование КАК ОсновноеСредствоПредставление,
	|	ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) КАК СостояниеУУ,
	|	ЕСТЬNULL(ПорядокУчетаОСБУ.СостояниеБУ, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) КАК СостояниеБУ
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
	|			&Период, 
	|			&ОтражатьВУпрУчете
	|				И Организация = &Организация
	|				И ОсновноеСредство В (&МассивОС)) КАК ПорядокУчетаОСУУ
	|		ПО ПорядокУчетаОСУУ.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|			&Период, 
	|			&ОтражатьВРеглУчете
	|				И Организация = &Организация
	|				И ОсновноеСредство В (&МассивОС)) КАК ПорядокУчетаОСБУ
	|		ПО ПорядокУчетаОСБУ.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка
	|
	|ГДЕ
	|	ОбъектыЭксплуатации.Ссылка В (&МассивОС)
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ОтражатьВРеглУчете", ОтражатьВРеглУчете);
	Запрос.УстановитьПараметр("ОтражатьВУпрУчете", ОтражатьВУпрУчете);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("МассивОС", КомпонентыАмортизации.ВыгрузитьКолонку("ОсновноеСредство"));
	Запрос.УстановитьПараметр("Период",   Дата - 1); // Для корректного закрытия месяца необходимо, чтобы ОС-владельцы были заведены раньше узлов и компонентов амортизации
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеСтроки = КомпонентыАмортизации.Найти(Выборка.ОсновноеСредство, "ОсновноеСредство");
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("КомпонентыАмортизации", ДанныеСтроки.НомерСтроки, "ОсновноеСредство");
		
		Если ОтражатьВУпрУчете
			И Выборка.СостояниеУУ = Перечисления.СостоянияОС.НеПринятоКУчету Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Основное средство ""%1"" не принято к учету в управленческом учете.';
											|en = 'The ""%1"" fixed asset is not recognized in management accounting.'"), Выборка.ОсновноеСредствоПредставление);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);

		ИначеЕсли ОтражатьВУпрУчете
			И Выборка.СостояниеУУ = Перечисления.СостоянияОС.СнятоСУчета Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Основное средство ""%1"" снято с учета в управленческом учете.';
											|en = 'The ""%1"" fixed asset is derecognized in management accounting.'"), Выборка.ОсновноеСредствоПредставление);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);

		КонецЕсли;
		
		Если ОтражатьВРеглУчете
			И Выборка.СостояниеБУ = Перечисления.СостоянияОС.НеПринятоКУчету Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Основное средство ""%1"" не принято к учету в бухгалтерском учете.';
											|en = 'The ""%1"" fixed asset is not recognized in local accounting.'"), Выборка.ОсновноеСредствоПредставление);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);

		ИначеЕсли ОтражатьВРеглУчете
			И Выборка.СостояниеБУ = Перечисления.СостоянияОС.СнятоСУчета Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Основное средство ""%1"" снято с учета в бухгалтерском учете.';
											|en = 'The ""%1"" fixed asset is derecognized in local accounting.'"), Выборка.ОсновноеСредствоПредставление);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
			
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ЗаполнитьРеквизитыПередЗаписью()

	ОчиститьНеиспользуемыеРеквизиты();
	
	Если ОтражатьВУпрУчете И ОтражатьВРеглУчете Тогда
		ДокументВДругомУчете = Неопределено;
	КонецЕсли;
	
	ВалютаУпр = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРегл = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	
	Для каждого ДанныеСтроки Из КомпонентыАмортизации Цикл
		
		Если НЕ НастройкиНалоговУчетныхПолитикПовтИсп.РеглУчетВНАВедетсяНезависимо(Организация, КонецМесяца(?(Дата <> '000101010000', Дата, ТекущаяДатаСеанса())))
			И ВалютаУпр = ВалютаРегл Тогда
		
			ДанныеСтроки.СтоимостьУУ = ДанныеСтроки.СтоимостьБУ;
		
		КонецЕсли; 
	
		Если ВалютаУпр = ВалютаРегл Тогда
			ДанныеСтроки.ЛиквидационнаяСтоимость = ДанныеСтроки.ЛиквидационнаяСтоимостьРегл;
		КонецЕсли;
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура ОчиститьНеиспользуемыеРеквизиты()
	
	ВспомогательныеРеквизиты = ВспомогательныеРеквизиты();
	
	ПараметрыРеквизитовОбъекта = ВнеоборотныеАктивыКлиентСервер.ЗначенияСвойствЗависимыхРеквизитов_ПринятиеКУчетуУзловКомпонентовАмортизации(
					ЭтотОбъект, ВспомогательныеРеквизиты);
					
	ОбщегоНазначенияУТКлиентСервер.ОчиститьНеиспользуемыеРеквизиты(ЭтотОбъект, ПараметрыРеквизитовОбъекта, "КомпонентыАмортизации");

КонецПроцедуры

Функция ВспомогательныеРеквизиты()
	
	ВспомогательныеРеквизиты = Новый Структура;
	ВспомогательныеРеквизиты.Вставить("ИспользоватьРеглУчет", РеглУчетСервер.ВедетсяРеглУчет(Дата));
	ВспомогательныеРеквизиты.Вставить("ИспользоватьОбъектыСтроительства", ПолучитьФункциональнуюОпцию("ИспользоватьОбъектыСтроительства"));
	ВспомогательныеРеквизиты.Вставить("ОтражатьВРеглУчете", ОтражатьВРеглУчете);
	ВспомогательныеРеквизиты.Вставить("ОтражатьВУпрУчете", ОтражатьВУпрУчете);
	ВспомогательныеРеквизиты.Вставить("ОтражатьВБУ", ОтражатьВРеглУчете);
	ВспомогательныеРеквизиты.Вставить("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());
	
	ВалютаУпр = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРегл = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	ВспомогательныеРеквизиты.Вставить("ВалютыСовпадают", ВалютаУпр = ВалютаРегл);
	
	ВспомогательныеРеквизиты.Вставить(
		"ЕстьУчетСебестоимости", 
		РасчетСебестоимостиПовтИсп.ФормироватьДвиженияПоРегистрамСебестоимости(Дата, Ложь));
		
	ВспомогательныеРеквизиты.Вставить(
		"РеглУчетВНАВедетсяНезависимо",
		НастройкиНалоговУчетныхПолитикПовтИсп.РеглУчетВНАВедетсяНезависимо(Организация, КонецМесяца(?(Дата <> '000101010000', Дата, ТекущаяДатаСеанса()))));
	
	ПринятиеКУчетуУзловКомпонентовАмортизацииЛокализация.ДополнитьВспомогательныеРеквизиты(ЭтотОбъект, ВспомогательныеРеквизиты);
	
	Возврат ВспомогательныеРеквизиты;

КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
