#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	Если СтрСравнить(ВидФормы, "ФормаОбъекта") = 0 Тогда
		Значения = ОбщегоНазначенияБЗК.ЗначенияСвойств(Параметры, "Ключ, ИдентификаторСообщения, НомерСтрокиСообщения");
		Если ЗначениеЗаполнено(Значения.Ключ) Тогда
			Возврат;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Значения.ИдентификаторСообщения) Или Не ЗначениеЗаполнено(Значения.НомерСтрокиСообщения) Тогда
			Возврат;
		КонецЕсли;
		Настройки = ЗапросыБЗК.НастройкиЗапросаКТаблице();
		Настройки.УчитыватьRLS = Истина;
		Настройки.Количество = 1;
		ЗапросыБЗК.ДобавитьОтбор(Настройки.Отбор, "ИдентификаторСообщения", "=", Значения.ИдентификаторСообщения);
		ЗапросыБЗК.ДобавитьОтбор(Настройки.Отбор, "НомерСтрокиСообщения", "=", Значения.НомерСтрокиСообщения);
		Запрос = ЗапросыБЗК.ЗапросКТаблице(Метаданные.Документы.ЗапросНедостающихСведенийПособияСФР, "Ссылка", Настройки);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СтандартнаяОбработка = Ложь;
			Параметры.Вставить("Ключ", Выборка.Ссылка);
			ВыбраннаяФорма = "Документ.ЗапросНедостающихСведенийПособияСФР.Форма.ФормаДокумента";
		КонецЕсли;
	КонецЕсли;
	#КонецЕсли
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.Печать

// Заполняет список команд печати.
// 
// Параметры:
//  КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной
//                                                            параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной
//                                            параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ФизическоеЛицо)
	|	И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ТекущиеДела

// См. ТекущиеДелаПереопределяемый.ПриОпределенииОбработчиковТекущихДел.
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.ЗапросНедостающихСведенийПособияСФР;
	Если Не СЭДОФСС.ДоступенОбменЧерезСЭДО()
		Или Не ПравоДоступа("Изменение", МетаданныеДокумента) Тогда
		Возврат; // Нет прав.
	КонецЕсли;
	
	МодульТекущиеДелаСервер = ОбщегоНазначения.ОбщийМодуль("ТекущиеДелаСервер");
	Разделы = МодульТекущиеДелаСервер.РазделыДляОбъекта(МетаданныеДокумента.ПолноеИмя());
	Если Разделы.Количество() = 0 Тогда
		Возврат; // Некорректное внедрение.
	КонецЕсли;
	
	Требования = ТребованияПоОтправке();
	ВРаботе                  = Требования.Количество();
	ТребуетсяЗагрузить       = Требования.НайтиСтроки(Новый Структура("ТребуетсяЗагрузить", Истина)).Количество();
	ТребуетсяОтветитьВсего   = Требования.НайтиСтроки(Новый Структура("ТребуетсяОтветить", Истина)).Количество();
	ТребуетсяОтветитьСегодня = Требования.НайтиСтроки(Новый Структура("ТребуетсяОтветитьСегодня", Истина)).Количество();
	ТребуетсяОтветитьЗавтра  = ТребуетсяОтветитьВсего - ТребуетсяОтветитьСегодня;
	ТребуетсяПодтвердить     = Требования.НайтиСтроки(Новый Структура("ТребуетсяПодтвердить", Истина)).Количество();
	ИмяДокумента             = МетаданныеДокумента.Имя;
	ПредставлениеСписка      = МетаданныеДокумента.ПредставлениеСписка;
	
	Для Каждого Раздел Из Разделы Цикл
		
		ПолноеИмяРаздела = СтрЗаменить(Раздел.ПолноеИмя(), ".", "_");
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = ИмяДокумента + "_ВРаботе_" + ПолноеИмяРаздела;
		Дело.ЕстьДела       = (ВРаботе > 0);
		Дело.Важное         = (ТребуетсяОтветитьСегодня > 0);
		Дело.Владелец       = Раздел;
		Дело.Представление  = ПредставлениеСписка;
		Дело.Количество     = ВРаботе;
		Дело.Подсказка      = НСтр("ru = 'Извещения о представлении недостающих документов для выплаты пособий СФР.';
									|en = 'Notifications about submission of the necessary documents to receive benefits from the Social Insurance Fund of Russia.'");
		Дело.ПараметрыФормы = Новый Структура("ТолькоВРаботе", Истина);
		Дело.Форма          = "Документ.ЗапросНедостающихСведенийПособияСФР.ФормаСписка";
		
		ИдентификаторРодителя = Дело.Идентификатор;
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = ИмяДокумента + "_Загрузить_" + ПолноеИмяРаздела;
		Дело.ЕстьДела       = (ТребуетсяЗагрузить > 0);
		Дело.Важное         = Истина;
		Дело.Владелец       = ИдентификаторРодителя;
		Дело.Представление  = НСтр("ru = 'Получить новые извещения из СФР';
									|en = 'Get new notifications from the Social Insurance Fund of Russia'");
		Дело.Количество     = ТребуетсяЗагрузить;
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = ИмяДокумента + "_ОтветитьСегодня_" + ПолноеИмяРаздела;
		Дело.ЕстьДела       = (ТребуетсяОтветитьСегодня > 0);
		Дело.Важное         = Истина;
		Дело.Владелец       = ИдентификаторРодителя;
		Дело.Представление  = НСтр("ru = 'Отправить недостающие сведения сегодня';
									|en = 'Send the missing information today'");
		Дело.Количество     = ТребуетсяОтветитьСегодня;
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = ИмяДокумента + "_Ответить_" + ПолноеИмяРаздела;
		Дело.ЕстьДела       = (ТребуетсяОтветитьЗавтра > 0);
		Дело.Важное         = Ложь;
		Дело.Владелец       = ИдентификаторРодителя;
		Дело.Представление  = НСтр("ru = 'Отправить недостающие сведения';
									|en = 'Send the missing information'");
		Дело.Количество     = ТребуетсяОтветитьЗавтра;
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = ИмяДокумента + "_Подтвердить_" + ПолноеИмяРаздела;
		Дело.ЕстьДела       = (ТребуетсяПодтвердить > 0);
		Дело.Важное         = Ложь;
		Дело.Владелец       = ИдентификаторРодителя;
		Дело.Представление  = НСтр("ru = 'Подтвердить получение';
									|en = 'Confirm receipt'");
		Дело.Количество     = ТребуетсяПодтвердить;
		
	КонецЦикла;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ТекущиеДела

#КонецОбласти

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс


#Область ОбновлениеИнформационнойБазы

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.34.3";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор   = Новый УникальныйИдентификатор("ed06b276-2bea-11f0-8154-a842a16ef6f5");
	Обработчик.Процедура       = "Документы.ЗапросНедостающихСведенийПособияСФР.ПовторноОбработатьСообщения1010";
	Обработчик.Комментарий     = НСтр("ru = 'Создание документов по сообщениям СЭДО СФР 1010.';
										|en = 'Create documents based on Social Insurance Fund EDI 1010 messages.'");
	
КонецПроцедуры

// Обработка об изменении расчета пособий.
//
// Параметры:
//   ПараметрыОбновления - Структура - Параметры отложенного обновления.
//
Процедура ПовторноОбработатьСообщения1010(ПараметрыОбновления = Неопределено) Экспорт
	СЭДОФСС.ПовторноОбработатьВходящиеСообщенияСЭДО(ПараметрыОбновления, 1010);
КонецПроцедуры

#КонецОбласти

#Область СЭДО

Функция ТипСообщения(Документ = Неопределено) Экспорт
	Возврат 1010;
КонецФункции

Функция Метаданные() Экспорт
	Возврат Метаданные.Документы.ЗапросНедостающихСведенийПособияСФР;
КонецФункции

Процедура ЗагрузитьУведомлениеОНаличииСообщения(ТипСообщения, Страхователь, Уведомление, ЕстьИзменения) Экспорт
	ИдентификаторСообщения = Уведомление.Идентификатор;
	ТребуетсяПодтверждение = Уведомление.ТребуетсяПодтверждение;
	
	СуществующиеДокументы = НайтиПоИдентификаторуСообщения(ИдентификаторСообщения);
	Если СуществующиеДокументы.Количество() > 0 Тогда
		Фильтр = Новый Структура;
		Фильтр.Вставить("Страхователь", Страхователь);
		Фильтр.Вставить("ПометкаУдаления", Ложь);
		Фильтр.Вставить("ТребуетсяПодтверждение", ТребуетсяПодтверждение);
		Если СуществующиеДокументы.НайтиСтроки(Фильтр).Количество() = СуществующиеДокументы.Количество() Тогда
			Возврат;
		КонецЕсли;
	Иначе
		СуществующиеДокументы.Добавить(); // Все реквизиты = Неопределено.
	КонецЕсли;
	
	Для Каждого СуществующийДокумент Из СуществующиеДокументы Цикл
		Если СуществующийДокумент.Загружен = Истина Или СуществующийДокумент.Обработан = Истина Тогда
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(СуществующийДокумент.Ссылка) Тогда
			ДокументОбъект = СуществующийДокумент.Ссылка.ПолучитьОбъект();
		Иначе
			ДокументОбъект = СоздатьДокумент();
		КонецЕсли;
		ДокументОбъект.Страхователь           = Страхователь;
		ДокументОбъект.ИдентификаторСообщения = ИдентификаторСообщения;
		ДокументОбъект.ТребуетсяПодтверждение = ТребуетсяПодтверждение;
		ДокументОбъект.Организация            = Страхователь;
		ДокументОбъект.ЗаполнитьДату();
		ДокументОбъект.НомерСтрокиСообщения = 1;
		ДокументОбъект.ОбновитьВторичныеДанные();
		СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, "БЗК", РежимЗаписиДокумента.Запись);
		
		ЕстьИзменения = Истина;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗагрузитьСообщение1010(Страхователь, ИдентификаторСообщения, ТекстXML, ТребуетсяПодтверждение, Результат, Кэш) Экспорт
	СтруктураДокумента = СтруктураДокументаИзXML(ТекстXML, Результат);
	Если СтруктураДокумента = Неопределено Тогда
		Возврат;
	ИначеЕсли СтруктураДокумента.Пособия = Неопределено Тогда
		СЭДОФСС.ЗаписатьОшибкуПоискаУзла(Результат, "benefit");
		Возврат;
	ИначеЕсли СтруктураДокумента.Пособия.Количество() = 0 Тогда
		СЭДОФСС.ЗаписатьОшибкуЗаполненностиУзла(Результат, "benefit");
		Возврат;
	КонецЕсли;
	
	Пособия = СтруктураДокумента.Пособия;
	
	ИзменяемыеРеквизиты = Новый Массив;
	Для Каждого КлючИЗначение Из СтруктураДокумента Цикл
		Если КлючИЗначение.Значение = Неопределено Тогда
			СтруктураДокумента.Удалить(КлючИЗначение.Ключ);
		Иначе
			ИзменяемыеРеквизиты.Добавить(КлючИЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;
	Для Каждого Колонка Из Пособия.Колонки Цикл
		ИзменяемыеРеквизиты.Добавить(Колонка.Имя);
	КонецЦикла;
	ИзменяемыеРеквизиты.Добавить("НомерПроцесса");
	ИзменяемыеРеквизиты.Добавить("Загружен");
	ИзменяемыеРеквизиты.Добавить("Страхователь");
	ИзменяемыеРеквизиты.Добавить("ИдентификаторСообщения");
	ИзменяемыеРеквизиты.Добавить("ТребуетсяПодтверждение");
	ИзменяемыеРеквизиты.Добавить("Дата");
	
	СтруктураДокумента.Вставить("КоличествоСтрокСообщения", Пособия.Количество());
	
	СуществующиеДокументы = НайтиПоИдентификаторуСообщения(ИдентификаторСообщения);
	
	Для Каждого ИнформацияОПособии Из Пособия Цикл
		
		СуществующийДокумент = СуществующиеДокументы.Найти(ИнформацияОПособии.НомерСтрокиСообщения, "НомерСтрокиСообщения");
		Если СуществующийДокумент <> Неопределено Тогда
			ДокументОбъект = СуществующийДокумент.Ссылка.ПолучитьОбъект();
			ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюРеквизитовШапки(ДокументОбъект, ИзменяемыеРеквизиты);
			ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюТабличнойЧасти(ДокументОбъект, "НедостающиеСведения");
		Иначе
			ДокументОбъект = СоздатьДокумент();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, СтруктураДокумента);
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ИнформацияОПособии);
		
		Если ЗначениеЗаполнено(ИнформацияОПособии.НомерПроцессаСтрокой) Тогда
			НомерПроцесса = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ИнформацияОПособии.НомерПроцессаСтрокой);
			Если ЗначениеЗаполнено(НомерПроцесса) Тогда
				ДокументОбъект.НомерПроцесса = НомерПроцесса;
			КонецЕсли;
		КонецЕсли;
		Если ИнформацияОПособии.НедостающиеСведения <> Неопределено
			И ИнформацияОПособии.НедостающиеСведения.Количество() > 0 Тогда
			ДокументОбъект.НедостающиеСведения.Загрузить(ИнформацияОПособии.НедостающиеСведения);
		КонецЕсли;
		
		ДокументОбъект.Загружен               = Истина;
		ДокументОбъект.Страхователь           = Страхователь;
		ДокументОбъект.ИдентификаторСообщения = ИдентификаторСообщения;
		ДокументОбъект.ТребуетсяПодтверждение = ТребуетсяПодтверждение;
		ДокументОбъект.ЗаполнитьДату(Кэш);
		ДокументОбъект.ОбновитьВторичныеДанные();
		
		СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, "БЗК", РежимЗаписиДокумента.Проведение);
	КонецЦикла;
	
	Результат.Обработано = Истина;
КонецПроцедуры

// См. ЭлектронныйДокументооборотСФССПереопределяемый.ПослеПолученияОтветаНаПодтверждениеОПрочтении.
Процедура ЗагрузитьОтветНаПодтверждениеПолученияСообщения(Страхователь, ИсходноеСообщение, Результат, ЕстьИзменения) Экспорт
	// Вызывается при получении сообщения с типом 11 в ответ на подтверждение о прочтении входящего сообщения.
	//   Страхователь - СправочникСсылка.Организации - организация, получатель сообщения.
	//   ИсходноеСообщение - Структура:
	//     * ИдентификаторСообщения - Строка - идентификатор исходного сообщения СЭДО, по которому отправлялось подтверждение.
	//     * Тип                    - Число  - тип исходного сообщения СЭДО, по которому отправлялось подтверждение.
	//     * ТекстОшибки            - Строка - ошибка приема подтверждения.
	//     * ТекстПредупреждения    - Строка - предупреждение приема подтверждения.
	//   Результат - Структура - результат обработки сообщения:
	//     * Обработано      - Булево - признак того, что сообщение было успешно обработано.
	//     * ОшибкаОбработки - Булево - признак того, что при обработке сообщения возникла ошибка.
	//     * ОписаниеОшибки  - Строка - описание ошибки обработки.
	
	// Поиск документа по идентификатору сообщения.
	СуществующиеДокументы = НайтиПоИдентификаторуСообщения(ИсходноеСообщение.ИдентификаторСообщения);
	
	// Заполнение подтверждения получения.
	Для Каждого СуществующийДокумент Из СуществующиеДокументы Цикл
		ДокументОбъект = СуществующийДокумент.Ссылка.ПолучитьОбъект();
		Если ДокументОбъект.ЗаполнитьПодтверждениеПолучения(Неопределено) Тогда
			СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, "БЗК", РежимЗаписиДокумента.Проведение);
			ЕстьИзменения = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СоставДокументов

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
//
Функция ОписаниеСоставаОбъекта() Экспорт
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаФизическоеЛицоВШапке("ФизическоеЛицо", "Сотрудник");
КонецФункции

#КонецОбласти

#Область ФиксацияВторичныхДанныхВДокументах

Функция ПараметрыФиксацииВторичныхДанных() Экспорт
	ФиксируемыеРеквизиты = ФиксируемыеРеквизиты();
	ФиксируемыеТаблицы = Новый Структура("НедостающиеСведения", СтрРазделить("Поле", ",", Ложь));
	Возврат ФиксацияВторичныхДанныхВДокументах.ПараметрыФиксации(ФиксируемыеРеквизиты, ФиксируемыеТаблицы);
КонецФункции

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область ФиксацияВторичныхДанныхВДокументах

Функция ФиксируемыеРеквизиты()
	ФиксируемыеРеквизиты = Новый Соответствие;
	
	// Входящие ссылки, исходящие ссылки, результат обработки.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения = "ЭтотОбъект";
	Группа.ИмяГруппы           = "СсылкиИВторичныеДанные";
	
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ВходящееЗаявление");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ВходящийЗапрос");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ВходящийОтветНаЗапрос");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ВходящийПервичныйДокумент");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ВходящийРеестр");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Обработан", Ложь);
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Организация");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Ребенок");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Сотрудник");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ФизическоеЛицо");
	
	// Реквизиты XML.
	РеквизитыРучногоЗаполнения = Новый Структура("Ответственный, Комментарий");
	ОбъектМетаданных = Метаданные();
	Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Шаблон.ИмяГруппы           = "ДанныеXML";
	Шаблон.ОснованиеЗаполнения = "ДанныеXML";
	Для Каждого Реквизит Из ОбъектМетаданных.Реквизиты Цикл
		Если РеквизитыРучногоЗаполнения.Свойство(Реквизит.Имя)
			Или ФиксируемыеРеквизиты[Реквизит.Имя] <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, Реквизит.Имя);
	КонецЦикла;
	Шаблон.Путь = "НедостающиеСведения";
	Для Каждого Реквизит Из ОбъектМетаданных.ТабличныеЧасти.НедостающиеСведения.Реквизиты Цикл
		Если РеквизитыРучногоЗаполнения.Свойство(Шаблон.Путь + Реквизит.Имя)
			Или ФиксируемыеРеквизиты[Шаблон.Путь + Реквизит.Имя] <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, Реквизит.Имя);
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(ФиксируемыеРеквизиты);
КонецФункции

#КонецОбласти

#Область XML

Функция СтруктураДокументаИзXML(ТекстXML, Результат) Экспорт
	// Поиск нужного узла.
	СтруктураDOM = СериализацияБЗК.СтруктураDOM(ТекстXML);
	КореньDOM = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "//*[local-name() = 'notice']");
	Если КореньDOM = Неопределено Тогда
		СЭДОФСС.ЗаписатьОшибкуПоискаУзла(Результат, "notice");
		Возврат Неопределено;
	КонецЕсли;
	
	// Подготовка структуры документа.
	СтруктураДокумента = Новый Структура(
	"ВерсияСпецификации,
	|ИдентификаторСообщения101,
	|ДатаСообщения,
	|ВходящийНомер,
	|ВходящаяДата,
	|КодНПА,
	|КодТОФ,
	|НаименованиеТОФ,
	|ВходящийНомерРеестра,
	|РегистрационныйНомерСФР,
	|Пособия,
	|ОтветственныйСФРФИО,
	|ОтветственныйСФРДолжность,
	|ОтветственныйСФРТелефон,
	|ПодписантСФРФИО,
	|ПодписантСФРДолжность,
	|НеобработанныеУзлы");
	
	// Чтение реквизитов узла.
	ПрочитатьАтрибутыКорняXML(КореньDOM, СтруктураДокумента);
	ПрочитатьРеквизитыXML(КореньDOM, СтруктураДокумента);
	
	Возврат СтруктураДокумента;
КонецФункции

Процедура ПрочитатьАтрибутыКорняXML(КореньDOM, СтруктураДокумента)
	// Пример корневого узла XML-сообщения:
	//  <n1:notice xmlns:n1="urn:ru:ecp:integration:vnim:notice:v3.0"
	//           xmlns:com="urn:ru:ecp:integration:types:common:v3.0"
	//           xmlns:p="urn:ru:ecp:integration:types:person:v3.0"
	//           specVersion="3.0.1"
	//           messageDateTime="2024-04-18T09:30:47Z">
	АтрибутыКорня = СериализацияБЗК.АтрибутыЭлементаDOM(КореньDOM, "specVersion,messageUuid101,messageDateTime");
	СтруктураДокумента.ВерсияСпецификации = АтрибутыКорня.specVersion;
	СтруктураДокумента.ИдентификаторСообщения101 = АтрибутыКорня.messageUuid101;
	СтруктураДокумента.ДатаСообщения = СериализацияБЗК.ДатаИзXML(АтрибутыКорня.messageDateTime);
КонецПроцедуры

Процедура ПрочитатьРеквизитыXML(КореньDOM, СтруктураДокумента)
	// Пример реквизитов XML-сообщения:
	//	<noticeNumber>5861</noticeNumber>
	//	<noticeDate>2024-04-18</noticeDate>
	//	<noticeReason>3</noticeReason>
	//	<tof>
	//		<com:code>086</com:code>
	//		<com:name>ОСФР ПО ЯРОСЛАВСКОЙ ОБЛАСТИ</com:name>
	//	</tof>
	//	<registryNum>2025-0228-1024-403-3104201008</registryNum>
	//	<insurerInfo>
	//		<regNumSFR>1000260750</regNumSFR>
	//	</insurerInfo>
	//	<benefit>
	//		<benefitInfo>
	//			<rowNumber>1</rowNumber>
	//			<benefit88>
	//				<dt1>2024-03-01</dt1>
	//			</benefit88>
	//		</benefitInfo>
	//		<insuredInfo>
	//			<snils>13700328015</snils>
	//			<fullName>
	//				<p:firstName>Иван</p:firstName>
	//				<p:lastName>Иванов</p:lastName>
	//				<p:middleName>Иванович</p:middleName>
	//			</fullName>
	//		</insuredInfo>
	//		<informationRequest>
	//			<item>
	//				<requestText>Прошу проверить корректность заполнения количества отработанных часов, уточнить сведения</requestText>
	//				<requestFields>
	//					<field>
	//						<fieldName>Данные для расчёта: Количество отработанных часов</fieldName>
	//						<fieldCode>submit2023WorkerRegistrRequest/row/calculationData/calcShifts</fieldCode>
	//					</field>
	//				</requestFields>
	//			</item>
	//		</informationRequest>
	//	</benefit>
	//	<senderInfo>
	//		<fullName>Петров Петр Петрович</fullName>
	//		<role>Заместитель начальника отдела назначения</role>
	//		<phone>+7 495 123 45 78 (1234)</phone>
	//	</senderInfo>
	//	<signatoryInfo>
	//		<fullName>Васильев Василий Васильевич</fullName>
	//		<role>Начальник отдела назначения</role>
	//	</signatoryInfo>
	УзлыDOM = СериализацияБЗК.УзлыЭлементаDOM(КореньDOM);
	НеобработанныеУзлы = Новый Массив;
	
	Если УзлыDOM.Свойство("noticeNumber") Тогда
		СтруктураДокумента.ВходящийНомер = СериализацияБЗК.СтрокаИзXML(УзлыDOM.noticeNumber);
		УзлыDOM.Удалить("noticeNumber");
	КонецЕсли;
	
	Если УзлыDOM.Свойство("noticeDate") Тогда
		СтруктураДокумента.ВходящаяДата = СериализацияБЗК.ДатаИзXML(УзлыDOM.noticeDate);
		УзлыDOM.Удалить("noticeDate");
	КонецЕсли;
	
	Если УзлыDOM.Свойство("noticeReason") Тогда
		СтруктураДокумента.КодНПА = СериализацияБЗК.СтрокаИзXML(УзлыDOM.noticeReason);
		УзлыDOM.Удалить("noticeReason");
	КонецЕсли;
	
	Если УзлыDOM.Свойство("tof") Тогда
		УзлыТОФ = СериализацияБЗК.УзлыЭлементаDOM(УзлыDOM.tof);
		Если УзлыТОФ.Свойство("code") Тогда
			СтруктураДокумента.КодТОФ = СериализацияБЗК.СтрокаИзXML(УзлыТОФ.code);
		КонецЕсли;
		Если УзлыТОФ.Свойство("name") Тогда
			СтруктураДокумента.НаименованиеТОФ = СериализацияБЗК.СтрокаИзXML(УзлыТОФ.name);
		КонецЕсли;
		УзлыDOM.Удалить("tof");
	КонецЕсли;
	
	Если УзлыDOM.Свойство("registryNum") Тогда
		СтруктураДокумента.ВходящийНомерРеестра = СериализацияБЗК.СтрокаИзXML(УзлыDOM.registryNum);
		УзлыDOM.Удалить("registryNum");
	КонецЕсли;
	
	Если УзлыDOM.Свойство("insurerInfo") Тогда
		УзлыСтрахователя = СериализацияБЗК.УзлыЭлементаDOM(УзлыDOM.insurerInfo);
		Если УзлыСтрахователя.Свойство("regNumSFR") Тогда
			СтруктураДокумента.РегистрационныйНомерСФР = СериализацияБЗК.СтрокаИзXML(УзлыСтрахователя.regNumSFR);
			УзлыСтрахователя.Удалить("regNumSFR");
		КонецЕсли;
		Для Каждого КлючИЗначение Из УзлыСтрахователя Цикл
			Ключ = "insurerInfo." + КлючИЗначение.Ключ;
			НеобработанныеУзлы.Добавить(Ключ + " = " + СериализацияБЗК.СтрокаИзXML(КлючИЗначение.Значение));
		КонецЦикла;
		УзлыDOM.Удалить("insurerInfo");
	КонецЕсли;
	
	Если УзлыDOM.Свойство("benefit") Тогда
		ВложенныеУзлы = СериализацияБЗК.СписокDOM(УзлыDOM, "benefit");
		СтруктураДокумента.Пособия = ПособияИзXML(ВложенныеУзлы, НеобработанныеУзлы);
		УзлыDOM.Удалить("benefit");
	КонецЕсли;
	
	// Данные работника СФР направившего извещение.
	Если УзлыDOM.Свойство("senderInfo") Тогда
		УзлыСФР = СериализацияБЗК.УзлыЭлементаDOM(УзлыDOM.senderInfo);
		Если УзлыСФР.Свойство("fullName") Тогда
			СтруктураДокумента.ОтветственныйСФРФИО = СериализацияБЗК.СтрокаИзXML(УзлыСФР.fullName);
		КонецЕсли;
		Если УзлыСФР.Свойство("role") Тогда
			СтруктураДокумента.ОтветственныйСФРДолжность = СериализацияБЗК.СтрокаИзXML(УзлыСФР.role);
		КонецЕсли;
		Если УзлыСФР.Свойство("phone") Тогда
			СтруктураДокумента.ОтветственныйСФРТелефон = СериализацияБЗК.СтрокаИзXML(УзлыСФР.phone);
		КонецЕсли;
		УзлыDOM.Удалить("senderInfo");
	КонецЕсли;
	
	// Данные работника СФР подписавшего извещение.
	Если УзлыDOM.Свойство("signatoryInfo") Тогда
		УзлыСФР = СериализацияБЗК.УзлыЭлементаDOM(УзлыDOM.signatoryInfo);
		Если УзлыСФР.Свойство("fullName") Тогда
			СтруктураДокумента.ПодписантСФРФИО = СериализацияБЗК.СтрокаИзXML(УзлыСФР.fullName);
		КонецЕсли;
		Если УзлыСФР.Свойство("role") Тогда
			СтруктураДокумента.ПодписантСФРДолжность = СериализацияБЗК.СтрокаИзXML(УзлыСФР.role);
		КонецЕсли;
		УзлыDOM.Удалить("signatoryInfo");
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из УзлыDOM Цикл
		НеобработанныеУзлы.Добавить(КлючИЗначение.Ключ + " = " + СериализацияБЗК.СтрокаИзXML(КлючИЗначение.Значение));
	КонецЦикла;
	
	СтруктураДокумента.НеобработанныеУзлы = СтрСоединить(НеобработанныеУзлы, ", ");
КонецПроцедуры

Функция ПособияИзXML(УзлыПособийDOM, НеобработанныеУзлы) Экспорт
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("НомерСтрокиСообщения");
	Результат.Колонки.Добавить("НомерПроцессаСтрокой");
	Результат.Колонки.Добавить("НомерСтрокиРеестра");
	Результат.Колонки.Добавить("КодВидаПособия");
	Результат.Колонки.Добавить("НомерЛН");
	Результат.Колонки.Добавить("РебенокСНИЛС");
	Результат.Колонки.Добавить("РебенокДатаРождения");
	Результат.Колонки.Добавить("ОтпускПоУходуДатаНачала");
	Результат.Колонки.Добавить("ОтпускПоУходуДатаОкончания");
	Результат.Колонки.Добавить("НачалоПериода");
	Результат.Колонки.Добавить("СотрудникСНИЛС");
	Результат.Колонки.Добавить("СотрудникФамилия");
	Результат.Колонки.Добавить("СотрудникИмя");
	Результат.Колонки.Добавить("СотрудникОтчество");
	Результат.Колонки.Добавить("НедостающиеСведения");
	
	НомерСтрокиСообщения = 0;
	Для Каждого ПособиеDOM Из УзлыПособийDOM Цикл
		НомерСтрокиСообщения = НомерСтрокиСообщения + 1;
		
		СтрокаТаблицы = Результат.Добавить();
		СтрокаТаблицы.НомерСтрокиСообщения = НомерСтрокиСообщения;
		
		УзлыПособия = СериализацияБЗК.УзлыЭлементаDOM(ПособиеDOM);
		
		Если УзлыПособия.Свойство("benefitInfo") Тогда
			УзлыПособия2 = СериализацияБЗК.УзлыЭлементаDOM(УзлыПособия.benefitInfo);
			Если УзлыПособия2.Свойство("proactiveProcessNum") Тогда
				СтрокаТаблицы.НомерПроцессаСтрокой = СериализацияБЗК.СтрокаИзXML(УзлыПособия2.proactiveProcessNum);
				УзлыПособия2.Удалить("proactiveProcessNum");
			КонецЕсли;
			Если УзлыПособия2.Свойство("rowNumber") Тогда
				СтрокаТаблицы.НомерСтрокиРеестра = СериализацияБЗК.ЧислоИзXML(УзлыПособия2.rowNumber);
				УзлыПособия2.Удалить("rowNumber");
			КонецЕсли;
			Если УзлыПособия2.Свойство("benefit1") Тогда
				СтрокаТаблицы.КодВидаПособия = "1";
				ЗагрузитьРеквизитыВидаПособия(СтрокаТаблицы, НеобработанныеУзлы, УзлыПособия2.benefit1);
				УзлыПособия2.Удалить("benefit1");
			КонецЕсли;
			Если УзлыПособия2.Свойство("benefit2") Тогда
				СтрокаТаблицы.КодВидаПособия = "2";
				ЗагрузитьРеквизитыВидаПособия(СтрокаТаблицы, НеобработанныеУзлы, УзлыПособия2.benefit2);
				УзлыПособия2.Удалить("benefit2");
			КонецЕсли;
			Если УзлыПособия2.Свойство("benefit4") Тогда
				СтрокаТаблицы.КодВидаПособия = "4";
				ЗагрузитьРеквизитыВидаПособия(СтрокаТаблицы, НеобработанныеУзлы, УзлыПособия2.benefit4);
				УзлыПособия2.Удалить("benefit4");
			КонецЕсли;
			Если УзлыПособия2.Свойство("benefit5") Тогда
				СтрокаТаблицы.КодВидаПособия = "5";
				ЗагрузитьРеквизитыВидаПособия(СтрокаТаблицы, НеобработанныеУзлы, УзлыПособия2.benefit5);
				УзлыПособия2.Удалить("benefit5");
			КонецЕсли;
			Если УзлыПособия2.Свойство("benefit6") Тогда
				СтрокаТаблицы.КодВидаПособия = "6";
				ЗагрузитьРеквизитыВидаПособия(СтрокаТаблицы, НеобработанныеУзлы, УзлыПособия2.benefit6);
				УзлыПособия2.Удалить("benefit6");
			КонецЕсли;
			// Ежемесячная выплата федеральным государственным гражданским служащим (ПП № 1652).
			Если УзлыПособия2.Свойство("benefit87") Тогда
				СтрокаТаблицы.КодВидаПособия = "87";
				ЗагрузитьРеквизитыВидаПособия(СтрокаТаблицы, НеобработанныеУзлы, УзлыПособия2.benefit87);
				УзлыПособия2.Удалить("benefit87");
			КонецЕсли;
			// Специальная социальная выплата отдельным категориям медицинских работников (ПП РФ № 2568).
			Если УзлыПособия2.Свойство("benefit88") Тогда
				СтрокаТаблицы.КодВидаПособия = "88";
				ЗагрузитьРеквизитыВидаПособия(СтрокаТаблицы, НеобработанныеУзлы, УзлыПособия2.benefit88);
				УзлыПособия2.Удалить("benefit88");
			КонецЕсли;
			// Необработанные узлы.
			Для Каждого КлючИЗначение Из УзлыПособия2 Цикл
				Ключ = "[" + НомерСтрокиСообщения + "].benefit.benefitInfo." + КлючИЗначение.Ключ;
				НеобработанныеУзлы.Добавить(Ключ + " = " + СериализацияБЗК.СтрокаИзXML(КлючИЗначение.Значение));
			КонецЦикла;
			УзлыПособия.Удалить("benefitInfo");
		КонецЕсли;
		
		Если УзлыПособия.Свойство("insuredInfo") Тогда
			ЗагрузитьРеквизитыЗастрахованногоЛица(СтрокаТаблицы, НеобработанныеУзлы, УзлыПособия.insuredInfo);
			УзлыПособия.Удалить("insuredInfo");
		КонецЕсли;
		
		Если УзлыПособия.Свойство("informationRequest") Тогда
			ЗагрузитьЗапрашиваемыеРеквизиты(СтрокаТаблицы, НеобработанныеУзлы, УзлыПособия.informationRequest);
			УзлыПособия.Удалить("informationRequest");
		КонецЕсли;
		
		// Необработанные узлы.
		Для Каждого КлючИЗначение Из УзлыПособия Цикл
			Ключ = "[" + НомерСтрокиСообщения + "].benefit." + КлючИЗначение.Ключ;
			НеобработанныеУзлы.Добавить(Ключ + " = " + СериализацияБЗК.СтрокаИзXML(КлючИЗначение.Значение));
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Процедура ЗагрузитьРеквизитыВидаПособия(СтрокаТаблицы, НеобработанныеУзлы, ВидПособияDOM)
	УзлыВида = СериализацияБЗК.УзлыЭлементаDOM(ВидПособияDOM);
	
	Если УзлыВида.Свойство("lnNumber") Тогда
		СтрокаТаблицы.НомерЛН = СериализацияБЗК.СтрокаИзXML(УзлыВида.lnNumber);
		УзлыВида.Удалить("lnNumber");
	КонецЕсли;
	Если УзлыВида.Свойство("childSnils") Тогда
		СтрокаТаблицы.РебенокСНИЛС = СЭДОФСС.СНИЛСИзXML(УзлыВида.childSnils);
		УзлыВида.Удалить("childSnils");
	КонецЕсли;
	Если УзлыВида.Свойство("childBirthDate") Тогда
		СтрокаТаблицы.РебенокДатаРождения = СериализацияБЗК.ДатаИзXML(УзлыВида.childBirthDate);
		УзлыВида.Удалить("childBirthDate");
	КонецЕсли;
	Если УзлыВида.Свойство("vacationPeriod") Тогда
		УзлыОтпуска = СериализацияБЗК.УзлыЭлементаDOM(УзлыВида.vacationPeriod, "begin, end");
		СтрокаТаблицы.ОтпускПоУходуДатаНачала    = СериализацияБЗК.ДатаИзXML(УзлыОтпуска.begin);
		СтрокаТаблицы.ОтпускПоУходуДатаОкончания = СериализацияБЗК.ДатаИзXML(УзлыОтпуска.end);
		УзлыВида.Удалить("vacationPeriod");
	КонецЕсли;
	Если УзлыВида.Свойство("dt1") Тогда
		СтрокаТаблицы.НачалоПериода = СериализацияБЗК.ДатаИзXML(УзлыВида.dt1);
		УзлыВида.Удалить("dt1");
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из УзлыВида Цикл
		Ключ = "[" + СтрокаТаблицы.НомерСтрокиСообщения + "].benefit.benefitInfo.benefit" + СтрокаТаблицы.КодВидаПособия
			+ "." + КлючИЗначение.Ключ;
		НеобработанныеУзлы.Добавить(Ключ + " = " + СериализацияБЗК.СтрокаИзXML(КлючИЗначение.Значение));
	КонецЦикла;
КонецПроцедуры

Процедура ЗагрузитьРеквизитыЗастрахованногоЛица(СтрокаТаблицы, НеобработанныеУзлы, СотрудникDOM)
	Узлы = СериализацияБЗК.УзлыЭлементаDOM(СотрудникDOM);
	
	Если Узлы.Свойство("snils") Тогда
		СтрокаТаблицы.СотрудникСНИЛС = СЭДОФСС.СНИЛСИзXML(Узлы.snils);
		Узлы.Удалить("snils");
	КонецЕсли;
	Если Узлы.Свойство("fullName") Тогда
		ФИО = СЭДОФСС.ФИОИзXML(Узлы.fullName);
		СтрокаТаблицы.СотрудникФамилия = ФИО.Фамилия;
		СтрокаТаблицы.СотрудникИмя = ФИО.Имя;
		СтрокаТаблицы.СотрудникОтчество = ФИО.Отчество;
		Узлы.Удалить("fullName");
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из Узлы Цикл
		Ключ = "[" + СтрокаТаблицы.НомерСтрокиСообщения + "].benefit.insuredInfo." + КлючИЗначение.Ключ;
		НеобработанныеУзлы.Добавить(Ключ + " = " + СериализацияБЗК.СтрокаИзXML(КлючИЗначение.Значение));
	КонецЦикла;
КонецПроцедуры

Процедура ЗагрузитьЗапрашиваемыеРеквизиты(СтрокаТаблицы, НеобработанныеУзлы, ЗапрашиваемыеРеквизитыDOM)
	ВложенныеУзлы = СериализацияБЗК.СписокDOM(ЗапрашиваемыеРеквизитыDOM, "item");
	
	НедостающиеСведения = Новый ТаблицаЗначений;
	НедостающиеСведения.Колонки.Добавить("Поле");
	НедостающиеСведения.Колонки.Добавить("Наименование");
	НедостающиеСведения.Колонки.Добавить("Описание");
	СтрокаТаблицы.НедостающиеСведения = НедостающиеСведения;
	
	Для Каждого ВложенныйУзелDOM Из ВложенныеУзлы Цикл
		РеквизитыУзлаDOM = СериализацияБЗК.УзлыЭлементаDOM(ВложенныйУзелDOM, "requestText, requestFields");
		Описание = СериализацияБЗК.СтрокаИзXML(РеквизитыУзлаDOM.requestText);
		Если РеквизитыУзлаDOM.requestFields = Неопределено И Не ЗначениеЗаполнено(Описание) Тогда
			Продолжить;
		КонецЕсли;
		ПоляDOM = СериализацияБЗК.СписокDOM(РеквизитыУзлаDOM.requestFields, "field");
		Если ПоляDOM.Количество() = 0 Тогда
			НедостающийРеквизит = НедостающиеСведения.Добавить();
			НедостающийРеквизит.Описание = Описание;
		КонецЕсли;
		Для Каждого ПолеDOM Из ПоляDOM Цикл
			РеквизитыВложенногоУзлаDOM = СериализацияБЗК.УзлыЭлементаDOM(ПолеDOM, "fieldCode, fieldName");
			НедостающийРеквизит = НедостающиеСведения.Добавить();
			НедостающийРеквизит.Поле = СериализацияБЗК.СтрокаИзXML(РеквизитыВложенногоУзлаDOM.fieldCode);
			НедостающийРеквизит.Наименование = СериализацияБЗК.СтрокаИзXML(РеквизитыВложенногоУзлаDOM.fieldName);
			НедостающийРеквизит.Описание = Описание;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Регламенты

Функция МаксимальнаяДатаПодтвержденияПолучения(Объект) Экспорт
	Возврат СЭДОФСС.СледующийРабочийДень(Объект.ВходящаяДата, РабочихДнейНаПодтверждениеПолучения());
КонецФункции

Функция МаксимальнаяДатаОбработки(Объект) Экспорт
	Если ЗначениеЗаполнено(Объект.ДатаОтправкиПодтверждения)
		И Объект.ДатаОтправкиПодтверждения < Объект.МаксимальнаяДатаПодтверждения Тогда
		ДатаОтсчета = Объект.ДатаОтправкиПодтверждения;
	Иначе
		ДатаОтсчета = Объект.МаксимальнаяДатаПодтверждения;
	КонецЕсли;
	Возврат СЭДОФСС.СледующийРабочийДень(ДатаОтсчета, РабочихДнейНаОтвет());
КонецФункции

// См. п.7 Положения № 1 утвержденного Постановлением Правительства РФ от 21.04.2011 N 294.
Функция РабочихДнейНаПодтверждениеПолучения()
	Возврат 1;
КонецФункции

// См. п.7 Положения № 1 утвержденного Постановлением Правительства РФ от 21.04.2011 N 294.
Функция РабочихДнейНаОтвет() Экспорт
	Возврат 5;
КонецФункции

#КонецОбласти

#Область ТекущиеДела

Функция ТребованияПоОтправке()
	НачалоТекущегоДня = НачалоДня(ТекущаяДатаСеанса());
	НачалоРабочегоДня = НачалоДня(СЭДОФСС.БлижайшийРабочийДень(НачалоТекущегоДня));
	// Бумагу отправляют через 2-3 дня, а на подтверждение получения дается 1 день.
	НачалоТекущегоДняМинусДваДня = НачалоТекущегоДня - 86400 * 2;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Извещение.Ссылка КАК Ссылка,
	|	НЕ Извещение.Загружен КАК ТребуетсяЗагрузить,
	|	Извещение.Загружен КАК ТребуетсяОтветить,
	|	ВЫБОР
	|		КОГДА Извещение.Загружен
	|				И Извещение.МаксимальнаяДатаОбработки <= &НачалоРабочегоДня
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТребуетсяОтветитьСегодня,
	|	ВЫБОР
	|		КОГДА Извещение.Загружен
	|				И НЕ Извещение.ПодтверждениеДоставлено
	|				И Извещение.ДатаОтправкиПодтверждения = &ПустаяДата
	|				И Извещение.МаксимальнаяДатаПодтверждения >= &НачалоТекущегоДняМинусДваДня
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТребуетсяПодтвердить
	|ИЗ
	|	Документ.ЗапросНедостающихСведенийПособияСФР КАК Извещение
	|ГДЕ
	|	НЕ Извещение.Обработан
	|	И НЕ Извещение.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("НачалоРабочегоДня", НачалоРабочегоДня);
	Запрос.УстановитьПараметр("НачалоТекущегоДняМинусДваДня", НачалоТекущегоДняМинусДваДня);
	Запрос.УстановитьПараметр("ПустаяДата", '00010101');
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

#КонецОбласти

#Область Заполнение

Процедура ПриЗаписиПервичногоДокумента(ПервичныйДокументОбъект, Отказ) Экспорт
	// Обновление вторичных данных в привилегированном режиме.
	УстановитьПривилегированныйРежим(Истина);
	
	ИменаСвойств = "Ссылка, ИсправленныйДокумент, НомерЛисткаНетрудоспособности,
	|НомерЗаменяемогоЛН, ДополнительныеСвойства";
	РеквизитыДокумента = ОбщегоНазначенияБЗК.ЗначенияСвойств(ПервичныйДокументОбъект, ИменаСвойств);
	РеквизитыДоЗаписи = ОбщегоНазначенияБЗК.ЗначенияСвойств(
		ОбщегоНазначенияБЗК.ЗначениеСвойства(РеквизитыДокумента.ДополнительныеСвойства, "ЗначенияРеквизитовДоЗаписи"),
		ИменаСвойств);
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый Массив;
	
	Ссылки = Новый Массив;
	Если Не ПервичныйДокументОбъект.ЭтоНовый() Тогда
		КоллекцииБЗК.ДобавитьНеПустоеЗначениеВМассив(Ссылки, РеквизитыДокумента.Ссылка);
	КонецЕсли;
	КоллекцииБЗК.ДобавитьНеПустоеЗначениеВМассив(Ссылки, РеквизитыДокумента.ИсправленныйДокумент);
	Если Ссылки.Количество() > 0 Тогда
		ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	Извещение.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗапросНедостающихСведенийПособияСФР КАК Извещение
		|ГДЕ
		|	Извещение.ВходящийПервичныйДокумент В(&Ссылки)
		|	И НЕ Извещение.Обработан");
		Запрос.УстановитьПараметр("Ссылки", Ссылки);
	КонецЕсли;
	
	НомераЛН = Новый Массив;
	КоллекцииБЗК.ДобавитьНеПустоеЗначениеВМассив(НомераЛН, РеквизитыДокумента.НомерЛисткаНетрудоспособности);
	КоллекцииБЗК.ДобавитьНеПустоеЗначениеВМассив(НомераЛН, РеквизитыДокумента.НомерЗаменяемогоЛН);
	КоллекцииБЗК.ДобавитьНеПустоеЗначениеВМассив(НомераЛН, РеквизитыДоЗаписи.НомерЛисткаНетрудоспособности);
	КоллекцииБЗК.ДобавитьНеПустоеЗначениеВМассив(НомераЛН, РеквизитыДоЗаписи.НомерЗаменяемогоЛН);
	Если НомераЛН.Количество() > 0 Тогда
		ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	Извещение.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗапросНедостающихСведенийПособияСФР КАК Извещение
		|ГДЕ
		|	Извещение.НомерЛН В(&НомераЛН)
		|	И НЕ Извещение.Обработан");
		Запрос.УстановитьПараметр("НомераЛН", НомераЛН);
	КонецЕсли;
	
	Если ТекстыЗапроса.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	// АПК:96-выкл Ключевое слово ОБЪЕДИНИТЬ использовано по назначению.
	Объединитель = Символы.ПС + Символы.ПС + "ОБЪЕДИНИТЬ" + Символы.ПС + Символы.ПС;
	// АПК:96-вкл
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, Объединитель);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбновитьВторичныеДанныеИЗаписатьДокумент(Выборка.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписиЗаявленияНаВыплатуПособия(ЗаявлениеОбъект, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый Массив;
	
	Если ЗначениеЗаполнено(ЗаявлениеОбъект.ИзвещениеИзФССДата)
		И ЗначениеЗаполнено(ЗаявлениеОбъект.ИзвещениеИзФССНомер) Тогда
		ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	Извещение.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗапросНедостающихСведенийПособияСФР КАК Извещение
		|ГДЕ
		|	Извещение.ВходящийНомер = &ИзвещениеИзФССНомер
		|	И Извещение.ВходящаяДата = &ИзвещениеИзФССДата
		|	И НЕ Извещение.Обработан");
		Запрос.УстановитьПараметр("ИзвещениеИзФССНомер", ЗаявлениеОбъект.ИзвещениеИзФССНомер);
		Запрос.УстановитьПараметр("ИзвещениеИзФССДата", ЗаявлениеОбъект.ИзвещениеИзФССДата);
	КонецЕсли;
	
	МассивСсылок = Новый Массив;
	КоллекцииБЗК.ДобавитьНеПустоеЗначениеВМассив(МассивСсылок, ЗаявлениеОбъект.Ссылка);
	КоллекцииБЗК.ДобавитьНеПустоеЗначениеВМассив(МассивСсылок, ЗаявлениеОбъект.ИсправленныйДокумент);
	Если МассивСсылок.Количество() > 0 Тогда
		ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	Извещение.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗапросНедостающихСведенийПособияСФР КАК Извещение
		|ГДЕ
		|	Извещение.ВходящееЗаявление В(&МассивСсылок)
		|	И НЕ Извещение.Обработан");
		Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗаявлениеОбъект.НомерЛисткаНетрудоспособности)
		И ЗначениеЗаполнено(ЗаявлениеОбъект.ФизическоеЛицо) Тогда
		ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	Извещение.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗапросНедостающихСведенийПособияСФР КАК Извещение
		|ГДЕ
		|	Извещение.ФизическоеЛицо = &ФизическоеЛицо
		|	И Извещение.НомерЛН = &НомерЛисткаНетрудоспособности
		|	И НЕ Извещение.Обработан");
		Запрос.УстановитьПараметр("ФизическоеЛицо", ЗаявлениеОбъект.ФизическоеЛицо);
		Запрос.УстановитьПараметр("НомерЛисткаНетрудоспособности", ЗаявлениеОбъект.НомерЛисткаНетрудоспособности);
	КонецЕсли;
	
	Если ТекстыЗапроса.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	// АПК:96-выкл Ключевое слово ОБЪЕДИНИТЬ использовано по назначению.
	Объединитель = Символы.ПС + Символы.ПС + "ОБЪЕДИНИТЬ" + Символы.ПС + Символы.ПС;
	// АПК:96-вкл
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, Объединитель);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбновитьВторичныеДанныеИЗаписатьДокумент(Выборка.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьВторичныеДанныеИЗаписатьДокумент(Ссылка, ВключитьБизнесЛогику = "БЗК") Экспорт
	Попытка
		ДокументОбъект = Ссылка.ПолучитьОбъект();
		ДокументОбъект.ОбновитьВторичныеДанные();
		СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, ВключитьБизнесЛогику, РежимЗаписиДокумента.Запись);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Текст = СтрШаблон(НСтр("ru = 'Не удалось обновить данные документа ""%1"":';
								|en = 'Cannot update the ""%1"" document data:'"), Ссылка);
		Уточнение = ОбщегоНазначенияКлиентСервер.УточнениеИсключения(ИнформацияОбОшибке, Текст);
		ВызватьИсключение(Уточнение.Текст, Уточнение.Категория, , , ИнформацияОбОшибке);
	КонецПопытки;
КонецПроцедуры

Функция СписокВыбораЗаявлений(ФизическоеЛицо, ПервичныйДокумент, ИсключаемоеЗаявление, Количество) Экспорт
	Результат = Новый СписокЗначений;
	Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		Возврат Результат;
	КонецЕсли;
	// Если первичный документ заполнен - получить последнее заявление по документу.
	Отбор = Новый Структура;
	Если ЗначениеЗаполнено(ПервичныйДокумент) Тогда
		Отбор.Вставить("ДокументОснование", ПервичныйДокумент);
	Иначе
		Отбор.Вставить("ФизическоеЛицо", ФизическоеЛицо);
	КонецЕсли;
	Поля = "Ссылка, Дата";
	Сортировки = "Дата Убыв, Ссылка Убыв";
	Запрос = Документы.ЗаявлениеСотрудникаНаВыплатуПособия.ЗапросПоДокументу(Отбор, Поля, Количество, Сортировки);
	Таблица = Запрос.Выполнить().Выгрузить();
	Результат.ЗагрузитьЗначения(Таблица.ВыгрузитьКолонку("Ссылка"));
	Если ЗначениеЗаполнено(ИсключаемоеЗаявление) Тогда
		ЭлементСписка = Результат.НайтиПоЗначению(ИсключаемоеЗаявление);
		Если ЭлементСписка <> Неопределено Тогда
			Результат.Удалить(ЭлементСписка);
		КонецЕсли;
	КонецЕсли;
	Возврат Результат;
КонецФункции

Функция РеестрыПоЗаявлению(Заявление, ПервичныйДокумент, ИсключаемоеЗаявление, Количество) Экспорт
	Если Не ЗначениеЗаполнено(Заявление) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	Реестр.Ссылка КАК Ссылка,
	|	Реестр.СтатусДокумента КАК СтатусДокумента,
	|	Реестр.Номер КАК Номер,
	|	Реестр.Дата КАК Дата,
	|	ВЫБОР
	|		КОГДА Реестр.СтатусДокумента = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявленийИРеестровНаВыплатуПособий.ПринятФСС)
	|			ТОГДА 6
	|		КОГДА Реестр.СтатусДокумента = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявленийИРеестровНаВыплатуПособий.ПереданВФСС)
	|			ТОГДА 5
	|		КОГДА Реестр.СтатусДокумента = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявленийИРеестровНаВыплатуПособий.Подготовлен)
	|			ТОГДА 4
	|		КОГДА Реестр.СтатусДокумента = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявленийИРеестровНаВыплатуПособий.НеПринятФСС)
	|			ТОГДА 3
	|		КОГДА Реестр.СтатусДокумента = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявленийИРеестровНаВыплатуПособий.Аннулирован)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ВесСостояния,
	|	Реестр.ПометкаУдаления КАК ПометкаУдаления,
	|	Реестр.Проведен КАК Проведен
	|ИЗ
	|	Документ.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий.СведенияНеобходимыеДляНазначенияПособий КАК ТаблицаСведений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий КАК Реестр
	|		ПО ТаблицаСведений.Ссылка = Реестр.Ссылка
	|ГДЕ
	|	&Условия
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПометкаУдаления,
	|	Проведен УБЫВ,
	|	ВесСостояния УБЫВ,
	|	Дата УБЫВ";
	
	Если Количество = 0 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1", "");
	ИначеЕсли Количество > 1 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1", "ПЕРВЫЕ " + Формат(Количество, "ЧГ="));
	КонецЕсли;
	
	Условия = Новый Массив;
	Условия.Добавить("ТаблицаСведений.Заявление = &Заявление");
	Запрос.УстановитьПараметр("Заявление", Заявление);
	Если ЗначениеЗаполнено(ПервичныйДокумент) Тогда
		Условия.Добавить("ТаблицаСведений.ПервичныйДокумент = &ПервичныйДокумент");
		Запрос.УстановитьПараметр("ПервичныйДокумент", ПервичныйДокумент);
	КонецЕсли;
	Если ЗначениеЗаполнено(ИсключаемоеЗаявление) Тогда
		Условия.Добавить("ТаблицаСведений.Заявление <> &ИсключаемоеЗаявление");
		Запрос.УстановитьПараметр("ИсключаемоеЗаявление", ИсключаемоеЗаявление);
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условия", СтрСоединить(Условия, " И "));
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

// Поиск документа по идентификатору сообщения.
Функция НайтиПоИдентификаторуСообщения(ИдентификаторСообщения, Количество = Неопределено) Экспорт
	Настройки = ЗапросыБЗК.НастройкиЗапросаКТаблице();
	Если Количество <> Неопределено Тогда
		Настройки.Количество = Количество;
	КонецЕсли;
	ЗапросыБЗК.ДобавитьОтбор(Настройки.Отбор, "ИдентификаторСообщения", "=", ИдентификаторСообщения);
	Поля = "Ссылка, Дата, ПометкаУдаления, Организация, Страхователь, Загружен, Обработан, ТребуетсяПодтверждение,
	|НомерСтрокиСообщения";
	Запрос = ЗапросыБЗК.ЗапросКТаблице(Метаданные(), Поля, Настройки);
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция НайтиПоВходящемуНомеруДате(ВходящийНомер, ВходящаяДата) Экспорт
	Если ТипЗнч(ВходящийНомер) = Тип("Число") Тогда
		ВходящийНомерЧислом = ВходящийНомер;
	Иначе
		ВходящийНомерЧислом = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ВходящийНомер);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Извещение.Ссылка КАК Ссылка,
	|	Извещение.ПометкаУдаления КАК ПометкаУдаления,
	|	Извещение.Представление КАК Представление
	|ИЗ
	|	Документ.ИзвещениеФСС КАК Извещение
	|ГДЕ
	|	Извещение.ВходящийНомер = &ВходящийНомер
	|	И Извещение.ВходящаяДата = &ВходящаяДата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Извещение.Ссылка,
	|	Извещение.ПометкаУдаления,
	|	Извещение.Представление
	|ИЗ
	|	Документ.ЗапросНедостающихСведенийПособияСФР КАК Извещение
	|ГДЕ
	|	Извещение.ВходящийНомер = &ВходящийНомер
	|	И Извещение.ВходящаяДата = &ВходящаяДата
	|
	|УПОРЯДОЧИТЬ ПО
	|	Извещение.ПометкаУдаления,
	|	Извещение.Ссылка УБЫВ";
	Запрос.УстановитьПараметр("ВходящийНомер", ВходящийНомерЧислом);
	Запрос.УстановитьПараметр("ВходящаяДата",  ВходящаяДата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

#КонецОбласти

#Область ФизическиеЛица

Процедура ПриИзмененииСНИЛСФизическогоЛица(ФизическоеЛицо, СтарыйСНИЛС, НовыйСНИЛС) Экспорт
	// В сообщениях 1010 Фонд явно указывает СНИЛС.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Шапка.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗапросНедостающихСведенийПособияСФР КАК Шапка
	|ГДЕ
	|	Шапка.СотрудникСНИЛС = &НовыйСНИЛС
	|	И Шапка.ФизическоеЛицо <> &ФизическоеЛицо
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Шапка.Ссылка
	|ИЗ
	|	Документ.ЗапросНедостающихСведенийПособияСФР КАК Шапка
	|ГДЕ
	|	Шапка.ФизическоеЛицо = &ФизическоеЛицо
	|	И Шапка.СотрудникСНИЛС <> &НовыйСНИЛС";
	Если ЗначениеЗаполнено(НовыйСНИЛС) Тогда
		Запрос.УстановитьПараметр("НовыйСНИЛС", НовыйСНИЛС);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Шапка.СотрудникСНИЛС = &НовыйСНИЛС", "ЛОЖЬ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Шапка.СотрудникСНИЛС <> &НовыйСНИЛС", "ИСТИНА");
	КонецЕсли;
	Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаВыборки Из Таблица Цикл
		Если Не ЗначениеЗаполнено(СтрокаВыборки.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		ДокументОбъект = СтрокаВыборки.Ссылка.ПолучитьОбъект();
		Если ДокументОбъект.СотрудникСНИЛС = НовыйСНИЛС И ЗначениеЗаполнено(НовыйСНИЛС) Тогда
			ДокументОбъект.ФизическоеЛицо = ФизическоеЛицо;
		Иначе
			ДокументОбъект.ФизическоеЛицо = Неопределено;
		КонецЕсли;
		ИменаРеквизитов = "ФизическоеЛицо, Организация, Сотрудник";
		ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюРеквизитовШапки(ДокументОбъект, ИменаРеквизитов);
		ДокументОбъект.ОбновитьВторичныеДанные();
		СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, "БЗК", РежимЗаписиДокумента.Запись);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

Функция ИсходящиеДокументы(Документ) Экспорт
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.ЗапросНедостающихСведенийПособияСФР")
		И Документ.ДополнительныеСвойства.Свойство("ИсходящиеДокументы") Тогда
		Возврат Документ.ДополнительныеСвойства.ИсходящиеДокументы;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Документ.ФизическоеЛицо)
		Или Не ЗначениеЗаполнено(Документ.ГоловнаяОрганизация)
		Или Не ЗначениеЗаполнено(Документ.ВходящийНомер)
		Или Не ЗначениеЗаполнено(Документ.ВходящаяДата) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИсходящийПервичныйДокумент = Неопределено;
	ИсходящееЗаявление = Неопределено;
	ИсходящийРеестр = Неопределено;
	ИсходящийВходящийЗапрос = Неопределено;
	ИсходящийОтветНаЗапрос = Неопределено;
	Обработан = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтветНаЗапрос.Ссылка КАК ОтветНаЗапрос,
	|	ОтветНаЗапрос.ВходящийЗапрос КАК ВходящийЗапрос,
	|	ОтветНаЗапрос.ПервичныйДокумент КАК ПервичныйДокумент,
	|	ОтветНаЗапрос.ДатаОтправки КАК ДатаОтправки
	|ИЗ
	|	Документ.ОтветНаЗапросФССДляРасчетаПособия КАК ОтветНаЗапрос
	|ГДЕ
	|	ОтветНаЗапрос.ИзвещениеФССНомер = &ИзвещениеФССНомер
	|	И ОтветНаЗапрос.ИзвещениеФССДата = &ИзвещениеФССДата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВходящийЗапрос.ОтветНаЗапрос,
	|	ВходящийЗапрос.Ссылка,
	|	ВходящийЗапрос.ПервичныйДокумент,
	|	ДАТАВРЕМЯ(1, 1, 1)
	|ИЗ
	|	Документ.ВходящийЗапросФССДляРасчетаПособия КАК ВходящийЗапрос
	|ГДЕ
	|	ВходящийЗапрос.ИзвещениеФССНомер = &ИзвещениеФССНомер
	|	И ВходящийЗапрос.ИзвещениеФССДата = &ИзвещениеФССДата
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОтправки УБЫВ";
	Запрос.УстановитьПараметр("ИзвещениеФССНомер", Документ.ВходящийНомер);
	Запрос.УстановитьПараметр("ИзвещениеФССДата", Документ.ВходящаяДата);
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Если Таблица.Количество() > 0 Тогда
		СтрокаТаблицы = Таблица[0];
		ИсходящийВходящийЗапрос = СтрокаТаблицы.ВходящийЗапрос;
		ИсходящийОтветНаЗапрос = СтрокаТаблицы.ОтветНаЗапрос;
		Если ЗначениеЗаполнено(СтрокаТаблицы.ДатаОтправки) Тогда
			Обработан = Истина;
		КонецЕсли;
	КонецЕсли;
	
	// Оптимизация: Ищем реестры ПВСО только если известно не было 101 сообщения.
	Если Не Обработан
		И Не ЗначениеЗаполнено(Документ.ИдентификаторСообщения101)
		И Не ЗначениеЗаполнено(ИсходящийВходящийЗапрос)
		И Не ЗначениеЗаполнено(ИсходящийОтветНаЗапрос) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.Ссылка КАК Реестр,
		|	Таблица.Заявление КАК Заявление,
		|	Таблица.ПервичныйДокумент КАК ПервичныйДокумент,
		|	Шапка.СтатусДокумента = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявленийИРеестровНаВыплатуПособий.ПереданВФСС)
		|		ИЛИ Шапка.СтатусДокумента = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявленийИРеестровНаВыплатуПособий.ПринятФСС) КАК Отправлен
		|ИЗ
		|	Документ.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий.СведенияНеобходимыеДляНазначенияПособий КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий КАК Шапка
		|		ПО Таблица.Ссылка = Шапка.Ссылка
		|ГДЕ
		|	Таблица.ИзвещениеИзФССНомер = &ИзвещениеИзФССНомер
		|	И Таблица.ИзвещениеИзФССДата = &ИзвещениеИзФССДата
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	NULL,
		|	Заявление.Ссылка,
		|	Заявление.ДокументОснование,
		|	ЛОЖЬ
		|ИЗ
		|	Документ.ЗаявлениеСотрудникаНаВыплатуПособия КАК Заявление
		|ГДЕ
		|	Заявление.ИзвещениеИзФССНомер = &ИзвещениеИзФССНомер
		|	И Заявление.ИзвещениеИзФССДата = &ИзвещениеИзФССДата";
		Запрос.УстановитьПараметр("ИзвещениеИзФССНомер", Документ.ВходящийНомер);
		Запрос.УстановитьПараметр("ИзвещениеИзФССДата", Документ.ВходящаяДата);
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
			Запрос.Текст = Запрос.Текст + Символы.ПС + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС + Символы.ПС +
			"ВЫБРАТЬ
			|	Таблица.Ссылка,
			|	NULL,
			|	NULL,
			|	Шапка.СостояниеРеестра = ЗНАЧЕНИЕ(Перечисление.СостоянияРеестровФСС.ПринятФСС)
			|		ИЛИ Шапка.СостояниеРеестра = ЗНАЧЕНИЕ(Перечисление.СостоянияРеестровФСС.ЧастичноПринятФСС)
			|ИЗ
			|	Документ.РеестрСтимулирующихВыплатМедицинскимИСоциальнымРаботникам.Сотрудники КАК Таблица
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеестрСтимулирующихВыплатМедицинскимИСоциальнымРаботникам КАК Шапка
			|		ПО Таблица.Ссылка = Шапка.Ссылка
			|ГДЕ
			|	Таблица.ИзвещениеИзФССНомер = &ИзвещениеИзФССНомер
			|	И Таблица.ИзвещениеИзФССДата = &ИзвещениеИзФССДата";
		КонецЕсли;
		
		Таблица = Запрос.Выполнить().Выгрузить();
		Для Каждого СтрокаТаблицы Из Таблица Цикл
			Если СтрокаТаблицы.Отправлен Тогда
				Обработан = Истина;
				ИсходящийРеестр = СтрокаТаблицы.Реестр;
				ИсходящееЗаявление = СтрокаТаблицы.Заявление;
				ИсходящийПервичныйДокумент = СтрокаТаблицы.ПервичныйДокумент;
				Прервать;
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаТаблицы.Реестр) И Не ЗначениеЗаполнено(ИсходящийРеестр) Тогда
				ИсходящийРеестр = СтрокаТаблицы.Реестр;
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаТаблицы.Заявление) И Не ЗначениеЗаполнено(ИсходящееЗаявление) Тогда
				ИсходящееЗаявление = СтрокаТаблицы.Заявление;
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаТаблицы.ПервичныйДокумент) И Не ЗначениеЗаполнено(ИсходящийПервичныйДокумент) Тогда
				ИсходящийПервичныйДокумент = СтрокаТаблицы.ПервичныйДокумент;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ИсходящийПервичныйДокумент", ИсходящийПервичныйДокумент);
	Результат.Вставить("ИсходящееЗаявление", ИсходящееЗаявление);
	Результат.Вставить("ИсходящийРеестр", ИсходящийРеестр);
	Результат.Вставить("ИсходящийОтветНаЗапрос", ИсходящийОтветНаЗапрос);
	Результат.Вставить("Обработан", Обработан);
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.ЗапросНедостающихСведенийПособияСФР") Тогда
		Документ.ДополнительныеСвойства.Вставить("ИсходящиеДокументы", Результат);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция СведенияОбЭЛН(Документ) Экспорт
	СведенияОбЭЛН = Неопределено;
	Если Документ.ДополнительныеСвойства.Свойство("СведенияОбЭЛН", СведенияОбЭЛН) Тогда
		Возврат СведенияОбЭЛН;
	КонецЕсли;
	Если ЗначениеЗаполнено(Документ.НомерЛН) Тогда
		СведенияОбЭЛН = РегистрыСведений.СведенияОбЭЛН.СведенияОбЭЛН(Документ.НомерЛН, Документ.ГоловнаяОрганизация);
	КонецЕсли;
	Документ.ДополнительныеСвойства.Вставить("СведенияОбЭЛН", СведенияОбЭЛН);
	Возврат СведенияОбЭЛН;
КонецФункции

Функция КраткоеПредставлениеНедостающихСведений(Документ) Экспорт
	ПредставленияСтрок = Новый Массив;
	Для Каждого СтрокаТаблицы Из Документ.НедостающиеСведения Цикл
		Если ЗначениеЗаполнено(СтрокаТаблицы.Наименование) Тогда
			ПредставленияСтрок.Добавить(СтрокаТаблицы.Наименование);
		ИначеЕсли ЗначениеЗаполнено(СтрокаТаблицы.Поле) Тогда
			ПредставленияСтрок.Добавить(СтрокаТаблицы.Поле);
		ИначеЕсли ЗначениеЗаполнено(СтрокаТаблицы.Описание) Тогда
			ПредставленияСтрок.Добавить(СтрокаТаблицы.Описание);
		КонецЕсли;
	КонецЦикла;
	Возврат СтрСоединить(ПредставленияСтрок, ", ");
КонецФункции

#КонецОбласти

#КонецЕсли