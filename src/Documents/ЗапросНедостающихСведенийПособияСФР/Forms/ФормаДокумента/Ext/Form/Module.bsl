///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ТребуетсяЗаполнитьИсходящиеДокументы = Ложь;
	ИсходящиеДокументы = Документы.ЗапросНедостающихСведенийПособияСФР.ИсходящиеДокументы(ТекущийОбъект);
	
	ПриПолученииДанныхНаСервере(ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = '%1 загружаются в автоматическом режиме';
				|en = '%1 are imported automatically'"),
			РеквизитФормыВЗначение("Объект").Метаданные().ПредставлениеСписка);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ПравоИзменения
		И ПравоПолученияЭД
		И Объект.Загружен
		И Объект.ТребуетсяПодтверждение
		И Не Объект.Обработан
		И Не ЗначениеЗаполнено(Объект.ДатаОтправкиПодтверждения) Тогда
		КонтекстПодтвержденияПолучения = СЭДОФСС.КонтекстПодтвержденияПолучения(Объект.Ссылка);
	Иначе
		КонтекстПодтвержденияПолучения = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если КонтекстПодтвержденияПолучения <> Неопределено Тогда
		СЭДОФССКлиент.ПредложитьПодтвердитьПолучение(КонтекстПодтвержденияПолучения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ИмяСобытия = "Запись_БольничныйЛист"
		Или ИмяСобытия = "Запись_ВходящийЗапросФССДляРасчетаПособия"
		Или ИмяСобытия = "Запись_ОтветНаЗапросФССДляРасчетаПособия"
		Или ИмяСобытия = "Запись_ЗаявлениеСотрудникаНаВыплатуПособия"
		Или ИмяСобытия = "Запись_РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий"
		Или ИмяСобытия = "Запись_РеестрСтимулирующихВыплатМедицинскимИСоциальнымРаботникам"
		Или ИмяСобытия = СЭДОФССКлиент.ИмяСобытияПослеПолученияСообщенийОтФСС()
		Или ИмяСобытия = СЭДОФССКлиент.ИмяСобытияПослеОтправкиПодтвержденияПолучения() Тогда
		ТребуетсяЗаполнитьИсходящиеДокументы = Истина;
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_БольничныйЛист"
		Или ИмяСобытия = "Запись_ОтпускПоУходуЗаРебенком"
		Или ИмяСобытия = "Запись_Отпуск"
		Или ИмяСобытия = СЭДОФССКлиент.ИмяСобытияПослеПолученияСообщенийОтФСС()
		Или ИмяСобытия = СЭДОФССКлиент.ИмяСобытияПослеОтправкиПодтвержденияПолучения() Тогда
		Если Модифицированность Тогда
			ОбновитьВторичныеДанныеОбъектаИЭлементыФормы();
		Иначе
			Прочитать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	ФиксацияЗаполнитьИдентификаторыФиксТЧ(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.СохранитьРеквизитыФормыФикс(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ФиксацияЗаполнитьРеквизитыФормыФикс(ТекущийОбъект);
	ФиксацияВторичныхДанныхВДокументахФормы.УстановитьОбъектЗафиксирован(ЭтотОбъект);
	
	ОбновитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ЗапросНедостающихСведенийПособияСФР", ПараметрыЗаписи, Объект.Ссылка);
	СЭДОФССКлиент.ОповеститьОНеобходимостиОбновитьТекущиеДела();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НомерСтрокиСообщенияНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура("Отбор", Новый Структура("ИдентификаторСообщения", Объект.ИдентификаторСообщения));
	ОткрытьФорму("Документ.ЗапросНедостающихСведенийПособияСФР.ФормаСписка", ПараметрыФормы, , Объект.ИдентификаторСообщения);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОрганизацияПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ФизическоеЛицоПриИзменении(Элемент)
	ФизическоеЛицоПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	СотрудникПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВходящийЗапросПриИзменении(Элемент)
	ВходящийЗапросПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВходящийОтветНаЗапросПриИзменении(Элемент)
	ВходящийОтветНаЗапросПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВходящийРеестрПриИзменении(Элемент)
	ВходящийРеестрПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВходящееЗаявлениеПриИзменении(Элемент)
	ВходящееЗаявлениеПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВходящийПервичныйДокументПриИзменении(Элемент)
	ВходящийПервичныйДокументПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура НадписьТребуетсяПодтверждениеОбработкаНавигационнойСсылки(Элемент, Адрес, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(Объект.Ссылка);
	СЭДОФССКлиент.ОтправитьПодтверждениеПолучения(МассивСсылок);
КонецПроцедуры

&НаКлиенте
Процедура НеобработанныеУзлыНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПодробноЗаголовок = СтрШаблон(НСтр("ru = 'Необработанные узлы сообщения %1';
										|en = 'Unprocessed message nodes: %1'"), Объект.ИдентификаторСообщения);
	ИнформированиеПользователяКлиент.ПоказатьПодробности(Объект.НеобработанныеУзлы, ПодробноЗаголовок);
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыНедостающиеСведения

&НаКлиенте
Процедура НедостающиеСведенияПриИзменении(Элемент)
	ОбновитьВторичныеДанныеНаСервере();
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПредыдущееИзвещение(Команда)
	Если Объект.НомерСтрокиСообщения = 1 Тогда
		Возврат;
	КонецЕсли;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИдентификаторСообщения", Объект.ИдентификаторСообщения);
	ПараметрыФормы.Вставить("НомерСтрокиСообщения", Объект.НомерСтрокиСообщения - 1);
	ОткрытьФорму("Документ.ЗапросНедостающихСведенийПособияСФР.ФормаОбъекта", ПараметрыФормы, , Ложь);
КонецПроцедуры

&НаКлиенте
Процедура СледующееИзвещение(Команда)
	Если Объект.НомерСтрокиСообщения = Объект.КоличествоСтрокСообщения Тогда
		Возврат;
	КонецЕсли;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИдентификаторСообщения", Объект.ИдентификаторСообщения);
	ПараметрыФормы.Вставить("НомерСтрокиСообщения", Объект.НомерСтрокиСообщения + 1);
	ОткрытьФорму("Документ.ЗапросНедостающихСведенийПособияСФР.ФормаОбъекта", ПараметрыФормы, , Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Если Записать() Тогда
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВключитьВозможностьРедактирования(Команда)
	ВключитьВозможностьРедактированияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВторичныеДанные(Команда)
	ОбновитьВторичныеДанныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьИсправления(Команда)
	ОтменитьВсеИсправленияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВопросВПоддержку(Команда)
	СЭДОФССКлиент.ОткрытьВопросВПоддержку(ПодготовитьВопросВПоддержку());
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствамиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеСвойствамиКлиент");
		МодульУправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область Форма

&НаСервере
Процедура ПриПолученииДанныхНаСервере(ТекущийОбъект)
	Если Элементы.КодВидаПособия.СписокВыбора.Количество() = 0 Тогда
		СЭДОФСС.СписокВыбораКодовВидаПособия(Элементы.КодВидаПособия.СписокВыбора, Объект.КодВидаПособия);
	КонецЕсли;
	
	ФиксацияВторичныхДанныхВДокументахФормы.ИнициализироватьМеханизмФиксацииРеквизитов(ЭтотОбъект, ТекущийОбъект);
	ФиксацияВторичныхДанныхВДокументахФормы.ПодключитьОбработчикиФиксацииИзмененийРеквизитов(ЭтотОбъект, ФиксацияБыстрыйПоискРеквизитов());
	ОбновитьВторичныеДанныеОбъектаИЭлементыФормы();
	
	Если Не ЗначениеЗаполнено(Объект.Ответственный) Тогда
		ЗначенияПоУмолчанию = Новый Структура("Ответственный");
		ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ЗначенияПоУмолчанию);
		Объект.Ответственный = ЗначенияПоУмолчанию.Ответственный;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьВторичныеДанныеОбъектаИЭлементыФормы()
	ОбновитьВторичныеДанныеНаСервере();
	ОбновитьЭлементыФормы();
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыФормы()
	
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбновитьФорму(ЭтотОбъект);
	ПравоИзменения   = (Элементы.Найти("ФормаПровести") <> Неопределено);
	ПравоПолученияЭД = СЭДОФСС.ЕстьПравоПолучения();
	РежимРедактирования = ?(ПравоИзменения, Элементы.ВключитьВозможностьРедактирования.Пометка, Ложь);
	
	СЭДОФСС.ОбновитьСвертываемыеГруппы(ЭтотОбъект);
	
	СЭДОФСС.НастроитьПолеВводаПервичногоДокумента(
		Объект.ВидПособия,
		Объект.Сотрудник,
		Элементы.ВходящийПервичныйДокумент,
		Объект.ВходящийПервичныйДокумент);
	
	ОбновитьКнопки();
	ОбновитьПодтверждениеПолучения();
	
	Элементы.ВходящийНомер.Видимость = Объект.Загружен;
	Элементы.ВходящаяДата.Видимость = Объект.Загружен;
	Элементы.Страхователь.Видимость = ЕстьФилиалыНеСтрахователи Или (Объект.Организация <> Объект.Страхователь);
	Элементы.КодНПА.Подсказка = ПредставлениеНПА(Объект.КодНПА);
	Элементы.НеобработанныеУзлыГруппа.Видимость = ЗначениеЗаполнено(Объект.НеобработанныеУзлы);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКнопки()
	
	ПравоИзмененияИПолученияЭД = ПравоИзменения И ПравоПолученияЭД;
	
	Кнопка = Элементы.Найти("ФормаПровестиИЗакрыть");
	Если Кнопка <> Неопределено Тогда
		Кнопка.КнопкаПоУмолчанию = Объект.Загружен И Модифицированность;
	КонецЕсли;
	
	Кнопка = Элементы.Найти("ФормаПодтвердитьПолучение");
	Если Кнопка <> Неопределено Тогда
		Кнопка.Видимость = ПравоИзмененияИПолученияЭД;
		Кнопка.ТолькоВоВсехДействиях = Не Объект.Загружен Или Не Объект.ТребуетсяПодтверждение
			Или ЗначениеЗаполнено(Объект.ДатаОтправкиПодтверждения);
	КонецЕсли;
	
	Если Не ПравоИзмененияИПолученияЭД Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПолучитьИзСЭДО", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаПовторноПолучитьИзСЭДО", "Видимость", Ложь);
	КонецЕсли;
	
	Если Объект.Загружен И Объект.КоличествоСтрокСообщения > 1 Тогда
		Элементы.НомерСтрокиСообщенияГруппа.Видимость = Истина;
		Элементы.ПредыдущееИзвещение.Доступность = Объект.НомерСтрокиСообщения > 1;
		Элементы.СледующееИзвещение.Доступность = Объект.НомерСтрокиСообщения < Объект.КоличествоСтрокСообщения;
	Иначе
		Элементы.НомерСтрокиСообщенияГруппа.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПодтверждениеПолучения()
	Если Не Объект.Загружен Тогда
		ТекущаяСтраница = Элементы.СтраницаТребуетсяПолучить;
	ИначеЕсли Не Объект.ТребуетсяПодтверждение Тогда
		ТекущаяСтраница = Элементы.СтраницаПодтверждениеНеТребуется;
	ИначеЕсли ЗначениеЗаполнено(Объект.ДатаОтправкиПодтверждения) Тогда
		Если Объект.ПодтверждениеДоставлено Тогда
			ТекущаяСтраница = Элементы.СтраницаПодтверждениеДоставлено;
			Элементы.НадписьПодтверждениеОтправленоИПолучено.Заголовок = СтрШаблон(
				НСтр("ru = 'Подтверждение получения отправлено %1 и доставлено СФР.';
					|en = 'Receipt confirmation was sent on %1 and delivered to the Social Insurance Fund of Russia.'"),
				Формат(Объект.ДатаОтправкиПодтверждения, "ДЛФ=D"));
			НадписьОсталосьДней = Элементы.НадписьОсталосьДней2;
		Иначе
			ТекущаяСтраница = Элементы.СтраницаПодтверждениеОтправлено;
			Элементы.НадписьПодтверждениеОтправленоНоНеПолучено.Заголовок = СтрШаблон(
				НСтр("ru = 'Подтверждение получения отправлено %1.';
					|en = 'Receipt confirmation was sent on %1.'"),
				Формат(Объект.ДатаОтправкиПодтверждения, "ДЛФ=D"));
			НадписьОсталосьДней = Элементы.НадписьОсталосьДней1;
		КонецЕсли;
		Если Объект.Обработан Тогда
			НадписьОсталосьДней.Заголовок  = "";
			НадписьОсталосьДней.ЦветТекста = Новый Цвет;
		Иначе
			ОсталосьДней = ОбщегоНазначенияБЗК.КоличествоДнейДоДаты(Объект.МаксимальнаяДатаОбработки);
			Если ОсталосьДней <= 1 Тогда
				НадписьОсталосьДней.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
					НСтр("ru = 'Необходимо <b>сегодня</b> отправить недостающие сведения в СФР';
						|en = 'It is required to send missing information to Social Insurance Fund <b>today</b>'"));
				НадписьОсталосьДней.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст;
			Иначе
				НадписьОсталосьДней.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
					СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
						НСтр("ru = ';Остался <b>%1 день</b> для отправки недостающих сведений в СФР;;Осталось <b>%1 дня</b> для отправки недостающих сведений в СФР;Осталось %1 дней для отправки недостающих сведений в СФР;';
							|en = ';<b>%1 day</b> remaining to send the missing information to Social Insurance Fund;;<b>%1 days</b> remaining to send the missing information to Social Insurance Fund;%1 days remaining to send the missing information to Social Insurance Fund;'"),
						ОсталосьДней));
				НадписьОсталосьДней.ЦветТекста = Новый Цвет;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(Объект.МаксимальнаяДатаПодтверждения) И Не Объект.Обработан Тогда
		ТекущаяСтраница = Элементы.СтраницаТребуетсяПодтверждение;
		ОсталосьДней = ОбщегоНазначенияБЗК.КоличествоДнейДоДаты(Объект.МаксимальнаяДатаПодтверждения);
		Если ОсталосьДней <= 1 Тогда
			Когда = НСтр("ru = '<b>сегодня</b>';
						|en = '<b>today</b>'");
			ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст;
		Иначе
			Когда = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = ';в течение %1 дня;;в течение %1 дней;в течение %1 дней;';
					|en = ';within %1 day;;within %1 days;within %1 days;'"),
				ОсталосьДней);
			ЦветТекста = Новый Цвет;
		КонецЕсли;
		ЗаголовокНадписи = СтрШаблон(
			НСтр("ru = 'Необходимо %1 <a href=""1"">подтвердить получение</a> Извещения, после чего отправить реестр с недостающими сведениями в течении <b>%2 рабочих дней</b>.';
				|en = 'It is required to %1 <a href=""1"">confirm the receipt</a> of the Notification and send the register with the missing data within <b>%2 workdays</b>.'"),
			Когда,
			Документы.ЗапросНедостающихСведенийПособияСФР.РабочихДнейНаОтвет());
		Элементы.НадписьТребуетсяПодтверждение.Заголовок  = СтроковыеФункции.ФорматированнаяСтрока(ЗаголовокНадписи);
		Элементы.НадписьТребуетсяПодтверждение.ЦветТекста = ЦветТекста;
	Иначе
		ТекущаяСтраница = Элементы.СтраницаПодтверждениеНеТребуется;
	КонецЕсли;
	СЭДОФССКлиентСервер.УстановитьСтраницу(Элементы.СтраницыПодтвержденияПолучения, ТекущаяСтраница);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеНПА(КодНПА)
	Если КодНПА = "1" Тогда
		ПредставлениеНПА = НСтр("ru = 'Часть 20 статьи 13 Федерального закона от 29.12.2006 № 255-ФЗ';
								|en = 'Part 20 of Article 13 of Federal Law No. 255-FZ dated 12/29/2006'");
	ИначеЕсли КодНПА = "2" Тогда
		ПредставлениеНПА = НСтр("ru = 'Пункт 32(2) Положения, утв. ПП РФ от 15.05.2006 № 286';
								|en = 'Clause 32(2) of the Regulation approved by Decree of the Government of the Russian Federation No. 286 dated 05/15/2006'");
	ИначеЕсли КодНПА = "3" Тогда
		ПредставлениеНПА = НСтр("ru = 'Постановление Правительства РФ от 31.12.2022 № 2568';
								|en = 'The Decree of the Government of the Russian Federation dated 12/31/2022 No. 2568'");
	ИначеЕсли КодНПА = "4" Тогда
		ПредставлениеНПА = НСтр("ru = 'Постановление Правительства РФ от 28.11.2024 № 1652';
								|en = 'The Decree of the Government of the Russian Federation dated 11/28/2024 No. 1652'");
	Иначе
		ПредставлениеНПА = "";
	КонецЕсли;
	Возврат ПредставлениеНПА;
КонецФункции

&НаСервереБезКонтекста
Процедура ДополнитьПредставление(Представление, РазделительДо, Строка)
	Представление = ?(Представление = "", Строка, Представление + РазделительДо + Строка);
КонецПроцедуры

#КонецОбласти

#Область СвертываемыеГруппы

&НаСервере
Процедура ПриОпределенииСвертываемыхГрупп(Группы) Экспорт
	Группы.Добавить(Элементы.СотрудникГруппа);
	Группы.Добавить(Элементы.ПособиеГруппа);
	Группы.Добавить(Элементы.НедостающиеСведенияГруппа);
	Группы.Добавить(Элементы.РанееОтправленныеСведенияГруппа);
	Группы.Добавить(Элементы.ИсходящиеДокументыГруппа);
	Группы.Добавить(Элементы.ТОФГруппа);
КонецПроцедуры

&НаСервере
Процедура ПослеДобавленияЭлементовСвертываемыхГрупп() Экспорт
	// Обработка не требуется.
КонецПроцедуры

&НаСервере
Процедура ПриОпределенииСвойствСвертываемойГруппы(Группа, Представление, Заполнена, Развернута, Видимость) Экспорт
	
	Если Группа = Элементы.СотрудникГруппа Тогда
		ОбновитьЭлементыГруппыСотрудник(Группа, Представление, Заполнена, Развернута, Видимость);
		
	ИначеЕсли Группа = Элементы.ПособиеГруппа Тогда
		ОбновитьЭлементыГруппыПособие(Группа, Представление, Заполнена, Развернута, Видимость);
		
	ИначеЕсли Группа = Элементы.НедостающиеСведенияГруппа Тогда
		ОбновитьЭлементыГруппыНедостающиеСведения(Группа, Представление, Заполнена, Развернута, Видимость);
		
	ИначеЕсли Группа = Элементы.РанееОтправленныеСведенияГруппа Тогда
		ОбновитьЭлементыГруппыРанееОтправленныеСведения(Группа, Представление, Заполнена, Развернута, Видимость);
		
	ИначеЕсли Группа = Элементы.ИсходящиеДокументыГруппа Тогда
		ОбновитьЭлементыГруппыИсходящиеДокументы(Группа, Представление, Заполнена, Развернута, Видимость);
		
	ИначеЕсли Группа = Элементы.ТОФГруппа Тогда
		ОбновитьЭлементыГруппыТОФ(Группа, Представление, Заполнена, Развернута, Видимость);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СвернутьРазвернутьГруппу(Элемент)
	СвернутьРазвернутьГруппу(Элемент.Имя);
КонецПроцедуры

&НаСервере
Процедура СвернутьРазвернутьГруппу(ИмяЭлемента)
	СЭДОФСС.СвернутьРазвернутьГруппу(ЭтотОбъект, ИмяЭлемента);
КонецПроцедуры

#КонецОбласти

#Область Команды

&НаСервере
Процедура ВключитьВозможностьРедактированияНаСервере()
	РежимРедактирования = Истина;
	Элементы.ВключитьВозможностьРедактирования.Пометка = Истина;
	Элементы.ВключитьВозможностьРедактирования.Доступность = Ложь;
	
	Для Каждого Элемент Из Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ПолеФормы")
			И Элемент.ТолькоПросмотр Тогда
			Элемент.ТолькоПросмотр = Ложь;
		ИначеЕсли ТипЗнч(Элемент) = Тип("ГруппаФормы")
			И Элемент.ТолькоПросмотр Тогда
			Элемент.ТолькоПросмотр = Ложь;
		ИначеЕсли ТипЗнч(Элемент) = Тип("ТаблицаФормы")
			И (Элемент.ТолькоПросмотр Или Не Элемент.ИзменятьСоставСтрок) Тогда
			Элемент.ТолькоПросмотр = Ложь;
			Элемент.ИзменятьПорядокСтрок = Истина;
			Элемент.ИзменятьСоставСтрок = Истина;
			Элемент.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Авто;
			Элемент.РастягиватьПоВертикали = Истина;
			Элемент.Шапка = Истина;
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьЭлементыФормы();
КонецПроцедуры

#КонецОбласти

#Область Организация

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	ЗафиксироватьИзменениеРеквизита("Организация");
	ОбновитьВторичныеДанныеОбъектаИЭлементыФормы();
КонецПроцедуры

#КонецОбласти

#Область Сотрудник

&НаСервере
Процедура ОбновитьЭлементыГруппыСотрудник(Группа, Представление, Заполнена, Развернута, Видимость)
	Видимость = Объект.Загружен;
	Если Не Видимость Тогда
		Возврат;
	КонецЕсли;
	Заполнена = ЗначениеЗаполнено(Объект.Сотрудник) И ЗначениеЗаполнено(Объект.ФизическоеЛицо);
	Если Развернута Тогда
		Элементы.СотрудникСНИЛС.Видимость = РежимРедактирования Или ЗначениеЗаполнено(Объект.СотрудникСНИЛС);
		Элементы.ГруппаСотрудникФИО.Видимость = РежимРедактирования Или ЗначениеЗаполнено(Объект.СотрудникФамилия);
	Иначе
		Представления = Новый Массив;
		Если Заполнена Тогда
			Представление = Строка(Объект.Сотрудник);
		ИначеЕсли ЗначениеЗаполнено(Объект.ФизическоеЛицо) Тогда
			Представление = СтрШаблон(НСтр("ru = '<%1 не найден в организации %2>';
											|en = '<%1 is not found in the ""%2"" company>'"),
				Строка(Объект.ФизическоеЛицо),
				Строка(Объект.Страхователь));
		ИначеЕсли ЗначениеЗаполнено(Объект.СотрудникСНИЛС) Тогда
			Представление = СтрШаблон(НСтр("ru = 'Не найден по СНИЛС %1 и ФИО %2';
											|en = 'Cannot find by SNILS %1 and full name %2'"),
				Объект.СотрудникСНИЛС,
				СокрЛП(Объект.СотрудникФамилия + " " + Объект.СотрудникИмя + " " + Объект.СотрудникОтчество));
		ИначеЕсли ЗначениеЗаполнено(Объект.СотрудникФамилия) Тогда
			Представление = СтрШаблон(НСтр("ru = 'Не найден по ФИО %1';
											|en = 'Cannot find by full name %1'"),
				СокрЛП(Объект.СотрудникФамилия + " " + Объект.СотрудникИмя + " " + Объект.СотрудникОтчество));
		Иначе
			Представление = НСтр("ru = '<В сообщении не указаны ФИО и СНИЛС>';
								|en = '<Full name and SNILS are not specified in the message>'");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СотрудникПриИзмененииНаСервере()
	ЗафиксироватьИзменениеРеквизита("ФизическоеЛицо");
	ЗафиксироватьИзменениеРеквизита("Сотрудник");
	ФиксацияВторичныхДанныхВДокументах.ЗаполнитьФизическоеЛицо(Объект);
	ОбновитьВторичныеДанныеОбъектаИЭлементыФормы();
КонецПроцедуры

&НаСервере
Процедура ФизическоеЛицоПриИзмененииНаСервере()
	ЗафиксироватьИзменениеРеквизита("ФизическоеЛицо");
	ЗафиксироватьИзменениеРеквизита("Сотрудник");
	Объект.Сотрудник = КадровыйУчет.ОсновнойСотрудникФизическогоЛица(
		Объект.ФизическоеЛицо,
		Объект.Организация,
		НачалоДня(Объект.ВходящаяДата));
	ОбновитьВторичныеДанныеОбъектаИЭлементыФормы();
КонецПроцедуры

#КонецОбласти

#Область Пособие

&НаСервере
Процедура ОбновитьЭлементыГруппыПособие(Группа, Представление, Заполнена, Развернута, Видимость)
	Видимость = Объект.Загружен;
	Если Не Видимость Тогда
		Возврат;
	КонецЕсли;
	ЕстьДанныеЛН = ЗначениеЗаполнено(Объект.НомерЛН);
	ЕстьДанныеОтпуска = ЗначениеЗаполнено(Объект.ОтпускПоУходуДатаНачала);
	ЕстьДанныеРебенка = ЗначениеЗаполнено(Объект.РебенокДатаРождения) Или ЗначениеЗаполнено(Объект.РебенокДатаРождения);
	ЕстьНачалоПериода = ЗначениеЗаполнено(Объект.НачалоПериода);
	Заполнена = ЗначениеЗаполнено(Объект.ВходящийПервичныйДокумент) Или ЕстьДанныеЛН
		Или ЕстьДанныеОтпуска Или ЕстьДанныеРебенка Или ЕстьНачалоПериода;
	Если Развернута Тогда
		Элементы.НомерЛН.Видимость = РежимРедактирования Или ЕстьДанныеЛН;
		Элементы.ОтпускПоУходуГруппа.Видимость = РежимРедактирования Или ЕстьДанныеОтпуска;
		Элементы.РебенокГруппа.Видимость = РежимРедактирования Или ЕстьДанныеРебенка;
		Элементы.НачалоПериода.Видимость = РежимРедактирования Или ЕстьНачалоПериода;
		Элементы.Ребенок.Видимость = СЭДОФСС.ВидимостьПоляРодственник();
	Иначе
		Представления = Новый Массив;
		ЭлементСпискаВыбора = Элементы.КодВидаПособия.СписокВыбора.НайтиПоЗначению(Объект.КодВидаПособия);
		Если ЭлементСпискаВыбора = Неопределено Тогда
			Представления.Добавить(Объект.КодВидаПособия);
		Иначе
			Представления.Добавить(ЭлементСпискаВыбора.Представление);
		КонецЕсли;
		Если ЕстьДанныеЛН Тогда
			Представления.Добавить(СтрШаблон(НСтр("ru = 'ЛН %1';
													|en = 'SLR %1'"), Объект.НомерЛН));
		КонецЕсли;
		Если ЕстьДанныеОтпуска Тогда
			Период = ОбщегоНазначенияБЗК.НаименованиеПериода(Объект.ОтпускПоУходуДатаНачала, Объект.ОтпускПоУходуДатаОкончания);
			Представления.Добавить(СтрШаблон(НСтр("ru = 'Отпуск по уходу %1';
													|en = 'Care leave %1'"), Период));
		КонецЕсли;
		Если ЕстьДанныеРебенка Тогда
			Представления.Добавить(НСтр("ru = 'Ребенок:';
										|en = 'Child:'"));
			Если ЗначениеЗаполнено(Объект.Ребенок) Тогда
				Представления.Добавить(Объект.Ребенок);
			Иначе
				Если ЗначениеЗаполнено(Объект.РебенокСНИЛС) Тогда
					Представления.Добавить(СтрШаблон(НСтр("ru = 'СНИЛС %1';
															|en = 'SNILS: %1'"), Объект.РебенокСНИЛС));
				КонецЕсли;
				Если ЗначениеЗаполнено(Объект.РебенокДатаРождения) Тогда
					Представления.Добавить(СтрШаблон(НСтр("ru = 'дата рождения %1';
															|en = 'date of birth %1'"), Формат(Объект.РебенокДатаРождения, "ДЛФ=D")));
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если ЕстьНачалоПериода Тогда
			Представления.Добавить(СтрШаблон(НСтр("ru = 'с %1';
													|en = 'from %1'"), Формат(Объект.НачалоПериода, "ДЛФ=D")));
		КонецЕсли;
		Представление = СтрСоединить(Представления, " ");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область НедостающиеСведения

&НаСервере
Процедура ОбновитьЭлементыГруппыНедостающиеСведения(Группа, Представление, Заполнена, Развернута, Видимость)
	Видимость = Объект.Загружен;
	Если Не Видимость Тогда
		Возврат;
	КонецЕсли;
	Количество = Объект.НедостающиеСведения.Количество();
	Заполнена = Количество > 0;
	Если Развернута Тогда
		Элементы.НедостающиеСведенияКратко.Видимость = Ложь;
		Если Количество > 3 Тогда
			Элементы.НедостающиеСведения.ВысотаВСтрокахТаблицы = 0;
			Элементы.НедостающиеСведения.РастягиватьПоВертикали = Истина;
		Иначе
			Элементы.НедостающиеСведения.ВысотаВСтрокахТаблицы = Количество + 1;
			Элементы.НедостающиеСведения.РастягиватьПоВертикали = Ложь;
		КонецЕсли;
	Иначе
		Представление = Объект.НедостающиеСведенияКратко;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область РанееОтправленныеСведения

&НаСервере
Процедура ОбновитьЭлементыГруппыРанееОтправленныеСведения(Группа, Представление, Заполнена, Развернута, Видимость)
	Видимость = Объект.Загружен;
	Если Не Видимость Тогда
		Возврат;
	КонецЕсли;
	ЕстьВходящийЗапрос = ЗначениеЗаполнено(Объект.ВходящийЗапрос) Или ЗначениеЗаполнено(Объект.НомерПроцесса);
	ЕстьВходящийОтветНаЗапрос = ЗначениеЗаполнено(Объект.ВходящийОтветНаЗапрос)
		Или ЗначениеЗаполнено(Объект.ИдентификаторСообщения101);
	ЕстьВходящийРеестр = ЗначениеЗаполнено(Объект.ВходящийНомерРеестра) Или ЗначениеЗаполнено(Объект.ВходящийРеестр)
		Или ЗначениеЗаполнено(Объект.ВходящееЗаявление);
	Заполнена = ЕстьВходящийЗапрос Или ЕстьВходящийОтветНаЗапрос Или ЕстьВходящийРеестр;
	Если Развернута Тогда
		Элементы.ГруппаВходящийЗапрос.Видимость = РежимРедактирования Или ЕстьВходящийЗапрос;
		Элементы.ГруппаВходящийОтветНаЗапрос.Видимость = РежимРедактирования Или ЕстьВходящийОтветНаЗапрос;
		Элементы.ГруппаВходящийРеестр.Видимость = РежимРедактирования Или ЕстьВходящийРеестр;
		Элементы.ВходящееЗаявление.Видимость = РежимРедактирования Или ЗначениеЗаполнено(Объект.ВходящееЗаявление)
			Или (ЕстьВходящийРеестр И Объект.КодВидаПособия <> "87" И Объект.КодВидаПособия <> "88");
	Иначе
		Представления = Новый Массив;
		Если ЕстьВходящийЗапрос Тогда
			Если ЗначениеЗаполнено(Объект.ВходящийЗапрос) Тогда
				Представления.Добавить(СокращенноеПредставление(Объект.ВходящийЗапрос, НСтр("ru = 'Входящий запрос';
																							|en = 'Incoming request'")));
			ИначеЕсли ЗначениеЗаполнено(Объект.НомерПроцесса) Тогда
				Представления.Добавить(СтрШаблон(НСтр("ru = 'процесс № %1';
														|en = 'process No. %1'"), Объект.НомерПроцесса));
			КонецЕсли;
		КонецЕсли;
		Если ЕстьВходящийОтветНаЗапрос Тогда
			Если ЗначениеЗаполнено(Объект.ВходящийОтветНаЗапрос) Тогда
				Представления.Добавить(СокращенноеПредставление(Объект.ВходящийОтветНаЗапрос, НСтр("ru = 'Ответ на запрос';
																									|en = 'Response to query'")));
			ИначеЕсли ЗначениеЗаполнено(Объект.ИдентификаторСообщения101) Тогда
				Представления.Добавить(СтрШаблон(НСтр("ru = 'Сообщение 101 %1';
														|en = 'Message 101 %1'"), Объект.ИдентификаторСообщения101));
			КонецЕсли;
		КонецЕсли;
		Если ЕстьВходящийРеестр Тогда
			Если ЗначениеЗаполнено(Объект.ВходящийРеестр) Тогда
				Представления.Добавить(СокращенноеПредставление(Объект.ВходящийРеестр, НСтр("ru = 'Реестр';
																							|en = 'Registry'")));
			ИначеЕсли ЗначениеЗаполнено(Объект.ВходящийНомерРеестра) Тогда
				Представления.Добавить(СтрШаблон(НСтр("ru = 'Реестр %1';
														|en = 'Registry %1'"), Объект.ВходящийНомерРеестра));
			КонецЕсли;
			Если ЗначениеЗаполнено(Объект.НомерСтрокиРеестра) Тогда
				Представления.Добавить(СтрШаблон(НСтр("ru = 'Строка %1';
														|en = 'Row %1'"), Объект.НомерСтрокиРеестра));
			КонецЕсли;
			Если ЗначениеЗаполнено(Объект.ВходящееЗаявление) Тогда
				Представления.Добавить(СокращенноеПредставление(Объект.ВходящийРеестр, НСтр("ru = 'Сведения для реестра';
																							|en = 'Information for the registry'")));
			КонецЕсли;
		КонецЕсли;
		Представление = СтрСоединить(Представления, " ");
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция СокращенноеПредставление(Ссылка, СокращенныйСиноним)
	Представление = Строка(Ссылка);
	Возврат СтрЗаменить(Представление, Ссылка.Метаданные().Синоним, СокращенныйСиноним);
КонецФункции

&НаСервере
Процедура ВходящийЗапросПриИзмененииНаСервере()
	ЗафиксироватьИзменениеРеквизита("ВходящийЗапрос");
	ОбновитьВторичныеДанныеОбъектаИЭлементыФормы();
КонецПроцедуры

&НаСервере
Процедура ВходящийОтветНаЗапросПриИзмененииНаСервере()
	ЗафиксироватьИзменениеРеквизита("ВходящийОтветНаЗапрос");
	ОбновитьВторичныеДанныеОбъектаИЭлементыФормы();
КонецПроцедуры

&НаСервере
Процедура ВходящийРеестрПриИзмененииНаСервере()
	ЗафиксироватьИзменениеРеквизита("ВходящийРеестр");
	ОбновитьВторичныеДанныеОбъектаИЭлементыФормы();
КонецПроцедуры

&НаСервере
Процедура ВходящееЗаявлениеПриИзмененииНаСервере()
	ЗафиксироватьИзменениеРеквизита("ВходящееЗаявление");
	Объект.ВходящийПервичныйДокумент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВходящееЗаявление, "ДокументОснование");
	ВходящийПервичныйДокументПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВходящийПервичныйДокументПриИзмененииНаСервере()
	ЗафиксироватьИзменениеРеквизита("ВходящийПервичныйДокумент");
	ОбновитьВторичныеДанныеОбъектаИЭлементыФормы();
КонецПроцедуры

#КонецОбласти

#Область ИсходящиеДокументы

&НаСервере
Процедура ОбновитьЭлементыГруппыИсходящиеДокументы(Группа, Представление, Заполнена, Развернута, Видимость)
	Если ТребуетсяЗаполнитьИсходящиеДокументы Тогда
		ТребуетсяЗаполнитьИсходящиеДокументы = Ложь;
		ИсходящиеДокументы = Документы.ЗапросНедостающихСведенийПособияСФР.ИсходящиеДокументы(Объект);
	КонецЕсли;
	Видимость = Объект.Загружен И ИсходящиеДокументы <> Неопределено;
	Если Не Видимость Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ИсходящиеДокументы);
	ЕстьБольничный = ЗначениеЗаполнено(Объект.НомерЛН) Или ЗначениеЗаполнено(ИсходящийПервичныйДокумент);
	ЕстьЗапросОтвет = ЗначениеЗаполнено(Объект.ВходящийОтветНаЗапрос)
		Или ЗначениеЗаполнено(Объект.ИдентификаторСообщения101)
		Или ЗначениеЗаполнено(ИсходящийВходящийЗапрос)
		Или ЗначениеЗаполнено(ИсходящийОтветНаЗапрос);
	ЕстьЗаявлениеРеестр = ЗначениеЗаполнено(Объект.ВходящийНомерРеестра) Или ЗначениеЗаполнено(Объект.ВходящийРеестр)
		Или ЗначениеЗаполнено(Объект.ВходящееЗаявление)
		Или ЗначениеЗаполнено(ИсходящееЗаявление)
		Или ЗначениеЗаполнено(ИсходящийРеестр);
	Заполнена = ЕстьБольничный Или ЕстьЗапросОтвет Или ЕстьЗаявлениеРеестр;
	Если Развернута Тогда
		Элементы.ИсходящийПервичныйДокумент.Видимость = РежимРедактирования Или ЕстьБольничный;
		Элементы.ГруппаИсходящийЗапросОтвет.Видимость = РежимРедактирования Или ЕстьЗапросОтвет;
		Элементы.ГруппаИсходящееЗаявлениеРеестр.Видимость = РежимРедактирования Или ЕстьЗаявлениеРеестр;
	Иначе
		Представления = Новый Массив;
		Если ЗначениеЗаполнено(ИсходящийПервичныйДокумент) Тогда
			Представления.Добавить(ИсходящийПервичныйДокумент);
		КонецЕсли;
		Если ЗначениеЗаполнено(ИсходящийВходящийЗапрос) Тогда
			Представления.Добавить(ИсходящийВходящийЗапрос);
		КонецЕсли;
		Если ЗначениеЗаполнено(ИсходящийОтветНаЗапрос) Тогда
			Представления.Добавить(ИсходящийОтветНаЗапрос);
		КонецЕсли;
		Если ЗначениеЗаполнено(ИсходящееЗаявление) Тогда
			Представления.Добавить(ИсходящееЗаявление);
		КонецЕсли;
		Если ЗначениеЗаполнено(ИсходящийРеестр) Тогда
			Представления.Добавить(ИсходящийРеестр);
		КонецЕсли;
		Представление = СтрСоединить(Представления, " ");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ТОФ

&НаСервере
Процедура ОбновитьЭлементыГруппыТОФ(Группа, Представление, Заполнена, Развернута, Видимость)
	Видимость = Объект.Загружен;
	Если Не Видимость Тогда
		Возврат;
	КонецЕсли;
	ЕстьКодНаименованиеТОФ = ЗначениеЗаполнено(Объект.КодТОФ) Или ЗначениеЗаполнено(Объект.НаименованиеТОФ);
	ЕстьОтветственныйСФР = ЗначениеЗаполнено(Объект.ОтветственныйСФРДолжность)
		Или ЗначениеЗаполнено(Объект.ОтветственныйСФРФИО) Или ЗначениеЗаполнено(Объект.ОтветственныйСФРТелефон);
	ЕстьПодписантСФР = ЗначениеЗаполнено(Объект.ПодписантСФРФИО) Или ЗначениеЗаполнено(Объект.ПодписантСФРДолжность);
	Заполнена = ЕстьКодНаименованиеТОФ Или ЕстьОтветственныйСФР Или ЕстьПодписантСФР;
	Если Развернута Тогда
		Элементы.КодНаименованиеТОФГруппа.Видимость = РежимРедактирования Или ЕстьКодНаименованиеТОФ;
		Элементы.ОтветственныйСФРГруппа.Видимость = РежимРедактирования Или ЕстьОтветственныйСФР;
		Элементы.ПодписантСФРГруппа.Видимость = РежимРедактирования Или ЕстьПодписантСФР;
	Иначе
		Представление = "";
		Если ЕстьКодНаименованиеТОФ Тогда
			Представление = СокрЛП(Объект.КодТОФ + " " + Объект.НаименованиеТОФ);
		КонецЕсли;
		Если ЕстьОтветственныйСФР Тогда
			Если Объект.ПодписантСФРФИО = Объект.ОтветственныйСФРФИО Тогда
				ЗаголовокФИО = НСтр("ru = 'Составлено и подписано:';
									|en = 'Created and signed:'");
			Иначе
				ЗаголовокФИО = НСтр("ru = 'Составлено:';
									|en = 'Created:'");
			КонецЕсли;
			ДополнитьПредставление(Представление, ", ", ЗаголовокФИО);
			Если ЗначениеЗаполнено(Объект.ОтветственныйСФРДолжность) Тогда
				ДополнитьПредставление(Представление, " ", Объект.ОтветственныйСФРДолжность);
			КонецЕсли;
			Если ЗначениеЗаполнено(Объект.ОтветственныйСФРФИО) Тогда
				ДополнитьПредставление(Представление, " ", Объект.ОтветственныйСФРФИО);
			КонецЕсли;
			Если ЗначениеЗаполнено(Объект.ОтветственныйСФРТелефон) Тогда
				ДополнитьПредставление(Представление, ", ", СтрШаблон(НСтр("ru = 'Телефон: %1';
																			|en = 'Telephone: %1'"), Объект.ОтветственныйСФРТелефон));
			КонецЕсли;
		КонецЕсли;
		Если ЕстьПодписантСФР И Объект.ПодписантСФРФИО <> Объект.ОтветственныйСФРФИО Тогда
			ДополнитьПредставление(Представление, ", ", НСтр("ru = 'Подписано:';
															|en = 'Signed:'"));
			Если ЗначениеЗаполнено(Объект.ПодписантСФРДолжность) Тогда
				ДополнитьПредставление(Представление, " ", Объект.ПодписантСФРДолжность);
			КонецЕсли;
			Если ЗначениеЗаполнено(Объект.ПодписантСФРФИО) Тогда
				ДополнитьПредставление(Представление, " ", Объект.ПодписантСФРФИО);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область Свойства

// СтандартныеПодсистемы.Свойства
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область ФиксацияВторичныхДанныхВДокументах

&НаСервере
Функция ОбъектЗафиксирован() Экспорт
	Возврат РеквизитФормыВЗначение("Объект").ОбъектЗафиксирован();
КонецФункции

&НаСервере
Процедура ОбновитьВторичныеДанныеНаСервере()
	Если ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбъектФормыЗафиксирован(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	ФиксацияЗаполнитьИдентификаторыФиксТЧ(ЭтотОбъект);
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.СохранитьРеквизитыФормыФикс(ЭтотОбъект, ДокументОбъект);
	
	ПараметрыФиксации = ЭтотОбъект["ПараметрыФиксацииВторичныхДанных"];
	ДокументИзменен = ДокументОбъект.ОбновитьВторичныеДанные(ПараметрыФиксации);
	
	ТребуетсяЗаполнитьИсходящиеДокументы = Ложь;
	ИсходящиеДокументы = Документы.ЗапросНедостающихСведенийПособияСФР.ИсходящиеДокументы(ДокументОбъект);
	
	Если ДокументИзменен Тогда
		Если ТолькоПросмотр Или Не ПравоДоступа("Изменение", ДокументОбъект.Метаданные()) Тогда
			ФиксацияВторичныхДанныхВДокументахФормы.ВывестиПредупреждениеОНаличииИзмененийВИсходныхДанныхКоторыеНельзяПрименить(
				ЭтотОбъект);
		Иначе
			Если Не ДокументОбъект.ЭтоНовый() Тогда
				ФиксацияВторичныхДанныхВДокументахФормы.УстановитьМодифицированность(ЭтотОбъект, Истина);
			КонецЕсли;
			ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
			ФиксацияЗаполнитьРеквизитыФормыФикс(Объект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ФиксацияОписаниеФормы(ПараметрыФиксацииВторичныхДанных) Экспорт
	ОписаниеФормы = ФиксацияВторичныхДанныхВДокументахФормы.ОписаниеФормы();
	
	ОписаниеЭлементовФормы = Новый Соответствие();
	ОписаниеЭлементаФормы = ФиксацияВторичныхДанныхВДокументахФормы.ОписаниеЭлементаФормы();
	ОписаниеЭлементаФормы.ПрефиксПути = "Объект";
	Для Каждого КлючИЗначение Из ПараметрыФиксацииВторичныхДанных.ОписаниеФиксацииРеквизитов Цикл
		ОписаниеЭлементовФормы.Вставить(КлючИЗначение.Ключ, ОписаниеЭлементаФормы);
		Если Не РежимРедактирования И КлючИЗначение.Значение.ИмяГруппы = "ДанныеXML" Тогда
			Элемент = ЭлементСвязанныйСФиксируемымРеквизитом(КлючИЗначение.Ключ, КлючИЗначение.Значение);
			Если Элемент <> Неопределено Тогда
				Элемент.ТолькоПросмотр = истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ОписаниеФормы.Вставить("ОписаниеЭлементовФормы", ОписаниеЭлементовФормы);
	
	ОписаниеФормы.Вставить("ФормаРедактируетсяПослеФиксации", Ложь);
	Возврат ОписаниеФормы;
КонецФункции

&НаСервере
Функция ЭлементСвязанныйСФиксируемымРеквизитом(Ключ, Значение)
	Элемент = Элементы.Найти(Ключ);
	Если Элемент = Неопределено Тогда
		Элемент = Элементы.Найти(Значение.ИмяРеквизита);
	КонецЕсли;
	Возврат Элемент;
КонецФункции

&НаСервере
Функция ФиксацияБыстрыйПоискРеквизитов()
	БыстрыйПоискРеквизитов = Новый Соответствие; // Ключ - имя элемента, значение - имя реквизита.
	
	ПараметрыФиксации = ЭтотОбъект["ПараметрыФиксацииВторичныхДанных"];
	Для Каждого КлючИЗначение Из ПараметрыФиксации.ОписаниеФиксацииРеквизитов Цикл
		Элемент = ЭлементСвязанныйСФиксируемымРеквизитом(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		Если Элемент <> Неопределено Тогда
			БыстрыйПоискРеквизитов.Вставить(Элемент.Имя, КлючИЗначение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат БыстрыйПоискРеквизитов;
КонецФункции

&НаСервере
Процедура ФиксацияЗаполнитьРеквизитыФормыФикс(ТекущийОбъект)
	ФиксацияВторичныхДанныхВДокументахФормы.ЗаполнитьРеквизитыФормыФикс(ЭтотОбъект, ТекущийОбъект);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ФиксацияЗаполнитьИдентификаторыФиксТЧ(Форма)
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ЗаполнитьИдентификаторыСтрок(Форма);
КонецПроцедуры

&НаСервере
Процедура ОтменитьВсеИсправленияНаСервере()
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОчиститьФиксациюИзменений(ЭтотОбъект, Объект);
	ФиксацияЗаполнитьРеквизитыФормыФикс(Объект);
	ОбновитьВторичныеДанныеОбъектаИЭлементыФормы();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗафиксироватьИзменениеРеквизитаВФорме(Элемент, СтандартнаяОбработка = Ложь) Экспорт
	ОписаниеЭлементов = ФиксацияБыстрыйПоискРеквизитов();
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.Подключаемый_ЗафиксироватьИзменениеРеквизитаВФорме(ЭтотОбъект, Элемент, ОписаниеЭлементов);
	ОсновныеРеквизиты = "СотрудникСНИЛС,РебенокСНИЛС,ВходящийНомерРеестра,КодВидаПособия,ДатаОтправкиПодтверждения,МаксимальнаяДатаПодтверждения,ВходящаяДата";
	Если СтрНайти(ОсновныеРеквизиты, Элемент.Имя) <> 0 Тогда
		ОбновитьВторичныеДанныеОбъектаИЭлементыФормы();
	КонецЕсли
КонецПроцедуры

&НаСервере
Процедура ЗафиксироватьИзменениеРеквизита(Имя)
	ОписаниеЭлементов = ФиксацияБыстрыйПоискРеквизитов();
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.Подключаемый_ЗафиксироватьИзменениеРеквизитаВФорме(
		ЭтотОбъект,
		Элементы[Имя],
		ОписаниеЭлементов);
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.СброситьФиксациюИзмененийРеквизитовПоОснованиюЗаполнения(
		ЭтотОбъект,
		Имя);
КонецПроцедуры

#КонецОбласти

#Область Поддержка

&НаСервере
Функция ПодготовитьВопросВПоддержку()
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Вложения = Новый Массив;
	Возврат СЭДОФСС.ПодготовитьВопросВПоддержку(ДокументОбъект, Вложения);
КонецФункции

#КонецОбласти

#КонецОбласти
