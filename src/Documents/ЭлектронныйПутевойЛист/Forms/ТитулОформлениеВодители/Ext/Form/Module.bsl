&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	                                                           
	ОбменСГИСЭПД.ПриСозданииНаСервереПодчиненнойФормы(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	Элементы.ТитулОформлениеВодители.Шапка = Элементы.ТитулОформлениеВодители.ПодчиненныеЭлементы.Количество() > 1;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	// Проверим уникальность водителей
	МассивИмен = Новый Массив;
	Для Каждого Стр Из ТитулОформлениеВодители Цикл
		ФИО = ВРег(СокрЛП(Стр.Фамилия) + СокрЛП(Стр.Имя) + СокрЛП(Стр.Отчество));	
		Если МассивИмен.Найти(ФИО) = Неопределено Тогда
			МассивИмен.Добавить(ФИО);
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Удалите дубли водителей в таблице';
											|en = 'Delete duplicate drivers in the table'"), , НСтр("ru = 'Ошибка';
																							|en = 'Error'"));	
			Возврат;
		КонецЕсли;
	КонецЦикла;
		
	ОбменСГИСЭПДКлиент.СохранитьПараметрыПодчиненнойФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбменСГИСЭПДКлиент.ПриОткрытииПодчиненнойФормы(ЭтотОбъект);
																		
КонецПроцедуры
			
&НаКлиенте
Функция ОписаниеРеквизитовФормы() Экспорт
	
	Возврат ОписаниеРеквизитовФормыСервер();
	
КонецФункции

&НаСервере
Функция ОписаниеРеквизитовФормыСервер()
	
	Возврат ОбменСГИСЭПД.ОписаниеРеквизитовФормы(ЭтаФорма);
		
КонецФункции

&НаКлиенте
Процедура ТитулОформлениеВодителиПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ИдентификаторСтроки = Неопределено;
		ТекущиеДанные.Свойство("ИдентификаторСтроки", ИдентификаторСтроки);
		ОбменСГИСЭПДКлиент.ОчиститьПодчиненныеТаблицы(ЭтотОбъект, Элемент.Имя, ИдентификаторСтроки, Отказ);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ХранимыеДанныеВодительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Элемент.Имя, Элементы.ТитулОформлениеВодители.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ХранимыеДанныеВодительОткрытие(Элемент, СтандартнаяОбработка)

	Если ТипЗнч(Элементы.ТитулОформлениеВодители.ТекущиеДанные.ХранимыеДанныеВодитель) <> Тип("СправочникСсылка.ХранимыеДанныеЭПД") Тогда
		СтандартнаяОбработка = Ложь;
		ОбменСГИСЭПДКлиент.ВывестиФормуВводаДанных(ЭтотОбъект, Элемент.Имя, Элементы.ТитулОформлениеВодители.ТекущиеДанные, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ХранимыеДанныеВодительПриИзменении(Элемент)
	
	ОбменСГИСЭПДКлиент.ХранимыеДанныеПриИзменении(ЭтотОбъект, 
		Элементы.ТитулОформлениеВодители.ТекущиеДанные.ХранимыеДанныеВодитель, 
		Элемент, 
		Элементы.ТитулОформлениеВодители.ТекущиеДанные);	
	
КонецПроцедуры

&НаКлиенте
Процедура ХранимыеДанныеВодительАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ГруппаДанных = СтрЗаменить(Элемент.Имя, "ХранимыеДанные", "");
	ПараметрыПолученияДанных.Отбор = ОбменСГИСЭПДКлиент.ПолучитьОтборХранимыхДанных(ЭтотОбъект, ЭтотОбъект, ГруппаДанных);
	ПараметрыПолученияДанных.СпособПоискаСтроки = ПредопределенноеЗначение("СпособПоискаСтрокиПриВводеПоСтроке.ЛюбаяЧасть");
	
КонецПроцедуры

&НаКлиенте
Процедура ТитулОформлениеВодителиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	ОбменСГИСЭПДКлиент.ТаблицаПриНачалеРедактирования(Элемент, ЭтотОбъект, НоваяСтрока, Копирование);

КонецПроцедуры