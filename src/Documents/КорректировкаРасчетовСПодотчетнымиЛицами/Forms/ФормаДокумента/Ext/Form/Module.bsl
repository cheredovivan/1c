
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат формы для анализа
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// Обработчик механизма "ВерсионированиеОбъектов"
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ИнтеграцияС1СДокументооборотом
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Объект);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// Обработчик подсистемы "Свойства"
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	// СтандартныеПодсистемы.РаботаСФайлами
	ПараметрыГиперссылки = РаботаСФайлами.ГиперссылкаФайлов();
	ПараметрыГиперссылки.Размещение = "КоманднаяПанель";
	РаботаСФайлами.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыГиперссылки);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	ИсправлениеДокументов.ПриСозданииНаСервере(ЭтотОбъект, Элементы.СтрокаИсправление);
	
	ПараметрыВыбораСтатейИАналитик = Документы.КорректировкаРасчетовСПодотчетнымиЛицами.ПараметрыВыбораСтатейИАналитик(Объект.ХозяйственнаяОперация);
	ДоходыИРасходыСервер.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	//++ НЕ УТ
	ПараметрыНастройки = Документы.КорректировкаРасчетовСПодотчетнымиЛицами.ПараметрыНастройкиСчетовУчета(Объект.ХозяйственнаяОперация);
	НастройкаСчетовУчетаСервер.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыНастройки);
	//-- НЕ УТ
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриЧтенииСозданииНаСервере();
	
	ПараметрыВыбораСтатейИАналитик = Документы.КорректировкаРасчетовСПодотчетнымиЛицами.ПараметрыВыбораСтатейИАналитик(Объект.ХозяйственнаяОперация);
	ДоходыИРасходыСервер.ПриЧтенииНаСервере(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	//++ НЕ УТ
	ПараметрыНастройки = Документы.КорректировкаРасчетовСПодотчетнымиЛицами.ПараметрыНастройкиСчетовУчета(Объект.ХозяйственнаяОперация);
	НастройкаСчетовУчетаСервер.ПриЧтенииНаСервере(ЭтотОбъект, ПараметрыНастройки);
	//-- НЕ УТ
	
	// Обработчик механизма "ДатыЗапретаИзменения"
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	СобытияФорм.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ИсправлениеДокументов.ПриЧтенииНаСервере(ЭтотОбъект, Элементы.СтрокаИсправление);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	ИсправлениеДокументовКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	//++ НЕ УТ
	НастройкаСчетовУчетаСервер.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	//-- НЕ УТ
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ИсправлениеДокументовКлиент.ПослеЗаписи(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	//++ НЕ УТ
	НастройкаСчетовУчетаСервер.ПослеЗаписиНаСервере(ЭтотОбъект);
	//-- НЕ УТ
	
	ДоходыИРасходыСервер.ПослеЗаписиНаСервере(ЭтотОбъект);
	
	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
	СобытияФорм.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ХозяйственнаяОперацияПриИзменении(Элемент)
	ПриИзмененииЭлементаВлияющегоНаЗадолженность(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ПриИзмененииЭлементаВлияющегоНаЗадолженность(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ПриИзмененииЭлементаВлияющегоНаЗадолженность(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	ПриИзмененииЭлементаВлияющегоНаЗадолженность(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СтрокаИсправлениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ИсправлениеДокументовКлиент.СтрокаИсправлениеОбработкаНавигационныйСсылки(
		ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКорректировкаЗадолженности

&НаКлиенте
Процедура КорректировкаЗадолженностиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	//++ НЕ УТ
	НастройкаСчетовУчетаКлиент.ПриНажатии(ЭтотОбъект, Поле, СтандартнаяОбработка);
	//-- НЕ УТ
	Возврат;
	
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаЗадолженностиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	//++ НЕ УТ
	Если НоваяСтрока И Копирование Тогда
		НастройкаСчетовУчетаКлиент.ПриКопированииСтроки(ЭтотОбъект, Элемент);
	КонецЕсли;
	//-- НЕ УТ
	Возврат;
	
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаЗадолженностиАналитикаРасходовНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	ДоходыИРасходыКлиент.НачалоВыбораАналитикиРасходов(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаЗадолженностиАналитикаРасходовАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ДоходыИРасходыКлиент.АвтоПодборАналитикиРасходов(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаЗадолженностиАналитикаРасходовОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ДоходыИРасходыКлиент.ОкончаниеВводаТекстаАналитикиРасходов(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаЗадолженностиСтатьяНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	ДоходыИРасходыКлиент.НачалоВыбораСтатьи(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаЗадолженностиСтатьяПриИзменении(Элемент)
	СтатьяПриИзмененииСервер(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаЗадолженностиСуммаПриИзменении(Элемент)
	
	СуммаПриИзмененииНаСервере(Элементы.КорректировкаЗадолженности.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаЗадолженностиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование ИЛИ Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	КорректировкаЗадолженностиПередНачаломДобавленияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаЗадолженностиВалютаПриИзменении(Элемент)
	ВалютаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаЗадолженностиПриАктивизацииЯчейки(Элемент)
	Если Элемент.ТекущийЭлемент = Элементы.КорректировкаЗадолженностиСуммаРегл
		 ИЛИ Элемент.ТекущийЭлемент = Элементы.КорректировкаЗадолженностиСуммаУпр Тогда
		КорректировкаЗадолженностиПриАктивизацииЯчейкиНаСервере();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Записать(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Провести(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОбщегоНазначенияУТКлиент.ПровестиИЗакрыть(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьПоОстаткам(Команда)
	
	ПараметрыПроверки = РаботаСТабличнымиЧастямиКлиент.ПараметрыПроверкиЗаполнения();
	ПараметрыПроверки.ПроверяемыеРеквизиты.Вставить("Организация",           НСтр("ru = 'Организация';
																					|en = 'Company'"));
	
	ПараметрыПолученияОстатков = ПараметрыПолученияОстатков(ЭтотОбъект);
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("ПараметрыПолученияОстатков", ПараметрыПолученияОстатков);
	ПараметрыПодбора.Вставить("ДанныеДокумента", Объект.КорректировкаЗадолженности);
	
	Оповещение = Новый ОписаниеОповещения("ПодборПоОстаткамПослеПроверки", ЭтотОбъект, ПараметрыПодбора);
	РаботаСТабличнымиЧастямиКлиент.ПроверитьВозможностьЗаполнения(ЭтотОбъект, Оповещение, ПараметрыПроверки);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_КомандаПанелиПрисоединенныхФайлов(Команда)
	РаботаСФайламиКлиент.КомандаУправленияПрисоединеннымиФайлами(ЭтотОбъект, Команда);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	РаботаСФайламиКлиент.ПолеПредпросмотраПроверкаПеретаскивания(ЭтотОбъект, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	РаботаСФайламиКлиент.ПолеПредпросмотраПеретаскивание(ЭтотОбъект, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраНажатие(Элемент, СтандартнаяОбработка)
	РаботаСФайламиКлиент.ПолеПредпросмотраНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПриИзмененииРеквизитов

&НаКлиенте
Асинх Процедура ПриИзмененииЭлементаВлияющегоНаЗадолженность(Элемент)
	
	ИменаЗависимыхРеквизитов = СоответствиеЭлементовИРеквизитов(ЭтотОбъект).Получить(Элемент);
	Если ИменаЗависимыхРеквизитов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект[ИменаЗависимыхРеквизитов.РеквизитОбъекта]
			= ЭтотОбъект[ИменаЗависимыхРеквизитов.РеквизитФормы] Тогда
		Возврат;
	КонецЕсли;
	
	Ответ = Ждать МожноОчиститьЗадолженностьАсинх();
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Объект[ИменаЗависимыхРеквизитов.РеквизитОбъекта]
			= ЭтотОбъект[ИменаЗависимыхРеквизитов.РеквизитФормы];
	Иначе
		
		Объект.КорректировкаЗадолженности.Очистить();
		ЭтотОбъект[ИменаЗависимыхРеквизитов.РеквизитФормы]
			= Объект[ИменаЗависимыхРеквизитов.РеквизитОбъекта];
			
		Если Элемент = Элементы.ХозяйственнаяОперация Тогда
			ХозяйственнаяОперацияПриИзмененииНаСервере();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура КорректировкаЗадолженностиПриАктивизацииЯчейкиНаСервере()
	
	ИдентификаторСтроки = Элементы.КорректировкаЗадолженности.ТекущаяСтрока;
	Если ИдентификаторСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РедактируемаяСтрока =
		Объект.КорректировкаЗадолженности.НайтиПоИдентификатору(ИдентификаторСтроки);
	ИзменитьДоступностьСуммРеглУпр(РедактируемаяСтрока);
	
КонецПроцедуры

&НаСервере
Процедура ВалютаПриИзмененииНаСервере()
	
	ИдентификаторСтроки = Элементы.КорректировкаЗадолженности.ТекущаяСтрока;
	Если ИдентификаторСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РедактируемаяСтрока =
		Объект.КорректировкаЗадолженности.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	ИзменитьДоступностьСуммРеглУпр(РедактируемаяСтрока);
	ПараметрыПолученияОстатков = ПараметрыПолученияОстатков(ЭтотОбъект);
	ЗаполнитьЗначенияСвойств(ПараметрыПолученияОстатков, РедактируемаяСтрока);
	Документы.КорректировкаРасчетовСПодотчетнымиЛицами.ПересчитатьСуммыРеглИУпр(РедактируемаяСтрока, ПараметрыПолученияОстатков);
	
КонецПроцедуры

&НаСервере
Процедура КорректировкаЗадолженностиПередНачаломДобавленияНаСервере()
	
	СтрокаКорректировки = Объект.КорректировкаЗадолженности.Добавить();
	СтрокаКорректировки.Валюта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Организация, "ВалютаРегламентированногоУчета");
	ДоходыИРасходыКлиентСервер.ПриДобавленииСтрокиВТаблицу(ЭтотОбъект, СтрокаКорректировки, "Объект.КорректировкаЗадолженности");
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьДоступностьСуммРеглУпр(РедактируемаяСтрока)
	
	Если РедактируемаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРегламентированногоУчета =
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Организация, "ВалютаРегламентированногоУчета");
	
	Элементы.КорректировкаЗадолженностиСуммаРегл.ТолькоПросмотр = РедактируемаяСтрока.Валюта = ВалютаРегламентированногоУчета;
	Элементы.КорректировкаЗадолженностиСуммаУпр.ТолькоПросмотр  = РедактируемаяСтрока.Валюта = ВалютаУправленческогоУчета;
	
КонецПроцедуры

&НаСервере
Процедура ХозяйственнаяОперацияПриИзмененииНаСервере()
	
	ПараметрыВыбораСтатейИАналитик = Документы.КорректировкаРасчетовСПодотчетнымиЛицами.ПараметрыВыбораСтатейИАналитик(Объект.ХозяйственнаяОперация);
	ДоходыИРасходыСервер.ПриИзмененииПараметровВыбораСтатей(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	//++ НЕ УТ
	ПараметрыНастройки = Документы.КорректировкаРасчетовСПодотчетнымиЛицами.ПараметрыНастройкиСчетовУчета(Объект.ХозяйственнаяОперация);
	НастройкаСчетовУчетаСервер.ПриИзмененииПараметровНастройки(ЭтотОбъект, ПараметрыНастройки);
	//-- НЕ УТ
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервере
Процедура СтатьяПриИзмененииСервер(ИмяЭлемента)
	
	ДоходыИРасходыКлиентСервер.СтатьяПриИзменении(ЭтотОбъект, Элементы[ИмяЭлемента]);
	//++ НЕ УТ
	ПараметрыНастройки = Документы.КорректировкаРасчетовСПодотчетнымиЛицами.ПараметрыНастройкиСчетовУчета(Объект.ХозяйственнаяОперация);
	НастройкаСчетовУчетаСервер.ПриИзмененииПараметровНастройки(ЭтотОбъект, ПараметрыНастройки);
	//-- НЕ УТ
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборПоОстаткамПослеПроверки(Результат, ПараметрыПодбора) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ПодборПоОстаткамЗавершение", ЭтотОбъект);
	ОткрытьФорму("Документ.КорректировкаРасчетовСПодотчетнымиЛицами.Форма.ПодборПоОстаткам",
	             ПараметрыПодбора,,,,,
	             Оповещение,
	             РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборПоОстаткамЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Для Каждого Остаток Из Результат Цикл
			
			Если Остаток.ПрисутствуетВДокументе Тогда
				ОтборСтрок = Новый Структура("ПодотчетноеЛицо,Валюта,ЦельВыдачи");
				ЗаполнитьЗначенияСвойств(ОтборСтрок, Остаток);
				СтрокиДокумента = Объект.КорректировкаЗадолженности.НайтиСтроки(ОтборСтрок);
				Если СтрокиДокумента.Количество() Тогда
					ЗаполнитьЗначенияСвойств(СтрокиДокумента[0], Остаток);
				КонецЕсли;
			Иначе
				ЗаполнитьЗначенияСвойств(Объект.КорректировкаЗадолженности.Добавить(), Остаток);
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Функция МожноОчиститьЗадолженностьАсинх()
	
	Если Объект.КорректировкаЗадолженности.Количество() Тогда
		ТекстВопроса = НСтр("ru = 'Суммы списания задолженности могут стать неактуальным.
		                        |Очистить список задолженности?';
		                        |en = 'AR/AP write-off amounts can become irrelevant.
		                        |Clear debt list?'");
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		Ответ = КодВозвратаДиалога.Да;
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СоответствиеЭлементовИРеквизитов(Форма)
	
	СоответствиеЭлементовИРеквизитов = Новый Соответствие;
	СвойстваСтруктурыИмен = "РеквизитОбъекта,РеквизитФормы";
	СоответствиеЭлементовИРеквизитов.Вставить(
		Форма.Элементы.Дата,
		Новый Структура(СвойстваСтруктурыИмен, "Дата", "ПредыдущаяДатаДокумента"));
	СоответствиеЭлементовИРеквизитов.Вставить(
		Форма.Элементы.Организация,
		Новый Структура(СвойстваСтруктурыИмен, "Организация", "ПредыдущаяОрганизация"));
	СоответствиеЭлементовИРеквизитов.Вставить(
		Форма.Элементы.Подразделение,
		Новый Структура(СвойстваСтруктурыИмен, "Подразделение", "ПредыдущееПодразделение"));
	СоответствиеЭлементовИРеквизитов.Вставить(
		Форма.Элементы.ХозяйственнаяОперация,
		Новый Структура(СвойстваСтруктурыИмен, "ХозяйственнаяОперация", "ПредыдущаяХозяйственнаяОперация"));
		
	Возврат СоответствиеЭлементовИРеквизитов;
		
КонецФункции

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ОбновитьКэшРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьКэшРеквизитов(Форма)
	
	СоответствиеЭлементовИРеквизитов = СоответствиеЭлементовИРеквизитов(Форма);
	Для Каждого ОписаниеЭлемента Из СоответствиеЭлементовИРеквизитов Цикл
		ИменаЗависимыхРеквизитов = ОписаниеЭлемента.Значение;
		Форма[ИменаЗависимыхРеквизитов.РеквизитФормы]
			= Форма.Объект[ИменаЗависимыхРеквизитов.РеквизитОбъекта];
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СуммаПриИзмененииНаСервере(ИдентификаторСтроки)
	
	ПараметрыПолученияОстатков = ПараметрыПолученияОстатков(ЭтотОбъект);
	РедактируемаяСтрока = Объект.КорректировкаЗадолженности.НайтиПоИдентификатору(ИдентификаторСтроки);
	ЗаполнитьЗначенияСвойств(ПараметрыПолученияОстатков, РедактируемаяСтрока);
	Документы.КорректировкаРасчетовСПодотчетнымиЛицами.ПересчитатьСуммыРеглИУпр(РедактируемаяСтрока, ПараметрыПолученияОстатков);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыПолученияОстатков(Форма)
	
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("Дата");
	ПараметрыПодбора.Вставить("Организация");
	ПараметрыПодбора.Вставить("Подразделение");
	ПараметрыПодбора.Вставить("ХозяйственнаяОперация");
	ЗаполнитьЗначенияСвойств(ПараметрыПодбора, Форма.Объект);
	
	ПараметрыПодбора.Вставить("ПодотчетноеЛицо");
	ПараметрыПодбора.Вставить("Валюта");
	ПараметрыПодбора.Вставить("ЦельВыдачи");
	
	Возврат ПараметрыПодбора;
	
КонецФункции

#КонецОбласти
