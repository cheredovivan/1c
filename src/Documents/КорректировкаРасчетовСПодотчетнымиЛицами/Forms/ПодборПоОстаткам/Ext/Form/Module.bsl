
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	ПараметрыПолученияОстатков = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры,
		                                                                        "ПараметрыПолученияОстатков",
		                                                                        Неопределено);
	ДанныеДокумента = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры,
		                                                             "ДанныеДокумента",
		                                                             Неопределено);
	
	АдресПараметровПолученияОстатков = ПоместитьВоВременноеХранилище(ПараметрыПолученияОстатков, УникальныйИдентификатор);
	
	ТаблицаОстатков.Очистить();
	Выборка = Документы.КорректировкаРасчетовСПодотчетнымиЛицами.ВыборкаОстатков(ПараметрыПолученияОстатков);
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаОстатков.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		ОтборСтрок = Новый Структура("ПодотчетноеЛицо,ЦельВыдачи,Валюта");
		ЗаполнитьЗначенияСвойств(ОтборСтрок, НоваяСтрока);
		НоваяСтрока.ПрисутствуетВДокументе = ДанныеДокумента.НайтиСтроки(ОтборСтрок).Количество();
		НоваяСтрока.СтрокаВыбрана          = НоваяСтрока.ПрисутствуетВДокументе;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаОстатков

&НаКлиенте
Процедура ТаблицаОстатковСуммаПриИзменении(Элемент)
	
	СуммаПриИзмененииНаСервере(Элементы.ТаблицаОстатков.ТекущаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	Результат = Новый Массив;
	ПереносимыеСвойства = "ПодотчетноеЛицо,ЦельВыдачи,Валюта,Сумма,СуммаРегл,СуммаУпр,ПрисутствуетВДокументе";
	Для Каждого Остаток Из ТаблицаОстатков Цикл
		Если Остаток.СтрокаВыбрана Тогда
			НоваяСтрока = Новый Структура(ПереносимыеСвойства);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Остаток);
			Результат.Добавить(НоваяСтрока);
		КонецЕсли;
	КонецЦикла;
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСтроки(Команда)
	ВыбратьСтрокиСервере(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьСтроки(Команда)
	ВыбратьСтрокиСервере(Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыбратьСтрокиСервере(ЗначениеВыбора = Истина)
	
	Для каждого СтрокаТаблицы Из ТаблицаОстатков.НайтиСтроки(Новый Структура("СтрокаВыбрана", Не ЗначениеВыбора)) Цикл
		СтрокаТаблицы.СтрокаВыбрана = ЗначениеВыбора;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаОстатков.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаОстатков.ПрисутствуетВДокументе");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Gray);
	
КонецПроцедуры

&НаСервере
Процедура СуммаПриИзмененииНаСервере(ИдентификаторСтроки)
	
	ПараметрыПолученияОстатков = ПолучитьИзВременногоХранилища(АдресПараметровПолученияОстатков);
	РедактируемаяСтрока = ТаблицаОстатков.НайтиПоИдентификатору(ИдентификаторСтроки);
	ЗаполнитьЗначенияСвойств(ПараметрыПолученияОстатков, РедактируемаяСтрока);
	Документы.КорректировкаРасчетовСПодотчетнымиЛицами.ПересчитатьСуммыРеглИУпр(РедактируемаяСтрока, ПараметрыПолученияОстатков);
	
КонецПроцедуры

#КонецОбласти
