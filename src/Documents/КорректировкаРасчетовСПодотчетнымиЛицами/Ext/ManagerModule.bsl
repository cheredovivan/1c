
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует описание реквизитов объекта, заполняемых по статистике их использования.
//
// Параметры:
//  ОписаниеРеквизитов - Структура - описание реквизитов, для которых необходимо получить значения по статистике
//
//
Процедура ЗадатьОписаниеЗаполняемыхРеквизитовПоСтатистике(ОписаниеРеквизитов) Экспорт
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Автор";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Автор";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов,
		"Организация", Параметры);
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Автор";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Автор";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов,
		"Подразделение", Параметры);
	
КонецПроцедуры

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив из Строка - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("РасчетыСПодотчетниками");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("СуммыДокументовВВалютахУчета");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("ИсправлениеДокументов");
	
	АвансовыйОтчетЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура, СписокЗначений Из Строка - коллекция таблиц данных для отражения в регистр.
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.КорректировкаРасчетовСПодотчетнымиЛицами") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос        = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаДенежныеСредстваУПодотчетныхЛиц(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		
		КорректировкаРасчетовСПодотчетнымиЛицамиЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	ПроведениеДокументов.ДобавитьЗапросыСторноДвижений(Запрос, ТекстыЗапроса, Регистры, ПустаяСсылка().Метаданные());
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Возвращает параметры выбора статей в документе.
// 
// Параметры:
// 	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция документа.
// 
// Возвращаемое значение:
// 	Массив из Структура см. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики
//
Функция ПараметрыВыбораСтатейИАналитик(ХозяйственнаяОперация) Экспорт
	
	СписаниеВДоходы =
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтнесениеНаДоходыЗадолженностиПередПодотчетнымЛицом;
	МассивПараметровВыбора = Новый Массив;
	
	#Область Статья
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект.КорректировкаЗадолженности";
	ПараметрыВыбора.Статья = "Статья";
	ПараметрыВыбора.ТипСтатьи = "ТипСтатьи";
	ПараметрыВыбора.ЗначениеПоУмолчанию = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	Если СписаниеВДоходы Тогда
		ПараметрыВыбора.ЗначениеПоУмолчанию = ПланыВидовХарактеристик.СтатьиДоходов.ПустаяСсылка();
	КонецЕсли;
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("КорректировкаЗадолженностиСтатья");
	
	#Область АналитикаДоходов
	ПараметрыВыбора.ВыборСтатьиДоходов = СписаниеВДоходы;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("КорректировкаЗадолженностиАналитикаДоходов");
	#КонецОбласти
	
	#Область АналитикаРасходов
	ПараметрыВыбора.ВыборСтатьиРасходов = НЕ СписаниеВДоходы;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("КорректировкаЗадолженностиАналитикаРасходов");
	#КонецОбласти
	
	#Область АналитикаАктивовПассивов
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить(
		        "КорректировкаЗадолженностиАналитикаАктивовПассивов");
	#КонецОбласти
	
	МассивПараметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
		
	Возврат МассивПараметровВыбора;
	
КонецФункции

//++ НЕ УТ

// Возвращает параметры настройки счетов учета в документе.
// 
// Параметры:
// 	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция документа.
// 	
// Возвращаемое значение:
// 	Массив Из См. НастройкаСчетовУчетаСервер.ПараметрыНастройки
//
Функция ПараметрыНастройкиСчетовУчета(ХозяйственнаяОперация) Экспорт
	
	МассивПараметров = Новый Массив();
	
	#Область НастройкаСчетаОтраженияСписанияРасходов
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект.КорректировкаЗадолженности";
	
	ПараметрыНастройки.ТипСтатьи = "ТипСтатьи";
	ПараметрыНастройки.СтатьяАктивовПассивов = "Статья";
	
	ПараметрыНастройки.Представление = "ПредставлениеОтраженияВРеглУчете";
	
	ПараметрыНастройки.Организация = "Объект.Организация";

	ПараметрыНастройки.ЭлементыФормы.Добавить("КорректировкаЗадолженностиПредставлениеОтраженияВРеглУчете");
	МассивПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	Возврат МассивПараметров;
	
КонецФункции

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	Возврат КорректировкаРасчетовСПодотчетнымиЛицамиЛокализация.ТекстОтраженияВРеглУчете();
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат "";
	
КонецФункции

//-- НЕ УТ

// Возвращает остатки денежных средств у подотчетных лиц
//
// Параметры:
//  ПараметрыПолученияОстатков - Структура:
//   *Дата                  - Дата - дата, на которую будут получены остатки.
//   *Организация           - СправочникСсылка.Организации.
//   *Подразделение         - СправочникСсылка.СтруктураПредприятия.
//   *ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - определяет характер получаемого остатка.
//                                                     если указана хоз. операция СписаниеЗадолженностиПодотчетногоЛица,
//                                                     то будут получены положительные остатки.
//                                                     В противном случае будут получены отрицательные остатки.
//   *ПодотчетноеЛицо       - СправочникСсылка.ФизическиеЛица - не обязательный параметр,
//                                                   если не задан, то будут получены остатки по всем подотчетным лицам.
//   *Валюта                - СправочникСсылка.Валюты         - не обязательный параметр,
//                                                             если не задан, то будут получены остатки по всем валютам.
//   *ЦельВыдачи            - СправочникСсылка.СтатьиДвиженияДенежныхСредств - не обязательный параметр,
//                                                               если не задан, то будут получены остатки оп всем целям.
//
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//   *Организация                    - СправочникСсылка.Организации,
//   *Подразделение                  - СправочникСсылка.СтруктураПредприятия,
//   *ПодотчетноеЛицо                - СправочникСсылка.ФизическиеЛица,
//   *ЦельВыдачи                     - СправочникСсылка.СтатьиДвиженияДенежныхСредств,
//   *Валюта                         - СправочникСсылка.Валюты,
//   *Сумма                          - Число,
//   *СуммаРегл                      - Число,
//   *СуммаУпр                       - Число,
//   *ДатаПоследнегоАвансовогоОтчета - Дата.
//
Функция ВыборкаОстатков(ПараметрыПолученияОстатков) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГраницаОстатков", Новый Граница(ПараметрыПолученияОстатков.Дата, ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Организация",   ПараметрыПолученияОстатков.Организация);
	Запрос.УстановитьПараметр("Подразделение", ПараметрыПолученияОстатков.Подразделение);
	Запрос.УстановитьПараметр("ЭтоСписаниеЗадолженности",
		ПараметрыПолученияОстатков.ХозяйственнаяОперация =
			Перечисления.ХозяйственныеОперации.СписаниеЗадолженностиПодотчетногоЛица);
		
	НеобязательныеПараметры = Новый Массив;
	НеобязательныеПараметры.Добавить("ПодотчетноеЛицо");
	НеобязательныеПараметры.Добавить("ЦельВыдачи");
	НеобязательныеПараметры.Добавить("Валюта");
	
	МассивТекстовПараметров = Новый Массив;
	МассивТекстовПараметров.Добавить("ИСТИНА");
	Для Каждого ИмяПараметра Из НеобязательныеПараметры Цикл
		ЗначениеПараметра = ПараметрыПолученияОстатков[ИмяПараметра];
		Если ЗначениеЗаполнено(ЗначениеПараметра) Тогда
			МассивТекстовПараметров.Добавить(СтрЗаменить("ИмяПараметра = &ИмяПараметра", "ИмяПараметра", ИмяПараметра));
			Запрос.УстановитьПараметр(ИмяПараметра, ЗначениеПараметра);
		КонецЕсли;
	КонецЦикла;
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДенежныеСредстваУПодотчетныхЛицОстатки.Организация КАК Организация,
	|	ДенежныеСредстваУПодотчетныхЛицОстатки.Подразделение КАК Подразделение,
	|	ДенежныеСредстваУПодотчетныхЛицОстатки.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	ДенежныеСредстваУПодотчетныхЛицОстатки.ЦельВыдачи КАК ЦельВыдачи,
	|	ДенежныеСредстваУПодотчетныхЛицОстатки.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА НЕ &ЭтоСписаниеЗадолженности
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ * ДенежныеСредстваУПодотчетныхЛицОстатки.СуммаОстаток КАК СуммаОстаток,
	|	ВЫБОР
	|		КОГДА НЕ &ЭтоСписаниеЗадолженности
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ * ДенежныеСредстваУПодотчетныхЛицОстатки.СуммаУпрОстаток КАК СуммаУпрОстаток,
	|	ВЫБОР
	|		КОГДА НЕ &ЭтоСписаниеЗадолженности
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ * ДенежныеСредстваУПодотчетныхЛицОстатки.СуммаРеглОстаток КАК СуммаРеглОстаток
	|ПОМЕСТИТЬ ВТОстаткиДенежныхСредств
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.Остатки(
	|			&ГраницаОстатков,
	|			Организация = &Организация
	|				И Подразделение = &Подразделение
	|				И &ДополнительныйОтбор) КАК ДенежныеСредстваУПодотчетныхЛицОстатки
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ЭтоСписаниеЗадолженности
	|				ТОГДА ДенежныеСредстваУПодотчетныхЛицОстатки.СуммаОстаток > 0
	|			ИНАЧЕ ДенежныеСредстваУПодотчетныхЛицОстатки.СуммаОстаток < 0
	|		КОНЕЦ
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПодотчетноеЛицо,
	|	Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиДенежныхСредств.Организация КАК Организация,
	|	ОстаткиДенежныхСредств.Подразделение КАК Подразделение,
	|	ОстаткиДенежныхСредств.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	ОстаткиДенежныхСредств.ЦельВыдачи КАК ЦельВыдачи,
	|	ОстаткиДенежныхСредств.Валюта КАК Валюта,
	|	ОстаткиДенежныхСредств.СуммаОстаток КАК Сумма,
	|	ОстаткиДенежныхСредств.СуммаУпрОстаток КАК СуммаУпр,
	|	ОстаткиДенежныхСредств.СуммаРеглОстаток КАК СуммаРегл,
	|	МАКСИМУМ(ЕСТЬNULL(Документ.Дата,ДАТАВРЕМЯ(1,1,1))) КАК ДатаПоследнегоАвансовогоОтчета
	|ИЗ
	|	ВТОстаткиДенежныхСредств КАК ОстаткиДенежныхСредств
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АвансовыйОтчет КАК Документ
	|		ПО (Документ.Организация = ОстаткиДенежныхСредств.Организация)
	|			И (Документ.ПодотчетноеЛицо = ОстаткиДенежныхСредств.ПодотчетноеЛицо)
	|			И (Документ.Подразделение = ОстаткиДенежныхСредств.Подразделение)
	|			И Документ.Проведен
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиДенежныхСредств.Организация,
	|	ОстаткиДенежныхСредств.Подразделение,
	|	ОстаткиДенежныхСредств.ПодотчетноеЛицо,
	|	ОстаткиДенежныхСредств.ЦельВыдачи,
	|	ОстаткиДенежныхСредств.Валюта,
	|	ОстаткиДенежныхСредств.СуммаОстаток,
	|	ОстаткиДенежныхСредств.СуммаУпрОстаток,
	|	ОстаткиДенежныхСредств.СуммаРеглОстаток
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДополнительныйОтбор", СтрСоединить(МассивТекстовПараметров, " И "));
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Пересчитывает суммы Регл и Упр редактируемой строки
//
// Параметры:
//  РедактируемаяСтрока - ДанныеФормыСтруктура - строка таблицы формы, в которой должен произойти пересчет
//  ПараметрыПолученияОстатков - Структура:
//   *Дата                  - Дата - дата, на которую будут получены остатки.
//   *Организация           - СправочникСсылка.Организации.
//   *Подразделение         - СправочникСсылка.СтруктураПредприятия.
//   *ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - определяет характер получаемого остатка.
//                                                     если указана хоз. операция СписаниеЗадолженностиПодотчетногоЛица,
//                                                     то будут получены положительные остатки.
//                                                     В противном случае будут получены отрицательные остатки.
//   *ПодотчетноеЛицо       - СправочникСсылка.ФизическиеЛица - не обязательный параметр,
//                                                   если не задан, то будут получены остатки по всем подотчетным лицам.
//   *Валюта                - СправочникСсылка.Валюты         - не обязательный параметр,
//                                                             если не задан, то будут получены остатки по всем валютам.
//   *ЦельВыдачи            - СправочникСсылка.СтатьиДвиженияДенежныхСредств - не обязательный параметр,
//                                                               если не задан, то будут получены остатки оп всем целям.
//
Процедура ПересчитатьСуммыРеглИУпр(РедактируемаяСтрока, ПараметрыПолученияОстатков) Экспорт
	
	ВыборкаОстатков = ВыборкаОстатков(ПараметрыПолученияОстатков);
	Если ВыборкаОстатков.Следующий() Тогда
		
		Если Не ВыборкаОстатков.Сумма = 0 Тогда
			
			РедактируемаяСтрока.СуммаРегл = ВыборкаОстатков.СуммаРегл * РедактируемаяСтрока.Сумма / ВыборкаОстатков.Сумма;
			РедактируемаяСтрока.СуммаУпр  = ВыборкаОстатков.СуммаУпр * РедактируемаяСтрока.Сумма / ВыборкаОстатков.Сумма;
			
		КонецЕсли;
		
	Иначе
			
		Коэффициенты = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентыПересчетаВалюты(РедактируемаяСтрока.Валюта,
			                                                                     РедактируемаяСтрока.Валюта,
			                                                                     ПараметрыПолученияОстатков.Дата,
			                                                                     ПараметрыПолученияОстатков.Организация);
			
		РедактируемаяСтрока.СуммаРегл = РедактируемаяСтрока.Сумма * Коэффициенты.КоэффициентПересчетаВВалютуРегл;
		РедактируемаяСтрока.СуммаУпр  = РедактируемаяСтрока.Сумма * Коэффициенты.КоэффициентПересчетаВВалютуРегл;
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	ИсправлениеДокументов.ДобавитьКомандуСторно(КомандыСозданияНаОсновании, ПустаяСсылка().Метаданные());
	ИсправлениеДокументов.ДобавитьКомандуИсправление(КомандыСозданияНаОсновании, ПустаяСсылка().Метаданные());
	
КонецПроцедуры

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Параметры:
//  Запрос - Запрос
//  ТекстыЗапроса - СписокЗначений из Строка.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса) Экспорт
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы("ИдентификаторСтроки");
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

// Процедура формирует текст для запроса для регистра сведений СуммыДокументовВВалютахУчета
//
// Параметры:
//  Запрос - Запрос
//  ТекстыЗапроса - СписокЗначений из Строка
//  Регистры - Структура, Строка, Неопределено - список имен регистров, для которых требуется формирование таблиц.
//
Процедура СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Регистры = Неопределено) Экспорт
	
	ИмяРегистра = "СуммыДокументовВВалютахУчета";
	
	Если Регистры <> Неопределено И Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса("СуммыДокументовВВалютахУчета", ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтСуммыДокументовВВалютахУчета(Запрос, ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица СуммыДокументовВВалютахУчета
	|"+
	"ВЫБРАТЬ
	|	*
	|ИЗ
	|	ВтСуммыДокументовВВалютахУчета";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

// Функция возвращает адаптированный текст запроса
//
// Параметры:
//  ИмяРегистра - Строка - имя регистра для которого формируется адаптированный текст запроса
//
// Возвращаемое значение:
//  Строка
Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос();
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.КорректировкаРасчетовСПодотчетнымиЛицами";
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента, Истина, ПереопределениеРасчетаПараметров);
	Иначе
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента);
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметровПроведения();
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Заполняет текст ограничения доступа
//
// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Подразделение)
	|	И ЗначениеРазрешено(КорректировкаЗадолженности.ПодотчетноеЛицо, NULL КАК ИСТИНА)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Ссылка");
	Поля.Добавить("Номер");
	Поля.Добавить("Дата");
	Поля.Добавить("ХозяйственнаяОперация");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 %2 от %3';
			|en = '%1 %2 dated %3'"),
		Данные.ХозяйственнаяОперация,
		Данные.Номер,
		Данные.Дата);
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	НастройкиХозяйственныхОпераций.Ссылка КАК НастройкаХозяйственнойОперации,
	|	ДанныеДокумента.Исправление КАК Исправление,
	|	ДанныеДокумента.СторнируемыйДокумент КАК СторнируемыйДокумент,
	|	ДанныеДокумента.ИсправляемыйДокумент КАК ИсправляемыйДокумент,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Автор КАК Автор,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Документ.КорректировкаРасчетовСПодотчетнымиЛицами КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
	|		ПО ДанныеДокумента.ХозяйственнаяОперация = НастройкиХозяйственныхОпераций.ХозяйственнаяОперация
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)
	
	Значения = Новый Структура;
	Значения.Вставить("ИдентификаторМетаданных",
	              ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.КорректировкаРасчетовСПодотчетнымиЛицами"));
	Значения.Вставить("КонтролироватьВыдачуПодОтчетВРазрезеЦелей",
	                  ПолучитьФункциональнуюОпцию("КонтролироватьВыдачуПодОтчетВРазрезеЦелей"));
	Значения.Вставить("ИспользоватьУчетПрочихДоходовРасходов",
	                  ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов"));
	Значения.Вставить("ИспользоватьУчетПрочихАктивовПассивов",
	                  ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихАктивовПассивов"));
	
	Если Не Реквизиты = Неопределено Тогда
		Значения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли;
	
	Возврат Значения;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтСуммыДокументовВВалютахУчета(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "СуммыДокументовВВалютеРегл";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка КАК Ссылка,
	|	&Период КАК Период,
	|	ТаблицаДокумента.Ссылка.Организация КАК Организация,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.Валюта КАК Валюта,
	|	ТаблицаДокумента.Валюта КАК ВалютаДокумента,
	|	ТаблицаДокумента.Валюта КАК ВалютаВзаиморасчетов,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
	|	НЕОПРЕДЕЛЕНО КАК ПериодБазыНДС,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектРасчетов,
	|	ЛОЖЬ КАК ОтражаетсяВРасчетах,
	|	0 КАК СуммаНДСВзаиморасчетов,
	|	0 КАК СуммаНДС,
	|	0 КАК СуммаНДСРегл,
	|	0 КАК СуммаНДСУпр,
	|	ТаблицаДокумента.Сумма КАК СуммаВзаиморасчетов,
	|	ТаблицаДокумента.Сумма КАК СуммаСНДС,
	|	ТаблицаДокумента.Сумма КАК СуммаБезНДС,
	|	ТаблицаДокумента.СуммаРегл КАК СуммаБезНДСРегл,
	|	ТаблицаДокумента.СуммаУпр КАК СуммаБезНДСУпр
	|ПОМЕСТИТЬ ВтСуммыДокументовВВалютахУчета
	|ИЗ
	|	Документ.КорректировкаРасчетовСПодотчетнымиЛицами.КорректировкаЗадолженности КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции // ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл()

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка КАК Ссылка,
	|	&Период КАК ДатаДокументаИБ,
	|	&Номер КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных КАК ТипСсылки,
	|	&Организация КАК Организация,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК Партнер,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	&Подразделение КАК МестоХранения,
	|	&Подразделение КАК Подразделение,
	|	&Автор КАК Ответственный,
	|	&Автор КАК Автор,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК Сумма,
	|	&Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ЛОЖЬ КАК ДополнительнаяЗапись,
	|	НЕОПРЕДЕЛЕНО КАК РазделительЗаписи,
	|	"""" КАК Дополнительно,
	|	&Период КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать КАК НомерПервичногоДокумента,
	|	&Исправление КАК СторноИсправление,
	|	&СторнируемыйДокумент КАК СторнируемыйДокумент,
	|	&ИсправляемыйДокумент КАК ИсправляемыйДокумент,
	|	&Период КАК ДатаОтраженияВУчете
	|ИЗ
	|	Документ.КорректировкаРасчетовСПодотчетнымиЛицами КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваУПодотчетныхЛиц(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДенежныеСредстваУПодотчетныхЛиц";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация =
	|				  ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтнесениеНаДоходыЗадолженностиПередПодотчетнымЛицом)
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ТаблицаДокумента.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	&Подразделение КАК Подразделение,
	|	ТаблицаДокумента.Валюта КАК Валюта,
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		ТаблицаДокумента.ЦельВыдачи
	|	КОНЕЦ КАК ЦельВыдачи,
	|	ТаблицаДокумента.Сумма КАК Сумма,
	|	ТаблицаДокумента.СуммаУпр КАК СуммаУпр,
	|	ТаблицаДокумента.СуммаРегл КАК СуммаРегл,
	|	ТаблицаДокумента.Сумма КАК КОтчету,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ВЫБОР КОГДА &КонтролироватьВыдачуПодОтчетВРазрезеЦелей ТОГДА
	|		ТаблицаДокумента.ЦельВыдачи
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	Документ.КорректировкаРасчетовСПодотчетнымиЛицами.КорректировкаЗадолженности КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы("ИдентификаторСтроки");
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ДанныеДокумента.Статья КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	&Подразделение КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	ДанныеДокумента.СуммаУпр КАК СуммаСНДС,
	|	ДанныеДокумента.СуммаУпр КАК СуммаБезНДС,
	|	ДанныеДокумента.СуммаУпр КАК СуммаБезНДСУпр,
	|	ДанныеДокумента.СуммаРегл КАК СуммаСНДСРегл,
	|	ДанныеДокумента.СуммаРегл КАК СуммаБезНДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	&ХозяйственнаяОперация ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	ДанныеДокумента.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	""""  КАК ИдентификаторСтроки,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ПОМЕСТИТЬ ВтИсходныеПрочиеРасходы
	|ИЗ
	|	Документ.КорректировкаРасчетовСПодотчетнымиЛицами.КорректировкаЗадолженности КАК ДанныеДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО ДанныеДокумента.Статья = СтатьиРасходов.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = Значение(Перечисление.ХозяйственныеОперации.СписаниеЗадолженностиПодотчетногоЛица)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация =
	|				  ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтнесениеНаДоходыЗадолженностиПередПодотчетнымЛицом)
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Статья КАК Статья,
	|	ДанныеДокумента.АналитикаАктивовПассивов КАК Аналитика,
	|	ДанныеДокумента.СуммаУпр КАК Сумма
	|ИЗ
	|	Документ.КорректировкаРасчетовСПодотчетнымиЛицами.КорректировкаЗадолженности КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.Статья ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов";
	
	МассивТекстов = Новый Массив;
	МассивТекстов.Добавить(ТекстЗапроса);
	МассивТекстов.Добавить(РегистрыНакопления.ПрочиеАктивыПассивы.ТекстЗапросаТаблицаПрочиеАктивыПассивы(Ложь));
	
	ТекстыЗапроса.Добавить(СтрСоединить(МассивТекстов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении()), ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеДоходы";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Статья КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов КАК АналитикаДоходов,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.СуммаУпр КАК Сумма,
	|	ДанныеДокумента.СуммаУпр КАК СуммаУпр,
	|	ДанныеДокумента.СуммаРегл КАК СуммаРегл,
	|	ДанныеДокумента.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	Документ.КорректировкаРасчетовСПодотчетнымиЛицами.КорректировкаЗадолженности КАК ДанныеДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиДоходов КАК СтатьиДоходов
	|	ПО ДанныеДокумента.Статья = СтатьиДоходов.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И &ИспользоватьУчетПрочихДоходовРасходов
	|	И ДанныеДокумента.Статья <> НЕОПРЕДЕЛЕНО
	|	И &ХозяйственнаяОперация =
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтнесениеНаДоходыЗадолженностиПередПодотчетнымЛицом)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеДокумента.НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Статья КАК Статья,
	|	ДанныеДокумента.АналитикаАктивовПассивов КАК Аналитика,
	//++ НЕ УТ
	|	ДанныеДокумента.НастройкаСчетовУчета КАК НастройкаСчетовУчета,
	//-- НЕ УТ
	
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация =
	|				  ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеЗадолженностиПодотчетногоЛица)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит)
	|	КОНЕЦ КАК ДебетКредит,
	|
	|	ДанныеДокумента.СуммаРегл КАК СуммаРегл,
	|	ДанныеДокумента.СуммаУпр КАК СуммаУпр,
	|	ДанныеДокумента.СуммаУпр КАК СуммаСНДС,
	|	ДанныеДокумента.СуммаУпр КАК СуммаБезНДС,
	|	
	|	ДанныеДокумента.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	Документ.КорректировкаРасчетовСПодотчетнымиЛицами.КорректировкаЗадолженности КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.Статья ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|	И ДанныеДокумента.Статья <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли