///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Формирует новый документ и заполняет его.
//
// Параметры:
//  ОснованиеПлатежа - ОпределяемыйТип.ДокументОперацииСБП - документ основание
//  СуммаОплаты - Число - сумм оплаты;
//  СуммаНДС - Число - рассчитанная сумма НДС;
//  ОблагаетсяНДС - Булево - признак использования НДС в платежной ссылке.
//
// Возвращаемое значение:
//  Структура - результат создания документа:
//   * ДокументОбъект.ПлатежнаяСсылкаСБП - созданный документ.
//
Функция НовыйДокументПлатежнаяСсылка(
		ОснованиеПлатежа,
		СуммаОплаты,
		СуммаНДС,
		ОблагаетсяНДС) Экспорт
	
	НовыйПлатежнаяСсылкаСБП = ПолучитьСсылку(Новый УникальныйИдентификатор);
	
	ПлатежнаяСсылкаСБП = СоздатьДокумент();
	ПлатежнаяСсылкаСБП.УстановитьСсылкуНового(НовыйПлатежнаяСсылкаСБП);
	
	ПлатежнаяСсылкаСБП.ОснованиеПлатежа = ОснованиеПлатежа;
	ПлатежнаяСсылкаСБП.Сумма = СуммаОплаты;
	ПлатежнаяСсылкаСБП.СуммаНДС = СуммаНДС;
	ПлатежнаяСсылкаСБП.ОблагаетсяНДС = ОблагаетсяНДС;
	ПлатежнаяСсылкаСБП.Дата = ТекущаяДатаСеанса();
	
	ДанныеДокумента = Новый Структура;
	ДанныеДокумента.Вставить("ПлатежнаяСсылкаСБП", ПлатежнаяСсылкаСБП);
	ДанныеДокумента.Вставить("НовыйПлатежнаяСсылкаСБП", НовыйПлатежнаяСсылкаСБП);
	
	Возврат ДанныеДокумента;
	
КонецФункции

// Возвращает параметры оплаты частичной оплаты.
//
// Параметры:
//  ПлатежнаяСсылкаСБП - Документ.ПлатежнаяСсылкаСБП - документ частичной оплаты;
//
// Возвращаемое значение:
//  Структура - данные оплат по документу в Системе быстрых платежей:
//    * ДокументОснование - ОпределяемыйТип.ДокументОперацииСБП - документ,
//      который отражает продажу в информационной базе;
//
Функция ПараметрыОплатыПлатежнойСсылки(ПлатежнаяСсылкаСБП) Экспорт
	
	ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		ПлатежнаяСсылкаСБП,
		"ОснованиеПлатежа");
	
	ПараметрыОплаты = Новый Структура;
	ПараметрыОплаты.Вставить("ДокументОснование", ДокументОснование);
	
	Возврат ПараметрыОплаты;
	
КонецФункции

// Возвращает данные частичных оплат по документу основанию платежа.
//
// Параметры:
//  ОснованиеПлатежа - ОпределяемыйТип.ДокументОперацииСБП - документ, который отражает
//    продажу в информационной базе;
//
// Возвращаемое значение:
//  Массив Из Структура - данные частичных оплат по документу основания в Системе быстрых платежей:
//    * ПлатежнаяСсылкаСБП - Документ.ПлатежнаяСсылкаСБП - документ частичной оплаты;
//    * ОснованиеПлатежа - Документ.ПлатежнаяСсылкаСБП - основание для формирования платежной ссылки;
//    * Дата - Дата - Дата создания частичной оплаты;
//    * Сумма - Число - Сумма частичной оплаты;
//    * СтатусОперации - Строка - идентификатор статуса операции;
//    * НастройкаПодключения -  СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей -
//       настройка выполнения операции СБП;
//    * ПредставлениеОперации - Строка - Представление частичной оплаты (содержит дату и сумму операции);
//    * ДатаОперации - Дата - фактическая дата оплаты в UTC;
//    * ИдентификаторОперации - Строка - идентификатор выполненной операции;
//    * ИдентификаторОплаты - Строка - идентификатор оплаты.
//
Функция ПолучитьДанныеЧастичныхОплат(ОснованиеПлатежа) Экспорт
	
	ЧастиЗапроса = Новый Массив;
	ШаблонЗапроса = "ВЫБРАТЬ
		|	ПлатежнаяСсылкаСБП.Ссылка КАК ПлатежнаяСсылкаСБП,
		|	ПлатежнаяСсылкаСБП.ОснованиеПлатежа КАК ОснованиеПлатежа,
		|	ПлатежнаяСсылкаСБП.Дата КАК ДатаОплаты,
		|	ПлатежнаяСсылкаСБП.Сумма КАК СуммаОплаты,
		|	ПлатежнаяСсылкаСБП.СуммаНДС КАК СуммаНДС,
		|	ПлатежнаяСсылкаСБП.ОблагаетсяНДС КАК ОблагаетсяНДС,
		|	ЕСТЬNULL(ИдентификаторыОперацийСБП.СтатусОперации, """") КАК СтатусОперации,
		|	ИдентификаторыОперацийСБП.НастройкаПодключения КАК НастройкаПодключения,
		|	ИдентификаторыОперацийСБП.ИдентификаторОплаты КАК ИдентификаторОплаты,
		|	ИдентификаторыОперацийСБП.ИдентификаторОперации КАК ИдентификаторОперации,
		|	ИдентификаторыОперацийСБП.ДатаОперации КАК ДатаОперации
		|ИЗ
		|	Документ.ПлатежнаяСсылкаСБП КАК ПлатежнаяСсылкаСБП
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.%1 КАК ИдентификаторыОперацийСБП
		|		ПО ПлатежнаяСсылкаСБП.Ссылка = ИдентификаторыОперацийСБП.ДокументОперации
		|ГДЕ
		|	ПлатежнаяСсылкаСБП.ОснованиеПлатежа = &ОснованиеПлатежа";
	
	СистемаБыстрыхПлатежейСобытия.ПриОпределенииЗапросаДанныхЧастичныхОплат(
		ЧастиЗапроса,
		ШаблонЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ЧастиЗапроса[0];
	Сч = 1;
	Пока Сч < ЧастиЗапроса.Количество() Цикл
		Запрос.Текст = Запрос.Текст
			+ Символы.ПС
			+ "ОБЪЕДИНИТЬ ВСЕ"
			+ Символы.ПС
			+ ЧастиЗапроса[Сч];
		Сч = Сч + 1;
	КонецЦикла;
	
	Запрос.УстановитьПараметр(
		"ОснованиеПлатежа",
		ОснованиеПлатежа);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ЧастичныеОплаты = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		ОписаниеОплаты = Новый Структура;
		ОписаниеОплаты.Вставить("ПлатежнаяСсылкаСБП", Выборка.ПлатежнаяСсылкаСБП);
		ОписаниеОплаты.Вставить("ДатаОплаты", Выборка.ДатаОплаты);
		ОписаниеОплаты.Вставить("СуммаОплаты", Выборка.СуммаОплаты);
		ОписаниеОплаты.Вставить("ОснованиеПлатежа", Выборка.ОснованиеПлатежа);
		ОписаниеОплаты.Вставить("СуммаНДС", Выборка.СуммаНДС);
		ОписаниеОплаты.Вставить("ОблагаетсяНДС", Выборка.ОблагаетсяНДС);
		ОписаниеОплаты.Вставить("СтатусОперации", Выборка.СтатусОперации);
		ОписаниеОплаты.Вставить("НастройкаПодключения", Выборка.НастройкаПодключения);
		ОписаниеОплаты.Вставить("ДатаОперации", Выборка.ДатаОперации);
		ОписаниеОплаты.Вставить("ИдентификаторОперации", Выборка.ИдентификаторОперации);
		ОписаниеОплаты.Вставить("ИдентификаторОплаты", Выборка.ИдентификаторОплаты);
		ОписаниеОплаты.Вставить(
			"ПредставлениеОперации",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Оплата от %1 на сумму %2 руб.';
					|en = 'Payment dated %1 in the amount of %2 rub.'"),
				Выборка.ДатаОплаты,
				Выборка.СуммаОплаты));
		ЧастичныеОплаты.Добавить(ОписаниеОплаты);
	КонецЦикла;
	
	Возврат ЧастичныеОплаты;
	
КонецФункции

#КонецОбласти

#КонецЕсли