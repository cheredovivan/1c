#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.РаботаСФайлами
	ДобавляемыеЭлементы = Новый Массив;
	ПараметрыПоля = РаботаСФайлами.ПолеФайла();
	ПараметрыПоля.ПутьКДанным = "ПрисоединенныйФайл1";
	ПараметрыПоля.Размещение = "ГруппаПредпросмотра1";
	ПараметрыПоля.ВыводитьЗаголовокФайла = Истина;
	ПараметрыПоля.ПоказыватьКоманднуюПанель = Ложь;
	ПараметрыПоля.ДобавлятьФайлы = Ложь;
	ПараметрыПоля.ВыбиратьФайл = Ложь;
	ПараметрыПоля.РедактироватьФайл = Ложь;
	ПараметрыПоля.ОчищатьФайл = Ложь;
	
	ДобавляемыеЭлементы.Добавить(ПараметрыПоля);
	ПараметрыПоля = РаботаСФайлами.ПолеФайла();
	ПараметрыПоля.ПутьКДанным = "ПрисоединенныйФайл2";
	ПараметрыПоля.Размещение = "ГруппаПредпросмотра2";
	ПараметрыПоля.ВыводитьЗаголовокФайла = Истина;
	ПараметрыПоля.ПоказыватьКоманднуюПанель = Ложь;
	ПараметрыПоля.ДобавлятьФайлы = Ложь;
	ПараметрыПоля.ВыбиратьФайл = Ложь;
	ПараметрыПоля.РедактироватьФайл = Ложь;
	ПараметрыПоля.ОчищатьФайл = Ложь;
	ДобавляемыеЭлементы.Добавить(ПараметрыПоля);
	
	ПараметрыПоля = РаботаСФайлами.ПолеФайла();
	ПараметрыПоля.ПутьКДанным = "ПрисоединенныйФайл3";
	ПараметрыПоля.Размещение = "ГруппаПредпросмотра3";
	ПараметрыПоля.ВыводитьЗаголовокФайла = Истина;
	ПараметрыПоля.ПоказыватьКоманднуюПанель = Ложь;
	ПараметрыПоля.ДобавлятьФайлы = Ложь;
	ПараметрыПоля.ВыбиратьФайл = Ложь;
	ПараметрыПоля.РедактироватьФайл = Ложь;
	ПараметрыПоля.ОчищатьФайл = Ложь;
	ДобавляемыеЭлементы.Добавить(ПараметрыПоля);
	
	РаботаСФайлами.ПриСозданииНаСервере(ЭтотОбъект, ДобавляемыеЭлементы);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства

	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПриСозданииНаСервереФормыОбъекта(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	// Конец БлокировкаИзмененияОбъектов
		
	// ЭДОБЗК
	ЭДОБЗК.ПриСозданииНаСервереФормыОбъекта(ЭтотОбъект, Отказ, СтандартнаяОбработка, Объект);
	// Конец ЭДОБЗК
	
	Если (Не Параметры.Свойство("Ключ") Или Параметры.Ключ.Пустая()) Тогда
		ОбновитьОтображениеЭлементовФормы();
		Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизацийЗарплатаКадрыБазовая") Тогда
			Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
			ПодписиДокументов.ЗаполнитьПодписиПоОрганизации(ЭтотОбъект);
		КонецЕсли;
		ДанныеВРеквизит();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	// СтандартныеПодсистемы.Свойства
    УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
	    ОбновитьЭлементыДополнительныхРеквизитов();
	    УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства

	// ЭДОБЗК
	ЭДОБЗККлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ЭДОБЗК
	
	Если ИмяСобытия = "ЗаписьПечатныхФормСЭЦП" Тогда
		Прочитать();
	КонецЕсли;
	
	Элементы["БлокировкаИзмененияОбъектаРасшифровкиГруппа_1_Надпись"].Заголовок = ТекстПричиныБлокировки();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПриЧтенииНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект,
		ПравоДоступа("Изменение", Метаданные.Документы.СогласиеНаПрисоединениеКЭДОБЗК));
	// Конец БлокировкаИзмененияОбъектов
			
	// ЭДОБЗК
	ЭДОБЗК.ПриЧтенииНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, Объект);
	// Конец ЭДОБЗК
	
	ДанныеВРеквизит();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ОсновнойСотрудник = КадровыйУчет.ОсновнойСотрудникФизическогоЛица(Объект.ФизическоеЛицо, Объект.Организация, Объект.Дата);
	Если Не ЗначениеЗаполнено(ОсновнойСотрудник) Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Данный сотрудник не работает в выбранной организации';
													|en = 'This employee does not work in the selected company'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ПослеЗаписиНаСервере(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПослеЗаписиНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец БлокировкаИзмененияОбъектов
	
	// ЭДОБЗК
	ЭДОБЗК.ПослеЗаписиНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи, Объект);
	// Конец ЭДОБЗК
	
	ДанныеВРеквизит();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
		
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОрганизацияПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ДатаПриИзмененииНаСервере();
КонецПроцедуры

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраНажатие(Элемент, СтандартнаяОбработка)
    
    РаботаСФайламиКлиент.ПолеПредпросмотраНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
    
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
    
    РаботаСФайламиКлиент.ПолеПредпросмотраПеретаскивание(ЭтотОбъект, Элемент,
                ПараметрыПеретаскивания, СтандартнаяОбработка);
    
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
    
    РаботаСФайламиКлиент.ПолеПредпросмотраПроверкаПеретаскивания(ЭтотОбъект, Элемент,
                ПараметрыПеретаскивания, СтандартнаяОбработка);
    
КонецПроцедуры

// Конец СтандартныеПодсистемы.РаботаСФайлами

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	ДатаНачалаПриИзмененииНаСервереБезКонтекста(Объект.Ссылка, ДатаНачала)	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_КомандаПанелиПрисоединенныхФайлов(Команда)
    РаботаСФайламиКлиент.КомандаУправленияПрисоединеннымиФайлами(ЭтотОбъект, Команда);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

// БлокировкаИзмененияОбъектов
&НаКлиенте
Процедура Подключаемый_РазблокироватьФормуОбъекта(Команда)
	БлокировкаИзмененияОбъектовКлиент.РазблокироватьФормуОбъекта(ЭтотОбъект, Объект.Ссылка);
КонецПроцедуры
// Конец БлокировкаИзмененияОбъектов

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СерверныеОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ЗаполнитьПодписиПоОрганизации(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ЗаполнитьПодписиПоОрганизации(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДатаНачалаПриИзмененииНаСервереБезКонтекста(Ссылка, ДатаНачала)
	РегистрыСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК.УстановитьДатуНачалаДействия(Ссылка, ДатаНачала);
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ОбновитьОтображениеЭлементовФормы()
	
	Элементы.ДекорацияПодписанныйДокумент.Заголовок = ДекорацияПодписанныйДокументЗаголовок(ДатаОкончания);
	
	Если Элементы.Найти("РазблокироватьФормуДляИзмененияОбъекта") <> Неопределено Тогда
		Элементы["БлокировкаИзмененияОбъектаРасшифровкиГруппа_1_Надпись"].Заголовок = ТекстПричиныБлокировки();
	КонецЕсли;
	
	ОбновитьПодключаемыеКоманды(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ДекорацияПодписанныйДокументЗаголовок(ДатаОкончания)
	Если ЗначениеЗаполнено(ДатаОкончания) Тогда
		Если ДатаОкончания >= ТекущаяДатаСеанса() Тогда
			Возврат СтрШаблон(НСтр("ru = 'Документ действует до %1';
									|en = 'Document is valid until %1'"), Формат(ДатаОкончания, "ДЛФ=DD"));	
		Иначе
			Возврат СтрШаблон(НСтр("ru = 'Документ прекратил свое действие %1';
									|en = 'Document expired on %1'"), Формат(ДатаОкончания, "ДЛФ=DD"));
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(БЗК_Состояние) Тогда
		Если БЗК_СостояниеПриема = Перечисления.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.Подтверждено Тогда
			Возврат НСтр("ru = 'Документ подписан сотрудником';
						|en = 'The document is signed by the employee'");
		ИначеЕсли БЗК_СостояниеПриема = Перечисления.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.ПустаяСсылка() Тогда
			Возврат НСтр("ru = 'Ожидается бумажный документ с подписью';
						|en = 'Awaiting the signed paper document'");
		ИначеЕсли БЗК_СостояниеПриема = Перечисления.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.Аннулировано Тогда
			Возврат НСтр("ru = 'Аннулирован';
						|en = 'Canceled'");
		КонецЕсли;
	ИначеЕсли Не ЗначениеЗаполнено(БЗК_Состояние) И Не Объект.Ссылка.Пустая() Тогда           
		Возврат НСтр("ru = 'Ожидается ответ от сотрудника';
					|en = 'Awaiting the employee response'");
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстПричиныБлокировки()
	Возврат НСтр("ru = 'Данное согласие отправлено в 1С:Кабинет сотрудника и его изменение может привести к ошибкам.
						|Если в документе имеются ошибки, разблокируйте документ и пометьте его на удаление.
						|После удаления, сформируйте новый документ согласия.';
						|en = 'This consent is sent to 1C:Employee Account, changing it might cause errors.
						|If the document contains errors, unlock the document and mark it for deletion.
						|After deletion, generate a new consent document.'");	
КонецФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ОбновитьПодключаемыеКоманды(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// ЗарплатаКадрыПодсистемы.ПодписиДокументов
&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементПриИзменении(Элемент)
	ПодписиДокументовКлиент.ПриИзмененииПодписывающегоЛица(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементНажатие(Элемент)
	ПодписиДокументовКлиент.РасширеннаяПодсказкаНажатие(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры
// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов

// ЭДОБЗК
&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПодключаемыеКоманды(УправляемаяФорма)
	ЭДОБЗККлиентСервер.ОбновитьКоманды(УправляемаяФорма, УправляемаяФорма.Объект, Истина);
КонецПроцедуры
// Конец ЭДОБЗК

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура ДанныеВРеквизит()
	
	ЗаполнитьДокументЭДОБЗКСогласия();
	
	МенеджерЗаписиСостояниеСогласияНаПрисоединениеКЭДОБЗК = РегистрыСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК.СоздатьМенеджерЗаписи();
	МенеджерЗаписиСостояниеСогласияНаПрисоединениеКЭДОБЗК.Ссылка = Объект.Ссылка;
	МенеджерЗаписиСостояниеСогласияНаПрисоединениеКЭДОБЗК.Прочитать();
	
	БЗК_Состояние = МенеджерЗаписиСостояниеСогласияНаПрисоединениеКЭДОБЗК.Состояние;
	БЗК_СостояниеПриема = МенеджерЗаписиСостояниеСогласияНаПрисоединениеКЭДОБЗК.СостояниеПриема;
	ДатаНачала = МенеджерЗаписиСостояниеСогласияНаПрисоединениеКЭДОБЗК.ДатаНачала;
	ДатаОкончания = МенеджерЗаписиСостояниеСогласияНаПрисоединениеКЭДОБЗК.ДатаОкончания;
	
	ОбновитьОтображениеЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДокументЭДОБЗКСогласия()
	
	ПоказыватьЭлектронныйДокументСоШтампами = Ложь;
	ДанныеДокументаЭДОБЗК = ЭДОБЗК.ДокументЭДОБЗКСогласияНаПрисоединениеКЭДОБЗК(Объект.Ссылка);
	Если ДанныеДокументаЭДОБЗК <> Неопределено Тогда
		
		ДокументЭДОБЗК = ДанныеДокументаЭДОБЗК.ДокументЭДОБЗК;
		ЭлектронныйДокумент = ДанныеДокументаЭДОБЗК.ЭлектронныйДокумент;
		Если ЗначениеЗаполнено(ЭлектронныйДокумент) Тогда
			УстановленныеПодписи = ЭлектроннаяПодписьЭДОБЗК.УстановленныеПодписи(ЭлектронныйДокумент);
			Если ЗначениеЗаполнено(УстановленныеПодписи) Тогда
				ПечатнаяФормаСоШтампами = ЭДОБЗК.ПечатнаяФормаСоШтампамиПоФайлуПечатнойФормы(
					ЭлектронныйДокумент, УстановленныеПодписи);
				Если ПечатнаяФормаСоШтампами <> Неопределено Тогда
					Поток = Новый ПотокВПамяти();
					ПечатнаяФормаСоШтампами.Записать(Поток, ТипФайлаТабличногоДокумента.PDF_A_1);
					ЭлектронныйДокументСоШтампами.Прочитать(Поток);
					ПоказыватьЭлектронныйДокументСоШтампами = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаЭлектронногоДокумента",
		"Видимость",
		ПоказыватьЭлектронныйДокументСоШтампами);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДокументЭДОБЗКВШапке",
		"Видимость",
		Не ПоказыватьЭлектронныйДокументСоШтампами);
	
	Файлы = Новый Массив;
	РаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту(Объект.Ссылка, Файлы);
	РеквизитыФайлов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Файлы, "ПометкаУдаления,Расширение");
	
	Файлы = Новый Массив;
	Для Каждого РеквизитыФайла Из РеквизитыФайлов Цикл
		Если РеквизитыФайла.Ключ = ЭлектронныйДокумент Тогда
			Продолжить;
		КонецЕсли;
		Если ЭДОБЗККлиентСервер.ЭтоРасширениеИзображения(РеквизитыФайла.Значение.Расширение) Тогда
			Если РеквизитыФайла.Значение.ПометкаУдаления Тогда
				Продолжить;
			КонецЕсли;
			Файлы.Добавить(РеквизитыФайла.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Счетчик = 1;
	Для Каждого Файл Из Файлы Цикл
		ЭтотОбъект[СтрШаблон("ПрисоединенныйФайл%1", Счетчик)] = Файл;
		Счетчик = Счетчик + 1;
		Если Счетчик = 4 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаПредпросмотра1",
		"Видимость",
		Файлы.Количество() >= 1);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаПредпросмотра2",
		"Видимость",
		Файлы.Количество() >= 2);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаПредпросмотра3",
		"Видимость",
		Файлы.Количество() >= 3);
	
	ВидимостьПредупреждения = Файлы.Количество() > 3;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПрисоединенныеФайлыНадпись",
		"Видимость",
		ВидимостьПредупреждения);
	
	Если ВидимостьПредупреждения Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ПрисоединенныеФайлыНадпись",
			"Заголовок",
			СтрШаблон(НСтр("ru = '%1 можно просмотреть по команде ""Присоединенные файлы""';
							|en = 'You can view %1 by clicking ""Attachments""'"),
			СтрокаСЧислом(
				НСтр("ru = ';Еще %1 файл;;Приложено %1 файла;Еще %1 файлов;Еще файлы';
					|en = ';%1 more file;;;;%1 more files'"),
				Файлы.Количество() - 3, ВидЧисловогоЗначения.Количественное)));
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаПредпросмотра",
		"Видимость",
		Файлы.Количество() > 0 Или ПоказыватьЭлектронныйДокументСоШтампами);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаПредпросмотра",
		"ОтображениеСтраниц",
		?(Файлы.Количество() = 1 И Не ПоказыватьЭлектронныйДокументСоШтампами
			Или Файлы.Количество() = 0 И ПоказыватьЭлектронныйДокументСоШтампами,
				ОтображениеСтраницФормы.Нет, ОтображениеСтраницФормы.Авто));
		
КонецПроцедуры

&НаКлиенте
Процедура ПрисоединенныеФайлыНадписьНажатие(Элемент)
	
	ПараметрыВыполненияКоманды = Новый Структура;
	ПараметрыВыполненияКоманды.Вставить("Источник", ЭтотОбъект);
	ПараметрыВыполненияКоманды.Вставить("Уникальность", Истина);
	ПараметрыВыполненияКоманды.Вставить("Окно", Окно);
	
	ЗарплатаКадрыКлиент.ОбработкаКомандыОткрытияПрисоединенныхФайлов(
		Объект.Ссылка, ПараметрыВыполненияКоманды);
	
КонецПроцедуры

#КонецОбласти
