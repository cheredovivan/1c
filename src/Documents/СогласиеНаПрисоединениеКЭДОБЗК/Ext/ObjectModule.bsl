#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
		
	Если ПометкаУдаления Тогда
		ЭДОБЗКВызовСервера.РазблокироватьФормуОбъекта(Ссылка);
	ИначеЕсли ЕстьДругоеСогласие() Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'По данной организации и сотруднику уже имеется актуальный документ согласия.';
													|en = 'The relevant consent document already exists for this company and employee.'"));
		Возврат;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Организация) Или Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПометкаУдаления Тогда
		СформироватьПечатнуюФормуСогласия();
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("НеВычислятьПрисоединениеКЭДОБЗК") Тогда
		
		ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
		РегистрыСведений.ФизическиеЛицаПрисоединенныеКЭДОБЗК.УстановитьПодключенКЭДОБЗКДляФизическогоЛица(ФизическиеЛица);
		
		МенеджерЗаписиСостояниеСогласия = РегистрыСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК.СоздатьМенеджерЗаписи();
		МенеджерЗаписиСостояниеСогласия.Ссылка = Ссылка;
		МенеджерЗаписиСостояниеСогласия.Прочитать();
		Если Не ЗначениеЗаполнено(МенеджерЗаписиСостояниеСогласия.ДатаНачала) Тогда
			МенеджерЗаписиСостояниеСогласия.Ссылка = Ссылка;
			МенеджерЗаписиСостояниеСогласия.ДатаНачала = Дата;
			МенеджерЗаписиСостояниеСогласия.Записать(Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Ссылка.Установить(Ссылка);
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЕстьДругоеСогласие()
	
	Запрос = Новый Запрос();
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СогласиеНаПрисоединениеКЭДОБЗК.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.СогласиеНаПрисоединениеКЭДОБЗК КАК СогласиеНаПрисоединениеКЭДОБЗК
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК КАК СостояниеСогласияНаПрисоединениеКЭДОБЗК
		|		ПО (СостояниеСогласияНаПрисоединениеКЭДОБЗК.Ссылка = СогласиеНаПрисоединениеКЭДОБЗК.Ссылка)
		|ГДЕ
		|	СогласиеНаПрисоединениеКЭДОБЗК.Проведен
		|	И СогласиеНаПрисоединениеКЭДОБЗК.Ссылка <> &Ссылка
		|	И СогласиеНаПрисоединениеКЭДОБЗК.ФизическоеЛицо = &ФизическоеЛицо
		|	И СогласиеНаПрисоединениеКЭДОБЗК.Организация = &Организация
		|	И ЕСТЬNULL(СостояниеСогласияНаПрисоединениеКЭДОБЗК.СостояниеПриема, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.Аннулировано)
		|	И (СостояниеСогласияНаПрисоединениеКЭДОБЗК.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ИЛИ СостояниеСогласияНаПрисоединениеКЭДОБЗК.ДатаОкончания ЕСТЬ NULL)";
	
	Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Процедура СформироватьПечатнуюФормуСогласия()
	
	Если Не ЭтоНовый() И ЗначениеЗаполнено(ДокументЭДОБЗКПечатнойФормы()) Тогда
		Возврат;			
	КонецЕсли;
	
	ПечатнаяФорма = Документы.СогласиеНаПрисоединениеКЭДОБЗК.ПечатьСогласияНаПрисоединениеКЭДОБЗК(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка),
		Новый СписокЗначений);
	ДанныеПечатнойФормы = ЭДОБЗКВызовСервера.ДобавитьПечатнуюФорму(
		ПечатнаяФорма,
		Ссылка,
		Документы.СогласиеНаПрисоединениеКЭДОБЗК.ИдентификаторКомандыПечатиЗаявления(),
		НазваниеПечатнойФормыСогласие(),
		Организация,
		ФизическоеЛицо,
		,
		Истина);
	
КонецПроцедуры

Функция ДокументЭДОБЗКПечатнойФормы()
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДокументЭДОБЗК.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
	               |ГДЕ
	               |	ДокументЭДОБЗК.ОснованиеДокумента = &ОснованиеДокумента
	               |	И ДокументЭДОБЗК.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("ОснованиеДокумента", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;	
	КонецЕсли;
	
	Возврат Неопределено;
		
КонецФункции

Функция НазваниеПечатнойФормыСогласие()
	
	ШаблонНазвания = НСтр("ru = '%1 Согласие на присоединение к ЭДО %2 %3';
							|en = '%1 Consent to join EDI %2%3'");
	ФИО = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФизическоеЛицо, "ФИО");
	ФамилияИнициалы = "";
	Попытка
		ФамилияИнициалы = ФизическиеЛицаЗарплатаКадрыКлиентСервер.ФамилияИнициалы(ФИО);
	Исключение
		ФамилияИнициалы = Строка(ФизическоеЛицо);
	КонецПопытки;
	
	Возврат СтрШаблон(ШаблонНазвания,
					  СокрЛП(ФамилияИнициалы),
					  СокрЛП(Строка(Номер)),
					  СокрЛП(Формат(Дата, "ДФ=yyyyMMdd")));
					  
КонецФункции

#КонецОбласти
	
#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли