#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Проводит документ по учетам. Если в параметре ВидыУчетов передано Неопределено, то документ проводится по всем учетам.
// Процедура вызывается из обработки проведения и может вызываться из вне.
// 
// Параметры:
//  ДокументСсылка	- ДокументСсылка.АктПроверкиСтраховыхВзносов
//  РежимПроведения - РежимПроведенияДокумента - Режим проведения документа (оперативный, неоперативный).
//  Отказ 			- Булево - Признак отказа от выполнения проведения.
//  ВидыУчетов 		- Строка - Список видов учета, по которым необходимо провести документ. Если параметр пустой или Неопределено, то документ проведется по всем учетам.
//  Движения 		- КоллекцияДвижений - Передается только при вызове из обработки проведения документа.
//  Объект			- ДокументОбъект.АктПроверкиСтраховыхВзносов - Передается только при вызове из обработки проведения документа.
//  ДополнительныеПараметры - Структура - Дополнительные параметры, необходимые для проведения документа.
//
Процедура ПровестиПоУчетам(ДокументСсылка, РежимПроведения, Отказ, ВидыУчетов = Неопределено, Движения = Неопределено, Объект = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	СтруктураВидовУчета = ПроведениеСервер.СтруктураВидовУчета();	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвиженийПоВидамУчета(РежимПроведения, ДокументСсылка, СтруктураВидовУчета, ВидыУчетов, Движения, Объект, Отказ);
	
	РеквизитыДляПроведения = РеквизитыДляПроведения(ДокументСсылка);
	ДанныеДляПроведения = ДанныеДляПроведения(РеквизитыДляПроведения, СтруктураВидовУчета);
	
	Если СтруктураВидовУчета.СтраховыеВзносы Тогда
		УчетСтраховыхВзносов.СформироватьДоходыСтраховыеВзносы(Движения, Отказ, РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.ПериодРегистрации, ДанныеДляПроведения.СведенияОДоходах, Истина, Истина);
		УчетСтраховыхВзносов.СформироватьИсчисленныеВзносы(Движения, Отказ, РеквизитыДляПроведения.Организация, КонецМесяца(РеквизитыДляПроведения.ПериодРегистрации), ДанныеДляПроведения.СтраховыеВзносы, , , Истина, РеквизитыДляПроведения.ОснованиеДляДоначисления);
		УчетСтраховыхВзносов.СформироватьСтраховыеВзносыПоФизическимЛицам(Движения, Отказ, РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.ПериодРегистрации, РеквизитыДляПроведения.Ссылка, ДанныеДляПроведения.СтраховыеВзносы);
	КонецЕсли;
	
	Если СтруктураВидовУчета.ОстальныеВидыУчета Или СтруктураВидовУчета.СтраховыеВзносы Тогда
		ДанныеДляПроведенияОтраженияЗарплатыВУчете = ОтражениеЗарплатыВУчете.НоваяСтруктураРезультатыРасчетаЗарплаты();
		ДанныеДляПроведенияОтраженияЗарплатыВУчете.СтраховыеВзносы = Движения.СтраховыеВзносыПоФизическимЛицам.Выгрузить();
		СтрокаСписокТаблиц = "НачисленнаяЗарплатаИВзносы";
		ОтражениеЗарплатыВБухучете.СформироватьДвиженияПоДокументу(Движения, Отказ, РеквизитыДляПроведения.Организация, НачалоМесяца(РеквизитыДляПроведения.ПериодРегистрации), ДанныеДляПроведенияОтраженияЗарплатыВУчете, СтрокаСписокТаблиц);
	КонецЕсли;

	ПроведениеСервер.ЗаписьДвиженийПоУчетам(Движения, СтруктураВидовУчета);
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ДляВсехСтрок( ЗначениеРазрешено(ФизическиеЛица.ФизическоеЛицо, NULL КАК ИСТИНА)
	|	) И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.АктПроверкиСтраховыхВзносов;
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РеквизитыДляПроведения(ДокументСсылка)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АктПроверкиСтраховыхВзносов.Ссылка КАК Ссылка,
		|	АктПроверкиСтраховыхВзносов.Организация КАК Организация,
		|	АктПроверкиСтраховыхВзносов.ПериодРегистрации КАК ПериодРегистрации,
		|	АктПроверкиСтраховыхВзносов.ОснованиеДляДоначисления КАК ОснованиеДляДоначисления
		|ИЗ
		|	Документ.АктПроверкиСтраховыхВзносов КАК АктПроверкиСтраховыхВзносов
		|ГДЕ
		|	АктПроверкиСтраховыхВзносов.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	РеквизитыДляПроведения = РеквизитыДляПроведенияПустаяСтруктура();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(РеквизитыДляПроведения, Выборка);
	КонецЦикла;
	
    Возврат РеквизитыДляПроведения;
	
КонецФункции

Функция ДанныеДляПроведения(РеквизитыДляПроведения, СтруктураВидовУчета)
	
	Если СтруктураВидовУчета.СтраховыеВзносы Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", РеквизитыДляПроведения.Ссылка);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеОДоходах.МесяцРасчетногоПериода КАК ДатаПолученияДохода,
		|	ДанныеОДоходах.ВидДохода КАК ВидДохода,
		|	СУММА(ДанныеОДоходах.Сумма) КАК Сумма,
		|	СУММА(ДанныеОДоходах.Скидка) КАК Скидка,
		|	ДанныеОДоходах.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ДанныеОДоходах.ОблагаетсяВзносамиНаДоплатуЛетчикам КАК ОблагаетсяВзносамиНаДоплатуЛетчикам,
		|	ДанныеОДоходах.ОблагаетсяВзносамиНаДоплатуШахтерам КАК ОблагаетсяВзносамиНаДоплатуШахтерам,
		|	ДанныеОДоходах.ЯвляетсяДоходомФармацевта КАК ЯвляетсяДоходомФармацевта,
		|	ДанныеОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ КАК ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	ДанныеОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией КАК ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	ДанныеОДоходах.КлассУсловийТруда КАК КлассУсловийТруда,
		|	ДанныеОДоходах.ЛьготныйТерриториальныйТариф КАК ЛьготныйТерриториальныйТариф
		|ИЗ
		|	Документ.АктПроверкиСтраховыхВзносов.СведенияОДоходах КАК ДанныеОДоходах
		|ГДЕ
		|	ДанныеОДоходах.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеОДоходах.ВидДохода,
		|	ДанныеОДоходах.ФизическоеЛицо,
		|	ДанныеОДоходах.ОблагаетсяЕНВД,
		|	ДанныеОДоходах.ОблагаетсяВзносамиНаДоплатуЛетчикам,
		|	ДанныеОДоходах.ОблагаетсяВзносамиНаДоплатуШахтерам,
		|	ДанныеОДоходах.ЯвляетсяДоходомФармацевта,
		|	ДанныеОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
		|	ДанныеОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
		|	ДанныеОДоходах.КлассУсловийТруда,
		|	ДанныеОДоходах.МесяцРасчетногоПериода,
		|	ДанныеОДоходах.ЛьготныйТерриториальныйТариф";
		
		ДанныеДляПроведения = Новый Структура;
		ДанныеДляПроведения.Вставить("СведенияОДоходах", Запрос.Выполнить().Выгрузить());
		ДанныеДляПроведения.Вставить("СтраховыеВзносы", УчетСтраховыхВзносов.ДанныеОВзносахИзДокумента(РеквизитыДляПроведения.Ссылка,,,,Истина, "МесяцРасчетногоПериода"));
		
	КонецЕсли;
	
	Если СтруктураВидовУчета.ОстальныеВидыУчета Тогда
	КонецЕсли;
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

Функция РеквизитыДляПроведенияПустаяСтруктура()
	
	РеквизитыДляПроведенияПустаяСтруктура = Новый Структура("Ссылка, ПериодРегистрации, Организация, ОснованиеДляДоначисления");	
	
	Возврат РеквизитыДляПроведенияПустаяСтруктура;
	
КонецФункции

#КонецОбласти

#КонецЕсли