#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ИнтеграцияИСМП.ЗапрещеноИспользованиеОбъектаВИСМП(Объект.Ссылка) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияИС.НастроитьВидимостьДокументаОснования(ЭтотОбъект);
	Элементы.ДокументОснование.ДоступныеТипы = Метаданные.ОпределяемыеТипы.ОснованиеРазрешениеНаОтгрузкуИСМП.Тип;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВерсионированиеОбъектов") Тогда
		МодульВерсионированиеОбъектов = ОбщегоНазначения.ОбщийМодуль("ВерсионированиеОбъектов");
		МодульВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыИС.ПриСозданииНаСервере(ЭтотОбъект);
	
	Если Объект.Ссылка.Пустая() Тогда
		ПриСозданииЧтенииНаСервере();
	КонецЕсли;
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриСозданииЧтенииНаСервере();
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыИС.ПриЧтенииНаСервере(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ЗадатьВопросПоВыполнениюПроверкиГИСМТ(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	Возврат;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Возврат;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	Если НовыйОбъект = Элементы.ДокументОснование.ДоступныеТипы.ПривестиЗначение(НовыйОбъект) Тогда
		Объект.ДокументОснование = НовыйОбъект;
		Модифицированность = Истина;
		Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ОбменДаннымиИСКлиентСервер.ИмяСобытияИзмененоСостояние(ОбщегоНазначенияИСМПКлиентСервер.ИмяПодсистемы())
		И Параметр.Ссылка = Объект.Ссылка Тогда
		
		Если Параметр.Свойство("ОбъектИзменен")
			И Параметр.ОбъектИзменен Тогда
			ОбновитьПредставленияНаФорме(Истина);
		Иначе
			ОбновитьПредставленияНаФорме(Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИмяСобытия = ОбменДаннымиИСКлиентСервер.ИмяСобытияВыполненОбмен(ОбщегоНазначенияИСМПКлиентСервер.ИмяПодсистемы())
	 И (Параметр = Неопределено
		Или (ТипЗнч(Параметр) = Тип("Структура") И Параметр.ОбновлятьСтатусВФормахДокументов)) Тогда
		
		ОбновитьПредставленияНаФорме(Истина);
		
	КонецЕсли;
	
	Если ИмяСобытия = СоответствиеТребованиямГИСМТКлиент.ИмяСобытияОповещенияПослеПроверкиДокументаНаСоответствиеТребованиямГИСМТ()
		И Параметр.Документ = Объект.Ссылка Тогда
		
		Если Не РезультатПроверкиГИСМТ = Неопределено Тогда
			
			ЗаполнитьЗначенияСвойств(РезультатПроверкиГИСМТ, Параметр.РезультатПроверки,, "ЭлектронныйДокумент");
			
		КонецЕсли;
		
		ЗадатьВопросПоВыполнениюПроверкиГИСМТ();
		
	КонецЕсли;
	
	ОбщегоНазначенияСобытияФормИСКлиентПереопределяемый.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СобытияФормИСКлиент.ОбработкаНавигационнойСсылки(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Возврат;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ОбщегоНазначенияСобытияФормИСКлиентПереопределяемый.ПередЗаписью(ЭтотОбъект, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Возврат;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ИнтеграцияИСКлиент.ПослеЗаписиВФормеОбъектаДокументаИС(
		ЭтотОбъект,
		Объект,
		ОбщегоНазначенияИСМПКлиентСервер.ИмяПодсистемы(),
		ПараметрыЗаписи);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Подключаемый_ОбновитьКоманды();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ЗаполнитьПредставлениеТитула();
	ОбновитьПредставленияНаФорме();
	РазблокироватьДанныеФормыДляРедактирования();
	
	ИнтеграцияИС.ПослеЗаписиНаСервереВФормеОбъектаДокументаИС(
		ЭтотОбъект,
		ТекущийОбъект,
		ОбщегоНазначенияИСМПКлиентСервер.ИмяПодсистемы(),
		ПараметрыЗаписи);
	
	НастроитьЭлементыФормы(ЭтотОбъект);
	
	СобытияФормИС.ПослеЗаписиНаСервере(ЭтотОбъект);
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ПринудительноеЗакрытие Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПредставлениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	ОбработкаНавигационнойСсылкиСтатус(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура СтатусПредставлениеВРамкеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	ОбработкаНавигационнойСсылкиСтатус(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеОчистка(Элемент, СтандартнаяОбработка)
	
	Объект.ЭлектронныйДокумент         = Неопределено;
	
	ДокументОснованиеОчисткаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	ДокументОснованиеПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.РазрешениеНаОтгрузкуИСМП.Форма.ФормаДокумента.Записать");
	
	ОчиститьСообщения();
	Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.РазрешениеНаОтгрузкуИСМП.Форма.ФормаДокумента.Провести");
	
	ОчиститьСообщения();
	ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.РазрешениеНаОтгрузкуИСМП.Форма.ФормаДокумента.ПровестиИЗакрыть");
	
	ОчиститьСообщения();
	ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	
	Если Записать(ПараметрыЗаписи) Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АрхивироватьДокумент(Команда)
	
	ОбщегоНазначенияИСКлиент.АрхивироватьДокументы(ЭтотОбъект, Объект.Ссылка, ОбщегоНазначенияИСМПКлиент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьПоОснованию(Команда)
	
	ОбработчикПерезаполненияПоОснованию();
	
КонецПроцедуры

#Область ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда) Экспорт
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды() Экспорт
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюКомандПодключенныхКОбъекту(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда) Экспорт
	
	СобытияФормИСКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда, Неопределено);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВыборОснования

&НаКлиенте
Процедура Подключаемый_ПослеВыбораОснования(ДанныеВыбора, ДополнительныеПараметры) Экспорт
	
	Если ДанныеВыбора = Элементы.ДокументОснование.ДоступныеТипы.ПривестиЗначение(ДанныеВыбора) Тогда
		Объект.ДокументОснование = ДанныеВыбора;
		Модифицированность       = Истина;
	КонецЕсли;
	
	ЗаполнитьДетали = (ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ОбработатьПерезаполнение"));
	Если ЗаполнитьДетали Тогда
		ОбработчикПерезаполненияПоОснованию();
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ДокументОснование,ЭлектронныйДокумент");
	
	ЗадатьВопросПоВыполнениюПроверкиГИСМТ();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикПерезаполненияПоОснованию()
	
	ОчиститьСообщения();
	
	ПерезаполнитьПоОснованиюСервер();
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьПоОснованиюСервер()
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	ТекущийОбъект.Заполнить(Объект.ДокументОснование);
	
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
	
	ПриСозданииЧтенииНаСервере();
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область Статус

&НаКлиенте
Процедура ОбработкаНавигационнойСсылкиСтатус(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОчиститьСообщения();
	
	Если (Не ЗначениеЗаполнено(Объект.Ссылка)) Или (Не Объект.Проведен) Тогда
		
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения(
			"СтатусПредставлениеОбработкаНавигационнойСсылкиЗавершение",
			ЭтотОбъект,
			Новый Структура("НавигационнаяСсылкаФорматированнойСтроки", НавигационнаяСсылкаФорматированнойСтроки));
		ТекстВопроса = НСтр("ru = 'Документ ""Разрешение на отгрузку"" не проведен. Провести?';
							|en = 'Документ ""Разрешение на отгрузку"" не проведен. Провести?'");
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	ИначеЕсли Модифицированность Тогда
		
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения(
			"СтатусПредставлениеОбработкаНавигационнойСсылкиЗавершение",
			ЭтотОбъект,
			Новый Структура("НавигационнаяСсылкаФорматированнойСтроки", НавигационнаяСсылкаФорматированнойСтроки));
		ТекстВопроса = НСтр("ru = 'Документ ""Разрешение на отгрузку"" был изменен. Провести?';
							|en = 'Документ ""Разрешение на отгрузку"" был изменен. Провести?'");
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ОбработатьНажатиеНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтатусИСМП()
	
	МенеджерОбъекта = ОбщегоНазначенияИС.МенеджерОбъектаПоСсылке(Объект.Ссылка);
	
	СтатусИСМП = МенеджерОбъекта.СтатусПоУмолчанию();
	ДальнейшееДействие = МенеджерОбъекта.ДальнейшееДействиеПоУмолчанию();
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Статусы.Статус КАК Статус,
		|	ВЫБОР
		|		КОГДА Статусы.ДальнейшееДействие1 В (&МассивДальнейшиеДействия)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.НеТребуется)
		|		ИНАЧЕ Статусы.ДальнейшееДействие1
		|	КОНЕЦ КАК ДальнейшееДействие1,
		|	ВЫБОР
		|		КОГДА Статусы.ДальнейшееДействие2 В (&МассивДальнейшиеДействия)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.НеТребуется)
		|		ИНАЧЕ Статусы.ДальнейшееДействие2
		|	КОНЕЦ КАК ДальнейшееДействие2,
		|	ВЫБОР
		|		КОГДА Статусы.ДальнейшееДействие3 В (&МассивДальнейшиеДействия)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.НеТребуется)
		|		ИНАЧЕ Статусы.ДальнейшееДействие3
		|	КОНЕЦ КАК ДальнейшееДействие3
		|ИЗ
		|	РегистрСведений.СтатусыДокументовИСМП КАК Статусы
		|ГДЕ
		|	Статусы.Документ = &Документ");
		
		Запрос.УстановитьПараметр("Документ",                 Объект.Ссылка);
		Запрос.УстановитьПараметр("МассивДальнейшиеДействия", ОбменДаннымиИСМП.НеотображаемыеВДокументахДальнейшиеДействия());
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			
			СтатусИСМП = Выборка.Статус;
			
			ДальнейшееДействие = Новый Массив;
			ДальнейшееДействие.Добавить(Выборка.ДальнейшееДействие1);
			ДальнейшееДействие.Добавить(Выборка.ДальнейшееДействие2);
			ДальнейшееДействие.Добавить(Выборка.ДальнейшееДействие3);
			
		КонецЕсли;
		
	КонецЕсли;

	ДопустимыеДействия = Новый Массив;
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеРазрешениеНаОтгрузкуПродукции);
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеРазрешениеНаОтгрузкуПродукции);
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеОперацию);
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеПередачуДанных);
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюИСМП.ПереформируйтеЭлектронныйДокумент);

	СтатусПредставление = ОбменДаннымиИСМП.ПредставлениеСтатуса(
		СтатусИСМП, ДальнейшееДействие, ДопустимыеДействия, ДополнительноеСостояние);
	
	РедактированиеФормыНеДоступно = СтатусИСМП <> Перечисления.СтатусыОбработкиРазрешенийНаОтгрузкуИСМП.Черновик
	                              И СтатусИСМП <> Перечисления.СтатусыОбработкиРазрешенийНаОтгрузкуИСМП.РазрешениеОшибка;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображениеСтатусаОшибкиПолученияРазрешения()
	
	Если СтатусИСМП = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиРазрешенийНаОтгрузкуИСМП.РазрешениеОшибка")
		И ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		
		ПодключитьОбработчикОжидания("ПоказатьСообщениеВСтатусеОшибкиРазрешения", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСообщениеВСтатусеОшибкиРазрешения() Экспорт
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаДополнительно;
	
	ШаблонТекстаСообщения = НСтр("ru = 'Исправьте ошибки в документе-основании %1 и переформируйте разрешение.';
								|en = 'Исправьте ошибки в документе-основании %1 и переформируйте разрешение.'");
	ТекстСообщения        = СтрШаблон(ШаблонТекстаСообщения, Объект.ДокументОснование);
	
	ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, Объект.ДокументОснование);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементыФормы(Форма)
	
	ЗависимыеОтСтатусаИСМП = Новый Массив;
	ЗависимыеОтСтатусаИСМП.Добавить("ГруппаНередактируемыеПослеОтправкиРеквизитыОсновное");
	
	ОбщегоНазначенияИСКлиентСервер.УстановитьДоступностьЭлементовФормы(
		Форма, ЗависимыеОтСтатусаИСМП, Не Форма.РедактированиеФормыНеДоступно);
	
КонецПроцедуры

#КонецОбласти

#Область Прочие

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	Инициализация       = ПустаяСтрока(СписокРеквизитов);
	Элементы            = Форма.Элементы;
	
	Если СтруктураРеквизитов.Свойство("ДокументОснование") Тогда
		ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюКомандПодключенныхКОбъекту(Форма);
	КонецЕсли;
	
	Если Инициализация Или СтруктураРеквизитов.Свойство("ЭлектронныйДокумент") Тогда
		Элементы.СтатусПредставление.Видимость       = Не ЗначениеЗаполнено(Форма.ДополнительноеСостояние);
		Элементы.СтатусПредставлениеВРамке.Видимость = ЗначениеЗаполнено(Форма.ДополнительноеСостояние);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставленияНаФорме(Прочитать = Ложь)
	
	Если Прочитать Тогда
		Прочитать();
	Иначе
		ОбновитьСтатусИСМП();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПредставлениеОбработкаНавигационнойСсылкиЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		 Возврат;
	КонецЕсли;
	
	Если ПроверитьЗаполнение() Тогда
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
		РазблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	Если Не Модифицированность И Объект.Проведен Тогда
		ОбработатьНажатиеНавигационнойСсылки(ДополнительныеПараметры.НавигационнаяСсылкаФорматированнойСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНажатиеНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ЗапроситьРазрешениеНаОтгрузкуПродукции" Тогда
		
		Если ЗначениеЗаполнено(АктуальныйЭлектронныйДокумент)
			И ЗначениеЗаполнено(Объект.ЭлектронныйДокумент)
			И Объект.ЭлектронныйДокумент <> АктуальныйЭлектронныйДокумент Тогда
			
			ОповещениеПослеВыбора = Новый ОписаниеОповещения("Подключаемый_ПерезаполнитьАктуальныйЭлектронныйДокументЗавершение", ЭтотОбъект);
			ПоказатьВопрос(ОповещениеПослеВыбора,
				НСтр("ru = 'Запрос разрешения по неактуальному электронному документу не будет обработан в ГИС МТ.
					|Перезаполнить?';
					|en = 'Запрос разрешения по неактуальному электронному документу не будет обработан в ГИС МТ.
					|Перезаполнить?'"),
				РежимДиалогаВопрос.ДаНет);
			Возврат;
			
		КонецЕсли;
		
		ЗапроситьРазрешениеПоЭлектронномуДокументу();
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОтменитьРазрешениеНаОтгрузкуПродукции" Тогда
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаПредставлениеТипула;
		
		ПараметрыОбработкиДокументов = ОбменДаннымиИСМПКлиентСервер.ПараметрыОбработкиДокументов();
		ПараметрыОбработкиДокументов.Ссылка             = Объект.Ссылка;
		ПараметрыОбработкиДокументов.Организация        = Объект.Организация;
		ПараметрыОбработкиДокументов.ДальнейшееДействие = ПредопределенноеЗначение(
			"Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.ОтменитеРазрешениеНаОтгрузкуПродукции");
		
		ОписаниеПриЗавершении = Новый ОписаниеОповещения(
			"Подключаемый_ПриЗавершенииОперации", ЭтотОбъект, ПараметрыОбработкиДокументов);
		
		ОбменДаннымиИСМПКлиент.ПодготовитьКПередаче(
			ЭтотОбъект,
			ПараметрыОбработкиДокументов,
			ОписаниеПриЗавершении);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОтменитьОперацию" Тогда
		
		ОбменДаннымиИСМПКлиент.ОтменитьПоследнююОперацию(Объект.Ссылка);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОтменитьПередачуДанных" Тогда
		
		ОбменДаннымиИСМПКлиент.ОтменитьПередачу(Объект.Ссылка);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПоказатьПричинуОшибки" Тогда
		
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("Документ", Объект.Ссылка);
		
		ОткрытьФорму(
			"Справочник.ИСМППрисоединенныеФайлы.Форма.ФормаОшибки",
			ПараметрыОткрытияФормы,
			ЭтотОбъект);
			
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПереформируйтеЭлектронныйДокумент" Тогда
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаПредставлениеТипула;
		ПараметрыДокумента = СообщениеОтправителяИдентификаторПоЭлектронномуДокументу(Объект.ЭлектронныйДокумент);
		
		ПараметрыПереформирования = ИнтерфейсДокументовЭДОВызовСервера.ПараметрыПереформированияДокумента(ПараметрыДокумента.Сообщение);
		ПараметрыПереформирования.Вставить("ИдентификаторПакета", ПараметрыДокумента.ИдентификаторПакета);
		ПараметрыПереформирования.Вставить("ОбъектУчета",         Объект.ДокументОснование);
		
		Если ПараметрыПереформирования.Отказ Тогда
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(ПараметрыПереформирования.ПричинаОтказа);
			Возврат;
			
		ИначеЕсли ЗначениеЗаполнено(ПараметрыПереформирования.ТекстВопроса) Тогда
			
			ОповещениеПослеВопроса = Новый ОписаниеОповещения("Подключаемый_ПослеПодтвержденияПереформированияЭлектронногоДокумента", ЭтотОбъект, ПараметрыПереформирования);
			ПоказатьВопрос(ОповещениеПослеВопроса,
				ПараметрыПереформирования.ТекстВопроса,
				РежимДиалогаВопрос.ОКОтмена,,,
				НСтр("ru = 'Перезаполнение документа';
					|en = 'Перезаполнение документа'"));
			Возврат;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(АктуальныйЭлектронныйДокумент)
			И ЗначениеЗаполнено(Объект.ЭлектронныйДокумент)
			И Объект.ЭлектронныйДокумент <> АктуальныйЭлектронныйДокумент Тогда
			ПерезаполнитьАктуальнымЭлектроннымДокументом();
		КонецЕсли;
		
		Оповещение = Новый ОписаниеОповещения("ПереформированиеЗавершениеФормированияЭлектронногоДокумента", 
			ЭтотОбъект, ПараметрыПереформирования);
		ИнтерфейсДокументовЭДОКлиент.ПереформироватьДокумент(Оповещение, ПараметрыПереформирования);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СообщениеОтправителяИдентификаторПоЭлектронномуДокументу(ЭлектронныйДокумент)
	
	СтруктураВозврата = Новый Структура("Сообщение, ИдентификаторПакета", Неопределено, Неопределено);
	
	СтруктураВозврата.ИдентификаторПакета = ПакетыДокументовЭДОПовтИспНаВызов.ИдентификаторПакетаДокумента(ЭлектронныйДокумент);
	СтруктураВозврата.Сообщение           = ЭлектронноеВзаимодействиеИСМП.СообщениеОтправителя(ЭлектронныйДокумент);
	
	Возврат СтруктураВозврата;
	
КонецФункции

&НаКлиенте
Процедура ПереформированиеЗавершениеФормированияЭлектронногоДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	Если ОбработкаНеисправностейБЭДКлиентСервер.ЕстьОшибки(Результат.КонтекстДиагностики) Тогда
		ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(Результат.КонтекстДиагностики);
	Иначе
		
		Если Результат.Переформирован Или Результат.СформированоИсправление Тогда
			
			Если Результат.СформированныеДокументы <> Неопределено 
				И Результат.СформированныеДокументы.Количество() Тогда
				
				Для Каждого СформированныйДокумент Из Результат.СформированныеДокументы Цикл
					Объект.ЭлектронныйДокумент = СформированныйДокумент.Ключ;
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьДокументВернутьВСтатусЧерновикПослеПереформированияЭД();
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьОбменОбработкаОжидания()
	
	ОбменДаннымиИСМПКлиент.ПродолжитьВыполнениеОбмена(ЭтотОбъект, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриЗавершенииОперации(Результат, ДополнительныеПараметры) Экспорт

	Прочитать();
	ОтображениеСтатусаОшибкиПолученияРазрешения();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДокументВернутьВСтатусЧерновикПослеПереформированияЭД()
	
	НовыйСтатус        = Документы.РазрешениеНаОтгрузкуИСМП.СтатусПоУмолчанию();
	ДальнейшиеДействия = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Документы.РазрешениеНаОтгрузкуИСМП.ДальнейшееДействиеПоУмолчанию());
	
	ПараметрыОбновления = РегистрыСведений.СтатусыДокументовИСМП.ВозвращаемоеЗначениеДальнейшиеДействияСтатус(НовыйСтатус, ДальнейшиеДействия);
	
	НачатьТранзакцию();
	
	Попытка
		
		РегистрыСведений.СтатусыДокументовИСМП.ОбновитьСтатус(Объект.Ссылка, ПараметрыОбновления);
		Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	
	ПравоИзменения = ПравоДоступа("Изменение", Метаданные.Документы.РазрешениеНаОтгрузкуИСМП);
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		
		РезультатПроверкиГИСМТ = 
			ИнтеграцияИСМПВызовСервера.ДанныеЭлектронногоДокументаИСостоянияПоОбъектуУчета(Объект.ДокументОснование, Объект.ЭлектронныйДокумент)[Объект.ДокументОснование];
		
	КонецЕсли;
	
	ЗаполнитьПредставлениеТитула();
	
	ОбновитьПредставленияНаФорме();
	НастроитьЭлементыФормы(ЭтотОбъект);
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредставлениеТитула()
	
	ДополнительноеСостояние       = Неопределено;
	АктуальныйЭлектронныйДокумент = Неопределено;
	
	Если ЗначениеЗаполнено(Объект.ЭлектронныйДокумент) Тогда
		
		СообщениеОтправителя = ЭлектронноеВзаимодействиеИСМП.СообщениеОтправителя(Объект.ЭлектронныйДокумент);
		Попытка
			ДвоичныеДанныеТитула = ЭлектронноеВзаимодействиеИСМП.ДвоичныеДанныеСообщения(СообщениеОтправителя, Истина);
		Исключение
			ОформитьПустоеПредставление();
			Возврат;
		КонецПопытки;
		
		ПараметрыВизуализации = ЭлектронноеВзаимодействиеИСМП.НовыеПараметрыВизуализацииЭлектронногоДокумента();
		ПараметрыВизуализации.ВыводитьКопияВерна          = Ложь;
		ПараметрыВизуализации.ВыводитьДопДанные           = Ложь;
		ПараметрыВизуализации.ВыводитьБанковскиеРеквизиты = Ложь;
		
		ПредставлениеТитула = ЭлектронноеВзаимодействиеИСМП.ПредставлениеЭлектронногоДокументаПоФайлу(
			СообщениеОтправителя,
			ДвоичныеДанныеТитула,,
			ПараметрыВизуализации);
		
		Элементы.ПредставлениеТитула.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.НеИспользовать;
		Элементы.ПредставлениеТитула.ОтображениеСостояния.Текст                          = "";
		Элементы.ПредставлениеТитула.ОтображениеСостояния.Видимость                      = Ложь;
		
		Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
			АктуальныйЭлектронныйДокумент = ЭлектронноеВзаимодействиеИСМП.СтатусДокументооборота(Объект.ДокументОснование).ЭлектронныйДокумент;
			Если АктуальныйЭлектронныйДокумент <> Объект.ЭлектронныйДокумент
				И СтатусИСМП <> Перечисления.СтатусыОбработкиРазрешенийНаОтгрузкуИСМП.РазрешениеОтменено Тогда
				ДополнительноеСостояние = Новый ФорматированнаяСтрока(
					НСтр("ru = 'Электронный документ не актуален';
						|en = 'Электронный документ не актуален'"));
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		ОформитьПустоеПредставление();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОформитьПустоеПредставление()
	
	ПредставлениеТитула.Очистить();
	Элементы.ПредставлениеТитула.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
	Элементы.ПредставлениеТитула.ОтображениеСостояния.Текст     = НСтр("ru = 'Электронный документ не сформирован';
																		|en = 'Электронный документ не сформирован'");
	Элементы.ПредставлениеТитула.ОтображениеСостояния.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьРазрешениеПоЭлектронномуДокументу()
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаПредставлениеТипула;
	
	ПараметрыОбработкиДокументов = ОбменДаннымиИСМПКлиентСервер.ПараметрыОбработкиДокументов();
	ПараметрыОбработкиДокументов.Ссылка             = Объект.Ссылка;
	ПараметрыОбработкиДокументов.Организация        = Объект.Организация;
	ПараметрыОбработкиДокументов.ДальнейшееДействие = ПредопределенноеЗначение(
		"Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеРазрешениеНаОтгрузкуПродукции");
	
	ОписаниеПриЗавершении = Новый ОписаниеОповещения(
		"Подключаемый_ПриЗавершенииОперации", ЭтотОбъект, ПараметрыОбработкиДокументов);
		
	ОбменДаннымиИСМПКлиент.ПодготовитьКПередаче(
		ЭтотОбъект,
		ПараметрыОбработкиДокументов,
		ОписаниеПриЗавершении);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеНовогоЭлектронногоДокумента()
	
	Документы.РазрешениеНаОтгрузкуИСМП.ЗаполнитьДаннымиЭлектронногоДокумента(Объект);
	ЗаполнитьПредставлениеТитула();
	ОбновитьПредставленияНаФорме();
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ДокументОснование,ЭлектронныйДокумент");
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьВопросПоОформлениюЭлектронногоДокумента(ЭтоРежимПриОткрытии = Ложь)
	
	ОтсутствуетЭДПоОснованию   = (Не ЗначениеЗаполнено(Объект.ЭлектронныйДокумент)
		И ЗначениеЗаполнено(Объект.ДокументОснование));
	ТребуетсяПереформироватьЭД = (ЭтоРежимПриОткрытии
		И СтатусИСМП = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиРазрешенийНаОтгрузкуИСМП.Черновик")
		И Не ЗначениеЗаполнено(АктуальныйЭлектронныйДокумент)
		И ЗначениеЗаполнено(Объект.ДокументОснование)
		И ЗначениеЗаполнено(Объект.ЭлектронныйДокумент));
		
	Если ОтсутствуетЭДПоОснованию Или ТребуетсяПереформироватьЭД Тогда
		
		ЗакрытьФормуПриОтказе = (ЭтоРежимПриОткрытии И ОтсутствуетЭДПоОснованию И Не ТребуетсяПереформироватьЭД);
		
		ДополнительныеПараметры = Новый Структура("ЭтоРежимПриОткрытии", ЭтоРежимПриОткрытии);
		
		ФормированиеЭДЗавершение      = Новый ОписаниеОповещения("Подключаемый_ФормированиеЭДЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ОтказФормированияЭДЗавершение = Новый ОписаниеОповещения(
			"Подключаемый_ОтказВыполненияПредварительныхДействийЗавершение", ЭтотОбъект, ЗакрытьФормуПриОтказе);
		ИнтеграцияИСМПКлиент.СформироватьЭлектронныйДокумент(
			ЭтотОбъект, Объект.ДокументОснование, ФормированиеЭДЗавершение, ОтказФормированияЭДЗавершение);
		
	ИначеЕсли ЭтоРежимПриОткрытии
		И ПравоИзменения
		И Не РедактированиеФормыНеДоступно
		И ЗначениеЗаполнено(АктуальныйЭлектронныйДокумент)
		И ЗначениеЗаполнено(Объект.ЭлектронныйДокумент)
		И Объект.ЭлектронныйДокумент <> АктуальныйЭлектронныйДокумент Тогда
		
		ТекстВопроса = НСтр("ru = 'Актуальный электронный документ отличается от текущего, перезаполнить?';
							|en = 'Актуальный электронный документ отличается от текущего, перезаполнить?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_ПерезаполнитьАктуальныйЭлектронныйДокументЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
		
	Иначе
		
		ПроверитьВыполнениеПредварительнойПроверкиГИСМТ(ЭтоРежимПриОткрытии);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьВопросПоВыполнениюПроверкиГИСМТ(ЭтоРежимПриОткрытии = Ложь)
	
	Если РезультатПроверкиГИСМТ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// если включена проверка документов ГИС МТ и она еще не выполнилась, перед формированием разрешения
	// необходимо проверить отсутствие ошибок по ней
	Если Не РезультатПроверкиГИСМТ.ПроверкаОтключена
		И РезультатПроверкиГИСМТ.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиДокументовИСМП.НеВыполнялось")
		И СтатусИСМП = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиРазрешенийНаОтгрузкуИСМП.Черновик")
		И АктуальныйЭлектронныйДокумент = Объект.ЭлектронныйДокумент Тогда
		
		ВыполнитьПредварительнуюПроверкуДокументаГИСМТ(ЭтоРежимПриОткрытии);
		
	ИначеЕсли Не РезультатПроверкиГИСМТ.ПроверкаОтключена
		И РезультатПроверкиГИСМТ.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиДокументовИСМП.ЗавершеноСОшибкой")
		И СтатусИСМП = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиРазрешенийНаОтгрузкуИСМП.Черновик")
		И АктуальныйЭлектронныйДокумент = Объект.ЭлектронныйДокумент Тогда
		
		ВыполнитьПредварительнуюПроверкуДокументаГИСМТ(ЭтоРежимПриОткрытии);
		
	Иначе
		ЗадатьВопросПоОформлениюЭлектронногоДокумента(ЭтоРежимПриОткрытии);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыполнениеПредварительнойПроверкиГИСМТ(ЭтоРежимПриОткрытии = Ложь)
	
	Если РезультатПроверкиГИСМТ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// если включена проверка документов ГИС МТ и ЭД отправлен на предварительную проверку, но еще ее не прошел,
	// ожидаем ответа от ГИС МТ, потому как нет точного понимания, что ошибок при проверке не найдено
	Если РезультатПроверкиГИСМТ.ПроверкаОтключена
		И РезультатПроверкиГИСМТ.ОтправленНаПредварительнуюПроверку
		И Не РезультатПроверкиГИСМТ.ВыполненаПредварительнаяПроверка
		И СтатусИСМП = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиРазрешенийНаОтгрузкуИСМП.Черновик") Тогда
		
		ДополнительныеПараметры = Новый Структура("ЭтоРежимПриОткрытии", ЭтоРежимПриОткрытии);
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("Подключаемый_ОжидатьВыполнениеПроверкиГИСМТЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ОповещениеОтказаПроверки = Новый ОписаниеОповещения(
			"Подключаемый_ОтказВыполненияПредварительныхДействийЗавершение", ЭтотОбъект, ЭтоРежимПриОткрытии);
		
		ИнтеграцияИСМПКлиент.ПроверитьОбъектУчетаГИСМТ(
			ЭтотОбъект, Объект.ДокументОснование, ОповещениеОЗавершении, ОповещениеОтказаПроверки);
			
	Иначе
		ОтображениеСтатусаОшибкиПолученияРазрешения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОтказВыполненияПредварительныхДействийЗавершение(Ответ, ЗакрытьФормуПриОтказе) Экспорт
	
	Если ЗакрытьФормуПриОтказе Тогда
		ПринудительноеЗакрытие = Истина;
		Закрыть(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПерезаполнитьАктуальныйЭлектронныйДокументЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПерезаполнитьАктуальнымЭлектроннымДокументом();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПослеПодтвержденияПереформированияЭлектронногоДокумента(Результат, ПараметрыПереформирования) Экспорт
	
	Если Не Результат = КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(АктуальныйЭлектронныйДокумент)
		И ЗначениеЗаполнено(Объект.ЭлектронныйДокумент)
		И Объект.ЭлектронныйДокумент <> АктуальныйЭлектронныйДокумент Тогда
		
		ПерезаполнитьАктуальнымЭлектроннымДокументом();
		
		ПараметрыДокумента = СообщениеОтправителяИдентификаторПоЭлектронномуДокументу(Объект.ЭлектронныйДокумент);
		
		ПараметрыПереформирования = ИнтерфейсДокументовЭДОВызовСервера.ПараметрыПереформированияДокумента(ПараметрыДокумента.Сообщение);
		ПараметрыПереформирования.Вставить("ИдентификаторПакета", ПараметрыДокумента.ИдентификаторПакета);
		ПараметрыПереформирования.Вставить("ОбъектУчета",         Объект.ДокументОснование);
		
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПереформированиеЗавершениеФормированияЭлектронногоДокумента", 
		ЭтотОбъект, ПараметрыПереформирования);
	ИнтерфейсДокументовЭДОКлиент.ПереформироватьДокумент(Оповещение, ПараметрыПереформирования);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОжидатьВыполнениеПроверкиГИСМТЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Ответ.ЕстьОшибки Тогда
		// есть ошибки проверки документов ГИС МТ
		Возврат;
	КонецЕсли;
	
	Если Не Ответ.РезультатПроверки.ОтправленНаПредварительнуюПроверку Тогда
		// документ не отправлен на предварительную проверку
		ЗадатьВопросПоОформлениюЭлектронногоДокумента(ДополнительныеПараметры.ЭтоРежимПриОткрытии);
		
	ИначеЕсли Ответ.РезультатПроверки.ОтправленНаПредварительнуюПроверку
		И Не Ответ.РезультатПроверки.ВыполненаПредварительнаяПроверка Тогда
		// на стороне ГИС МТ еще не обработано выполнение предварительной проверки, ожидаем ответа
		ПроверитьВыполнениеПредварительнойПроверкиГИСМТ(ДополнительныеПараметры.ЭтоРежимПриОткрытии);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьАктуальнымЭлектроннымДокументом()
	
	Объект.ЭлектронныйДокумент = АктуальныйЭлектронныйДокумент;
	
	ЗаполнитьДокументВернутьВСтатусЧерновикПослеПереформированияЭД();
	
	ЗаполнитьПредставлениеТитула();
	ОбновитьПредставленияНаФорме();
	НастроитьЭлементыФормы(ЭтотОбъект);
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПредварительнуюПроверкуДокументаГИСМТ(ЭтоРежимПриОткрытии)
	
	ДополнительныеПараметры = Новый Структура("ЭтоРежимПриОткрытии", ЭтоРежимПриОткрытии);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("Подключаемый_ОжидатьВыполнениеПроверкиГИСМТЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОповещениеОтказаПроверки = Новый ОписаниеОповещения(
		"Подключаемый_ОтказВыполненияПредварительныхДействийЗавершение", ЭтотОбъект, ЭтоРежимПриОткрытии);
	
	ИнтеграцияИСМПКлиент.ПроверитьОбъектУчетаГИСМТ(
		ЭтотОбъект, Объект.ДокументОснование, ОповещениеОЗавершении, ОповещениеОтказаПроверки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияПредварительнойПроверкиДокументаГИСМТ(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Результат.ЕстьОшибки Тогда
		
		ЗадатьВопросПоОформлениюЭлектронногоДокумента(ДополнительныеПараметры.ЭтоРежимПриОткрытии);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ФормированиеЭДЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьДанныеНовогоЭлектронногоДокумента();
	ПроверитьВыполнениеПредварительнойПроверкиГИСМТ(ПараметрыОповещения.ЭтоРежимПриОткрытии);
	
КонецПроцедуры

&НаСервере
Процедура ДокументОснованиеОчисткаНаСервере()
	
	ЗаполнитьПредставлениеТитула();
	ОбновитьПредставленияНаФорме();
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ДокументОснование,ЭлектронныйДокумент");
	
КонецПроцедуры

&НаСервере
Процедура ДокументОснованиеПриИзмененииНаСервере()
	
	ЗаполнитьПредставлениеТитула();
	ОбновитьПредставленияНаФорме();
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ДокументОснование,ЭлектронныйДокумент");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
