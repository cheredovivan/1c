#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		Если Не ЗначениеЗаполнено(ИдентификаторДокумента) Тогда
			ИдентификаторДокумента = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Ответственный) Тогда
			ЗаполняемыеЗначения = Новый Структура("Ответственный");
			ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ЗаполняемыеЗначения);
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗаполняемыеЗначения);
		КонецЕсли;
	Иначе
		Если ПометкаУдаления И Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления") Тогда
			ДополнительныеСвойства.Вставить("УстановленаПометкаУдаления", Истина);
			Если Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.ДанныеПриняты Тогда
				ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Данные для оформления приняты. Пометка на удаление ""%1"" не предусмотрена';
						|en = 'The registration data is accepted. Cannot mark the ""%1"" for deletion'"),
					Ссылка);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, , , Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	УстановленаПометкаУдаления = Неопределено;
	ДополнительныеСвойства.Свойство("УстановленаПометкаУдаления", УстановленаПометкаУдаления);
	Если УстановленаПометкаУдаления = Истина Или Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.Аннулирован Тогда
		
		МенеджерЗаписи = РегистрыСведений.ВыгруженныеОбъектыУправлениеПерсоналом.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Ссылка 		= Ссылка;
		МенеджерЗаписи.Приложение 	= Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
		МенеджерЗаписи.ТипОбъекта 	= Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗапросДанныхДляОформленияНаРаботу;
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= Ссылка;
			МенеджерЗаписи.ТипОбъекта 	= Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗапросДанныхДляОформленияНаРаботу;
			МенеджерЗаписи.Прочитать();
			ЗарегистрироватьУдаление = Истина;
			Если МенеджерЗаписи.Выбран() И МенеджерЗаписи.ВыгружатьУдаление Тогда
				ЗарегистрироватьУдаление = Ложь;
			КонецЕсли;
			
			Если ЗарегистрироватьУдаление Тогда
				МенеджерЗаписи.Ссылка 		= Ссылка;
				МенеджерЗаписи.ТипОбъекта 	= Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗапросДанныхДляОформленияНаРаботу;
				МенеджерЗаписи.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
				МенеджерЗаписи.ВыгружатьУдаление = Истина;
				МенеджерЗаписи.Записать();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ВызватьИсключение НСтр("ru = 'Создание документа копированием не поддерживается';
							|en = 'Cannot create a document by copying'");
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли