
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.РежимВыбора Тогда
		Элементы.Список.РежимВыбора = Истина;
	КонецЕсли;
	
	УстановитьУсловноеОформлениеСписка();
	УстановитьПараметрыЗапроса();
	
	СтруктураПараметровОтбора = Новый Структура();
	ЗарплатаКадры.ДобавитьПараметрОтбора(СтруктураПараметровОтбора, "ФизическоеЛицо",
			Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"), НСтр("ru = 'Физическое лицо';
																		|en = 'Person'"));
		
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список",,
			СтруктураПараметровОтбора, "СписокКритерииОтбора");

	ПараметрыОтбора = Новый Соответствие();
	ПараметрыОтбора.Вставить("ПоказыватьПринятыеДанные", ПоказыватьПринятыеДанные);
	УстановитьОтбор(ПараметрыОтбора);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.КоманднаяПанельФормы;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ДанныеДляОформленияНаРаботу" Тогда
		ОбновитьСписокНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказыватьПринятыеДанныеПриИзменении(Элемент)
	
	ПараметрыОтбора = Новый Соответствие();
	ПараметрыОтбора.Вставить("ПоказыватьПринятыеДанные", ПоказыватьПринятыеДанные);
	УстановитьОтбор(ПараметрыОтбора);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)

	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПараметрОтбораПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.ПараметрОтбораНаФормеСДинамическимСпискомПриИзменении(ЭтотОбъект, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформлениеСписка()
	
	УсловноеОформлениеКД = Список.КомпоновщикНастроек.Настройки.УсловноеОформление;
	УсловноеОформлениеКД.ИдентификаторПользовательскойНастройки = "ОсновноеОформление";
	
	// Удаление предустановленных элементов оформления.
	Предустановленные = Новый Массив;
	ЭлементыУсловногоОформления = УсловноеОформлениеКД.Элементы;
	Для каждого ЭлементУсловногоОформления Из ЭлементыУсловногоОформления Цикл
		Если ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
			Предустановленные.Добавить(ЭлементУсловногоОформления);
		КонецЕсли;
	КонецЦикла;
	Для каждого ЭлементУсловногоОформления Из Предустановленные Цикл
		ЭлементыУсловногоОформления.Удалить(ЭлементУсловногоОформления);
	КонецЦикла;
	
	// Установка оформления для просроченных заявок.
	ЭлементОформления = УсловноеОформлениеКД.Элементы.Добавить();
	ЭлементОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ЭлементОтбораДанных = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("СрокИсполнения");
	ЭлементОтбораДанных.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Заполнено;
	ЭлементОтбораДанных.Использование 	= Истина;
	
	ЭлементОтбораДанных = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("СрокИсполнения");
	ЭлементОтбораДанных.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Меньше;
	ЭлементОтбораДанных.ПравоеЗначение 	= ТекущаяДатаСеанса();
	ЭлементОтбораДанных.Использование 	= Истина;
	
	ЭлементОтбораДанных = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("СостояниеЗапроса");
	ЭлементОтбораДанных.ВидСравнения 	= ВидСравненияКомпоновкиДанных.НеРавно;
	ЭлементОтбораДанных.ПравоеЗначение 	= Перечисления.СостоянияЗапросовДанныхДляОформленияНаРаботу.ДанныеПолучены;
	ЭлементОтбораДанных.Использование 	= Истина;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПросроченныеДанныеЦвет);
	
	// Установка оформления для выполненных заявок.
	ЭлементОформления = УсловноеОформлениеКД.Элементы.Добавить();
	ЭлементОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ЭлементОтбораДанных = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Состояние");
	ЭлементОтбораДанных.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение 	= Перечисления.СостоянияДанныхДляОформленияНаРаботу.ДанныеПриняты;
	ЭлементОтбораДанных.Использование 	= Истина;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтбор(ПараметрыОтбора)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "Состояние", Перечисления.СостоянияДанныхДляОформленияНаРаботу.ДанныеПриняты,
		ВидСравненияКомпоновкиДанных.НеРавно, , Не ПараметрыОтбора["ПоказыватьПринятыеДанные"]);
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокНаСервере()

	Элементы.Список.Обновить();

КонецПроцедуры

&НаСервере
Процедура НастроитьДинамическийСписокНаСервере(ОписаниеМодификации) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		МодульЗарплатаКадрыРасширенный = ОбщегоНазначения.ОбщийМодуль("ЗарплатаКадрыРасширенный");
		МодульЗарплатаКадрыРасширенный.НастроитьДинамическийСписокПоОписаниюМодификации(ЭтаФорма, ОписаниеМодификации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПараметрМодификацииВыбор(Элемент, ИмяПараметра, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		МодульЗарплатаКадрыРасширенныйКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ЗарплатаКадрыРасширенныйКлиент");
		МодульЗарплатаКадрыРасширенныйКлиент.ПараметрМодификацииОбработкаНавигационнойСсылки(ЭтотОбъект, Элемент.Родитель.Имя, ИмяПараметра);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыЗапроса()
	
	ПараметрАннулирован 				= НСтр("ru = 'Аннулирован';
												|en = 'Canceled'");
	ПараметрДанныеПриняты 				= НСтр("ru = 'Данные приняты';
												|en = 'The data is accepted'");
	ПараметрДанныеПолучены 				= НСтр("ru = 'Данные получены';
													|en = 'Data received'");
	ПараметрЗарегистрированаОтправка 	= НСтр("ru = 'Зарегистрирована отправка';
												|en = 'Sending is registered'");
	ПараметрЗапросОтправлен 			= НСтр("ru = 'Запрос отправлен';
												|en = 'The request is sent'");
	ПараметрТребуетсяУточнение 			= НСтр("ru = 'Требуется уточнение';
													|en = 'Specification is required'");
	ПараметрТребуетсяУточнениеЗарегистрированаОтправка = НСтр("ru = 'Требуется уточнение, зарегистрирована отправка';
																|en = 'Clarification is required. Sending is registered'");
	ПараметрТребуетсяУточнениеЗапросОтправлен = НСтр("ru = 'Требуется уточнение, запрос отправлен';
													|en = 'Clarification is required. The request is sent'");
	
	Список.Параметры.УстановитьЗначениеПараметра("ДанныеПриняты", ПараметрДанныеПриняты);
	Список.Параметры.УстановитьЗначениеПараметра("ДанныеПолучены", ПараметрДанныеПолучены);
	Список.Параметры.УстановитьЗначениеПараметра("ЗарегистрированаОтправка", ПараметрЗарегистрированаОтправка);
	Список.Параметры.УстановитьЗначениеПараметра("ЗапросОтправлен", ПараметрЗапросОтправлен);
	Список.Параметры.УстановитьЗначениеПараметра("ТребуетсяУточнение", ПараметрТребуетсяУточнение);
	Список.Параметры.УстановитьЗначениеПараметра("ТребуетсяУточнениеЗарегистрированаОтправка", ПараметрТребуетсяУточнениеЗарегистрированаОтправка);
	Список.Параметры.УстановитьЗначениеПараметра("ТребуетсяУточнениеЗапросОтправлен", ПараметрТребуетсяУточнениеЗапросОтправлен);
	Список.Параметры.УстановитьЗначениеПараметра("Аннулирован", ПараметрАннулирован);

КонецПроцедуры

#КонецОбласти


