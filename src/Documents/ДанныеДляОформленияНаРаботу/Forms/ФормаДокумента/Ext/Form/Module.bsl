
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоНовый = Параметры.Ключ.Пустая();
	
	Если ЭтоНовый Тогда
		
		ЗапрашиваемыеЗначения = Новый Структура;
		ЗапрашиваемыеЗначения.Вставить("Организация", "Объект.Организация");
		ЗапрашиваемыеЗначения.Вставить("Ответственный", "Объект.Ответственный");
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтотОбъект, ЗапрашиваемыеЗначения);
		
		ЗаполнитьЗапрашиваемыеДанные();
		Объект.СрокИсполнения = ТекущаяДатаСеанса() + 7 * 86400;
		Объект.Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.Новый;
		
		ПриПолученииДанныхНаСервере();
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриПолученииДанныхНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ЭтоНовый = Ложь;
	ЗаполнитьТаблицуФайлов();
	ОбновитьСостояние();
	ОбновитьЭУ();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, ПараметрОповещения, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, ПараметрОповещения) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ИмяСобытия = "Запись_ФизическиеЛица" И Источник = Объект.ФизическоеЛицо Тогда
		ОбновитьДанныеФизическогоЛица(ЭтаФорма, Объект.ФизическоеЛицо);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ДанныеДляОформленияНаРаботуЗаписанДокумент", Объект.Ссылка, ВладелецФормы);
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФизическоеЛицоПриИзменении(Элемент)
	
		ОбновитьДанныеФизическогоЛица(ЭтаФорма, Объект.ФизическоеЛицо);
	
КонецПроцедуры

&НаКлиенте
Процедура ФИОПринятыПриИзменении(Элемент)
	
	УстановитьЗначениеДанныеПриняты("ФИОПриняты", ФИОПриняты);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументПринятПриИзменении(Элемент)
	
	УстановитьЗначениеДанныеПриняты("ДокументПринят", ДокументПринят);
	
КонецПроцедуры

&НаКлиенте
Процедура МестоРожденияПринятоПриИзменении(Элемент)
	
	УстановитьЗначениеДанныеПриняты("МестоРожденияПринято", МестоРожденияПринято);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаРожденияПринятаПриИзменении(Элемент)
	
	УстановитьЗначениеДанныеПриняты("ДатаРожденияПринята", ДатаРожденияПринята);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолПринятПриИзменении(Элемент)
	
	УстановитьЗначениеДанныеПриняты("ПолПринят", ПолПринят);
	
КонецПроцедуры

&НаКлиенте
Процедура ИННПринятПриИзменении(Элемент)
	
	УстановитьЗначениеДанныеПриняты("ИННПринят", ИННПринят);
	
КонецПроцедуры

&НаКлиенте
Процедура СНИЛСПринятПриИзменении(Элемент)
	
	УстановитьЗначениеДанныеПриняты("СНИЛСПринят", СНИЛСПринят);
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеВБракеПринятыПриИзменении(Элемент)
	
	УстановитьЗначениеДанныеПриняты("СостояниеВБракеПриняты", СостояниеВБракеПриняты);
	
КонецПроцедуры

&НаКлиенте
Процедура МобильныйТелефонПринятПриИзменении(Элемент)
	
	УстановитьЗначениеДанныеПриняты("МобильныйТелефонПринят", МобильныйТелефонПринят);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭлектроннаяПочтаПринятаПриИзменении(Элемент)
	
	УстановитьЗначениеДанныеПриняты("ЭлектроннаяПочтаПринята", ЭлектроннаяПочтаПринята);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресПроживанияПринятПриИзменении(Элемент)
	
	УстановитьЗначениеДанныеПриняты("АдресПроживанияПринят", АдресПроживанияПринят);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПолученныеФайлы

&НаКлиенте
Процедура ПолученныеФайлыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ФайлыПредставление" Тогда
		
		ТекущиеДанные = Элементы.ПолученныеФайлы.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Или ТекущиеДанные.Файлы.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Если ТекущиеДанные.Файлы.Количество() = 1 Тогда
			ДанныеФайла = РаботаСФайламиКлиент.ДанныеФайла(ТекущиеДанные.Файлы[0].Значение, УникальныйИдентификатор);
			РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла, Ложь);
		Иначе
			
			ТекстЗаголовка = НСтр("ru = 'Файлы';
									|en = 'Files'");
			ТекстЗаголовка = СтрШаблон("%1: %2", ТекстЗаголовка, Строка(ТекущиеДанные.ТипДанных));
			
			ПараметрыОткрытия = Новый Структура("ТекстЗаголовка,ПрисоединенныеФайлы");
			ПараметрыОткрытия.ТекстЗаголовка 		= ТекстЗаголовка;
			ПараметрыОткрытия.ПрисоединенныеФайлы 	= ТекущиеДанные.Файлы;
			
			ОткрытьФорму("Документ.ДанныеДляОформленияНаРаботу.Форма.ФормаВыбораФайлов",
						ПараметрыОткрытия, ЭтаФорма, Истина,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область ОбработчикиПодключаемыхКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

&НаКлиенте
Процедура ОтправитьВКабинет(Команда)
	
	ОтправитьВКабинетНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьЗапрашиваемыеДанные(Команда)
	
	ОткрытьФормуЗапрашиваемыхДанных(Ложь);
	
КонецПроцедуры
	
&НаКлиенте
Процедура ПоказатьПовторноЗапрашиваемыеДанные(Команда)
	
	ОткрытьФормуЗапрашиваемыхДанных(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьУточнениеДанных(Команда)
	
	ЗапроситьУточнениеДанныхНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьДанные(Команда)
	
	ПринятьДанныеНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиПодсистем

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти 

#Область ОтправкаЗапросаСКабинет

&НаКлиенте
Функция ЗапросГотовКОтправке()
	
	Отказ = Ложь;
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = НСтр("ru = 'Не указана организация';
								|en = 'Company is not specified'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"Организация","Объект.Организация");
		Отказ = Истина;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.ФизическоеЛицо) Тогда
		ТекстСообщения = НСтр("ru = 'Не указано физическое лицо';
								|en = 'The person is not specified'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ФизическоеЛицо","Объект.ФизическоеЛицо");
		Отказ = Истина;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.СрокИсполнения) Тогда
		ТекстСообщения = НСтр("ru = 'Не указан срок исполнения';
								|en = 'The due date is not specified'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"СрокИсполнения","Объект.СрокИсполнения");
		Отказ = Истина;
	КонецЕсли;
	
	ЗапрашиваемыеДанные = Объект.ЗапрашиваемыеДанные;
	ПовторноЗапрашиваемыеДанные = Ложь;
	Если Объект.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияДанныхДляОформленияНаРаботу.ТребуетсяУточнениеДанных") Тогда
		ЗапрашиваемыеДанные = Объект.ПовторноЗапрашиваемыеДанные;
		ПовторноЗапрашиваемыеДанные = Истина;
	КонецЕсли;
	ЕстьЗапрашиваемыеДанные = Ложь;
	Для каждого СтрокаТЧ Из ЗапрашиваемыеДанные Цикл
		Если СтрокаТЧ.Запрашивать Тогда
			ЕстьЗапрашиваемыеДанные = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если Не ЕстьЗапрашиваемыеДанные Тогда
		Если ПовторноЗапрашиваемыеДанные Тогда
			ТекстСообщения = НСтр("ru = 'Не выбраны данные запрашиваемые для уточнения';
									|en = 'The data requested for clarification is not selected'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Не выбраны запрашиваемые данные';
									|en = 'The requested data is not selected'");
		КонецЕсли;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ДанныеФизическогоЛицаЗаполнены Тогда
		Возврат Истина;
	Иначе
		ТекстСообщения = Элементы.ДекорацияПояснениеДанныхФизическогоЛица.Заголовок;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ФизическоеЛицо","Объект.ФизическоеЛицо");
		Возврат Ложь;
	КонецЕсли;

КонецФункции

&НаКлиенте
Процедура ОтправитьВКабинетНаКлиенте()

	Если ЗапросГотовКОтправке() Тогда
		
		ЗаголовокВопроса = НСтр("ru = 'Подтверждение';
								|en = 'Confirm'");
		ТекстВопроса = НСтр("ru = 'Отправка запроса данных в сервис 1С:Кабинет сотрудника.';
							|en = 'Send a data request to 1C:Employee Account.'");
		
		ТекстЗаписать = НСтр("ru = 'Продолжение возможно только после записи документа.
		|Записать и продолжить?';
		|en = 'You can continue only after saving the document. 
		|Save and continue?'");
		
		Если Модифицированность Или ЭтоНовый Тогда
			ТекстВопроса = СтрШаблон("%1%2%3", ТекстВопроса, Символы.ПС, ТекстЗаписать);
		КонецЕсли;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьЗапросВКабинетПродолжение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, ИнтеграцияУправлениеПерсоналомКлиента.ОписаниеКнопокВопроса(),,,ЗаголовокВопроса);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросВКабинетПродолжение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Если Модифицированность Или ЭтоНовый Тогда
			Записать();
		КонецЕсли;
		
		ОтправитьЗапросВКабинетНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтправитьЗапросВКабинетНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗапросДанныхДляОформленияНаРаботу;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Объект.Ссылка);
		ЭлементБлокировки.УстановитьЗначение("ТипОбъекта", ТипОбъекта);
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СостоянияЗапросовДанныхДляОформленияНаРаботу");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Объект.Ссылка);
		Блокировка.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Ссылка.Установить(Объект.Ссылка);
		НаборЗаписей.Отбор.ТипОбъекта.Установить(ТипОбъекта);
		
		ВыгружатьУдаление = Объект.ПометкаУдаления Или Объект.Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.Аннулирован;
		
		ЗаписьНабора = НаборЗаписей.Добавить();
		ЗаписьНабора.Ссылка = Объект.Ссылка;
		ЗаписьНабора.ТипОбъекта = ТипОбъекта;
		ЗаписьНабора.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
		ЗаписьНабора.ВыгружатьУдаление = ВыгружатьУдаление;
		
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыСведений.СостоянияЗапросовДанныхДляОформленияНаРаботу.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Ссылка.Установить(Объект.Ссылка);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда
			Запись = НаборЗаписей.Добавить();
			Запись.Ссылка = Объект.Ссылка;
		Иначе
			Запись = НаборЗаписей[0];
		КонецЕсли;
		Запись.Состояние = Перечисления.СостоянияЗапросовДанныхДляОформленияНаРаботу.ЗарегистрированаОтправка;
		НаборЗаписей.Записать();
		
		ОбновляемыеРеквизиты = Новый Соответствие;
		Если Не Объект.ЗарегистрированаОтправка Тогда
			ОбновляемыеРеквизиты.Вставить("ЗарегистрированаОтправка", Истина);
		КонецЕсли;
		Если Объект.Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.ТребуетсяУточнениеДанных Тогда
			Если Не Объект.ЗарегистрированаОтправкаУточнения Тогда
				ОбновляемыеРеквизиты.Вставить("ЗарегистрированаОтправкаУточнения", Истина);
			КонецЕсли;
			Если Объект.УточнениеДанныхЗагружено Тогда
				ОбновляемыеРеквизиты.Вставить("УточнениеДанныхЗагружено", Ложь);
			КонецЕсли;
		КонецЕсли;
		Если ОбновляемыеРеквизиты.Количество() > 0 Тогда
			ДокументОбъект = РеквизитФормыВЗначение("Объект");
			Для каждого КлючИЗначение Из ОбновляемыеРеквизиты Цикл
				ДокументОбъект[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
			КонецЦикла;
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			Прочитать();
		КонецЕсли;
		
		СообщениеОВыполнении = НСтр("ru = 'Запрос зарегистрирован для отправки в сервис.';
									|en = 'The request is registered to be sent to the service.'");

		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(ИнтеграцияКабинетСотрудника.ИменаСобытийЖР().ПрочиеСобытия,
			УровеньЖурналаРегистрации.Ошибка,,, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())); 
			
		СообщениеОВыполнении = НСтр("ru = 'Не удалось зарегистрировать передачу запроса в сервис. Подробности см. в журнале регистрации.';
									|en = 'Cannot register the request transfer to the service. See the event log for details.'");
		
	КонецПопытки;
	
	ОбщегоНазначения.СообщитьПользователю(СообщениеОВыполнении);
	
	ОбновитьСостояние();
	ОбновитьЭУ();

КонецПроцедуры

#КонецОбласти

#Область УточнениеДанных

&НаКлиенте
Процедура ЗапроситьУточнениеДанныхНаКлиенте()
	
	ВсеДанныеПриняты = Истина;
	Для каждого СтрокаТЧ Из Объект.ЗапрашиваемыеДанные Цикл
		ОписаниеДанных = ОписаниеЗапрашиваемыхДанных[СтрокаТЧ.ТипДанных];
		Если СтрокаТЧ.Запрашивать Тогда
			Если ОписаниеДанных.ЗапросДанных И Не СтрокаТЧ.ДанныеПриняты Тогда
				ВсеДанныеПриняты = Ложь;
				Прервать;
			ИначеЕсли ОписаниеДанных.ЗапросФайлов И Не СтрокаТЧ.ФайлыПриняты Тогда
				ВсеДанныеПриняты = Ложь;
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ВсеДанныеПриняты Тогда
		ТекстПредупреждения = НСтр("ru = 'Все данные отмечены как принятые.';
									|en = 'All data is marked as accepted.'");
		ПоказатьПредупреждение(,ТекстПредупреждения,5);
		Возврат;
	КонецЕсли;
	
	ЗаголовокВопроса = НСтр("ru = 'Подтверждение';
							|en = 'Confirm'");
	ТекстВопроса = НСтр("ru = 'Запрос уточнения данных. Выберите данные требующие уточнения.';
						|en = 'Request for data clarification. Select the data that needs to be clarified.'");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗапроситьУточнениеДанныхПродолжение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, ИнтеграцияУправлениеПерсоналомКлиента.ОписаниеКнопокВопроса(),,,ЗаголовокВопроса);

КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьУточнениеДанныхПродолжение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияДанныхДляОформленияНаРаботу.ТребуетсяУточнениеДанных");
	Объект.ЗарегистрированаОтправкаУточнения = Ложь;
	Объект.УточнениеДанныхЗагружено = Ложь;
	Объект.СрокИсполнения = Неопределено;
	Объект.СообщениеУточненияДанных = "";
	ЗаполнитьПовторноЗапрашиваемыеДанные(ЭтаФорма);
	ОбновитьЭУ();
	ОткрытьФормуЗапрашиваемыхДанных(Истина);

КонецПроцедуры

#КонецОбласти

#Область ПринятиеДанных

&НаКлиенте
Процедура ПринятьДанныеНаКлиенте()
	
	ЕстьНеПринятыеДанные = Ложь;
	Для каждого СтрокаТЧ Из Объект.ЗапрашиваемыеДанные Цикл
		ОписаниеДанных = ОписаниеЗапрашиваемыхДанных[СтрокаТЧ.ТипДанных];
		Если СтрокаТЧ.Запрашивать Тогда
			Если ОписаниеДанных.ЗапросДанных И Не СтрокаТЧ.ДанныеПриняты Тогда
				ЕстьНеПринятыеДанные = Истина;
				Прервать;
			ИначеЕсли ОписаниеДанных.ЗапросФайлов И Не СтрокаТЧ.ФайлыПриняты Тогда
				ЕстьНеПринятыеДанные = Истина;
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьНеПринятыеДанные Тогда
		ТекстПредупреждения = НСтр("ru = 'Не все данные отмечены как принятые.';
									|en = 'Not all data is marked as accepted.'");
		ПоказатьПредупреждение(,ТекстПредупреждения,5);
		Возврат;
	КонецЕсли;
	
	ЗаголовокВопроса = НСтр("ru = 'Подтверждение';
							|en = 'Confirm'");
	ТекстВопроса = НСтр("ru = 'Данные для оформления на работу будут отмечены как принятые.';
						|en = 'The employment registration data will be marked as accepted.'");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПринятьДанныеПродолжение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, ИнтеграцияУправлениеПерсоналомКлиента.ОписаниеКнопокВопроса(),,,ЗаголовокВопроса);

КонецПроцедуры

&НаКлиенте
Процедура ПринятьДанныеПродолжение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПринятьДанныеНаСервере();

КонецПроцедуры

&НаСервере
Процедура ПринятьДанныеНаСервере()

	УстановитьПривилегированныйРежим(Истина);
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗапросДанныхДляОформленияНаРаботу;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Объект.Ссылка);
		ЭлементБлокировки.УстановитьЗначение("ТипОбъекта", ТипОбъекта);
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СостоянияЗапросовДанныхДляОформленияНаРаботу");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Объект.Ссылка);
		Блокировка.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Ссылка.Установить(Объект.Ссылка);
		НаборЗаписей.Отбор.ТипОбъекта.Установить(ТипОбъекта);
		
		ЗаписьНабора = НаборЗаписей.Добавить();
		ЗаписьНабора.Ссылка = Объект.Ссылка;
		ЗаписьНабора.ТипОбъекта = ТипОбъекта;
		ЗаписьНабора.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
		
		НаборЗаписей.Записать();
		
		ДокументОбъект = РеквизитФормыВЗначение("Объект");
		ДокументОбъект.Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.ДанныеПриняты;
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		
		СообщениеОВыполнении = НСтр("ru = 'Данные приняты.';
									|en = 'The data is accepted.'");
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(ИнтеграцияКабинетСотрудника.ИменаСобытийЖР().ПрочиеСобытия,
			УровеньЖурналаРегистрации.Ошибка,,, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())); 
			
		СообщениеОВыполнении = НСтр("ru = 'Не зарегистрировать прием данных. Подробности см. в журнале регистрации.';
									|en = 'Data receipt is not registered. See the event log for details.'");
		
	КонецПопытки;
	
	Прочитать();
	ОбщегоНазначения.СообщитьПользователю(СообщениеОВыполнении);
	
	ОбновитьСостояние();
	ОбновитьЭУ();

КонецПроцедуры

#КонецОбласти

#Область ЗапрашиваемыеДанные

&НаКлиенте
Процедура ОткрытьФормуЗапрашиваемыхДанных(ПовторныйЗапрос)
	
	Если ПовторныйЗапрос Тогда
		ЗапрашиваемыеДанные = Объект.ПовторноЗапрашиваемыеДанные;
		РежимТолькоПросмотр = ТолькоПросмотр Или Объект.ЗарегистрированаОтправкаУточнения;
	Иначе
		РежимТолькоПросмотр = ТолькоПросмотр Или Объект.ЗарегистрированаОтправка;
		ЗапрашиваемыеДанные = Объект.ЗапрашиваемыеДанные;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("ЗапрашиваемыеДанные,ПовторныйЗапрос,ТолькоПросмотр", ЗапрашиваемыеДанные, ПовторныйЗапрос, РежимТолькоПросмотр);
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбновитьЗапрашиваемыеДанные", ЭтотОбъект);
	ОткрытьФорму("Документ.ДанныеДляОформленияНаРаботу.Форма.ЗапрашиваемыеДанные",
		ПараметрыОткрытия, ЭтотОбъект,,,,ОповещениеОЗакрытии,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры
	
&НаСервере
Процедура ЗаполнитьЗапрашиваемыеДанные()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТипыДанных.Ссылка КАК ТипДанных,
	|	НЕ ТипыДанных.НеЗапрашивать КАК Запрашивать
	|ИЗ
	|	Справочник.ТипыДанныхДляОформленияНаРаботу КАК ТипыДанных
	|ГДЕ
	|	НЕ ТипыДанных.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Запрашивать УБЫВ,
	|	ТипыДанных.Наименование";
	
	ТипыДанных = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТЗ Из ТипыДанных Цикл
		ЗаполнитьЗначенияСвойств(Объект.ЗапрашиваемыеДанные.Добавить(), СтрокаТЗ);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗапрашиваемыеДанные(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПовторныйЗапрос = Результат.ПовторныйЗапрос;
	Если ПовторныйЗапрос Тогда
		Для каждого СтрокаТЧ Из Объект.ПовторноЗапрашиваемыеДанные Цикл
			СтрокаТЧ.Запрашивать = Результат.ЗапрашиваемыеДанные[СтрокаТЧ.ТипДанных];
		КонецЦикла;
	Иначе
		Для каждого СтрокаТЧ Из Объект.ЗапрашиваемыеДанные Цикл
			СтрокаТЧ.Запрашивать = Результат.ЗапрашиваемыеДанные[СтрокаТЧ.ТипДанных];
		КонецЦикла;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ДанныеФизическогоЛица

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДанныеФизическогоЛица(Форма, ФизическоеЛицо)
	
	Если Форма.Объект.ЗарегистрированаОтправка Тогда
		Форма.Элементы.ДекорацияПочта.Видимость = Ложь;
		Форма.Элементы.ДекорацияПояснениеДанныхФизическогоЛица.Видимость = Ложь;
	КонецЕсли;
	
	Результат = РезультатПроверкиФизическогоЛица(ФизическоеЛицо);
	Форма.ДанныеФизическогоЛицаЗаполнены = Результат.ДанныеЗаполнены;
	Если Не ЗначениеЗаполнено(Результат.СтрокаПочта) Тогда
		Форма.Элементы.ДекорацияПочта.Заголовок = "";
		Форма.Элементы.ДекорацияПояснениеДанныхФизическогоЛица.Заголовок = "";
	Иначе
		Форма.Элементы.ДекорацияПочта.Заголовок = Результат.СтрокаПочта;
		Форма.Элементы.ДекорацияПояснениеДанныхФизическогоЛица.Заголовок = Результат.СтрокаПояснение;
	КонецЕсли;
	
	Если Результат.ЕстьОшибки Тогда
		Форма.Элементы.ДекорацияПояснениеДанныхФизическогоЛица.ЦветТекста = Форма.ЦветСтиляПоясняющийОшибкуТекст;
	Иначе
		Форма.Элементы.ДекорацияПояснениеДанныхФизическогоЛица.ЦветТекста = Форма.ЦветСтиляИнформационнойНадписи;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция РезультатПроверкиФизическогоЛица(ФизическоеЛицо)
	
	Результат = Новый Структура("ДанныеЗаполнены,ЕстьОшибки,СтрокаПочта,СтрокаПояснение", Ложь,Ложь);
	
	Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		Возврат Результат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	КонтактнаяИнформация = ИнтеграцияУправлениеПерсоналом.КонтактнаяИнформацияФизическихЛиц(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо));
	УстановитьПривилегированныйРежим(Ложь);
	
	ЭлектроннаяПочта = КонтактнаяИнформация.АдресаПочты[ФизическоеЛицо];
	
	СтрокаПочта 	= НСтр("ru = '<не указана>';
							|en = '<not specified>'");
	ПочтаУказана = Ложь;
	ПочтаСоответствуетТребованиям = Ложь;
	Если ЗначениеЗаполнено(ЭлектроннаяПочта) Тогда
		СтрокаПочта = СОКРЛП(ЭлектроннаяПочта);
		ПочтаУказана = Истина;
		ПочтаСоответствуетТребованиям = ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(ЭлектроннаяПочта);
	КонецЕсли;
	
	Результат.СтрокаПочта = СтрШаблон(НСтр("ru = 'Электронная почта: %1';
											|en = 'Email: %1'"),СтрокаПочта);
	
	Если ПочтаУказана И ПочтаСоответствуетТребованиям Тогда
		Результат.СтрокаПояснение = НСтр("ru = 'Убедитесь, что задан действующий адрес электронной почты';
										|en = 'Make sure you entered a valid email address.'");
		Результат.ДанныеЗаполнены = Истина;
	Иначе
		Результат.ЕстьОшибки = Истина;
		Результат.ДанныеЗаполнены = Ложь;
		СообщениеПочтаНеСоответствуетТребованиям = "";
		Если ПочтаУказана И Не ПочтаСоответствуетТребованиям Тогда
			Результат.СтрокаПояснение = НСтр("ru = 'Адрес электронной почты указан некорректно';
											|en = 'Incorrect email address'");
		Иначе
			Результат.СтрокаПояснение = НСтр("ru = 'Не указан адрес электронной почты';
											|en = 'An email address is not specified'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПовторноЗапрашиваемыеДанные(Форма)

	Форма.Объект.ПовторноЗапрашиваемыеДанные.Очистить();
	Для каждого СтрокаТЧ Из Форма.Объект.ЗапрашиваемыеДанные Цикл
		ОписаниеДанных = Форма.ОписаниеЗапрашиваемыхДанных[СтрокаТЧ.ТипДанных];
		Если СтрокаТЧ.Запрашивать И (ОписаниеДанных.ЗапросДанных И Не СтрокаТЧ.ДанныеПриняты
			Или ОписаниеДанных.ЗапросФайлов И Не СтрокаТЧ.ФайлыПриняты)Тогда
			НоваяСтрока = Форма.Объект.ПовторноЗапрашиваемыеДанные.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
			НоваяСтрока.Запрашивать = Истина;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	ОписаниеЗапрашиваемыхДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.ОписаниеЗапрашиваемыхДанных();
	
	ЦветСтиляПоясняющийОшибкуТекст 	= ЦветаСтиля.ПоясняющийОшибкуТекст;
	ЦветСтиляИнформационнойНадписи  = ЦветаСтиля.ТекстИнформационнойНадписи;
	ЦветСтиляЦветТекстаПоля 		= ЦветаСтиля.ЦветТекстаПоля;
	
	СправочникМенеджер = Справочники.ТипыДанныхДляОформленияНаРаботу;
	ИмяДанныхТипДанных = Новый Соответствие;
	ИмяДанныхТипДанных.Вставить("ФИОПриняты", 				СправочникМенеджер.ФИО);
	ИмяДанныхТипДанных.Вставить("ДатаРожденияПринята", 		СправочникМенеджер.ДатаРождения);
	ИмяДанныхТипДанных.Вставить("МобильныйТелефонПринят", 	СправочникМенеджер.МобильныйТелефон);
	ИмяДанныхТипДанных.Вставить("ЭлектроннаяПочтаПринята", 	СправочникМенеджер.ЭлектроннаяПочта);
	ИмяДанныхТипДанных.Вставить("СНИЛСПринят", 				СправочникМенеджер.СНИЛС);
	ИмяДанныхТипДанных.Вставить("ИННПринят", 				СправочникМенеджер.ИНН);
	ИмяДанныхТипДанных.Вставить("ПолПринят", 				СправочникМенеджер.Пол);
	ИмяДанныхТипДанных.Вставить("ДокументПринят", 			СправочникМенеджер.ДокументУдостоверяющийЛичность);
	ИмяДанныхТипДанных.Вставить("МестоРожденияПринято", 	СправочникМенеджер.МестоРождения);
	ИмяДанныхТипДанных.Вставить("АдресПроживанияПринят", 	СправочникМенеджер.АдресПроживания);
	ИмяДанныхТипДанных.Вставить("СостояниеВБракеПриняты", 	СправочникМенеджер.СостояниеВБраке);
	ИмяДанныеПринятыТипДанных = Новый ФиксированноеСоответствие(ИмяДанныхТипДанных);
	
	Для каждого СтрокаТЧ Из Объект.ЗапрашиваемыеДанные Цикл
		Если СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.ФИО Тогда
			ФИОПриняты = СтрокаТЧ.ДанныеПриняты;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.ДатаРождения Тогда
			ДатаРожденияПринята = СтрокаТЧ.ДанныеПриняты;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.МобильныйТелефон Тогда
			МобильныйТелефонПринят = СтрокаТЧ.ДанныеПриняты;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.ЭлектроннаяПочта Тогда
			ЭлектроннаяПочтаПринята = СтрокаТЧ.ДанныеПриняты;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.СНИЛС Тогда
			СНИЛСПринят = СтрокаТЧ.ДанныеПриняты;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.ИНН Тогда
			ИННПринят = СтрокаТЧ.ДанныеПриняты;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.Пол Тогда
			ПолПринят = СтрокаТЧ.ДанныеПриняты;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.ДокументУдостоверяющийЛичность Тогда
			ДокументПринят = СтрокаТЧ.ДанныеПриняты;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.МестоРождения Тогда
			МестоРожденияПринято = СтрокаТЧ.ДанныеПриняты;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.АдресПроживания Тогда
			АдресПроживанияПринят = СтрокаТЧ.ДанныеПриняты;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.СостояниеВБраке Тогда
			СостояниеВБракеПриняты = СтрокаТЧ.ДанныеПриняты;
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьДанныеФизическогоЛица(ЭтаФорма, Объект.ФизическоеЛицо);
	ЗаполнитьТаблицуФайлов();
	ОбновитьСостояние();
	ОбновитьЭУ();
	
	ОтборСтрок = Новый Структура("Запрашивать,ЗапросФайлов", Истина, Истина);
	Элементы.ПолученныеФайлы.ОтборСтрок = Новый ФиксированнаяСтруктура(ОтборСтрок);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуФайлов()
	
	Если ЭтоНовый Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Владелец", Объект.Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПрисоединенныеФайлы.Ссылка КАК Файл,
	|	ПрисоединенныеФайлы.ТипДанных КАК ТипДанных
	|ИЗ
	|	Справочник.ДанныеДляОформленияНаРаботуПрисоединенныеФайлы КАК ПрисоединенныеФайлы
	|ГДЕ
	|	ПрисоединенныеФайлы.ВладелецФайла = &Владелец
	|	И ПрисоединенныеФайлы.Вложение
	|	И НЕ ПрисоединенныеФайлы.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТипДанных";
	РезультатЗапроса = Запрос.Выполнить();
	
	ЗагруженныеФайлы = Новый Соответствие;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ТипДанных") Цикл
		ПрисоединенныеФайлы = Новый Массив;
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка.Файл) Тогда
				ПрисоединенныеФайлы.Добавить(Выборка.Файл);
			КонецЕсли;
		КонецЦикла;
		ЗагруженныеФайлы.Вставить(Выборка.ТипДанных, ПрисоединенныеФайлы);
	КонецЦикла;
	
	Для каждого СтрокаТЧ Из Объект.ЗапрашиваемыеДанные Цикл
		ОписаниеДанных = ОписаниеЗапрашиваемыхДанных[СтрокаТЧ.ТипДанных];
		Если СтрокаТЧ.Запрашивать И ОписаниеДанных.ЗапросФайлов Тогда
			СтрокаТЧ.ЗапросФайлов = Истина;
			ПрисоединенныеФайлы = ЗагруженныеФайлы[СтрокаТЧ.ТипДанных];
			Если ЗначениеЗаполнено(ПрисоединенныеФайлы) Тогда
				СтрокаТЧ.Файлы.ЗагрузитьЗначения(ПрисоединенныеФайлы);
				Если ПрисоединенныеФайлы.Количество() = 1 Тогда
					ФайлыПредставление = Строка(ПрисоединенныеФайлы[0]);
				Иначе
					ФайлыПредставление = ПредставлениеКоличестваФайлов(ПрисоединенныеФайлы.Количество());
				КонецЕсли;
				СтрокаТЧ.ФайлыПредставление = ФайлыПредставление;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ПредставлениеКоличестваФайлов(КоличествоФайлов) Экспорт
	Возврат СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(НСтр("ru = ';%1 файл;;%1 файла;%1 файлов;';
																			|en = ';%1 file;;;;%1 files'"), КоличествоФайлов); 
КонецФункции

&НаКлиенте
Процедура УстановитьЗначениеДанныеПриняты(ИмяСвойства, ЗначениеСвойства)

	НайденныеСтроки = Объект.ЗапрашиваемыеДанные.НайтиСтроки(Новый Структура("ТипДанных", ИмяДанныеПринятыТипДанных[ИмяСвойства]));
	НайденныеСтроки[0].ДанныеПриняты = ЗначениеСвойства;

КонецПроцедуры

&НаСервере
Процедура ОбновитьСостояние()
	
	СостояниеЗапроса 	= Неопределено;
	ДатаОтправкиЗапроса = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СостоянияЗапросов.Состояние КАК Состояние,
	|	СостоянияЗапросов.ДатаОтправки КАК ДатаОтправки
	|ИЗ
	|	РегистрСведений.СостоянияЗапросовДанныхДляОформленияНаРаботу КАК СостоянияЗапросов
	|ГДЕ
	|	СостоянияЗапросов.Ссылка = &Ссылка";
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		СостоянияЗапросов = РезультатЗапроса.Выгрузить();
		СостояниеЗапроса 	= СостоянияЗапросов[0].Состояние;
		ДатаОтправкиЗапроса = СостоянияЗапросов[0].ДатаОтправки;
	КонецЕсли;
	
	ТекстСостояния = "<неопределен>";
	Если Объект.Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.Новый Тогда
		Если Не ЗначениеЗаполнено(СостояниеЗапроса) Тогда
			ТекстСостояния = НСтр("ru = 'Новый документ';
									|en = 'New document'");
		ИначеЕсли СостояниеЗапроса = Перечисления.СостоянияЗапросовДанныхДляОформленияНаРаботу.ЗарегистрированаОтправка Тогда
			ТекстСостояния = НСтр("ru = 'Новый документ. Зарегистрирована отправка в сервис';
									|en = 'New document. Sending to the service is registered'")
		ИначеЕсли СостояниеЗапроса = Перечисления.СостоянияЗапросовДанныхДляОформленияНаРаботу.ЗапросОтправлен Тогда
			ШаблонТекстСостояния = НСтр("ru = 'Данные из сервиса еще не получены. Запрос отправлен: %1';
										|en = 'The data is not yet received from the service. The request is sent: %1'");
			ТекстСостояния = СтрШаблон(ШаблонТекстСостояния, ДатаОтправкиЗапроса);
		КонецЕсли;
	ИначеЕсли Объект.Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.ДанныеПолучены Тогда
		Если Объект.ЗарегистрированаОтправкаУточнения Тогда
			ТекстСостояния = НСтр("ru = 'Получены уточняющие данные. После проверки можно принять данные или запросить уточнение.';
									|en = 'Clarifying data is received. After the check, you can accept the data or request clarification.'");
		Иначе
			ТекстСостояния = НСтр("ru = 'Данные получены. После проверки можно принять данные или запросить уточнение.';
									|en = 'The data is received. After the check, you can accept the data or request clarification.'");
		КонецЕсли;
	ИначеЕсли Объект.Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.ТребуетсяУточнениеДанных Тогда
		Если Не Объект.ЗарегистрированаОтправкаУточнения Тогда
			ТекстСостояния = НСтр("ru = 'Отправьте данные в сервис для получения уточнений';
									|en = 'Send the data to the service for clarification'");
		ИначеЕсли СостояниеЗапроса = Перечисления.СостоянияЗапросовДанныхДляОформленияНаРаботу.ЗарегистрированаОтправка Тогда
			ТекстСостояния = НСтр("ru = 'Требуется уточнение данных. Зарегистрирована отправка в сервис';
									|en = 'Data clarification is required. Sending to the service is registered'")
		ИначеЕсли СостояниеЗапроса = Перечисления.СостоянияЗапросовДанныхДляОформленияНаРаботу.ЗапросОтправлен Тогда
			ШаблонТекстСостояния = НСтр("ru = 'Данные из сервиса еще не получены. Запрос отправлен: %1';
										|en = 'The data is not yet received from the service. The request is sent: %1'");
			ТекстСостояния = СтрШаблон(ШаблонТекстСостояния, ДатаОтправкиЗапроса);
		КонецЕсли;
	ИначеЕсли Объект.Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.ДанныеПриняты Тогда
		ТекстСостояния = НСтр("ru = 'Данные приняты';
								|en = 'The data is accepted'");
	ИначеЕсли Объект.Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.Аннулирован Тогда
		ТекстСостояния = НСтр("ru = 'Документ аннулирован';
								|en = 'The document is canceled'");
	КонецЕсли;
	Элементы.ДекорацияСостояниеДокумента.Заголовок = ТекстСостояния;

КонецПроцедуры

&НаСервере
Процедура ОбновитьЭУ()
	
	ДоступноРешениеОПриемеНаРаботу = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы.КадровыеРешения") Тогда
		МодульКадровыеРешения = ОбщегоНазначения.ОбщийМодуль("КадровыеРешения");
		ДоступноРешениеОПриемеНаРаботу = МодульКадровыеРешения.ДоступноЧтениеРешенийОПриемеНаРаботу();
	КонецЕсли;
	Элементы.ОснованиеДокумента.Видимость = ДоступноРешениеОПриемеНаРаботу;
	
	// Блокируем реквизиты если это не новый документ.
	ИменаЭлементов = "Организация,ФизическоеЛицо";
	ИменаБлокируемыхЭлементов = СтрРазделить(ИменаЭлементов, ",");
	Для каждого ИмяЭлемента Из ИменаБлокируемыхЭлементов Цикл
		Элементы[ИмяЭлемента].ТолькоПросмотр = Не ЭтоНовый;
	КонецЦикла;
	
	Элементы.ПринятьДанные.КнопкаПоУмолчанию 		= Объект.ДанныеПолучены;
	Элементы.ДанныеДляОформления.Видимость 			= Объект.ДанныеПолучены;
	Элементы.ГруппаКомандПринятьДанные.Видимость 	= Объект.ДанныеПолучены;
	Элементы.ГруппаКомандПринятьДанные.Доступность 	= Объект.Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.ДанныеПолучены;
	
	Элементы.СообщениеУточненияДанных.Видимость = (Объект.Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.ТребуетсяУточнениеДанных);
	
	Элементы.ОтправитьВКабинет.Видимость = Объект.Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.Новый
		Или Объект.Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.ТребуетсяУточнениеДанных;
		
	Элементы.ПоказатьПовторноЗапрашиваемыеДанные.Видимость = (Объект.Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.ТребуетсяУточнениеДанных
		Или Объект.ЗарегистрированаОтправкаУточнения)
		И Объект.Состояние <> Перечисления.СостоянияДанныхДляОформленияНаРаботу.ДанныеПриняты;
	
	ТолькоПросмотр = Объект.ПометкаУдаления
		Или Объект.Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.Аннулирован
		Или Объект.Состояние = Перечисления.СостоянияДанныхДляОформленияНаРаботу.ДанныеПриняты;
		
	Для каждого СтрокаТЧ Из Объект.ЗапрашиваемыеДанные Цикл
		Если СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.ФИО Тогда
			Элементы.ГруппаФИО.Видимость = СтрокаТЧ.Запрашивать;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.ДатаРождения Тогда
			Элементы.ГруппаДатаРождения.Видимость = СтрокаТЧ.Запрашивать;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.МобильныйТелефон Тогда
			Элементы.ГруппаМобильныйТелефон.Видимость = СтрокаТЧ.Запрашивать;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.ЭлектроннаяПочта Тогда
			Элементы.ГруппаЭлектроннаяПочта.Видимость = СтрокаТЧ.Запрашивать;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.СНИЛС Тогда
			Элементы.ГруппаСНИЛС.Видимость = СтрокаТЧ.Запрашивать;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.ИНН Тогда
			Элементы.ГруппаИНН.Видимость = СтрокаТЧ.Запрашивать;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.Пол Тогда
			Элементы.ГруппаПол.Видимость = СтрокаТЧ.Запрашивать;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.ДокументУдостоверяющийЛичность Тогда
			Элементы.ГруппаДокумент.Видимость = СтрокаТЧ.Запрашивать;
			Элементы.ГруппаДокументРеквизиты.Видимость = ЗначениеЗаполнено(Объект.ДокументВид)
				И Объект.ДокументВид = Справочники.ВидыДокументовФизическихЛиц.ПаспортРФ;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.МестоРождения Тогда
			Элементы.ГруппаМестоРождения.Видимость = СтрокаТЧ.Запрашивать;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.АдресПроживания Тогда
			Элементы.ГруппаАдресПроживания.Видимость = СтрокаТЧ.Запрашивать;
		ИначеЕсли СтрокаТЧ.ТипДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.СостояниеВБраке Тогда
			Элементы.ГруппаСостояниеВБраке.Видимость = СтрокаТЧ.Запрашивать;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.Отчество.Видимость = Не Объект.ОтчествоОтсутствует;
	Элементы.ОтчествоОтсутствует.Видимость = Объект.ОтчествоОтсутствует;
	
	ТекстАдресПроживания = "";
	Если Объект.АдресПроживанияСовпадаетСРегистрацией Тогда
		ТекстАдресПроживания = НСтр("ru = 'совпадает с адресом постоянной регистрации';
									|en = 'Matches the permanent registration address'");
	ИначеЕсли Объект.АдресПроживанияСовпадаетСВременнойРегистрацией Тогда
		ТекстАдресПроживания = НСтр("ru = 'совпадает с адресом временной регистрации';
									|en = 'Matches the temporary registration address'");
	КонецЕсли;
	Элементы.АдресМестаПроживания.Видимость = ПустаяСтрока(ТекстАдресПроживания);
	Элементы.ТекстАдресПроживания.Видимость = Не ПустаяСтрока(ТекстАдресПроживания);
	
	СотрудникиКлиентСервер.ОбработатьОтображениеПоляИНН(Объект.ИНН, Элементы.ИНН,  ЭтаФорма);
	СотрудникиКлиентСервер.ОбработатьОтображениеПоляСтраховойНомерПФР(Объект.СНИЛС ,Элементы.СНИЛС, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти