
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ЗаявкиСотрудниковФормы.ПриСозданииНаСервере(ЭтотОбъект, ОписаниеДокумента(ЭтаФорма));
	ИнициализацияФормы();
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаявкиСотрудниковФормы.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект, ОписаниеДокумента(ЭтаФорма));
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ВывестиДокументыИзТаблицыДокументовЗаявкиНаСервере();
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, ПараметрОповещения, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, ПараметрОповещения) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	ЗаявкиСотрудниковФормыКлиент.ОбработкаОповещения(ЭтаФорма, ИмяСобытия, ПараметрОповещения, Источник);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_УдалитьДокументПоЗаявке(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ЗаявкиСотрудниковФормыКлиент.УдалитьДокументПоЗаявке(ЭтаФорма, Элемент);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область ОбработчикиПодключаемыхКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

&НаКлиенте
Процедура ЗаявкаВыполнена(Команда)
	
	РезультатВыполненияЗаявки = ПредопределенноеЗначение("Перечисление.РезультатыВыполненияЗаявокКабинетСотрудника.Выполнена");
	ВыполнитьЗаявкуНаКлиенте(РезультатВыполненияЗаявки);

КонецПроцедуры

&НаКлиенте
Процедура ЗаявкаОтклонена(Команда)
	
	РезультатВыполненияЗаявки = ПредопределенноеЗначение("Перечисление.РезультатыВыполненияЗаявокКабинетСотрудника.Отклонена");
	ВыполнитьЗаявкуНаКлиенте(РезультатВыполненияЗаявки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТиповойОтвет(Команда)
	
	ЗаявкиСотрудниковФормыКлиент.ВыбратьТиповойОтвет(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйДокумент(Команда)
	
	Если Объект.ТипЗаявки = ПредопределенноеЗначение("Перечисление.ТипыЗаявокКабинетСотрудника.ПособиеПриРожденииРебенка")
		Или Объект.ТипЗаявки = ПредопределенноеЗначение("Перечисление.ТипыЗаявокКабинетСотрудника.ЕжемесячноеПособиеПоУходуЗаРебенком") Тогда
		ОткрытьФорму("Документ.ИсходящееСообщениеОСтраховомСлучаеФСС.ФормаОбъекта", ПараметрыЗаполненияПособия(), ЭтотОбъект);
	ИначеЕсли Объект.ТипЗаявки = ПредопределенноеЗначение("Перечисление.ТипыЗаявокКабинетСотрудника.ОтпускПоБеременностиИРодам") Тогда
		ПричинаНетрудоспособности = ПредопределенноеЗначение("Перечисление.ПричиныНетрудоспособности.ПоБеременностиИРодам");
		ПараметрыЗаполнения = ЗаявкиСотрудниковФормыКлиентВнутренний.ПараметрыЗаполненияБольничныйЛист(ЭтаФорма, ПричинаНетрудоспособности);
		ОткрытьФорму("Документ.БольничныйЛист.ФормаОбъекта", ПараметрыЗаполнения, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьДокумент(Команда)
	
	ЗначенияДляВыбораДокумента = ЗначенияДляВыбораДокумента(Объект.Организация, Объект.Сотрудник, Объект.ТипЗаявки);
	ЗаявкиСотрудниковФормыКлиент.ВыполнитьКомандуВыбратьДокумент(ЭтаФорма, ЗначенияДляВыбораДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСрокИсполнения(Команда)
	
	ЗаявкиСотрудниковФормыКлиент.ИзменитьСрокИсполнения(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиПодсистем

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область ЗавершениеЗаявки

&НаКлиенте
Процедура ВыполнитьЗаявкуНаКлиенте(РезультатВыполненияЗаявки)

	Если ЗаявкиСотрудниковФормыКлиент.ЗаявкаГотоваКВыполнению(ЭтаФорма, РезультатВыполненияЗаявки, ОписаниеДокумента(ЭтаФорма)) Тогда
		ЗаявкиСотрудниковФормыКлиент.НачатьВыполнениеКомандыЗаявкаВыполнена(ЭтаФорма, РезультатВыполненияЗаявки);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция РезультатЗаписиВыполненияЗаявки(РезультатВыполненияЗаявки) Экспорт

	Возврат РезультатЗаписиВыполненияЗаявкиНаСервере(РезультатВыполненияЗаявки);

КонецФункции

&НаСервере
Функция РезультатЗаписиВыполненияЗаявкиНаСервере(РезультатВыполненияЗаявки) Экспорт
	
	Возврат ЗаявкиСотрудников.РезультатЗаписиВыполненияЗаявки(ЭтаФорма, РезультатВыполненияЗаявки, ОписаниеДокумента(ЭтаФорма));
	
КонецФункции

&НаКлиенте
Процедура ОбработчикЗавершенияЗаписиВыполненияЗаявки(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаявкиСотрудниковФормыКлиент.ОбработчикЗавершенияЗаписиВыполненияЗаявки(Результат, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму()
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область ОбработатьЗаписьСвязаннойЗаявки

&НаКлиенте
Процедура ОбработатьЗаписьСвязаннойЗаявки() Экспорт

	ОбработатьЗаписьСвязаннойЗаявкиНаСервере();

КонецПроцедуры

&НаСервере
Процедура ОбработатьЗаписьСвязаннойЗаявкиНаСервере()
	
	ОписаниеДокумента = ОписаниеДокумента(ЭтаФорма);
	ЗаявкиСотрудниковФормы.ПрочитатьВывестиСвязанныеЗаявки(ЭтаФорма, Объект, ОписаниеДокумента);
	ЗаявкиСотрудниковФормы.УстановитьДоступностьВыполненияПоСвязаннымЗаявкам(ЭтаФорма,ОписаниеДокумента);

КонецПроцедуры

#КонецОбласти

#Область ВыводДокументовИзТаблицыДокументовЗаявки

&НаКлиенте
Процедура ВывестиДокументыИзТаблицыДокументовЗаявки() Экспорт
	
	ВывестиДокументыИзТаблицыДокументовЗаявкиНаСервере()
	
КонецПроцедуры

&НаСервере
Процедура ВывестиДокументыИзТаблицыДокументовЗаявкиНаСервере()
	
	ЗаявкиСотрудниковФормы.ВывестиДокументыИзТаблицыДокументовЗаявки(ЭтаФорма, ОписаниеДокумента(ЭтаФорма));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработатьИзменениеТЧДокументыПоЗаявке

&НаКлиенте
Процедура ОбработатьИзменениеДокументыПоЗаявке() Экспорт

	ОбработатьИзменениеДокументыПоЗаявкеНаСервере();
	Модифицированность = Истина;

КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеДокументыПоЗаявкеНаСервере()
	
	ВывестиДокументыИзТаблицыДокументовЗаявкиНаСервере();

КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ИнициализацияФормы()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ГруппаДокументыПоЗаявкеГлавная", "Видимость", Объект.ВариантИсправления <> Перечисления.ВариантыИсправленияЗаявокКабинетСотрудника.Отмена);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ГруппаДанныеЗаявления", "ОтображатьЗаголовок", Объект.ВариантИсправления = Перечисления.ВариантыИсправленияЗаявокКабинетСотрудника.Перенос);
	
	УстановитьТекстПоясненияПорядкаВыполнения();
	
	ТекстЗаявления = "";
	ПоказыватьДатуРождения 	= Ложь;
	ПоказыватьФИО 			= Ложь;
	ПоказыватьОтпускПоБеременностиИРодам = Ложь;
	Если Объект.ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ПособиеПриРожденииРебенка Тогда
		ТекстЗаявления = НСтр("ru = 'Заявление на единовременное пособие при рождении ребенка';
								|en = 'Application for one-time allowance on childbirth'");
		ПоказыватьДатуРождения 	= Истина;
		ПоказыватьФИО 			= Истина;
	ИначеЕсли Объект.ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ЕжемесячноеПособиеПоУходуЗаРебенком Тогда
		ТекстЗаявления = НСтр("ru = 'Заявление на ежемесячное пособие по уходу за ребенком';
								|en = 'Application for monthly childcare allowance'");
		ПоказыватьДатуРождения 	= Истина;
		ПоказыватьФИО 			= Истина;
	ИначеЕсли Объект.ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ОтпускПоБеременностиИРодам Тогда
		ТекстЗаявления = НСтр("ru = 'Заявление на отпуск по беременности и родам';
								|en = 'Maternity leave application'");
		ПоказыватьОтпускПоБеременностиИРодам = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДатаРожденияРебенка", "Видимость", ПоказыватьДатуРождения);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФИОРебенка", "Видимость", ПоказыватьФИО);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаОтпускПоБеременностиИРодам", "Видимость", ПоказыватьОтпускПоБеременностиИРодам);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияТипЗаявления", "Заголовок", ТекстЗаявления);
	
КонецПроцедуры

&НаСервере
Функция УстановитьТекстПоясненияПорядкаВыполнения()
	
	Строка1 = НСтр("ru = 'Для выполнения заявки надо создать новый документ или выбрать уже существующий,';
					|en = 'To complete the request, create a new document or select an existing one.'");
	Строка2 = НСтр("ru = 'после этого заявку можно отметить как выполненную. Для отклонения заявки необходимо указать причину в ""Сообщении сотруднику"".';
					|en = 'Then you can mark the request as completed. To reject the request, specify the reason in the ""Message to employee"".'");
	ТекстПояснения = СтрШаблон("%1 %2", Строка1, Строка2);
	
	Элементы.ДекорацияВыполнениеПояснение.Заголовок = ТекстПояснения;

КонецФункции

&НаСервереБезКонтекста
Функция ЗначенияДляВыбораДокумента(Организация, Сотрудник, ТипЗаявки)
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", 	Организация);
	Отбор.Вставить("Сотрудник", 	Сотрудник);
	
	ЗначенияДляВыбора = Новый СписокЗначений;
	Если ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ПособиеПриРожденииРебенка
		Или ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ЕжемесячноеПособиеПоУходуЗаРебенком Тогда
		ЗначенияДляВыбора.Добавить(Метаданные.Документы.ИсходящееСообщениеОСтраховомСлучаеФСС.ПолноеИмя(), Метаданные.Документы.ИсходящееСообщениеОСтраховомСлучаеФСС.Синоним);
		Если ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ПособиеПриРожденииРебенка Тогда
			ВидПособия = Перечисления.ПособияНазначаемыеФСС.ЕдиновременноеПособиеПриРожденииРебенка;
		Иначе
			ВидПособия = Перечисления.ПособияНазначаемыеФСС.ЕжемесячноеПособиеПоУходуЗаРебенком;
		КонецЕсли;
		Отбор.Вставить("ВидПособия", ВидПособия);
	Иначе
		Отбор.Вставить("ПричинаНетрудоспособности", Перечисления.ПричиныНетрудоспособности.ПоБеременностиИРодам);
		ЗначенияДляВыбора.Добавить(Метаданные.Документы.БольничныйЛист.ПолноеИмя(), Метаданные.Документы.БольничныйЛист.Синоним);
	КонецЕсли;
	
	Возврат Новый Структура("Отбор,ЗначенияДляВыбораВидаДокумента", Отбор, ЗначенияДляВыбора);

КонецФункции

&НаСервере
Функция ПараметрыЗаполненияПособия()

	ПараметрыЗаполнения = Новый Структура("Основание");
	
	Если Объект.ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ПособиеПриРожденииРебенка Тогда
		ВидПособия = Перечисления.ПособияНазначаемыеФСС.ЕдиновременноеПособиеПриРожденииРебенка;
	Иначе
		ВидПособия = Перечисления.ПособияНазначаемыеФСС.ЕжемесячноеПособиеПоУходуЗаРебенком;
	КонецЕсли;
	
	Основание = Новый Структура;
	Основание.Вставить("ФизическоеЛицо", 	Объект.ФизическоеЛицо);
	Основание.Вставить("Организация", 		Объект.Организация);
	Основание.Вставить("Сотрудник", 		Объект.Сотрудник);
	Основание.Вставить("ВидПособия", 		ВидПособия);
	
	ПараметрыЗаполнения.Основание = Основание;
		
	Возврат ПараметрыЗаполнения;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеДокумента(Форма)

	Описание = ЗаявкиСотрудниковКлиентСервер.НовоеОписаниеДокументаЗаявки();
	Описание.ПроверятьСвязанныеЗаявки 		= Истина;
	Описание.ДоступнаОтменаЗаявки 			= Истина;
	Описание.ДокументыПоЗаявкеИмя 			= "ДокументыПоЗаявке";
	Описание.ДокументыПоЗаявкеДокументИмя 	= "ДокументСсылка";
	Описание.ВыводитьДокументыЗаявки 		= Истина;
	
	Возврат Описание;

КонецФункции

#КонецОбласти