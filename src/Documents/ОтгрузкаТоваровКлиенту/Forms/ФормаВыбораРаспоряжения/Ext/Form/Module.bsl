
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Возврат при получении формы для анализа.
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокРаспоряжений();
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокРаспоряжений

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Закрыть(Элементы.СписокРаспоряжений.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ОбщегоНазначенияУТКлиент.ПроверитьНаличиеВыделенныхВСпискеСтрок(Элементы.СписокРаспоряжений) Тогда
		Закрыть(Элементы.СписокРаспоряжений.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокРаспоряжений()
	
	МенеджерНакладной = Документы.ОтгрузкаТоваровКлиенту;
	
	РеквизитыШапки = МенеджерНакладной.РеквизитыШапки();
	ЗаполнитьЗначенияСвойств(РеквизитыШапки, Параметры);
	
	Распоряжения = МенеджерНакладной.РаспоряженияНакладной(Параметры.Ссылка, РеквизитыШапки);
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(Распоряжения, Параметры.Ссылка);
	
	ПараметрыЗаполнения = МенеджерНакладной.ПараметрыЗаполненияДокумента();
	МенеджерНакладной.ИнициализироватьПараметрыЗаполнения(ПараметрыЗаполнения, РеквизитыШапки, Распоряжения);
	
	ТаблицаНакладная = МенеджерНакладной.ДанныеТаблицыТоварыДокумента(Неопределено);
	ТаблицаНакладная.Колонки.Удалить("Ссылка");
	
	КолонкиСпискаРаспоряжений = Новый Массив();
	ТаблицаРаспоряжений = СписокРаспоряжений.Выгрузить();
	Для Каждого КолонкаТаблицы Из ТаблицаРаспоряжений.Колонки Цикл
	
		Если ТаблицаНакладная.Колонки.Найти(КолонкаТаблицы.Имя) = Неопределено Тогда
			ТаблицаНакладная.Колонки.Добавить(КолонкаТаблицы.Имя, КолонкаТаблицы.ТипЗначения);
		КонецЕсли;
	
		КолонкиСпискаРаспоряжений.Добавить(КолонкаТаблицы.Имя);
		
	КонецЦикла;
	
	МенеджерНакладной.ЗаполнитьПоЗаказамОрдерам(ТаблицаНакладная, Параметры.Ссылка, ПараметрыЗаполнения);
	
	ТаблицаНакладная.Свернуть(СтрСоединить(КолонкиСпискаРаспоряжений, ","));
	
	СписокРаспоряжений.Загрузить(ТаблицаНакладная);
	
КонецПроцедуры

#КонецОбласти

