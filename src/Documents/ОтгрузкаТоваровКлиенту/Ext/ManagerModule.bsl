#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует описание реквизитов объекта, заполняемых по статистике их использования.
//
// Параметры:
//  ОписаниеРеквизитов - Структура - описание реквизитов, для которых необходимо получить значения по статистике
//
//
Процедура ЗадатьОписаниеЗаполняемыхРеквизитовПоСтатистике(ОписаниеРеквизитов) Экспорт
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Автор";
	Параметры.РазрезыСбораСтатистики.ИспользоватьТолькоЗаполненные = "Партнер, Соглашение";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Автор";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов, "Организация", Параметры);	
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьТолькоЗаполненные = "Партнер, Соглашение, Организация";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов, "Менеджер", Параметры);
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Автор, Менеджер";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Автор";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов, "Подразделение", Параметры);	
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Автор";
	Параметры.РазрезыСбораСтатистики.ИспользоватьТолькоЗаполненные = "Партнер, Соглашение";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Автор";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов, "Склад", Параметры);	
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Организация";
	Параметры.РазрезыСбораСтатистики.ИспользоватьТолькоЗаполненные = "Договор, Валюта";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Организация";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов, "БанковскийСчетОрганизации", Параметры);	
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Контрагент";
	Параметры.РазрезыСбораСтатистики.ИспользоватьТолькоЗаполненные = "Договор, Валюта";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Контрагент";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов, "БанковскийСчетКонтрагента", Параметры);
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Склад, Менеджер";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Склад";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов, "Отпустил", Параметры);
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Склад, Менеджер";
	Параметры.РазрезыСбораСтатистики.ИспользоватьТолькоЗаполненные = "Отпустил";
	Параметры.ЗаполнятьПриУсловии.ПоляОбъектаЗаполнены = "Склад";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов, "ОтпустилДолжность", Параметры);
	
КонецПроцедуры

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("Обеспечение");
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("ОперативныйУчетТоваровОрганизаций");
	МеханизмыДокумента.Добавить("Продажи");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("СерийныйУчет");
	МеханизмыДокумента.Добавить("СуммыДокументовВВалютахУчета");
	МеханизмыДокумента.Добавить("АдресныйСклад");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("УчетНДС");
	МеханизмыДокумента.Добавить("ИсправлениеДокументов");
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	
	ОтгрузкаТоваровКлиентуЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);

КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  СписокЗначений, Структура - Данные документа для проведения
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.ОтгрузкаТоваровКлиенту") Тогда
		ДокументОбъект = Документ;
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументОбъект = Документ.ПолучитьОбъект();
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаДвиженияНоменклатураНоменклатура(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРаспоряженияНаОтгрузку(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры);
		
		ОтразитьВУчетеНДС(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
		
		ОтгрузкаТоваровКлиентуЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
	КонецЕсли;
	
	ОтразитьРаспределениеЗапасовДвижения(Запрос, ТекстыЗапроса, Регистры);
	ЗапланироватьОтгрузкуТоваров(Запрос, ТекстыЗапроса, Регистры);
	ОформитьОтгрузкуТоваров(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеДокументов.ДобавитьЗапросыСторноДвижений(Запрос, ТекстыЗапроса, Регистры, ПустаяСсылка().Метаданные());
	РасчетСебестоимостиПроведениеДокументов.ОтразитьВМеханизмеУчетаЗатратИСебестоимости(ДокументСсылка,
		Запрос, ТекстыЗапроса, Регистры);
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//	Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область СозданиеНаОсновании

// Добавляет команду создания документа "Отгрузка товаров клиенту".
//
// Параметры:
//	КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//  Неопределено, СтрокаТаблицыЗначений - Добавить команду создать на основании
Функция ДобавитьКомандуСоздатьНаОснованииЗаказа(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ОтгрузкаТоваровКлиенту) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ОтгрузкаТоваровКлиенту.ПолноеИмя();
		КомандаСоздатьНаОсновании.Обработчик =
			"СозданиеНаОснованииУТКлиент.ОтгрузкаТоваровКлиентуСоздатьНаОсновании";
		КомандаСоздатьНаОсновании.Представление =
			ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ОтгрузкаТоваровКлиенту);
		
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьОтгрузкуБезПереходаПраваСобственности2_5";
		КомандаСоздатьНаОсновании.МножественныйВыбор = Истина;
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Добавляет команду создания документа "Отгрузка товаров клиенту".
//
// Параметры:
//	КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//  Неопределено, СтрокаТаблицыЗначений - Добавить команду создать на основании
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ОтгрузкаТоваровКлиенту) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ОтгрузкаТоваровКлиенту.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление =
			ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ОтгрузкаТоваровКлиенту);
		
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьОтгрузкуБезПереходаПраваСобственности2_5";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет список команд создания на основании.
//
// Параметры:
//	КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//	Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьАктыРасхожденийПослеОтгрузки") Тогда
		
		Команда = Документы.АктОРасхожденияхПослеОтгрузки.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
		Если Команда <> Неопределено Тогда
			Команда.Представление = НСтр("ru = 'Акт о расхождениях после отгрузки товаров клиенту и до перехода права собственности';
										|en = 'Sales dispute after goods shipment to customer and before transfer of title to goods'");
		КонецЕсли;
		
	КонецЕсли;
		
	Документы.ПоручениеЭкспедитору.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	Документы.РасходныйОрдерНаТовары.ДобавитьКомандуСоздатьНаОснованииРасходныйОрдерНаТовары(КомандыСозданияНаОсновании);
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	Справочники.Претензии.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	ИсправлениеДокументов.ДобавитьКомандуИсправление(КомандыСозданияНаОсновании, ПустаяСсылка().Метаданные());
	ИсправлениеДокументов.ДобавитьКомандуСторно(КомандыСозданияНаОсновании, ПустаяСсылка().Метаданные());
	
	Документы.ПоступлениеТоваровОтКлиента.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	Документы.РеализацияТоваровУслуг.ДобавитьКомандуСоздатьПереходПраваСобственностиНаОснованииЗаказа(
		КомандыСозданияНаОсновании);
	
	Команда = Документы.ОприходованиеИзлишковОтгруженныхТоваров.ДобавитьКомандуСоздатьНаОсновании(
		КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.Представление = НСтр("ru = 'Оприходование излишков товаров, отгруженных клиенту';
									|en = 'Recognition of stock surplus shipped to customer'");
	КонецЕсли;

	Команда = Документы.СписаниеОтгруженныхТоваров.ДобавитьКомандуСоздатьНаОсновании(
		КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.Представление = НСтр("ru = 'Списание товаров, отгруженных клиенту';
									|en = 'Write-off of goods shipped to customer'");
	КонецЕсли;
	
	ОтгрузкаТоваровКлиентуЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);
	
КонецПроцедуры

#КонецОбласти

#Область Отчеты

// Определяет список команд отчетов.
//
// Параметры:
//	КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//	Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Отчеты.ОстаткиТоваровОрганизаций.ДобавитьКомандуОтчета(КомандыОтчетов);;
	
	КомандаОтчет = Отчеты.СостояниеВыполненияДокументов.ДобавитьКомандуСостояниеВыполненияРеализацииАкта(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		КомандаОтчет.Порядок = 1;
	КонецЕсли;
	
	ОтгрузкаТоваровКлиентуЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область ПроводкиРеглУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	Возврат ОтгрузкаТоваровКлиентуЛокализация.ТекстОтраженияВРеглУчете();
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
// 
// Возвращаемое значение:
//  Строка - Текст запроса временной таблицы отражения в регл учете
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат ОтгрузкаТоваровКлиентуЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

#Область Назначения

// Инициализирует параметры, обслуживающие выбор назначений в формах документа.
//
//  Возвращаемое значение:
//  См. Справочники.Назначения.МакетФормыВыбораНазначений
//
Функция МакетФормыВыбораНазначений() Экспорт
	
	МакетФормы = Справочники.Назначения.МакетФормыВыбораНазначений();
	
	ШаблонНазначения = Справочники.Назначения.ДобавитьШаблонНазначений(МакетФормы);
	ШаблонНазначения.НаправлениеДеятельности = "Объект.НаправлениеДеятельности";
	ШаблонНазначения.ВидимыеОтборыНаФорме.Вставить("НаправлениеДеятельности", НСтр("ru = 'Только назначения направления деятельности ""%1""';
																					|en = 'The ""%1"" line of business assignments only'"));
	
	// Остатки товаров на складе-отправителе.
	ОписаниеКолонок = Справочники.Назначения.ДобавитьОписаниеКолонок(МакетФормы, "ОбеспечениеЗаказов", Истина, "Объект.Товары.Назначение");
	ОписаниеКолонок.Колонки.НайтиПоЗначению("ВНаличии").Пометка = Истина;
	
	ОписаниеКолонок.ПутиКДанным.Номенклатура     = "Объект.Товары.Номенклатура";
	ОписаниеКолонок.ПутиКДанным.Характеристика   = "Объект.Товары.Характеристика";
	ОписаниеКолонок.ПутиКДанным.Склад            = "Объект.Товары.Склад";
	
	Возврат МакетФормы;
	
КонецФункции

// Порядок обработки документа при изменении направления деятельности.
//
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения.
//
// Возвращаемое значение:
// 	см. НаправленияДеятельностиСервер.ПорядокОбработкиДокументаПриИзмененииНаправленияДеятельности.
//
Функция ПорядокОбработкиДокументаПриИзмененииНаправленияДеятельности(Форма) Экспорт
	
	ПорядокОбработкиДокумента = НаправленияДеятельностиСервер.ПорядокОбработкиДокументаПриИзмененииНаправленияДеятельности();
	ПорядокОбработкиДокумента.ИменаТабличныхЧастейДляЗаполненияНазначения = "Товары";
	ТаблицаУсловий = НаправленияДеятельностиСервер.УсловияОбработкиНазначенийВСтроках("ТипНоменклатуры");
	ПорядокОбработкиДокумента.УсловияОбработкиСтрок.Вставить("Товары", ТаблицаУсловий);
	ПорядокОбработкиДокумента.ИмяЭлементаФормыОбособленно = "ТоварыОбособленно";
	ПорядокОбработкиДокумента.ИмяГруппыЭлементовКомандыОбособленно = "ТоварыГруппаКомандыОбособленно";
	ПорядокОбработкиДокумента.ИмяРеквизитаПоЗаказу = "ОтгрузкаПоЗаказам";
	
	Возврат ПорядокОбработкиДокумента;
	
КонецФункции

#КонецОбласти

#Область Серии

// Имена реквизитов, от значений которых зависят параметры указания серий.
//
// Возвращаемое значение:
//	Строка - имена реквизитов, перечисленные через запятую.
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	
	ИменаРеквизитов = "Склад,ХозяйственнаяОперация,Дата";
	
	Возврат ИменаРеквизитов;
	
КонецФункции

// Возвращает параметры указания серий для товаров, указанных в документе.
//
// Параметры:
//	Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий.
//
// Возвращаемое значение:
//	см. НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	ИспользоватьСерииНоменклатуры  =
		ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатурыСклад", Новый Структура());
	УчитыватьСебестоимостьПоСериям =
		ПолучитьФункциональнуюОпцию("УчитыватьСебестоимостьПоСериямСклад", Новый Структура());
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ОтгрузкаТоваровКлиенту";
	
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = УчитыватьСебестоимостьПоСериям;
	
	ПараметрыУказанияСерий.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ОтгрузкаКлиенту);
	
	ПараметрыУказанияСерий.ЭтоНакладная = Истина;
	ПараметрыУказанияСерий.Дата         = Объект.Дата;
	
	ПараметрыУказанияСерий.ПланированиеОтбора = Истина;
	ПараметрыУказанияСерий.ФактОтбора         = Истина;
	
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Склад");
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Назначение");
	
	ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Добавить("СтатусУказанияСерий");
	ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Добавить("СтатусУказанияСерийНаСкладах");
	ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Добавить("СтатусУказанияСерийОтгруженныхТоваров");
	
	ПараметрыУказанияСерий.ИменаПолейДляОпределенияРаспоряжения.Добавить("ОтгрузкаПоЗаказам");
	ПараметрыУказанияСерий.ИменаПолейДляОпределенияРаспоряжения.Добавить("Товары_ЗаказКлиента");
	ПараметрыУказанияСерий.ИменаПолейДляОпределенияРаспоряжения.Добавить("Товары_КодСтроки");
	
	ПараметрыУказанияСерий.РегистрироватьСерии = НоменклатураКлиентСервер.НеобходимоРегистрироватьСерии(
													ПараметрыУказанияСерий);
	
	ПараметрыУказанияСерий.ПараметрыЗапроса.Вставить("ХозяйственнаяОперация", Объект.ХозяйственнаяОперация);
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания серий.
//
// Параметры:
//	ПараметрыУказанияСерий - см. НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//
// Возвращаемое значение:
//	Строка - текст запроса.
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Товары.НомерСтроки         КАК НомерСтроки,
	|	Товары.Склад               КАК Склад,
	|	Товары.Номенклатура        КАК Номенклатура,
	|	Товары.Характеристика      КАК Характеристика,
	|	Товары.Назначение          КАК Назначение,
	|	Товары.Серия               КАК Серия,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	Товары.СтатусУказанияСерийНаСкладах КАК СтатусУказанияСерийНаСкладах,
	|	Товары.СтатусУказанияСерийОтгруженныхТоваров КАК СтатусУказанияСерийОтгруженныхТоваров,
	|	Товары.Количество          КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	Товары.Номенклатура      КАК Номенклатура,
	|	Товары.Характеристика    КАК Характеристика,
	|	Товары.Назначение        КАК Назначение,
	|	Товары.Серия             КАК Серия,
	|	Товары.Склад             КАК Склад,
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры КАК ВидНоменклатуры,
	|	СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыДляЗапроса
	|ИЗ
	|	Товары КАК Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	Товары.Серия,
	|	Товары.Склад,
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	Серии.Номенклатура   КАК Номенклатура,
	|	Серии.Характеристика КАК Характеристика,
	|	Серии.Назначение     КАК Назначение,
	|	Серии.Склад          КАК Склад,
	|	Серии.Количество     КАК Количество
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	&Серии КАК Серии
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	Серии.Номенклатура      КАК Номенклатура,
	|	Серии.Характеристика    КАК Характеристика,
	|	Серии.Назначение        КАК Назначение,
	|	Серии.Склад             КАК Склад,
	|	СУММА(Серии.Количество) КАК Количество
	|ПОМЕСТИТЬ СерииДляЗапроса
	|ИЗ
	|	Серии КАК Серии
	|
	|СГРУППИРОВАТЬ ПО
	|	Серии.Номенклатура,
	|	Серии.Характеристика,
	|	Серии.Назначение,
	|	Серии.Склад
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 4
	|ВЫБРАТЬ
	|	Товары.НомерСтроки         КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	Товары.СтатусУказанияСерийНаСкладах КАК СтарыйСтатусУказанияСерийНаСкладах,
	|	Товары.СтатусУказанияСерийОтгруженныхТоваров КАК СтарыйСтатусУказанияСерийОтгруженныхТоваров,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА 14
	|					ИНАЧЕ 13
	|				КОНЕЦ
	|		КОГДА &ТолькоСерииДляСебестоимости
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтгрузки
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА 10
	|					ИНАЧЕ 9
	|				КОНЕЦ
	|		КОГДА Склады.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				И &Дата >= Склады.ДатаНачалаОрдернойСхемыПриОтгрузке
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтбора
	|				И &ПланированиеОтбора
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчетСерийПоFEFO
	|						ТОГДА
	|							ВЫБОР
	|								КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|										И ТоварыДляЗапроса.Количество > 0
	|									ТОГДА 6
	|								ИНАЧЕ 5
	|							КОНЕЦ
	|					ИНАЧЕ
	|						ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|									И ТоварыДляЗапроса.Количество > 0
	|								ТОГДА 8
	|							ИНАЧЕ 7
	|						КОНЕЦ
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПоФактуОтбора
	|				И НЕ (Склады.ИспользоватьОрдернуюСхемуПриОтгрузке
	|						И &Дата >= Склады.ДатаНачалаОрдернойСхемыПриОтгрузке)
	|				И &ФактОтбора
	|				И ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриОтгрузкеКлиенту
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьОстаткиСерий
	|						ТОГДА
	|							ВЫБОР
	|								КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|										И ТоварыДляЗапроса.Количество > 0
	|									ТОГДА 4
	|								ИНАЧЕ 3
	|							КОНЕЦ
	|					ИНАЧЕ
	|						ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|									И ТоварыДляЗапроса.Количество > 0
	|								ТОГДА 2
	|							ИНАЧЕ 1
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ                    КАК СтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ТоварыДляЗапроса.ВидНоменклатуры.ПолитикаУчетаСерий ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ
	|			ВЫБОР
	|				КОГДА ТоварыДляЗапроса.ВидНоменклатуры.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|						И ТоварыДляЗапроса.ВидНоменклатуры.ПолитикаУчетаСерий.УчетСерийТоваровОтгруженныхКлиенту
	|					ТОГДА
	|						ВЫБОР
	|							КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА 18
	|							ИНАЧЕ 17
	|						КОНЕЦ
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ                    КАК СтатусУказанияСерийОтгруженныхТоваров
	|ПОМЕСТИТЬ Статусы
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляЗапроса КАК ТоварыДляЗапроса
	|		ПО Товары.Номенклатура = ТоварыДляЗапроса.Номенклатура
	|			И Товары.Характеристика = ТоварыДляЗапроса.Характеристика
	|			И Товары.Назначение = ТоварыДляЗапроса.Назначение
	|			И Товары.Склад = ТоварыДляЗапроса.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ СерииДляЗапроса КАК СерииДляЗапроса
	|		ПО ТоварыДляЗапроса.Номенклатура = СерииДляЗапроса.Номенклатура
	|			И ТоварыДляЗапроса.Характеристика = СерииДляЗапроса.Характеристика
	|			И ТоварыДляЗапроса.Назначение = СерииДляЗапроса.Назначение
	|			И ТоварыДляЗапроса.Склад = СерииДляЗапроса.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|		ПО ТоварыДляЗапроса.ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка
	|			И ТоварыДляЗапроса.Склад = ПолитикиУчетаСерий.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|			ПО ТоварыДляЗапроса.Склад = Склады.Ссылка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 5
	|ВЫБРАТЬ
	|	Статусы.НомерСтроки                           КАК НомерСтроки,
	|	Статусы.СтатусУказанияСерий                   КАК СтатусУказанияСерийНаСкладах,
	|	Статусы.СтатусУказанияСерийОтгруженныхТоваров КАК СтатусУказанияСерийОтгруженныхТоваров,
	|	ВЫБОР
	|		КОГДА Статусы.СтатусУказанияСерий = 0
	|			ТОГДА Статусы.СтатусУказанияСерийОтгруженныхТоваров
	|		ИНАЧЕ Статусы.СтатусУказанияСерий
	|	КОНЕЦ                                         КАК СтатусУказанияСерий
	|ИЗ
	|	Статусы КАК Статусы
	|ГДЕ
	|	ВЫБОР
	|		КОГДА Статусы.СтатусУказанияСерий = 0
	|			ТОГДА Статусы.СтатусУказанияСерийОтгруженныхТоваров
	|		ИНАЧЕ Статусы.СтатусУказанияСерий
	|	КОНЕЦ <> Статусы.СтарыйСтатусУказанияСерий
	|	ИЛИ Статусы.СтатусУказанияСерий <> Статусы.СтарыйСтатусУказанияСерийНаСкладах
	|	ИЛИ Статусы.СтатусУказанияСерийОтгруженныхТоваров <> Статусы.СтарыйСтатусУказанияСерийОтгруженныхТоваров
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает значение распоряжения на отгрузку.
//
// Параметры:
//	ЗначенияПолейДляОпределенияРаспоряжения - Структура - состав полей определяется значением
//														поля ИменаПолейДляОпределенияРаспоряжения параметров указания серий этого документа.
//
// Возвращаемое значение:
//	ДокументСсылка - ссылка на распоряжение для выполнения складской операции
//
Функция РаспоряжениеНаВыполнениеСкладскойОперации(ЗначенияПолейДляОпределенияРаспоряжения) Экспорт
	
	Если ЗначенияПолейДляОпределенияРаспоряжения.Товары_КодСтроки <> 0 Тогда
		Возврат ЗначенияПолейДляОпределенияРаспоряжения.Товары_ЗаказКлиента;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область Доставка

// Функция возвращает текст запроса для определения реквизитов доставки.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстЗапросаРеквизитыДоставки() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Шапка.Номер             КАК Номер,
	|	Шапка.Проведен          КАК Проведен,
	|	Шапка.Ссылка            КАК Ссылка,
	|	Шапка.Дата              КАК Дата,
	|	Шапка.Партнер           КАК ПолучательОтправитель,
	|	Шапка.ПеревозчикПартнер КАК Перевозчик,
	|	Шапка.СпособДоставки    КАК СпособДоставки,
	|	Шапка.ЗонаДоставки      КАК Зона,
	|
	|	ВЫБОР КОГДА СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчикаПоАдресу)
	|		ТОГДА Шапка.АдресДоставкиПеревозчика
	|		ИНАЧЕ Шапка.АдресДоставки
	|		КОНЕЦ               КАК Адрес,
	|
	|	ВЫБОР КОГДА СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчикаПоАдресу)
	|		ТОГДА Шапка.АдресДоставкиПеревозчикаЗначенияПолей
	|		ИНАЧЕ Шапка.АдресДоставкиЗначенияПолей
	|		КОНЕЦ               КАК АдресЗначенияПолей,
	|
	|	Шапка.ВремяДоставкиС    КАК ВремяС,
	|	Шапка.ВремяДоставкиПо   КАК ВремяПо,
	|	Шапка.ДополнительнаяИнформацияПоДоставке
	|		                    КАК ДополнительнаяИнформация,
	|	Т.Склад                 КАК Склад,
	|	ИСТИНА                  КАК ДоставитьПолностью,
	|	Шапка.ОсобыеУсловияПеревозки КАК ОсобыеУсловияПеревозки,
	|	Шапка.ОсобыеУсловияПеревозкиОписание КАК ОсобыеУсловияПеревозкиОписание,
	|	Шапка.Соглашение.РазбиватьРасходныеОрдераПоРаспоряжениям КАК РазбиватьРасходныеОрдераПоРаспоряжениям
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Т.Ссылка КАК Ссылка,
	|		Т.Склад КАК Склад
	|	ИЗ
	|		Документ.ОтгрузкаТоваровКлиенту.Товары КАК Т
	|	ГДЕ
	|		Т.Ссылка В(&Ссылки)
	|	) КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровКлиенту КАК Шапка
	|		ПО (Шапка.Ссылка = Т.Ссылка)
	|ГДЕ
	|	НЕ Шапка.ОтгрузкаПоЗаказам
	|		ИЛИ НЕ &ИспользоватьРасширенныеВозможностиЗаказаКлиента
	|		ИЛИ Шапка.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчика)
	|				И НЕ &ИспользоватьЗаданияНаПеревозкуДляУчетаДоставкиПеревозчиками";
	
	ПодставитьТекстИспользоватьРасширенныеВозможностиЗаказаКлиента(ТекстЗапроса, "Шапка");
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область СостояниеВыполненияДокументов

// Осуществляет инициализацию структуры состояния выполнения документа.
//
// Возвращаемое значение:
//	см. Отчеты.СостояниеВыполненияДокументов.ИнициализироватьСтруктуруСостояниеВыполненияДокумента.
//
Функция СтруктураСостояниеВыполненияДокумента() Экспорт
	
	СтруктураСостояние = Отчеты.СостояниеВыполненияДокументов.ИнициализироватьСтруктуруСостояниеВыполненияДокумента();
	
	СтруктураДопЗапросов = Новый Структура;
	СтруктураДопЗапросов.Вставить("ТекстЗапросаВТТоварыРаспоряжениеНакладнаяОтгружено",
		ТекстЗапросаВТТоварыРаспоряжениеНакладнаяОтгружено());
	СтруктураДопЗапросов.Вставить("ТекстЗапросаВтДокументы",
		ТекстЗапросаВтДокументы());
	
	СтруктураСостояние.Вставить("ЭтоНакладная", Истина);
	СтруктураСостояние.Вставить("ВыводитьТаблицыОтгрузка", 1);
	СтруктураСостояние.Вставить("ВыводитьТаблицуППС", 2);
	СтруктураСостояние.Вставить("ИмяТЧТоварыОтгрузка", "Товары");
	СтруктураСостояние.Вставить("ИмяПоляНакладнаяПоЗаказам", "ОтгрузкаПоЗаказам");
	СтруктураСостояние.Вставить("ЕстьСуммовыеПоказателиОтгрузки", Истина);
	СтруктураСостояние.Вставить("СтруктураДопЗапросов", СтруктураДопЗапросов);
	СтруктураСостояние.Вставить("ИмяПоляСумма", "Сумма");
	
	Возврат СтруктураСостояние;
	
КонецФункции

#КонецОбласти

#Область ОснованиеДляПечати

// Возвращает структуру основания по данными документа
//
// Параметры:
//	Объект - ДанныеФормыСтруктура, ДокументОбъект.ОтгрузкаТоваровКлиенту - Объект документа, по которому необходимо
//																			получить текст основания.
//
// Возвращаемое значение:
//	Структура- Структура с наименованием, датой и номером основания.
//
Функция СтруктураОснованияДляПечати(Объект) Экспорт
	
	ЗаказОснование = Неопределено;
	Если Объект.ОтгрузкаПоЗаказам Тогда
	
		Если ЗначениеЗаполнено(Объект.ЗаказКлиента) Тогда
			
			Если ТипЗнч(Объект.ЗаказКлиента) <> Тип("ДокументСсылка.ОтгрузкаТоваровКлиенту") Тогда
				ЗаказОснование = Объект.ЗаказКлиента;
			КонецЕсли;
		
		Иначе
			
			Для Каждого Стр Из Объект.Товары Цикл
				
				Если ЗначениеЗаполнено(Стр.ЗаказКлиента)
					И ТипЗнч(Стр.ЗаказКлиента) <> Тип("ДокументСсылка.ОтгрузкаТоваровКлиенту") Тогда
					
					ЗаказОснование = Стр.ЗаказКлиента;
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
		
		КонецЕсли;
	
	КонецЕсли;
	
	ПорядокРасчетов = ВзаиморасчетыСервер.ПорядокРасчетов(Ложь, ЗаказОснование, Объект.Соглашение, Объект.Договор);
	
	СтруктураОснования = СтруктураОснования(Объект, ПорядокРасчетов);
	
	Если ЗначениеЗаполнено(СтруктураОснования.Основание) Тогда
		СтруктураОснования.Основание = СтруктураОснования.Основание + ", " + НСтр("ru = 'отгрузка клиенту';
																					|en = 'shipment to customer'");
	Иначе
		СтруктураОснования.Основание = НСтр("ru = 'Отгрузка клиенту';
											|en = 'shipment to customer'");
	КонецЕсли;
	
	Возврат СтруктураОснования;
	
КонецФункции

// Возвращает таблицу значений по умолчанию для реквизита "Основание".
//
// Параметры:
//	Объект - ДанныеФормыСтруктура, ДокументОбъект.ОтгрузкаТоваровКлиенту - объект документа, по которому необходимо
//																			получить список выбора.
//
// Возвращаемое значение:
//	ТаблицаЗначений - Таблица значений с реквизитами оснований.
//
Функция ТаблицаОснованийДляПечати(Объект) Экспорт
	
	ТаблицаОснований = Новый ТаблицаЗначений;
	ТаблицаОснований.Колонки.Добавить("Основание", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(300)));
	ТаблицаОснований.Колонки.Добавить("ОснованиеДата",
		Новый ОписаниеТипов("Дата",,,,, Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	ТаблицаОснований.Колонки.Добавить("ОснованиеНомер",
		Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(128)));
	
	СтруктураОснования = СтруктураОснования(Объект, Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов);
	
	Если ЗначениеЗаполнено(СтруктураОснования.Основание) Тогда
		
		ДобавленнаяСтрока = ТаблицаОснований.Добавить();
		ЗаполнитьЗначенияСвойств(ДобавленнаяСтрока, СтруктураОснования);
		
		ДобавленнаяСтрока.Основание = СтруктураОснования.Основание + ", " + НСтр("ru = 'отгрузка клиенту';
																				|en = 'shipment to customer'");
		
		Если ЗначениеЗаполнено(СтруктураОснования.ОснованиеДата)
			И ЗначениеЗаполнено(СтруктураОснования.ОснованиеНомер) Тогда
			
			ТекстРасширенный = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 № %2 от %3';
					|en = '%1 No. %2 from %3'"),
				СтруктураОснования.Основание,
				СтруктураОснования.ОснованиеНомер,
				Формат(СтруктураОснования.ОснованиеДата, "ДЛФ=DD"));
			
			ТекстРасширенный = ТекстРасширенный + ", " + НСтр("ru = 'отгрузка клиенту';
																|en = 'shipment to customer'");
			
			ДобавленнаяСтрока = ТаблицаОснований.Добавить();
			ЗаполнитьЗначенияСвойств(ДобавленнаяСтрока, СтруктураОснования);
			
			ДобавленнаяСтрока.Основание = ТекстРасширенный;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СтруктураОснования = СтруктураОснования(Объект, Перечисления.ПорядокРасчетов.ПоЗаказам);
	
	Если ЗначениеЗаполнено(СтруктураОснования.Основание) Тогда
		ДобавленнаяСтрока = ТаблицаОснований.Добавить();
		ЗаполнитьЗначенияСвойств(ДобавленнаяСтрока, СтруктураОснования);
		ДобавленнаяСтрока.Основание = СтруктураОснования.Основание + ", " + НСтр("ru = 'отгрузка клиенту';
																				|en = 'shipment to customer'");
	КонецЕсли;
	
	Если ТаблицаОснований.Количество() = 0 Тогда
		ДобавленнаяСтрока = ТаблицаОснований.Добавить();
		ДобавленнаяСтрока.Основание = НСтр("ru = 'Отгрузка клиенту';
											|en = 'shipment to customer'");
	КонецЕсли;
	
	Возврат ТаблицаОснований;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Позволяет указать объекты метаданных, для которых задана логика ограничения доступа к данным.
//
// Параметры:
//	Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Партнер)
	|	И ЗначениеРазрешено(Склад)
	|	И ЗначениеРазрешено(Подразделение)
	|	И ЗначениеРазрешено(ХозяйственнаяОперация)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Осуществляет проверку заполненности проверяемых реквизитов.
//
// Параметры:
//	 ОтгрузкаТоваров    - ДокументСсылка.ОтгрузкаТоваровКлиенту - Документ, на основании которого осуществляется ввод
//	 Статус             - ПеречислениеСсылка - Статус документа, на основании которого осуществляется ввод
//	 ЕстьОшибкиПроведен - Булево - Если Истина - документ, на основании которого осуществляется ввод, не проведен
//
Процедура ПроверитьВозможностьВводаНаОсновании(ОтгрузкаТоваров, Статус = Неопределено,
	ЕстьОшибкиПроведен = Ложь) Экспорт
	
	ПараметрыПроверки = ОбщегоНазначенияУТ.ПараметрыПроверкиВозможностиВводаНаОсновании();
	ПараметрыПроверки.ЕстьОшибкиПроведен = ЕстьОшибкиПроведен;
	ПараметрыПроверки.ЕстьОшибкиСтатус = Ложь;
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОснованииСПараметрами(ОтгрузкаТоваров, ПараметрыПроверки);
	
КонецПроцедуры

// Функция выполняет проверки документа-распоряжения при создании расходного ордера.
//
// Параметры:
//	Распоряжение - ОпределяемыйТип.РаспоряжениеНаОтгрузку - ссылка на документ-распоряжение.
//	Склад        - СправочникСсылка.Склады                - ссылка на элемент справочника Склады.
//
// Возвращаемое значение:
//	Структура - результаты проверки:
//	* Операция                   - Строка - Тип операции. "Отказ", если проверки прошли с ошибками
//	* ТекстОшибки                - Строка - Обязательно должен быть заполнен, когда Операция = "Отказ"
//	* ГруппаСкладовВРаспоряжении - Булево - В распоряжении выбрана группа складов
//	* ДатаОтгрузки               - Дата - 
//	* ИспользоватьОрдернуюСхему  - Булево - На складе используется ордерная схема.
//
Функция ВыполнитьПроверкиРаспоряжения(Распоряжение, Склад) Экспорт
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Операция", "");
	СтруктураВозврата.Вставить("ТекстОшибки", "");
	СтруктураВозврата.Вставить("ГруппаСкладовВРаспоряжении", Ложь);
	СтруктураВозврата.Вставить("ДатаОтгрузки");
	СтруктураВозврата.Вставить("ИспользоватьОрдернуюСхему");
	
	Если Не ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Распоряжение, "Склад")) Тогда
		
		ТекстОшибки = НСтр("ru = 'Не заполнено поле ""Склад"" в документе ""%Распоряжение%"". Невозможно создать ордер.';
							|en = '""Warehouse"" in the ""%Распоряжение%"" document is not filled in. Cannot create a note.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Распоряжение%", Строка(Распоряжение));
		
		СтруктураВозврата.ТекстОшибки = ТекстОшибки;
		СтруктураВозврата.Операция    = "Отказ";
		
		Возврат СтруктураВозврата;
		
	КонецЕсли;
	
	СтруктураГруппыСкладов = Новый Структура("ЭтоГруппа", "Склад.ЭтоГруппа");
	ЗначениеГруппыСкладов  = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Распоряжение, СтруктураГруппыСкладов);
	
	ГруппаСкладовВРаспоряжении = ЗначениеГруппыСкладов.ЭтоГруппа;
	СтруктураВозврата.ГруппаСкладовВРаспоряжении = ГруппаСкладовВРаспоряжении; 
	
	Реквизиты = Новый Структура("ДатаОтгрузки, Склад, ИспользоватьОрдернуюСхему", "Дата");
	
	Если Не ГруппаСкладовВРаспоряжении Тогда
		
		Реквизиты.Склад = "Склад";
		Реквизиты.ИспользоватьОрдернуюСхему = "
		|ВЫБОР
		|	КОГДА ЕстьNULL(Склад.ИспользоватьОрдернуюСхемуПриОтгрузке, Ложь)
		|			И Дата >= ЕстьNULL(Склад.ДатаНачалаОрдернойСхемыПриОтгрузке, ДАТАВРЕМЯ(1,1,1))
		|		ТОГДА ИСТИНА
		|	ИНАЧЕ ЛОЖЬ
		|КОНЕЦ";
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Распоряжение, Реквизиты);
		
		ИспользоватьОрдернуюСхему = ЗначенияРеквизитов.ИспользоватьОрдернуюСхему;
		СтруктураВозврата.ИспользоватьОрдернуюСхему = ИспользоватьОрдернуюСхему;
		
		Если ИспользоватьОрдернуюСхему Тогда
			Склад = ЗначенияРеквизитов.Склад;
			
			ЗаполнитьЗначенияСвойств(СтруктураВозврата, ЗначенияРеквизитов, "ДатаОтгрузки");
		Иначе
			
			ТекстОшибки = НСтр("ru = 'Для склада ""%Склад%"" оформление расходных ордеров не требуется.
								|Заполнение документа не выполнено.';
								|en = 'For warehouse ""%Склад%"" it is not required to create goods issues.
								|The document is not completed.'");
			
			СтруктураВозврата.ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Склад%", Строка(ЗначенияРеквизитов.Склад));
			СтруктураВозврата.Операция    = "Отказ";
			
			Возврат СтруктураВозврата;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ГруппаСкладовВРаспоряжении Тогда
		
		Если Не ЗначениеЗаполнено(Склад) Тогда
			
			МассивСкладов = Документы.РасходныйОрдерНаТовары.ОрдерныеСкладыПриОтгрузке(Распоряжение, Неопределено);
			
			Если МассивСкладов.Количество() = 0 Тогда
				
				ТекстОшибки = НСтр("ru = 'Для складов документа ""%Распоряжение%"" оформление приходных ордеров не требуется.
									|Заполнение документа не выполнено.';
									|en = 'It is not required to register goods receipts for warehouses of the ""%Распоряжение%"" document.
									|The document is not populated.'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Распоряжение%", Строка(Распоряжение));
				
				СтруктураВозврата.ТекстОшибки = ТекстОшибки;
				СтруктураВозврата.Операция    = "Отказ";
				
				Возврат СтруктураВозврата;
				
			ИначеЕсли МассивСкладов.Количество() = 1 Тогда
				
				Склад = МассивСкладов[0];
				
			Иначе
				
				СтруктураВозврата.Операция = "Возврат";
				
				Возврат СтруктураВозврата;
				
			КонецЕсли;
			
		Иначе
			
			СтруктураВозврата.Операция = "Возврат";
			
			Возврат СтруктураВозврата;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтруктураВозврата
	
КонецФункции

// Возвращает пустые значения заказов.
//
// Возвращаемое значение:
//	Массив - массив пустых значений.
//
Функция ПустыеЗначенияЗаказов() Экспорт
	
	ПустыеЗначенияЗаказов = Новый Массив;
	ПустыеЗначенияЗаказов.Добавить(Неопределено);
	ПустыеЗначенияЗаказов.Добавить(Документы.ЗаказКлиента.ПустаяСсылка());
	
	Возврат ПустыеЗначенияЗаказов;
	
КонецФункции

// Формирует массив допустимых статусов на основании настроек программы
//
// Параметры:
//	ИмяДокумента - Строка - Имя вводимого документа.
//
// Возвращаемое значение:
//  Массив - массив допустимых статусов.
//
Функция ДопустимыеСтатусыВводаНаОсновании(ИмяДокумента = "") Экспорт
	
	МассивДопустимыхСтатусов = Новый Массив;
	МассивДопустимыхСтатусов.Добавить(Неопределено);
	
	Возврат МассивДопустимыхСтатусов;
	
КонецФункции

// Возвращает структуру с наименованием табличных частей документа, хранящих информацию о товарах.
//
// Возвращаемое значение:
//	Структура - коллекция, содержащая следующие наименования табличных частей:
//		* Товары - ТаблицаЗначений, ТабличнаяЧасть, Неопределено - данные о товарах документа.
//	
Функция КоллекцияТабличныхЧастейТоваров() Экспорт
	
	ТаблицыДокумента = Новый Структура("Товары");
	
	Возврат ТаблицыДокумента;
	
КонецФункции

// Возвращает таблицу Товары
//
// Параметры:
//  Накладная - ДокументСсылка.ОтгрузкаТоваровКлиенту - ссылка на накладную ТЧ Товары которой необходимо вернуть
//
// Возвращаемое значение:
//  ТаблицаЗначений - Табличная часть документа поступления товаров.
//
Функция ДанныеТаблицыТоварыДокумента(Накладная) Экспорт
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаТовары.Характеристика КАК Характеристика,
		|	ТаблицаТовары.Назначение КАК Назначение,
		|	ТаблицаТовары.Упаковка КАК Упаковка,
		|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ТаблицаТовары.Количество КАК Количество,
		|	ТаблицаТовары.ВидЦены КАК ВидЦены,
		|	ТаблицаТовары.Цена КАК Цена,
		|	ТаблицаТовары.Сумма КАК Сумма,
		|	ТаблицаТовары.КодСтроки КАК КодСтроки,
		|	ТаблицаТовары.Склад КАК Склад,
		|	ТаблицаТовары.СтатусУказанияСерий КАК СтатусУказанияСерий,
		|	ТаблицаТовары.ЗаказКлиента КАК ЗаказКлиента,
		|	ТаблицаТовары.СрокПоставки КАК СрокПоставки,
		|	ТаблицаТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ТаблицаТовары.Серия КАК Серия,
		|	ТаблицаТовары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ТаблицаТовары.НоменклатураНабора КАК НоменклатураНабора,
		|	ТаблицаТовары.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	ТаблицаТовары.АналитикаУчетаНаборов КАК АналитикаУчетаНаборов,
		|	ТаблицаТовары.КодТНВЭД КАК КодТНВЭД,
		|	ТаблицаТовары.АналитикаУчетаНоменклатурыОтгрузки КАК АналитикаУчетаНоменклатурыОтгрузки,
		|	ТаблицаТовары.СтатусУказанияСерийОтгруженныхТоваров КАК СтатусУказанияСерийОтгруженныхТоваров
		|ИЗ
		|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ТаблицаТовары
		|ГДЕ
		|	ТаблицаТовары.Ссылка = &Накладная";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Накладная", Накладная);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

// Процедура пересчитывает поле КоличествоУпаковок и другие зависимые поля
//
// Параметры:
//  Товары				 - ДанныеФормыКоллекция - Табличная часть "Товары"
//  ПараметрыЗаполнения	 - Структура -
//
Процедура ОбновитьЗависимыеРеквизитыТабличнойЧасти(Товары, ПараметрыЗаполнения) Экспорт
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
	
	КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьТЧ(
		Товары,
		СтруктураДействий,
		КэшированныеЗначения);
	
КонецПроцедуры

// Возвращает структуру параметров для заполнения налогообложения НДС продажи.
//
// Параметры:
//  Объект - ДокументОбъект.ОтгрузкаТоваровКлиенту, ДокументСсылка.ОтгрузкаТоваровКлиенту, ДанныеФормыСтруктура -
//  документ, по которому необходимо сформировать параметры.
//
// Возвращаемое значение:
//  См. УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалогообложенияНДСПродажи
//
Функция ПараметрыЗаполненияНалогообложенияНДСПродажи(Объект) Экспорт
	
	ПараметрыЗаполнения = УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалогообложенияНДСПродажи();
	
	РеквизитыДокумента = 
		"Дата,
		|Договор,
		|НаправлениеДеятельности,
		|Организация,
		|Подразделение,
		|Склад";
	
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.ОтгрузкаТоваровКлиенту")
		Или ТипЗнч(Объект) = Тип("ДанныеФормыСтруктура") Тогда
		ДанныеОбъекта = Объект;
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументСсылка.ОтгрузкаТоваровКлиенту") Тогда
		ДанныеОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект, РеквизитыДокумента);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения, ДанныеОбъекта, РеквизитыДокумента);
	ПараметрыЗаполнения.РеализацияТоваров = Истина;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

// Возвращает текст запроса для временной таблицы ВтТаблицаВидыЗапасов
// 
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//
// Возвращаемое значение:
//	Строка - 
//
Функция ТекстЗапросаВтТаблицаВидыЗапасовДекоратор(Запрос, ТекстыЗапроса) Экспорт

	Возврат ТекстЗапросаВтТаблицаВидыЗапасов(Запрос, ТекстыЗапроса);
	
КонецФункции

#Область УчетНДС

// Инициализирует параметры регистрации счетов-фактур (выданных)
//
// Параметры:
//  Объект		- ДокументОбъект.ОтгрузкаТоваровКлиенту, ДанныеФормыСтруктура	- документ, для которого необходимо получить параметры.
//
// Возвращаемое значение:
//  См. УчетНДСУПКлиентСервер.ПараметрыРегистрацииСчетовФактурВыданных
//
Функция ПараметрыРегистрацииСчетовФактурВыданных(Объект) Экспорт
	
	ПараметрыРегистрации = УчетНДСУПКлиентСервер.ПараметрыРегистрацииСчетовФактурВыданных();
	
	ПараметрыРегистрации.Ссылка					= Объект.Ссылка;
	ПараметрыРегистрации.Дата					= Объект.Дата;
	ПараметрыРегистрации.Организация			= Объект.Организация;
	ПараметрыРегистрации.Контрагент				= Объект.Контрагент;
	ПараметрыРегистрации.НалогообложениеНДС		= Объект.НалогообложениеНДС;
	Если Объект.Исправление Тогда
		ПараметрыРегистрации.ИсправлениеОшибок = Истина;
	КонецЕсли;
	
	ПараметрыРегистрации.НачислениеНДСПриОтгрузке  = Истина;
	ПараметрыРегистрации.РеализацияТоваров         = Истина;
	ПараметрыРегистрации.РеализацияРаботУслуг      = Истина;
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

// Инициализирует параметры регистрации уведомлений о перемещении прослеживаемых товаров
//
// Параметры:
//  Объект		- ДокументОбъект.ОтгрузкаТоваровКлиенту, ДанныеФормыСтруктура	- документ, для которого необходимо получить параметры.
//
// Возвращаемое значение:
// 	Структура - Структура параметров с ключами:
// 	     * Ссылка - ДокументСсылка - Ссылка на документ отгрузки.
// 	     * Дата - Дата - Дата документа продажи.
// 	     * Организация - СправочникСсылка.Организации - Организация, в которой отражается продажа товаров.
// 	     * Контрагент - СправочникСсылка.Контрагенты,
// 	                    СправочникСсылка.Организации - Контрагент или организация покупатель.
//       * Склад - СправочникСсылка.Склады -
// 	     * НалогообложениеНДС - ПеречислениеСсылка.ТипыНалогообложенияНДС - Налогообложение НДС документа продажи.
// 	     [Параметры операции]
// 	     * ПродажаВСтраныТаможенногоСоюза - Булево - Признак того, что отражается операция перемещения товаров в страну таможенного союза.
//       * ВедетсяУчетПоРНПТ - Булево - Признак того, что в табличной части документа есть прослеживаемые товары
//
Функция ПараметрыРегистрацииУведомленийОПеремещенииПрослеживаемыхТоваров(Объект) Экспорт
	
	ПараметрыРегистрации = Новый Структура;
	
	ПараметрыРегистрации.Вставить("Ссылка",               Объект.Ссылка);
	ПараметрыРегистрации.Вставить("Дата",                 Объект.Дата);
	ПараметрыРегистрации.Вставить("Организация",          Объект.Организация);
	ПараметрыРегистрации.Вставить("Контрагент",           Объект.Контрагент);
	ПараметрыРегистрации.Вставить("Склад",                Объект.Склад);
	ПараметрыРегистрации.Вставить("НалогообложениеНДС",   Объект.НалогообложениеНДС);
	ПараметрыРегистрации.Вставить("ПродажаВСтраныТаможенногоСоюза", Ложь);
	ПараметрыРегистрации.Вставить("ВедетсяУчетПоРНПТ",    Ложь);
	ПараметрыРегистрации.Вставить("ПереходПраваСобственности", Ложь);
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ЗначенияРеквизитовКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Контрагент, "ЮрФизЛицо,СтранаРегистрации.УчастникЕАЭС");
		Если ЗначенияРеквизитовКонтрагента.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент") 
			И ЗначениеЗаполнено(ЗначенияРеквизитовКонтрагента.СтранаРегистрацииУчастникЕАЭС)
			И ЗначенияРеквизитовКонтрагента.СтранаРегистрацииУчастникЕАЭС Тогда		
			ПараметрыРегистрации.ПродажаВСтраныТаможенногоСоюза = Истина;
			Если Объект.Товары.НайтиСтроки(Новый Структура("ВедетсяУчетПоРНПТ", Истина)).Количество() Тогда
				ПараметрыРегистрации.ВедетсяУчетПоРНПТ = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеПоРаспоряжению

// Функция конструктор для структуры РеквизитыШапки.
//
// Возвращаемое значение:
//  Структура:
//  	* Организация - СправочникСсылка.Организации
//  	* Склад - СправочникСсылка.Склады
//  	* Подразделение - СправочникСсылка.СтруктураПредприятия
//  	* ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации
//  	* Ссылка - ДокументСсылка.ОтгрузкаТоваровКлиенту
//  	* Партнер - СправочникСсылка.Партнеры
//  	* Контрагент - СправочникСсылка.Контрагенты
//  	* Соглашение - СправочникСсылка.СоглашенияСКлиентами
//  	* Договор - СправочникСсылка.ДоговорыКонтрагентов
//  	* Валюта - СправочникСсылка.Валюты
//  	* Сделка - СправочникСсылка.СделкиСКлиентами
//  	* НаправлениеДеятельности - СправочникСсылка.НаправленияДеятельности
//  	* НалогообложениеНДС - ПеречислениеСсылка.ТипыНалогообложенияНДС
//  	* ЦенаВключаетНДС - Булево
//
Функция РеквизитыШапки() Экспорт

	РеквизитыШапки = Новый Структура;
	
	РеквизитыШапки.Вставить("Организация",             Справочники.Организации.ПустаяСсылка());
	РеквизитыШапки.Вставить("Склад",                   Справочники.Склады.ПустаяСсылка());
	РеквизитыШапки.Вставить("Подразделение",           Справочники.СтруктураПредприятия.ПустаяСсылка());
	РеквизитыШапки.Вставить("ХозяйственнаяОперация",   Перечисления.ХозяйственныеОперации.ПустаяСсылка());
	РеквизитыШапки.Вставить("Ссылка",                  Документы.ОтгрузкаТоваровКлиенту.ПустаяСсылка());
	РеквизитыШапки.Вставить("Партнер",                 Справочники.Партнеры.ПустаяСсылка());
	РеквизитыШапки.Вставить("Контрагент",              Справочники.Контрагенты.ПустаяСсылка());
	РеквизитыШапки.Вставить("Соглашение",              Справочники.СоглашенияСКлиентами.ПустаяСсылка());
	РеквизитыШапки.Вставить("Договор",                 Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
	РеквизитыШапки.Вставить("Валюта",                  Справочники.Валюты.ПустаяСсылка());
	РеквизитыШапки.Вставить("Сделка",                  Справочники.СделкиСКлиентами.ПустаяСсылка());
	РеквизитыШапки.Вставить("НаправлениеДеятельности", Справочники.НаправленияДеятельности.ПустаяСсылка());
	РеквизитыШапки.Вставить("НалогообложениеНДС",      Перечисления.ТипыНалогообложенияНДС.ПустаяСсылка());
	РеквизитыШапки.Вставить("ЦенаВключаетНДС",         Ложь);
	
	Возврат РеквизитыШапки;
	
КонецФункции

// Возвращает массив содержащий переданную накладную, а так же все заказы, которые подходят
// под отбор по реквизитам шапки.
//
// Параметры:
// 	Накладная - ДокументСсылка.ОтгрузкаТоваровКлиенту - накладная.
// 	РеквизитыШапки - Структура - реквизиты шапки:
// 		* Организация - СправочникСсылка.Организации.
// 		* Склад - СправочникСсылка.Склады.
// 		* Сделка - СправочникСсылка.СделкиСКлиентами.
// 		* ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации.
// 		* НаправлениеДеятельности - СправочникСсылка.НаправленияДеятельности.
//
// Возвращаемое значение:
// 	Массив Из ДокументСсылка.
//
Функция РаспоряженияНакладной(Накладная, РеквизитыШапки) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Распоряжение
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка.Проведен
	|	И ДанныеДокумента.Ссылка.Организация = &Организация
	|	И ДанныеДокумента.Ссылка.Партнер = &Партнер
	|	И ДанныеДокумента.Ссылка.Контрагент = &Контрагент
	|	И (НЕ &ИспользоватьСоглашенияСКлиентами
	|		ИЛИ ДанныеДокумента.Ссылка.Соглашение = &Соглашение)
	|	И ДанныеДокумента.Ссылка.Договор = &Договор
	|	И ДанныеДокумента.Склад В ИЕРАРХИИ (&Склад)
	|	И &Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|	И ДанныеДокумента.Ссылка.Валюта = &Валюта
	|	И ДанныеДокумента.Ссылка.Сделка = &Сделка
	|	И ДанныеДокумента.Ссылка.НалогообложениеНДС = &НалогообложениеНДС
	|	И ДанныеДокумента.Ссылка.ЦенаВключаетНДС = &ЦенаВключаетНДС
	|	И (НЕ &ИспользоватьНаправленияДеятельности
	|		ИЛИ ДанныеДокумента.Ссылка.НаправлениеДеятельности = &НаправлениеДеятельности)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Распоряжение
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|	И ДанныеДокумента.Организация = &Организация
	|	И ДанныеДокумента.Партнер = &Партнер
	|	И ДанныеДокумента.Контрагент = &Контрагент
	|	И (НЕ &ИспользоватьСоглашенияСКлиентами
	|		ИЛИ ДанныеДокумента.Ссылка.Соглашение = &Соглашение)
	|	И ДанныеДокумента.Договор = &Договор
	|	И ДанныеДокумента.Склад В ИЕРАРХИИ (&Склад)
	|	И &Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|	И ДанныеДокумента.Валюта = &Валюта
	|	И ДанныеДокумента.Сделка = &Сделка
	|	И ДанныеДокумента.Ссылка.НалогообложениеНДС = &НалогообложениеНДС
	|	И ДанныеДокумента.Ссылка.ЦенаВключаетНДС = &ЦенаВключаетНДС
	|	И (НЕ &ИспользоватьНаправленияДеятельности
	|		ИЛИ ДанныеДокумента.Ссылка.НаправлениеДеятельности = &НаправлениеДеятельности)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Накладная КАК Распоряжение
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Накладная",               Накладная);
	Запрос.УстановитьПараметр("Организация",             РеквизитыШапки.Организация);
	Запрос.УстановитьПараметр("Партнер",                 РеквизитыШапки.Партнер);
	Запрос.УстановитьПараметр("Контрагент",              РеквизитыШапки.Контрагент);
	Запрос.УстановитьПараметр("Соглашение",              РеквизитыШапки.Соглашение);
	Запрос.УстановитьПараметр("Договор",                 РеквизитыШапки.Договор);
	Запрос.УстановитьПараметр("Склад",                   РеквизитыШапки.Склад);
	Запрос.УстановитьПараметр("Валюта",                  РеквизитыШапки.Валюта);
	Запрос.УстановитьПараметр("Сделка",                  РеквизитыШапки.Сделка);
	Запрос.УстановитьПараметр("НаправлениеДеятельности", РеквизитыШапки.НаправлениеДеятельности);
	Запрос.УстановитьПараметр("НалогообложениеНДС",      РеквизитыШапки.НалогообложениеНДС);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС",         РеквизитыШапки.ЦенаВключаетНДС);
	Запрос.УстановитьПараметр("ИспользоватьСоглашенияСКлиентами",
		ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами"));
	Запрос.УстановитьПараметр("ИспользоватьНаправленияДеятельности",
		ПолучитьФункциональнуюОпцию("ИспользоватьУчетДоходовПоНаправлениямДеятельности"));
	
	Распоряжения = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Распоряжение");
	Возврат ОбщегоНазначенияКлиентСервер.СвернутьМассив(Распоряжения);
	
КонецФункции

// Возвращает параметры заполнения документа по заявкам на возврат и ордерам поступления.
//
// Возвращаемое значение:
//  Структура:
//  	* МассивЗаказов - Неопределено
//  	* ФормаОткрыта - Булево
//  	* ЗаполнятьПоОрдеру - Булево
//  	* ДополнятьСериямиПоЗаявке - Булево
//  	* ОформлениеЧерезРМ - Булево
//  	* АктОРасхождениях - ДокументСсылка.АктОРасхожденияхПослеОтгрузки
//  	* ОснованиеАкта - ДокументСсылка.ОтгрузкаТоваровКлиенту
//  	* РеквизитыШапки - Неопределено
//  	* Организация - СправочникСсылка.Организации
//  	* ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации
//  	* Склад - СправочникСсылка.Склады
//  	* Подразделение - СправочникСсылка.СтруктураПредприятия
//  	* ИмяПоляЗаказ - Строка
//  	* КлючевыеПоля - Строка
//  	* Ресурсы - Массив из Строка
//  	* ОтображатьСообщение - Булево
//
Функция ПараметрыЗаполненияДокумента() Экспорт
	
	ПараметрыЗаполнения = Новый Структура();
	
	ПараметрыЗаполнения.Вставить("МассивЗаказов",     Неопределено);
	ПараметрыЗаполнения.Вставить("ФормаОткрыта",      Ложь);
	ПараметрыЗаполнения.Вставить("ЗаполнятьПоОрдеру", Неопределено);
	
	ПараметрыЗаполнения.Вставить("ДополнятьСериямиПоЗаявке", Ложь);
	ПараметрыЗаполнения.Вставить("ОформлениеЧерезРМ",        Ложь);
	
	ПараметрыЗаполнения.Вставить("АктОРасхождениях",        Документы.АктОРасхожденияхПослеОтгрузки.ПустаяСсылка());
	ПараметрыЗаполнения.Вставить("ОснованиеАкта",           Документы.ОтгрузкаТоваровКлиенту.ПустаяСсылка());
	
	ПараметрыЗаполнения.Вставить("РеквизитыШапки",          Неопределено);
	ПараметрыЗаполнения.Вставить("Организация",             Справочники.Организации.ПустаяСсылка());
	ПараметрыЗаполнения.Вставить("ХозяйственнаяОперация",   Перечисления.ХозяйственныеОперации.ПустаяСсылка());
	ПараметрыЗаполнения.Вставить("Склад",                   Справочники.Склады.ПустаяСсылка());
	ПараметрыЗаполнения.Вставить("Подразделение",           Справочники.СтруктураПредприятия.ПустаяСсылка());
	
	ПараметрыЗаполнения.Вставить("ИмяПоляЗаказ", "ЗаказКлиента");
	ПараметрыЗаполнения.Вставить("КлючевыеПоля", "");
	
	ПараметрыЗаполнения.Вставить("Ресурсы", Новый Массив);
	
	ПараметрыЗаполнения.Вставить("ОтображатьСообщение", Ложь);
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

// Выполняет инициализацию параметров заполнения документа по данным документа.
//
// Параметры:
//  ПараметрыЗаполнения - Структура - параметры заполнения документа по умолчанию.
//  РеквизитыШапки - Структура - данные документа, на основании которых будет выполняться заполнение параметров.
//  МассивЗаказов - Массив из ДокументСсылка.ОтгрузкаТоваровКлиенту, ДокументСсылка.КорректировкаРеализации -
//  	массив на заявки, по которым будет выполняться заполнение.
//
Процедура ИнициализироватьПараметрыЗаполнения(ПараметрыЗаполнения, РеквизитыШапки, МассивЗаказов) Экспорт
	
	ПараметрыЗаполнения.МассивЗаказов  = МассивЗаказов;
	ПараметрыЗаполнения.РеквизитыШапки = РеквизитыШапки;
	ПараметрыЗаполнения.Организация    = РеквизитыШапки.Организация;
	ПараметрыЗаполнения.Склад          = РеквизитыШапки.Склад;
	
	Если ПараметрыЗаполнения.ЗаполнятьПоОрдеру = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РаспоряженияНаОтгрузку.Склад КАК Склад,
		|	(РаспоряженияНаОтгрузку.КОтгрузкеКоличествоОборот -
		|		РаспоряженияНаОтгрузку.ОтгруженоКоличествоОборот) КАК КОтгрузкеОстаток
		|ИЗ
		|	#РаспоряженияНаОтгрузкуОборотыРазвернутая КАК РаспоряженияНаОтгрузку
		|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ПорядокОформленияНакладныхРасходныхОрдеров КАК ТаблицаКонстант
		|		ПО ИСТИНА
		|ГДЕ
		|	(РаспоряженияНаОтгрузку.КОтгрузкеКоличествоОборот -
		|		РаспоряженияНаОтгрузку.ОтгруженоКоличествоОборот) > 0
		|	И ВЫРАЗИТЬ(РаспоряженияНаОтгрузку.Склад КАК Справочник.Склады).ИспользоватьОрдернуюСхемуПриОтгрузке
		|	И &ДатаПроверки >= ВЫРАЗИТЬ(РаспоряженияНаОтгрузку.Склад КАК Справочник.Склады).ДатаНачалаОрдернойСхемыПриОтгрузке
		|	И ТаблицаКонстант.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокОформленияНакладныхРасходныхОрдеров.СначалаОрдера)";
		
		ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
		ПараметрыФормированияТаблицы.ТекстУсловия =
			"Распоряжение В (&Распоряжения)";
		ПараметрыФормированияТаблицы.Группировка = "Склад";
		ПараметрыФормированияТаблицы.Показатели = "КОтгрузке, Отгружено";
		ПараметрыФормированияТаблицы.Ресурсы = "Количество";
		
		РаспоряженияНаОтгрузкуОборотыРазвернутая =
			РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
				ПараметрыФормированияТаблицы);
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"#РаспоряженияНаОтгрузкуОборотыРазвернутая",
			РаспоряженияНаОтгрузкуОборотыРазвернутая);
		
		Запрос.УстановитьПараметр("Распоряжения", МассивЗаказов);
		Запрос.УстановитьПараметр("ДатаПроверки", ТекущаяДатаСеанса());
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		ПараметрыЗаполнения.ЗаполнятьПоОрдеру = Выборка.Следующий();
		
	КонецЕсли;
	
	ПараметрыЗаполнения.КлючевыеПоля = КлючевыеПоляТаблицыТоваровРаспоряжения();
	
КонецПроцедуры

// Возвращает список реквизитов, по которым происходит сопоставление таблиц при заполнении по заказам/ордерам
// 
// Возвращаемое значение:
//  Строка
//
Функция КлючевыеПоляТаблицыТоваровРаспоряжения() Экспорт
    
    Возврат "ЗаказКлиента, Номенклатура, Характеристика, Назначение, Серия, Склад";
    
КонецФункции

// Производит заполнение переданного параметра Таблица по заказам и складским ордерам.
//
// Параметры:
//  Товары				 - ТаблицаЗначений - Таблица Товары для заполнения
//  Регистратор			 - ДокументСсылка.ОтгрузкаТоваровКлиенту - Ссылка на текущую накладную
//  ПараметрыЗаполнения	 - Структура - Параметры по умолчанию определены в методе ПараметрыЗаполненияДокумента()
//  ШтрихкодыУпаковок	 - ТаблицаЗначений - Таблица Штрихкоды упаковок
//
Процедура ЗаполнитьПоЗаказамОрдерам(Товары, Регистратор, ПараметрыЗаполнения, ШтрихкодыУпаковок = Неопределено) Экспорт
	
	ПодборПоЗаказамОрдерам = ПараметрыЗаполнения.Ресурсы.Найти("ПодборПоЗаказамОрдерам") <> Неопределено;
	
	ТекстыЗапроса = Новый Массив();
	
	// 1. Получаем текст запроса по остаткам к отгрузке: [к отгрузке] - [отгружено]
	
	ТекстыЗапроса.Добавить(ТекстЗапросаОстаткиТоваровКОформлению());
	
	// 2. Соединяем остатки к отгрузке из п.1. с данные из таблиц документов распоряжений.
	// Поля Номер, Дата, СуммаДокумента и т.д. требуются для отображения в форме выбора распоряжений.
		
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Склады.Ссылка КАК Склад
	|ПОМЕСТИТЬ ВтСклады
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	Склады.Ссылка В ИЕРАРХИИ(&Склад)
	|	И Склады.ИспользоватьОрдернуюСхемуПриОтгрузке
	|	И &ТекущаяДата >= Склады.ДатаНачалаОрдернойСхемыПриОтгрузке
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка В(&Распоряжения)
	|	И Таблица.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить)
	|	И НЕ Таблица.Отменено
	|	И Таблица.Ссылка.Проведен
	|	И Таблица.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента.ЗаменяющиеТовары КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка В(&Распоряжения)
	|	И Таблица.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить)
	|	И НЕ Таблица.Отменено
	|	И Таблица.Ссылка.Проведен
	|	И Таблица.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеУчета.Распоряжение              КАК ЗаказКлиента,
	|	ДанныеУчета.Распоряжение              КАК Ссылка,
	|	Таблица.Номенклатура                  КАК Номенклатура,
	|	Таблица.Характеристика                КАК Характеристика,
	|	ВЫБОР
	|		КОГДА Таблица.Обособленно
	|			ТОГДА Таблица.Ссылка.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ                                 КАК Назначение,
	|	Таблица.Серия                         КАК Серия,
	|	Таблица.КодСтроки                     КАК КодСтроки,
	|	Таблица.Количество                    КАК Количество,
	|	ДанныеУчета.КОформлениюКоличество     КАК КОформлениюКоличество,
	|	ДанныеУчета.КОформлениюСумма          КАК Сумма,
	|	Таблица.Упаковка                      КАК Упаковка,
	|	ВЫБОР
	|		КОГДА Таблица.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ДанныеУчета.КОформлениюСумма / ДанныеУчета.КОформлениюКоличество
	|		ИНАЧЕ
	|			ДанныеУчета.КОформлениюСумма / ДанныеУчета.КОформлениюКоличество
	|				* &ТекстЗапросаКоэффициентУпаковки
	|	КОНЕЦ                                 КАК Цена,
	|	Таблица.Склад                         КАК Склад,
	|	Таблица.НоменклатураНабора            КАК НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора          КАК ХарактеристикаНабора,
	|	ВЫБОР
	|		КОГДА Таблица.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке = ИСТИНА
	|				И Таблица.Склад.ДатаНачалаОрдернойСхемыПриОтгрузке <= &ТекущаяДата
	|				И НЕ Таблица.Ссылка.ЭтоЗаказКакСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                 КАК ПроверятьОтгрузку,
	|	ТИПЗНАЧЕНИЯ(ДанныеУчета.Распоряжение) КАК ТипРаспоряжения,
	|	Таблица.Ссылка.Номер                  КАК Номер,
	|	Таблица.Ссылка.Дата                   КАК Дата,
	|	Таблица.Ссылка.СуммаДокумента         КАК СуммаДокумента,
	|	Таблица.Ссылка.Валюта                 КАК Валюта,
	|	Таблица.Ссылка.Партнер                КАК Партнер,
	|	Таблица.Ссылка.Соглашение             КАК Соглашение,
	|	Таблица.Ссылка.Статус                 КАК Статус,
	|	Таблица.Ссылка.Контрагент             КАК Контрагент,
	|	Таблица.Ссылка.Организация            КАК Организация,
	|	Таблица.Ссылка.ХозяйственнаяОперация  КАК ХозяйственнаяОперация,
	|	Таблица.Ссылка.Менеджер               КАК Менеджер,
	|	Таблица.Ссылка.Комментарий            КАК Комментарий
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК Таблица
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДанныеУчета КАК ДанныеУчета
	|		ПО ДанныеУчета.Распоряжение = Таблица.Ссылка
	|			И ДанныеУчета.Номенклатура = Таблица.Номенклатура
	|			И ДанныеУчета.Характеристика = Таблица.Характеристика
	|			И ДанныеУчета.КодСтроки = Таблица.КодСтроки
	|ГДЕ
	|	НЕ Таблица.Отменено
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеУчета.Распоряжение              КАК ЗаказКлиента,
	|	ДанныеУчета.Распоряжение              КАК Ссылка,
	|	Таблица.Номенклатура                  КАК Номенклатура,
	|	Таблица.Характеристика                КАК Характеристика,
	|	ВЫБОР
	|		КОГДА Таблица.Обособленно
	|			ТОГДА Таблица.Ссылка.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ                                 КАК Назначение,
	|	Таблица.Серия                         КАК Серия,
	|	Таблица.КодСтроки                     КАК КодСтроки,
	|	Таблица.Количество                    КАК Количество,
	|	ДанныеУчета.КОформлениюКоличество     КАК КОформлениюКоличество,
	|	ДанныеУчета.КОформлениюСумма          КАК Сумма,
	|	Таблица.Упаковка                      КАК Упаковка,
	|	ВЫБОР
	|		КОГДА Таблица.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ДанныеУчета.КОформлениюСумма / ДанныеУчета.КОформлениюКоличество
	|		ИНАЧЕ
	|			ДанныеУчета.КОформлениюСумма / ДанныеУчета.КОформлениюКоличество
	|				* &ТекстЗапросаКоэффициентУпаковки
	|	КОНЕЦ                                 КАК Цена,
	|	Таблица.Склад                         КАК Склад,
	|	Таблица.НоменклатураНабора            КАК НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора          КАК ХарактеристикаНабора,
	|	ВЫБОР
	|		КОГДА Таблица.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке = ИСТИНА
	|				И Таблица.Склад.ДатаНачалаОрдернойСхемыПриОтгрузке <= &ТекущаяДата
	|				И НЕ Таблица.Ссылка.ЭтоЗаказКакСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                 КАК ПроверятьОтгрузку,
	|	ТИПЗНАЧЕНИЯ(ДанныеУчета.Распоряжение) КАК ТипРаспоряжения,
	|	Таблица.Ссылка.Номер                  КАК Номер,
	|	Таблица.Ссылка.Дата                   КАК Дата,
	|	Таблица.Ссылка.СуммаЗамены            КАК СуммаДокумента,
	|	Таблица.Ссылка.Валюта                 КАК Валюта,
	|	Таблица.Ссылка.Партнер                КАК Партнер,
	|	Таблица.Ссылка.Соглашение             КАК Соглашение,
	|	Таблица.Ссылка.Статус                 КАК Статус,
	|	Таблица.Ссылка.Контрагент             КАК Контрагент,
	|	Таблица.Ссылка.Организация            КАК Организация,
	|	Таблица.Ссылка.ХозяйственнаяОперация  КАК ХозяйственнаяОперация,
	|	Таблица.Ссылка.Менеджер               КАК Менеджер,
	|	Таблица.Ссылка.Комментарий            КАК Комментарий
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента.ЗаменяющиеТовары КАК Таблица
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДанныеУчета КАК ДанныеУчета
	|		ПО ДанныеУчета.Распоряжение = Таблица.Ссылка
	|			И ДанныеУчета.Номенклатура = Таблица.Номенклатура
	|			И ДанныеУчета.Характеристика = Таблица.Характеристика
	|			И ДанныеУчета.КодСтроки = Таблица.КодСтроки
	|ГДЕ
	|	НЕ Таблица.Отменено
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"Таблица.Упаковка",
			"Таблица.Номенклатура"));
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, НакладныеСервер.ТекстРазделителяЗапросов());
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Распоряжения", ПараметрыЗаполнения.МассивЗаказов);
	Запрос.УстановитьПараметр("Склад", ПараметрыЗаполнения.Склад);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
	
	Результаты = Запрос.ВыполнитьПакет();
	РезультатСтрокиОтгрузить = Результаты[Результаты.ВГраница()-1];
	ВыборкаТовары = Результаты[Результаты.ВГраница()].Выбрать();
	
	Если РезультатСтрокиОтгрузить.Пустой()
		И ВыборкаТовары.Количество() = 0
		И ПараметрыЗаполнения.ОтображатьСообщение Тогда
		
		ТекстОшибки = ОбеспечениеВДокументахСервер.ТекстОшибкиНетТоваровДоступныхДляОтгрузки();
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Отбор = Новый Соответствие;
	Если ЗначениеЗаполнено(ПараметрыЗаполнения.Склад) Тогда
		ОбщегоНазначенияУТ.ДобавитьЭлементОтбораВКоллекцию(Отбор, "Склад", "ВЫБРАТЬ Склад ИЗ ВтСклады");
	КонецЕсли;
	
	// Получаем данные из ордеров: количество Собрано и количество Собирается.
	// Собирается - пользователю для информации.
	Если ПараметрыЗаполнения.ЗаполнятьПоОрдеру Тогда
	
		Запрос.Текст = РегистрыНакопления.ТоварыКОтгрузке.ТекстЗапросаОсталосьОформить(Отбор);
		ТаблицаОрдера = Запрос.Выполнить().Выгрузить();
		
		КолонкаТаблицы = ТаблицаОрдера.Колонки.Количество; // КолонкаТаблицыЗначений
		КолонкаТаблицы.Имя = "КоличествоВОрдере";
		КолонкаТаблицы = ТаблицаОрдера.Колонки.Распоряжение; // КолонкаТаблицыЗначений
		КолонкаТаблицы.Имя = ПараметрыЗаполнения.ИмяПоляЗаказ;
		
		Если ТаблицаОрдера.Колонки.Найти("Собирается") <> Неопределено Тогда
			КолонкаТаблицы = ТаблицаОрдера.Колонки.Собирается; // КолонкаТаблицыЗначений
			КолонкаТаблицы.Имя = "КоличествоСобирается";
		КонецЕсли;
		
	Иначе
		
		ТаблицаОрдера = Новый ТаблицаЗначений();
		
	КонецЕсли;
	
	Если Товары.Колонки.Найти("ТребуетсяПересчетСуммы") = Неопределено Тогда
		Товары.Колонки.Добавить("ТребуетсяПересчетСуммы", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	Если Товары.Колонки.Найти("КоличествоУпаковок") = Неопределено Тогда
		Товары.Колонки.Добавить("КоличествоУпаковок", Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	Если ПодборПоЗаказамОрдерам Тогда
		
		Если Товары.Колонки.Найти("КоличествоВЗаказе") = Неопределено Тогда
			Товары.Колонки.Добавить("КоличествоВЗаказе", Новый ОписаниеТипов("Число"));
		КонецЕсли;
		
		Если Товары.Колонки.Найти("КоличествоСобирается") = Неопределено Тогда 
			Товары.Колонки.Добавить("КоличествоСобирается", Новый ОписаниеТипов("Число"));
		КонецЕсли;
		
		Если Товары.Колонки.Найти("КоличествоВОрдере") = Неопределено Тогда 
			Товары.Колонки.Добавить("КоличествоВОрдере", Новый ОписаниеТипов("Число"));
		КонецЕсли;
		
		ОтгруженныеТовары = ТаблицаОрдера.Скопировать();
		
	КонецЕсли;
	
	ПроверитьСклады = Неопределено;
	ПараметрыЗаполнения.Свойство("ПроверитьСклады", ПроверитьСклады);
	
	Пока ВыборкаТовары.Следующий() Цикл
		
		Если ЗначениеЗаполнено(ПроверитьСклады) Тогда
			СкладыПоСсылке = ПроверитьСклады.Получить(ВыборкаТовары.ЗаказКлиента);
			Если СкладыПоСсылке <> Неопределено
				 И СкладыПоСсылке.Найти(ВыборкаТовары.Склад) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		КоличествоКОтгрузке = ВыборкаТовары.КОформлениюКоличество;
		ТребуетсяПересчетСуммы = Ложь;
		
		// Если требуется заполнять по ордеру, и склад ордерный, то берем количество к отгрузке из ордера
		// ЗаполнятьПоОрдеру = Истина и ПроверятьОтгрузку = Ложь - такая ситуация возможна, если в шапке документа
		// склад - группа, а в ТЧ склад неордерный.
		Если ПараметрыЗаполнения.ЗаполнятьПоОрдеру И ВыборкаТовары.ПроверятьОтгрузку Тогда
			
			ПараметрыПоиска = Новый Структура(ПараметрыЗаполнения.КлючевыеПоля);
			ЗаполнитьЗначенияСвойств(ПараметрыПоиска, ВыборкаТовары);
			
			НайденныеСтроки = ТаблицаОрдера.НайтиСтроки(ПараметрыПоиска);
			
			Если НайденныеСтроки.Количество() > 0 Тогда
				
				ОтгруженнаяСтрока = НайденныеСтроки[0];
				
				Если ОтгруженнаяСтрока.КоличествоВОрдере > 0 Тогда
					
					Если ОтгруженнаяСтрока.КоличествоВОрдере < ВыборкаТовары.Количество Тогда
						
						КоличествоКОтгрузке = ОтгруженнаяСтрока.КоличествоВОрдере;
						ОтгруженнаяСтрока.КоличествоВОрдере = 0;
						ТребуетсяПересчетСуммы = Истина;
						
					Иначе
						
						КоличествоКОтгрузке = ВыборкаТовары.Количество;
						ОтгруженнаяСтрока.КоличествоВОрдере =
							ОтгруженнаяСтрока.КоличествоВОрдере
							- ВыборкаТовары.Количество;
						
					КонецЕсли;
					
				ИначеЕсли ПодборПоЗаказамОрдерам Тогда
					КоличествоКОтгрузке = 0;
				Иначе
					Продолжить;
				КонецЕсли;
				
			ИначеЕсли ПодборПоЗаказамОрдерам Тогда
				КоличествоКОтгрузке = 0;
			Иначе
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		// Поиск строки актуален для формы Подбора по распоряжениям/ордерам - в нее (в форму) в качестве параметра
		// передается таблица Товары документа Отгрузка товаров. Для всех остальных случаев добавляем новую строку.
		ПараметрыПоиска = Новый Структура(ПараметрыЗаполнения.КлючевыеПоля + ", КодСтроки");
		ЗаполнитьЗначенияСвойств(ПараметрыПоиска, ВыборкаТовары);
		
		НайденныеСтроки = Товары.НайтиСтроки(ПараметрыПоиска);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			СтрокаТаб = НайденныеСтроки[0];
		Иначе
			СтрокаТаб = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаб, ВыборкаТовары,, "Количество");
			СтрокаТаб.Количество = КоличествоКОтгрузке;
			СтрокаТаб.ТребуетсяПересчетСуммы = ТребуетсяПересчетСуммы;
		КонецЕсли;
		
		// Для случая Подбора по распоряжениям/ордерам заполним колонки Количество в заказе, Количество Собирается и
		// Количество в ордере (собрано)
		Если ПодборПоЗаказамОрдерам Тогда
			
			СтрокаТаб.КоличествоВЗаказе = ?(ВыборкаТовары.КодСтроки <> 0, ВыборкаТовары.КОформлениюКоличество, 0);
			
			Если ПараметрыЗаполнения.ЗаполнятьПоОрдеру И ВыборкаТовары.ПроверятьОтгрузку Тогда
				
				ПараметрыПоиска = Новый Структура(ПараметрыЗаполнения.КлючевыеПоля);
				ЗаполнитьЗначенияСвойств(ПараметрыПоиска, ВыборкаТовары);
				
				НайденныеСтроки = ОтгруженныеТовары.НайтиСтроки(ПараметрыПоиска);
				
				Если НайденныеСтроки.Количество() > 0 Тогда
					
					ОтгруженнаяСтрока = НайденныеСтроки[0];
					
					Если ОтгруженнаяСтрока.КоличествоСобирается > 0 Тогда
						СтрокаТаб.КоличествоСобирается = ОтгруженнаяСтрока.КоличествоСобирается;
						ОтгруженнаяСтрока.КоличествоСобирается = 0;
					КонецЕсли;
					
					Если ОтгруженнаяСтрока.КоличествоВОрдере > 0 Тогда
						
						Если ОтгруженнаяСтрока.КоличествоВОрдере <= ВыборкаТовары.Количество Тогда
							
							СтрокаТаб.КоличествоВОрдере = ОтгруженнаяСтрока.КоличествоВОрдере;
							ОтгруженнаяСтрока.КоличествоВОрдере = 0;
							ОтгруженныеТовары.Удалить(ОтгруженнаяСтрока);
							
						Иначе
							
							СтрокаТаб.КоличествоВОрдере = ВыборкаТовары.Количество;
							ОтгруженнаяСтрока.КоличествоВОрдере =
								ОтгруженнаяСтрока.КоличествоВОрдере
								- ВыборкаТовары.Количество;
							
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
			
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПараметрыЗаполнения.ЗаполнятьПоОрдеру Тогда
		
		НакладныеСервер.УдалитьПустыеСтроки(ТаблицаОрдера, "КоличествоВОрдере");
		
		Для Каждого ОтгруженнаяСтрока Из ТаблицаОрдера Цикл
			
			Если ОтгруженнаяСтрока.КоличествоВОрдере < 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ПараметрыПоиска = Новый Структура(ПараметрыЗаполнения.КлючевыеПоля);
			ЗаполнитьЗначенияСвойств(ПараметрыПоиска, ОтгруженнаяСтрока);
			
			НайденныеСтроки = Товары.НайтиСтроки(ПараметрыПоиска);
			
			Если НайденныеСтроки.Количество() > 0 Тогда
				
				НайденнаяСтрока = НайденныеСтроки[0];
				
				Если ПодборПоЗаказамОрдерам Тогда
					НайденнаяСтрока.КоличествоВОрдере =
						НайденнаяСтрока.КоличествоВОрдере + ОтгруженнаяСтрока.КоличествоВОрдере;
				Иначе
					НайденнаяСтрока.Количество = НайденнаяСтрока.Количество + ОтгруженнаяСтрока.КоличествоВОрдере;
					НайденнаяСтрока.ТребуетсяПересчетСуммы = Истина;
				КонецЕсли;
				
			Иначе
				
				СтрокаТаб = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаб, ОтгруженнаяСтрока);
				
				СтрокаТаб.Количество = ОтгруженнаяСтрока.КоличествоВОрдере;
				СтрокаТаб.КоличествоУпаковок = ОтгруженнаяСтрока.КоличествоВОрдере;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Пересчитываем количество упаковок для всех строк
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьТЧ(
		Товары,
		СтруктураДействий);
	
	// Пересчитываем сумма только в тех строках, где то необходимо
	СтруктураДействий.Очистить();
	СтруктураДействий.Вставить("ПересчитатьСумму");
	Для Каждого Стр Из Товары Цикл
		
		Если Стр.ТребуетсяПересчетСуммы Тогда
			ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(Стр, СтруктураДействий, Неопределено);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ПараметрыЗаполнения.МассивЗаказов) Тогда
		ОтгрузкаТоваровКлиентуЛокализация.ЗаполнитьШтрихКодыУпаковок(ПараметрыЗаполнения.МассивЗаказов, Товары, ШтрихкодыУпаковок);
	КонецЕсли;
	
КонецПроцедуры

// Формирует данные необходимые для проверки ввода накладной на основании распоряжений.
// см. СозданиеНаОснованииУТВызовСервера.ОтгрузкаТоваровКлиентуПараметрыОткрытияФормы
//
// Параметры:
//  Распоряжения - Массив из ДокументСсылка- список распоряжений, на основании которых оформляется накладная
//
// Возвращаемое значение:
//  см. ПараметрыДляПроверкиВводаНаОсновании
//
Функция ДанныеДляПроверкиВводаНаОсновании(Распоряжения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Таблица.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК Таблица
	|ГДЕ
	|	(Таблица.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить)
	|		ИЛИ Таблица.Ссылка.ЭтоЗаказКакСчет)
	|	И Таблица.Ссылка В (&Распоряжения)
	|	И НЕ Таблица.Отменено
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Таблица.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента.ЗаменяющиеТовары КАК Таблица
	|ГДЕ
	|	(Таблица.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить)
	|		ИЛИ Таблица.Ссылка.ЭтоЗаказКакСчет)
	|	И Таблица.Ссылка В (&Распоряжения)
	|	И НЕ Таблица.Отменено
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.ЗаказКлиента КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка В (&Распоряжения)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка В (&Распоряжения)";
	
	Запрос.УстановитьПараметр("Распоряжения", Распоряжения);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РезультатТоварыКОтгрузке = РезультатЗапроса[РезультатЗапроса.ВГраница() - 1];
	ТаблицаХозяйственныеОперации = РезультатЗапроса[РезультатЗапроса.ВГраница()].Выгрузить();
	
	ДанныеДляПроверкиВводаНаОсновании = ПараметрыДляПроверкиВводаНаОсновании();
	ДанныеДляПроверкиВводаНаОсновании.ЕстьОтгрузить = Не РезультатТоварыКОтгрузке.Пустой();
	ДанныеДляПроверкиВводаНаОсновании.ХозяйственныеОперации = ТаблицаХозяйственныеОперации.ВыгрузитьКолонку("ХозяйственнаяОперация");

	Возврат ДанныеДляПроверкиВводаНаОсновании;

КонецФункции

// Функция-конструктор параметров для проверки ввода на основании
//
// Возвращаемое значение:
// 	Структура:
// 	* ЕстьОтгрузить - Булево.
// 	* ХозяйственныеОперации - Массив Из ПеречислениеСсылка.ХозяйственныеОперации.
//
Функция ПараметрыДляПроверкиВводаНаОсновании() Экспорт
	
	ПараметрыДляПроверкиВводаНаОсновании = Новый Структура;
	ПараметрыДляПроверкиВводаНаОсновании.Вставить("ЕстьОтгрузить", Ложь);
	ПараметрыДляПроверкиВводаНаОсновании.Вставить("ХозяйственныеОперации", Новый Массив);
	
	Возврат ПараметрыДляПроверкиВводаНаОсновании;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("ХозяйственнаяОперация");
	Поля.Добавить("Номер");
	Поля.Добавить("Дата");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПредставлениеОбъекта = НСтр("ru = 'Отгрузка товаров клиенту';
								|en = 'Goods shipment to customer'");
	
	Представление = СтрШаблон(НСтр("ru = '%1 %2 от %3';
									|en = '%1 %2 dated %3'"),
		ПредставлениеОбъекта,
		Данные.Номер,
		Данные.Дата);
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	
	Возврат ИсточникиДанных;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                КАК Ссылка,
	|	ДанныеДокумента.Номер                 КАК Номер,
	|	ДанныеДокумента.Дата                  КАК Период,
	|	ДанныеДокумента.ЗаказКлиента          КАК ЗаказКлиента,
	|	ДанныеДокумента.Дата                  КАК ДатаРаспоряжения,
	|	ДанныеДокумента.Партнер               КАК Партнер,
	|	ДанныеДокумента.Контрагент            КАК Контрагент,
	|	ЕСТЬNULL(ДанныеДокумента.Контрагент.СтранаРегистрации.УчастникЕАЭС, ЛОЖЬ) КАК КонтрагентУчастникЕАЭС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДанныеДокумента.Контрагент.ЮрФизЛицо,
	|			ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                 КАК КонтрагентНеРезидент,
	|	ДанныеДокумента.Соглашение            КАК Соглашение,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация           КАК Организация,
	|	ДанныеДокумента.Организация.СтранаРегистрации КАК СтранаРегистрации,
	|	ДанныеДокумента.Договор               КАК Договор,
	|	ДанныеДокумента.Склад                 КАК Склад,
	|	ДанныеДокумента.Валюта                КАК Валюта,
	|	ДанныеДокумента.Комментарий           КАК Комментарий,
	|	ДанныеДокумента.Менеджер              КАК Менеджер,
	|	ДанныеДокумента.Автор                 КАК Автор,
	|	ДанныеДокумента.Сделка                КАК Сделка,
	|	ДанныеДокумента.НалогообложениеНДС    КАК НалогообложениеНДС,
	|	ИСТИНА                                КАК ЦенаВключаетНДС,
	|	ЕСТЬNULL(ДанныеДокумента.Сделка.ОбособленныйУчетТоваровПоСделке, ЛОЖЬ) КАК ОбособленныйУчетТоваровПоСделке,
	|	ДанныеДокумента.Подразделение         КАК Подразделение,
	|	ЕСТЬNULL(ДанныеДокумента.Подразделение.ВариантОбособленногоУчетаТоваров,
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПустаяСсылка)) КАК ВариантОбособленногоУчетаТоваров,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.СуммаДокумента        КАК СуммаДокумента,
	|	ДанныеДокумента.ПометкаУдаления       КАК ПометкаУдаления,
	|	ДанныеДокумента.Проведен              КАК Проведен,
	|	ДанныеДокумента.Исправление           КАК Исправление,
	|	ДанныеДокумента.СторнируемыйДокумент  КАК СторнируемыйДокумент,
	|	ДанныеДокумента.ИсправляемыйДокумент  КАК ИсправляемыйДокумент,
	|	ДанныеДокумента.ОтгрузкаПоЗаказам     КАК ОтгрузкаПоЗаказам,
	|	ДанныеДокумента.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|	НастройкиХозяйственныхОпераций.Ссылка КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
	|		ПО ДанныеДокумента.ХозяйственнаяОперация = НастройкиХозяйственныхОпераций.ХозяйственнаяОперация
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	Реквизиты = РезультатЗапроса.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ИнформацияПоДоговору    = "";
	НомерНаПечать           = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер);
	ИдентификаторМетаданных =
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ОтгрузкаТоваровКлиенту");
	
	Если ЗначениеЗаполнено(Реквизиты.Договор) Тогда
		
		ШаблонСтроки = "ru = 'По договору ""%1""';
						|en = 'Under the ""%1"" contract'"; // @НСтр
		ИнформацияПоДоговору = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСтроки,
			Реквизиты.Договор);
			
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ИдентификаторМетаданных",        ИдентификаторМетаданных);
	Запрос.УстановитьПараметр("ОтгрузкаПоНесколькимЗаказам",    Не ЗначениеЗаполнено(Реквизиты.ЗаказКлиента));
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",     Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("НомерНаПечать",                  НомерНаПечать);
	Запрос.УстановитьПараметр("ИнформацияПоДоговору",           ИнформацияПоДоговору);
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения",  Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	
	Запрос.УстановитьПараметр("ИспользоватьРасширенныеВозможностиЗаказаКлиента",
							ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента"));
	
	Запрос.УстановитьПараметр("НалогообложениеОрганизации",
		УчетНДСУП.ПараметрыУчетаПоОрганизации(Запрос.Параметры.Организация, Запрос.Параметры.Период).ОсновноеНалогообложениеНДСПродажи);
	
	УчетНДСУП.УстановитьПараметрыЗапросаСтавкиНДС(Запрос.Параметры, Запрос.Параметры.Организация, Запрос.Параметры.Период);
	Запрос.УстановитьПараметр("ТипыНалогообложенияНДСНеоблагаемые", УчетНДСУП.ТипыНалогообложенияНДСНеоблагаемые());
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
	УчетПрослеживаемыхТоваровЛокализация.УстановитьПараметрыИспользованияУчетаПрослеживаемыхТоваров(Запрос);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаДвиженияНоменклатураНоменклатура(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияНоменклатураНоменклатура";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасов", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                        КАК Период,
	|	&ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.Организация КАК Организация,
	|	&Подразделение                 КАК Подразделение,
	|	ТаблицаВидыЗапасов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаВидыЗапасов.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ                          КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.Склад       КАК Склад,
	|	ТаблицаВидыЗапасов.ТипЗапасов  КАК ТипЗапасов,
	|	ТаблицаВидыЗапасов.ВидЗапасов  КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.ВидЗапасов  КАК ИсточникГФУНоменклатуры,
	|	&НаправлениеДеятельности       КАК КорНаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаВидыЗапасов.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ                          КАК КорАналитикаУчетаНоменклатуры,
	|	&Договор                       КАК КорСклад,
	|	ТаблицаВидыЗапасов.ТипЗапасов  КАК КорТипЗапасов,
	|	ТаблицаВидыЗапасов.ВидЗапасов  КАК КорВидЗапасов,
	|	ТаблицаВидыЗапасов.ВидЗапасов  КАК КорИсточникГФУНоменклатуры,
	|	ТаблицаВидыЗапасов.Количество  КАК Количество,
	|	ТаблицаВидыЗапасов.Количество  КАК КорКоличество,
	|	0                              КАК Стоимость,
	|	0                              КАК СтоимостьБезНДС,
	|	0                              КАК СтоимостьРегл
	|ИЗ
	|	ВтТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияСерийТоваров";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период														КАК Период,
	|	ТаблицаСерии.Серия											КАК Серия,
	|	ТаблицаСерии.Номенклатура									КАК Номенклатура,
	|	ТаблицаСерии.Характеристика									КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаСерии.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаСерии.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ														КАК Назначение,
	|	&Ссылка														КАК Документ,
	|	ТаблицаСерии.Склад											КАК Отправитель,
	|	ЗНАЧЕНИЕ(Справочник.СкладскиеПомещения.ПустаяСсылка)		КАК ПомещениеОтправителя,
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ОтгрузкаКлиенту)	КАК СкладскаяОперация,
	|	&Партнер													КАК Получатель,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаСерии.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				ИЛИ &Период < ТаблицаСерии.Склад.ДатаНачалаОрдернойСхемыПриОтгрузке
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ														КАК ЭтоСкладскоеДвижение,
	|	ТаблицаСерии.Количество										КАК Количество
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Серии КАК ТаблицаСерии
	|
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И ТаблицаСерии.Количество <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период														КАК Период,
	|	ТаблицаСерии.Серия											КАК Серия,
	|	ТаблицаСерии.Номенклатура									КАК Номенклатура,
	|	ТаблицаСерии.Характеристика									КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаСерии.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаСерии.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ														КАК Назначение,
	|	&Ссылка														КАК Документ,
	|	ТаблицаСерии.Склад											КАК Отправитель,
	|	ЗНАЧЕНИЕ(Справочник.СкладскиеПомещения.ПустаяСсылка)		КАК ПомещениеОтправителя,
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ОтгрузкаКлиенту)	КАК СкладскаяОперация,
	|	&Партнер													КАК Получатель,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаСерии.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				ИЛИ &Период < ТаблицаСерии.Склад.ДатаНачалаОрдернойСхемыПриОтгрузке
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ														КАК ЭтоСкладскоеДвижение,
	|	ТаблицаСерии.Количество										КАК Количество
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ТаблицаСерии
	|
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И ТаблицаСерии.Количество <> 0
	|	И ТаблицаСерии.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРаспоряженияНаОтгрузку(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РаспоряженияНаОтгрузкуИВозврат";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	// 1. Отгрузка по распоряжению
	"ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки    КАК НомерСтроки,
	|	ТаблицаТовары.Ссылка.Дата    КАК Период,
	|	ТаблицаТовары.ЗаказКлиента   КАК Распоряжение,
	|	ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.КодСтроки      КАК КодСтроки,
	|	ТаблицаТовары.Склад          КАК Склад,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерийНаСкладах В (10, 14)
	|			ТОГДА ТаблицаТовары.Серия
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                        КАК Серия,
	|	ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Отгружено) КАК Показатель,
	|	ТаблицаТовары.Количество     КАК Количество,
	|	ТаблицаТовары.Сумма          КАК Сумма
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.Ссылка.ОтгрузкаПоЗаказам
	|	И ТаблицаТовары.КодСтроки <> 0
	|	И НЕ ТаблицаТовары.ЗаказКлиента В (
	|					ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка),
	|					ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка),
	|					НЕОПРЕДЕЛЕНО
	|					)
	|	И ВЫБОР
	|		КОГДА ТаблицаТовары.ЗаказКлиента ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА НЕ ВЫРАЗИТЬ(ТаблицаТовары.ЗаказКлиента КАК Документ.ЗаказКлиента).ЭтоЗаказКакСчет
	|		КОГДА ТаблицаТовары.ЗаказКлиента ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА НЕ ВЫРАЗИТЬ(ТаблицаТовары.ЗаказКлиента КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ЭтоЗаказКакСчет
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 2. Отгрузка сверх распоряжения
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки    КАК НомерСтроки,
	|	ТаблицаТовары.Ссылка.Дата    КАК Период,
	|	ТаблицаТовары.ЗаказКлиента   КАК Распоряжение,
	|	ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.КодСтроки      КАК КодСтроки,
	|	ВЫБОР
	|		КОГДА ТаблицаПоказателей.Ссылка = ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлению)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаТовары.Склад
	|	КОНЕЦ                        КАК Склад,
	|	ВЫБОР
	|		КОГДА ТаблицаПоказателей.Ссылка В (
	|				ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОтгрузке),
	|				ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Отгружено)
	|				)
	|			И ТаблицаТовары.СтатусУказанияСерийНаСкладах В (10, 14)
	|			ТОГДА
	|				ТаблицаТовары.Серия
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                        КАК Серия,
	|	ТаблицаПоказателей.Ссылка    КАК Показатель,
	|	ТаблицаТовары.Количество     КАК Количество,
	|	ТаблицаТовары.Сумма          КАК Сумма
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат КАК ТаблицаПоказателей
	|		ПО ТаблицаПоказателей.Ссылка В (ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлению),
	|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОтгрузке),
	|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Отгружено))
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.Ссылка.ОтгрузкаПоЗаказам
	|	И ТаблицаТовары.КодСтроки = 0
	|	И НЕ ТаблицаТовары.ЗаказКлиента В (
	|			ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка),
	|			ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка),
	|			НЕОПРЕДЕЛЕНО
	|			)
	|	И ВЫБОР
	|		КОГДА ТаблицаТовары.ЗаказКлиента ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА НЕ ВЫРАЗИТЬ(ТаблицаТовары.ЗаказКлиента КАК Документ.ЗаказКлиента).ЭтоЗаказКакСчет
	|		КОГДА ТаблицаТовары.ЗаказКлиента ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА НЕ ВЫРАЗИТЬ(ТаблицаТовары.ЗаказКлиента КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ЭтоЗаказКакСчет
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 3. Отгрузка без распоряжения или отгрузка по распоряжению (заявке на возврат), но заказ
	// (заявка на возврат) имеет признак ЭтоЗаказКакСчет
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки    КАК НомерСтроки,
	|	ТаблицаТовары.Ссылка.Дата    КАК Период,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Ссылка.Исправление
	|			ТОГДА ТаблицаТовары.Ссылка.ИсправляемыйДокумент
	|		ИНАЧЕ
	|			ТаблицаТовары.Ссылка
	|	КОНЕЦ                        КАК Распоряжение,
	|	ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.КодСтроки      КАК КодСтроки,
	|	ВЫБОР
	|		КОГДА ТаблицаПоказателей.Ссылка = ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлению)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаТовары.Склад
	|	КОНЕЦ                        КАК Склад,
	|	ВЫБОР
	|		КОГДА ТаблицаПоказателей.Ссылка В (
	|				ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Заказано),
	|				ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОтгрузке),
	|				ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Отгружено)
	|				)
	|			И ТаблицаТовары.СтатусУказанияСерийНаСкладах В (10, 14)
	|			ТОГДА
	|				ТаблицаТовары.Серия
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                        КАК Серия,
	|	ТаблицаПоказателей.Ссылка    КАК Показатель,
	|	ТаблицаТовары.Количество     КАК Количество,
	|	ТаблицаТовары.Сумма          КАК Сумма
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат КАК ТаблицаПоказателей
	|		ПО ТаблицаПоказателей.Ссылка В (
	|				ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Заказано),
	|				ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлению),
	|				ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОтгрузке),
	|				ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Отгружено)
	|				)
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И (ТаблицаТовары.КодСтроки <> 0
	|		И НЕ ТаблицаТовары.Ссылка.ОтгрузкаПоЗаказам
	|		И ТаблицаТовары.ЗаказКлиента В (
	|				ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка),
	|				ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка),
	|				НЕОПРЕДЕЛЕНО
	|				)
	|		ИЛИ
	|		ТаблицаТовары.Ссылка.ОтгрузкаПоЗаказам
	|		И НЕ ТаблицаТовары.ЗаказКлиента В (
	|				ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка),
	|				ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка),
	|				НЕОПРЕДЕЛЕНО
	|				)
	|		И ВЫБОР
	|			КОГДА ТаблицаТовары.ЗаказКлиента ССЫЛКА Документ.ЗаказКлиента
	|				ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.ЗаказКлиента КАК Документ.ЗаказКлиента).ЭтоЗаказКакСчет
	|			КОГДА ТаблицаТовары.ЗаказКлиента ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|				ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.ЗаказКлиента КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ЭтоЗаказКакСчет
	|			ИНАЧЕ
	|				ЛОЖЬ
	|		КОНЕЦ
	|	)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	Показатель";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка						КАК Ссылка,
	|	&Период						КАК ДатаДокументаИБ,
	|	&Номер						КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных	КАК ТипСсылки,
	|	&Организация				КАК Организация,
	|	&ХозяйственнаяОперация		КАК ХозяйственнаяОперация,
	|	&Партнер					КАК Партнер,
	|	&Контрагент					КАК Контрагент,
	|	&Договор					КАК Договор,
	|	&НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	&Склад						КАК МестоХранения,
	|	&Подразделение				КАК Подразделение,
	|	&Менеджер					КАК Ответственный,
	|	&Автор						КАК Автор,
	|	&Комментарий				КАК Комментарий,
	|	&Валюта						КАК Валюта,
	|	&СуммаДокумента				КАК Сумма,
	|	&Проведен					КАК Проведен,
	|	&ПометкаУдаления			КАК ПометкаУдаления,
	|	ЛОЖЬ						КАК ДополнительнаяЗапись,
	|	&ИнформацияПоДоговору		КАК Дополнительно,
	|	&Период						КАК ДатаПервичногоДокумента,
	|	&Исправление				КАК СторноИсправление,
	|	&СторнируемыйДокумент		КАК СторнируемыйДокумент,
	|	&ИсправляемыйДокумент		КАК ИсправляемыйДокумент,
	|	&НомерНаПечать				КАК НомерПервичногоДокумента,
	|	&Период						КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО				КАК Приоритет
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Регистры = Неопределено) Экспорт
	
	ТекстЗапросаДанных = 
	"ВЫБРАТЬ
	|	""ВидыЗапасов"" КАК ИсточникДанных,
	|	ИСТИНА КАК РаспределятьОбщуюСумму,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Ссылка.Дата КАК Дата,
	|	ТаблицаДокумента.Ссылка.Организация КАК Организация,
	|	ТаблицаДокумента.Ссылка.Валюта КАК ВалютаДокумента,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаВзаиморасчетов,
	|	ТаблицаДокумента.Ссылка.Дата КАК ПериодБазыНДС,
	|	ТаблицаДокумента.Ссылка.Дата КАК ДатаКурса,
	|
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.Сумма КАК СуммаБезНДС,
	|	&СтавкаНДСБезНДС КАК СтавкаНДС,
	|	0 КАК СуммаНДС,
	|	0 КАК СуммаВзаиморасчетов,
	|	0 КАК СуммаНДСВзаиморасчетов,
	|	0 КАК СуммаБезНДСРегл,
	|	0 КАК СуммаБезНДСУпр,
	|
	|	ЛОЖЬ КАК ОтражаетсяВРасчетах,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектРасчетов,
	|	ЛОЖЬ КАК ПересчитыватьПоДаннымРасчетов
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.ВидыЗапасов КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|";
	
	РегистрыСведений.СуммыДокументовВВалютахУчета.СформироватьПоДаннымДокумента(
		Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаДанных);
	
КонецПроцедуры

Процедура ЗапланироватьОтгрузкуТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ТекстЗапросаДанныхДокумента = 
	"ВЫБРАТЬ
	|	ТоварыДокумента.Ссылка				КАК Ссылка,
	|	ДанныеШапки.Дата					КАК Период,
	|	ВЫБОР
	|		КОГДА ДанныеШапки.ОтгрузкаПоЗаказам
	|				И &ИспользоватьРасширенныеВозможностиЗаказа
	|				И НЕ ТоварыДокумента.ЗаказКлиента В (
	|										НЕОПРЕДЕЛЕНО,
	|										ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка),
	|										ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка)
	|										)
	|			ТОГДА ТоварыДокумента.ЗаказКлиента
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ								КАК Заказ,
	|	ТоварыДокумента.Ссылка				КАК Накладная,
	|	ДанныеШапки.Исправление				КАК Исправление,
	|	ДанныеШапки.ИсправляемыйДокумент	КАК ИсправляемыйДокумент,
	|	ДанныеШапки.Партнер					КАК Получатель,
	|	ТоварыДокумента.Склад				КАК Склад,
	|	ТоварыДокумента.Номенклатура		КАК Номенклатура,
	|	ТоварыДокумента.Характеристика		КАК Характеристика,
	|	ТоварыДокумента.Назначение			КАК Назначение,
	|	ТоварыДокумента.Серия				КАК Серия,
	|	ТоварыДокумента.СтатусУказанияСерий	КАК СтатусУказанияСерий,
	|	ТоварыДокумента.Количество			КАК Количество,
	|	ВЫБОР
	|		КОГДА ДанныеШапки.ОтгрузкаПоЗаказам
	|				И &ИспользоватьРасширенныеВозможностиЗаказа
	|				И ТоварыДокумента.КодСтроки = 0
	|				И НЕ ТоварыДокумента.ЗаказКлиента В (
	|										НЕОПРЕДЕЛЕНО,
	|										ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка),
	|										ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка)
	|										)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ								КАК СверхЗаказа,
	|	ЛОЖЬ								КАК Отменено,
	|	ИСТИНА								КАК ЭтоНакладная,
	|	ВЫБОР
	|		КОГДА ДанныеШапки.ОтгрузкаПоЗаказам
	|				И &ИспользоватьРасширенныеВозможностиЗаказа
	|				И НЕ ТоварыДокумента.ЗаказКлиента В (
	|										НЕОПРЕДЕЛЕНО,
	|										ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка),
	|										ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка)
	|										)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ								КАК ОтгрузкаПоЗаказу
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ТоварыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровКлиенту КАК ДанныеШапки
	|		ПО ТоварыДокумента.Ссылка = ДанныеШапки.Ссылка
	|
	|ГДЕ
	|	ТоварыДокумента.Ссылка В(&Ссылка)
	|	И ТоварыДокумента.Количество <> 0";
	
	ПодставитьТекстИспользоватьРасширенныеВозможностиЗаказаКлиента(
		ТекстЗапросаДанныхДокумента, "ДанныеШапки", "&ИспользоватьРасширенныеВозможностиЗаказа", "ТоварыДокумента");
	
	ИспользоватьРасширенныеВозможностиЗаказа = ?(ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента"),
												"ИСТИНА",
												"ЛОЖЬ");
	
	ТекстЗапросаДанныхДокумента = СтрЗаменить(ТекстЗапросаДанныхДокумента,
												"&ИспользоватьРасширенныеВозможностиЗаказа",
												ИспользоватьРасширенныеВозможностиЗаказа);
	
	СкладыСервер.ЗапланироватьОтгрузкуТоваров(Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаДанныхДокумента);
	
КонецПроцедуры

Процедура ОформитьОтгрузкуТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ТекстЗапросаДанныхДокумента = 
	"ВЫБРАТЬ
	|	ТоварыДокумента.Ссылка				КАК Ссылка,
	|	ДанныеШапки.Дата					КАК Период,
	|	ВЫБОР
	|		КОГДА ДанныеШапки.ОтгрузкаПоЗаказам
	|				И &ИспользоватьРасширенныеВозможностиЗаказа
	|				И НЕ ТоварыДокумента.ЗаказКлиента В (
	|										НЕОПРЕДЕЛЕНО,
	|										ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка),
	|										ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка)
	|										)
	|			ТОГДА ТоварыДокумента.ЗаказКлиента
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ								КАК Заказ,
	|	ТоварыДокумента.Ссылка				КАК Накладная,
	|	ДанныеШапки.Исправление				КАК Исправление,
	|	ДанныеШапки.ИсправляемыйДокумент	КАК ИсправляемыйДокумент,
	|	ДанныеШапки.Партнер					КАК Получатель,
	|	ТоварыДокумента.Склад				КАК Склад,
	|	ТоварыДокумента.Номенклатура		КАК Номенклатура,
	|	ТоварыДокумента.Характеристика		КАК Характеристика,
	|	ТоварыДокумента.Назначение			КАК Назначение,
	|	ТоварыДокумента.Серия				КАК Серия,
	|	ТоварыДокумента.СтатусУказанияСерий	КАК СтатусУказанияСерий,
	|	ТоварыДокумента.Количество			КАК Количество,
	|	ВЫБОР
	|		КОГДА ДанныеШапки.ОтгрузкаПоЗаказам
	|				И &ИспользоватьРасширенныеВозможностиЗаказа
	|				И ТоварыДокумента.КодСтроки = 0
	|				И НЕ ТоварыДокумента.ЗаказКлиента В (
	|										НЕОПРЕДЕЛЕНО,
	|										ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка),
	|										ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка)
	|										)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ								КАК СверхЗаказа,
	|	ВЫБОР
	|		КОГДА ДанныеШапки.ОтгрузкаПоЗаказам
	|				И &ИспользоватьРасширенныеВозможностиЗаказа
	|				И НЕ ТоварыДокумента.ЗаказКлиента В (
	|										НЕОПРЕДЕЛЕНО,
	|										ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка),
	|										ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка)
	|										)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ								КАК ОтгрузкаПоЗаказу
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ТоварыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровКлиенту КАК ДанныеШапки
	|		ПО ТоварыДокумента.Ссылка = ДанныеШапки.Ссылка
	|
	|ГДЕ
	|	ТоварыДокумента.Ссылка В(&Ссылка)
	|	И ТоварыДокумента.Количество <> 0
	|";
	
	ПодставитьТекстИспользоватьРасширенныеВозможностиЗаказаКлиента(
		ТекстЗапросаДанныхДокумента, "ДанныеШапки", "&ИспользоватьРасширенныеВозможностиЗаказа");
	
	ИспользоватьРасширенныеВозможностиЗаказа =
		?(ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента"),
			"ИСТИНА",
			"ЛОЖЬ");
	
	ТекстЗапросаДанныхДокумента = СтрЗаменить(ТекстЗапросаДанныхДокумента,
												"&ИспользоватьРасширенныеВозможностиЗаказа",
												ИспользоватьРасширенныеВозможностиЗаказа);
	
	ОбъектМетаданных	= Метаданные.Документы.ОтгрузкаТоваровКлиенту;
	ИмяТЧСерии			= "Серии";
	
	СкладыСервер.ОформитьОтгрузкуТоваров(Запрос,
										ТекстыЗапроса,
										Регистры,
										ТекстЗапросаДанныхДокумента,
										ОбъектМетаданных,
										ИмяТЧСерии);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыОрганизаций";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТоварыОрганизаций", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТоварыОрганизаций(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ВтТоварыОрганизаций.НомерСтроки				КАК НомерСтроки,
	|	втТоварыОрганизаций.ВидДвижения				КАК ВидДвижения,
	|	втТоварыОрганизаций.Период					КАК Период,
	|	втТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	втТоварыОрганизаций.Организация				КАК Организация,
	|	втТоварыОрганизаций.ВидЗапасов				КАК ВидЗапасов,
	|	втТоварыОрганизаций.НомерГТД				КАК НомерГТД,
	|	втТоварыОрганизаций.Количество				КАК Количество,
	|	втТоварыОрганизаций.КоличествоПоРНПТ		КАК КоличествоПоРНПТ,
	|	втТоварыОрганизаций.ХозяйственнаяОперация	КАК ХозяйственнаяОперация,
	|	втТоварыОрганизаций.НалогообложениеНДС		КАК НалогообложениеНДС,
	|	втТоварыОрганизаций.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	|	втТоварыОрганизаций.КорВидЗапасов			КАК КорВидЗапасов,
	|	втТоварыОрганизаций.ОрганизацияОтгрузки		КАК ОрганизацияОтгрузки,
	|	втТоварыОрганизаций.Номенклатура			КАК Номенклатура,
	|	втТоварыОрганизаций.Характеристика			КАК Характеристика
	|ИЗ
	|	втТоварыОрганизаций КАК втТоварыОрганизаций
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса временной таблицы движений регистра накопления ТоварыОрганизаций.
//
// Параметры:
//	Запрос - Запрос - запрос проведения документа.
//	ТекстыЗапроса - СписокЗначений Из Строка - коллекция текстов запроса проведения.
//
// Возвращаемое значение:
//	Строка - текст запроса временной таблицы движений регистра накопления ТоварыОрганизаций.
//
Функция ТекстЗапросаВтТоварыОрганизаций(Запрос, ТекстыЗапроса) Экспорт
	
	ИмяТаблицы = "ВтТоварыОрганизаций";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасов", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки		КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период								КАК Период,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасовРеализацияЗапасовДругойОрганизации
	|			ТОГДА ТаблицаВидыЗапасов.ВидЗапасовВидЗапасовВладельцаОрганизация
	|		ИНАЧЕ ТаблицаВидыЗапасов.Организация
	|	КОНЕЦ								КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасовРеализацияЗапасовДругойОрганизации
	|			ТОГДА ТаблицаВидыЗапасов.ВидЗапасовВидЗапасовВладельца
	|		ИНАЧЕ ТаблицаВидыЗапасов.ВидЗапасов
	|	КОНЕЦ								КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД			КАК НомерГТД,
	|	ТаблицаВидыЗапасов.НомерГТДТипНомераГТД КАК НомерГТДТипНомераГТД,
	|	ТаблицаВидыЗапасов.Количество		КАК Количество,
	|	ТаблицаВидыЗапасов.КоличествоПоРНПТ	КАК КоличествоПоРНПТ,
	|	ТаблицаВидыЗапасов.СуммаБезНДСРегл	КАК СтоимостьПоРНПТ,
	|	ТаблицаВидыЗапасов.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	&ХозяйственнаяОперация				КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО						КАК НалогообложениеНДС,
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО						КАК КорВидЗапасов,
	|	ТаблицаВидыЗапасов.Организация		КАК ОрганизацияОтгрузки,
	|	ТаблицаВидыЗапасов.Номенклатура		КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика	КАК Характеристика
	|ПОМЕСТИТЬ втТоварыОрганизаций
	|ИЗ
	|	ВтТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки		КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Период								КАК Период,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатурыОтгрузки	КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.Организация		КАК Организация,
	|	ТаблицаВидыЗапасов.ВидЗапасов		КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД			КАК НомерГТД,
	|	ТаблицаВидыЗапасов.НомерГТДТипНомераГТД КАК НомерГТДТипНомераГТД,
	|	ТаблицаВидыЗапасов.Количество		КАК Количество,
	|	ТаблицаВидыЗапасов.КоличествоПоРНПТ	КАК КоличествоПоРНПТ,
	|	ТаблицаВидыЗапасов.СуммаБезНДСРегл	КАК СтоимостьПоРНПТ,
	|	ТаблицаВидыЗапасов.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	&ХозяйственнаяОперация				КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО						КАК НалогообложениеНДС,
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО						КАК КорВидЗапасов,
	|	ТаблицаВидыЗапасов.Организация		КАК ОрганизацияОтгрузки,
	|	ТаблицаВидыЗапасов.Номенклатура		КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика	КАК Характеристика
	|ИЗ
	|	ВтТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерГТДТипНомераГТД
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаВидыЗапасов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаВидыЗапасов";
	
	ИнициализироватьКлючиАналитикиУчетаНоменклатуры(Запрос);
	СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ВидыЗапасов.НомерСтроки                КАК НомерСтроки,
	|	ВидыЗапасов.Ссылка                     КАК Ссылка,
	|	ВидыЗапасов.ЗаказКлиента               КАК ЗаказКлиента,
	|	&Организация                           КАК Организация,
	|	Аналитика.МестоХранения                КАК Склад,
	|	ВидыЗапасов.ВидЗапасов                 КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасов.ТипЗапасов      КАК ТипЗапасов,
	|	ВидыЗапасов.ВидЗапасов.РеализацияЗапасовДругойОрганизации КАК ВидЗапасовРеализацияЗапасовДругойОрганизации,
	|	ВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца КАК ВидЗапасовВидЗапасовВладельца,
	|	ВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.Организация КАК ВидЗапасовВидЗапасовВладельцаОрганизация,
	|	ВидыЗапасов.АналитикаУчетаНаборов      КАК АналитикаУчетаНаборов,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	АналитикаБезНазначения.КлючАналитики   КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ВидыЗапасов.АналитикаУчетаНоменклатурыОтгрузки КАК АналитикаУчетаНоменклатурыОтгрузки,
	|	АналитикаОтгрузкиБезНазначения.КлючАналитики КАК АналитикаУчетаНоменклатурыОтгрузкиБезНазначения,
	|	ЕСТЬNULL(СтавкиНДСНоменклатурыСтраны.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)) КАК СтавкаНДС,
	|	Аналитика.Номенклатура                 КАК Номенклатура,
	|	Аналитика.Характеристика               КАК Характеристика,
	|	Аналитика.Серия						   КАК Серия,
	|	ВидыЗапасов.Упаковка                   КАК Упаковка,
	|	ВидыЗапасов.Количество                 КАК Количество,
	|	ВидыЗапасов.КоличествоУпаковок         КАК КоличествоУпаковок,
	|	ВидыЗапасов.КоличествоПоРНПТ           КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0)     КАК СуммаБезНДСРегл,
	|	ВидыЗапасов.Сумма                      КАК Сумма,
	|	ВидыЗапасов.НомерГТД                   КАК НомерГТД,
	|	ВидыЗапасов.НомерГТД.ТипНомераГТД      КАК НомерГТДТипНомераГТД,
	|	ВидыЗапасов.КодТНВЭД                   КАК КодТНВЭД,
	|	ВидыЗапасов.КодСтроки                  КАК КодСтроки,
	|	ЕСТЬNULL(Аналитика.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ) КАК ПрослеживаемыйТовар,
	|	ВЫБОР
	|		КОГДА Аналитика.Назначение.НаправлениеДеятельности ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|		ИНАЧЕ Аналитика.Назначение.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА &НалогообложениеНДС В (&ТипыНалогообложенияНДСНеУчитываетсяВСтоимости)
	|			И ЕСТЬNULL(СтавкиНДС.Ссылка, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт) И ВидыЗапасов.КодТНВЭД.СырьевойТовар
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг)
	|		КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
	|		КОГДА ЕСТЬNULL(Аналитика.Назначение.ВидДеятельностиНДС, &НалогообложениеНДС) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)
	|		ТОГДА &НалогообложениеОрганизации
	|		ИНАЧЕ ЕСТЬNULL(Аналитика.Назначение.ВидДеятельностиНДС, &НалогообложениеНДС)
	|	КОНЕЦ КАК ВидДеятельностиНДС,
	|	ВидыЗапасов.ИдентификаторСтроки        КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВтТаблицаВидыЗапасов
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.ВидыЗапасов КАК ВидыЗапасов
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО 
	|		ВидыЗапасов.Ссылка = Суммы.Ссылка
	|		И ВидыЗапасов.ИдентификаторСтроки = Суммы.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ВидыЗапасов.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|		ПО Аналитика.Номенклатура = АналитикаБезНазначения.Номенклатура
	|			И Аналитика.Характеристика = АналитикаБезНазначения.Характеристика
	|			И Аналитика.Серия = АналитикаБезНазначения.Серия
	|			И Аналитика.МестоХранения = АналитикаБезНазначения.МестоХранения
	|			И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	//++ НЕ УТ
	|			И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = АналитикаБезНазначения.СтатьяКалькуляции
	//-- НЕ УТ
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаОтгрузкиБезНазначения
	|		ПО ВидыЗапасов.АналитикаУчетаНоменклатурыОтгрузки.Номенклатура = АналитикаОтгрузкиБезНазначения.Номенклатура
	|			И ВидыЗапасов.АналитикаУчетаНоменклатурыОтгрузки.Характеристика = АналитикаОтгрузкиБезНазначения.Характеристика
	|			И ВидыЗапасов.АналитикаУчетаНоменклатурыОтгрузки.Серия = АналитикаОтгрузкиБезНазначения.Серия
	|			И ВидыЗапасов.АналитикаУчетаНоменклатурыОтгрузки.МестоХранения = АналитикаОтгрузкиБезНазначения.МестоХранения
	|			И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаОтгрузкиБезНазначения.Назначение
	//++ НЕ УТ
	|			И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = АналитикаОтгрузкиБезНазначения.СтатьяКалькуляции
	//-- НЕ УТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(&Период, Страна = &СтранаРегистрации) КАК СтавкиНДСНоменклатурыСтраны
	|		ПО Аналитика.Номенклатура = СтавкиНДСНоменклатурыСтраны.Номенклатура
	|			И (СтавкиНДСНоменклатурыСтраны.СтавкаНДС.НачалоПериода <= &Период
	|				ИЛИ СтавкиНДСНоменклатурыСтраны.СтавкаНДС.НачалоПериода = ДАТАВРЕМЯ(1, 1, 1))
	|			И (СтавкиНДСНоменклатурыСтраны.СтавкаНДС.КонецПериода >= &Период
	|				ИЛИ СтавкиНДСНоменклатурыСтраны.СтавкаНДС.КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(&Период, Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) КАК СтавкиНДСНоменклатуры
	|		ПО Аналитика.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура
	|			И (СтавкиНДСНоменклатуры.СтавкаНДС.НачалоПериода <= &Период
	|				ИЛИ СтавкиНДСНоменклатуры.СтавкаНДС.НачалоПериода = ДАТАВРЕМЯ(1, 1, 1))
	|			И (СтавкиНДСНоменклатуры.СтавкаНДС.КонецПериода >= &Период
	|				ИЛИ СтавкиНДСНоменклатуры.СтавкаНДС.КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСтавкиНДС.СрезПоследних(&Период, Страна = &СтранаРегистрации) КАК ОсновныеСтавкиНДС
	|		ПО (ОсновныеСтавкиНДС.СтавкаНДС.НачалоПериода <= &Период
	|			ИЛИ ОсновныеСтавкиНДС.СтавкаНДС.НачалоПериода = ДАТАВРЕМЯ(1, 1, 1))
	|			И (ОсновныеСтавкиНДС.СтавкаНДС.КонецПериода >= &Период
	|				ИЛИ ОсновныеСтавкиНДС.СтавкаНДС.КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтавкиНДС КАК СтавкиНДС
	|		ПО (СтавкиНДСНоменклатурыСтраны.СтавкаНДС = СтавкиНДС.Ссылка
	|				И (СтавкиНДС.НачалоПериода <= &Период ИЛИ СтавкиНДС.НачалоПериода = ДАТАВРЕМЯ(1, 1, 1))
	|				И (СтавкиНДС.КонецПериода >= &Период ИЛИ СтавкиНДС.КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|			ИЛИ СтавкиНДСНоменклатурыСтраны.СтавкаНДС ЕСТЬ NULL И СтавкиНДСНоменклатуры.СтавкаНДС = СтавкиНДС.Ссылка
	|				И (СтавкиНДС.НачалоПериода <= &Период ИЛИ СтавкиНДС.НачалоПериода = ДАТАВРЕМЯ(1, 1, 1))
	|				И (СтавкиНДС.КонецПериода >= &Период ИЛИ СтавкиНДС.КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|			ИЛИ СтавкиНДСНоменклатурыСтраны.СтавкаНДС ЕСТЬ NULL И ОсновныеСтавкиНДС.СтавкаНДС ЕСТЬ NULL И СтавкиНДСНоменклатуры.СтавкаНДС = СтавкиНДС.Ссылка
	|				И (СтавкиНДС.НачалоПериода <= &Период ИЛИ СтавкиНДС.НачалоПериода = ДАТАВРЕМЯ(1, 1, 1))
	|				И (СтавкиНДС.КонецПериода >= &Период ИЛИ СтавкиНДС.КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|		)
	|
	|ГДЕ
	|	ВидыЗапасов.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ИнициализироватьКлючиАналитикиУчетаНоменклатуры(Запрос)
	
	Если Запрос.Параметры.Свойство("КлючиАналитикиУчетаНоменклатурыИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросАналитик = Новый Запрос;
	ЗапросАналитик.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаАналитик.Номенклатура   КАК Номенклатура,
	|	ТаблицаАналитик.Характеристика КАК Характеристика,
	|	&ПустоеНазначение              КАК Назначение,
	|	ТаблицаАналитик.Серия          КАК Серия,
	|	ТаблицаАналитик.Склад          КАК Склад
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Товары.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
	|		Товары.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|		Товары.АналитикаУчетаНоменклатуры.Серия          КАК Серия,
	|		Товары.АналитикаУчетаНоменклатуры.МестоХранения  КАК Склад
	|	ИЗ
	|		Документ.ОтгрузкаТоваровКлиенту.Товары КАК Товары
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|			ПО Товары.АналитикаУчетаНоменклатуры.Номенклатура = Аналитика.Номенклатура
	|				И Товары.АналитикаУчетаНоменклатуры.Характеристика = Аналитика.Характеристика
	|				И Товары.АналитикаУчетаНоменклатуры.Серия = Аналитика.Серия
	|				И Товары.АналитикаУчетаНоменклатуры.МестоХранения = Аналитика.МестоХранения
	|				И &ПустоеНазначение = Аналитика.Назначение
	//++ НЕ УТ
	|				И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика.СтатьяКалькуляции
	//-- НЕ УТ
	|
	|	ГДЕ
	|		Товары.Ссылка = &Ссылка
	|		И Аналитика.Номенклатура ЕСТЬ NULL
	|		И НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Товары.АналитикаУчетаНоменклатурыОтгрузки.Номенклатура   КАК Номенклатура,
	|		Товары.АналитикаУчетаНоменклатурыОтгрузки.Характеристика КАК Характеристика,
	|		Товары.АналитикаУчетаНоменклатурыОтгрузки.Серия          КАК Серия,
	|		Товары.АналитикаУчетаНоменклатурыОтгрузки.МестоХранения  КАК Склад
	|	ИЗ
	|		Документ.ОтгрузкаТоваровКлиенту.Товары КАК Товары
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|			ПО Товары.АналитикаУчетаНоменклатурыОтгрузки.Номенклатура = Аналитика.Номенклатура
	|				И Товары.АналитикаУчетаНоменклатурыОтгрузки.Характеристика = Аналитика.Характеристика
	|				И Товары.АналитикаУчетаНоменклатурыОтгрузки.Серия = Аналитика.Серия
	|				И Товары.АналитикаУчетаНоменклатурыОтгрузки.МестоХранения = Аналитика.МестоХранения
	|				И &ПустоеНазначение = Аналитика.Назначение
	//++ НЕ УТ
	|				И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика.СтатьяКалькуляции
	//-- НЕ УТ
	|
	|	ГДЕ
	|		Товары.Ссылка = &Ссылка
	|		И Аналитика.Номенклатура ЕСТЬ NULL
	|		И НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|	
	|	) КАК ТаблицаАналитик";
	
	ЗапросАналитик.УстановитьПараметр("Ссылка",				Запрос.Параметры.Ссылка);
	ЗапросАналитик.УстановитьПараметр("ПустоеНазначение",	Справочники.Назначения.ПустаяСсылка());
	ЗапросАналитик.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоНазначениям",
										Запрос.Параметры.УчитыватьСебестоимостьТоваровПоНазначениям);
	
	Выборка = ЗапросАналитик.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка);
	КонецЦикла;
	
	//Наборы
	ЗапросАналитик = Новый Запрос;
	ЗапросАналитик.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.АналитикаУчетаНаборов.НоменклатураНабора		КАК НоменклатураНабора,
	|	Товары.АналитикаУчетаНаборов.ХарактеристикаНабора	КАК ХарактеристикаНабора
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.ВидыЗапасов КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНаборов КАК Аналитика
	|		ПО Товары.АналитикаУчетаНаборов.НоменклатураНабора = Аналитика.НоменклатураНабора
	|			И Товары.АналитикаУчетаНаборов.ХарактеристикаНабора = Аналитика.ХарактеристикаНабора
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|	И Аналитика.НоменклатураНабора ЕСТЬ NULL";
	
	ЗапросАналитик.УстановитьПараметр("Ссылка", Запрос.Параметры.Ссылка);
	
	Выборка = ЗапросАналитик.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаНаборов.СоздатьКлючАналитики(Выборка);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КлючиАналитикиУчетаНоменклатурыИнициализированы", Истина);
	
КонецПроцедуры

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента      = "Документ.ОтгрузкаТоваровКлиенту";
	СинонимТаблицыДокумента = "";
	ВЗапросеЕстьИсточник    = Истина;
	
	ТекстЗапросаПереопределенияХозяйственнойОперации = "
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтгрузкаТоваровКлиенту)
		|		ИНАЧЕ ТаблицаТовары.Ссылка.ХозяйственнаяОперация
		|	КОНЕЦ";
	
	ЗначенияПараметров = Новый Структура();
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных",
								ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяДокумента));
	ЗначенияПараметров.Вставить("ИспользоватьРасширенныеВозможностиЗаказаКлиента",
								ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента"));
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать",         """""");
	ПереопределениеРасчетаПараметров.Вставить("ОтгрузкаПоЗаказам",     "ТаблицаТовары.Ссылка.ОтгрузкаПоЗаказам");
	ПереопределениеРасчетаПараметров.Вставить("ИнформацияПоДоговору",  """""");
	ПереопределениеРасчетаПараметров.Вставить("ХозяйственнаяОперация", ТекстЗапросаПереопределенияХозяйственнойОперации);
	
	Если ИмяРегистра = "РаспоряженияНаОтгрузкуИВозврат" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРаспоряженияНаОтгрузку(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ТаблицаТовары";
		
	ИначеЕсли ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если СинонимТаблицыДокумента <> "ТаблицаТовары" Тогда
		Для Каждого ПараметрРасчета Из ПереопределениеРасчетаПараметров Цикл
			ПереопределениеРасчетаПараметров[ПараметрРасчета.Ключ] = СтрЗаменить(
				ПараметрРасчета.Значение, "ТаблицаТовары", СинонимТаблицыДокумента);
		КонецЦикла;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ВЗапросеЕстьИсточник,
			ПереопределениеРасчетаПараметров);
		
	Иначе
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ПереопределениеРасчетаПараметров);
		
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ОтразитьРаспределениеЗапасовДвижения(Запрос, ТекстыЗапроса, Регистры)
	
	ТекстЗапросаТабЧасть =
	"ВЫБРАТЬ
	|	ТабЧасть.Ссылка         КАК Ссылка,
	|	ТабЧасть.Ссылка.Дата    КАК Период,
	|	ТабЧасть.Номенклатура   КАК Номенклатура,
	|	ТабЧасть.Характеристика КАК Характеристика,
	|	ТабЧасть.Склад          КАК Склад,
	|	ТабЧасть.Назначение     КАК Назначение,
	|	ТабЧасть.Количество     КАК Количество,
	|	НЕОПРЕДЕЛЕНО            КАК ЗапланированныйРасходРаспределенногоЗапаса,
	|	ИСТИНА                  КАК КонтрольСвободногоОстатка
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ТабЧасть
	|ГДЕ
	|	ТабЧасть.КодСтроки = 0
	|		ИЛИ НЕ ТабЧасть.Ссылка.ОтгрузкаПоЗаказам
	|		ИЛИ ТабЧасть.ЗаказКлиента В (
	|				НЕОПРЕДЕЛЕНО,
	|				ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка),
	|				ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка)
	|				)
	|		ИЛИ ЕСТЬNULL(ТабЧасть.ЗаказКлиента.ЭтоЗаказКакСчет, ЛОЖЬ)";
	
	РаспределениеЗапасовДвижения.РасходЗапаса(Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаТабЧасть);
	
КонецПроцедуры

Процедура ОтразитьВУчетеНДС(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	Если Не УчетНДСУП.ТребуетсяПроведениеПоРегистрамНДС(Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасов", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЦенности = "
	|ВЫБРАТЬ
	|	ОтгрузкаТоваров.Дата КАК Период,
	|	ОтгрузкаТоваров.Ссылка КАК Ссылка,
	|	ОтгрузкаТоваров.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ОтгрузкаТоваров.Контрагент КАК Контрагент,
	|	ОтгрузкаТоваров.Договор КАК Договор,
	|	ОтгрузкаТоваров.Грузоотправитель КАК Грузоотправитель,
	|	ОтгрузкаТоваров.Грузополучатель КАК Грузополучатель,
	|	ОтгрузкаТоваров.Организация КАК Организация,
	|	ОтгрузкаТоваров.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ОтгрузкаТоваров.Исправление
	|			ТОГДА ОтгрузкаТоваров.ИсправляемыйДокумент
	|		ИНАЧЕ
	|			ОтгрузкаТоваров.Ссылка
	|	КОНЕЦ КАК ДокументРеализации,
	|	ЛОЖЬ КАК РеализацияЧерезКомиссионера,
	|	ВЫБОР
	|		КОГДА ОтгрузкаТоваров.Исправление
	|			ТОГДА ОтгрузкаТоваров.Ссылка
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДокументКорректировкиРеализации,
	|	ВЫБОР
	|		КОГДА ОтгрузкаТоваров.ОтгрузкаПоЗаказам
	|			И НЕ ВидыЗапасовТЧ.ЗаказКлиента.ЭтоЗаказКакСчет
	|			ТОГДА ВидыЗапасовТЧ.ЗаказКлиента
	|		КОГДА ОтгрузкаТоваров.Исправление
	|			ТОГДА ОтгрузкаТоваров.ИсправляемыйДокумент
	|		ИНАЧЕ
	|			ОтгрузкаТоваров.Ссылка
	|	КОНЕЦ КАК РаспоряжениеНаОтгрузку,
	|	ОтгрузкаТоваров.Исправление КАК ИсправлениеОшибок,
	|	ЛОЖЬ КАК КорректировкаПоСогласованиюСторон,
	|	ЛОЖЬ КАК РеализацияВРозницу,
	|	ВЫБОР
	|		КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|			ТОГДА ЕСТЬNULL(ТоварыЗаказаКлиента.СтавкаНДС, &СтавкаНДСНаЭкспорт)
	|		ИНАЧЕ
	|			ЕСТЬNULL(ТоварыЗаказаКлиента.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка))
	|	КОНЕЦ КАК СтавкаНДС,
	|	ВидыЗапасовТЧ.Номенклатура                           КАК Номенклатура,
	|	ВидыЗапасовТЧ.Характеристика                         КАК Характеристика,
	|	ВидыЗапасовТЧ.Серия                                  КАК Серия,
	|	НЕОПРЕДЕЛЕНО                                         КАК НоменклатураПартнера,
	|	ВидыЗапасовТЧ.АналитикаУчетаНаборов.НоменклатураНабора     КАК НоменклатураНабора,
	|	ВидыЗапасовТЧ.АналитикаУчетаНаборов.ХарактеристикаНабора   КАК ХарактеристикаНабора,
	|	"""" КАК Содержание,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|				ИЛИ ВидыЗапасовТЧ.КоличествоУпаковок = 0
	|			ТОГДА ВидыЗапасовТЧ.Количество
	|		ИНАЧЕ ВидыЗапасовТЧ.КоличествоУпаковок
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ВидыЗапасовТЧ.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВидыЗапасовТЧ.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыЗаказаКлиента.Количество, 0) = 0
	|			ТОГДА ВидыЗапасовТЧ.Сумма
	|		ИНАЧЕ
	|			ТоварыЗаказаКлиента.СуммаСНДС * ВидыЗапасовТЧ.Количество / ТоварыЗаказаКлиента.Количество
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыЗаказаКлиента.Количество, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ
	|			ТоварыЗаказаКлиента.СуммаНДС * ВидыЗапасовТЧ.Количество / ТоварыЗаказаКлиента.Количество
	|	КОНЕЦ КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА &Валюта <> &ВалютаРегламентированногоУчета
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(ТоварыЗаказаКлиента.Количество, 0) = 0
	|			ТОГДА ВидыЗапасовТЧ.Сумма
	|		ИНАЧЕ
	|			ТоварыЗаказаКлиента.СуммаСНДС * ВидыЗапасовТЧ.Количество / ТоварыЗаказаКлиента.Количество
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА &Валюта <> &ВалютаРегламентированногоУчета
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(ТоварыЗаказаКлиента.Количество, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ
	|			ТоварыЗаказаКлиента.СуммаНДС * ВидыЗапасовТЧ.Количество / ТоварыЗаказаКлиента.Количество
	|	КОНЕЦ КАК СуммаНДСРегл,
	|	ВидыЗапасовТЧ.НомерГТД КАК НомерГТД,
	|	ВидыЗапасовТЧ.КодТНВЭД КАК КодТНВЭД,
	|	ВидыЗапасовТЧ.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасовТЧ.НомерСтроки КАК НомерСтроки,
	|	ВидыЗапасовТЧ.КодСтроки КАК КодСтроки,
	|	ВидыЗапасовТЧ.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВидыЗапасовТЧ.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ОтгрузкаТоваров.НалогообложениеНДС КАК НалогообложениеНДС,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ОтгрузкаТоваров
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаВидыЗапасов КАК ВидыЗапасовТЧ
	|		ПО ОтгрузкаТоваров.Ссылка = ВидыЗапасовТЧ.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ТоварыЗаказаКлиента
	|		ПО ВидыЗапасовТЧ.ЗаказКлиента = ТоварыЗаказаКлиента.Ссылка
	|			И ВидыЗапасовТЧ.КодСтроки = ТоварыЗаказаКлиента.КодСтроки
	|			И НЕ ВидыЗапасовТЧ.ЗаказКлиента.ЭтоЗаказКакСчет
	|		
	|ГДЕ
	// Это условие необходимо сохранить именно таким, в целях сохранения быстродействия на СУБД Postgresql
	|	ВЫБОР
	|			КОГДА НЕ ВидыЗапасовТЧ.Ссылка ЕСТЬ NULL
	|				ТОГДА ИСТИНА
	|		КОНЕЦ";
	
	ТекстЦенности = СтрЗаменить(
		ТекстЦенности,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ВидыЗапасовТЧ.Упаковка",
			"ВидыЗапасовТЧ.АналитикаУчетаНоменклатуры.Номенклатура"));
	
	УчетНДСУП.ОтразитьОтгрузкуКлиенту(Запрос, ТекстыЗапроса, Регистры, ТекстЦенности);
	
КонецПроцедуры

#Область ПартионныйУчет

Функция ОписаниеРегистровУчетаЗатратИСебестоимости(Документ) Экспорт
	
	ОписаниеРегистров = Новый Массив;
	ОписаниеРегистров.Добавить(Метаданные.РегистрыНакопления.СебестоимостьТоваров);
	
	Возврат ОписаниеРегистров;
	
КонецФункции

Функция УстановитьДополнительныеПараметрыОперацийУчетаЗатратИСебестоимости(Документ, Запрос = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Массив;
	
	Если Запрос <> Неопределено Тогда
		// Нет дополнительных параметров.
	КонецЕсли;
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

Функция СформироватьДополнительныеТаблицыОперацийУчетаЗатратИСебестоимости(Документ, Запрос = Неопределено, ТекстыЗапроса = Неопределено) Экспорт
	
	ДополнительныеТаблицы = Новый Массив;
	ДополнительныеТаблицы.Добавить("ВтТаблицаВидыЗапасов");
	
	Если Запрос <> Неопределено Тогда
	
		Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса(ДополнительныеТаблицы[0], ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаВидыЗапасов(Запрос, ТекстыЗапроса);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДополнительныеТаблицы;
	
КонецФункции

Функция ОписаниеОперацийУчетаСебестоимости(Документ) Экспорт
	
	ОписаниеОпераций = Новый Массив;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	&Период КАК Период,
	|	&Ссылка КАК Ссылка,
	|
	// Поля учета номенклатуры
	|	&Организация КАК Организация,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	ТаблицаВидыЗапасов.ВидДеятельностиНДС КАК ВидДеятельностиНДСДокумента,
	|
	// Корреспондирующие поля
	|	НЕОПРЕДЕЛЕНО КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО КАК КорПартия,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатурыОтгрузки КАК КорАналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК КорВидЗапасов,
	|
	// Поля аналитики финансового учета
	|	&Сделка КАК Сделка,
	|	&Подразделение КАК Подразделение,
	|	&Менеджер КАК Менеджер,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
	|
	// Количественные и суммовые показатели
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ТаблицаВидыЗапасов.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|
	// Прочие поля
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|";
	
	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.Перемещение,
		ТекстЗапроса);
		
	Возврат ОписаниеОпераций;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область Печать

// Возвращает правила печати печатной формы Задания на отбор (размещение) товаров.
//
// Возвращаемое значение:
//	Структура - состав свойств см. Обработки.ПечатьЗаданияНаОтборРазмещениеТоваров.ДобавитьКомандуПечати.
//
Функция ПравилаПечатиЗаданияНаОтборРазмещение() Экспорт
	
	ПравилаПечатиЗадания = Обработки.ПечатьЗаданияНаОтборРазмещениеТоваров.ПравилаПечатиЗаданияНаОтборРазмещение();
	ПравилаПечатиЗадания.СкладыВТЧ = Истина;
	ПравилаПечатиЗадания.ИспользуютсяИсправленияДокумента = Истина;
		
	Возврат ПравилаПечатиЗадания;
	
КонецФункции

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	ЭтоПартнер = ПраваПользователяПовтИсп.ЭтоПартнер();
	
	// Отгрузка товаров клиенту
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная";
	КомандаПечати.Представление = НСтр("ru = 'Отгрузка товаров клиенту';
										|en = 'Goods shipment to customer'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 11;
	
	Если ПравоДоступа("Чтение", Метаданные.Справочники.СертификатыНоменклатуры)
		И ПолучитьФункциональнуюОпцию("ИспользоватьСертификатыНоменклатуры") Тогда
		
		// Реестр сертификатов номенклатуры
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "СертификатыНоменклатурыРеестр";
		КомандаПечати.Представление = НСтр("ru = 'Сертификаты (реестр)';
											|en = 'Certificates (registry)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 33;
		
		// Изображения сертификатов номенклатуры
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "СертификатыНоменклатурыИзображенияИзДокументов";
		КомандаПечати.Представление = НСтр("ru = 'Сертификаты (по каждой позиции документа)';
											|en = 'Certificates (by each document item)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 34;
		
		// Изображения сертификатов номенклатуры
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "СертификатыНоменклатурыИзображенияИзДокументовБезДублей";
		КомандаПечати.Представление = НСтр("ru = 'Сертификаты (по одному на сертификат)';
											|en = 'Certificates (one per certificate)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 35;
		
	КонецЕсли;
	
	Если Не ЭтоПартнер Тогда
		// Задание на отбор товаров
		Обработки.ПечатьЗаданияНаОтборРазмещениеТоваров.ДобавитьКомандуПечати(КомандыПечати, "ЗаданиеНаОтбор", 36);
	КонецЕсли;
	
	Если Не ЭтоПартнер
		И ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеДоставкой") Тогда
		
		// Этикетки доставки
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Обработчик = "УправлениеПечатьюУТКлиент.ПечатьЭтикетокДоставки";
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьЭтикетокИЦенников.Печать";
		КомандаПечати.Идентификатор = "ЭтикеткаДоставки";
		КомандаПечати.Представление = НСтр("ru = 'Этикетки доставки';
											|en = 'Delivery labels'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 38;

	КонецЕсли;
	
	ОтгрузкаТоваровКлиентуЛокализация.ДобавитьКомандыПечати(КомандыПечати);
	
КонецПроцедуры

// Формирует печатные формы объекта.
//
// Параметры:
//	МассивОбъектов        - Массив           - массив ссылок на объекты которые нужно распечатать,
//	ПараметрыПечати       - Структура        - структура дополнительных параметров печати,
//	КоллекцияПечатныхФорм - ТаблицаЗначений  - сформированные табличные документы,
//	ОбъектыПечати         - СписокЗначений   - список объектов печати,
//	ПараметрыВывода       - Структура        - параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"Накладная",
			НСтр("ru = 'Отгрузка товаров клиенту';
				|en = 'Goods shipment to customer'"),
			СформироватьПечатнуюФормуНакладная(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.ОтгрузкаТоваровКлиенту.ПФ_MXL_ОтгрузкаТоваровКлиенту");
	КонецЕсли;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СертификатыНоменклатурыРеестр") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СертификатыНоменклатурыРеестр",
			НСтр("ru = 'Сертификаты (реестр)';
				|en = 'Certificates (registry)'"),
			Справочники.СертификатыНоменклатуры.СформироватьПечатнуюФормуРеестрСертификатовНоменклатуры(МассивОбъектов,
																										ОбъектыПечати),
			,
			"Справочник.СертификатыНоменклатуры.ПФ_MXL_РеестрСертификатов");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СертификатыНоменклатурыИзображенияИзДокументов") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СертификатыНоменклатурыИзображенияИзДокументов",
			НСтр("ru = 'Сертификаты (по каждой позиции документа)';
				|en = 'Certificates (by each document item)'"),
			Справочники.СертификатыНоменклатуры.СформироватьПечатнуюФормуИзображенияСертификатовИзДокументов(МассивОбъектов,
																											ОбъектыПечати),
			,
			"Справочник.СертификатыНоменклатуры.ПФ_MXL_ИзображенияСертификатов");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СертификатыНоменклатурыИзображенияИзДокументовБезДублей") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СертификатыНоменклатурыИзображенияИзДокументовБезДублей",
			НСтр("ru = 'Сертификаты (по одному на сертификат)';
				|en = 'Certificates (one per certificate)'"),
			Справочники.СертификатыНоменклатуры.СформироватьПечатнуюФормуИзображенияСертификатовИзДокументовБезДублей(
				МассивОбъектов,ОбъектыПечати),
			,
			"Справочник.СертификатыНоменклатуры.ПФ_MXL_ИзображенияСертификатов");
	КонецЕсли;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "КомплектДокументов") Тогда
		КоллекцияПечатныхФорм.Очистить();
		
		СформироватьКомплектПечатныхФорм(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати);
	КонецЕсли;
	
	ОтгрузкаТоваровКлиентуЛокализация.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати,
		ПараметрыВывода);
	ФормированиеПечатныхФорм.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);
	
КонецПроцедуры

// Сформировать печатные формы объектов.
//
// Параметры:
//	МассивОбъектов        - Массив           - массив ссылок на объекты которые нужно распечатать,
//	ПараметрыПечати       - Структура        - структура дополнительных параметров печати,
//	КоллекцияПечатныхФорм - ТаблицаЗначений  - сформированные табличные документы,
//	ОбъектыПечати         - СписокЗначений   - список объектов печати.
//
// Возвращаемое значение:
//  Неопределено
//
Функция СформироватьКомплектПечатныхФорм(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати) Экспорт
	
	Перем АдресКомплектаПечатныхФорм;
	
	Если ТипЗнч(ПараметрыПечати) = Тип("Структура")
		И ПараметрыПечати.Свойство("АдресКомплектаПечатныхФорм", АдресКомплектаПечатныхФорм) Тогда
		
		КомплектПечатныхФорм = ПолучитьИзВременногоХранилища(АдресКомплектаПечатныхФорм);
		
	Иначе
		
		КомплектПечатныхФорм =
			РегистрыСведений.НастройкиПечатиОбъектов.КомплектПечатныхФорм(
				Метаданные.Документы.ОтгрузкаТоваровКлиенту.ПолноеИмя(),
				МассивОбъектов,
				Неопределено);
		
	КонецЕсли;
	
	Если КомплектПечатныхФорм = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КомплектыПечатиПоОбъектам =
		РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(
			КоллекцияПечатныхФорм,
			КомплектПечатныхФорм,
			МассивОбъектов,
			"Накладная");
	
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			СформироватьПечатнуюФормуНакладная(КомплектПечати.Объекты, ОбъектыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам =
		РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(
			КоллекцияПечатныхФорм,
			КомплектПечатныхФорм,
			МассивОбъектов,
			"ЗаданиеНаОтборРазмещениеТовара");
	
	ПараметрыПечати.Вставить("ТипЗадания", "ЗаданиеНаОтбор");
	
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтгрузкаТоваровКлиенту", КомплектПечати.Объекты);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьЗаданияНаОтборРазмещениеТоваров.СформироватьЗаданиеНаОтборРазмещениеТовара(
				СтруктураТипов,
				ОбъектыПечати,
				ПараметрыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам =
		РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(
			КоллекцияПечатныхФорм,
			КомплектПечатныхФорм,
			МассивОбъектов,
			"СертификатыНоменклатурыРеестр");
	
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Справочники.СертификатыНоменклатуры.СформироватьПечатнуюФормуРеестрСертификатовНоменклатуры(
				КомплектПечати.Объекты,
				ОбъектыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам =
		РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(
			КоллекцияПечатныхФорм,
			КомплектПечатныхФорм,
			МассивОбъектов,
			"СертификатыНоменклатурыИзображенияИзДокументов");
	
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Справочники.СертификатыНоменклатуры.СформироватьПечатнуюФормуИзображенияСертификатовИзДокументов(
				КомплектПечати.Объекты,
				ОбъектыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам =
		РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(
			КоллекцияПечатныхФорм,
			КомплектПечатныхФорм,
			МассивОбъектов,
			"СертификатыНоменклатурыИзображенияИзДокументовБезДублей");
	
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Справочники.СертификатыНоменклатуры.СформироватьПечатнуюФормуИзображенияСертификатовИзДокументовБезДублей(
				КомплектПечати.Объекты,
				ОбъектыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"InvoiceInt");
	
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтгрузкаТоваровКлиенту", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьСчетовНаОплату.СформироватьПечатнуюФормуInvoice(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
	КонецЦикла;
	
	ОтгрузкаТоваровКлиентуЛокализация.СформироватьКомплектПечатныхФорм(
		МассивОбъектов,
		ПараметрыПечати,
		КоллекцияПечатныхФорм,
		ОбъектыПечати,
		КомплектПечатныхФорм);
	
	РегистрыСведений.НастройкиПечатиОбъектов.СформироватьКомплектВнешнихПечатныхФорм(
		"Документ.ОтгрузкаТоваровКлиенту",
		МассивОбъектов,
		ПараметрыПечати,
		КоллекцияПечатныхФорм,
		ОбъектыПечати);
	
КонецФункции

Функция КомплектПечатныхФорм() Экспорт
	
	КомплектПечатныхФорм = РегистрыСведений.НастройкиПечатиОбъектов.ПодготовитьКомплектПечатныхФорм();
	
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм,
																			"Накладная",
																			НСтр("ru = 'Отгрузка товаров клиенту';
																				|en = 'Goods shipment to customer'"),
																			2);
	Если Константы.ИспользоватьМеждународныеПечатныеФормы.Получить() Тогда
		РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм, "InvoiceInt", НСтр("ru = 'Invoice';
																														|en = 'Invoice'"), 2);
	КонецЕсли;
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм,
																			"РеестрНомеровГТД",
																			НСтр("ru = 'Реестр номеров ГТД';
																				|en = 'CCD number registry'"),
																			0);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСертификатыНоменклатуры") Тогда
		РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм,
																				"СертификатыНоменклатурыРеестр",
																				НСтр("ru = 'Сертификаты (реестр)';
																					|en = 'Certificates (registry)'"),
																				0);
		
		РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм,
																				"СертификатыНоменклатурыИзображенияИзДокументов",
																				НСтр("ru = 'Сертификаты (по каждой позиции документа)';
																					|en = 'Certificates (by each document item)'"),
																				0);
		
		РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм,
																				"СертификатыНоменклатурыИзображенияИзДокументовБезДублей",
																				НСтр("ru = 'Сертификаты (по одному на сертификат)';
																					|en = 'Certificates (one per certificate)'"),
																				0);
	КонецЕсли;
	
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм,
																			"ЗаданиеНаОтборРазмещениеТовара",
																			НСтр("ru = 'Задание на отбор товаров';
																				|en = 'Picking job'"),
																			0);
	
	ОтгрузкаТоваровКлиентуЛокализация.КомплектПечатныхФорм(КомплектПечатныхФорм);
	
	Возврат КомплектПечатныхФорм;
	
КонецФункции

// Функция формирует печатную форму документов отгрузки товаров клиенту.
//
// Параметры:
//	МассивОбъектов  - Массив         - массив ссылок на объекты которые нужно распечатать,
//	ОбъектыПечати   - СписокЗначений - список объектов печати.
//
// Возвращаемое значение:
//	ТабличныйДокумент - печатная форма документов отгрузки товаров клиенту.
//
Функция СформироватьПечатнуюФормуНакладная(МассивОбъектов, ОбъектыПечати)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтгрузкаТоваров.Ссылка          КАК Ссылка,
	|	ОтгрузкаТоваров.Номер           КАК Номер,
	|	ОтгрузкаТоваров.Дата            КАК Дата,
	|	ОтгрузкаТоваров.ИсправляемыйДокумент.Номер КАК НомерИсправляемогоДокумента,
	|	ОтгрузкаТоваров.ИсправляемыйДокумент.Дата  КАК ДатаИсправляемогоДокумента,
	|	ОтгрузкаТоваров.Партнер         КАК Партнер,
	|	ОтгрузкаТоваров.Контрагент      КАК Получатель,
	|	ОтгрузкаТоваров.Организация     КАК Организация,
	|	ОтгрузкаТоваров.Организация.Префикс КАК Префикс,
	|	ОтгрузкаТоваров.Валюта          КАК Валюта,
	|	ОтгрузкаТоваров.Отпустил        КАК ОтпускПроизвел,
	|	ОтгрузкаТоваров.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ОтгрузкаТоваров
	|ГДЕ
	|	ОтгрузкаТоваров.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ОтгрузкаТоваров.Ссылка         КАК Ссылка,
	|	ОтгрузкаТоваров.НомерСтроки    КАК НомерСтроки,
	|	ВариантыКомплектации.Ссылка    КАК ВариантКомплектацииНоменклатуры,
	|	ВариантыКомплектации.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ВариантыКомплектации.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	ОтгрузкаТоваров.НоменклатураНабора   КАК НоменклатураНабора,
	|	ОтгрузкаТоваров.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ОтгрузкаТоваров.Номенклатура   КАК Номенклатура,
	|	ОтгрузкаТоваров.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ОтгрузкаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ОтгрузкаТоваров.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ОтгрузкаТоваров.Упаковка
	|	КОНЕЦ                          КАК ЕдиницаИзмерения,
	|	ОтгрузкаТоваров.Упаковка       КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ОтгрузкаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ &ТекстЗапросаКоэффициентУпаковки1
	|	КОНЕЦ                          КАК Коэффициент,
	|	ОтгрузкаТоваров.Количество     КАК Количество,
	|	ОтгрузкаТоваров.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ОтгрузкаТоваров.Сумма / ОтгрузкаТоваров.КоличествоУпаковок КАК Цена,
	|	ОтгрузкаТоваров.Сумма          КАК Сумма
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ОтгрузкаТоваров
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектации
	|		ПО ВариантыКомплектации.Владелец = ОтгрузкаТоваров.НоменклатураНабора
	|		И ВариантыКомплектации.Характеристика = ОтгрузкаТоваров.ХарактеристикаНабора
	|		И ВариантыКомплектации.Основной
	|ГДЕ
	|	ОтгрузкаТоваров.Ссылка В(&МассивДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НоменклатураНабора,
	|	ХарактеристикаНабора
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ДокументыПечати
	|ИЗ
	|	Товары КАК Товары
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.ВариантКомплектацииНоменклатуры КАК ВариантКомплектацииНоменклатуры
	|ПОМЕСТИТЬ КомплектацииДокументов
	|ИЗ
	|	Товары КАК Товары
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 4
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка                КАК Ссылка,
	|	МИНИМУМ(ТаблицаТоваров.НомерСтроки)  КАК НомерСтроки,
	|	ТаблицаТоваров.НоменклатураНабора    КАК НоменклатураНабора,
	|	ТаблицаТоваров.ХарактеристикаНабора  КАК ХарактеристикаНабора,
	|	СУММА(ТаблицаТоваров.Сумма)          КАК Сумма
	|ПОМЕСТИТЬ ВременнаяТаблицаНаборыПодготовка
	|ИЗ
	|	Товары КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.НоменклатураНабора,
	|	ТаблицаТоваров.ХарактеристикаНабора
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НоменклатураНабора,
	|	ХарактеристикаНабора
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 5
	|ВЫБРАТЬ
	|	Товары.Ссылка               КАК Ссылка,
	|	ВЫБОР
	|		КОГДА Товары.ВариантКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента = Товары.Номенклатура
	|				И Товары.ВариантКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента = Товары.Характеристика
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                       КАК ОсновнаяКомплектующая,
	|	Товары.ВариантКомплектацииНоменклатуры КАК ВариантКомплектацииНоменклатуры,
	|	Товары.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	Товары.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	Товары.НоменклатураНабора   КАК НоменклатураНабора,
	|	Товары.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	Товары.Номенклатура         КАК Номенклатура,
	|	Товары.Характеристика       КАК Характеристика,
	|	0                           КАК КоличествоПоУмолчанию,
	|	Товары.Количество           КАК Количество
	|ПОМЕСТИТЬ ВременнаяТаблицаНаборыДополнительноЧастьПервая
	|ИЗ
	|	Товары КАК Товары
	|ГДЕ
	|	Товары.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыПечати.Ссылка                     КАК Ссылка,
	|	ЛОЖЬ                                       КАК ОсновнаяКомплектующая,
	|	ВариантыКомплектации.Ссылка                КАК ВариантКомплектацииНоменклатуры,
	|	ВариантыКомплектации.Ссылка.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ВариантыКомплектации.Ссылка.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	ВариантыКомплектации.Ссылка.Владелец       КАК НоменклатураНабора,
	|	ВариантыКомплектации.Ссылка.Характеристика КАК ХарактеристикаНабора,
	|	ВариантыКомплектации.Номенклатура          КАК Номенклатура,
	|	ВариантыКомплектации.Характеристика        КАК Характеристика,
	|	СУММА(ВариантыКомплектации.Количество)     КАК КоличествоПоУмолчанию,
	|	0                                          КАК Количество
	|ИЗ
	|	Справочник.ВариантыКомплектацииНоменклатуры.Товары КАК ВариантыКомплектации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыПечати КАК ДокументыПечати
	|		ПО ИСТИНА
	|ГДЕ
	|	ВариантыКомплектации.Ссылка В
	|		(ВЫБРАТЬ
	|			Комплектации.ВариантКомплектацииНоменклатуры
	|		ИЗ КомплектацииДокументов КАК Комплектации)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыПечати.Ссылка,
	|	ВариантыКомплектации.Ссылка,
	|	ВариантыКомплектации.Ссылка.Владелец,
	|	ВариантыКомплектации.Ссылка.Характеристика,
	|	ВариантыКомплектации.Номенклатура,
	|	ВариантыКомплектации.Характеристика
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 6
	|ВЫБРАТЬ
	|	Таблица.Ссылка               КАК Ссылка,
	|	МАКСИМУМ(Таблица.ОсновнаяКомплектующая) КАК ОсновнаяКомплектующая,
	|	Таблица.ВариантКомплектацииНоменклатуры КАК ВариантКомплектацииНоменклатуры,
	|	Таблица.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	Таблица.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	Таблица.НоменклатураНабора   КАК НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	Таблица.Номенклатура         КАК Номенклатура,
	|	Таблица.Характеристика       КАК Характеристика,
	|	СУММА(Таблица.КоличествоПоУмолчанию) КАК КоличествоПоУмолчанию,
	|	СУММА(Таблица.Количество)    КАК Количество
	|ПОМЕСТИТЬ ВременнаяТаблицаНаборыДополнительноЧастьВторая
	|ИЗ
	|	ВременнаяТаблицаНаборыДополнительноЧастьПервая КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Ссылка,
	|	Таблица.ВариантКомплектацииНоменклатуры,
	|	Таблица.ВариантРасчетаЦеныНабора,
	|	Таблица.ВариантПредставленияНабораВПечатныхФормах,
	|	Таблица.НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 7
	|ВЫБРАТЬ
	|	Результат.Ссылка               КАК Ссылка,
	|	Результат.ВариантКомплектацииНоменклатуры КАК ВариантКомплектацииНоменклатуры,
	|	Результат.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	Результат.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	Результат.НоменклатураНабора   КАК НоменклатураНабора,
	|	Результат.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ВЫРАЗИТЬ(МИНИМУМ(ВЫБОР
	|		КОГДА Результат.КоличествоПоУмолчанию <> 0
	|				И Результат.ОсновнаяКомплектующая
	|			ТОГДА Результат.Количество / Результат.КоличествоПоУмолчанию
	|		ИНАЧЕ NULL
	|	КОНЕЦ) + 0.5 КАК Число(10,0)) - 1 КАК Количество,
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА Результат.КоличествоПоУмолчанию <> 0
	|			ТОГДА Результат.Количество / Результат.КоличествоПоУмолчанию
	|		ИНАЧЕ NULL
	|	КОНЕЦ)                         КАК КоэффициентМаксимум,
	|	ВЫРАЗИТЬ(МИНИМУМ(ВЫБОР
	|		КОГДА Результат.КоличествоПоУмолчанию <> 0
	|			ТОГДА Результат.Количество / Результат.КоличествоПоУмолчанию
	|		ИНАЧЕ NULL
	|	КОНЕЦ) + 0.5 КАК Число(10,0)) - 1 КАК КоэффициентМинимум
	|ПОМЕСТИТЬ ВременнаяТаблицаНаборыДополнительно
	|ИЗ
	|	ВременнаяТаблицаНаборыДополнительноЧастьВторая КАК Результат
	|
	|СГРУППИРОВАТЬ ПО
	|	Результат.Ссылка,
	|	Результат.ВариантКомплектацииНоменклатуры,
	|	Результат.ВариантРасчетаЦеныНабора,
	|	Результат.ВариантПредставленияНабораВПечатныхФормах,
	|	Результат.НоменклатураНабора,
	|	Результат.ХарактеристикаНабора
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НоменклатураНабора,
	|	ХарактеристикаНабора
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 8
	|ВЫБРАТЬ
	|	Таблица.Ссылка               КАК Ссылка,
	|	Таблица.НомерСтроки          КАК НомерСтроки,
	|	ВременнаяТаблицаНаборыДополнительно.ВариантКомплектацииНоменклатуры КАК ВариантКомплектацииНоменклатуры,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА ВременнаяТаблицаНаборыДополнительно.ВариантПредставленияНабораВПечатныхФормах = ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ТолькоНабор)
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие)
	|				ИНАЧЕ ВременнаяТаблицаНаборыДополнительно.ВариантПредставленияНабораВПечатныхФормах
	|				КОНЕЦ = ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие)
	|				И ВременнаяТаблицаНаборыДополнительно.ВариантРасчетаЦеныНабора В(
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоЦенам),
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоДолям))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.РассчитываетсяИзЦенКомплектующих)
	|		ИНАЧЕ ВременнаяТаблицаНаборыДополнительно.ВариантРасчетаЦеныНабора
	|	КОНЕЦ                        КАК ВариантРасчетаЦеныНабора,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаНаборыДополнительно.ВариантПредставленияНабораВПечатныхФормах = ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ТолькоНабор)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие)
	|		ИНАЧЕ ВременнаяТаблицаНаборыДополнительно.ВариантПредставленияНабораВПечатныхФормах
	|	КОНЕЦ                        КАК ВариантПредставленияНабораВПечатныхФормах,
	|	Таблица.НоменклатураНабора   КАК НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.Количество, 1) КАК Количество,
	|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.Количество, 1) КАК КоличествоУпаковок,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаНаборыДополнительно.КоэффициентМинимум = ВременнаяТаблицаНаборыДополнительно.КоэффициентМаксимум
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                        КАК ПолныйНабор,
	|	Таблица.Сумма                КАК Сумма
	|ПОМЕСТИТЬ ВременнаяТаблицаНаборы
	|ИЗ
	|	ВременнаяТаблицаНаборыПодготовка КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНаборыДополнительно КАК ВременнаяТаблицаНаборыДополнительно
	|		ПО Таблица.Ссылка = ВременнаяТаблицаНаборыДополнительно.Ссылка
	|			И Таблица.НоменклатураНабора = ВременнаяТаблицаНаборыДополнительно.НоменклатураНабора
	|			И Таблица.ХарактеристикаНабора = ВременнаяТаблицаНаборыДополнительно.ХарактеристикаНабора
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НоменклатураНабора,
	|	ХарактеристикаНабора
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 9
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Ссылка               КАК Ссылка,
	|	ВложенныйЗапрос.НомерСтроки          КАК НомерСтроки,
	|	ВложенныйЗапрос.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	ВложенныйЗапрос.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ВложенныйЗапрос.НоменклатураНабора   КАК НоменклатураНабора,
	|	ВложенныйЗапрос.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ВложенныйЗапрос.Номенклатура         КАК Номенклатура,
	|	ВложенныйЗапрос.Номенклатура.Код     КАК Код,
	|	ВложенныйЗапрос.Номенклатура.Артикул КАК Артикул,
	|	ВложенныйЗапрос.Номенклатура.НаименованиеПолное КАК ТоварНаименованиеПолное,
	|	ВложенныйЗапрос.Характеристика.НаименованиеПолное КАК Характеристика,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВложенныйЗапрос.Упаковка.Наименование
	|	КОНЕЦ                                КАК Упаковка,
	|	ВложенныйЗапрос.КоличествоУпаковок   КАК Количество,
	|	ВложенныйЗапрос.Цена                 КАК Цена,
	|	ВложенныйЗапрос.Сумма                КАК Сумма,
	|	0                                    КАК СуммаНДС,
	|	ВложенныйЗапрос.ЭтоКомплектующие     КАК ЭтоКомплектующие,
	|	ВложенныйЗапрос.ЭтоНабор             КАК ЭтоНабор,
	|	ВложенныйЗапрос.ПолныйНабор          КАК ПолныйНабор,
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Ссылка               КАК Ссылка,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
	|				ТОГДА ВременнаяТаблицаНаборы.НомерСтроки
	|			ИНАЧЕ Таблица.НомерСтроки
	|		КОНЕЦ                        КАК НомерСтроки,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
	|				ТОГДА ВременнаяТаблицаНаборы.ВариантРасчетаЦеныНабора
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка)
	|		КОНЕЦ                        КАК ВариантРасчетаЦеныНабора,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
	|				ТОГДА ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ПустаяСсылка)
	|		КОНЕЦ                        КАК ВариантПредставленияНабораВПечатныхФормах,
	|		Таблица.НоменклатураНабора   КАК НоменклатураНабора,
	|		Таблица.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|		Таблица.Номенклатура         КАК Номенклатура,
	|		Таблица.Характеристика       КАК Характеристика,
	|		Таблица.ЕдиницаИзмерения     КАК ЕдиницаИзмерения,
	|		Таблица.Упаковка             КАК Упаковка,
	|		Таблица.Количество           КАК Количество,
	|		Таблица.КоличествоУпаковок   КАК КоличествоУпаковок,
	|		Таблица.Цена                 КАК Цена,
	|		Таблица.Сумма                КАК Сумма,
	|		ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС) КАК СтавкаНДС,
	|		0                            КАК СуммаНДС,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ                        КАК ЭтоКомплектующие,
	|		ЛОЖЬ                         КАК ЭтоНабор,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
	|				ТОГДА ВременнаяТаблицаНаборы.ПолныйНабор
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ                        КАК ПолныйНабор
	|	ИЗ
	|		Товары КАК Таблица
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНаборы КАК ВременнаяТаблицаНаборы
	|			ПО ВременнаяТаблицаНаборы.Ссылка = Таблица.Ссылка
	|				И ВременнаяТаблицаНаборы.НоменклатураНабора = Таблица.НоменклатураНабора
	|				И ВременнаяТаблицаНаборы.ХарактеристикаНабора = Таблица.ХарактеристикаНабора
	|	ГДЕ
	|		Таблица.НоменклатураНабора = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|		ИЛИ (Таблица.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			И ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах В(
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ТолькоКомплектующие),
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие)))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВременнаяТаблицаНаборы.Ссылка               КАК Ссылка,
	|		ВременнаяТаблицаНаборы.НомерСтроки          КАК НомерСтроки,
	|		ВременнаяТаблицаНаборы.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|		ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|		ВременнаяТаблицаНаборы.НоменклатураНабора   КАК НоменклатураНабора,
	|		ВременнаяТаблицаНаборы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|		ВременнаяТаблицаНаборы.НоменклатураНабора   КАК Номенклатура,
	|		ВременнаяТаблицаНаборы.ХарактеристикаНабора КАК Характеристика,
	|		ВременнаяТаблицаНаборы.НоменклатураНабора.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|		ВременнаяТаблицаНаборы.Количество           КАК Количество,
	|		ВременнаяТаблицаНаборы.КоличествоУпаковок   КАК КоличествоУпаковок,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.КоличествоУпаковок, 1) <> 0
	|				ТОГДА (ВременнаяТаблицаНаборы.Сумма) / ЕСТЬNULL(ВременнаяТаблицаНаборы.КоличествоУпаковок, 1)
	|			ИНАЧЕ 0
	|		КОНЕЦ                                       КАК Цена,
	|		ВременнаяТаблицаНаборы.Сумма                КАК Сумма,
	|		ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)       КАК СтавкаНДС,
	|		0                                           КАК СуммаНДС,
	|		ЛОЖЬ                                        КАК ЭтоКомплектующие,
	|		ИСТИНА                                      КАК ЭтоНабор,
	|		ВременнаяТаблицаНаборы.ПолныйНабор          КАК ПолныйНабор
	|	ИЗ
	|		ВременнаяТаблицаНаборы КАК ВременнаяТаблицаНаборы
	|	ГДЕ
	|		ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах В(
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ТолькоНабор),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие))) КАК ВложенныйЗапрос
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВложенныйЗапрос.Ссылка,
	|	НомерСтроки,
	|	ЭтоНабор УБЫВ
	|
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ОтгрузкаТоваров.Упаковка",
			"ОтгрузкаТоваров.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ВложенныйЗапрос.Упаковка",
			"ВложенныйЗапрос.Номенклатура"));
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	
	УстановитьПривилегированныйРежим(Истина);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	ПервыйДокумент = Истина;
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов();
	ИмяКолонкиКодов = КолонкаКодов.ИмяКолонки;
	ПредставлениеКолонкиКодов = КолонкаКодов.ПредставлениеКолонки;
	ВыводитьКоды   = ЗначениеЗаполнено(ИмяКолонкиКодов);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОтгрузкаТоваровКлиенту_Накладная";
	
	РезультатДанныеПечати			= МассивРезультатов[0]; // РезультатЗапроса
	РезультатВыборкаПоДокументам	= МассивРезультатов[9]; // РезультатЗапроса
	
	ДанныеПечати		= РезультатДанныеПечати.Выбрать();
	ВыборкаПоДокументам	= РезультатВыборкаПоДокументам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ДанныеПечати.Следующий() Цикл
		
		// Найдем в выборке товары по текущему документу
		СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);
		ВыборкаПоДокументам.НайтиСледующий(СтруктураПоиска);
		
		// Макет необходимо получать для каждого документа, т.к. размеры колонок изменяются динамически
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ОтгрузкаТоваровКлиенту.ПФ_MXL_ОтгрузкаТоваровКлиенту");
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент    = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Выводим шапку накладной
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьМакета,
			ДанныеПечати.Ссылка);
			
		СтрокаЗаголовка = НСтр("ru = 'Отгрузка товаров клиенту';
								|en = 'Goods shipment to customer'", ОбщегоНазначения.КодОсновногоЯзыка());
		
		ТекстЗаголовка = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(
			ДанныеПечати, СтрокаЗаголовка);
		
		СтруктураДанныхШапки = Новый Структура;
		СтруктураДанныхШапки.Вставить("ТекстЗаголовка", ТекстЗаголовка);
		
		ОбластьМакета.Параметры.Заполнить(СтруктураДанныхШапки);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Поставщик");
		
		ДатаПолученияСведений = ?(ЗначениеЗаполнено(ДанныеПечати.ДатаИсправляемогоДокумента),
									ДанныеПечати.ДатаИсправляемогоДокумента,
									ДанныеПечати.Дата);
		
		ПредставлениеПоставщика = ФормированиеПечатныхФорм.ОписаниеОрганизации(
			ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Организация, ДатаПолученияСведений),
				"ПолноеНаименование");
		
		СтруктураДанныхПоставщик = Новый Структура;
		СтруктураДанныхПоставщик.Вставить("Поставщик",               ДанныеПечати.Организация);
		СтруктураДанныхПоставщик.Вставить("ПредставлениеПоставщика", ПредставлениеПоставщика);
		
		ОбластьМакета.Параметры.Заполнить(СтруктураДанныхПоставщик);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Покупатель");
		
		ПредставлениеПолучателя = ФормированиеПечатныхФорм.ОписаниеОрганизации(
			ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Получатель, ДатаПолученияСведений),
				"ПолноеНаименование");
		
		СтруктураДанныхПокупатель = Новый Структура;
		СтруктураДанныхПокупатель.Вставить("Получатель",              ДанныеПечати.Получатель);
		СтруктураДанныхПокупатель.Вставить("ПредставлениеПолучателя", ПредставлениеПолучателя);
		
		ОбластьМакета.Параметры.Заполнить(СтруктураДанныхПокупатель);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Выводим заголовок таблицы Товары
		ШапкаТаблицы = "ШапкаТаблицы";
		ПостфиксТовар = "Товар";
		Данные = "Данные";
		
		ОбластьКолонкаТовар = Макет.Область("ПерваяКолонкаТовара");
		ОбластьКолонкаТовар.ШиринаКолонки = ОбластьКолонкаТовар.ШиринаКолонки
			+ ?(ВыводитьКоды, 0, Макет.Область("КолонкаКодов").ШиринаКолонки);
		
		ОбластьНомера = Макет.ПолучитьОбласть(ШапкаТаблицы + "|НомерСтроки");
		ОбластьКодов  = Макет.ПолучитьОбласть(ШапкаТаблицы + "|КолонкаКодов");
		ОбластьТовар  = Макет.ПолучитьОбласть(ШапкаТаблицы + "|" + ПостфиксТовар);
		ОбластьДанных = Макет.ПолучитьОбласть(ШапкаТаблицы + "|" + Данные);
		
		ТабличныйДокумент.Вывести(ОбластьНомера);
		
		Если ВыводитьКоды Тогда
			СтруктураДанныхКоды = Новый Структура("ИмяКолонкиКодов", ПредставлениеКолонкиКодов);
			
			ОбластьКодов.Параметры.Заполнить(СтруктураДанныхКоды);
			ТабличныйДокумент.Присоединить(ОбластьКодов);
		КонецЕсли;
		
		ТабличныйДокумент.Присоединить(ОбластьТовар);
		ТабличныйДокумент.Присоединить(ОбластьДанных);
		
		СтрокаТаблицы = "СтрокаТаблицы";
		ОбластьНомераСтрокиСтандарт = Макет.ПолучитьОбласть(СтрокаТаблицы + "|НомерСтроки");
		ОбластьКодовСтрокиСтандарт  = Макет.ПолучитьОбласть(СтрокаТаблицы + "|КолонкаКодов");
		ОбластьТоварСтрокиСтандарт  = Макет.ПолучитьОбласть(СтрокаТаблицы + "|" + ПостфиксТовар);
		ОбластьДанныхСтрокиСтандарт = Макет.ПолучитьОбласть(СтрокаТаблицы + "|" + Данные);
		
		ИспользоватьНаборы = Ложь;
		
		Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(ВыборкаПоДокументам, "ЭтоНабор") Тогда
			ИспользоватьНаборы = Истина;
			
			СтрокаТаблицыНабор = "СтрокаТаблицыНабор";
			СтрокаТаблицыКомплектующие = "СтрокаТаблицыКомплектующие";
			ОбластьНомераСтрокиНабор         = Макет.ПолучитьОбласть(СтрокаТаблицыНабор + "|НомерСтроки");
			ОбластьНомераСтрокиКомплектующие = Макет.ПолучитьОбласть(СтрокаТаблицыКомплектующие + "|НомерСтроки");
			ОбластьКодовСтрокиНабор          = Макет.ПолучитьОбласть(СтрокаТаблицыНабор + "|КолонкаКодов");
			ОбластьКодовСтрокиКомплектующие  = Макет.ПолучитьОбласть(СтрокаТаблицыКомплектующие + "|КолонкаКодов");
			ОбластьТоварСтрокиНабор          = Макет.ПолучитьОбласть(СтрокаТаблицыНабор + "|" + ПостфиксТовар);
			ОбластьТоварСтрокиКомплектующие  = Макет.ПолучитьОбласть(СтрокаТаблицыКомплектующие + "|" + ПостфиксТовар);
			ОбластьДанныхСтрокиНабор         = Макет.ПолучитьОбласть(СтрокаТаблицыНабор + "|" + Данные);
			ОбластьДанныхСтрокиКомплектующие = Макет.ПолучитьОбласть(СтрокаТаблицыКомплектующие + "|" + Данные);
		КонецЕсли;
		
		Сумма		= 0;
		НомерСтроки	= 0;
		
		ПустыеДанные = НаборыСервер.ПустыеДанные();
		
		// Выводим строки таблицы Товары
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			Если НаборыСервер.ИспользоватьОбластьНабор(ВыборкаПоТоварам, ИспользоватьНаборы) Тогда
				ОбластьКодовСтроки  = ОбластьКодовСтрокиНабор;
				ОбластьНомераСтроки = ОбластьНомераСтрокиНабор;
				ОбластьДанныхСтроки = ОбластьДанныхСтрокиНабор;
				ОбластьТоварСтроки  = ОбластьТоварСтрокиНабор;
			ИначеЕсли НаборыСервер.ИспользоватьОбластьКомплектующие(ВыборкаПоТоварам, ИспользоватьНаборы) Тогда
				ОбластьКодовСтроки  = ОбластьКодовСтрокиКомплектующие;
				ОбластьНомераСтроки = ОбластьНомераСтрокиКомплектующие;
				ОбластьДанныхСтроки = ОбластьДанныхСтрокиКомплектующие;
				ОбластьТоварСтроки  = ОбластьТоварСтрокиКомплектующие;
			Иначе
				ОбластьКодовСтроки  = ОбластьКодовСтрокиСтандарт;
				ОбластьНомераСтроки = ОбластьНомераСтрокиСтандарт;
				ОбластьДанныхСтроки = ОбластьДанныхСтрокиСтандарт;
				ОбластьТоварСтроки  = ОбластьТоварСтрокиСтандарт;
			КонецЕсли;
			
			Если НаборыСервер.ВыводитьТолькоЗаголовок(ВыборкаПоТоварам, ИспользоватьНаборы) Тогда
				УстановитьПараметр(ОбластьНомераСтроки, "НомерСтроки", Неопределено);
			Иначе
				НомерСтроки = НомерСтроки + 1;
				
				УстановитьПараметр(ОбластьНомераСтроки, "НомерСтроки", НомерСтроки);
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьНомераСтроки);
			
			Если ВыводитьКоды Тогда
				СтруктураДанныхКоды = Новый Структура("Артикул", ВыборкаПоТоварам[ИмяКолонкиКодов]);
				
				ОбластьКодовСтроки.Параметры.Заполнить(СтруктураДанныхКоды);
				ТабличныйДокумент.Присоединить(ОбластьКодовСтроки);
			КонецЕсли;
			
			ПрефиксИПостфикс = НаборыСервер.ПолучитьПрефиксИПостфикс(ВыборкаПоТоварам, ИспользоватьНаборы);
			
			ОбластьТоварСтроки.Параметры.Заполнить(ВыборкаПоТоварам);
			
			ДополнительныеПараметрыПолученияНаименованияДляПечати = НоменклатураКлиентСервер.ДополнительныеПараметрыПредставлениеНоменклатурыДляПечати();
			ДополнительныеПараметрыПолученияНаименованияДляПечати.ВозвратнаяТара = ВыборкаПоТоварам.ЭтоВозвратнаяТара;
			ДополнительныеПараметрыПолученияНаименованияДляПечати.КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
			
			Товар = ПрефиксИПостфикс.Префикс + НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
				ВыборкаПоТоварам.ТоварНаименованиеПолное,
				ВыборкаПоТоварам.Характеристика,
				,
				,
				ДополнительныеПараметрыПолученияНаименованияДляПечати) + ПрефиксИПостфикс.Постфикс;
			
			СтруктураДанныхТовар = Новый Структура("Товар", Товар);
			
			ОбластьТоварСтроки.Параметры.Заполнить(СтруктураДанныхТовар);
			ТабличныйДокумент.Присоединить(ОбластьТоварСтроки);
			
			Если НаборыСервер.ВыводитьТолькоЗаголовок(ВыборкаПоТоварам, ИспользоватьНаборы) Тогда
				ОбластьДанныхСтроки.Параметры.Заполнить(ПустыеДанные);
			Иначе
				ОбластьДанныхСтроки.Параметры.Заполнить(ВыборкаПоТоварам);
			КонецЕсли;
			
			ТабличныйДокумент.Присоединить(ОбластьДанныхСтроки);
			
			Если Не НаборыСервер.ИспользоватьОбластьКомплектующие(ВыборкаПоТоварам, ИспользоватьНаборы) Тогда
				Сумма = Сумма + ВыборкаПоТоварам.Сумма;
			КонецЕсли;
			
		КонецЦикла;
		
		// Выводим подвал
		ПодвалТаблицы = "ПодвалТаблицы";
		ОбластьНомера = Макет.ПолучитьОбласть(ПодвалТаблицы + "|НомерСтроки");
		ОбластьКодов  = Макет.ПолучитьОбласть(ПодвалТаблицы + "|КолонкаКодов");
		ОбластьТовар  = Макет.ПолучитьОбласть(ПодвалТаблицы + "|" + ПостфиксТовар);
		ОбластьДанных = Макет.ПолучитьОбласть(ПодвалТаблицы + "|" + Данные);
		
		ТабличныйДокумент.Вывести(ОбластьНомера);
		
		Если ВыводитьКоды Тогда
			ТабличныйДокумент.Присоединить(ОбластьКодов);
		КонецЕсли;
		
		ТабличныйДокумент.Присоединить(ОбластьТовар);
		
		СтруктураДанныхВсего = Новый Структура;
		СтруктураДанныхВсего.Вставить("Всего", ФормированиеПечатныхФорм.ФорматСумм(Сумма));
		
		ОбластьДанных.Параметры.Заполнить(СтруктураДанныхВсего);
		ТабличныйДокумент.Присоединить(ОбластьДанных);
		
		// Выводим Сумму прописью
		ОбластьМакета = Макет.ПолучитьОбласть("СуммаПрописью");
		
		СуммаКПрописи = Сумма;
		СуммаПрописью = РаботаСКурсамиВалютУТ.СформироватьСуммуПрописью(СуммаКПрописи, ДанныеПечати.Валюта);
		
		ИтоговаяСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Всего наименований %1, на сумму %2';
				|en = 'Total items %1 in the amount of %2'", ОбщегоНазначения.КодОсновногоЯзыка()),
			НомерСтроки,
			ФормированиеПечатныхФорм.ФорматСумм(СуммаКПрописи, ДанныеПечати.Валюта));
		
		СтруктураДанныхСуммаПрописью = Новый Структура;
		СтруктураДанныхСуммаПрописью.Вставить("СуммаПрописью",  СуммаПрописью);
		СтруктураДанныхСуммаПрописью.Вставить("ИтоговаяСтрока", ИтоговаяСтрока);
		
		ОбластьМакета.Параметры.Заполнить(СтруктураДанныхСуммаПрописью);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Выводим подписи
		ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
		
		Если ЗначениеЗаполнено(ДанныеПечати.ОтпускПроизвел) Тогда
			ОтпускПроизвел = ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(ДанныеПечати.ОтпускПроизвел, ДатаПолученияСведений);
			
			СтруктураДанныхОтпускПроизвел = Новый Структура;
			СтруктураДанныхОтпускПроизвел.Вставить("ОтпускПроизвел", ОтпускПроизвел);
			
			ОбластьМакета.Параметры.Заполнить(СтруктураДанныхОтпускПроизвел);
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати,
			ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Заполняет структуру данными о получателях печатных форм.
// Параметры:
// 	СтруктураДанныхОбъектаПечати - см. ФормированиеПечатныхФорм.ЗаполнитьДанныеСтруктурыПолучателяПоТипуОбъекта.СтруктураДанныхОбъектаПечати
// 
Процедура ЗаполнитьСтруктуруПолучателейПечатныхФорм(СтруктураДанныхОбъектаПечати) Экспорт
	
	СтруктураДанныхОбъектаПечати.ОсновнойПолучатель = "Партнер";
	
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Партнер");
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов") Тогда
		СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Контрагент");
	КонецЕсли;
	
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Грузоотправитель");
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Грузополучатель");
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеДоставкой") Тогда
		СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("ПеревозчикПартнер");
	КонецЕсли;
	
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("КонтактноеЛицо");
	
КонецПроцедуры

Функция ДобавлятьКомандыПечатиКомплектаДокументов() Экспорт
	Возврат Не ПраваПользователяПовтИсп.ЭтоПартнер();
КонецФункции

#КонецОбласти

#Область Прочее

Функция ТекстЗапросаВТТоварыРаспоряжениеНакладнаяОтгружено()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВтДокументы.ДокументСсылка       КАК Ссылка,
	|	ДокументТовары.Склад             КАК Склад,
	|	ДокументТовары.Номенклатура      КАК Номенклатура,
	|	ДокументТовары.Характеристика    КАК Характеристика,
	|	ДокументТовары.Серия             КАК Серия,
	|	СУММА(ДокументТовары.Количество) КАК Количество,
	|	СУММА(ДокументТовары.Сумма)      КАК СуммаСНДС
	|ПОМЕСТИТЬ ВТТоварыРаспоряженияОтгружено
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ДокументТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
	|		ПО (ВтДокументы.ДокументСсылка = ДокументТовары.Ссылка)
	|ГДЕ
	|	ВтДокументы.Проведен
	|	И (НЕ ДокументТовары.Ссылка.ОтгрузкаПоЗаказам
	|			ИЛИ НЕ &ИспользоватьРасширенныеВозможностиЗаказаКлиента)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтДокументы.ДокументСсылка,
	|	ДокументТовары.Склад,
	|	ДокументТовары.Номенклатура,
	|	ДокументТовары.Характеристика,
	|	ДокументТовары.Серия
	|;
	|";
	
	ПодставитьТекстИспользоватьРасширенныеВозможностиЗаказаКлиента(ТекстЗапроса, "ДокументТовары.Ссылка", , "ДокументТовары");
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтДокументы()
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаТовары.ЗаказКлиента КАК ДокументСсылка,
	|	ТИПЗНАЧЕНИЯ(ТаблицаТовары.ЗаказКлиента) КАК ТипДокумента,
	|	ТаблицаТовары.ЗаказКлиента.Дата КАК ДатаДокумента,
	|	ТаблицаТовары.ЗаказКлиента.Проведен КАК Проведен,
	|	ТаблицаТовары.ЗаказКлиента.Номер КАК НомерДокумента
	|ПОМЕСТИТЬ ВтДокументы
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка В (&МассивДокументов)
	|	И ТаблицаТовары.Ссылка.ОтгрузкаПоЗаказам
	|	И ТаблицаТовары.ЗаказКлиента <> НЕОПРЕДЕЛЕНО
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Документ.Ссылка,
	|	ТИПЗНАЧЕНИЯ(Документ.Ссылка),
	|	Документ.Дата,
	|	Документ.Проведен,
	|	Документ.Номер
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК Документ
	|ГДЕ
	|	Документ.Ссылка В (&МассивДокументов)
	|	И НЕ Документ.ОтгрузкаПоЗаказам";
	
	Возврат ТекстЗапроса
		+ ОбщегоНазначения.РазделительПакетаЗапросов();
	
КонецФункции

// Возвращает текст основания по данным документа и указанному порядку расчетов.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура, ДокументОбъект.ОтгрузкаТоваровКлиенту - Объект документа, по которому необходимо
// получить текст основания.
//	ПорядокРасчетов - ПеречислениеСсылка.ПорядокРасчетов - Порядок расчетов.
//
// Возвращаемое значение:
//	Структура - Структура с наименованием, датой и номером основания.
//
Функция СтруктураОснования(Объект, ПорядокРасчетов)
	
	СтруктураОснование = Новый Структура;
	СтруктураОснование.Вставить("Основание");
	СтруктураОснование.Вставить("ОснованиеНомер");
	СтруктураОснование.Вставить("ОснованиеДата");
	
	Если (ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов
		Или ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамНакладным)
		И ЗначениеЗаполнено(Объект.Договор) Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ДоговорыКонтрагентов.НаименованиеДляПечати КАК Основание,
			|	ДоговорыКонтрагентов.Дата                  КАК ОснованиеДата,
			|	ДоговорыКонтрагентов.Номер                 КАК ОснованиеНомер
			|ИЗ
			|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
			|ГДЕ
			|	ДоговорыКонтрагентов.Ссылка = &Ссылка");
		Запрос.УстановитьПараметр("Ссылка", Объект.Договор);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СтруктураОснование.Основание = СокрЛП(Выборка.Основание);
			СтруктураОснование.ОснованиеДата = Выборка.ОснованиеДата;
			СтруктураОснование.ОснованиеНомер = СокрЛП(Выборка.ОснованиеНомер);
		КонецЕсли;
		
	ИначеЕсли (ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказам
		Или ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказамНакладным)
		И Объект.ОтгрузкаПоЗаказам Тогда
		
		МассивЗаказов = Неопределено;
		Если ЗначениеЗаполнено(Объект.ЗаказКлиента) Тогда
			МассивЗаказов = Новый Массив;
			МассивЗаказов.Добавить(Объект.ЗаказКлиента);
		ИначеЕсли Объект.Товары.Количество() <> 0 Тогда 
			Если ТипЗнч(Объект) = Тип("Структура") Тогда
				МассивЗаказов = Объект.Товары.ВыгрузитьКолонку("ЗаказКлиента");
			Иначе
				МассивЗаказов = Объект.Товары.Выгрузить(,"ЗаказКлиента").ВыгрузитьКолонку("ЗаказКлиента");
			КонецЕсли;
		КонецЕсли;
		
		Если МассивЗаказов <> Неопределено Тогда
		
			Запрос = Новый Запрос(
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	ЗаказыКлиентов.НомерПоДаннымКлиента КАК НомерПоДаннымКлиента,
				|	ЗаказыКлиентов.ДатаПоДаннымКлиента  КАК ДатаПоДаннымКлиента,
				|	ЗаказыКлиентов.Номер                КАК Номер,
				|	ЗаказыКлиентов.Дата                 КАК Дата,
				|	&СинонимЗаказа                      КАК Синоним
				|ИЗ
				|	Документ.ЗаказКлиента КАК ЗаказыКлиентов
				|ГДЕ
				|	ЗаказыКлиентов.Ссылка В(&МассивЗаказов)
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	НЕОПРЕДЕЛЕНО          КАК НомерПоДаннымКлиента,
				|	НЕОПРЕДЕЛЕНО          КАК ДатаПоДаннымКлиента,
				|	ЗаявкиНаВозврат.Номер КАК Номер,
				|	ЗаявкиНаВозврат.Дата  КАК Дата,
				|	&СинонимЗаявки        КАК Синоним
				|ИЗ
				|	Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК ЗаявкиНаВозврат
				|ГДЕ
				|	ЗаявкиНаВозврат.Ссылка В(&МассивЗаказов)
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	НЕОПРЕДЕЛЕНО            КАК НомерПоДаннымКлиента,
				|	НЕОПРЕДЕЛЕНО            КАК ДатаПоДаннымКлиента,
				|	ОтгрузкаТоваров.Номер   КАК Номер,
				|	ОтгрузкаТоваров.Дата    КАК Дата,
				|	&СинонимОтгрузки        КАК Синоним
				|ИЗ
				|	Документ.ОтгрузкаТоваровКлиенту КАК ОтгрузкаТоваров
				|ГДЕ
				|	ОтгрузкаТоваров.Ссылка В(&МассивЗаказов)

				|");
				
			Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
			Запрос.УстановитьПараметр("СинонимЗаказа",
				НСтр("ru = 'Заказ клиента';
					|en = 'Sales order'", ОбщегоНазначения.КодОсновногоЯзыка()));
			Запрос.УстановитьПараметр("СинонимЗаявки",
				НСтр("ru = 'Заявка на замену';
					|en = 'Request for replacement'", ОбщегоНазначения.КодОсновногоЯзыка()));
			Запрос.УстановитьПараметр("СинонимОтгрузки",
				НСтр("ru = 'Отгрузка товаров клиенту';
					|en = 'Goods shipment to customer'", ОбщегоНазначения.КодОсновногоЯзыка()));
				
			Выборка = Запрос.Выполнить().Выбрать();
			
			ТекстПоЗаказам = "";
			ОдноОснование = Выборка.Количество() = 1;
			Пока Выборка.Следующий() Цикл
				
				Если ЗначениеЗаполнено(Выборка.НомерПоДаннымКлиента) И ЗначениеЗаполнено(Выборка.ДатаПоДаннымКлиента) Тогда
					
					ТекстПоЗаказам = ТекстПоЗаказам
						+ ", "
						+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = '%1 № %2 от %3';
								|en = '%1 No. %2 from %3'", ОбщегоНазначения.КодОсновногоЯзыка()),
							Выборка.Синоним,
							Выборка.НомерПоДаннымКлиента,
							Формат(Выборка.ДатаПоДаннымКлиента, "ДЛФ=DD"));
					
					ДатаПоЗаказам  = Выборка.ДатаПоДаннымКлиента;
					НомерПоЗаказам = Выборка.НомерПоДаннымКлиента;
					
				Иначе
					
					НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.Номер);
					ЗаголовокДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = '%1 № %2 от %3';
							|en = '%1 No. %2 from %3'", ОбщегоНазначения.КодОсновногоЯзыка()),
						Выборка.Синоним,
						НомерНаПечать,
						Формат(Выборка.Дата, "ДЛФ=DD"));
					
					ТекстПоЗаказам = ТекстПоЗаказам + ", " + ЗаголовокДокумента;
					ДатаПоЗаказам  = Выборка.Дата;
					НомерПоЗаказам = ?(ОдноОснование,ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.Номер),"");
					
				КонецЕсли;
			КонецЦикла;
			СтруктураОснование.Основание =  СокрЛП(Сред(ТекстПоЗаказам, 3));
			СтруктураОснование.ОснованиеДата = ?(ОдноОснование, ДатаПоЗаказам, "");
			СтруктураОснование.ОснованиеНомер = НомерПоЗаказам;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтруктураОснование; // Возврат значения по умолчанию
	
КонецФункции

Процедура УстановитьПараметр(ОбластьМакета, ИмяПараметра, ЗначениеПараметра)
	
	ОбластьМакета.Параметры.Заполнить(Новый Структура(ИмяПараметра, ЗначениеПараметра));
	
КонецПроцедуры

Функция ТекстЗапросаОстаткиТоваровКОформлению(ИмяВременнойТаблицы = "ВтДанныеУчета")

	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Набор.Распоряжение                 КАК Распоряжение,
	|	Набор.Номенклатура                 КАК Номенклатура,
	|	Набор.Характеристика               КАК Характеристика,
	|	Набор.КодСтроки                    КАК КодСтроки,
	|	Набор.Серия                        КАК Серия,
	|	Набор.Склад                        КАК Склад,
	|	СУММА(Набор.КОформлениюКоличество) КАК КОформлениюКоличество,
	|	СУММА(Набор.КОформлениюСумма)      КАК КОформлениюСумма
	|ПОМЕСТИТЬ ИмяВременнойТаблицы
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Распоряжение                   КАК Распоряжение,
	|		Таблица.Номенклатура                   КАК Номенклатура,
	|		Таблица.Характеристика                 КАК Характеристика,
	|		Таблица.КодСтроки                      КАК КодСтроки,
	|		Таблица.Серия                          КАК Серия,
	|		Таблица.Склад                          КАК Склад,
	|		Таблица.КОтгрузкеКоличествоОборот
	|			- Таблица.ОтгруженоКоличествоОборот КАК КОформлениюКоличество,
	|		Таблица.КОтгрузкеСуммаОборот
	|			- Таблица.ОтгруженоСуммаОборот      КАК КОформлениюСумма
	|	ИЗ
	|		#РаспоряженияНаОтгрузкуОборотыРазвернутая КАК Таблица
	|	ГДЕ
	|		Таблица.КОтгрузкеКоличествоОборот
	|			- Таблица.ОтгруженоКоличествоОборот <> 0
	|		ИЛИ Таблица.КОтгрузкеСуммаОборот
	|			- Таблица.ОтгруженоСуммаОборот <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Таблица.Распоряжение        КАК Распоряжение,
	|		Таблица.Номенклатура        КАК Номенклатура,
	|		Таблица.Характеристика      КАК Характеристика,
	|		Таблица.КодСтроки           КАК КодСтроки,
	|		Таблица.Серия               КАК Серия,
	|		Таблица.Склад               КАК Склад,
	|		Таблица.ОтгруженоКоличество КАК КОформлениюКоличество,
	|		Таблица.ОтгруженоСумма      КАК КОформлениюСумма
	|	ИЗ
	|		#РаспоряженияНаОтгрузкуДвиженияРазвернутая КАК Таблица
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Ссылка         КАК Распоряжение,
	|		ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.КодСтроки      КАК КодСтроки,
	|		ТаблицаТовары.Серия          КАК Серия,
	|		ТаблицаТовары.Склад          КАК Склад,
	|		ТаблицаТовары.Количество     КАК КОформлениюКоличество,
	|		ТаблицаТовары.СуммаСНДС      КАК КОформлениюСумма
	|	ИЗ
	|		Документ.ЗаказКлиента.Товары КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка В (&Распоряжения)
	|		И ТаблицаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|		И ТаблицаТовары.Ссылка.ЭтоЗаказКакСчет
	|		И НЕ ТаблицаТовары.Отменено
	|		И ВЫБОР
	|			КОГДА &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			КОГДА ВЫРАЗИТЬ(&Склад КАК Справочник.Склады).ЭтоГруппа
	|				ТОГДА ТаблицаТовары.Склад В ИЕРАРХИИ (&Склад)
	|			ИНАЧЕ
	|				ТаблицаТовары.Склад = &Склад
	|		КОНЕЦ
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Ссылка         КАК Распоряжение,
	|		ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.КодСтроки      КАК КодСтроки,
	|		ТаблицаТовары.Серия          КАК Серия,
	|		ТаблицаТовары.Склад          КАК Склад,
	|		ТаблицаТовары.Количество     КАК КОформлениюКоличество,
	|		ТаблицаТовары.СуммаСНДС      КАК КОформлениюСумма
	|	ИЗ
	|		Документ.ЗаявкаНаВозвратТоваровОтКлиента.ЗаменяющиеТовары КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка В (&Распоряжения)
	|		И ТаблицаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|		И ТаблицаТовары.Ссылка.ЭтоЗаказКакСчет
	|		И НЕ ТаблицаТовары.Отменено
	|		И ВЫБОР
	|			КОГДА &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			КОГДА ВЫРАЗИТЬ(&Склад КАК Справочник.Склады).ЭтоГруппа
	|				ТОГДА ТаблицаТовары.Склад В ИЕРАРХИИ (&Склад)
	|			ИНАЧЕ
	|				ТаблицаТовары.Склад = &Склад
	|		КОНЕЦ
	|	) КАК Набор
	|
	|СГРУППИРОВАТЬ ПО
	|	Набор.Распоряжение,
	|	Набор.Номенклатура,
	|	Набор.Характеристика,
	|	Набор.КодСтроки,
	|	Набор.Серия,
	|	Набор.Склад
	|
	|ИМЕЮЩИЕ
	|	СУММА(Набор.КОформлениюКоличество) > 0
	|";
	
	// Обороты регистра РаспоряженияНаОтгрузкуИВозврат
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение, Номенклатура, Характеристика, КодСтроки, Серия, Склад";
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"	Распоряжение В (&Распоряжения)
		|	И Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
		|	И ВЫБОР
		|		КОГДА &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		КОГДА ВЫРАЗИТЬ(&Склад КАК Справочник.Склады).ЭтоГруппа
		|			ТОГДА Склад В ИЕРАРХИИ (&Склад)
		|		ИНАЧЕ
		|			Склад = &Склад
		|	КОНЕЦ
		|";
	ПараметрыФормированияТаблицы.Показатели = "КОтгрузке, Отгружено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество, Сумма";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая",
		РаспоряженияНаОтгрузкуОборотыРазвернутая);
	
	// Физическая таблица регистра РаспоряженияНаОтгрузкуИВозврат
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"	Активность
		|	И Регистратор = &Регистратор
		|	И Распоряжение В (&Распоряжения)
		|	И ВЫБОР
		|		КОГДА &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		КОГДА ВЫРАЗИТЬ(&Склад КАК Справочник.Склады).ЭтоГруппа
		|			ТОГДА Склад В ИЕРАРХИИ (&Склад)
		|		ИНАЧЕ
		|			Склад = &Склад
		|	КОНЕЦ
		|";
	ПараметрыФормированияТаблицы.Показатели = "Отгружено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество, Сумма";
	ПараметрыФормированияТаблицы.ФизическаяТаблица = Истина;
	
	РаспоряженияНаОтгрузкуДвиженияРазвернутая =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуДвиженияРазвернутая",
		РаспоряженияНаОтгрузкуДвиженияРазвернутая);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяВременнойТаблицы", ИмяВременнойТаблицы);
	
	Возврат ТекстЗапроса;

КонецФункции

// Возвращает хозяйственные операции договора.
//
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ХозяйственныеОперации
// 
Функция ХозяйственнаяОперацияДоговора() Экспорт
	
	ХозяйственныеОперации = Новый Массив;
	ХозяйственныеОперации.Добавить(Перечисления.ХозяйственныеОперации.РеализацияКлиенту);
	ХозяйственныеОперации.Добавить(Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности);
	
	Возврат ХозяйственныеОперации;
	
КонецФункции

#КонецОбласти

#Область ШаблоныСообщений

// Вызывается при подготовке шаблонов сообщений и позволяет переопределить список реквизитов и вложений.
//
// Параметры:
//  Реквизиты               - ДеревоЗначений - список реквизитов шаблона:
//         * Имя            - Строка - Уникальное имя общего реквизита.
//         * Представление  - Строка - Представление общего реквизита.
//         * Тип            - Тип    - Тип реквизита. По умолчанию строка.
//         * Формат         - Строка - Формат вывода значения для чисел, дат, строк и булевых значений.
//  Вложения                - ТаблицаЗначений - печатные формы и вложения:
//         * Имя            - Строка - Уникальное имя вложения.
//         * Представление  - Строка - Представление варианта.
//         * ТипФайла       - Строка - Тип вложения, который соответствует расширению файла: "pdf", "png", "jpg", mxl"
//                                      и др.
//  ДополнительныеПараметры - Структура - дополнительные сведения о шаблоне сообщений.
//
Процедура ПриПодготовкеШаблонаСообщения(Реквизиты, Вложения, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Вызывается в момент создания сообщений по шаблону для заполнения значений реквизитов и вложений.
//
// Параметры:
//  Сообщение - Структура - структура с ключами:
//    * ЗначенияРеквизитов - Соответствие из КлючИЗначение- список используемых в шаблоне реквизитов:
//      * Ключ     - Строка - имя реквизита в шаблоне;
//      * Значение - Строка - значение заполнения в шаблоне.
//    * ЗначенияОбщихРеквизитов - Соответствие из КлючИЗначение - список используемых в шаблоне общих реквизитов:
//      * Ключ     - Строка - имя реквизита в шаблоне;
//      * Значение - Строка - значение заполнения в шаблоне.
//    * Вложения - Соответствие из КлючИЗначение - значения реквизитов
//      * Ключ     - Строка - имя вложения в шаблоне;
//      * Значение - ДвоичныеДанные, Строка - двоичные данные или адрес во временном хранилище вложения.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//  ДополнительныеПараметры - Структура -  Дополнительная информация о шаблоне сообщения.
//
Процедура ПриФормированииСообщения(Сообщение, ПредметСообщения, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Заполняет список получателей SMS при отправке сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиSMS - ТаблицаЗначений - список получается SMS:
//     * НомерТелефона - Строка - номер телефона, куда будет отправлено сообщение SMS.
//     * Представление - Строка - представление получателя сообщения SMS.
//     * Контакт       - СправочникСсылка - контакт, которому принадлежит номер телефона.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//
Процедура ПриЗаполненииТелефоновПолучателейВСообщении(ПолучателиSMS, ПредметСообщения) Экспорт
	
КонецПроцедуры

// Заполняет список получателей письма при отправки сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиПисьма - ТаблицаЗначений - список получается письма:
//     * Адрес           - Строка - адрес электронной почты получателя.
//     * Представление   - Строка - представление получается письма.
//     * ВариантОтправки - Строка - Варианты отправки письма: "Кому", "Копия", "СкрытаяКопия", "ОбратныйАдреса";
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//
Процедура ПриЗаполненииПочтыПолучателейВСообщении(ПолучателиПисьма, ПредметСообщения) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область УнифицированныеМетодыПоХозяйственнымОперациям

#Область ФункциональныеОпцииВТекстахЗапросов

// Изменяет в тексте запроса текст параметра использование расширенных возможностей заказа клиента.
// Должен отработать синхронно унифицированному методу ИспользоватьРасширенныеВозможностиЗаказаКлиента.
//
// Параметры:
//	ТекстЗапроса				- Строка - текст запроса.
//	ИмяТаблицы					- Строка - имя таблицы в запросе.
//	Параметр					- Строка - параметр в запросе.
//	ИмяТаблицыТовары			- Строка - имя таблицы товаров в запросе.
//	ИмяРеквизитаЗаказКлиента	- Строка - имя реквизита заказ клиента.
//
Процедура ПодставитьТекстИспользоватьРасширенныеВозможностиЗаказаКлиента(
		ТекстЗапроса,
		ИмяТаблицы,
		Параметр = "&ИспользоватьРасширенныеВозможностиЗаказаКлиента",
		ИмяТаблицыТовары = "",
		ИмяРеквизитаЗаказКлиента = "ЗаказКлиента") Экспорт
	
	Если Не ПустаяСтрока(ИмяТаблицыТовары) Тогда
		НовыйТекст = "НЕ ЕСТЬNULL(%1.%2.ЭтоЗаказКакСчет, НЕ %3)"; //@Query-part
		НовыйТекст = СтрШаблон(НовыйТекст, ИмяТаблицыТовары, ИмяРеквизитаЗаказКлиента, Параметр);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, Параметр, НовыйТекст);
	КонецЕсли;
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область Форма

// Получает договор по умолчанию.
//
// Параметры:
//    Объект                         - ДанныеФормыСтруктура, ДокументОбъект.ОтгрузкаТоваровКлиенту - Объект, из которого будут взяты основные параметры для поиска.
//    ОтборПоВалюте                  - Булево - Вести поиск по указанной валюте взаиморасчетов искомого договора (Истина) или
//												без учета валюты взаиморасчетов (Ложь).
//    ОтборПоНаправлениюДеятельности - Булево - Вести поиск по указанному направлению деятельности искомого договора (Истина) или
//												по всем направлениям (Ложь).
//
// Возвращаемое значение:
//    СправочникСсылка.ДоговорыКонтрагентов - договор контрагента по умолчанию.
//
Функция ПолучитьДоговорПоУмолчанию(Объект,
										ОтборПоВалюте = Истина,
										ОтборПоНаправлениюДеятельности = Истина) Экспорт
	
	Валюта = Неопределено;
	Если ОтборПоВалюте Тогда
		Валюта = Объект.Валюта;
	КонецЕсли;
	
	НаправлениеДеятельности = Неопределено;
	Если ОтборПоНаправлениюДеятельности
	   И ПолучитьФункциональнуюОпцию("ИспользоватьУчетДоходовПоНаправлениямДеятельности") Тогда
		НаправлениеДеятельности = Объект.НаправлениеДеятельности;
	КонецЕсли;
	
	ХозяйственныеОперации = ХозяйственнаяОперацияДоговора();
	
	Договор = ПродажиСервер.ПолучитьДоговорПоУмолчанию(Объект,
		ХозяйственныеОперации,
		Валюта,
		НаправлениеДеятельности);
	
	Возврат Договор;
	
КонецФункции

// Настраивает форму.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - содержит:
//           * Элементы - ВсеЭлементыФормы - содержит:
//              ** Партнер - ПолеВвода - содержит:
//                  *** Заголовок - Строка - 
//              ** ТоварыЗаказКлиента - ПолеВвода - содержит:
//                  *** Заголовок - Строка -
//              ** ЗакрытьЗаказ - КнопкаКоманднойПанели - содержит:
//                  *** Доступность - Булево - 
//              ** ЗакрытьЗаказы - КнопкаКоманднойПанели - содержит:
//                  *** Доступность - Булево - 
//  Номер - Число - 
//  Дата  - Дата - 
//
Процедура НастроитьФорму(Форма, Номер, Дата) Экспорт
	
	Элементы  = Форма.Элементы;
	Параметры = Форма.Параметры;
	
	Форма.АвтоЗаголовок = Ложь;
	Форма.Заголовок     = ЗаголовокФормыДокумента(Не ЗначениеЗаполнено(Параметры.Ключ), Номер, Дата);

	ПравоНаИзменениеЗаказов = ПравоДоступа("Изменение", Метаданные.Документы.ЗаказКлиента);
	Элементы.ЗакрытьЗаказ.Доступность  = ПравоНаИзменениеЗаказов;
	Элементы.ЗакрытьЗаказы.Доступность = ПравоНаИзменениеЗаказов;
	
	ИспользоватьПартнеровИКонтрагентов =
		ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов");
	ИспользоватьСоглашенияСКлиентами =
		ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
	
	Элементы.СтраницыПроблемаПроверкаКонтрагентаКонтрагент.Видимость =
		ИспользоватьПартнеровИКонтрагентов;
	
	Элементы.Соглашение.Видимость = ИспользоватьСоглашенияСКлиентами;
	Элементы.ТоварыЗаполнитьЦеныПоСоглашению.Видимость = Элементы.Соглашение.Видимость;
	
КонецПроцедуры

// Настраивает параметры выбора и связи параметров выбора реквизитов формы.
//
// Параметры:
//  Форма  - ФормаКлиентскогоПриложения -
//  Объект - ДанныеФормыСтруктура -
//
Процедура НастроитьПараметрыВыбораЭлементов(Форма, Объект) Экспорт
	
	Элементы = Форма.Элементы;
	
	// Партнер
	МассивПараметровВыбора = Новый Массив;
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Клиент", Истина));
			
	Элементы.Партнер.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
	
	// Контрагент
	МассивПараметровВыбора = Новый Массив;
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("ЗаголовокПоПартнеру", НСтр("ru = 'По клиенту';
																					|en = 'By customer'")));
			
	Элементы.Контрагент.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
	
	//Договор
	МассивПараметровВыбора = Новый Массив;
	
	МассивПараметровВыбора.Добавить(
		Новый ПараметрВыбора("Отбор.Статус", Перечисления.СтатусыДоговоровКонтрагентов.Действует));
	
	МассивПараметровВыбора.Добавить(
		Новый ПараметрВыбора("Отбор.ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.РеализацияКлиенту));
	
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.ПометкаУдаления", Ложь));

	МассивСвязейПараметровВыбора = Новый Массив;
	
	МассивСвязейПараметровВыбора.Добавить(
		Новый СвязьПараметраВыбора("Отбор.Контрагент", "Объект.Контрагент", РежимИзмененияСвязанногоЗначения.Очищать));
	
	МассивСвязейПараметровВыбора.Добавить(
		Новый СвязьПараметраВыбора("Отбор.Организация", "Объект.Организация", РежимИзмененияСвязанногоЗначения.Очищать));
		
	МассивСвязейПараметровВыбора.Добавить(
		Новый СвязьПараметраВыбора("Партнер", "Объект.Партнер", РежимИзмененияСвязанногоЗначения.Очищать));
	
	МассивСвязейПараметровВыбора.Добавить(
		Новый СвязьПараметраВыбора("Соглашение", "Объект.Соглашение", РежимИзмененияСвязанногоЗначения.НеИзменять));
	
	МассивСвязейПараметровВыбора.Добавить(
		Новый СвязьПараметраВыбора(
			"Отбор.ВалютаВзаиморасчетов",
			"Объект.Валюта"));
	
	Элементы.Договор.ПараметрыВыбора       = Новый ФиксированныйМассив(МассивПараметровВыбора);
	Элементы.Договор.СвязиПараметровВыбора = Новый ФиксированныйМассив(МассивСвязейПараметровВыбора);
	
	// Направление деятельности
	Если Не Элементы.Найти("НаправлениеДеятельности") = Неопределено Тогда
		
		МассивПараметровВыбора = Новый Массив;
		МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.УчетДоходов", Истина));
		
		Элементы.НаправлениеДеятельности.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает заголовок формы документа отгрузка товаров клиенту.
//
// Параметры:
//  НовыйДокумент - Булево - 
//  Номер - Число - 
//  Дата - Дата - 
//
// Возвращаемое значение:
//  Строка - 
//
Функция ЗаголовокФормыДокумента(НовыйДокумент, Номер, Дата) Экспорт
	
	Если НовыйДокумент Тогда
		Заголовок = СтрШаблон(НСтр("ru = '%1 (создание)';
									|en = '%1 (Create)'"), ПредставлениеОбъекта());
	Иначе
		Заголовок = СтрШаблон(НСтр("ru = '%1 %2 от %3';
									|en = '%1 %2 dated %3'"), ПредставлениеОбъекта(), Номер, Дата);
	КонецЕсли;
	
	Возврат Заголовок;
	
КонецФункции

// Возвращает представление документа отгрузка товаров клиенту.
//
// Возвращаемое значение:
//  Строка - 
//
Функция ПредставлениеОбъекта() Экспорт
	
	Возврат НСтр("ru = 'Отгрузка товаров клиенту';
				|en = 'Goods shipment to customer'");
	
КонецФункции

// Привязывает строки таблицы Товары к распоряжениям.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - 
//  Объект - ДанныеФормыСтруктура - Содержит:
//  		  * Ссылка - ДокументСсылка.ОтгрузкаТоваровКлиенту - 
//  ПараметрыУказанияСерий - см. НоменклатураКлиентСервер.ПараметрыУказанияСерий
//  ОтобратьПоЗаказу - Булево - 
//  ЦенаВключаетНДС - Булево -
//
Процедура ПривязатьСтрокиКЗаказам(Форма, Объект, ПараметрыУказанияСерий, ОтобратьПоЗаказу, ЦенаВключаетНДС) Экспорт
	
	Если Не Форма.ИспользоватьЗаказыКлиентов
		Или (Не Форма.ИспользоватьОтгрузкуПоНесколькимЗаказам
			И Не ЗначениеЗаполнено(Объект.ЗаказКлиента)) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапроса = Новый Массив;
	
	МенеджерНакладной = Документы.ОтгрузкаТоваровКлиенту;
	
	// 1. Получаем текст запроса по остаткам к отгрузке: [к отгрузке] - [отгружено]
	РеквизитыШапки = МенеджерНакладной.РеквизитыШапки();
	ЗаполнитьЗначенияСвойств(РеквизитыШапки, Объект);
	РеквизитыШапки.Вставить("ЦенаВключаетНДС", ЦенаВключаетНДС);
	
	Распоряжения = МенеджерНакладной.РаспоряженияНакладной(Объект.Ссылка, РеквизитыШапки);
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(Распоряжения, Объект.Ссылка);
	
	ТекстыЗапроса.Добавить(ТекстЗапросаОстаткиТоваровКОформлению("ТоварыКОформлению"));
	
	// 2. Получаем дубли строк во временной таблице, полученной в п.1.
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*)                    КАК КоличествоДублей,
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	ТоварыКОформлению.Склад          КАК Склад
	|ПОМЕСТИТЬ ДублиТоваровКОформлению
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	ТоварыКОформлению.Склад";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// 3. Соединяем полученные в п.1 и п.2 таблицы, а также добавляем данные из таблиц документов
	// распоряжений.
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТоварыКОформлению.Распоряжение          КАК ЗаказКлиента,
	|	ТоварыКОформлению.КодСтроки             КАК КодСтроки,
	|	ТоварыКОформлению.Номенклатура          КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика        КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЗаказКлиентаТовары.Ссылка ЕСТЬ НЕ NULL
	|			И ЗаказКлиентаТовары.Обособленно
	|			ТОГДА ЗаказКлиентаТовары.Ссылка.Назначение
	|		КОГДА ЗаявкаНаВозвратТовары.Ссылка ЕСТЬ НЕ NULL
	|			И ЗаявкаНаВозвратТовары.Обособленно
	|			ТОГДА ЗаявкаНаВозвратТовары.Ссылка.Назначение
	|		КОГДА ОтгрузкаКлиентуТовары.Ссылка ЕСТЬ НЕ NULL
	|			ТОГДА ОтгрузкаКлиентуТовары.Назначение
	|		ИНАЧЕ Значение(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ                                   КАК Назначение,
	|	ТоварыКОформлению.Серия                 КАК Серия,
	|	ТоварыКОформлению.КОформлениюКоличество КАК КОформлению,
	|	ВЫБОР
	|		КОГДА ЗаказКлиентаТовары.Ссылка ЕСТЬ НЕ NULL
	|				И ЗаказКлиентаТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ТоварыКОформлению.КОформлениюСумма / ТоварыКОформлению.КОформлениюКоличество
	|		КОГДА ЗаявкаНаВозвратТовары.Ссылка ЕСТЬ НЕ NULL
	|				И ЗаявкаНаВозвратТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ТоварыКОформлению.КОформлениюСумма / ТоварыКОформлению.КОформлениюКоличество
	|		КОГДА ОтгрузкаКлиентуТовары.Ссылка ЕСТЬ НЕ NULL
	|				И ОтгрузкаКлиентуТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ТоварыКОформлению.КОформлениюСумма / ТоварыКОформлению.КОформлениюКоличество
	|		КОГДА ЗаказКлиентаТовары.Ссылка ЕСТЬ НЕ NULL
	|			ТОГДА ТоварыКОформлению.КОформлениюСумма * &ТекстЗапросаКоэффициентУпаковкиЗаказы / ТоварыКОформлению.КОформлениюКоличество
	|		КОГДА ЗаявкаНаВозвратТовары.Ссылка ЕСТЬ НЕ NULL
	|			ТОГДА ТоварыКОформлению.КОформлениюСумма * &ТекстЗапросаКоэффициентУпаковкиЗаявкиНаВозврат / ТоварыКОформлению.КОформлениюКоличество
	|		КОГДА ОтгрузкаКлиентуТовары.Ссылка ЕСТЬ НЕ NULL
	|			ТОГДА ТоварыКОформлению.КОформлениюСумма * &ТекстЗапросаКоэффициентУпаковкиОтгрузкиКлиенту / ТоварыКОформлению.КОформлениюКоличество
	|		ИНАЧЕ 0
	|	КОНЕЦ                                   КАК Цена,
	|	ТоварыКОформлению.Склад                 КАК Склад,
	|	ВЫБОР
	|		КОГДА ЗаказКлиентаТовары.Ссылка ЕСТЬ НЕ NULL
	|			ТОГДА ЗаказКлиентаТовары.СрокПоставки
	|		КОГДА ЗаявкаНаВозвратТовары.Ссылка ЕСТЬ НЕ NULL
	|			ТОГДА ЗаявкаНаВозвратТовары.СрокПоставки
	|		КОГДА ОтгрузкаКлиентуТовары.Ссылка ЕСТЬ НЕ NULL
	|			ТОГДА ОтгрузкаКлиентуТовары.СрокПоставки
	|	КОНЕЦ                                   КАК СрокПоставки,
	|	0                                       КАК Распределено,
	|	ДублиТоваров.КоличествоДублей           КАК КоличествоДублей
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДублиТоваровКОформлению КАК ДублиТоваров
	|		ПО ТоварыКОформлению.Номенклатура = ДублиТоваров.Номенклатура
	|			И ТоварыКОформлению.Характеристика = ДублиТоваров.Характеристика
	|			И ТоварыКОформлению.Склад = ДублиТоваров.Склад
	|			И ТоварыКОформлению.Серия = ДублиТоваров.Серия
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|		ПО ЗаказКлиентаТовары.Ссылка = ТоварыКОформлению.Распоряжение
	|			И ЗаказКлиентаТовары.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И ЗаказКлиентаТовары.Характеристика = ТоварыКОформлению.Характеристика
	|			И ЗаказКлиентаТовары.КодСтроки = ТоварыКОформлению.КодСтроки
	|			И ЗаказКлиентаТовары.Склад = ТоварыКОформлению.Склад
	|			И ЗаказКлиентаТовары.Серия = ТоварыКОформлению.Серия
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровКлиенту.Товары КАК ОтгрузкаКлиентуТовары
	|		ПО ОтгрузкаКлиентуТовары.Ссылка = ТоварыКОформлению.Распоряжение
	|			И ОтгрузкаКлиентуТовары.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И ОтгрузкаКлиентуТовары.Характеристика = ТоварыКОформлению.Характеристика
	|			И ОтгрузкаКлиентуТовары.КодСтроки = ТоварыКОформлению.КодСтроки
	|			И ОтгрузкаКлиентуТовары.Склад = ТоварыКОформлению.Склад
	|			И ОтгрузкаКлиентуТовары.Серия = ТоварыКОформлению.Серия
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаВозвратТоваровОтКлиента.ЗаменяющиеТовары КАК ЗаявкаНаВозвратТовары
	|		ПО ЗаявкаНаВозвратТовары.Ссылка = ТоварыКОформлению.Распоряжение
	|			И ЗаявкаНаВозвратТовары.КодСтроки = ТоварыКОформлению.КодСтроки
	|			И ЗаявкаНаВозвратТовары.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И ЗаявкаНаВозвратТовары.Характеристика = ТоварыКОформлению.Характеристика
	|			И ЗаявкаНаВозвратТовары.Склад = ТоварыКОформлению.Склад
	|			И ЗаявкаНаВозвратТовары.Серия = ТоварыКОформлению.Серия
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Склад,
	|	ТоварыКОформлению.Серия,
	|	ВЫБОР
	|		КОГДА ЗаказКлиентаТовары.Ссылка ЕСТЬ НЕ NULL
	|			И ЗаказКлиентаТовары.Обособленно
	|			ТОГДА ЗаказКлиентаТовары.Ссылка.Назначение
	|		КОГДА ЗаявкаНаВозвратТовары.Ссылка ЕСТЬ НЕ NULL
	|			И ЗаявкаНаВозвратТовары.Обособленно
	|			ТОГДА ЗаявкаНаВозвратТовары.Ссылка.Назначение
	|		КОГДА ОтгрузкаКлиентуТовары.Ссылка ЕСТЬ НЕ NULL
	|			ТОГДА ОтгрузкаКлиентуТовары.Назначение
	|		ИНАЧЕ Значение(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЗаказКлиентаТовары.Ссылка ЕСТЬ НЕ NULL
	|			ТОГДА ЗаказКлиентаТовары.ДатаОтгрузки
	|		КОГДА ЗаявкаНаВозвратТовары.Ссылка ЕСТЬ НЕ NULL
	|			ТОГДА ЗаявкаНаВозвратТовары.ДатаОтгрузки
	|		КОГДА ОтгрузкаКлиентуТовары.Ссылка ЕСТЬ НЕ NULL
	|			ТОГДА ОтгрузкаКлиентуТовары.Ссылка.Дата
	|	КОНЕЦ,
	|	ТоварыКОформлению.Распоряжение,
	|	ТоварыКОформлению.КодСтроки
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковкиЗаказы",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ЗаказКлиентаТовары.Упаковка",
			"ЗаказКлиентаТовары.Номенклатура"));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковкиЗаявкиНаВозврат",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ЗаявкаНаВозвратТовары.Упаковка",
			"ЗаявкаНаВозвратТовары.Номенклатура"));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковкиОтгрузкиКлиенту",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ОтгрузкаКлиентуТовары.Упаковка",
			"ОтгрузкаКлиентуТовары.Номенклатура"));
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос.УстановитьПараметр("Регистратор", Объект.Ссылка);
	Запрос.УстановитьПараметр("Распоряжения", Распоряжения);
	Запрос.УстановитьПараметр("ТипНоменклатурыТовар", Перечисления.ТипыНоменклатуры.Товар);
	Запрос.УстановитьПараметр("Склад", Объект.Склад);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	ТаблицаНераспределенныхТоваров = Новый ТаблицаЗначений();
	ТаблицаНераспределенныхТоваров.Колонки.Добавить("ЗаказКлиента");
	ТаблицаНераспределенныхТоваров.Колонки.Добавить("КодСтроки");
	ТаблицаНераспределенныхТоваров.Колонки.Добавить("Номенклатура");
	ТаблицаНераспределенныхТоваров.Колонки.Добавить("Характеристика");
	ТаблицаНераспределенныхТоваров.Колонки.Добавить("Назначение");
	ТаблицаНераспределенныхТоваров.Колонки.Добавить("Серия");
	ТаблицаНераспределенныхТоваров.Колонки.Добавить("Количество");
	ТаблицаНераспределенныхТоваров.Колонки.Добавить("Цена");
	ТаблицаНераспределенныхТоваров.Колонки.Добавить("Склад");
	ТаблицаНераспределенныхТоваров.Колонки.Добавить("СрокПоставки");
	
	ИменаЗаполняемыхСвойств = "ЗаказКлиента, КодСтроки, Назначение, СрокПоставки";
	
	Пока Выборка.Следующий() Цикл
		
		СтруктураПоиска = Новый Структура();
		СтруктураПоиска.Вставить("КодСтроки",      0);
		СтруктураПоиска.Вставить("Номенклатура",   Выборка.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика", Выборка.Характеристика);
		СтруктураПоиска.Вставить("Серия",          Выборка.Серия);
		СтруктураПоиска.Вставить("Цена",           Выборка.Цена);
		СтруктураПоиска.Вставить("Склад",          Выборка.Склад);
		
		НайденныеСтроки = Объект.Товары.НайтиСтроки(СтруктураПоиска);
		НераспределенноеКоличество = Выборка.КОформлению;
		
		// Если дублей нет, распределяем строку распоряжения по строкам накладной
		Если Выборка.КоличествоДублей < 2 Тогда
			
			Для Каждого ТекСтрока Из НайденныеСтроки Цикл
				
				ЗаполнитьЗначенияСвойств(ТекСтрока, Выборка, ИменаЗаполняемыхСвойств);
				
				НераспределенноеКоличество = НераспределенноеКоличество - ТекСтрока.Количество;
				
				Если НераспределенноеКоличество <= 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		// Если дубли есть, распределяем строки распоряжений по строкам накладной с учетом количества
		Иначе
			
			// На первом проходе заполним только если количество в строке распоряжения и накладной совпадают
			НайденаПодходящаяСтрока = Ложь;
			
			Для Каждого ТекСтрока Из НайденныеСтроки Цикл
				
				Если ТекСтрока.Количество = Выборка.КОформлению Тогда
					
					НайденаПодходящаяСтрока = Истина;
					
					ЗаполнитьЗначенияСвойств(ТекСтрока, Выборка, ИменаЗаполняемыхСвойств);
					
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если Не НайденаПодходящаяСтрока Тогда
				НоваяСтрока = ТаблицаНераспределенныхТоваров.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				
				НоваяСтрока.Количество = НераспределенноеКоличество;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Распределим дубли товаров, нераспределенные на первом проходе
	Если ТаблицаНераспределенныхТоваров.Количество() > 0 Тогда
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить(
			"ЗаполнитьДубликатыЗависимыхРеквизитов", Новый Структура("БезВозвратнойТары", "Сумма"));
		
		ОбрабатываемыеСтроки = Новый Массив();
		
		Для Каждого НераспределеннаяСтрока Из ТаблицаНераспределенныхТоваров Цикл
			
			СтруктураПоиска = Новый Структура();
			СтруктураПоиска.Вставить("ЗаказКлиента",   Неопределено);
			СтруктураПоиска.Вставить("КодСтроки",      0);
			СтруктураПоиска.Вставить("Номенклатура",   НераспределеннаяСтрока.Номенклатура);
			СтруктураПоиска.Вставить("Характеристика", НераспределеннаяСтрока.Характеристика);
			СтруктураПоиска.Вставить("Серия",          НераспределеннаяСтрока.Серия);
			СтруктураПоиска.Вставить("Цена",           НераспределеннаяСтрока.Цена);
			СтруктураПоиска.Вставить("Склад",          НераспределеннаяСтрока.Склад);
			
			СтрокиТовары = Объект.Товары.НайтиСтроки(СтруктураПоиска);
			
			Для Каждого СтрокаТовары Из СтрокиТовары Цикл
				
				Если СтрокаТовары.Количество = НераспределеннаяСтрока.Количество Тогда
					
					ЗаполнитьЗначенияСвойств(СтрокаТовары, НераспределеннаяСтрока, ИменаЗаполняемыхСвойств);
					
					НераспределеннаяСтрока.Количество = 0;
					
				ИначеЕсли СтрокаТовары.Количество > НераспределеннаяСтрока.Количество Тогда
					
					Разница = СтрокаТовары.Количество - НераспределеннаяСтрока.Количество;
					
					НоваяСтрокаТовары = Объект.Товары.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары, СтрокаТовары);
					
					НоваяСтрокаТовары.Количество = Разница;
					
					СтрокиТовары.Добавить(НоваяСтрокаТовары);
					
					ЗаполнитьЗначенияСвойств(СтрокаТовары, НераспределеннаяСтрока, ИменаЗаполняемыхСвойств + ",Количество");
					
					НераспределеннаяСтрока.Количество = 0;
					
					ОбрабатываемыеСтроки.Добавить(СтрокаТовары);
					ОбрабатываемыеСтроки.Добавить(НоваяСтрокаТовары);
					
				ИначеЕсли СтрокаТовары.Количество < НераспределеннаяСтрока.Количество Тогда
					
					Разница = НераспределеннаяСтрока.Количество - СтрокаТовары.Количество;
					
					ЗаполнитьЗначенияСвойств(СтрокаТовары, НераспределеннаяСтрока, ИменаЗаполняемыхСвойств);
					
					НераспределеннаяСтрока.Количество = Разница;
					
				КонецЕсли;
				
				Если НераспределеннаяСтрока.Количество = 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
		КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
		ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
			ОбрабатываемыеСтроки,
			СтруктураДействий,
			Объект.Товары,
			КэшированныеЗначения);
		
	КонецЕсли;
	
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли
