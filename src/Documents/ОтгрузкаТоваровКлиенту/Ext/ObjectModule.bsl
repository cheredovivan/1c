#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет условия продаж в документе.
//
// Параметры:
//	УсловияПродаж - Структура - Данные для заполнения.
//
Процедура ЗаполнитьУсловияПродаж(Знач УсловияПродаж) Экспорт
	
	Если УсловияПродаж = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УсловияПродаж.Валюта) Тогда
		Валюта = УсловияПродаж.Валюта;
	КонецЕсли;
	
	ВалютаРегл = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(УсловияПродаж.Организация);
	НаправлениеДеятельности = УсловияПродаж.НаправлениеДеятельности;
	
	ИзмененаОрганизация = ЗначениеЗаполнено(УсловияПродаж.Организация)
		И УсловияПродаж.Организация <> Организация;
	
	Если ИзмененаОрганизация Тогда
		
		Организация = УсловияПродаж.Организация;
		
		СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
		СтруктураПараметров.Организация = Организация;
		СтруктураПараметров.НаправлениеДеятельности = НаправлениеДеятельности;
		
		БанковскийСчетОрганизации = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);
		
	КонецЕсли;
	
	Если Не УсловияПродаж.Типовое
		И ЗначениеЗаполнено(УсловияПродаж.Контрагент)
		И УсловияПродаж.Контрагент <> Контрагент Тогда
			
		Контрагент = УсловияПродаж.Контрагент;
		БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(Контрагент);
		
	КонецЕсли;
	
	ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент);
	
	Договор = Документы.ОтгрузкаТоваровКлиенту.ПолучитьДоговорПоУмолчанию(ЭтотОбъект);
	
	РеквизитыДоговора = Новый Структура("Валюта, НаправлениеДеятельности, Подразделение", "ВалютаВзаиморасчетов");
	Справочники.ДоговорыКонтрагентов.ЗаполнитьРеквизитыДокумента(ЭтотОбъект, Договор, РеквизитыДоговора);
	
	ПродажиСервер.ЗаполнитьБанковскиеСчетаПоДоговору(Договор, БанковскийСчетОрганизации, БанковскийСчетКонтрагента);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетДоходовПоНаправлениямДеятельности") Тогда
		НаправленияДеятельностиСервер.ЗаполнитьНаправлениеПоУмолчанию(НаправлениеДеятельности, Соглашение, Договор);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УсловияПродаж.Склад) Тогда
		
		Склад = УсловияПродаж.Склад;
		
		СтруктураОтветственного = ПродажиСервер.ПолучитьОтветственногоПоСкладу(Склад);
		Если СтруктураОтветственного <> Неопределено Тогда
			Отпустил = СтруктураОтветственного.Ответственный;
			ОтпустилДолжность = СтруктураОтветственного.ОтветственныйДолжность;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не УсловияПродаж.Типовое
		И ЗначениеЗаполнено(УсловияПродаж.КонтактноеЛицо)
		И Не ЗначениеЗаполнено(КонтактноеЛицо) Тогда
		
		КонтактноеЛицо = УсловияПродаж.КонтактноеЛицо;
		
	КонецЕсли;
	
	ПартнерыИКонтрагенты.ЗаполнитьКонтактноеЛицоПартнераПоУмолчанию(Партнер, КонтактноеЛицо);
	
	КоэффициентПересчетаРегл = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(
		Валюта,
		ВалютаРегл,
		ТекущаяДатаСеанса());
	
КонецПроцедуры

// Заполняет условия продаж по умолчанию.
//
Процедура ЗаполнитьУсловияПродажПоУмолчанию() Экспорт
	
	ИспользоватьСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
	
	Если ЗначениеЗаполнено(Партнер)
		Или Не ИспользоватьСоглашенияСКлиентами Тогда
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ВыбранноеСоглашение",   Соглашение);
		ПараметрыОтбора.Вставить("ПустаяСсылкаДокумента", Документы.ОтгрузкаТоваровКлиенту.ПустаяСсылка());
		ПараметрыОтбора.Вставить("ИсключитьГруппыСкладовДоступныеВЗаказах", Истина);
		ПараметрыОтбора.Вставить("ОтгрузкаБезПереходаПраваСобственности25", Истина);
		ПараметрыОтбора.Вставить("ХозяйственныеОперации",
			ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияКлиенту"));
		
		УсловияПродажПоУмолчанию =
			ПродажиСервер.ПолучитьУсловияПродажПоУмолчанию(
				Партнер,
				ПараметрыОтбора,
				Истина);
		
		Если УсловияПродажПоУмолчанию <> Неопределено Тогда
			
			Если Не ИспользоватьСоглашенияСКлиентами
				Или (Соглашение <> УсловияПродажПоУмолчанию.Соглашение
					И ЗначениеЗаполнено(УсловияПродажПоУмолчанию.Соглашение)) Тогда
				
				Соглашение = УсловияПродажПоУмолчанию.Соглашение;
				ЗаполнитьУсловияПродаж(УсловияПродажПоУмолчанию);
				
				Если Не Исправление Тогда
					ПараметрыЗаполнения = Документы.ОтгрузкаТоваровКлиенту.ПараметрыЗаполненияНалогообложенияНДСПродажи(ЭтотОбъект);
					УчетНДСУП.ЗаполнитьНалогообложениеНДСПродажи(НалогообложениеНДС, ПараметрыЗаполнения);
				КонецЕсли;
				
				Если ИспользоватьСоглашенияСКлиентами Тогда
					ЗаполнитьЦеныПоУсловиямПродаж();
				КонецЕсли;
				
			Иначе
				Соглашение = УсловияПродажПоУмолчанию.Соглашение;
				
				ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент);
			КонецЕсли;
			
		Иначе
			Соглашение = Неопределено;
			
			ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент);
		КонецЕсли;
		
		БанковскийСчетКонтрагента =
			ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(Контрагент,
																						Неопределено,
																						БанковскийСчетКонтрагента);
		
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеДоставкой") Тогда
		АдресДоставки = ФормированиеПечатныхФорм.ПолучитьАдресИзКонтактнойИнформации(Партнер);
	КонецЕсли;
	
	ПартнерыИКонтрагенты.ЗаполнитьКонтактноеЛицоПартнераПоУмолчанию(Партнер, КонтактноеЛицо);
	
КонецПроцедуры

// Заполняет условия продаж по соглашению в отгрузке товаров клиенту.
//
Процедура ЗаполнитьУсловияПродажПоСоглашению() Экспорт
	
	УсловияПродаж = ПродажиСервер.ПолучитьУсловияПродаж(Соглашение, Истина, Истина);
	ЗаполнитьУсловияПродаж(УсловияПродаж);
	
	Если Не Исправление Тогда
		ПараметрыЗаполнения = Документы.ОтгрузкаТоваровКлиенту.ПараметрыЗаполненияНалогообложенияНДСПродажи(ЭтотОбъект);
		УчетНДСУП.ЗаполнитьНалогообложениеНДСПродажи(НалогообложениеНДС, ПараметрыЗаполнения);
	КонецЕсли;
	
	ЗаполнитьЦеныПоУсловиямПродаж();
	
	СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
	СтруктураПараметров.Организация    = Организация;
	СтруктураПараметров.БанковскийСчет = БанковскийСчетОрганизации;
	
	БанковскийСчетОрганизации = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);
	БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(Контрагент,
																									Неопределено,
																									БанковскийСчетКонтрагента);
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеДоставкой") Тогда
		АдресДоставки = ФормированиеПечатныхФорм.ПолучитьАдресИзКонтактнойИнформации(Партнер);
	КонецЕсли;
	
КонецПроцедуры

// Формирует временные таблицы с данными документа.
//
// Возвращаемое значение:
//	МенеджерВременныхТаблиц - менеджер временных таблиц.
//
Функция ВременныеТаблицыДанныхДокумента() Экспорт
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Дата        КАК Дата,
	|	НЕОПРЕДЕЛЕНО КАК Партнер,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка) КАК Соглашение,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Менеджер КАК Менеджер,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) КАК ТипЗапасов
	|ПОМЕСТИТЬ ТаблицаДанныхДокумента
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки    КАК НомерСтроки,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.ЗаказКлиента   КАК Заказ,
	|	ТаблицаТоваров.Номенклатура   КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Назначение     КАК Назначение,
	|	ТаблицаТоваров.Упаковка       КАК Упаковка,
	|	ТаблицаТоваров.Серия          КАК Серия,
	|	ТаблицаТоваров.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ТаблицаТоваров.Количество     КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|				ИЛИ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|			ТОГДА 0
	|		ИНАЧЕ &ТекстПоляТаблицаТоваровКоличествоПоРНПТ_
	|	КОНЕЦ                         КАК КоличествоПоРНПТ,
	|	&ТекстПоляТаблицаТоваровНомерГТД_ КАК НомерГТД,
	|	ТаблицаТоваров.Склад          КАК Склад,
	|	ТаблицаТоваров.Цена           КАК Цена,
	|	ТаблицаТоваров.Сумма          КАК СуммаВознаграждения,
	|	ИСТИНА                        КАК ПодбиратьВидыЗапасов
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки      КАК НомерСтроки,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ЗаказКлиента     КАК ЗаказКлиента,
	|	ТаблицаВидыЗапасов.ВидЗапасов       КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.Количество       КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|				ИЛИ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.КоличествоПоРНПТ
	|	КОНЕЦ                               КАК КоличествоПоРНПТ,
	|	ТаблицаВидыЗапасов.Цена             КАК Цена,
	|	ТаблицаВидыЗапасов.Сумма            КАК Сумма,
	|	ТаблицаВидыЗапасов.ЗаказКлиента     КАК Заказ,
	|	ТаблицаВидыЗапасов.НомерГТД         КАК НомерГТД
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	&ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки      КАК НомерСтроки,
	|	ТаблицаВидыЗапасов.ВидЗапасов       КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ЗаказКлиента     КАК ЗаказКлиента,
	|	ТаблицаВидыЗапасов.Количество       КАК Количество,
	|	ТаблицаВидыЗапасов.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ТаблицаВидыЗапасов.НомерГТД         КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Цена             КАК Цена,
	|	ТаблицаВидыЗапасов.ЗаказКлиента     КАК Заказ,
	|	ТаблицаВидыЗапасов.Сумма            КАК СуммаВознаграждения,
	|	&ВидыЗапасовУказаныВручную          КАК ВидыЗапасовУказаныВручную
	|ПОМЕСТИТЬ ТаблицаВидыЗапасов
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаВидыЗапасов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|";
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ТаблицаТоваров = ?(ДополнительныеСвойства.Свойство("ТаблицыЗаполненияВидовЗапасовПриОбмене")
							И ДополнительныеСвойства.ТаблицыЗаполненияВидовЗапасовПриОбмене <> Неопределено
							И ДополнительныеСвойства.ТаблицыЗаполненияВидовЗапасовПриОбмене.Свойство("Товары"),
						ДополнительныеСвойства.ТаблицыЗаполненияВидовЗапасовПриОбмене.Товары,
						Товары);
	
	Запрос.УстановитьПараметр("ТаблицаТоваров",        ТаблицаТоваров);
	Запрос.УстановитьПараметр("ТаблицаВидыЗапасов",    ВидыЗапасов);
	Запрос.УстановитьПараметр("Дата",                  Дата);
	Запрос.УстановитьПараметр("Организация",           Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("ВидыЗапасовУказаныВручную", ВидыЗапасовУказаныВручную);
	Запрос.УстановитьПараметр("Менеджер", Менеджер);
	
	УчетПрослеживаемыхТоваровЛокализация.УстановитьПараметрыИспользованияУчетаПрослеживаемыхТоваров(Запрос);
	
	ЗапасыСервер.ДополнитьВременныеТаблицыОбязательнымиКолонками(Запрос);
	ЗапасыСервер.ПроверитьНеобходимостьПерезаполненияВидовЗапасовДокумента(ЭтотОбъект, Запрос);
	
	ОбщегоНазначенияУТ.ЗаменитьОтсутствующиеПоляТаблицыЗначенийВТекстеЗапроса(
		ТаблицаТоваров,
		Запрос.Текст,
		"&ТекстПоляТаблицаТоваровКоличествоПоРНПТ_",
		"ТаблицаТоваров",
		"КоличествоПоРНПТ",
		"ТаблицаТоваров.КоличествоПоРНПТ",
		"0");
	
	ОбщегоНазначенияУТ.ЗаменитьОтсутствующиеПоляТаблицыЗначенийВТекстеЗапроса(
		ТаблицаТоваров,
		Запрос.Текст,
		"&ТекстПоляТаблицаТоваровНомерГТД_",
		"ТаблицаТоваров",
		"НомерГТД",
		"ТаблицаТоваров.НомерГТД",
		"ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)");
	
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

// Процедура формирует временную таблицу товаров с аналитикой обособленного учета.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц,
//								который будет содержать созданную таблицу.
//
Процедура СформироватьВременнуюТаблицуТоваровИАналитики(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка) КАК Партнер,
	|	ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка) КАК Соглашение,
	|	ТаблицаТоваров.Номенклатура               КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика             КАК Характеристика,
	|	ТаблицаТоваров.Назначение                 КАК Назначение,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СтатусУказанияСерий = 14
	|			ТОГДА ТаблицаТоваров.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                     КАК Серия,
	|	ТаблицаТоваров.Количество                 КАК Количество,
	|	ТаблицаТоваров.Склад                      КАК Склад,
	|	ТаблицаДанныхДокумента.Менеджер           КАК Менеджер,
	|	ТаблицаДанныхДокумента.Сделка             КАК Сделка,
	|	ТаблицаДанныхДокумента.Подразделение      КАК Подразделение,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС
	|ПОМЕСТИТЬ ТаблицаТоваровИАналитики
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|		ПО ИСТИНА
	|ГДЕ
	|	ТаблицаТоваров.Номенклатура.ТипНоменклатуры В(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|													ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|;
	|";
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Заполняет аналитики учета номенклатуры. Используется в отчете ОстаткиТоваровОрганизаций.
//
Процедура ЗаполнитьАналитикиУчетаНоменклатуры() Экспорт
	
	ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров();
	
КонецПроцедуры

// Инициализирует параметры заполнения видов запасов дополнительных свойств документа, используемых при записи документа
// в режиме 'Проведения' или 'Отмены проведения'.
//
// Параметры:
//	ДокументОбъект - ДокументОбъект.ОтгрузкаТоваровКлиенту - документ, для которого выполняется инициализация параметров.
//	РежимЗаписи - РежимЗаписиДокумента - режим записи документа.
//
Процедура ИнициализироватьПараметрыЗаполненияВидовЗапасовДляПроведения(ДокументОбъект, РежимЗаписи = Неопределено) Экспорт
	
	ДокументОбъект.ДополнительныеСвойства.Вставить("ПараметрыЗаполненияВидовЗапасов", ПараметрыЗаполненияВидовЗапасов());
	
КонецПроцедуры

// Заполняет реквизиты, хранящие информацию о видах запасов и аналитиках учета номенклатуры в табличной части 'Товары'
// документа, а также заполняет табличную часть 'ВидыЗапасов'.
//
// Параметры:
//	Отказ - Булево - признак того, что не удалось заполнить данные.
//	ТаблицыДокумента - см. Документы.ОтгрузкаТоваровКлиенту.КоллекцияТабличныхЧастейТоваров.
//
Процедура ЗаполнитьВидыЗапасовПриОбмене(Отказ, ТаблицыДокумента) Экспорт
	
	ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров();
	
	Если ТаблицыДокумента <> Неопределено Тогда
		ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров(ТаблицыДокумента);
		ДополнительныеСвойства.Вставить("ТаблицыЗаполненияВидовЗапасовПриОбмене", ТаблицыДокумента);
	Иначе
		ИмяПараметра = "ТаблицыДокумента";
		
		ТекстИсключения = НСтр("ru = 'Для заполнения видов запасов не передан параметр ""%1"".';
								|en = 'The ""%1"" parameter is not transferred to fill in the inventory owner attributes.'");
		ТекстИсключения = СтрШаблон(ТекстИсключения, ИмяПараметра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ЗаполнитьВидыЗапасов(Отказ);
	ДополнительныеСвойства.Удалить("ТаблицыЗаполненияВидовЗапасовПриОбмене");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ПараметрыПроверкиКоличества = Новый Структура("Товары");
	
	НоменклатураСервер.ПроверитьЗаполнениеКоличества(
		ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, ПараметрыПроверкиКоличества["Товары"]);
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ОтгрузкаТоваровКлиенту);
	
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект, ПараметрыУказанияСерий, Отказ, МассивНепроверяемыхРеквизитов);
	
	Для ТекИндекс = 0 По Товары.Количество() - 1 Цикл
		
		СтрокаТовары = Товары[ТекИндекс]; // СтрокаТабличнойЧасти
		
		Если ОтгрузкаПоЗаказам
			И Не ЗначениеЗаполнено(ЗаказКлиента)
			И Не ЗначениеЗаполнено(СтрокаТовары.ЗаказКлиента) Тогда
			
			ТекстОшибки = НСтр("ru = 'Не заполнено поле ""Распоряжение"" в строке %НомерСтроки% списка ""Товары""';
								|en = 'The ""Reference"" field is not filled in the %НомерСтроки% line of the ""Goods"" list'");
			ТекстОшибки =  СтрЗаменить(ТекстОшибки, "%НомерСтроки%", СтрокаТовары.НомерСтроки);
			ПолеОшибки  = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары",
																			СтрокаТовары.НомерСтроки,
																			"ЗаказКлиента");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, ПолеОшибки, Неопределено, Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДоставкаТоваров.ПроверитьЗаполнениеРеквизитовДоставки(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ,
		ОтгрузкаПоЗаказам);
	
	Если ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоПодразделениямМенеджерам") Тогда
		ПроверяемыеРеквизиты.Добавить("Подразделение");
	КонецЕсли;
	
	ИсправлениеДокументов.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	Если Не Отказ
		И ОбщегоНазначенияУТ.ПроверитьЗаполнениеРеквизитовОбъекта(ЭтотОбъект, ПроверяемыеРеквизиты) Тогда
		
		Отказ = Истина;
		
	КонецЕсли;
	
	Если Не ОтгрузкаПоЗаказам
		И Не ПраваПользователяПовтИсп.СозданиеРеализацииТоваровУслугБезЗаказа() Тогда
		ТекстОшибки = НСтр("ru = 'Нет прав на создание отгрузки товаров клиенту без распоряжения';
							|en = 'Insufficient rights to create goods shipment to customer without a reference'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Ссылка, , , Отказ);
	КонецЕсли;
	
	ПродажиСервер.ПроверитьКорректностьЗаполненияДокументаПродажи(ЭтотОбъект, Отказ);
	ПродажиСервер.ПроверитьЗапретОтгрузки(Партнер, Отказ);
	
	ЗатратыСервер.ПроверитьИспользованиеПартионногоУчета22(ЭтотОбъект, Дата, Отказ);
	
	ОтгрузкаТоваровКлиентуЛокализация.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокументПередЗаполнением();
	
	СтруктураПараметров = Новый Структура("РеквизитыШапки, СкладОтгрузки, ЗаполнятьПоОрдеру, ПараметрыОформления, МассивОснований");
	СтруктураПараметров.РеквизитыШапки = Новый Структура();
	
	ЗаполненНаОснованииДокумента = Ложь;
	ТипДанныхЗаполнения          = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") И ДанныеЗаполнения.Свойство("АктОРасхождениях") Тогда
		
		ЗаполнитьДокументНаОснованииАктаОРасхождениях(ДанныеЗаполнения);
		ЗаполненНаОснованииДокумента = Истина;
	
	ИначеЕсли ТипДанныхЗаполнения = Тип("Структура") Тогда
		
		// Заполнение из формы списка распоряжений.
		Если ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
			
			// Если передан склад - необходимо заполнять товары только по указанном складу.
			ДанныеЗаполнения.Свойство("СкладОтгрузки", СтруктураПараметров.СкладОтгрузки);
			ДанныеЗаполнения.Свойство("РеквизитыШапки", СтруктураПараметров.РеквизитыШапки);
			ДанныеЗаполнения.Свойство("ЗаполнятьПоОрдеру", СтруктураПараметров.ЗаполнятьПоОрдеру);
			ДанныеЗаполнения.Свойство("ПараметрыОформления", СтруктураПараметров.ПараметрыОформления);
			
			ДокОснование = ДанныеЗаполнения.ДокументОснование;
			Если ТипЗнч(ДокОснование) = Тип("Массив") И ЗначениеЗаполнено(ДокОснование) Тогда
				СтруктураПараметров.МассивОснований = ДокОснование;
				ДокОснование = ДанныеЗаполнения.ДокументОснование[0];
			КонецЕсли;
				
			ТипОснования = ТипЗнч(ДокОснование);
			Если ТипОснования = Тип("ДокументСсылка.ОтгрузкаТоваровКлиенту") Тогда
				ИсправлениеДокументов.ЗаполнитьИсправление(ЭтотОбъект, ДокОснование);
			ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЗаказКлиента") Тогда
				ЗаполнитьДокументПоРаспоряжениям(ДокОснование, СтруктураПараметров);
			Иначе
				ЗаполнитьДокументПоРаспоряжениям(ДанныеЗаполнения.ДокументОснование, СтруктураПараметров);
				ЗаполненНаОснованииДокумента = Истина;
			КонецЕсли;
			
		Иначе
			ЗаполнитьДокументПоОтбору(ДанныеЗаполнения);
		КонецЕсли;

	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказКлиента") Тогда

		ЗаполнитьДокументПоРаспоряжениям(ДанныеЗаполнения, СтруктураПараметров);
		ЗаполненНаОснованииДокумента = Истина;
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("СправочникСсылка.СделкиСКлиентами") Тогда
		
		ЗаполнитьДокументНаОснованииСделкиПоПродаже(ДанныеЗаполнения);
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("СправочникСсылка.СоглашенияСКлиентами") Тогда
		
		ЗаполнитьДокументНаОснованииИндивидуальногоСоглашенияСКлиентом(ДанныеЗаполнения);
	
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка." + Метаданные().Имя) Тогда
		
		ИсправлениеДокументов.ЗаполнитьИсправление(ЭтотОбъект, ДанныеЗаполнения);
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		
		ЗаполнитьДокументНаОснованииПриобретенияТоваровУслуг(ДанныеЗаполнения);
		ЗаполненНаОснованииДокумента = Истина;

	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
		
		ЗаполнитьДокументНаОснованииВозвратаТоваровОтКлиента(ДанныеЗаполнения);
		ЗаполненНаОснованииДокумента = Истина;

	КонецЕсли;
	
	Если Не ЗаполненНаОснованииДокумента Тогда
		
		ИспользоватьСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
		Если Не ЗначениеЗаполнено(ЗаказКлиента) И Не ИспользоватьСоглашенияСКлиентами Тогда
			
			ЗаполнитьУсловияПродажПоУмолчанию();
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОтветственныеЛицаСервер.ЗаполнитьМенеджера(ЭтотОбъект, Ложь);
	ЗаполнениеОбъектовПоСтатистике.ЗаполнитьРеквизитыОбъекта(ЭтотОбъект, ДанныеЗаполнения);
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	ОтветственныеЛицаСервер.ЗаполнитьМенеджера(ЭтотОбъект, Ложь);
	Если Не ЗначениеЗаполнено(Менеджер) Тогда
		Менеджер = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	ОтгрузкаТоваровКлиентуЛокализация.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ИсправлениеДокументов.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	СуммаДокумента         = ПолучитьСуммуДокумента();
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ОтгрузкаТоваровКлиенту);
	
	ПараметрыОкругления = Новый Структура("Товары");
	НоменклатураСервер.ОкруглитьКоличествоШтучныхТоваров(ЭтотОбъект, РежимЗаписи, ПараметрыОкругления["Товары"]);
	
	НоменклатураСервер.ОчиститьНеиспользуемыеСерии(ЭтотОбъект, ПараметрыУказанияСерий);
	
	ПараметрыРегистрации = Документы.ОтгрузкаТоваровКлиенту.ПараметрыРегистрацииСчетовФактурВыданных(ЭтотОбъект);
	УчетНДСУП.АктуализироватьСчетаФактурыВыданныеПередЗаписью(ПараметрыРегистрации, РежимЗаписи, ПометкаУдаления, Проведен);
	
	Если ОтгрузкаПоЗаказам
		И ЗначениеЗаполнено(ЗаказКлиента) Тогда
		Для Каждого ТекСтрока Из Товары Цикл
			Если Не ЗначениеЗаполнено(ТекСтрока.ЗаказКлиента) Тогда
				ТекСтрока.ЗаказКлиента = ЗаказКлиента;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ОтгрузкаПоЗаказам Тогда
		ЗаказыСервер.УстановитьКлючВСтрокахТабличнойЧасти(ЭтотОбъект, "Товары");
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров();
		РегистрыСведений.АналитикаУчетаНаборов.ЗаполнитьВКоллекции(Товары);
		ЗаполнитьВидыЗапасов(Отказ);
		
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		
		Если Не ВидыЗапасовУказаныВручную Тогда
			ВидыЗапасов.Очистить();
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначенияУТ.ЗаполнитьИдентификаторыДокумента(ЭтотОбъект, "Товары,ВидыЗапасов");
	
	Если ЭтоНовый()
		И Не ЗначениеЗаполнено(Номер) Тогда
		
		УстановитьНовыйНомер();
		
	КонецЕсли;
	
	ОтгрузкаТоваровКлиентуЛокализация.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
	ОтгрузкаТоваровКлиентуЛокализация.ПриЗаписи(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ИнициализироватьПараметрыЗаполненияВидовЗапасовДляПроведения(ЭтотОбъект);
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ДоставкаТоваров.ОтразитьСостояниеДоставки(Ссылка, Отказ);
	
	ПараметрыРегистрации = Документы.ОтгрузкаТоваровКлиенту.ПараметрыРегистрацииСчетовФактурВыданных(ЭтотОбъект);
	УчетНДСУП.АктуализироватьСчетаФактурыВыданныеПриПроведении(ПараметрыРегистрации);
	
	ОтгрузкаТоваровКлиентуЛокализация.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ИнициализироватьПараметрыЗаполненияВидовЗапасовДляПроведения(ЭтотОбъект);
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ДоставкаТоваров.ОтразитьСостояниеДоставки(Ссылка, Отказ, Истина);
	
	ПараметрыРегистрации = Документы.ОтгрузкаТоваровКлиенту.ПараметрыРегистрацииСчетовФактурВыданных(ЭтотОбъект);
	УчетНДСУП.АктуализироватьСчетаФактурыВыданныеПриУдаленииПроведения(ПараметрыРегистрации);
	
	ОтгрузкаТоваровКлиентуЛокализация.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИсправлениеДокументов.ПриКопировании(ЭтотОбъект, ОбъектКопирования);
	
	ОтгрузкаПоЗаказам                     = Ложь;
	ЗаказКлиента                          = Неопределено;
	ВидыЗапасовУказаныВручную             = Ложь;
	ИспользоватьСоглашенияСКлиентами      = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
	
	Если ЗначениеЗаполнено(Соглашение) И ИспользоватьСоглашенияСКлиентами Тогда
		
		УсловияПродаж = ПродажиСервер.ПолучитьУсловияПродаж(Соглашение, Истина, Истина);
		
		Если УсловияПродаж.СтатусСоглашения = Перечисления.СтатусыСоглашенийСКлиентами.Закрыто Тогда
			Соглашение = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	Для Каждого ТекСтрока Из Товары Цикл
		
		ТекСтрока.ЗаказКлиента          = Неопределено;
		ТекСтрока.КодСтроки             = 0;
		ТекСтрока.ИдентификаторСтроки   = "";
		ТекСтрока.Назначение            = Неопределено;
		
	КонецЦикла;
	
	НазначениеПоУмолчанию = НаправленияДеятельностиСервер.ТолкающееНазначение(НаправлениеДеятельности);
	НакладныеСервер.ЗаполнитьНазначенияВТабличнойЧасти(Товары, НазначениеПоУмолчанию);
	
	Серии.Очистить();
	ВидыЗапасов.Очистить();
	
	ИнициализироватьДокумент();
	
	ОбщегоНазначенияУТ.ОчиститьИдентификаторыДокумента(ЭтотОбъект, "Товары,ВидыЗапасов");
	
	ОтгрузкаТоваровКлиентуЛокализация.ПриКопировании(ЭтотОбъект, ОбъектКопирования);
	
	Автор = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

Процедура ЗаполнитьДокументПоОтбору(Знач ДанныеЗаполнения)

	Если ДанныеЗаполнения.Свойство("РеквизитыШапки") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения.РеквизитыШапки);
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("Партнер") Тогда
		Партнер = ДанныеЗаполнения.Партнер;
		
		ИспользоватьСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
		Если ИспользоватьСоглашенияСКлиентами Тогда
			ЗаполнитьУсловияПродажПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("Товары") Тогда
		Для Каждого ТекСтрока Из ДанныеЗаполнения.Товары Цикл
			НоваяСтрока = Товары.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоРаспоряжениям(ДокументОснование, Параметры);
	
	РеквизитыЗаказа = Параметры.РеквизитыШапки;
	
	Если Параметры.МассивОснований = Неопределено Тогда
		Параметры.МассивОснований = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументОснование);
	КонецЕсли;
	
	ТипОснования = ТипЗнч(ДокументОснование);
	Если ТипОснования = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Таблица.Ссылка                                КАК ЗаказКлиента,
		|	Таблица.Партнер                               КАК Партнер,
		|	Таблица.Контрагент                            КАК Контрагент,
		|	Таблица.Договор                               КАК Договор,
		|	Таблица.Организация                           КАК Организация,
		|	Таблица.Соглашение                            КАК Соглашение,
		|	Таблица.Сделка                                КАК Сделка,
		|	Таблица.Склад                                 КАК Склад,
		|	Таблица.Склад.ЭтоГруппа
		|		И Таблица.Склад.ВыборГруппы = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах) КАК ЗапрещеноВыбиратьГруппуСкладов,
		|	Таблица.Статус                                КАК СтатусДокумента,
		|	НЕ Таблица.Проведен                           КАК ЕстьОшибкиПроведен,
		|	Таблица.Подразделение                         КАК Подразделение,
		|	Таблица.Валюта                                КАК Валюта,
		|	Таблица.НалогообложениеНДС                    КАК НалогообложениеНДС,
		|	Таблица.ФормаОплаты                           КАК ФормаОплаты,
		|	Таблица.ГрафикОплаты                          КАК ГрафикОплаты,
		|	Таблица.БанковскийСчет                        КАК БанковскийСчет,
		|	Таблица.Касса                                 КАК Касса,
		|	Таблица.БанковскийСчетКонтрагента             КАК БанковскийСчетКонтрагента,
		|	Таблица.Грузоотправитель                      КАК Грузоотправитель,
		|	Таблица.Грузополучатель                       КАК Грузополучатель,
		|	Таблица.БанковскийСчетГрузоотправителя        КАК БанковскийСчетГрузоотправителя,
		|	Таблица.БанковскийСчетГрузополучателя         КАК БанковскийСчетГрузополучателя,
		|	ВЫБОР
		|		КОГДА
		|			Таблица.Склад.ЭтоГруппа И Таблица.Склад.ВыборГруппы = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах)
		|		ТОГДА
		|			ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|		ИНАЧЕ
		|			Таблица.Склад
		|	КОНЕЦ                                         КАК СкладОтгрузки,
		|	Таблица.СпособДоставки                        КАК СпособДоставки,
		|	Таблица.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчика) КАК СпособДоставкиСиламиПеревозчика,
		|	Таблица.ПеревозчикПартнер                     КАК ПеревозчикПартнер,
		|	Таблица.АдресДоставки                         КАК АдресДоставки,
		|	Таблица.АдресДоставкиЗначенияПолей            КАК АдресДоставкиЗначенияПолей,
		|	Таблица.АдресДоставкиПеревозчика              КАК АдресДоставкиПеревозчика,
		|	Таблица.АдресДоставкиПеревозчикаЗначенияПолей КАК АдресДоставкиПеревозчикаЗначенияПолей,
		|	Таблица.ЗонаДоставки                          КАК ЗонаДоставки,
		|	Таблица.ВремяДоставкиС                        КАК ВремяДоставкиС,
		|	Таблица.ВремяДоставкиПо                       КАК ВремяДоставкиПо,
		|	Таблица.ОсобыеУсловияПеревозки                КАК ОсобыеУсловияПеревозки,
		|	Таблица.ОсобыеУсловияПеревозкиОписание        КАК ОсобыеУсловияПеревозкиОписание,
		|	Таблица.ДополнительнаяИнформацияПоДоставке    КАК ДополнительнаяИнформацияПоДоставке,
		|	Таблица.НаправлениеДеятельности               КАК НаправлениеДеятельности,
		|	Таблица.КонтактноеЛицо                        КАК КонтактноеЛицо,
		|	Таблица.КартаЛояльности                       КАК КартаЛояльности,
		|	Таблица.ГруппаФинансовогоУчета                КАК ГруппаФинансовогоУчета,
		|	ИСТИНА                                        КАК ОтгрузкаПоЗаказам,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтгрузкаТоваровКлиенту) КАК ХозяйственнаяОперация,
		|	Таблица.Руководитель                          КАК Руководитель,
		|	Таблица.ГлавныйБухгалтер                      КАК ГлавныйБухгалтер,
		|	ВЫБОР
		|		КОГДА Таблица.Склад.ЭтоГруппа
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ВыбранаГруппаСкладов
		|ИЗ
		|	Документ.ЗаказКлиента КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка = &ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РаспоряженияНаОтгрузкуОтгружено.Склад КАК Склад
		|ИЗ
		|	#РаспоряженияНаОтгрузкуОборотыРазвернутая КАК РаспоряженияНаОтгрузкуОтгружено
		|ГДЕ
		|	РаспоряженияНаОтгрузкуОтгружено.КОтгрузкеКоличествоОборот
		|		- РаспоряженияНаОтгрузкуОтгружено.ОтгруженоКоличествоОборот > 0
		|";
		
		ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
		ПараметрыФормированияТаблицы.ТекстУсловия = "
			|	Распоряжение = &ДокументОснование
			|";
		ПараметрыФормированияТаблицы.Группировка = "Склад";
		ПараметрыФормированияТаблицы.Показатели = "КОтгрузке, Отгружено";
		ПараметрыФормированияТаблицы.Ресурсы = "Количество";
		
		РаспоряженияНаОтгрузкуОборотыРазвернутая =
			РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
				ПараметрыФормированияТаблицы);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"#РаспоряженияНаОтгрузкуОборотыРазвернутая",
			РаспоряженияНаОтгрузкуОборотыРазвернутая);
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		РеквизитыЗаказа = РезультатЗапроса[РезультатЗапроса.ВГраница()-1].Выбрать();
		РеквизитыЗаказа.Следующий();
		
		Документы.ЗаказКлиента.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыЗаказа.ЗаказКлиента,
			РеквизитыЗаказа.СтатусДокумента,
			РеквизитыЗаказа.ЕстьОшибкиПроведен);
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыЗаказа);
		
		Если РеквизитыЗаказа.ЗапрещеноВыбиратьГруппуСкладов Тогда
			МассивСкладов = РезультатЗапроса[РезультатЗапроса.ВГраница()].Выгрузить().ВыгрузитьКолонку("Склад");
		КонецЕсли;
		
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОтгрузкаТоваровКлиенту") Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.Ссылка КАК ЗаказКлиента,
		|	Таблица.Партнер КАК Партнер,
		|	Таблица.Контрагент КАК Контрагент,
		|	Таблица.Договор КАК Договор,
		|	Таблица.Организация КАК Организация,
		|	Таблица.Соглашение КАК Соглашение,
		|	Таблица.Сделка КАК Сделка,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Склад.ЭтоГруппа
		|		И Таблица.Склад.ВыборГруппы = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах) КАК ЗапрещеноВыбиратьГруппуСкладов,
		|	Таблица.Подразделение КАК Подразделение,
		|	Таблица.Валюта КАК Валюта,
		|	Таблица.НалогообложениеНДС КАК НалогообложениеНДС,
		|	НЕОПРЕДЕЛЕНО КАК ФормаОплаты,
		|	НЕОПРЕДЕЛЕНО КАК ГрафикОплаты,
		|	НЕОПРЕДЕЛЕНО КАК БанковскийСчет,
		|	НЕОПРЕДЕЛЕНО КАК Касса,
		|	Таблица.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
		|	Таблица.Грузоотправитель КАК Грузоотправитель,
		|	Таблица.Грузополучатель КАК Грузополучатель,
		|	Таблица.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
		|	Таблица.БанковскийСчетГрузополучателя КАК БанковскийСчетГрузополучателя,
		|	ВЫБОР
		|		КОГДА Таблица.Склад.ЭтоГруппа
		|		И Таблица.Склад.ВыборГруппы = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|		ИНАЧЕ Таблица.Склад
		|	КОНЕЦ КАК СкладОтгрузки,
		|	Таблица.СпособДоставки КАК СпособДоставки,
		|	Таблица.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчика) КАК
		|		СпособДоставкиСиламиПеревозчика,
		|	Таблица.ПеревозчикПартнер КАК ПеревозчикПартнер,
		|	Таблица.АдресДоставки КАК АдресДоставки,
		|	Таблица.АдресДоставкиЗначенияПолей КАК АдресДоставкиЗначенияПолей,
		|	Таблица.АдресДоставкиПеревозчика КАК АдресДоставкиПеревозчика,
		|	Таблица.АдресДоставкиПеревозчикаЗначенияПолей КАК АдресДоставкиПеревозчикаЗначенияПолей,
		|	Таблица.ЗонаДоставки КАК ЗонаДоставки,
		|	Таблица.ВремяДоставкиС КАК ВремяДоставкиС,
		|	Таблица.ВремяДоставкиПо КАК ВремяДоставкиПо,
		|	Таблица.ОсобыеУсловияПеревозки КАК ОсобыеУсловияПеревозки,
		|	Таблица.ОсобыеУсловияПеревозкиОписание КАК ОсобыеУсловияПеревозкиОписание,
		|	Таблица.ДополнительнаяИнформацияПоДоставке КАК ДополнительнаяИнформацияПоДоставке,
		|	Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Таблица.КонтактноеЛицо КАК КонтактноеЛицо,
		|	НЕОПРЕДЕЛЕНО КАК КартаЛояльности,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчета,
		|	ИСТИНА КАК ОтгрузкаПоЗаказам,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтгрузкаТоваровКлиенту) КАК ХозяйственнаяОперация,
		|	Таблица.Руководитель КАК Руководитель,
		|	Таблица.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
		|	ВЫБОР
		|		КОГДА Таблица.Склад.ЭтоГруппа
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ВыбранаГруппаСкладов
		|ИЗ
		|	Документ.ОтгрузкаТоваровКлиенту КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка = &ДокументОснование
		|";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
		
		РеквизитыЗаказа = Запрос.Выполнить().Выбрать();
		РеквизитыЗаказа.Следующий();
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыЗаказа);
		
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента") Тогда
	
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Таблица.Ссылка                                КАК ЗаказКлиента,
		|	Таблица.Партнер                               КАК Партнер,
		|	Таблица.Контрагент                            КАК Контрагент,
		|	Таблица.Договор                               КАК Договор,
		|	Таблица.Организация                           КАК Организация,
		|	Таблица.Соглашение                            КАК Соглашение,
		|	Таблица.Сделка                                КАК Сделка,
		|	Таблица.Склад                                 КАК Склад,
		|	Таблица.Склад.ЭтоГруппа
		|		И Таблица.Склад.ВыборГруппы = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах) КАК ЗапрещеноВыбиратьГруппуСкладов,
		|	Таблица.Подразделение                         КАК Подразделение,
		|	Таблица.Валюта                                КАК Валюта,
		|	Таблица.НалогообложениеНДС                    КАК НалогообложениеНДС,
		|	Таблица.ФормаОплаты                           КАК ФормаОплаты,
		|	Таблица.ГрафикОплаты                          КАК ГрафикОплаты,
		|	Таблица.БанковскийСчет                        КАК БанковскийСчет,
		|	Таблица.Касса                                 КАК Касса,
		|	Таблица.БанковскийСчетКонтрагента             КАК БанковскийСчетКонтрагента,
		|	Таблица.Грузоотправитель                      КАК Грузоотправитель,
		|	Таблица.Грузополучатель                       КАК Грузополучатель,
		|	Таблица.БанковскийСчетГрузоотправителя        КАК БанковскийСчетГрузоотправителя,
		|	Таблица.БанковскийСчетГрузополучателя         КАК БанковскийСчетГрузополучателя,
		|	ВЫБОР
		|		КОГДА
		|			Таблица.Склад.ЭтоГруппа И Таблица.Склад.ВыборГруппы = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах)
		|		ТОГДА
		|			ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|		ИНАЧЕ
		|			Таблица.Склад
		|	КОНЕЦ                                         КАК СкладОтгрузки,
		|	Таблица.СпособДоставки                        КАК СпособДоставки,
		|	Таблица.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчика) КАК СпособДоставкиСиламиПеревозчика,
		|	Таблица.ПеревозчикПартнер                     КАК ПеревозчикПартнер,
		|	Таблица.АдресДоставки                         КАК АдресДоставки,
		|	Таблица.АдресДоставкиЗначенияПолей            КАК АдресДоставкиЗначенияПолей,
		|	Таблица.АдресДоставкиПеревозчика              КАК АдресДоставкиПеревозчика,
		|	Таблица.АдресДоставкиПеревозчикаЗначенияПолей КАК АдресДоставкиПеревозчикаЗначенияПолей,
		|	Таблица.ЗонаДоставки                          КАК ЗонаДоставки,
		|	Таблица.ВремяДоставкиС                        КАК ВремяДоставкиС,
		|	Таблица.ВремяДоставкиПо                       КАК ВремяДоставкиПо,
		|	Таблица.ОсобыеУсловияПеревозки                КАК ОсобыеУсловияПеревозки,
		|	Таблица.ОсобыеУсловияПеревозкиОписание        КАК ОсобыеУсловияПеревозкиОписание,
		|	Таблица.ДополнительнаяИнформацияПоДоставке    КАК ДополнительнаяИнформацияПоДоставке,
		|	Таблица.НаправлениеДеятельности               КАК НаправлениеДеятельности,
		|	Таблица.КонтактноеЛицо                        КАК КонтактноеЛицо,
		|	Таблица.КартаЛояльности                       КАК КартаЛояльности,
		|	Таблица.ГруппаФинансовогоУчета                КАК ГруппаФинансовогоУчета,
		|	ИСТИНА                                        КАК ОтгрузкаПоЗаказам,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтгрузкаТоваровКлиенту) КАК ХозяйственнаяОперация,
		|	Таблица.Руководитель                          КАК Руководитель,
		|	Таблица.ГлавныйБухгалтер                      КАК ГлавныйБухгалтер
		|ИЗ
		|	Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка = &ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РаспоряженияНаОтгрузкуОтгружено.Склад КАК Склад
		|ИЗ
		|	#РаспоряженияНаОтгрузкуОборотыРазвернутая КАК РаспоряженияНаОтгрузкуОтгружено
		|ГДЕ
		|	РаспоряженияНаОтгрузкуОтгружено.КОтгрузкеКоличествоОборот
		|		- РаспоряженияНаОтгрузкуОтгружено.ОтгруженоКоличествоОборот > 0
		|";
		
		ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
		ПараметрыФормированияТаблицы.ТекстУсловия = "
			|	Распоряжение = &ДокументОснование
			|";
		ПараметрыФормированияТаблицы.Группировка = "Склад";
		ПараметрыФормированияТаблицы.Показатели = "КОтгрузке, Отгружено";
		ПараметрыФормированияТаблицы.Ресурсы = "Количество";
		
		РаспоряженияНаОтгрузкуОборотыРазвернутая =
			РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
				ПараметрыФормированияТаблицы);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"#РаспоряженияНаОтгрузкуОборотыРазвернутая",
			РаспоряженияНаОтгрузкуОборотыРазвернутая);
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
		
		РеквизитыЗапроса = Запрос.ВыполнитьПакет();
		РеквизитыЗаказа = РеквизитыЗапроса[РеквизитыЗапроса.ВГраница()-1].Выбрать();
		РеквизитыЗаказа.Следующий();
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыЗаказа);
		
		Если РеквизитыЗаказа.ЗапрещеноВыбиратьГруппуСкладов Тогда
			МассивСкладов = РезультатЗапроса[РеквизитыЗапроса.ВГраница()].Выгрузить().ВыгрузитьКолонку("Склад");
		КонецЕсли;
		
	ИначеЕсли ТипОснования = Тип("Массив") Тогда
	
		РеквизитыЗаказа.Вставить("ОтгрузкаПоЗаказам", Истина);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыЗаказа);

		Валюта = РеквизитыЗаказа.ВалютаВзаиморасчетов;
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтгрузкаТоваровКлиенту;
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаТовары.Распоряжение КАК Распоряжение
		|ИЗ
		|	Документ.ПоступлениеТоваровОтКлиента.Товары КАК ТаблицаТовары
		|ГДЕ
		|	ТаблицаТовары.Ссылка В (&МассивОснований)
		|	И ТаблицаТовары.Ссылка.УменьшитьОтгрузкуПоРаспоряжению
		|	И ТаблицаТовары.КодСтроки <> 0
		|	И НЕ (ТаблицаТовары.Распоряжение ССЫЛКА Документ.КорректировкаРеализации
		|		И ТаблицаТовары.Распоряжение <> ЗНАЧЕНИЕ(Документ.КорректировкаРеализации.ПустаяСсылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РаспоряженияНаОтгрузкуОтгружено.Склад КАК Склад
		|ИЗ
		|	#РаспоряженияНаОтгрузкуОборотыРазвернутая КАК РаспоряженияНаОтгрузкуОтгружено
		|ГДЕ
		|	РаспоряженияНаОтгрузкуОтгружено.КОтгрузкеКоличествоОборот
		|		- РаспоряженияНаОтгрузкуОтгружено.ОтгруженоКоличествоОборот > 0
		|";
		
		ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
		ПараметрыФормированияТаблицы.ТекстУсловия = "
			|	Распоряжение В (&МассивДокументов)
			|";
		ПараметрыФормированияТаблицы.Группировка = "Склад";
		ПараметрыФормированияТаблицы.Показатели = "КОтгрузке, Отгружено";
		ПараметрыФормированияТаблицы.Ресурсы = "Количество";
		
		РаспоряженияНаОтгрузкуОборотыРазвернутая =
			РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
				ПараметрыФормированияТаблицы);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"#РаспоряженияНаОтгрузкуОборотыРазвернутая",
			РаспоряженияНаОтгрузкуОборотыРазвернутая);
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("МассивОснований", Параметры.МассивОснований);
		Запрос.УстановитьПараметр("МассивДокументов", ДокументОснование);
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		РаспоряженияПоступления = РезультатЗапроса[РезультатЗапроса.ВГраница() - 1].Выгрузить().ВыгрузитьКолонку("Распоряжение");
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Параметры.МассивОснований, РаспоряженияПоступления);
		
		Если РеквизитыЗаказа.ЗапрещеноВыбиратьГруппуСкладов Тогда
			МассивСкладов = РезультатЗапроса[РезультатЗапроса.ВГраница()].Выгрузить().ВыгрузитьКолонку("Склад");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами")
		И ЗначениеЗаполнено(РеквизитыЗаказа.Соглашение) Тогда
	
		РеквизитыСоглашения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыЗаказа.Соглашение,
			"ВозможнаРеализацияБезПереходаПраваСобственности, ИспользуютсяДоговорыКонтрагентов, Представление");
		
		Если Не РеквизитыСоглашения.ВозможнаРеализацияБезПереходаПраваСобственности Тогда
			
			ТекстИсключения = НСтр("ru = 'По соглашению ""%1"" невозможна отгрузка товаров без перехода права собственности.';
									|en = 'Cannot ship goods without transfer of title to goods under the ""%1"" agreement.'");
			ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстИсключения,
				РеквизитыСоглашения.Представление);
			
			ВызватьИсключение ТекстИсключения;
			
		КонецЕсли;

		Если Не РеквизитыСоглашения.ИспользуютсяДоговорыКонтрагентов Тогда
			
			ТекстИсключения = НСтр("ru = 'В соглашении ""%1"" не используются договоры с контрагентами. Использование договоров является обязательным для отгрузки товаров без перехода права собственности.';
									|en = 'Contracts with counterparties are not used in the ""%1"" agreement. Using contracts is required for goods shipment without transfer of title to goods.'");
			ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстИсключения,
				РеквизитыСоглашения.Представление);
			
			ВызватьИсключение ТекстИсключения;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыЗаполненияДокумента = Документы.ОтгрузкаТоваровКлиенту.ПараметрыЗаполненияДокумента();
	Если Параметры.ЗаполнятьПоОрдеру <> Неопределено Тогда
		ПараметрыЗаполненияДокумента.ЗаполнятьПоОрдеру = Параметры.ЗаполнятьПоОрдеру;
	КонецЕсли;
	
	Документы.ОтгрузкаТоваровКлиенту.ИнициализироватьПараметрыЗаполнения(ПараметрыЗаполненияДокумента,
		РеквизитыЗаказа, Параметры.МассивОснований);
	
	Если ЗначениеЗаполнено(Параметры.СкладОтгрузки) Тогда
		Склад = Параметры.СкладОтгрузки;
		ПараметрыЗаполненияДокумента.Склад = Склад;
	ИначеЕсли РеквизитыЗаказа.ЗапрещеноВыбиратьГруппуСкладов И МассивСкладов.Количество() > 0 Тогда 
		Склад = МассивСкладов[0];
		ПараметрыЗаполненияДокумента.Склад = Склад;
	КонецЕсли;
	
	ПараметрыОформления = Параметры.ПараметрыОформления;
	Если ПараметрыОформления <> Неопределено 
		 И ПараметрыОформления.Свойство("ПроверитьСклады") Тогда
		ПараметрыЗаполненияДокумента.Вставить("ПроверитьСклады", ПараметрыОформления.ПроверитьСклады);
	КонецЕсли;
	
	ПараметрыЗаполненияДокумента.ОтображатьСообщение = Истина;
	
	ТаблицаНакладная = Документы.ОтгрузкаТоваровКлиенту.ДанныеТаблицыТоварыДокумента(Ссылка);
	
	Документы.ОтгрузкаТоваровКлиенту.ЗаполнитьПоЗаказамОрдерам(
		ТаблицаНакладная,
		Неопределено,
		ПараметрыЗаполненияДокумента,
		ШтрихкодыУпаковок);
	
	Товары.Загрузить(ТаблицаНакладная);

	НакладныеСервер.УдалитьПустыеСтроки(Товары, "Количество");

	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(
		ЭтотОбъект,
		Документы.ОтгрузкаТоваровКлиенту);
		
	НоменклатураСервер.ЗаполнитьСерииПоFEFO(ЭтотОбъект, ПараметрыУказанияСерий, Ложь);
	
	СтруктураОснование = Документы.ОтгрузкаТоваровКлиенту.СтруктураОснованияДляПечати(ЭтотОбъект);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураОснование);
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииСделкиПоПродаже(Знач ДокументОснование)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СделкиСКлиентами.Ссылка              КАК Сделка,
	|	СделкиСКлиентами.Партнер             КАК Партнер,
	|	СделкиСКлиентами.СоглашениеСКлиентом КАК Соглашение,
	|	КонтактныеЛица.КонтактноеЛицо        КАК КонтактноеЛицо
	|ИЗ
	|	Справочник.СделкиСКлиентами КАК СделкиСКлиентами
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СделкиСКлиентами.ПартнерыИКонтактныеЛица КАК КонтактныеЛица
	|		ПО КонтактныеЛица.Ссылка = СделкиСКлиентами.Ссылка
	|			И КонтактныеЛица.Партнер = СделкиСКлиентами.Партнер
	|			И КонтактныеЛица.КонтактноеЛицо <> ЗНАЧЕНИЕ(Справочник.КонтактныеЛицаПартнеров.ПустаяСсылка)
	|ГДЕ
	|	СделкиСКлиентами.Ссылка = &ДокументОснование";
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Выборка.Следующий();
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОснованииСделкиПоПродаже(Выборка.Партнер);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ИспользоватьСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
	Если ИспользоватьСоглашенияСКлиентами Тогда
		
		Если ЗначениеЗаполнено(Соглашение) Тогда
			ЗаполнитьУсловияПродажПоСоглашению();
		Иначе
			ЗаполнитьУсловияПродажПоУмолчанию();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииИндивидуальногоСоглашенияСКлиентом(Знач ДокументОснование)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СоглашениеСКлиентом.Ссылка          КАК Соглашение,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтгрузкаТоваровКлиенту) КАК ХозяйственнаяОперация,
	|	СоглашениеСКлиентом.Партнер         КАК Партнер,
	|	СоглашениеСКлиентом.КонтактноеЛицо  КАК КонтактноеЛицо,
	|	СоглашениеСКлиентом.Статус          КАК СтатусДокумента,
	|	СоглашениеСКлиентом.ВозможнаРеализацияБезПереходаПраваСобственности КАК ВозможнаРеализацияБезПереходаПраваСобственности,
	|	ВЫБОР
	|		КОГДА СоглашениеСКлиентом.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийСКлиентами.Действует)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ                               КАК ЕстьОшибкиСтатус,
	|	СоглашениеСКлиентом.Типовое         КАК ЕстьОшибкиТиповое,
	|	СоглашениеСКлиентом.ИспользуютсяДоговорыКонтрагентов КАК ИспользуютсяДоговорыКонтрагентов,
	|	ПРЕДСТАВЛЕНИЕ(СоглашениеСКлиентом.Ссылка) КАК ПредставлениеСоглашения
	|ИЗ
	|	Справочник.СоглашенияСКлиентами  КАК СоглашениеСКлиентом
	|ГДЕ
	|	СоглашениеСКлиентом.Ссылка = &ДокументОснование";
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Выборка.Следующий();
	
	Если Не Выборка.ВозможнаРеализацияБезПереходаПраваСобственности Тогда

		ТекстОшибки = НСтр("ru = 'Невозможно оформить документ ""%1"" без перехода права собственности на основании %2. Для оформления реализации товаров воспользуйтесь документом ""%3""';
							|en = 'Cannot issue the ""%1"" document without transfer of title to goods based on %2. To register the sale, use the ""%3"" document'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки,
											Метаданные.Документы.ОтгрузкаТоваровКлиенту.Синоним,
											ДокументОснование,
											Метаданные.Документы.РеализацияТоваровУслуг.Синоним);
		
		ВызватьИсключение ТекстОшибки;

	КонецЕсли;
	
	Если Не Выборка.ИспользуютсяДоговорыКонтрагентов Тогда
		
		ТекстИсключения = НСтр("ru = 'В соглашении ""%1"" не используются договоры с контрагентами. Использование договоров является обязательным для отгрузки товаров без перехода права собственности.';
								|en = 'Contracts with counterparties are not used in the ""%1"" agreement. Using contracts is required for goods shipment without transfer of title to goods.'");
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстИсключения,
			Выборка.ПредставлениеСоглашения);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
	ПродажиСервер.ПроверитьВозможностьВводаНаОснованииСоглашения("ОтгрузкаТоваровКлиенту",
		Выборка.ХозяйственнаяОперация);
	
	МассивДопустимыхСтатусов = Новый Массив();
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыСоглашенийСКлиентами.Действует);
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОснованииСоглашения(Выборка.ЕстьОшибкиТиповое);
	
	ПараметрыПроверки = ОбщегоНазначенияУТ.ПараметрыПроверкиВозможностиВводаНаОсновании();
	ПараметрыПроверки.ЕстьОшибкиСтатус = Выборка.ЕстьОшибкиСтатус;
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОснованииСПараметрами(Выборка.Соглашение, ПараметрыПроверки);

	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ЗаполнитьУсловияПродажПоСоглашению();
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииПриобретенияТоваровУслуг(Знач ДокументОснование)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПриобретениеТоваровУслуг.Ссылка                  КАК ДокументОснование,
	|	ПриобретениеТоваровУслуг.Ссылка                  КАК Ссылка,
	|	ПриобретениеТоваровУслуг.Организация             КАК Организация,
	|	ПриобретениеТоваровУслуг.Склад                   КАК Склад,
	|	ПриобретениеТоваровУслуг.Валюта                  КАК Валюта,
	|	ПриобретениеТоваровУслуг.Сделка                  КАК Сделка,
	|	ПриобретениеТоваровУслуг.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕ ПриобретениеТоваровУслуг.Проведен             КАК ЕстьОшибкиПроведен
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
	|ГДЕ
	|	ПриобретениеТоваровУслуг.Ссылка = &ДокументОснование
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ТоварыПоступления.Склад               КАК Склад,
	|	ТоварыПоступления.Номенклатура        КАК Номенклатура,
	|	ТоварыПоступления.Характеристика      КАК Характеристика,
	|	ТоварыПоступления.Назначение          КАК Назначение,
	|	ТоварыПоступления.Упаковка            КАК Упаковка,
	|	ТоварыПоступления.Серия               КАК Серия,
	|	ТоварыПоступления.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ТоварыПоступления.Количество          КАК Количество,
	|	ТоварыПоступления.КоличествоУпаковок  КАК КоличествоУпаковок
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК ТоварыПоступления
	|ГДЕ
	|	ТоварыПоступления.Ссылка = &ДокументОснование
	|	И ТоварыПоступления.Номенклатура.ТипНоменклатуры В
	|	(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	СерииПоступления.Склад          КАК Склад,
	|	СерииПоступления.Номенклатура   КАК Номенклатура,
	|	СерииПоступления.Характеристика КАК Характеристика,
	|	СерииПоступления.Назначение     КАК Назначение,
	|	СерииПоступления.Серия          КАК Серия,
	|	СерииПоступления.Количество     КАК Количество
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Серии КАК СерииПоступления
	|ГДЕ
	|	СерииПоступления.Ссылка = &ДокументОснование
	|	И СерииПоступления.Номенклатура.ТипНоменклатуры В
	|	(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))";
	
	УстановитьПривилегированныйРежим(Истина);
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	ВыборкаШапка = ПакетЗапросов[0].Выбрать();
	ВыборкаШапка.Следующий();
	
	ПараметрыПроверки = ОбщегоНазначенияУТ.ПараметрыПроверкиВозможностиВводаНаОсновании();
	ПараметрыПроверки.ЕстьОшибкиПроведен = ВыборкаШапка.ЕстьОшибкиПроведен;
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОснованииСПараметрами(ВыборкаШапка.Ссылка, ПараметрыПроверки);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	
	Товары.Загрузить(ПакетЗапросов[1].Выгрузить());
	Серии.Загрузить(ПакетЗапросов[2].Выгрузить());
	
	НазначениеДляЗаполнения = НаправленияДеятельностиСервер.ТолкающееНазначение(НаправлениеДеятельности);
	
	Для Каждого Строка Из Товары Цикл
		
		Если Строка.Назначение <> НазначениеДляЗаполнения Тогда
			Строка.Назначение = Справочники.Назначения.ПустаяСсылка();
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Строка Из Серии Цикл
		
		Если Строка.Назначение <> НазначениеДляЗаполнения Тогда
			Строка.Назначение = Справочники.Назначения.ПустаяСсылка();
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ОтгрузкаТоваровКлиенту);
	НоменклатураСервер.ЗаполнитьСерииПоFEFO(ЭтотОбъект, ПараметрыУказанияСерий, Ложь);
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииВозвратаТоваровОтКлиента(Знач ДокументОснование)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВозвратТоваровОтКлиента.Ссылка               КАК Ссылка,
	|	ВозвратТоваровОтКлиента.Организация          КАК Организация,
	|	ВозвратТоваровОтКлиента.Партнер              КАК Партнер,
	|	ВозвратТоваровОтКлиента.Контрагент           КАК Контрагент,
	|	ВозвратТоваровОтКлиента.Сделка               КАК Сделка,
	|	ВозвратТоваровОтКлиента.Соглашение           КАК Соглашение,
	|	ВозвратТоваровОтКлиента.Валюта               КАК Валюта,
	|	ВозвратТоваровОтКлиента.СуммаДокумента       КАК СуммаДокумента,
	|	ВозвратТоваровОтКлиента.СуммаДокумента       КАК СуммаВзаиморасчетов,
	|	ВозвратТоваровОтКлиента.Склад                КАК Склад,
	|	ВозвратТоваровОтКлиента.Договор              КАК Договор,
	|	ВозвратТоваровОтКлиента.ВозвратПереданнойМногооборотнойТары КАК ВернутьМногооборотнуюТару,
	|	ВозвратТоваровОтКлиента.ПредусмотренЗалогЗаТару КАК ТребуетсяЗалогЗаТару,
	|	ВозвратТоваровОтКлиента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтгрузкаТоваровКлиенту) КАК ХозяйственнаяОперация,
	|	НЕ ВозвратТоваровОтКлиента.Проведен          КАК ЕстьОшибкиПроведен,
	|	ВЫБОР
	|		КОГДА &ИспользоватьСоглашенияСКлиентами
	|			ТОГДА ВозвратТоваровОтКлиента.Соглашение.ВозможнаРеализацияБезПереходаПраваСобственности
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ВозможнаРеализацияБезПереходаПраваСобственности,
	|	ВЫБОР
	|		КОГДА &ИспользоватьСоглашенияСКлиентами
	|			ТОГДА ВозвратТоваровОтКлиента.Соглашение.ИспользуютсяДоговорыКонтрагентов
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ИспользуютсяДоговорыВСоглашении,
	|	ПРЕДСТАВЛЕНИЕ(ВозвратТоваровОтКлиента.Соглашение) КАК ПредставлениеСоглашения,
	|
	|	ВозвратТоваровОтКлиента.Товары.(
	|		Номенклатура             КАК Номенклатура,
	|		Характеристика           КАК Характеристика,
	|		Упаковка                 КАК Упаковка,
	|		Назначение               КАК Назначение,
	|		КоличествоУпаковок       КАК КоличествоУпаковок,
	|		Количество               КАК Количество,
	|		ВЫБОР
	|			КОГДА Количество <> 0
	|				ТОГДА СуммаСНДС / Количество
	|				ИНАЧЕ 0
	|		КОНЕЦ КАК Цена,
	|		СуммаСНДС                КАК Сумма,
	|		Ссылка.Склад             КАК Склад
	|	) КАК Товары,
	|
	|	ВозвратТоваровОтКлиента.Серии.(
	|		Номенклатура             КАК Номенклатура,
	|		Характеристика           КАК Характеристика,
	|		Назначение               КАК Назначение,
	|		Серия                    КАК Серия,
	|		Количество               КАК Количество
	|	) КАК Серии
	|
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента КАК ВозвратТоваровОтКлиента
	|ГДЕ
	|	ВозвратТоваровОтКлиента.Ссылка = &ДокументОснование";
	
	ИспользоватьСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("ИспользоватьСоглашенияСКлиентами", ИспользоватьСоглашенияСКлиентами);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если Не Выборка.ВозможнаРеализацияБезПереходаПраваСобственности Тогда
		
		ТекстОшибки = НСтр("ru = 'Невозможно оформить документ ""%1"" без перехода права собственности на основании %2. Для оформления реализации товаров воспользуйтесь документом ""%3""';
							|en = 'Cannot issue the ""%1"" document without transfer of title to goods based on %2. To register the sale, use the ""%3"" document'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки,
											Метаданные.Документы.ОтгрузкаТоваровКлиенту.Синоним,
											ДокументОснование,
											Метаданные.Документы.РеализацияТоваровУслуг.Синоним);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	Если Не Выборка.ИспользуютсяДоговорыВСоглашении Тогда
		
		ТекстИсключения = НСтр("ru = 'В соглашении ""%1"" не используются договоры с контрагентами. Использование договоров является обязательным для отгрузки товаров без перехода права собственности.';
								|en = 'Contracts with counterparties are not used in the ""%1"" agreement. Using contracts is required for goods shipment without transfer of title to goods.'");
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстИсключения,
			Выборка.ПредставлениеСоглашения);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
	ПараметрыПроверки = ОбщегоНазначенияУТ.ПараметрыПроверкиВозможностиВводаНаОсновании();
	ПараметрыПроверки.ЕстьОшибкиПроведен = Выборка.ЕстьОшибкиПроведен;
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОснованииСПараметрами(Выборка.Ссылка, ПараметрыПроверки);

	// Заполнение шапки и табличных частей
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	Товары.Загрузить(Выборка.Товары.Выгрузить());
	Серии.Загрузить(Выборка.Серии.Выгрузить());
	
	НазначениеДляЗаполнения = НаправленияДеятельностиСервер.ТолкающееНазначение(НаправлениеДеятельности);
	Для Каждого Строка Из Товары Цикл
		Если Строка.Назначение <> НазначениеДляЗаполнения Тогда
			Строка.Назначение = Справочники.Назначения.ПустаяСсылка();
		КонецЕсли;
	КонецЦикла;
	Для Каждого Строка Из Серии Цикл
		Если Строка.Назначение <> НазначениеДляЗаполнения Тогда
			Строка.Назначение = Справочники.Назначения.ПустаяСсылка();
		КонецЕсли;
	КонецЦикла;
	
	// Заполнение статусов указания серий
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.РеализацияТоваровУслуг);
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ЭтотОбъект, ПараметрыУказанияСерий);
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииАктаОРасхождениях(ДанныеЗаполнения)

	АктОРасхождениях = ДанныеЗаполнения.АктОРасхождениях;
	ОснованиеАкта = ДанныеЗаполнения.ОснованиеАкта;
	ВводитьИсправление = ДанныеЗаполнения.Исправление;
	Допоставка = ДанныеЗаполнения.Допоставка;
	
	ТекстЗапроса = "";
	
	// Ввод исправления доступен только в акте о расхождениях с основанием Отгрузка товаров клиенту
	Если ВводитьИсправление Тогда

		ИсправлениеДокументов.ЗаполнитьИсправление(ЭтотОбъект, ОснованиеАкта);
	
	ИначеЕсли Допоставка Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОтгрузкаТовары.Ссылка.Партнер                               КАК Партнер,
		|	ОтгрузкаТовары.Ссылка.Контрагент                            КАК Контрагент,
		|	ОтгрузкаТовары.Ссылка.БанковскийСчетКонтрагента             КАК БанковскийСчетКонтрагента,
		|	ОтгрузкаТовары.Ссылка.Соглашение                            КАК Соглашение,
		|	ОтгрузкаТовары.Ссылка.ХозяйственнаяОперация                 КАК ХозяйственнаяОперация,
		|	ОтгрузкаТовары.Ссылка.Организация                           КАК Организация,
		|	ОтгрузкаТовары.Ссылка.БанковскийСчетОрганизации             КАК БанковскийСчетОрганизации,
		|	ОтгрузкаТовары.Ссылка.Договор                               КАК Договор,
		|	ОтгрузкаТовары.Ссылка.Склад                                 КАК Склад,
		|	ОтгрузкаТовары.Ссылка.Валюта                                КАК Валюта,
		|	ОтгрузкаТовары.Ссылка.СпособДоставки                        КАК СпособДоставки,
		|	ОтгрузкаТовары.Ссылка.ПеревозчикПартнер                     КАК ПеревозчикПартнер,
		|	ОтгрузкаТовары.Ссылка.АдресДоставки                         КАК АдресДоставки,
		|	ОтгрузкаТовары.Ссылка.АдресДоставкиЗначенияПолей            КАК АдресДоставкиЗначенияПолей,
		|	ОтгрузкаТовары.Ссылка.АдресДоставкиПеревозчика              КАК АдресДоставкиПеревозчика,
		|	ОтгрузкаТовары.Ссылка.АдресДоставкиПеревозчикаЗначенияПолей КАК АдресДоставкиПеревозчикаЗначенияПолей,
		|	ОтгрузкаТовары.Ссылка.ЗонаДоставки                          КАК ЗонаДоставки,
		|	ОтгрузкаТовары.Ссылка.ВремяДоставкиС                        КАК ВремяДоставкиС,
		|	ОтгрузкаТовары.Ссылка.ВремяДоставкиПо                       КАК ВремяДоставкиПо,
		|	ОтгрузкаТовары.Ссылка.ОсобыеУсловияПеревозки                КАК ОсобыеУсловияПеревозки,
		|	ОтгрузкаТовары.Ссылка.ОсобыеУсловияПеревозкиОписание        КАК ОсобыеУсловияПеревозкиОписание,
		|	ОтгрузкаТовары.Ссылка.Сделка                                КАК Сделка,
		|	ОтгрузкаТовары.Ссылка.КонтактноеЛицо                        КАК КонтактноеЛицо,
		|	ОтгрузкаТовары.Ссылка.Подразделение                         КАК Подразделение,
		|	ОтгрузкаТовары.Ссылка.НаправлениеДеятельности               КАК НаправлениеДеятельности,
		|	ОтгрузкаТовары.Ссылка.ДопоставкаПоПередаче                  КАК ДопоставкаПоПередаче,
		|	ОтгрузкаТовары.Ссылка.СуммаДокумента                        КАК СуммаДокумента,
		|	ОтгрузкаТовары.Ссылка.Грузоотправитель                      КАК Грузоотправитель,
		|	ОтгрузкаТовары.Ссылка.БанковскийСчетГрузоотправителя        КАК БанковскийСчетГрузоотправителя,
		|	ОтгрузкаТовары.Ссылка.Грузополучатель                       КАК Грузополучатель,
		|	ОтгрузкаТовары.Ссылка.БанковскийСчетГрузополучателя         КАК БанковскийСчетГрузополучателя,
		|	ОтгрузкаТовары.Ссылка.Руководитель                          КАК Руководитель,
		|	ОтгрузкаТовары.Ссылка.ГлавныйБухгалтер                      КАК ГлавныйБухгалтер,
		|	ОтгрузкаТовары.Ссылка.Отпустил                              КАК Отпустил,
		|	ОтгрузкаТовары.Ссылка.ОтпустилДолжность                     КАК ОтпустилДолжность,
		|	ОтгрузкаТовары.Ссылка.КоэффициентПересчетаРегл              КАК КоэффициентПересчетаРегл,
		|	ОтгрузкаТовары.Ссылка.ДоверенностьВыдана                    КАК ДоверенностьВыдана,
		|	ОтгрузкаТовары.Ссылка.ДоверенностьДата                      КАК ДоверенностьДата,
		|	ОтгрузкаТовары.Ссылка.ДоверенностьЛицо                      КАК ДоверенностьЛицо,
		|	ОтгрузкаТовары.Ссылка.ДоверенностьНомер                     КАК ДоверенностьНомер,
		|	ОтгрузкаТовары.Ссылка.АдресДоставкиЗначение                 КАК АдресДоставкиЗначение,
		|	ОтгрузкаТовары.Ссылка.АдресДоставкиПеревозчикаЗначение      КАК АдресДоставкиПеревозчикаЗначение,
		|	ОтгрузкаТовары.Ссылка.НалогообложениеНДС                    КАК НалогообложениеНДС
		|ИЗ
		|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ОтгрузкаТовары
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ  Документ.АктОРасхожденияхПослеОтгрузки.Товары КАК АктТовары
		|		ПО ОтгрузкаТовары.Номенклатура = АктТовары.Номенклатура
		|			И ОтгрузкаТовары.Характеристика = АктТовары.Характеристика
		|			И ОтгрузкаТовары.КодСтроки = АктТовары.КодСтроки
		|			И (ОтгрузкаТовары.ЗаказКлиента = АктТовары.ЗаказКлиента
		|				ИЛИ ОтгрузкаТовары.Ссылка = АктТовары.ЗаказКлиента
		|				ИЛИ ОтгрузкаТовары.Ссылка.ИсправляемыйДокумент = АктТовары.ЗаказКлиента)
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
		|		ПО ОтгрузкаТовары.Ссылка = РеестрДокументов.СторнируемыйДокумент
		|			И РеестрДокументов.Проведен
		|			И НЕ РеестрДокументов.ДополнительнаяЗапись
		|ГДЕ
		|	АктТовары.Ссылка = &АктОРасхождениях
		|	И АктТовары.Ссылка.ТипОснованияАктаОРасхождении = ЗНАЧЕНИЕ(Перечисление.ТипыОснованияАктаОРасхождении.РеализацияТоваровУслуг)
		|	И АктТовары.Действие В(
		|	          ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка),
		|	          ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяСписаниеДопоставка)
		|	          )
		|	И РеестрДокументов.Ссылка ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОтгрузкаТоваровКлиенту.Партнер                               КАК Партнер,
		|	ОтгрузкаТоваровКлиенту.Контрагент                            КАК Контрагент,
		|	ОтгрузкаТоваровКлиенту.БанковскийСчетКонтрагента             КАК БанковскийСчетКонтрагента,
		|	ОтгрузкаТоваровКлиенту.Соглашение                            КАК Соглашение,
		|	ОтгрузкаТоваровКлиенту.ХозяйственнаяОперация                 КАК ХозяйственнаяОперация,
		|	ОтгрузкаТоваровКлиенту.Организация                           КАК Организация,
		|	ОтгрузкаТоваровКлиенту.БанковскийСчетОрганизации             КАК БанковскийСчетОрганизации,
		|	ОтгрузкаТоваровКлиенту.Договор                               КАК Договор,
		|	ОтгрузкаТоваровКлиенту.Склад                                 КАК Склад,
		|	ОтгрузкаТоваровКлиенту.Валюта                                КАК Валюта,
		|	ОтгрузкаТоваровКлиенту.СпособДоставки                        КАК СпособДоставки,
		|	ОтгрузкаТоваровКлиенту.ПеревозчикПартнер                     КАК ПеревозчикПартнер,
		|	ОтгрузкаТоваровКлиенту.АдресДоставки                         КАК АдресДоставки,
		|	ОтгрузкаТоваровКлиенту.АдресДоставкиЗначенияПолей            КАК АдресДоставкиЗначенияПолей,
		|	ОтгрузкаТоваровКлиенту.АдресДоставкиПеревозчика              КАК АдресДоставкиПеревозчика,
		|	ОтгрузкаТоваровКлиенту.АдресДоставкиПеревозчикаЗначенияПолей КАК АдресДоставкиПеревозчикаЗначенияПолей,
		|	ОтгрузкаТоваровКлиенту.ЗонаДоставки                          КАК ЗонаДоставки,
		|	ОтгрузкаТоваровКлиенту.ВремяДоставкиС                        КАК ВремяДоставкиС,
		|	ОтгрузкаТоваровКлиенту.ВремяДоставкиПо                       КАК ВремяДоставкиПо,
		|	ОтгрузкаТоваровКлиенту.ОсобыеУсловияПеревозки                КАК ОсобыеУсловияПеревозки,
		|	ОтгрузкаТоваровКлиенту.ОсобыеУсловияПеревозкиОписание        КАК ОсобыеУсловияПеревозкиОписание,
		|	ОтгрузкаТоваровКлиенту.Сделка                                КАК Сделка,
		|	ОтгрузкаТоваровКлиенту.КонтактноеЛицо                        КАК КонтактноеЛицо,
		|	ОтгрузкаТоваровКлиенту.Подразделение                         КАК Подразделение,
		|	ОтгрузкаТоваровКлиенту.НаправлениеДеятельности               КАК НаправлениеДеятельности,
		|	ОтгрузкаТоваровКлиенту.ДопоставкаПоПередаче                  КАК ДопоставкаПоПередаче,
		|	ОтгрузкаТоваровКлиенту.СуммаДокумента                        КАК СуммаДокумента,
		|	ОтгрузкаТоваровКлиенту.Грузоотправитель                      КАК Грузоотправитель,
		|	ОтгрузкаТоваровКлиенту.БанковскийСчетГрузоотправителя        КАК БанковскийСчетГрузоотправителя,
		|	ОтгрузкаТоваровКлиенту.Грузополучатель                       КАК Грузополучатель,
		|	ОтгрузкаТоваровКлиенту.БанковскийСчетГрузополучателя         КАК БанковскийСчетГрузополучателя,
		|	ОтгрузкаТоваровКлиенту.Руководитель                          КАК Руководитель,
		|	ОтгрузкаТоваровКлиенту.ГлавныйБухгалтер                      КАК ГлавныйБухгалтер,
		|	ОтгрузкаТоваровКлиенту.Отпустил                              КАК Отпустил,
		|	ОтгрузкаТоваровКлиенту.ОтпустилДолжность                     КАК ОтпустилДолжность,
		|	ОтгрузкаТоваровКлиенту.КоэффициентПересчетаРегл              КАК КоэффициентПересчетаРегл,
		|	ОтгрузкаТоваровКлиенту.ДоверенностьВыдана                    КАК ДоверенностьВыдана,
		|	ОтгрузкаТоваровКлиенту.ДоверенностьДата                      КАК ДоверенностьДата,
		|	ОтгрузкаТоваровКлиенту.ДоверенностьЛицо                      КАК ДоверенностьЛицо,
		|	ОтгрузкаТоваровКлиенту.ДоверенностьНомер                     КАК ДоверенностьНомер,
		|	ОтгрузкаТоваровКлиенту.АдресДоставкиЗначение                 КАК АдресДоставкиЗначение,
		|	ОтгрузкаТоваровКлиенту.АдресДоставкиПеревозчикаЗначение      КАК АдресДоставкиПеревозчикаЗначение,
		|	ОтгрузкаТоваровКлиенту.НалогообложениеНДС                    КАК НалогообложениеНДС
		|ИЗ
		|	Документ.ОтгрузкаТоваровКлиенту КАК ОтгрузкаТоваровКлиенту
		|ГДЕ
		|	ОтгрузкаТоваровКлиенту.Ссылка = &ОснованиеАктаОРасхождениях
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	АктОРасхожденияхТовары.Номенклатура   КАК Номенклатура,
	|	АктОРасхожденияхТовары.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА &Допоставка
	|			И АктОРасхожденияхТовары.Ссылка.ТипОснованияАктаОРасхождении = ЗНАЧЕНИЕ(Перечисление.ТипыОснованияАктаОРасхождении.РеализацияТоваровУслуг)
	|			И АктОРасхожденияхТовары.Ссылка.СпособОтраженияРасхождений В (
	|					ЗНАЧЕНИЕ(Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ОформлениеКорректировкиКакИсправлениеПервичныхДокументов),
	|					ЗНАЧЕНИЕ(Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ОформлениеКорректировкиПоСогласованию)
	|					)
	|			ТОГДА 0
	|		ИНАЧЕ
	|			АктОРасхожденияхТовары.КодСтроки
	|	КОНЕЦ                                 КАК КодСтроки,
	|	АктОРасхожденияхТовары.Склад          КАК Склад,
	|	АктОРасхожденияхТовары.Назначение     КАК Назначение,
	|	ВЫБОР
	|		КОГДА &Допоставка
	|			ТОГДА АктОРасхожденияхТовары.КоличествоПоДокументу - АктОРасхожденияхТовары.Количество
	|		ИНАЧЕ
	|			АктОРасхожденияхТовары.Количество
	|	КОНЕЦ                                 КАК Количество,
	|	ВЫБОР
	|		КОГДА АктОРасхожденияхТовары.Ссылка.ЦенаВключаетНДС
	|			ТОГДА АктОРасхожденияхТовары.Цена
	|		КОГДА АктОРасхожденияхТовары.КоличествоУпаковок <> 0
	|			И АктОРасхожденияхТовары.Реализация ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА АктОРасхожденияхТовары.Сумма / АктОРасхожденияхТовары.КоличествоУпаковок
	|		КОГДА АктОРасхожденияхТовары.КоличествоУпаковок <> 0
	|			ТОГДА АктОРасхожденияхТовары.СуммаСНДС / АктОРасхожденияхТовары.КоличествоУпаковок
	|		КОГДА АктОРасхожденияхТовары.КоличествоУпаковокПоДокументу <> 0
	|			И АктОРасхожденияхТовары.Реализация ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА АктОРасхожденияхТовары.СуммаПоДокументу / АктОРасхожденияхТовары.КоличествоУпаковокПоДокументу
	|		КОГДА АктОРасхожденияхТовары.КоличествоУпаковокПоДокументу <> 0
	|			ТОГДА АктОРасхожденияхТовары.СуммаСНДСПоДокументу / АктОРасхожденияхТовары.КоличествоУпаковокПоДокументу
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                 КАК Цена,
	|	АктОРасхожденияхТовары.Серия          КАК Серия,
	|	ВЫБОР
	|		КОГДА АктОРасхожденияхТовары.ЗаказКлиента ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ
	|			АктОРасхожденияхТовары.ЗаказКлиента
	|	КОНЕЦ                                 КАК ЗаказКлиента,
	|	АктОРасхожденияхТовары.Упаковка       КАК Упаковка,
	|	АктОРасхожденияхТовары.ВидЦены        КАК ВидЦены
	|ИЗ
	|	Документ.АктОРасхожденияхПослеОтгрузки.Товары КАК АктОРасхожденияхТовары
	|ГДЕ
	|	АктОРасхожденияхТовары.Ссылка = &АктОРасхождениях
	|	И АктОРасхожденияхТовары.Реализация = &ОснованиеАктаОРасхождениях
	|	И ВЫБОР
	|		КОГДА &Допоставка
	|			ТОГДА АктОРасхожденияхТовары.Действие В (
	|			        ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка),
	|			        ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяСписаниеДопоставка)
	|			        )
	|		ИНАЧЕ
	|			АктОРасхожденияхТовары.Действие В (
	|			        ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется),
	|			        ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка),
	|			        ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного),
	|			        ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного)
	|			        )
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	АктОРасхожденияхТовары.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктОРасхожденияхСерии.Номенклатура       КАК Номенклатура,
	|	АктОРасхожденияхСерии.Характеристика     КАК Характеристика,
	|	АктОРасхожденияхСерии.Склад              КАК Склад,
	|	АктОРасхожденияхСерии.Назначение         КАК Назначение,
	|	ВЫБОР
	|		КОГДА &Допоставка
	|			ТОГДА АктОРасхожденияхСерии.КоличествоПоДокументу - АктОРасхожденияхСерии.Количество
	|		ИНАЧЕ
	|			АктОРасхожденияхСерии.Количество
	|	КОНЕЦ                                    КАК Количество,
	|	АктОРасхожденияхСерии.Серия              КАК Серия
	|ИЗ
	|	Документ.АктОРасхожденияхПослеОтгрузки.Серии КАК АктОРасхожденияхСерии
	|ГДЕ
	|	АктОРасхожденияхСерии.Ссылка = &АктОРасхождениях
	|	И АктОРасхожденияхСерии.Реализация = &ОснованиеАктаОРасхождениях
	|	И ВЫБОР
	|		КОГДА &Допоставка
	|			ТОГДА АктОРасхожденияхСерии.Действие В (
	|			        ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка),
	|			        ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяСписаниеДопоставка)
	|			        )
	|		ИНАЧЕ
	|			АктОРасхожденияхСерии.Действие В (
	|			        ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется),
	|			        ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка),
	|			        ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного),
	|			        ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного)
	|			        )
	|	КОНЕЦ
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Допоставка", Допоставка);
	Запрос.УстановитьПараметр("АктОРасхождениях", АктОРасхождениях);
	Запрос.УстановитьПараметр("ОснованиеАктаОРасхождениях", ОснованиеАкта);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	Если ВводитьИсправление Тогда
		
		ТаблицаТоварыАкта = РезультатыЗапроса[РезультатыЗапроса.ВГраница()-1].Выгрузить();
		ТаблицаСерииАкта = РезультатыЗапроса[РезультатыЗапроса.ВГраница()].Выгрузить();
		
		ОтгрузкаТовары = Товары.Выгрузить();
		ОтгрузкаСерии = Серии.Выгрузить();
		
		ПоляПоиска = "КодСтроки, " + Документы.ОтгрузкаТоваровКлиенту.КлючевыеПоляТаблицыТоваровРаспоряжения();
		
		Для Каждого СтрокаТаблицы Из ТаблицаТоварыАкта Цикл 
			
			ПараметрыПоиска = Новый Структура(ПоляПоиска);
			ЗаполнитьЗначенияСвойств(ПараметрыПоиска, СтрокаТаблицы);
			
			НайденныеСтроки = ОтгрузкаТовары.НайтиСтроки(ПараметрыПоиска);
			
			Если НайденныеСтроки.Количество() > 0 Тогда 
				
				Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
					НайденнаяСтрока.Количество = СтрокаТаблицы.Количество;
				КонецЦикла;
				
			Иначе // для добавления строк из акта сверх распоряжения
				
				НоваяСтрока = ОтгрузкаТовары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				НоваяСтрока.Количество = СтрокаТаблицы.Количество;
				
			КонецЕсли;
		
		КонецЦикла;
		
		ПоляПоиска = "Номенклатура, Характеристика, Назначение, Серия, Склад";
		
		Для Каждого СтрокаТаблицы Из ТаблицаСерииАкта Цикл 
			
			ПараметрыПоиска = Новый Структура(ПоляПоиска);
			ЗаполнитьЗначенияСвойств(ПараметрыПоиска, СтрокаТаблицы);
			
			НайденныеСтроки = ОтгрузкаСерии.НайтиСтроки(ПараметрыПоиска);
			
			Если НайденныеСтроки.Количество() > 0 Тогда 
				
				Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
					
					Если НайденнаяСтрока.Количество > СтрокаТаблицы.Количество Тогда
						
						НайденнаяСтрока.Количество = СтрокаТаблицы.Количество;
						
					Иначе
						
						НоваяСтрока = ОтгрузкаСерии.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяСтрока);
						НоваяСтрока.Количество = СтрокаТаблицы.Количество - НайденнаяСтрока.Количество;
						
					КонецЕсли;
					
				КонецЦикла;
				
			Иначе // для добавления строк из акта сверх распоряжения
				
				НоваяСтрока = ОтгрузкаСерии.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				НоваяСтрока.Количество = СтрокаТаблицы.Количество;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Товары.Загрузить(ОтгрузкаТовары);
		Серии.Загрузить(ОтгрузкаСерии);
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		
		ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьТЧ(
			Товары,
			СтруктураДействий);
		
		НакладныеСервер.УдалитьПустыеСтроки(Товары, "Количество");
		
		ОснованиеАктаОРасхожденияхДляИсправления = ОснованиеАкта;
		
	ИначеЕсли Допоставка Тогда
	
		ВыборкаШапка = РезультатыЗапроса[РезультатыЗапроса.ВГраница()-2].Выбрать();
		ТаблицаТоварыАкта = РезультатыЗапроса[РезультатыЗапроса.ВГраница()-1].Выгрузить();
		
		ВыборкаШапка.Следующий();
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
		
		Для Каждого СтрокаТовары Из ТаблицаТоварыАкта Цикл
			
			Если ЗначениеЗаполнено(СтрокаТовары.ЗаказКлиента) Тогда
				ОтгрузкаПоЗаказам = Истина;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Товары.Загрузить(ТаблицаТоварыАкта);
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		
		ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьТЧ(
			Товары,
			СтруктураДействий);
		
		НакладныеСервер.УдалитьПустыеСтроки(Товары, "Количество");
		
		ОснованиеАктаОРасхождениях = ОснованиеАкта;
		
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект,
			Документы.ОтгрузкаТоваровКлиенту);
		
		НоменклатураСервер.ЗаполнитьСерииПоFEFO(ЭтотОбъект, ПараметрыУказанияСерий, Ложь);
		
	КонецЕсли;
	
	СтруктураОснование = Документы.ОтгрузкаТоваровКлиенту.СтруктураОснованияДляПечати(ЭтотОбъект);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураОснование);
	
КонецПроцедуры

Процедура ИнициализироватьДокументПередЗаполнением()
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
КонецПроцедуры

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Или НЕ ДанныеЗаполнения.Свойство("Организация") Тогда
		Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	КонецЕсли;
	
	ВалютаРегл  = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Или НЕ ДанныеЗаполнения.Свойство("Валюта") Тогда
		Если Не ЗначениеЗаполнено(Валюта) Тогда
			Валюта = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
		КонецЕсли;
	КонецЕсли;
	Автор         = Пользователи.ТекущийПользователь();
	Подразделение = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Менеджер, Подразделение);
	
	Если Не Исправление Тогда
		ПараметрыЗаполнения = Документы.ОтгрузкаТоваровКлиенту.ПараметрыЗаполненияНалогообложенияНДСПродажи(ЭтотОбъект);
		УчетНДСУП.ЗаполнитьНалогообложениеНДСПродажи(НалогообложениеНДС, ПараметрыЗаполнения);
	КонецЕсли;
	
	ИспользоватьСкладыВТабличнойЧасти = ПолучитьФункциональнуюОпцию("ИспользоватьСкладыВТабличнойЧастиДокументовПродажи");
	
	СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
	СтруктураПараметров.Организация    = Организация;
	СтруктураПараметров.БанковскийСчет = БанковскийСчетОрганизации;
	
	БанковскийСчетОрганизации = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);
	БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(Контрагент,
																									Неопределено,
																									БанковскийСчетКонтрагента);
	
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Или НЕ ДанныеЗаполнения.Свойство("Склад") Тогда
		Склад = ЗначениеНастроекПовтИсп.ПолучитьСкладПоУмолчанию(Склад, ИспользоватьСкладыВТабличнойЧасти, Истина);
	КонецЕсли;
	
	СтруктураОтветственного = ПродажиСервер.ПолучитьОтветственногоПоСкладу(Склад);
	
	Если СтруктураОтветственного <> Неопределено Тогда
		Отпустил = СтруктураОтветственного.Ответственный;
		ОтпустилДолжность = СтруктураОтветственного.ОтветственныйДолжность;
	КонецЕсли;
	
	КоэффициентПересчетаРегл = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(Валюта,
																								ВалютаРегл,
																								ТекущаяДатаСеанса());
	
	СкладГруппа = Справочники.Склады.ЭтоГруппаИСкладыИспользуютсяВТЧДокументовПродажи(Склад);
	СкладыСервер.ЗаполнитьСкладыВТабличнойЧасти(Склад, СкладГруппа, Товары, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ВидыЗапасов

Процедура ЗаполнитьВидыЗапасов(Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерВременныхТаблиц  = ВременныеТаблицыДанныхДокумента();
	ПерезаполнитьВидыЗапасов = ЗапасыСервер.ПроверитьНеобходимостьПерезаполненияВидовЗапасовДокумента(ЭтотОбъект);
	
	Если Не Проведен
		Или ПерезаполнитьВидыЗапасов
		Или ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц)
		Или ПроверитьИзменениеТоваров(МенеджерВременныхТаблиц) Тогда
		
		ПараметрыЗаполнения = ПараметрыЗаполненияВидовЗапасов();
		
		ЗапасыСервер.ЗаполнитьВидыЗапасовПоТоварамОрганизаций(ЭтотОбъект,
																МенеджерВременныхТаблиц,
																Отказ,
																ПараметрыЗаполнения);
		
		ВидыЗапасов.Свернуть("АналитикаУчетаНоменклатуры, ВидЗапасов, ЗаказКлиента, НомерГТД, КодТНВЭД",
							"Количество, КоличествоУпаковок, КоличествоПоРНПТ, Сумма");
		
		Если Не Отказ Тогда
			ЗаполнитьДопКолонкиВидовЗапасов();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц)
	
	ИменаРеквизитов = "Дата, Организация, НалогообложениеНДС, Партнер, Договор";
	
	Возврат ЗапасыСервер.ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц,
																Ссылка,
																ИменаРеквизитов);
	
КонецФункции

Функция ПроверитьИзменениеТоваров(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТаблицаТоваров.Количество КАК Количество,
	|		ТаблицаТоваров.СуммаВознаграждения КАК СуммаВознаграждения
	|	ИЗ
	|		ТаблицаТоваров КАК ТаблицаТоваров
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		-ТаблицаВидыЗапасов.Количество КАК Количество,
	|		-ТаблицаВидыЗапасов.СуммаВознаграждения КАК СуммаВознаграждения
	|	ИЗ
	|		ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|	
	|	) КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаТоваров.Количество) <> 0
	|	ИЛИ СУММА(ТаблицаТоваров.СуммаВознаграждения) <> 0";

	РезультатЗапрос = Запрос.Выполнить();
	
	Возврат Не РезультатЗапрос.Пустой();
	
КонецФункции

Функция ПараметрыЗаполненияВидовЗапасов()
	
	ПараметрыЗаполнения = ЗапасыСервер.ПараметрыЗаполненияВидовЗапасов();
	ПараметрыЗаполнения.СторнируемыйДокумент = СторнируемыйДокумент;
	ПараметрыЗаполнения.ПодбиратьВТЧТоварыПринятыеНаОтветственноеХранение.Вставить(
		Перечисления.ТипыЗапасов.ТоварНаХраненииСПравомПродажи,
		"Никогда");
	
	ОтборыВидовЗапасов = ПараметрыЗаполнения.ОтборыВидовЗапасов;
	ОтборыВидовЗапасов.НалогообложениеНДС = НалогообложениеНДС;
	
	УчетНДСУП.ПараметрыЗаполненияВидовЗапасовПоНалогообложению(ОтборыВидовЗапасов,
																Организация,
																Дата,
																НалогообложениеНДС);
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

Процедура ЗаполнитьДопКолонкиВидовЗапасов()
	
	КолонкиГруппировки =
		"АналитикаУчетаНоменклатуры,
		|АналитикаУчетаНоменклатурыОтгрузки,
		|АналитикаУчетаНаборов,
		|НоменклатураНабора,
		|ХарактеристикаНабора,
		|Упаковка,
		|Цена,
		|ЗаказКлиента,
		|КодСтроки,
		|КодТНВЭД";
	
	КолонкиСуммирования = "Количество, КоличествоУпаковок, Сумма";
	
	ВыгружаемыеКолонки = КолонкиГруппировки + ", " + КолонкиСуммирования;
	
	ТаблицаТовары = Товары.Выгрузить(, ВыгружаемыеКолонки);
	ТаблицаТовары.Свернуть(КолонкиГруппировки, КолонкиСуммирования);
	
	Параметры       = Новый Структура("Количество, КоличествоУпаковок, Сумма");
	СтруктураПоиска = Новый Структура("АналитикаУчетаНоменклатуры, ЗаказКлиента, КоличествоУпаковок");
	
	Для Каждого СтрокаТоваров Из ТаблицаТовары Цикл
		ЗаполнитьЗначенияСвойств(Параметры, СтрокаТоваров);
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТоваров);
		
		СтруктураПоиска.КоличествоУпаковок = 0;
		
		ЗаполнитьСтрокуВидовЗапасов(СтрокаТоваров, СтруктураПоиска, Параметры);
		
		Если Параметры.Количество <> 0
			И ЗначениеЗаполнено(СтрокаТоваров.ЗаказКлиента) Тогда
			
			СтруктураПоиска.ЗаказКлиента = Неопределено;
			
			ЗаполнитьСтрокуВидовЗапасов(СтрокаТоваров, СтруктураПоиска, Параметры);
			
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыОтбора = Новый Структура("Количество", 0);
	УдаляемыеСтроки = ВидыЗапасов.НайтиСтроки(ПараметрыОтбора);
	
	Для Каждого СтрокаТаблицы Из УдаляемыеСтроки Цикл
		ВидыЗапасов.Удалить(СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокуВидовЗапасов(СтрокаТоваров, СтруктураПоиска, Параметры)
	
	Для Каждого СтрокаЗапасов Из ВидыЗапасов.НайтиСтроки(СтруктураПоиска) Цикл
		
		Если СтрокаЗапасов.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		КоличествоПоСтроке = Мин(Параметры.Количество, СтрокаЗапасов.Количество);
		
		НоваяСтрока = ВидыЗапасов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗапасов);
		
		НоваяСтрока.КодТНВЭД = СтрокаТоваров.КодТНВЭД;
		НоваяСтрока.КодСтроки = СтрокаТоваров.КодСтроки;
		НоваяСтрока.АналитикаУчетаНоменклатурыОтгрузки = СтрокаТоваров.АналитикаУчетаНоменклатурыОтгрузки;
		НоваяСтрока.АналитикаУчетаНаборов = СтрокаТоваров.АналитикаУчетаНаборов;
		НоваяСтрока.Упаковка			= СтрокаТоваров.Упаковка;
		НоваяСтрока.Количество			= КоличествоПоСтроке;
		НоваяСтрока.КоличествоПоРНПТ	= КоличествоПоСтроке * СтрокаЗапасов.КоличествоПоРНПТ / СтрокаЗапасов.Количество;
		НоваяСтрока.Цена				= СтрокаТоваров.Цена;
		НоваяСтрока.ЗаказКлиента		= ?(ЗначениеЗаполнено(СтрокаТоваров.ЗаказКлиента),
											СтрокаТоваров.ЗаказКлиента,
											Неопределено);
		
		Если Параметры.Количество <> 0 Тогда
			НоваяСтрока.КоличествоУпаковок = КоличествоПоСтроке * Параметры.КоличествоУпаковок / Параметры.Количество;
			
			НоваяСтрока.Сумма = КоличествоПоСтроке * Параметры.Сумма / Параметры.Количество;
		КонецЕсли;
		
		СтрокаЗапасов.Количество		= СтрокаЗапасов.Количество - НоваяСтрока.Количество;
		СтрокаЗапасов.КоличествоПоРНПТ	= СтрокаЗапасов.КоличествоПоРНПТ - НоваяСтрока.КоличествоПоРНПТ;
		
		СтрокаЗапасов.Сумма = СтрокаЗапасов.Сумма - НоваяСтрока.Сумма;
		
		Параметры.Количество			= Параметры.Количество - НоваяСтрока.Количество;
		Параметры.КоличествоУпаковок	= Параметры.КоличествоУпаковок - НоваяСтрока.КоличествоУпаковок;
		
		Параметры.Сумма = Параметры.Сумма - НоваяСтрока.Сумма;
		
		Если Параметры.Количество = 0 Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет аналитики учета номенклатуры в табличных частях документа, хранящих информацию о товарах.
// Если параметр не передан, тогда будет выполнено заполнение данных в табличных частях документа.
//
// Параметры:
//	ТаблицыДокумента - см. Документы.ОтгрузкаТоваровКлиенту.КоллекцияТабличныхЧастейТоваров.
//
Процедура ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров(ТаблицыДокумента = Неопределено)
	
	Если ТаблицыДокумента = Неопределено Тогда
		ТаблицыДокумента = Документы.ОтгрузкаТоваровКлиенту.КоллекцияТабличныхЧастейТоваров();
		
		ЗаполнитьЗначенияСвойств(ТаблицыДокумента, ЭтотОбъект);
	КонецЕсли;
	
	ТаблицаТовары = ТаблицыДокумента.Товары;
	
	МестаУчета = РегистрыСведений.АналитикаУчетаНоменклатуры.МестаУчета(Неопределено,
																		Склад,
																		Подразделение,
																		Партнер,
																		Договор);
	
	ИменаПолей = РегистрыСведений.АналитикаУчетаНоменклатуры.ИменаПолейКоллекцииПоУмолчанию();
	ИменаПолей.СтатусУказанияСерий = "СтатусУказанияСерийНаСкладах";
	
	// Если Склад - группа, то для аналитики учета номенклатуры склад берем из ТЧ.
	РеквизитыСклада = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Склад, "ЭтоГруппа, ВыборГруппы");
	
	Если ЗначениеЗаполнено(Склад)
		И РеквизитыСклада.ЭтоГруппа
		И РеквизитыСклада.ВыборГруппы = Перечисления.ВыборГруппыСкладов.РазрешитьВЗаказахИНакладных Тогда
		
		ИменаПолей.Вставить("Произвольный", "Склад");
		
	КонецЕсли;
	
	РегистрыСведений.АналитикаУчетаНоменклатуры.ЗаполнитьВКоллекции(ТаблицаТовары,
																	МестаУчета,
																	ИменаПолей);
	
	МестаУчетаУПартнеров = РегистрыСведений.АналитикаУчетаНоменклатуры.МестаУчета(ХозяйственнаяОперация,
																					Договор,
																					Подразделение,
																					Партнер,
																					Договор);
	
	ИменаПолей.СтатусУказанияСерий        = "СтатусУказанияСерийОтгруженныхТоваров";
	ИменаПолей.АналитикаУчетаНоменклатуры = "АналитикаУчетаНоменклатурыОтгрузки";
	
	ИменаПолей.Вставить("Произвольный", "");
	
	РегистрыСведений.АналитикаУчетаНоменклатуры.ЗаполнитьВКоллекции(ТаблицаТовары,
																	МестаУчетаУПартнеров,
																	ИменаПолей);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ЗаполнитьЦеныПоУсловиямПродаж()
	
	ИменаПолей = "ВидЦены, Цена, СрокПоставки";
	
	ПараметрыЗаполнения = Новый Структура();
	ПараметрыЗаполнения.Вставить("ПоляЗаполнения",     ИменаПолей);
	ПараметрыЗаполнения.Вставить("Дата",               Дата);
	ПараметрыЗаполнения.Вставить("Организация",        Организация);
	ПараметрыЗаполнения.Вставить("Валюта",             Валюта);
	ПараметрыЗаполнения.Вставить("Соглашение",         Соглашение);
	ПараметрыЗаполнения.Вставить("РассчитыватьНаборы", Истина);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСумму",     "КоличествоУпаковок");
	
	ЦеныПредприятияЗаполнениеСервер.ЗаполнитьЦены(Товары, Неопределено, ПараметрыЗаполнения, СтруктураДействий);
	
КонецПроцедуры

// Возвращает сумму документа по табличной части Товары.
//
// Возвращаемое значение:
// 	Число.
//
Функция ПолучитьСуммуДокумента() Экспорт
	
	ТоварыДокумента = Товары.Выгрузить(, "Номенклатура, Сумма");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Товары", ТоварыДокумента);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТ.Номенклатура КАК Номенклатура,
	|	ВТ.Сумма КАК Сумма
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК ВТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(Товары.Сумма), 0) КАК Сумма
	|ИЗ
	|	Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблицаНоменклатуры
	|		ПО Товары.Номенклатура = ТаблицаНоменклатуры.Ссылка
	|		И ТаблицаНоменклатуры.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Сумма;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
