#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ДляВсехСтрок( ЗначениеРазрешено(Сотрудники.ФизическоеЛицо, NULL КАК ИСТИНА)
	|	) И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	ВоинскийУчет.ДляОтбораПоОрганизации(Настройки);
	ВоинскийУчет.ДляОбъектаСПрисоединеннымиФайлами(Настройки);
КонецПроцедуры

#КонецОбласти

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.ПервоначальнаяПостановкаНаВоинскийУчет;
	ОписаниеСоставаОбъекта = ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	ОписаниеСоставаОбъекта.ЗаполнятьФизическиеЛицаПоСотрудникам = Ложь;
	Возврат ОписаниеСоставаОбъекта;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Идентификатор = "ПФ_MXL_СписокПервоначальнойПостановкиНаВоинскийУчет";
	КомандаПечати.Представление = НСтр("ru = 'Список первоначальной постановки на воинский учет';
										|en = 'Список первоначальной постановки на воинский учет'");
	КомандаПечати.Порядок = 10;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	
КонецПроцедуры

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ОшибкиПечати          - Список значений  - Ошибки печати  (значение - ссылка на объект, представление - текст
//                           ошибки).
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя
//                           области в которой был выведен объект).
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_СписокПервоначальнойПостановкиНаВоинскийУчет") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ПФ_MXL_СписокПервоначальнойПостановкиНаВоинскийУчет", 
			НСтр("ru = 'Список первоначальной постановки на воинский учет';
				|en = 'Список первоначальной постановки на воинский учет'"), 
			ПечатнаяФормаПервоначальнойПостановкиНаВоинскийУчет(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;					
						
КонецПроцедуры								

Функция ПечатнаяФормаПервоначальнойПостановкиНаВоинскийУчет(МассивОбъектов, ОбъектыПечати)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПервоначальнаяПостановкаНаВоинскийУчет";
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ВыборкаПоШапкеДокумента = ЗапросПоШапкеДляПечати(МассивОбъектов).Выбрать();
	ВыборкаПоРаботникам = ЗапросПоСотрудникамДляПечати(МассивОбъектов).Выбрать();
	
	Пока ВыборкаПоШапкеДокумента.СледующийПоЗначениюПоля("Ссылка") Цикл 
		
		Макет 		  = УправлениеПечатью.МакетПечатнойФормы("Отчет.ВоинскийУчетОбщий.ПФ_MXL_СписокПервоначальнойПостановкиНаВоинскийУчет2021");
		Заголовок 	  = Макет.ПолучитьОбласть("Заголовок");
		Шапка 		  = Макет.ПолучитьОбласть("Шапка");
		СтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
		Подвал 		  = Макет.ПолучитьОбласть("Подвал");
		
		ВыборкаПоРаботникам.Сбросить();
		
		Если ВыборкаПоРаботникам.НайтиСледующий(Новый Структура("Ссылка", ВыборкаПоШапкеДокумента.Ссылка)) Тогда
			
			ВыборкаПоРаботникам.СледующийПоЗначениюПоля("Ссылка");
			
			Пока ВыборкаПоРаботникам.СледующийПоЗначениюПоля("Военкомат") Цикл 
		
				НачалоФормы = ДокументРезультат.ВысотаТаблицы + 1;
				
				ЗаполнитьЗначенияСвойств(Заголовок.Параметры, ВыборкаПоРаботникам);
				ЗаполнитьЗначенияСвойств(Заголовок.Параметры, ВыборкаПоШапкеДокумента);
				
				Заголовок.Параметры.ГодРождения = СтрЗаменить(ВыборкаПоШапкеДокумента.ГодПостановкиНаУчет - 17, Символы.НПП, "");
				Заголовок.Параметры.ГодПостановкиНаУчет = СтрЗаменить(ВыборкаПоШапкеДокумента.ГодПостановкиНаУчет, Символы.НПП, "");
				
				ДокументРезультат.Вывести(Заголовок);
				ДокументРезультат.Вывести(Шапка);
				
				НомерСтроки = 1;
				ДанныеАнкет = Новый Массив;
				
				Пока ВыборкаПоРаботникам.Следующий() Цикл
					
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы.Параметры, ВыборкаПоРаботникам);
					СтрокаТаблицы.Параметры.ДатаРожденияПредставление = Формат(ВыборкаПоРаботникам.ДатаРождения, "ДЛФ=Д");
					СтрокаТаблицы.Параметры.ДокументСерия = ВыборкаПоРаботникам.СерияПаспорта;
					СтрокаТаблицы.Параметры.ДокументНомер = ВыборкаПоРаботникам.НомерПаспорта;
					СтрокаТаблицы.Параметры.ТелефонДомашнийПредставление = ВыборкаПоРаботникам.НомерТелефона;
					СтрокаТаблицы.Параметры.СистемныеПоляНомерПоПорядкуВГруппировке = НомерСтроки;
					
					СтрокаТаблицы.Параметры.АдресПоПропискеПредставление = ВыборкаПоРаботникам.АдресРегистрацииПоМестуЖительства;
					СтрокаТаблицы.Параметры.АдресМестаПроживанияПредставление = ВыборкаПоРаботникам.АдресРегистрацииПоМестуПребывания;
					СтрокаТаблицы.Параметры.АдресДляИнформированияПредставление = ВыборкаПоРаботникам.АдресФактическогоПроживания;
					
					Если ВыборкаПоРаботникам.АдресРегистрацииПоМестуЖительства = ВыборкаПоРаботникам.АдресРегистрацииПоМестуПребывания Тогда
						СтрокаТаблицы.Параметры.АдресМестаПроживанияПредставление = "";
					КонецЕсли;
					
					Если ВыборкаПоРаботникам.АдресФактическогоПроживания = ВыборкаПоРаботникам.АдресРегистрацииПоМестуЖительства
						Или ВыборкаПоРаботникам.АдресФактическогоПроживания = ВыборкаПоРаботникам.АдресРегистрацииПоМестуПребывания Тогда
						СтрокаТаблицы.Параметры.АдресДляИнформированияПредставление = "";
					КонецЕсли;
					
					Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, СтрокаТаблицы) Тогда
						ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					
					ДокументРезультат.Вывести(СтрокаТаблицы);
					НомерСтроки = НомерСтроки + 1;
					
					Анкета = Макет.ПолучитьОбласть("Анкета");
					ЗаполнитьЗначенияСвойств(Анкета.Параметры, ВыборкаПоРаботникам);
					ЗаполнитьЗначенияСвойств(Анкета.Параметры, ВыборкаПоШапкеДокумента);
					Анкета.Параметры.ФИОПолные = ВыборкаПоРаботникам.Фамилия + " " + ВыборкаПоРаботникам.Имя + " " + ВыборкаПоРаботникам.Отчество;
					Анкета.Параметры.ГодРождения = Формат(ВыборкаПоРаботникам.ДатаРождения, "ДФ=гггг");
					Анкета.Параметры.ДатаОтчета = Формат(ВыборкаПоШапкеДокумента.Дата, "ДЛФ=ДД");
					ДанныеАнкет.Добавить(Анкета);
					
				КонецЦикла;
			
				Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, Подвал) Тогда
					ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(Подвал.Параметры, ВыборкаПоШапкеДокумента);
				Подвал.Параметры.ДатаОтчета = Формат(ВыборкаПоШапкеДокумента.Дата, "ДЛФ=ДД");
				ДокументРезультат.Вывести(Подвал);
				
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
				
				Для Каждого ДанныеАнкеты Из ДанныеАнкет Цикл
					ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
					ДокументРезультат.Вывести(ДанныеАнкеты);
				КонецЦикла;
				
				УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, НачалоФормы, ОбъектыПечати, ВыборкаПоШапкеДокумента.Ссылка);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДокументРезультат;
	
КонецФункции

Функция ЗапросПоШапкеДляПечати(МассивСсылок)
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПервоначальнаяПостановкаНаВоинскийУчет.Ссылка КАК Ссылка,
		|	ПервоначальнаяПостановкаНаВоинскийУчет.Дата КАК Дата,
		|	ПервоначальнаяПостановкаНаВоинскийУчет.Номер КАК Номер,
		|	ПервоначальнаяПостановкаНаВоинскийУчет.НомерПриказа КАК НомерПриказа,
		|	ПервоначальнаяПостановкаНаВоинскийУчет.Организация КАК Организация,
		|	ПервоначальнаяПостановкаНаВоинскийУчет.ГодПостановкиНаУчет КАК ГодПостановкиНаУчет,
		|	ПервоначальнаяПостановкаНаВоинскийУчет.ФактическийАдресОрганизации КАК ФактическийАдресОрганизации,
		|	ПервоначальнаяПостановкаНаВоинскийУчет.Руководитель КАК Руководитель,
		|	ПервоначальнаяПостановкаНаВоинскийУчет.ДолжностьРуководителя КАК ДолжностьРуководителя,
		|	ПервоначальнаяПостановкаНаВоинскийУчет.ОтветственныйЗаВУР КАК ОтветственныйЗаВУР,
		|	ПервоначальнаяПостановкаНаВоинскийУчет.ДолжностьОтветственногоЗаВУР КАК ДолжностьОтветственногоЗаВУР
		|ПОМЕСТИТЬ ВТДанныеДокументов
		|ИЗ
		|	Документ.ПервоначальнаяПостановкаНаВоинскийУчет КАК ПервоначальнаяПостановкаНаВоинскийУчет
		|ГДЕ
		|	ПервоначальнаяПостановкаНаВоинскийУчет.Ссылка В(&МассивСсылок)";
	
	Запрос.Выполнить();
	
	ИменаПолейОтветственныхЛиц = Новый Массив;
	ИменаПолейОтветственныхЛиц.Добавить("Руководитель");
	ИменаПолейОтветственныхЛиц.Добавить("ОтветственныйЗаВУР");
	ЗарплатаКадры.СоздатьВТФИООтветственныхЛиц(Запрос.МенеджерВременныхТаблиц, Истина, ИменаПолейОтветственныхЛиц, "ВТДанныеДокументов");
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДокументов.ОтветственныйЗаВУР КАК ОтветственныйЗаВУР
		|ИЗ
		|	ВТДанныеДокументов КАК ДанныеДокументов";
	
	МассивФизическихЛиц = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ОтветственныйЗаВУР");
	
	РабочиеТелефоныПодписантов = КонтактнаяИнформацияБЗК.КонтактнаяИнформацияОбъектов(
		МассивФизическихЛиц, , Справочники.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица);
		
	РабочиеТелефоны = Новый ТаблицаЗначений;
	РабочиеТелефоны.Колонки.Добавить("Объект", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	РабочиеТелефоны.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(20)));
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РабочиеТелефоныПодписантов, РабочиеТелефоны);
	
	Запрос.УстановитьПараметр("РабочиеТелефоны", РабочиеТелефоны);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РабочиеТелефоны.Объект КАК ОтветственныйЗаВУР,
		|	РабочиеТелефоны.Представление КАК Телефон
		|ПОМЕСТИТЬ ВТРабочиеТелефоны
		|ИЗ
		|	&РабочиеТелефоны КАК РабочиеТелефоны
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеДокументов.Ссылка КАК Ссылка,
		|	ДанныеДокументов.Дата КАК Дата,
		|	ДанныеДокументов.Номер КАК Номер,
		|	ДанныеДокументов.НомерПриказа КАК НомерПриказа,
		|	ДанныеДокументов.Организация КАК Организация,
		|	ДанныеДокументов.ГодПостановкиНаУчет КАК ГодПостановкиНаУчет,
		|	ДанныеДокументов.ФактическийАдресОрганизации КАК ФактАдресОрганизации,
		|	ДанныеДокументов.Руководитель КАК Руководитель,
		|	ДанныеДокументов.ДолжностьРуководителя КАК ДолжностьРуководителя,
		|	ДанныеДокументов.ОтветственныйЗаВУР КАК ОтветственныйЗаВУР,
		|	ДанныеДокументов.ДолжностьОтветственногоЗаВУР КАК ДолжностьОтветственногоЗаВУР,
		|	ВЫБОР
		|		КОГДА ПОДСТРОКА(Организации.НаименованиеПолное, 1, 10) <> """"
		|			ТОГДА Организации.НаименованиеПолное
		|		ИНАЧЕ Организации.Наименование
		|	КОНЕЦ КАК ПолноеНаименованиеОрганизации,
		|	Организации.НаименованиеПолное КАК НаименованиеПолное,
		|	Организации.НаименованиеСокращенное КАК НаименованиеСокращенное,
		|	ФИОРуководителя.РасшифровкаПодписи КАК РуководительРасшифровкаПодписи,
		|	ФИООтветственногоЗаВУР.РасшифровкаПодписи КАК ОтветственныйЗаВУРРасшифровкаПодписи,
		|	РабочиеТелефоны.Телефон КАК ТелефонОтветственногоЗаВУР
		|ИЗ
		|	ВТДанныеДокументов КАК ДанныеДокументов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО ДанныеДокументов.Организация = Организации.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИОРуководителя
		|		ПО ДанныеДокументов.Руководитель = ФИОРуководителя.ФизическоеЛицо
		|			И ДанныеДокументов.Ссылка = ФИОРуководителя.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИООтветственногоЗаВУР
		|		ПО ДанныеДокументов.ОтветственныйЗаВУР = ФИООтветственногоЗаВУР.ФизическоеЛицо
		|			И ДанныеДокументов.Ссылка = ФИООтветственногоЗаВУР.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРабочиеТелефоны КАК РабочиеТелефоны
		|		ПО ДанныеДокументов.ОтветственныйЗаВУР = РабочиеТелефоны.ОтветственныйЗаВУР
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДанныеДокументов.Дата,
		|	Ссылка";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапросПоСотрудникамДляПечати(МассивСсылок)
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Сотрудники.Ссылка КАК Ссылка,
		|	Сотрудники.НомерСтроки КАК НомерСтроки,
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Сотрудники.Военкомат КАК Военкомат,
		|	Сотрудники.Военкомат.Наименование КАК НаименованиеВоенкомата,
		|	ВЫБОР
		|		КОГДА Сотрудники.Военкомат.НаименованиеДляЭлектронногоОбмена = """"
		|			ТОГДА Сотрудники.Военкомат.Наименование
		|		ИНАЧЕ Сотрудники.Военкомат.НаименованиеДляЭлектронногоОбмена
		|	КОНЕЦ КАК ВоенкоматНаименованиеДляЭлектронногоОбмена,
		|	Сотрудники.Военкомат.Адрес КАК АдресВоенкомата,
		|	Сотрудники.Фамилия КАК Фамилия,
		|	Сотрудники.Имя КАК Имя,
		|	Сотрудники.Отчество КАК Отчество,
		|	Сотрудники.СтраховойНомерСФР КАК СтраховойНомерСФР,
		|	Сотрудники.ДатаРождения КАК ДатаРождения,
		|	Сотрудники.Пол КАК Пол,
		|	Сотрудники.СерияПаспорта КАК СерияПаспорта,
		|	Сотрудники.НомерПаспорта КАК НомерПаспорта,
		|	Сотрудники.ДатаВыдачиПаспорта КАК ДатаВыдачиПаспорта,
		|	Сотрудники.ДатаРегистрацииПоМестуЖительства КАК ДатаРегистрацииПоМестуЖительства,
		|	Сотрудники.АдресРегистрацииПоМестуЖительства КАК АдресРегистрацииПоМестуЖительства,
		|	Сотрудники.ДатаРегистрацииПоМестуПребывания КАК ДатаРегистрацииПоМестуПребывания,
		|	Сотрудники.АдресРегистрацииПоМестуПребывания КАК АдресРегистрацииПоМестуПребывания,
		|	Сотрудники.ДатаФактическогоПроживания КАК ДатаФактическогоПроживания,
		|	Сотрудники.АдресФактическогоПроживания КАК АдресФактическогоПроживания,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(Сотрудники.АдресРегистрацииПоМестуПребывания, """") = """"
		|			ТОГДА Сотрудники.АдресРегистрацииПоМестуЖительства
		|		ИНАЧЕ Сотрудники.АдресРегистрацииПоМестуПребывания
		|	КОНЕЦ КАК АдресМестаПроживанияПредставление,
		|	Сотрудники.Должность КАК Должность,
		|	Сотрудники.НомерТелефона КАК НомерТелефона,
		|	Сотрудники.Подразделение КАК Подразделение
		|ИЗ
		|	Документ.ПервоначальнаяПостановкаНаВоинскийУчет.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&МассивСсылок)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	Военкомат,
		|	НомерСтроки";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапрашиваемыеКадровыеДанныеСотрудников() Экспорт

	ЗапрашиваемыеДанные = "Фамилия, Имя, Отчество, СтраховойНомерПФР, Пол, ДатаРождения,
		|ДокументСерия, ДокументНомер, ДокументДатаВыдачи, ВоинскийУчетВоенкомат, 
		|АдресПоПропискеПредставление, АдресМестаПроживанияПредставление, АдресДляИнформированияПредставление, 
		|ДатаРегистрации, ДатаВременнойРегистрации, ДатаФактическогоПроживания,
		|ТелефонДомашнийПредставление, ТелефонРабочийПредставление, ТелефонМобильныйПредставление,
		|Подразделение, Должность";
	
	Возврат ЗапрашиваемыеДанные;
	
КонецФункции

Функция КадровыеДанныеСотрудников(ДанныеСотрудников) Экспорт
	
	ЗапрашиваемыеДанные = ЗапрашиваемыеКадровыеДанныеСотрудников();
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудниковПоТаблицеЗначений(Истина, ДанныеСотрудников, ЗапрашиваемыеДанные);
	
	Возврат КадровыеДанные;
	
КонецФункции

Процедура ЗаполнитьДанныеСтрокиСотрудника(СтрокаСотрудника, КадровыеДанные, ДатаДокумента) Экспорт 
	
	ЗаполнитьЗначенияСвойств(СтрокаСотрудника, КадровыеДанные, , "ДатаФактическогоПроживания");
	
	СтрокаСотрудника.Военкомат = КадровыеДанные.ВоинскийУчетВоенкомат; 
	СтрокаСотрудника.СтраховойНомерСФР = КадровыеДанные.СтраховойНомерПФР;
	СтрокаСотрудника.СерияПаспорта = СтрЗаменить(КадровыеДанные.ДокументСерия, " ", "");
	СтрокаСотрудника.НомерПаспорта = СтрЗаменить(КадровыеДанные.ДокументНомер, " ", "");
	СтрокаСотрудника.ДатаВыдачиПаспорта = КадровыеДанные.ДокументДатаВыдачи;
	СтрокаСотрудника.ДатаРегистрацииПоМестуЖительства = КадровыеДанные.ДатаРегистрации;
	СтрокаСотрудника.АдресРегистрацииПоМестуЖительства = КадровыеДанные.АдресПоПропискеПредставление;
	
	Если Не ПолучитьФункциональнуюОпцию("ЗаполнятьТолькоАдресПоПропискеВСведенияхДляВоенкоматов") Тогда 
		СтрокаСотрудника.ДатаРегистрацииПоМестуПребывания = КадровыеДанные.ДатаВременнойРегистрации;
		СтрокаСотрудника.АдресРегистрацииПоМестуПребывания = КадровыеДанные.АдресМестаПроживанияПредставление;
		СтрокаСотрудника.ДатаФактическогоПроживания = КадровыеДанные.ДатаФактическогоПроживания;
		СтрокаСотрудника.АдресФактическогоПроживания = КадровыеДанные.АдресДляИнформированияПредставление;
	КонецЕсли;
	
	СтрокаСотрудника.НомерТелефона = КадровыеДанные.ТелефонМобильныйПредставление;
	
	Если Не ЗначениеЗаполнено(СтрокаСотрудника.НомерТелефона) Тогда 
		СтрокаСотрудника.НомерТелефона = ?(ЗначениеЗаполнено(КадровыеДанные.ТелефонДомашнийПредставление),
			КадровыеДанные.ТелефонДомашнийПредставление, КадровыеДанные.ТелефонРабочийПредставление);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаФормированияФайла(Объект) Экспорт
	
	ТекстФайла = СформироватьВыходнойФайл(Объект.Ссылка);
	ВоинскийУчет.ЗаписатьФайлОтчетности(Объект.Ссылка, Объект.ИмяФайлаОтчетности, ТекстФайла, "UTF-8");
	
КонецПроцедуры

Функция СформироватьВыходнойФайл(Ссылка)
	
	СписокДокументов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка);
	ВыборкаПоШапкеДокумента = ЗапросПоШапкеДляПечати(СписокДокументов).Выбрать();
	ВыборкаПоРаботникам = ЗапросПоСотрудникамДляПечати(СписокДокументов).Выбрать();
	
	ВыборкаПоШапкеДокумента.Следующий();
	
	ТекстФайла = "Наименование военного комиссариата;Фамилия;Имя;Отчество (при наличии);Дата рождения;Пол;Серия паспорта;Номер паспорта;Дата выдачи паспорта;СНИЛС;Занимаемая должность (курс/класс);Дата регистрации по месту жительства;Адрес регистрации по месту жительства;Дата регистрации по месту пребывания;Адрес регистрации по месту пребывания;Дата фактического проживания;Адрес фактического проживания;Номер телефона" + Символы.ПС;
	
	Пока ВыборкаПоРаботникам.Следующий() Цикл 
		ДанныеСтроки = Новый Массив;
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.ВоенкоматНаименованиеДляЭлектронногоОбмена);
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.Фамилия);
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.Имя);
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.Отчество);
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаРождения, "ДЛФ=Д"));
		ДанныеСтроки.Добавить(Строка(ВыборкаПоРаботникам.Пол));
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.СерияПаспорта);
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.НомерПаспорта);
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаВыдачиПаспорта, "ДЛФ=Д"));
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.СтраховойНомерСФР);
		ДанныеСтроки.Добавить(Строка(ВыборкаПоРаботникам.Должность));
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаРегистрацииПоМестуЖительства, "ДЛФ=Д"));
		ДанныеСтроки.Добавить(ВоинскийУчет.ФорматАдресаДляЭлектронногоОбмена(ВыборкаПоРаботникам.АдресРегистрацииПоМестуЖительства));
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаРегистрацииПоМестуПребывания, "ДЛФ=Д"));
		ДанныеСтроки.Добавить(ВоинскийУчет.ФорматАдресаДляЭлектронногоОбмена(ВыборкаПоРаботникам.АдресРегистрацииПоМестуПребывания));
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаФактическогоПроживания, "ДЛФ=Д"));
		ДанныеСтроки.Добавить(ВоинскийУчет.ФорматАдресаДляЭлектронногоОбмена(ВыборкаПоРаботникам.АдресФактическогоПроживания));
		ДанныеСтроки.Добавить(ВоинскийУчет.ФорматТелефонаДляЭлектронногоОбмена(ВыборкаПоРаботникам.НомерТелефона));
		ТекстФайла = ТекстФайла + СтрСоединить(ДанныеСтроки, ";") + Символы.ПС;
	КонецЦикла;
	
	Возврат ТекстФайла;
		
КонецФункции

#КонецОбласти

#КонецЕсли