#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Проводит документ по учетам. Если в параметре ВидыУчетов передано Неопределено, то документ проводится по всем учетам.
// Процедура вызывается из обработки проведения и может вызываться из вне.
// 
// Параметры:
//  ДокументСсылка	- ДокументСсылка.ОперацияУчетаПоСтраховымВзносам
//  РежимПроведения - РежимПроведенияДокумента - Режим проведения документа (оперативный, неоперативный).
//  Отказ 			- Булево - Признак отказа от выполнения проведения.
//  ВидыУчетов 		- Строка - Список видов учета, по которым необходимо провести документ. Если параметр пустой или Неопределено, то документ проведется по всем учетам.
//  Движения 		- КоллекцияДвижений - Передается только при вызове из обработки проведения документа.
//  Объект			- ДокументОбъект.ОперацияУчетаПоСтраховымВзносам - Передается только при вызове из обработки проведения документа.
//  ДополнительныеПараметры - Структура - Дополнительные параметры, необходимые для проведения документа.
//
Процедура ПровестиПоУчетам(ДокументСсылка, РежимПроведения, Отказ, ВидыУчетов = Неопределено, Движения = Неопределено, Объект = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	СтруктураВидовУчета = ПроведениеСервер.СтруктураВидовУчета();	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвиженийПоВидамУчета(РежимПроведения, ДокументСсылка, СтруктураВидовУчета, ВидыУчетов, Движения, Объект, Отказ);
	
	ДанныеДляПроведения = ДанныеДляПроведения(ДокументСсылка, СтруктураВидовУчета);
	
	Если СтруктураВидовУчета.СтраховыеВзносы Тогда
		УчетСтраховыхВзносов.СформироватьДоходыСтраховыеВзносы(Движения, Отказ, ДанныеДляПроведения.Организация, ДанныеДляПроведения.МесяцРасчетногоПериода, ДанныеДляПроведения.НачисленияПоСотрудникам, Истина);
		УчетСтраховыхВзносов.СформироватьИсчисленныеВзносы(Движения, Отказ, ДанныеДляПроведения.Организация, ДанныеДляПроведения.МесяцРасчетногоПериода, ДанныеДляПроведения.ВзносыИсчисленные);	
		УчетСтраховыхВзносов.СформироватьСтраховыеВзносыПоФизическимЛицам(Движения, Отказ, ДанныеДляПроведения.Организация, ДанныеДляПроведения.МесяцРасчетногоПериода, ДокументСсылка, ДанныеДляПроведения.ВзносыИсчисленные);
	КонецЕсли;
	
	Если СтруктураВидовУчета.ОстальныеВидыУчета Или СтруктураВидовУчета.СтраховыеВзносы Тогда
		УчетПособийСоциальногоСтрахования.СформироватьПособия(Движения, Отказ, ДанныеДляПроведения.Организация, ДанныеДляПроведения.МесяцРасчетногоПериода, ДанныеДляПроведения.Пособия, ДанныеДляПроведения.ПособияПоУходу);
	КонецЕсли;

	ПроведениеСервер.ЗаписьДвиженийПоУчетам(Движения, СтруктураВидовУчета);
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаФизическоеЛицоВШапке("ФизическоеЛицо", "");
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения(Ссылка, СтруктураВидовУчета)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеОДоходах.Ссылка.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеОДоходах.ВидДохода КАК ВидДохода,
	|	СУММА(ДанныеОДоходах.Результат) КАК Сумма,
	|	СУММА(ДанныеОДоходах.Скидка) КАК Скидка,
	|	ДанныеОДоходах.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	ДанныеОДоходах.ОблагаетсяВзносамиНаДоплатуЛетчикам КАК ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	ДанныеОДоходах.ОблагаетсяВзносамиНаДоплатуШахтерам КАК ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	ДанныеОДоходах.ЯвляетсяДоходомФармацевта КАК ЯвляетсяДоходомФармацевта,
	|	ДанныеОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ КАК ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	ДанныеОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией КАК ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ДанныеОДоходах.КлассУсловийТруда КАК КлассУсловийТруда,
	|	ДанныеОДоходах.ЛьготныйТерриториальныйТариф КАК ЛьготныйТерриториальныйТариф,
	|	ДанныеОДоходах.ИспользоватьБюджетныйТарифНСиПЗ КАК ИспользоватьБюджетныйТарифНСиПЗ
	|ИЗ
	|	Документ.ОперацияУчетаПоСтраховымВзносам.СведенияОДоходах КАК ДанныеОДоходах
	|ГДЕ
	|	ДанныеОДоходах.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеОДоходах.ВидДохода,
	|	ДанныеОДоходах.Ссылка.ФизическоеЛицо,
	|	ДанныеОДоходах.ОблагаетсяЕНВД,
	|	ДанныеОДоходах.ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	ДанныеОДоходах.ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	ДанныеОДоходах.ЯвляетсяДоходомФармацевта,
	|	ДанныеОДоходах.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	ДанныеОДоходах.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ДанныеОДоходах.КлассУсловийТруда,
	|	ДанныеОДоходах.ЛьготныйТерриториальныйТариф,
	|	ДанныеОДоходах.ИспользоватьБюджетныйТарифНСиПЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Пособия.ВидПособияСоциальногоСтрахования КАК ВидПособияСоциальногоСтрахования,
	|	Пособия.Ссылка.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Пособия.ВидЗанятости КАК ВидЗанятости,
	|	Пособия.ФинансированиеФедеральнымБюджетом КАК ФинансированиеФедеральнымБюджетом,
	|	ВЫБОР
	|		КОГДА Пособия.УчитыватьКакНовыйСтраховойСлучай
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтраховыеСлучаи,
	|	СУММА(Пособия.ОплаченныеДни) КАК ОплаченныеДни,
	|	СУММА(Пособия.СуммаВсего) КАК СуммаВсего,
	|	СУММА(Пособия.СуммаСверхНорм) КАК СуммаСверхНорм,
	|	Пособия.ДатаСтраховогоСлучая КАК ДатаСтраховогоСлучая
	|ИЗ
	|	Документ.ОперацияУчетаПоСтраховымВзносам.ПособияПоСоциальномуСтрахованию КАК Пособия
	|ГДЕ
	|	Пособия.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Пособия.Ссылка.ФизическоеЛицо,
	|	Пособия.ДатаСтраховогоСлучая,
	|	Пособия.ВидПособияСоциальногоСтрахования,
	|	Пособия.ФинансированиеФедеральнымБюджетом,
	|	ВЫБОР
	|		КОГДА Пособия.УчитыватьКакНовыйСтраховойСлучай
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	Пособия.ВидЗанятости
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Пособия.Ссылка.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Пособия.ВидЗанятости КАК ВидЗанятости,
	|	Пособия.ФинансированиеФедеральнымБюджетом КАК ФинансированиеФедеральнымБюджетом,
	|	1 КАК СтраховыеСлучаи,
	|	ВЫБОР
	|		КОГДА Пособия.ПособиеПоУходуЗаПервымРебенком <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтраховыеСлучаиПоУходуЗаПервымРебенком,
	|	Пособия.ПособиеПоУходуЗаПервымРебенком КАК СуммаВсегоПоУходуЗаПервымРебенком,
	|	Пособия.ПособиеПоУходуЗаПервымРебенкомСверхНорм КАК СуммаСверхНормПоУходуЗаПервымРебенком,
	|	ВЫБОР
	|		КОГДА Пособия.ПособиеПоУходуЗаВторымРебенком <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтраховыеСлучаиПоУходуЗаВторымРебенком,
	|	Пособия.ПособиеПоУходуЗаВторымРебенком КАК СуммаВсегоПоУходуЗаВторымРебенком,
	|	Пособия.ПособиеПоУходуЗаВторымРебенкомСверхНорм КАК СуммаСверхНормПоУходуЗаВторымРебенком,
	|	Пособия.НачалоОтпускаПоУходу КАК ДатаСтраховогоСлучая
	|ИЗ
	|	Документ.ОперацияУчетаПоСтраховымВзносам.ПособияПоУходуЗаРебенком КАК Пособия
	|ГДЕ
	|	Пособия.Ссылка = &Ссылка
	|	И (Пособия.ПособиеПоУходуЗаПервымРебенком <> 0
	|			ИЛИ Пособия.ПособиеПоУходуЗаВторымРебенком <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацияУчетаПоСтраховымВзносам.МесяцРасчетногоПериода КАК МесяцРасчетногоПериода,
	|	ОперацияУчетаПоСтраховымВзносам.Организация КАК Организация
	|ИЗ
	|	Документ.ОперацияУчетаПоСтраховымВзносам КАК ОперацияУчетаПоСтраховымВзносам
	|ГДЕ
	|	ОперацияУчетаПоСтраховымВзносам.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацияУчетаПоСтраховымВзносам.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	Документ.ОперацияУчетаПоСтраховымВзносам КАК ОперацияУчетаПоСтраховымВзносам
	|ГДЕ
	|	ОперацияУчетаПоСтраховымВзносам.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Результаты = Запрос.ВыполнитьПакет();
	
	ВыборкаРеквизиты = Результаты[3].Выбрать();
	
	ДанныеДляПроведения = Новый Структура("НачисленияПоСотрудникам, ВзносыИсчисленные, Пособия, ПособияПоУходу, МенеджерВременныхТаблиц, Организация, МесяцРасчетногоПериода");
	
	Пока ВыборкаРеквизиты.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДанныеДляПроведения, ВыборкаРеквизиты);
	КонецЦикла;
	
	Если СтруктураВидовУчета.СтраховыеВзносы Тогда
		НачисленияПоСотрудникам = Результаты[0].Выгрузить();
		ДанныеДляПроведения.НачисленияПоСотрудникам = НачисленияПоСотрудникам;
		ДанныеДляПроведения.ВзносыИсчисленные = УчетСтраховыхВзносов.ДанныеОВзносахИзДокумента(Ссылка, "Документ.ОперацияУчетаПоСтраховымВзносам", , , Ложь, "МесяцРасчетногоПериода");	
	КонецЕсли;
	
	Если СтруктураВидовУчета.ОстальныеВидыУчета Или СтруктураВидовУчета.СтраховыеВзносы Тогда
		Пособия = Результаты[1].Выгрузить();
		ПособияПоУходу = Результаты[2].Выгрузить();
		ДанныеДляПроведения.Пособия = Пособия;
		ДанныеДляПроведения.ПособияПоУходу = ПособияПоУходу;
		ДанныеДляПроведения.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	КонецЕсли;
						
	Возврат ДанныеДляПроведения;

КонецФункции


#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли