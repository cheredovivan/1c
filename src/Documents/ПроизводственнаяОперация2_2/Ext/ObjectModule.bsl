#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Устанавливает статус для объекта документа
//
// Параметры:
//	НовыйСтатус - Строка - Имя статуса, который будет установлен у заказов
//	ДополнительныеПараметры - Структура - Структура дополнительных параметров установки статуса.
//
// Возвращаемое значение:
//	Булево - Истина, в случае успешной установки нового статуса.
//
Функция УстановитьСтатус(НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	Статус = ОперативныйУчетПроизводстваКлиентСервер.СтатусОперацииСсылка(НовыйСтатус);
	
	Документы.ПроизводственнаяОперация2_2.ЗаполнитьРеквизитыПриУстановкеСтатуса(ЭтотОбъект);
	
	Возврат ПроверитьЗаполнение();
	
КонецФункции

// Заполняет документ по данным НСИ
//
// Параметры:
//	ДанныеЗаполнения - Неопределено, Структура - из:
//		* ЭтапСпецификации - СправочникСсылка.ЭтапыПроизводства
//		* ДанныеСпецификации - см. Справочники.РесурсныеСпецификации.ДанныеСпецификацииКонструктор
//	СохранитьИсполнителей - Булево - определяет необходимость сохранения исполнителей трудозатрат
//	СохранитьСерии - Булево - определяет необходимость сохранения серий в выходных изделиях и материалах
//
Процедура ЗаполнитьПоНормативамОперации(ДанныеЗаполнения = Неопределено, СохранитьИсполнителей = Ложь, СохранитьСерии = Ложь) Экспорт
	
	Если СохранитьИсполнителей И НЕ ЗначениеЗаполнено(Исполнитель) Тогда
		ДополнительныеСвойства.Вставить("Трудозатраты", Трудозатраты.Выгрузить());
	КонецЕсли;
	
	Если СохранитьСерии Тогда
		ДополнительныеСвойства.Вставить("ВыходныеИзделияСерии", ВыходныеИзделияСерии.Выгрузить());
		ДополнительныеСвойства.Вставить("ВыходныеИзделия", ВыходныеИзделия.Выгрузить());
		ДополнительныеСвойства.Вставить("МатериалыИРаботы", МатериалыИРаботы.Выгрузить());
	КонецЕсли;
	
	ОчиститьТабличныеЧасти();
	
	ПараметрыТехпроцесса = РедакторПроизводственногоПроцессаВызовСервера.ПараметрыТехпроцессаЭтапа(Этап);
	ВладелецОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Операция, "Владелец");
	
	Если ТипЗнч(ВладелецОперации) = Тип("СправочникСсылка.ТехнологическиеПроцессы")
			И (ПараметрыТехпроцесса.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Альтернативный
				ИЛИ ПараметрыТехпроцесса.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Индивидуальный)
			И (ДанныеСпецификации(ДанныеЗаполнения) = Неопределено
				ИЛИ ДанныеСпецификации(ДанныеЗаполнения).Операции.НайтиСтроки(ОтборОперации(ДанныеЗаполнения)).Количество() = 0) Тогда
		ДанныеОперации = ДанныеОперацииТехнологическогоПроцесса(ДанныеЗаполнения, ПараметрыТехпроцесса);
	ИначеЕсли ТипЗнч(ВладелецОперации) = Тип("СправочникСсылка.МаршрутныеКарты") Тогда
		ДанныеОперации = ДанныеОперацииМаршрутнойКарты();
	Иначе
		ДанныеОперации = ДанныеОперацииСпецификации(ДанныеЗаполнения);
	КонецЕсли;
	
	Если ДанныеОперации = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка расчета нормативов для заполнения операции.';
								|en = 'An error occurred when calculating standards for filling the operation.'");
	КонецЕсли;
	
	СписокРеквизитов = "ВидОперации,ВариантНаладки,МожноПовторить,МожноПропустить,Контроль,ЕдиницаИзмерения,Загрузка,
	|Непрерывная,ПередаточнаяПартия,ВремяШтучное,ВремяШтучноеЕдИзм,ВремяПЗ,ВремяПЗЕдИзм";
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеОперации, СписокРеквизитов);
	Если РабочийЦентр.Пустая() ИЛИ ТипЗнч(РабочийЦентр) = Тип("СправочникСсылка.ВидыРабочихЦентров") Тогда
		ДанныеОперации.КоэффициентВремениРаботы = 1;
	Иначе
		ДанныеОперации.КоэффициентВремениРаботы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РабочийЦентр, "КоэффициентВремениРаботы");
	КонецЕсли;
	
	Наименование = ДанныеОперации.ОперацияПредставление;
	РассчитатьВремяВыполнения(ДанныеОперации);
	
	Настройки = Документы.ПроизводственнаяОперация2_2.НастройкиУчета(
		Подразделение,
		ВладелецОперации);
	
	Если Настройки.ИспользоватьВыходныеИзделия Тогда
		Для каждого ИмяТЧ Из СтрРазделить("ВыходныеИзделия,ВозвратныеОтходы", ",") Цикл
			Если ДанныеОперации.Свойство(ИмяТЧ) Тогда
				Для каждого Строка Из ДанныеОперации[ИмяТЧ] Цикл
					ЗаполнитьЗначенияСвойств(ВыходныеИзделия.Добавить(), Строка);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Настройки.ИспользоватьМатериалы Тогда
		
		ЗаполнитьМатериалыПоНормативам(ДанныеОперации.Материалы);
		
	КонецЕсли;
	
	Для каждого Строка Из ДанныеОперации.Трудозатраты Цикл
		ЗаполнитьЗначенияСвойств(Трудозатраты.Добавить(), Строка);
	КонецЦикла;
	
	ВосстановитьИсполнителейТрудозатрат();
	
	ВосстановитьСерии();
	
	Документы.ПроизводственнаяОперация2_2.ЗаполнитьСтатусыУказанияСерий(ЭтотОбъект);
	
	Если ПараметрыТехпроцесса.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Индивидуальный Тогда
		МожноПовторить = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
		Если ДанныеЗаполнения.Свойство("Статус")
			И Статус <> Метаданные.Документы.ПроизводственнаяОперация2_2.Реквизиты.Статус.ЗначениеЗаполнения Тогда
			
			Документы.ПроизводственнаяОперация2_2.ЗаполнитьРеквизитыПриУстановкеСтатуса(ЭтотОбъект);
			
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("ВидРабочегоЦентра")
			И НЕ ЗначениеЗаполнено(РабочийЦентр) Тогда
			РабочийЦентр = ДанныеЗаполнения.ВидРабочегоЦентра;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("СпособЗаполнения")
			И ДанныеЗаполнения.СпособЗаполнения = "ЗаполнитьПоОперации" Тогда
			
			Если ДанныеЗаполнения.Свойство("КлючОперации") Тогда
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения.КлючОперации); // см. УправлениеПроизводствомКлиентСервер.КлючПроизводственнойОперации
			Иначе
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, "Этап,Операция,ИдентификаторОперации");
			КонецЕсли;
			
			ЗаполнитьПоНормативамОперации(ДанныеЗаполнения);
			
			Если ДанныеЗаполнения.Свойство("ВидыРабот") Тогда
				Отбор = Новый Структура("ВидРабот");
				Для Каждого СтрокаВидовРабот Из ДанныеЗаполнения.ВидыРабот Цикл
					
					Если НЕ ЗначениеЗаполнено(СтрокаВидовРабот.Исполнитель) Тогда
						Продолжить;
					КонецЕсли;
					
					Отбор.ВидРабот = СтрокаВидовРабот.ВидРабот;
					
					СтрокиТрудозатрат = Трудозатраты.НайтиСтроки(Отбор);
					
					Для Каждого Строка Из СтрокиТрудозатрат Цикл
						Строка.Исполнитель = СтрокаВидовРабот.Исполнитель;
					КонецЦикла;
					
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЭтапПроизводства2_2") Тогда
		
		Этап = ДанныеЗаполнения;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ОперацияКакРаспоряжениеВыработки = УправлениеПроизводством.ИспользуетсяОперацияКакРаспоряжениеВыработки(Дата);
	
	Если НЕ Этап.Пустая() Тогда
		
		Реквизиты = "Организация" + ?(Подразделение.Пустая(), ",Подразделение", "") + ",ОперацияКакРаспоряжениеВыработки";
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Этап, Реквизиты));
		
	КонецЕсли;
	
	УправлениеПроизводствомКлиентСервер.УстановитьТипИсполнителя(
		Исполнитель,
		ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(Подразделение).ИспользоватьБригадныеНаряды,
		ПроизводствоСервер.ИспользуетсяУчетТрудозатратВРазрезеСотрудников(Дата));
	
	НаОснованииНСИ = НЕ Операция.Пустая();
	
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ПроверитьЗаполнениеРеквизитовШапки(Отказ, МассивНепроверяемыхРеквизитов);
	
	Если Статус = Перечисления.СтатусыПроизводственныхОпераций.НеВыполнена
		ИЛИ Статус = Перечисления.СтатусыПроизводственныхОпераций.Пропущена Тогда
		ИсключитьРеквизитыТабличныхЧастей(ПроверяемыеРеквизиты);
	Иначе
		ПроверитьЗаполнениеНоменклатуры(Отказ, МассивНепроверяемыхРеквизитов);
		ПроверитьЗаполнениеХарактеристик(Отказ, МассивНепроверяемыхРеквизитов);
		ПроверитьЗаполнениеСерий(Отказ, МассивНепроверяемыхРеквизитов);
		ПроверитьЗаполнениеКоличества(Отказ, МассивНепроверяемыхРеквизитов);
		ПроверитьЗаполнениеБригад(Отказ, МассивНепроверяемыхРеквизитов);
		//++ Локализация
		ПроверитьЗаполнениеИсполнителей(Отказ, МассивНепроверяемыхРеквизитов);
		//-- Локализация
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	УстановитьКлючВСтрокахТабличныхЧастей(Отказ);
	
	ДанныеДоИзменения =  ДанныеОперацииДоИзменения();
	ДополнительныеСвойства.Вставить("ДанныеДоИзменения", ДанныеДоИзменения);
	
	// Проверка объекта
	Если Не РежимГрупповойОбработки() Тогда
		
		Если Не Этап.Пустая() Тогда
			
			СтатусЭтапа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Этап, "Статус");
			
			Если СтатусЭтапа = Перечисления.СтатусыЭтаповПроизводства2_2.Завершен Тогда
				
				ОбщегоНазначения.СообщитьПользователю(
					НСтр("ru = 'Изменение операции, принадлежащей завершенному этапу, невозможно.';
						|en = 'Cannot change the operation that belongs to a completed stage.'"),
					ЭтотОбъект,
					"Этап",,
					Отказ);
					
			ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.Проведение
				И СтатусЭтапа = Перечисления.СтатусыЭтаповПроизводства2_2.Формируется
				И (КоличествоФакт > 0 ИЛИ КоличествоБрак > 0 ИЛИ КоличествоНаДоработке > 0 ИЛИ КоличествоНаКонтроле > 0) Тогда
				
				ОбщегоНазначения.СообщитьПользователю(
					СтрШаблон(НСтр("ru = 'Нельзя отражать ход выполнения операций по этапу в статусе ""%1"".';
									|en = 'Cannot display the progress of operations of the stage in the ""%1"" status.'"), СтатусЭтапа),
					ЭтотОбъект,
					"Этап",,
					Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не СменноеЗадание.Пустая() Тогда
		
			СтатусЗадания = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СменноеЗадание, "Статус");
			
			Если СтатусЗадания = Перечисления.СтатусыСменныхЗаданий.Закрыто Тогда
			
				ОбщегоНазначения.СообщитьПользователю(
					НСтр("ru = 'Изменение операции, принадлежащей закрытому сменному заданию, невозможно.';
						|en = 'Cannot edit the operation that belongs to a closed shift task.'"),
					ЭтотОбъект,
					"СменноеЗадание",,
					Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Заполнение реквизитов
	Для каждого КлючИЗначение Из НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ПроизводственнаяОперация2_2) Цикл
		
		ПараметрыУказанияСерий = КлючИЗначение.Значение; // НоменклатураКлиентСервер.ПараметрыУказанияСерий
		
		Если ПараметрыУказанияСерий.ФактОтбора Тогда
			НоменклатураСервер.ОчиститьНеиспользуемыеСерии(ЭтотОбъект, ПараметрыУказанияСерий);
		КонецЕсли;
		
	КонецЦикла;
	
	Если НаОснованииНСИ
		И (Статус = Перечисления.СтатусыПроизводственныхОпераций.НеВыполнена
			ИЛИ Статус = Перечисления.СтатусыПроизводственныхОпераций.Пропущена) Тогда
		
		ОчиститьТабличныеЧасти();
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Исполнитель) Тогда
		Для каждого Строка Из Трудозатраты Цикл
			Строка.Исполнитель = Исполнитель;
		КонецЦикла;
	КонецЕсли;
	
	ЗаполнитьНомер();
	ЗаполнитьИдентификатор(РежимЗаписи);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
	ПересчитатьОчередьОпераций(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Не РежимГрупповойОбработки() Тогда
		ДобавитьЗаданияКОбработкеЭтаповПроизводства();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Номер = "";
	НомерОперацииЭтапа = 0;
	
	Если РедакторПроизводственногоПроцессаКлиентСервер.ЭтоОперацияИндивидуальная(ОбъектКопирования) Тогда
		
		Операция = Неопределено;
		ИдентификаторОперации = 0;
	
	ИначеЕсли НЕ ОбъектКопирования.РежимРазбиения() Тогда
		
		Операция = Неопределено;
		ИдентификаторОперации = 0;
		
		НаОснованииПланирования = Ложь;
		НаОснованииНСИ = Ложь;
		ВключенаВТехпроцесс = Ложь;
		
		НомерПартии = 0;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеДокумента

Функция ДанныеОперацииМаршрутнойКарты()
	
	Параметры = ПараметрыПолученияДанныхОперации();
	Если Параметры = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеПоНоменклатуре = Справочники.МаршрутныеКарты.ДанныеПоНоменклатуре();
	ЗаполнитьЗначенияСвойств(ДанныеПоНоменклатуре, Параметры);
	
	Результат = Справочники.МаршрутныеКарты.ДанныеОперацииМаршрутнойКарты(
		Операция,
		Количество,
		Параметры.КоэффициентТехнологическогоПроцесса,
		ДанныеПоНоменклатуре);
		
	ОписаниеТаблиц = ОписаниеТаблицДляПересчетаНормативов();
	
	Для каждого КлючИЗначение Из ОписаниеТаблиц Цикл
		
		ОписаниеТаблицы = КлючИЗначение.Значение;
		
		КоэффициентТекущейОперации = (ОписаниеТаблицы.КоличествоОпераций / Количество);
		
		Для каждого Строка Из Результат[ОписаниеТаблицы.ИмяТЧ] Цикл
			
			Если ОписаниеТаблицы.ЕстьУпаковки Тогда
				Строка.КоличествоУпаковок = Строка.КоличествоУпаковок * КоэффициентТекущейОперации;
			КонецЕсли;
			Строка.Количество = Строка.Количество * КоэффициентТекущейОперации;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеОперацииСпецификации(ДанныеЗаполнения = Неопределено)
	
	Результат          = Новый Структура;
	ОписаниеТаблиц     = ОписаниеТаблицДляПересчетаНормативов();
	ДанныеСпецификации = ДанныеСпецификации(ДанныеЗаполнения);
	
	Если НЕ ЗначениеЗаполнено(ДанныеСпецификации) Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	Для каждого КлючИЗначение Из ОписаниеТаблиц Цикл
		Результат.Вставить(КлючИЗначение.Значение.ИмяТЧ, ДанныеСпецификации[КлючИЗначение.Ключ].СкопироватьКолонки());
	КонецЦикла;
	
	НайденныеСтроки = ДанныеСпецификации.Операции.НайтиСтроки(ОтборОперации());
	Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
		
		СтрокаОперация = НайденныеСтроки[0];
		
		Для каждого Колонка Из ДанныеСпецификации.Операции.Колонки Цикл
			Результат.Вставить(Колонка.Имя, СтрокаОперация[Колонка.Имя]);
		КонецЦикла;
		
		Для каждого КлючИЗначение Из ОписаниеТаблиц Цикл
			
			ОписаниеТаблицы = КлючИЗначение.Значение;
			Таблица         = Результат[ОписаниеТаблицы.ИмяТЧ]; // ТаблицаЗначений
			
			Для каждого СтрокаНСИ Из ДанныеСпецификации[КлючИЗначение.Ключ] Цикл
				
				Если СтрокаНСИ.Этап <> СтрокаОперация.Этап Тогда
					Продолжить;
				КонецЕсли;
				
				Если Справочники.РесурсныеСпецификации.СтрокаДанныхСпецификацииПринадлежитОперации(СтрокаОперация, СтрокаНСИ, ОписаниеТаблицы.Изделие) Тогда
					
					НоваяСтрока = Таблица.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНСИ);
					
					КоэффициентТекущейОперации = (ОписаниеТаблицы.КоличествоОпераций / СтрокаОперация.КоличествоНаПартию);
					Если ОписаниеТаблицы.ЕстьУпаковки Тогда
						НоваяСтрока.КоличествоУпаковок = НоваяСтрока.КоличествоУпаковок * КоэффициентТекущейОперации;
					КонецЕсли;
					НоваяСтрока.Количество = НоваяСтрока.Количество * КоэффициентТекущейОперации;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	Иначе
		
		Для каждого Колонка Из ДанныеСпецификации.Операции.Колонки Цикл
			Результат.Вставить(Колонка.Имя, Колонка.ТипЗначения.ПривестиЗначение(0));
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеОперацииТехнологическогоПроцесса(ДанныеЗаполнения, ПараметрыТехпроцесса)
	
	Результат = Новый Структура;
	ОтборОперация = Новый Структура("Операция", Операция);
	ОписаниеТаблиц = ОписаниеТаблицДляПересчетаНормативов();
	
	Параметры = ПараметрыПолученияДанныхОперации();
	Если Параметры = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеПоНоменклатуре = Справочники.ТехнологическиеПроцессы.ДанныеПоНоменклатуре();
	ЗаполнитьЗначенияСвойств(ДанныеПоНоменклатуре, Параметры);
	
	ДанныеТП = Справочники.ТехнологическиеПроцессы.ДанныеТехнологическогоПроцессаПоНоменклатуре(
		Параметры.ТехнологическийПроцесс,
		Параметры.КоэффициентТехнологическогоПроцесса,
		ДанныеПоНоменклатуре,
		Справочники.ТехнологическиеПроцессы.ПараметрыВыборкиДанных("Операции,МатериалыИУслуги,Трудозатраты"));
	
	Для каждого Колонка Из ДанныеТП.Операции.Колонки Цикл
		Результат.Вставить(Колонка.Имя, Колонка.ТипЗначения.ПривестиЗначение(0));
	КонецЦикла;
	
	НайденныеСтроки = ДанныеТП.Операции.НайтиСтроки(ОтборОперация);
	Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
		
		ЗаполнитьЗначенияСвойств(Результат, НайденныеСтроки[0]);
		
		Если ПараметрыТехпроцесса.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Альтернативный Тогда
			РедакторПроизводственногоПроцесса.ДобавитьРесурсыСпецификации(
				ДанныеТП,
				ДанныеСпецификации(ДанныеЗаполнения),
				РедакторПроизводственногоПроцесса.ПереченьДанныхПриДобавленииРесурсовСпецификации(Результат),
				Операция);
		КонецЕсли;
		
		Для каждого КлючИЗначение Из ОписаниеТаблиц Цикл
			
			ОписаниеТаблицы = КлючИЗначение.Значение;
			Если НЕ ДанныеТП.Свойство(КлючИЗначение.Ключ) Тогда
				Продолжить;
			КонецЕсли;
			
			Результат.Вставить(ОписаниеТаблицы.ИмяТЧ, ДанныеТП[КлючИЗначение.Ключ].Скопировать(ОтборОперация));
			
			КоэффициентТекущейОперации = (ОписаниеТаблицы.КоличествоОпераций / Результат.КоличествоНаПартию);
			
			Для каждого Строка Из Результат[ОписаниеТаблицы.ИмяТЧ] Цикл
				
				Если ОписаниеТаблицы.ЕстьУпаковки Тогда
					Строка.КоличествоУпаковок = Строка.КоличествоУпаковок * КоэффициентТекущейОперации;
				КонецЕсли;
				Строка.Количество = Строка.Количество * КоэффициентТекущейОперации;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьМатериалыПоНормативам(Материалы) 
	
	Для каждого Строка Из Материалы Цикл
		ЗаполнитьЗначенияСвойств(МатериалыИРаботы.Добавить(), Строка);
	КонецЦикла;
	
	МатериалыЭтапа = Документы.ЭтапПроизводства2_2.ОстаткиОбеспечиваемыхМатериалов(Этап, "Характеристика");
	МатериалыЭтапа.Индексы.Добавить("Номенклатура, Характеристика");
	
	СтруктураПоиска = Новый Структура("Номенклатура, Характеристика");
	КэшированныеЗначения = Неопределено;
	
	Для Индекс = 0 По МатериалыИРаботы.Количество()-1 Цикл
		
		Строка = МатериалыИРаботы[Индекс];
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, Строка);
		НайденныеСтроки = МатериалыЭтапа.НайтиСтроки(СтруктураПоиска);
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			
			ОтметитьСтрокаМатериалаОтсутствуетВЭтапе(Строка);
			
		Иначе
			
			СтрокаМатериалЭтапа = НайденныеСтроки[0];
			
			Если СтрокаМатериалЭтапа.Количество = Строка.Количество Тогда
				
				МатериалыЭтапа.Удалить(СтрокаМатериалЭтапа);
				
			ИначеЕсли СтрокаМатериалЭтапа.Количество > Строка.Количество Тогда
				
				СтрокаМатериалЭтапа.Количество = СтрокаМатериалЭтапа.Количество - Строка.Количество;
				
			Иначе
				
				КоличествоНедостача = Строка.Количество - СтрокаМатериалЭтапа.Количество;
				
				Строка.Количество = СтрокаМатериалЭтапа.Количество;
				ПересчитатьКоличествоУпаковок(Строка, КэшированныеЗначения);
				
				НоваяСтрока = МатериалыИРаботы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				НоваяСтрока.Количество = КоличествоНедостача;
				ПересчитатьКоличествоУпаковок(НоваяСтрока, КэшированныеЗначения);
				ОтметитьСтрокаМатериалаОтсутствуетВЭтапе(НоваяСтрока);
				
				МатериалыЭтапа.Удалить(СтрокаМатериалЭтапа);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура РассчитатьВремяВыполнения(ДанныеОперации)
	
	СтруктураРасчета = ОперативныйУчетПроизводстваКлиентСервер.СтруктураРасчетаОбщегоВремениВыполнения();
	ЗаполнитьЗначенияСвойств(СтруктураРасчета, ДанныеОперации);
	
	КоэффициентПересчета = Справочники.ТехнологическиеОперации.РассчитатьКоэффициентПересчетаНормативов(
		Операция,
		?(Статус = Перечисления.СтатусыПроизводственныхОпераций.Выполнена,
			КоличествоФакт + КоличествоБрак + КоличествоНаДоработке + КоличествоНаКонтроле,
			Количество - КоличествоОтменено));
	
	ОперативныйУчетПроизводстваКлиентСервер.РассчитатьОбщееВремяВыполненияОперации(
		СтруктураРасчета,
		КоэффициентПересчета);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураРасчета, "ВремяВыполнения, ВремяВыполненияЕдИзм");
	
КонецПроцедуры

Процедура ОтметитьСтрокаМатериалаОтсутствуетВЭтапе(Строка)
	
	Строка.НоменклатураМаршрутнойКарты = Строка.Номенклатура;
	Строка.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
	
	Строка.ХарактеристикаМаршрутнойКарты = Строка.Характеристика;
	Строка.Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	
	Строка.УпаковкаМаршрутнойКарты = Строка.Упаковка;
	Строка.Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
	
КонецПроцедуры

Процедура ПересчитатьКоличествоУпаковок(СтрокаТабличнойЧасти, КэшированныеЗначения)
	
	СтруктураДействий = Новый Структура("ПересчитатьКоличествоУпаковок");
	
	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТабличнойЧасти, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

Процедура ОчиститьТабличныеЧасти()
	
	Для каждого ИмяТЧ Из ТабличныеЧасти() Цикл
		ЭтотОбъект[ИмяТЧ].Очистить();
	КонецЦикла;
	
КонецПроцедуры

Функция ТабличныеЧасти()
	
	Результат = Новый Массив;
	
	Результат.Добавить("МатериалыИРаботы");
	Результат.Добавить("Трудозатраты");
	Результат.Добавить("ВыходныеИзделия");
	Результат.Добавить("ВыходныеИзделияСерии");
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеСпецификации(ДанныеЗаполнения = Неопределено)
	
	ДанныеСпецификации = Неопределено;
	
	Если НЕ ДополнительныеСвойства.Свойство("ДанныеСпецификации", ДанныеСпецификации) Тогда
		
		Если ЗначениеЗаполнено(ДанныеЗаполнения) И ДанныеЗаполнения.Свойство("ДанныеСпецификации", ДанныеСпецификации) Тогда
			
			Если НЕ ДанныеСпецификации.Свойство("Этапы") ИЛИ ДанныеСпецификации.Этапы.Количество() > 1 Тогда
			
				ДанныеСпецификации = ОбщегоНазначения.СкопироватьРекурсивно(ДанныеСпецификации);
				
				Справочники.РесурсныеСпецификации.ПрименитьОтборПоДаннымСпецификации(
						ДанныеСпецификации,
						Новый Структура("Этап", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Этап, "Этап")));
					
			КонецЕсли;
			
		Иначе
			
			ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				Этап,
				"Этап,Распоряжение,НазначениеПродукция,ПартияПроизводства,КоличествоУпаковокПлан,УпаковкаПлан");
			
			ДанныеПартииПроизводства = Новый Структура(
					Документы.ЭтапПроизводства2_2.ДанныеПартииПроизводства(
						ЗначенияРеквизитов.Распоряжение,
						ЗначенияРеквизитов.НазначениеПродукция,
						ЗначенияРеквизитов.ПартияПроизводства));
			ДанныеПартииПроизводства.Количество = ЗначенияРеквизитов.КоличествоУпаковокПлан
				* Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(ЗначенияРеквизитов.УпаковкаПлан, ДанныеПартииПроизводства.Номенклатура);
			
			ДанныеСпецификации = Документы.ЭтапПроизводства2_2.ПолучитьНормативыПоСпецификации(
				ДанныеПартииПроизводства,
				Ложь,
				"ВыходныеИзделия,ВозвратныеОтходы,МатериалыИУслуги,Трудозатраты,Операции",
				Новый Структура("Этап", ЗначенияРеквизитов.Этап));
			
		КонецЕсли;
		
		ДополнительныеСвойства.Вставить("ДанныеСпецификации", ДанныеСпецификации);
		
	КонецЕсли;
	
	Возврат ДанныеСпецификации;
	
КонецФункции

Функция ПараметрыПолученияДанныхОперации()
	
	ТекстыЗапросов = Новый Массив;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Этапы.Ссылка             КАК Этап,
	|	Этапы.Подразделение      КАК Подразделение,
	|	Этапы.ПартияПроизводства КАК ПартияПроизводства
	|ПОМЕСТИТЬ ДанныеЭтапа
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК Этапы
	|ГДЕ
	|	Этапы.Ссылка = &Этап";
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
	ТекстыЗапросов.Добавить(Документы.ЭтапПроизводства2_2.ТекстЗапросаДанныеОсновногоИзделияПартийПроизводства("ДанныеОсновногоИзделия"));
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеЭтапа.Подразделение                                                                                                КАК Подразделение,
	|	ДанныеЭтапа.ПартияПроизводства                                                                                           КАК ПартияПроизводства,
	|	Очередь.ТехнологическийПроцесс                                                                                           КАК ТехнологическийПроцесс,
	|	Очередь.КоэффициентТехнологическогоПроцесса                                                                              КАК КоэффициентТехнологическогоПроцесса,
	|	ДанныеОсновногоИзделия.Номенклатура                                                                                      КАК Номенклатура,
	|	ДанныеОсновногоИзделия.Характеристика                                                                                    КАК Характеристика,
	|	ДанныеОсновногоИзделия.ПартияПроизводства.Спецификация                                                                   КАК Спецификация,
	|	ВЫРАЗИТЬ(ДанныеОсновногоИзделия.ПартияПроизводства.Документ КАК Документ.ЗаказНаПроизводство2_2)                         КАК Распоряжение,
	|	ВЫРАЗИТЬ(ДанныеОсновногоИзделия.ПартияПроизводства.Документ КАК Документ.ЗаказНаПроизводство2_2).Подразделение           КАК ПодразделениеДиспетчер,
	|	ВЫРАЗИТЬ(ДанныеОсновногоИзделия.ПартияПроизводства.Документ КАК Документ.ЗаказНаПроизводство2_2).НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫРАЗИТЬ(ДанныеОсновногоИзделия.ПартияПроизводства.Документ КАК Документ.ЗаказНаПроизводство2_2).НачатьНеРанее           КАК НачалоПроизводства,
	|	ВЫБОР
	|		КОГДА ДанныеОсновногоИзделия.КоличествоТЧ <> 0
	|			ТОГДА ДанныеОсновногоИзделия.КоличествоТЧ
	|		ИНАЧЕ ДанныеОсновногоИзделия.КоличествоШапка
	|	КОНЕЦ                                                                                                                    КАК Количество
	|
	|ИЗ
	|	ДанныеЭтапа КАК ДанныеЭтапа
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОсновногоИзделия КАК ДанныеОсновногоИзделия
	|	ПО ДанныеЭтапа.ПартияПроизводства = ДанныеОсновногоИзделия.ПартияПроизводства
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОчередьПроизводственныхОпераций КАК Очередь
	|	ПО ДанныеЭтапа.Этап = Очередь.Этап
	|	И Очередь.ИдентификаторОперации = &ИдентификаторОперации
	|
	|";
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПартииПроизводства", "ВЫБРАТЬ Т.ПартияПроизводства ИЗ ДанныеЭтапа КАК Т");
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Этап", Этап);
	Запрос.УстановитьПараметр("ИдентификаторОперации", ИдентификаторОперации);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Производит пересчет ресурсов индивидуальной операции
// 
// Параметры:
//  ИтогиДоИзменения - см. ОперативныйУчетПроизводстваКлиентСервер.СтруктураИтоговКонструктор
//
Процедура ПересчитатьРесурсыИндивидуальнойОперации(ИтогиДоИзменения) Экспорт
	
	ИтогиТекущие = ОперативныйУчетПроизводстваКлиентСервер.СтруктураИтоговКонструктор(ЭтотОбъект);
	
	ДельтаБрак     = ИтогиТекущие.КоличествоБрак - ИтогиДоИзменения.КоличествоБрак;
	ДельтаОтменено = ИтогиТекущие.КоличествоОтменено - ИтогиДоИзменения.КоличествоОтменено;
	
	Если ДельтаБрак <> 0 ИЛИ ДельтаОтменено <> 0 Тогда
		
		Для каждого ИмяТЧ Из СтрРазделить("ВыходныеИзделия,МатериалыИРаботы,Трудозатраты", ",") Цикл
			
			Изделие = ИмяТЧ = "ВыходныеИзделия";
			
			ДельтаДо    = ?(Изделие, ИтогиДоИзменения.КоличествоОтменено + ИтогиДоИзменения.КоличествоБрак, ИтогиДоИзменения.КоличествоОтменено);
			ДельтаПосле = ?(Изделие, ИтогиТекущие.КоличествоОтменено + ИтогиТекущие.КоличествоБрак, ИтогиТекущие.КоличествоОтменено);
			
			Для каждого Строка Из ЭтотОбъект[ИмяТЧ] Цикл
				
				Для каждого Показатель Из СтрРазделить("Количество"+?(ИмяТЧ <> "Трудозатраты", ",КоличествоУпаковок", ""), ",") Цикл
					
					ПоказательИсходный = Строка[Показатель] * ИтогиДоИзменения.Количество / (ИтогиДоИзменения.Количество - ДельтаДо);
					Строка[Показатель] = ПоказательИсходный * (ИтогиТекущие.Количество - ДельтаПосле) / ИтогиТекущие.Количество;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаЗаполнения

Процедура ПроверитьЗаполнениеРеквизитовШапки(Отказ, МассивНепроверяемыхРеквизитов)
	
	Если НЕ (Статус = Перечисления.СтатусыПроизводственныхОпераций.Выполняется
		ИЛИ Статус = Перечисления.СтатусыПроизводственныхОпераций.Выполнена) Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("НачалоВыполнения");
		
	КонецЕсли;
	
	Если Статус <> Перечисления.СтатусыПроизводственныхОпераций.Выполнена Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ОкончаниеВыполнения");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РабочийЦентр) Тогда
		
		ИспользуютсяВариантыНаладки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			РабочийЦентр,
			?(ТипЗнч(РабочийЦентр) = Тип("СправочникСсылка.РабочиеЦентры"),
				"ВидРабочегоЦентра.ИспользуютсяВариантыНаладки",
				"ИспользуютсяВариантыНаладки"));
			
	Иначе
			
		ИспользуютсяВариантыНаладки = Ложь;
		
	КонецЕсли;
	
	Если НЕ ИспользуютсяВариантыНаладки Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ВариантНаладки");
		
	КонецЕсли;
	
	Если НЕ НаОснованииНСИ Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Операция");
		
	Иначе
		
		МассивНепроверяемыхРеквизитов.Добавить("Количество");
		
	КонецЕсли;
	
	Настройки = Документы.ПроизводственнаяОперация2_2.НастройкиУчета(Подразделение, Неопределено);
	
	Если НЕ Настройки.ИспользоватьСменныеЗадания
		ИЛИ Статус = Перечисления.СтатусыПроизводственныхОпераций.Формируется Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("СменноеЗадание");
		
	КонецЕсли;
	
	Если НЕ Настройки.ИспользоватьУчастки
		ИЛИ Статус = Перечисления.СтатусыПроизводственныхОпераций.Формируется
		ИЛИ Статус = Перечисления.СтатусыПроизводственныхОпераций.Пропущена Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Участок");
		
	КонецЕсли;
	
	Если НЕ МожноПропустить
		И Статус = Перечисления.СтатусыПроизводственныхОпераций.Пропущена Тогда
		
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Операция не может быть пропущена';
				|en = 'Operation cannot be skipped'"),
			ОбщегоНазначенияУТ.КлючДанныхДляСообщенияПользователю(ЭтотОбъект),
			"Статус",
			"Объект",
			Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеНоменклатуры(Отказ, МассивНепроверяемыхРеквизитов)
	
	Если Статус = Перечисления.СтатусыПроизводственныхОпераций.Выполнена Тогда
		Возврат; // Стандартная проверка реквизитов
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИРаботы.Номенклатура");
	
	КлючДанных  = ОбщегоНазначенияУТ.КлючДанныхДляСообщенияПользователю(ЭтотОбъект);
	ПутьКДанным = "Объект";
	
	ШаблонОшибка = НСтр("ru = 'Не заполнена колонка ""Номенклатура"" в строке %НомерСтроки% списка ""Материалы и работы""';
						|en = 'Column ""Items"" in line %НомерСтроки% of the ""Materials and works"" list is not filled in'");
	
	Для Каждого Строка Из МатериалыИРаботы Цикл
		
		Если НЕ ЗначениеЗаполнено(Строка.Номенклатура)
			И НЕ ЗначениеЗаполнено(Строка.НоменклатураМаршрутнойКарты) Тогда
			
			ТекстСообщения = СтрЗаменить(ШаблонОшибка, "%НомерСтроки%", Строка(Строка.НомерСтроки));
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("МатериалыИРаботы", Строка.НомерСтроки, "Номенклатура");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, КлючДанных, Поле, ПутьКДанным, Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеХарактеристик(Отказ, МассивНепроверяемыхРеквизитов)
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверки.ИмяТЧ = "МатериалыИРаботы";
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(
		ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверки.ИмяТЧ = "ВыходныеИзделия";
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(
		ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеСерий(Отказ, МассивНепроверяемыхРеквизитов)
	
	ОтключитьПроверку = "ПроверкаЗаполненияОтключитьКонтрольСерий";
	
	Для каждого КлючИЗначение Из НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ПроизводственнаяОперация2_2) Цикл
		
		ПараметрыУказанияСерий = КлючИЗначение.Значение; // НоменклатураКлиентСервер.ПараметрыУказанияСерий
		
		Если НЕ ПараметрыУказанияСерий.ФактОтбора Тогда
			ДополнительныеСвойства.Вставить(ОтключитьПроверку);
		ИначеЕсли ДополнительныеСвойства.Свойство(ОтключитьПроверку) Тогда
			ДополнительныеСвойства.Удалить(ОтключитьПроверку);
		КонецЕсли;
		
		НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект,
			ПараметрыУказанияСерий,
			Отказ,
			МассивНепроверяемыхРеквизитов);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеКоличества(Отказ, МассивНепроверяемыхРеквизитов)
	
	УправлениеПроизводством.ПроверитьЗаполнениеКоличестваВТЧ(
		ЭтотОбъект, "МатериалыИРаботы", МассивНепроверяемыхРеквизитов, Отказ);
	
	УправлениеПроизводством.ПроверитьЗаполнениеКоличестваВТЧ(
		ЭтотОбъект, "ВыходныеИзделия", МассивНепроверяемыхРеквизитов, Отказ);
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеБригад(Отказ, МассивНепроверяемыхРеквизитов)
	
	Если Статус <> Перечисления.СтатусыПроизводственныхОпераций.Выполнена
		ИЛИ ЗначениеЗаполнено(Исполнитель) Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Трудозатраты.Исполнитель");
		
	КонецЕсли;
	
КонецПроцедуры

//++ Локализация
Процедура ПроверитьЗаполнениеИсполнителей(Отказ, МассивНепроверяемыхРеквизитов)
	
	Если Статус <> Перечисления.СтатусыПроизводственныхОпераций.Выполнена Тогда
		Возврат;
	КонецЕсли;
	
	КлючДанных  = ОбщегоНазначенияУТ.КлючДанныхДляСообщенияПользователю(ЭтотОбъект);
	ПутьКДанным = "Объект";
	
	Если ПроизводствоСервер.ИспользуетсяУчетТрудозатратВРазрезеСотрудников(ОкончаниеВыполнения) Тогда
		
		Если ЗначениеЗаполнено(Исполнитель) И ТипЗнч(Исполнитель) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			
			ТекстСообщения = НСтр("ru = 'В поле ""Исполнитель"" указано физическое лицо. Необходимо указать сотрудника.';
									|en = 'In the ""Assignee"" field, a person is specified. Specify an employee.'");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, КлючДанных, "Исполнитель", ПутьКДанным, Отказ);
			
		КонецЕсли;
		
		ПроверяемыеСотрудники = Новый Массив;
		
		Если ЗначениеЗаполнено(Исполнитель) И ТипЗнч(Исполнитель) = Тип("СправочникСсылка.Сотрудники") Тогда
			
			ПроверяемыеСотрудники.Добавить(Исполнитель);
			УволенныеСотрудники = ПроизводствоСервер.УволенныеСотрудники(ПроверяемыеСотрудники);
			
			Если УволенныеСотрудники.Количество() > 0
				И ОкончаниеВыполнения >= УволенныеСотрудники[0].ДатаУвольнения Тогда
				
				ТекстСообщения = НСтр("ru = 'В поле ""Исполнитель"" указан уволенный сотрудник.';
										|en = 'In the ""Assignee"" field, a dismissed employee is specified.'");
			
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, КлючДанных, "Исполнитель", ПутьКДанным, Отказ);
				
			КонецЕсли;
		КонецЕсли;
		
		Если МассивНепроверяемыхРеквизитов.Найти("Трудозатраты.Исполнитель") = Неопределено Тогда
			
			ШаблонОшибкаФизЛицо = НСтр("ru = 'В поле ""Исполнитель"" в строке %1 списка ""Трудозатраты"" указано физическое лицо. Необходимо указать сотрудника.';
										|en = 'In the ""Assignee"" field, in line %1 of the ""Labor costs"" list, a person is specified. Specify an employee.'");
			
			Для Каждого Строка Из Трудозатраты Цикл
				Если ЗначениеЗаполнено(Строка.Исполнитель)
					И ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
					
					ТекстСообщения = СтрШаблон(ШаблонОшибкаФизЛицо, Строка(Строка.НомерСтроки));
					
					Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Трудозатраты", Строка.НомерСтроки, "Исполнитель");
				
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, КлючДанных, Поле, ПутьКДанным, Отказ);
					
				КонецЕсли;
			КонецЦикла;
			
			Если Отказ Тогда
				Возврат;
			КонецЕсли;
			
			ПроверяемыеСотрудники.Добавить(Трудозатраты.ВыгрузитьКолонку("Исполнитель"));
			УволенныеСотрудники = ПроизводствоСервер.УволенныеСотрудники(ПроверяемыеСотрудники);
			
			ШаблонОшибкаУволенный = НСтр("ru = 'В поле ""Исполнитель"" в строке %1 списка ""Трудозатраты"" указан уволенный сотрудник.';
										|en = 'In the ""Assignee"" field, in line %1 of the ""Labor costs"" field, a dismissed employee is specified.'");
			
			Если УволенныеСотрудники.Количество() > 0 Тогда
				Для Каждого Строка Из Трудозатраты Цикл
					
					УволенныйСотрудник = УволенныеСотрудники.Найти(Строка.Исполнитель, "Сотрудник");
					Если УволенныйСотрудник <> Неопределено
						И ОкончаниеВыполнения >= УволенныйСотрудник.ДатаУвольнения Тогда
							
							ТекстСообщения = СтрШаблон(ШаблонОшибкаУволенный, Строка(Строка.НомерСтроки));
							
							Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Трудозатраты", Строка.НомерСтроки, "Исполнитель");
							
							ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, КлючДанных, Поле, ПутьКДанным, Отказ);
							
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Если ЗначениеЗаполнено(Исполнитель) И ТипЗнч(Исполнитель) = Тип("СправочникСсылка.Сотрудники") Тогда
			
			ТекстСообщения = НСтр("ru = 'В поле ""Исполнитель"" указан сотрудник. Необходимо указать физическое лицо.';
									|en = 'In the ""Assignee"" field, an employee is specified. Specify a person.'");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, КлючДанных, "Исполнитель", ПутьКДанным, Отказ);
			
		КонецЕсли;
		
		Если МассивНепроверяемыхРеквизитов.Найти("Трудозатраты.Исполнитель") = Неопределено Тогда
			
			ШаблонОшибкаСотрудник = НСтр("ru = 'В поле ""Исполнитель"" в строке %1 списка ""Трудозатраты"" указан сотрудник. Необходимо указать физическое лицо.';
										|en = 'In the ""Assignee"" field, in line %1 of the ""Labor costs"" list, an employee is specified. Specify a person.'");
			
			Для Каждого Строка Из Трудозатраты Цикл
				Если ЗначениеЗаполнено(Строка.Исполнитель)
					И ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.Сотрудники") Тогда
					
					ТекстСообщения = СтрШаблон(ШаблонОшибкаСотрудник, Строка(Строка.НомерСтроки));
					
					Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Трудозатраты", Строка.НомерСтроки, "Исполнитель");
				
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, КлючДанных, Поле, ПутьКДанным, Отказ);
					
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
//-- Локализация

Процедура ИсключитьРеквизитыТабличныхЧастей(ПроверяемыеРеквизиты)
	
	ИменаТЧ = ТабличныеЧасти();
	
	Для Индекс = -ПроверяемыеРеквизиты.ВГраница() По 0 Цикл
		ЧастиПути = СтрРазделить(ПроверяемыеРеквизиты[-Индекс], ".");
		Если ИменаТЧ.Найти(ЧастиПути[0]) <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(-Индекс);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеСвязанныхДанных

Функция ДанныеОперацииДоИзменения()
	
	СоставРеквизитов = 
		"Статус,
		|Этап,
		|Проведен,
		|ПометкаУдаления,
		|РабочийЦентр,
		|СменноеЗадание,
		|Исполнитель,
		|ВремяВыполнения,
		|ВремяВыполненияЕдИзм,
		|Количество,
		|КоличествоНаКонтроле,
		|КоличествоНаДоработке,
		|КоличествоБрак,
		|КоличествоОтменено,
		|КоличествоФакт,
		|ТребуетПовторения,
		|НомерОперации,
		|НомерСледующейОперации,
		|Протокол,
		|ИдентификаторОперации";
	
	Если НЕ ЭтоНовый() Тогда
		ДанныеДоИзменения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, СоставРеквизитов);
	Иначе
		ДанныеДоИзменения = Новый Структура(СоставРеквизитов);
		ЗаполнитьЗначенияСвойств(ДанныеДоИзменения, ЭтотОбъект);
	КонецЕсли;
	
	Возврат ДанныеДоИзменения;
	
КонецФункции

Процедура ДобавитьЗаданияКОбработкеЭтаповПроизводства()
	
	ИзменилсяЭтап = ИзменилисьДанныеДокумента("Этап", Ложь);
	БылПроведенИЗавершен = ДополнительныеСвойства.ДанныеДоИзменения.Проведен
		И ДополнительныеСвойства.ДанныеДоИзменения.Статус = Перечисления.СтатусыПроизводственныхОпераций.Выполнена;
	
	Если ИзменилсяЭтап
		И НЕ ДополнительныеСвойства.ДанныеДоИзменения.Этап.Пустая() Тогда
		
		Действия = РегистрыСведений.ЗаданияКОбработкеЭтаповПроизводства.СтруктураДействий();
		Действия.ОбновитьСостояние = Истина;
		Если БылПроведенИЗавершен Тогда
			Действия.ЗаполнитьПоОперациям = НЕ РежимРедактораПроизводственногоПроцесса();
		КонецЕсли;
		
		РегистрыСведений.ЗаданияКОбработкеЭтаповПроизводства.ДобавитьЗадание(
			ДополнительныеСвойства.ДанныеДоИзменения.Этап,
			Действия);
		
	КонецЕсли;
	
	Если НЕ Этап.Пустая() Тогда
		
		Действия = РегистрыСведений.ЗаданияКОбработкеЭтаповПроизводства.СтруктураДействий();
		Действия.ОбновитьСостояние = ИзменилисьДанныеДокумента(СписокРеквизитовОбновитьСостояниеЭтапа());
		Если (ПроведениеДокументов.СвойстваДокумента(ЭтотОбъект).РежимЗаписи = РежимЗаписиДокумента.Проведение
				И Статус = Перечисления.СтатусыПроизводственныхОпераций.Выполнена)
			ИЛИ (БылПроведенИЗавершен И НЕ ИзменилсяЭтап) Тогда
			
			Действия.ЗаполнитьПоОперациям = НЕ РежимРедактораПроизводственногоПроцесса();
			
		КонецЕсли;
		
		РегистрыСведений.ЗаданияКОбработкеЭтаповПроизводства.ДобавитьЗадание(Этап, Действия);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ИзменилисьДанныеДокумента(СписокРеквизитов = "", ПроверитьСостояние = Истина)
	
	ДанныеДоИзменения = ДополнительныеСвойства.ДанныеДоИзменения;
	СвойстваДокумента = ПроведениеДокументов.СвойстваДокумента(ЭтотОбъект);
	
	Если ПроверитьСостояние Тогда
		Если (ДанныеДоИзменения.Проведен
				И СвойстваДокумента.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения)
			ИЛИ (НЕ ДанныеДоИзменения.Проведен
				И СвойстваДокумента.РежимЗаписи = РежимЗаписиДокумента.Проведение)
			ИЛИ ЭтоНовый() Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(СписокРеквизитов) Тогда
		МассивРеквизитов = СтрРазделить(СписокРеквизитов, ",");
		Для каждого ИмяРеквизита Из МассивРеквизитов Цикл
			Если ЭтотОбъект[ИмяРеквизита] <> ДанныеДоИзменения[ИмяРеквизита] Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция СписокРеквизитовОбновитьСостояниеЭтапа()
	
	Результат = "Этап,Статус,Количество,КоличествоФакт,КоличествоБрак";
	Возврат Результат;
	
КонецФункции

Функция СписокРеквизитовПересчитатьОчередьОпераций()
	
	Результат = "Статус,Количество,ТребуетПовторения,ВремяВыполнения,ВремяВыполненияЕдИзм,
	|КоличествоНаКонтроле,КоличествоНаДоработке,КоличествоБрак,КоличествоОтменено,КоличествоФакт,
	|НомерОперации,НомерСледующейОперации,РабочийЦентр,Исполнитель,СменноеЗадание,
	|ИдентификаторОперации";
	Возврат СтрЗаменить(Результат, Символы.ПС, "");
	
КонецФункции

Функция СписокРеквизитовЗаполняемыеПоляОчереди()
	
	Результат = "Этап,Подразделение,Операция,ИдентификаторОперации,НомерОперации,НомерСледующейОперации,
	|СменноеЗадание,РабочийЦентр,Исполнитель";
	Возврат СтрЗаменить(Результат, Символы.ПС, "");
	
КонецФункции

Процедура ПересчитатьОчередьОпераций(Отказ)

	Если Отказ
		ИЛИ НЕ ИзменилисьДанныеДокумента(СписокРеквизитовПересчитатьОчередьОпераций()) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ НаОснованииНСИ Тогда
		
		Набор = РегистрыСведений.ОчередьПроизводственныхОпераций.СоздатьНаборЗаписей();
		Набор.Отбор.ПроизводственнаяОперация.Установить(Ссылка);
		
		Если Проведен
			И ВключенаВТехпроцесс > 0
			И НЕ РежимРедактораПроизводственногоПроцесса() Тогда
			
			ПараметрыТехпроцесса = РедакторПроизводственногоПроцессаВызовСервера.ПараметрыТехпроцессаЭтапа(Этап);
			Если ПараметрыТехпроцесса.ТипТехнологическогоПроцесса <> Перечисления.ТипыТехнологическихПроцессовЭтапа.Индивидуальный Тогда
				ОбщегоНазначения.СообщитьПользователю(
					РедакторПроизводственногоПроцессаКлиентСервер.ТекстПереключитьТехпроцессВИндивидуальныйРежим(),,,,Отказ);
				Возврат;
			КонецЕсли;
			
			Запись = Набор.Добавить();
			
			Запись.ПроизводственнаяОперация = Ссылка;
			Запись.Распоряжение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Этап, "Распоряжение");
			ЗаполнитьЗначенияСвойств(Запись, ЭтотОбъект, СписокРеквизитовЗаполняемыеПоляОчереди());
			Запись.Требуется = Количество - КоличествоОтменено;
			
			СписокРеквизитов = Новый Структура;
			СписокРеквизитов.Вставить("Статус", "Статус");
			СписокРеквизитов.Вставить("Проведен", "Проведен");
			СписокРеквизитов.Вставить("ИспользоватьПооперационноеПланирование", "Подразделение.ИспользоватьПооперационноеПланирование");
			СписокРеквизитов.Вставить("ИспользоватьСменныеЗадания", "Подразделение.ИспользоватьСменныеЗадания");
			ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Этап, СписокРеквизитов);
			
			Запись.ВАрхиве = Документы.ЭтапПроизводства2_2.РеквизитОчередьВАрхиве(ЗначенияРеквизитов);
			Запись.КоэффициентТехнологическогоПроцесса = ПараметрыТехпроцесса.КоэффициентТехнологическогоПроцесса;
			
		КонецЕсли;
		
		Набор.Записать();
		
	КонецЕсли;
	
	Если РежимГрупповойОбработки() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРасчетаОчереди = РегистрыСведений.ОчередьПроизводственныхОпераций.ПараметрыРасчетаОчереди();
	
	Если НаОснованииНСИ Тогда
		
		КлючОперации = УправлениеПроизводствомКлиентСервер.КлючПроизводственнойОперации();
		ЗаполнитьЗначенияСвойств(КлючОперации, ЭтотОбъект);
		
		ПараметрыРасчетаОчереди.РассчитыватьВсеПоследующиеОперации = ИзменилисьДанныеДокумента("КоличествоБрак", Ложь)
			ИЛИ ИзменилисьДанныеДокумента() И КоличествоБрак > 0;
		
		РегистрыСведений.ОчередьПроизводственныхОпераций.ПересчитатьОчередь(КлючОперации, ПараметрыРасчетаОчереди, Отказ);
		
		Если НаОснованииПланирования Тогда
			РегистрыСведений.ПооперационноеРасписание2_2.ПересчитатьКоличественныеРесурсыПоКлючу(КлючОперации, НомерПартии, Отказ);
		КонецЕсли;
		
	ИначеЕсли ВключенаВТехпроцесс > 0 Тогда
		
		ДанныеЭтапа = Документы.ЭтапПроизводства2_2.ДанныеДляРасчетаОчередиОпераций(Этап, Истина)[Этап];
		РегистрыСведений.ОчередьПроизводственныхОпераций.РассчитатьОчередьПоДаннымЭтапа(Этап, ДанныеЭтапа, ПараметрыРасчетаОчереди);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ЗаполнитьНомер()
	
	Если НЕ ЗначениеЗаполнено(Этап)
		ИЛИ (ЗначениеЗаполнено(Номер) И ДополнительныеСвойства.ДанныеДоИзменения.Этап = Этап) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МАКСИМУМ(ПроизводственнаяОперация2_2.НомерОперацииЭтапа) КАК НомерОперацииЭтапа
	|ПОМЕСТИТЬ ВТНомерОперацииЭтапа
	|ИЗ
	|	Документ.ПроизводственнаяОперация2_2 КАК ПроизводственнаяОперация2_2
	|ГДЕ
	|	ПроизводственнаяОперация2_2.Этап = &Этап
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭтапПроизводства2_2.Номер КАК НомерЭтапа,
	|	ЕСТЬNULL(ВТНомерОперацииЭтапа.НомерОперацииЭтапа, 0) КАК НомерОперацииЭтапа
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНомерОперацииЭтапа КАК ВТНомерОперацииЭтапа
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ЭтапПроизводства2_2.Ссылка = &Этап");
	
	Запрос.УстановитьПараметр("Этап", Этап);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	НомерОперацииЭтапа = Выборка.НомерОперацииЭтапа + 1;
	Номер = "" + Выборка.НомерЭтапа + "." + Формат(НомерОперацииЭтапа, "ЧГ=0");
	
КонецПроцедуры

Процедура ЗаполнитьИдентификатор(РежимЗаписи)
	
	Если НаОснованииНСИ
			ИЛИ НЕ ВключенаВТехпроцесс Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение И ИдентификаторОперации = 0 Тогда
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВложенныйЗапрос.ИдентификаторОперации
		|ИЗ
		|	(ВЫБРАТЬ
		|		0 КАК ИдентификаторОперации
		|	ГДЕ
		|		&ИдентификаторОперации = 0
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Т1.ИдентификаторОперации
		|	ИЗ
		|		Документ.ПроизводственнаяОперация2_2 КАК Т1
		|	ГДЕ
		|		Т1.Этап = &Этап
		|		И Т1.Проведен
		|		И (&ИдентификаторОперации = 0
		|				ИЛИ ИСТИНА В
		|					(ВЫБРАТЬ ПЕРВЫЕ 1
		|						ИСТИНА
		|					ИЗ
		|						Документ.ПроизводственнаяОперация2_2 КАК Т2
		|
		|						ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК Т3
		|						ПО Т3.Этап = &Этап
		|							И Т3.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)
		|							И Т3.ИдентификаторОперации = &ИдентификаторОперации
		|						
		|					ГДЕ
		|						Т2.Этап = &Этап
		|						И Т2.Проведен
		|						И Т2.ИдентификаторОперации = &ИдентификаторОперации
		|						И Т2.Ссылка <> &Ссылка
		|						И Т3.ИдентификаторОперации IS NULL))
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Т1.ИдентификаторОперации
		|	ИЗ
		|		РегистрСведений.ОчередьПроизводственныхОпераций КАК Т1
		|	ГДЕ
		|		Т1.Этап = &Этап
		|		И (&ИдентификаторОперации = 0
		|				ИЛИ ИСТИНА В
		|					(ВЫБРАТЬ ПЕРВЫЕ 1
		|						ИСТИНА
		|					ИЗ
		|						РегистрСведений.ОчередьПроизводственныхОпераций КАК Т2
		|
		|						ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК Т3
		|						ПО Т3.Этап = &Этап
		|							И Т3.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)
		|							И Т3.ИдентификаторОперации = &ИдентификаторОперации
		|						
		|					ГДЕ
		|						Т2.Этап = &Этап
		|						И Т2.ИдентификаторОперации = &ИдентификаторОперации
		|						И Т2.ПроизводственнаяОперация <> &Ссылка
		|						И Т3.ИдентификаторОперации IS NULL))) КАК ВложенныйЗапрос
		|	
		|УПОРЯДОЧИТЬ ПО
		|	ИдентификаторОперации УБЫВ
		|");
		
		Запрос.УстановитьПараметр("Этап", Этап);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("ИдентификаторОперации", ИдентификаторОперации);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ИдентификаторОперации = Выборка.ИдентификаторОперации + 1;
		КонецЕсли;
		
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		
		ИдентификаторОперации = 0;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьРежимГрупповойОбработки() Экспорт
	
	ДополнительныеСвойства.Вставить("КлючРежимГрупповойОбработки");
	
КонецПроцедуры

// Операция находится в режиме групповой обработки
// 
// Возвращаемое значение:
//  Булево
Функция РежимГрупповойОбработки() Экспорт

	Возврат ДополнительныеСвойства.Свойство("КлючРежимГрупповойОбработки");
	
КонецФункции

Функция ОтборОперации(ДанныеЗаполнения = Неопределено)
	
	Результат = Новый Структура("Операция,ИдентификаторОперации", Операция, ИдентификаторОперации);
	
	Если ДанныеЗаполнения <> Неопределено Тогда
		
		Если ДанныеЗаполнения.Свойство("КлючОперации") Тогда
			ЗаполнитьЗначенияСвойств(Результат, ДанныеЗаполнения.КлючОперации);
		Иначе
			ЗаполнитьЗначенияСвойств(Результат, ДанныеЗаполнения);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура УстановитьРежимРазбиения() Экспорт
	
	ДополнительныеСвойства.Вставить("Разбиение");
	
КонецПроцедуры

// Операция находится в режиме разбиения
// 
// Возвращаемое значение:
//  Булево
Функция РежимРазбиения() Экспорт
	
	Возврат ДополнительныеСвойства.Свойство("Разбиение");
	 
КонецФункции

Процедура УстановитьРежимРедактораПроизводственногоПроцесса() Экспорт
	
	ДополнительныеСвойства.Вставить("КлючРежимРедактораПроизводственногоПроцесса");
	
КонецПроцедуры

// Операция находится в режиме записи из редактора производственного процесса
// 
// Возвращаемое значение:
//  Булево
Функция РежимРедактораПроизводственногоПроцесса() Экспорт
	
	Возврат ДополнительныеСвойства.Свойство("КлючРежимРедактораПроизводственногоПроцесса");
	
КонецФункции

Процедура УстановитьКлючВСтрокахТабличныхЧастей(Отказ)
	
	ЗаказыСервер.УстановитьКлючВСтрокахТабличнойЧасти(
		ЭтотОбъект, "Трудозатраты", "МаксимальныйКодСтрокиТрудозатраты");
	
КонецПроцедуры

Процедура ВосстановитьИсполнителейТрудозатрат()
	
	ТрудозатратыДоПересчета = Неопределено; // ТаблицаЗначений
	
	Если НЕ ДополнительныеСвойства.Свойство("Трудозатраты", ТрудозатратыДоПересчета) Тогда
		Возврат;
	КонецЕсли;
	
	ТрудозатратыРезультат = Трудозатраты.ВыгрузитьКолонки();
	
	ТрудозатратыДоПересчета.Свернуть("ВидРабот,Исполнитель,НазначениеРабот", "Количество");
	ТрудозатратыДоПересчета.Индексы.Добавить("ВидРабот");
	
	СтруктураПоиска = Новый Структура("ВидРабот");
	
	Пока Трудозатраты.Количество() > 0 Цикл
		
		ДанныеСтроки = Трудозатраты[0];
		Если ДанныеСтроки.Количество = 0 Тогда
			Трудозатраты.Удалить(0);
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТрудозатратыРезультат.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСтроки);
		
		СтруктураПоиска.ВидРабот = ДанныеСтроки.ВидРабот;
		
		Для каждого НайденнаяСтрока Из ТрудозатратыДоПересчета.НайтиСтроки(СтруктураПоиска) Цикл
			Если НайденнаяСтрока.Количество > 0 
				И (ЗначениеЗаполнено(НайденнаяСтрока.Исполнитель)
					ИЛИ ЗначениеЗаполнено(НайденнаяСтрока.НазначениеРабот)) Тогда
				
				Зачесть = Мин(ДанныеСтроки.Количество, НайденнаяСтрока.Количество);
				
				НоваяСтрока.Количество      = Зачесть;
				НоваяСтрока.Исполнитель     = НайденнаяСтрока.Исполнитель;
				НоваяСтрока.НазначениеРабот = НайденнаяСтрока.НазначениеРабот;
				
				НайденнаяСтрока.Количество  = НайденнаяСтрока.Количество - Зачесть;
				
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		ДанныеСтроки.Количество = ДанныеСтроки.Количество - НоваяСтрока.Количество;
		
	КонецЦикла;
	
	Трудозатраты.Загрузить(ТрудозатратыРезультат);
	
КонецПроцедуры

Процедура ВосстановитьСерии()
	
	ТаблицаДоПересчета = Неопределено; // ТаблицаЗначений
	
	Для каждого ИмяТЧ Из СтрРазделить("ВыходныеИзделия,МатериалыИРаботы", ",") Цикл
		
		Если НЕ ДополнительныеСвойства.Свойство(ИмяТЧ, ТаблицаДоПересчета) Тогда
			Продолжить;
		КонецЕсли;
		
		ТаблицаДоПересчета.Свернуть("Номенклатура,Характеристика,Серия,Упаковка", "Количество,КоличествоУпаковок");
		
		ВосстановитьСерииВТаблице(ЭтотОбъект[ИмяТЧ], ТаблицаДоПересчета);
		
	КонецЦикла;
	
	Если ДополнительныеСвойства.Свойство("ВыходныеИзделияСерии", ТаблицаДоПересчета)
		И ТаблицаДоПересчета.Количество() > 0 Тогда
		
		ВыходныеИзделияСерии.Загрузить(ВыходныеИзделия.Выгрузить());
		ВыходныеИзделияСерии.Свернуть("Номенклатура,Характеристика,Количество");
		
		ВосстановитьСерииВТаблице(ВыходныеИзделияСерии, ТаблицаДоПересчета, Ложь, Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВосстановитьСерииВТаблице(ТаблицаТекущая, ТаблицаДоПересчета, ЕстьУпаковки = Истина, ОставитьСтрокиБезСерии = Истина)
	
	РеквизитыУпаковки = "Номенклатура,Упаковка";
	РеквизитыПоиска   = "Номенклатура,Характеристика";
	СтруктураПоиска   = Новый Структура(РеквизитыПоиска);
	
	ТаблицаДоПересчета.Индексы.Добавить(РеквизитыПоиска);
	ТаблицаРезультат = ТаблицаТекущая.ВыгрузитьКолонки(); // ТаблицаЗначений
	
	Если ЕстьУпаковки Тогда
		НоменклатураУпаковка = ТаблицаРезультат.СкопироватьКолонки(РеквизитыУпаковки);
		ОбщегоНазначенияУТ.ДобавитьСтрокиВТаблицу(НоменклатураУпаковка, ТаблицаТекущая);
		ОбщегоНазначенияУТ.ДобавитьСтрокиВТаблицу(НоменклатураУпаковка, ТаблицаДоПересчета);
		КоэффициентыУпаковок = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентыУпаковок(НоменклатураУпаковка);
	КонецЕсли;
	
	Пока ТаблицаТекущая.Количество() > 0 Цикл
		
		ДанныеСтроки = ТаблицаТекущая[0];
		Если ДанныеСтроки.Количество = 0 Тогда
			ТаблицаТекущая.Удалить(0);
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаРезультат.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСтроки);
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ДанныеСтроки, РеквизитыПоиска);
		
		Для каждого НайденнаяСтрока Из ТаблицаДоПересчета.НайтиСтроки(СтруктураПоиска) Цикл
			Если НайденнаяСтрока.Количество > 0
				И ЗначениеЗаполнено(НайденнаяСтрока.Серия) Тогда
				
				Зачесть = Мин(ДанныеСтроки.Количество, НайденнаяСтрока.Количество);
				
				НоваяСтрока.Количество     = Зачесть;
				НоваяСтрока.Серия          = НайденнаяСтрока.Серия;
				НайденнаяСтрока.Количество = НайденнаяСтрока.Количество - Зачесть;
				
				Если ЕстьУпаковки Тогда
					Коэффициент = КоэффициентыУпаковок[НайденнаяСтрока.Номенклатура][НайденнаяСтрока.Упаковка];
					НоваяСтрока.КоличествоУпаковок = Зачесть / ?(ЗначениеЗаполнено(Коэффициент), Коэффициент, 1);
					НоваяСтрока.Упаковка           = НайденнаяСтрока.Упаковка;
				КонецЕсли;
				
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		ДанныеСтроки.Количество = ДанныеСтроки.Количество - НоваяСтрока.Количество;
		
		Если ЕстьУпаковки Тогда
			Коэффициент = КоэффициентыУпаковок[ДанныеСтроки.Номенклатура][ДанныеСтроки.Упаковка];
			ДанныеСтроки.КоличествоУпаковок = ДанныеСтроки.КоличествоУпаковок - НоваяСтрока.Количество / ?(ЗначениеЗаполнено(Коэффициент), Коэффициент, 1);
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаТекущая.Очистить();
	Для Индекс = 0 По ТаблицаРезультат.Количество() - 1 Цикл
		ДанныеСтроки = ТаблицаРезультат[Индекс];
		Если ОставитьСтрокиБезСерии
			ИЛИ ЗначениеЗаполнено(ДанныеСтроки.Серия) Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаТекущая.Добавить(), ДанныеСтроки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ОписаниеТаблицДляПересчетаНормативов()
	
	КоличествоОперацийИзделия = ?(Статус = Перечисления.СтатусыПроизводственныхОпераций.Выполнена,
								КоличествоФакт + КоличествоНаДоработке + КоличествоНаКонтроле,
								Количество - КоличествоБрак - КоличествоОтменено);
	КоличествоОпераций        = ?(Статус = Перечисления.СтатусыПроизводственныхОпераций.Выполнена,
								КоличествоФакт + КоличествоБрак + КоличествоНаДоработке + КоличествоНаКонтроле,
								Количество - КоличествоОтменено);
								
	СписокРеквизитов      = "ИмяТЧ,Изделие,ЕстьУпаковки,КоличествоОпераций";
	
	ОписаниеТаблиц = Новый Структура;
	ОписаниеТаблиц.Вставить("ВыходныеИзделия",  Новый Структура(СписокРеквизитов, "ВыходныеИзделия",  Истина, Истина, КоличествоОперацийИзделия));
	ОписаниеТаблиц.Вставить("ВозвратныеОтходы", Новый Структура(СписокРеквизитов, "ВозвратныеОтходы", Истина, Истина, КоличествоОпераций));
	ОписаниеТаблиц.Вставить("МатериалыИУслуги", Новый Структура(СписокРеквизитов, "Материалы",        Ложь,   Истина, КоличествоОпераций));
	ОписаниеТаблиц.Вставить("Трудозатраты",     Новый Структура(СписокРеквизитов, "Трудозатраты",     Ложь,   Ложь,   КоличествоОпераций));
	
	Возврат ОписаниеТаблиц;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
