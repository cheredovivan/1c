
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Документ.Ссылка                                 КАК Ссылка,
	|	Документ.Дата                                   КАК Дата,
	|	Документ.Номер                                  КАК Номер,
	|	Документ.Подразделение                          КАК Подразделение,
	|	Документ.Этап                                   КАК Этап,
	|	Документ.Операция                               КАК Операция,
	|	Документ.ИдентификаторОперации                  КАК ИдентификаторОперации,
	|	Документ.Исполнитель                            КАК Исполнитель,
	|	Документ.Исполнитель                            КАК ИсполнительНов,
	|	Документ.Участок                                КАК Участок,
	|	Документ.Участок                                КАК УчастокНов,
	|	ВЫБОР
	|		КОГДА Документ.РабочийЦентр ССЫЛКА Справочник.ВидыРабочихЦентров
	|			ТОГДА Документ.РабочийЦентр
	|		ИНАЧЕ
	|			Документ.РабочийЦентр.ВидРабочегоЦентра
	|	КОНЕЦ                                                                          КАК ВидРабочегоЦентра,
	|	ВЫБОР
	|		КОГДА Документ.РабочийЦентр ССЫЛКА Справочник.ВидыРабочихЦентров
	|			ТОГДА Документ.РабочийЦентр
	|		ИНАЧЕ
	|			Документ.РабочийЦентр.ВидРабочегоЦентра
	|	КОНЕЦ                                                                          КАК ВидРабочегоЦентраНов,
	|	ВЫБОР
	|		КОГДА Документ.РабочийЦентр ССЫЛКА Справочник.РабочиеЦентры
	|			ТОГДА Документ.РабочийЦентр
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.РабочиеЦентры.ПустаяСсылка)
	|	КОНЕЦ                                                                          КАК РабочийЦентр,
	|	ВЫБОР
	|		КОГДА Документ.РабочийЦентр ССЫЛКА Справочник.РабочиеЦентры
	|			ТОГДА Документ.РабочийЦентр
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.РабочиеЦентры.ПустаяСсылка)
	|	КОНЕЦ                                                                          КАК РабочийЦентрНов,
	|
	|	&ТекстКоличествоВсего                                                          КАК КоличествоВсего,
	|	&ТекстКоличествоКВыполнению                                                    КАК КоличествоКВыполнению,
	|	&ТекстКоличествоКВыполнению                                                    КАК Количество,
	|	ЕСТЬNULL(Документ.Операция.Количество, 1)                                      КАК КоличествоШтучное,
	|	ЕСТЬNULL(Документ.Операция.ЕдиницаИзмерения, Документ.ЕдиницаИзмерения)        КАК ЕдиницаИзмерения,
	|
	|	Документ.ВремяШтучное                                                          КАК ВремяШтучное,
	|	Документ.ВремяШтучноеЕдИзм                                                     КАК ВремяШтучноеЕдИзм,
	|	Документ.ВремяПЗ                                                               КАК ВремяПЗ,
	|	Документ.ВремяПЗЕдИзм                                                          КАК ВремяПЗЕдИзм,
	|	Документ.ВремяВыполнения                                                       КАК ВремяВыполненияВсего,
	|	Документ.ВремяВыполненияЕдИзм                                                  КАК ВремяВыполненияЕдИзм
	|
	|ИЗ
	|	Документ.ПроизводственнаяОперация2_2  КАК Документ
	|ГДЕ
	|	Документ.Ссылка = &Ссылка";
	
	ТекстКоличествоВсего = "Документ.Количество
	|		- Документ.КоличествоОтменено";
	
	ТекстКоличествоКВыполнению = "Документ.Количество
	|		- Документ.КоличествоОтменено
	|		- Документ.КоличествоНаКонтроле
	|		- Документ.КоличествоНаДоработке
	|		- Документ.КоличествоБрак
	|		- Документ.КоличествоФакт";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстКоличествоВсего",       ТекстКоличествоВсего);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстКоличествоКВыполнению", ТекстКоличествоКВыполнению);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Параметры.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ЗаполнитьРеквизитыРЦ();
	
	ПересчитатьВремяПриИзмененииКоличества(ЭтотОбъект);
	
	ЗаполнитьПредставлениеЕдиницыИзмерения(ЭтотОбъект);
	
	Заголовок = СтрШаблон(НСтр("ru = 'Деление операции %1';
								|en = 'Operation division %1'"), Выборка.Номер);
	
	Элементы.КоличествоНов.МаксимальноеЗначение = КоличествоКВыполнению;
	Элементы.ВремяВыполненияНов.МаксимальноеЗначение = ВремяВыполнения;
	
	НастроитьВидимостьЭлементовУчасток(Выборка.Подразделение);
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		АдаптироватьПодМобильныйКлиент();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДанныеРазблокированы Тогда
		РазблокироватьДанные(Ссылка, ВладелецФормы.УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Количество = 0 Тогда
		ОчиститьСообщения();
		ТекстСообщения = НСтр("ru = 'Нет количества, доступного для отделения в другую операцию.';
								|en = 'There is no quantity available to be separated into another operation.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		РазблокироватьДанные(Ссылка, ВладелецФормы.УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Разбить(Команда)
	
	Если КоличествоНов = 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'Введены некорректные данные';
								|en = 'Incorrect data entered'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"КоличествоНов");
		Возврат;
		
	КонецЕсли;
	
	Если Количество = 0 И КоличествоКВыполнению = КоличествоВсего Тогда
		
		ТекстСообщения = НСтр("ru = 'Введены некорректные данные';
								|en = 'Incorrect data entered'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"Количество");
		Возврат;
		
	КонецЕсли;
	
	РазбитьНаСервере(ВладелецФормы.УникальныйИдентификатор);
	
	ОповеститьОбИзменении(Ссылка);
	
	КлючОперации = УправлениеПроизводствомКлиентСервер.КлючПроизводственнойОперации();
	ЗаполнитьЗначенияСвойств(КлючОперации, ЭтотОбъект);
	
	ПараметрОповещения = Новый Структура;
	ПараметрОповещения.Вставить("Подразделение", Подразделение);
	ПараметрОповещения.Вставить("КлючОперации",  КлючОперации);
	ПараметрОповещения.Вставить("Ссылка",        Ссылка);
	Оповестить("Запись_ПроизводственнаяОперация2_2", ПараметрОповещения, УникальныйИдентификатор);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИсполнительНовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИсполнительНовНачалоВыбораЗавершение", ЭтотОбъект);
	
	ПроизводствоКлиент.ОткрытьФормуВыбораИсполнителя(
		Организация,
		Подразделение,
		ИсполнительНов,
		Дата,
		,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительНовАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ИсполнительПолучениеДанныхВыбора(ДанныеВыбора, Текст, Подразделение, Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительНовОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ИсполнительПолучениеДанныхВыбора(ДанныеВыбора, Текст, Подразделение, Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидРабочегоЦентраНовПриИзменении(Элемент)
	
	ЗаполнитьРеквизитыРЦ();
	
КонецПроцедуры

&НаКлиенте
Процедура РабочийЦентрНовПриИзменении(Элемент)
	
	ЗаполнитьРеквизитыРЦ();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоНовПриИзменении(Элемент)
	
	Количество = КоличествоКВыполнению - КоличествоНов;
	
	ПересчитатьВремяПриИзмененииКоличества(ЭтотОбъект);
	
	ЗаполнитьПредставлениеЕдиницыИзмерения(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВремяВыполненияНовПриИзменении(Элемент)
	
	Если ВремяШтучное <> 0 ИЛИ ВремяПЗ <> 0 Тогда
	
		КоличествоНов = ОперативныйУчетПроизводстваКлиентСервер.РассчитатьКоличествоОперацииОтВремени(
			ВремяВыполненияНов,
			ВремяШтучное,
			ВремяПЗ,
			КоличествоШтучное,
			РеквизитыРЦНов.КоэффициентВремениРаботы);
			
	Иначе
		
		КоличествоНов = Количество * ВремяВыполненияНов / ВремяВыполненияВсего;
		
	КонецЕсли;
		
	Количество = КоличествоВсего - КоличествоНов;
	
	ПересчитатьВремяПриИзмененииКоличества(ЭтотОбъект);
	
	ЗаполнитьПредставлениеЕдиницыИзмерения(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьВидимостьЭлементовУчасток(Подразделение)
	
	Если ЗначениеЗаполнено(Подразделение) Тогда
		
		ОтборФункциональныхОпций = Новый Структура("Подразделение", Подразделение); 
		УстановитьПараметрыФункциональныхОпцийФормы(ОтборФункциональныхОпций);
		Элементы.ДекорацияУчасток.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьПроизводственныеУчастки", ОтборФункциональныхОпций);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительНовНачалоВыбораЗавершение(Результат, Параметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		
		ИсполнительНов = Результат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеЕдиницыИзмерения(Форма)

	Форма.ЕдиницаИзмеренияПредставление = УправлениеПроизводствомКлиентСервер.ПредставлениеЕдиницыИзмеренияОперации(
		Форма.ЕдиницаИзмерения,
		Форма.Количество);
		
	Форма.ЕдиницаИзмеренияПредставлениеНов = УправлениеПроизводствомКлиентСервер.ПредставлениеЕдиницыИзмеренияОперации(
		Форма.ЕдиницаИзмерения,
		Форма.КоличествоНов);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИсполнительПолучениеДанныхВыбора(ДанныеВыбора, Текст, Подразделение, Ссылка)
	
	ДанныеВыбора = Новый СписокЗначений;
	ПараметрыОтбора = Новый Структура("Организация, Подразделение, Дата");
	ПараметрыОтбора.Подразделение = Подразделение;
	ПараметрыОтбора.Дата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Дата");
	ПроизводствоСервер.ЗаполнитьДанныеВыбораПриВводеИсполнителя(ДанныеВыбора, Текст, ПараметрыОтбора);
	
КонецПроцедуры

&НаСервере
Процедура РазбитьНаСервере(ИдентификаторФормы)
	
	ЗначенияЗаполнения = Документы.ПроизводственнаяОперация2_2.ПараметрыРазбиенияОперацииКонструктор();
	ЗначенияЗаполнения.Исполнитель       = ИсполнительНов;
	ЗначенияЗаполнения.Участок           = УчастокНов;
	ЗначенияЗаполнения.РабочийЦентр      = ?(ЗначениеЗаполнено(РабочийЦентрНов), РабочийЦентрНов, ВидРабочегоЦентраНов);
	ЗначенияЗаполнения.Количество        = КоличествоНов;
	ЗначенияЗаполнения.ВремяВыполнения   = ВремяВыполненияНов;
	
	Попытка
		
		Документы.ПроизводственнаяОперация2_2.Разделить(Ссылка, ЗначенияЗаполнения, Ложь);
		
	Исключение
		
		РазблокироватьДанныеДляРедактирования(Ссылка, ИдентификаторФормы);
		ДанныеРазблокированы = Истина;
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Не удалось выполнить действие по причине: %1';
				|en = 'Cannot perform the action. Reason: %1'"),
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	РазблокироватьДанныеДляРедактирования(Ссылка, ИдентификаторФормы);
	ДанныеРазблокированы = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура РазблокироватьДанные(Ссылка, ИдентификаторФормы)
	
	РазблокироватьДанныеДляРедактирования(Ссылка, ИдентификаторФормы);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьПодМобильныйКлиент()
	
	Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	Заголовок = НСтр("ru = 'Новая операция';
					|en = 'New operation'");
	
	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Верх;
	
	Элементы.ФормаРазбить.Отображение = ОтображениеКнопки.Картинка;
	Элементы.ФормаРазбить.Картинка    = БиблиотекаКартинок.ЗаписатьИЗакрыть;
	
	Элементы.Группа4.ОтображатьЗаголовок = Ложь;
	
	Элементы.Группа2.Видимость = Ложь;
	Элементы.Группа5.Видимость = Ложь;
	
	Элементы.ИсполнительНов.ПоложениеЗаголовка       = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.УчастокНов.ПоложениеЗаголовка           = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ВидРабочегоЦентраНов.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.РабочийЦентрНов.ПоложениеЗаголовка      = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.КоличествоНов.ПоложениеЗаголовка        = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ВремяВыполненияНов.ПоложениеЗаголовка   = ПоложениеЗаголовкаЭлементаФормы.Лево;
	
	Элементы.ВремяВыполненияНов.Ширина = 0;
	Элементы.ВремяВыполненияНов.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ВремяВыполненияЕдИзм1.Ширина = 0;
	Элементы.ВремяВыполненияЕдИзм1.АвтоМаксимальнаяШирина = Ложь;
	Элементы.ВремяВыполненияЕдИзм1.МаксимальнаяШирина = 7;
	Элементы.ВремяВыполненияЕдИзм1.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
	
	Элементы.КоличествоНов.Ширина = 0;
	Элементы.КоличествоНов.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ЕдиницаИзмеренияПредставлениеНов.Ширина = 0;
	Элементы.ЕдиницаИзмеренияПредставлениеНов.АвтоМаксимальнаяШирина = Ложь;
	Элементы.ЕдиницаИзмеренияПредставлениеНов.МаксимальнаяШирина = 7;
	Элементы.ЕдиницаИзмеренияПредставлениеНов.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
	
	Элементы.ВидРабочегоЦентраНов.Заголовок = НСтр("ru = 'Вид РЦ';
													|en = 'Work center type'");
	Элементы.РабочийЦентрНов.Заголовок      = НСтр("ru = 'Рабочий центр';
													|en = 'Work center'");
	Элементы.УчастокНов.Заголовок           = НСтр("ru = 'Участок';
													|en = 'Area'");
	
	Если НЕ КоличествоКВыполнению = КоличествоВсего Тогда
		
		КоличествоНов      = Количество;
		Количество         = 0;
		ВремяВыполненияНов = ВремяВыполнения;
		ВремяВыполнения    = 0;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПересчитатьВремяПриИзмененииКоличества(Форма)
	
	Если Форма.ВремяШтучное <> 0 ИЛИ Форма.ВремяПЗ <> 0 Тогда
		
		Если Форма.Количество = Форма.КоличествоВсего Тогда
			
			Форма.ВремяВыполнения = Форма.ВремяВыполненияВсего;
			
		Иначе
		
			Форма.ВремяВыполнения = ОперативныйУчетПроизводстваКлиентСервер.РассчитатьВремяОперацииОтКоличества(
					Форма.Количество,
					Форма.КоличествоШтучное,
					Форма.ВремяШтучное,
					Форма.ВремяПЗ,
					Форма.РеквизитыРЦ.ПараллельнаяЗагрузка,
					Форма.РеквизитыРЦ.КоэффициентВремениРаботы);
				
		КонецЕсли;
		
		Форма.ВремяВыполненияНов = ОперативныйУчетПроизводстваКлиентСервер.РассчитатьВремяОперацииОтКоличества(
				Форма.КоличествоНов,
				Форма.КоличествоШтучное,
				Форма.ВремяШтучное,
				Форма.ВремяПЗ,
				Форма.РеквизитыРЦНов.ПараллельнаяЗагрузка,
				Форма.РеквизитыРЦНов.КоэффициентВремениРаботы);
		
	Иначе
		
		Форма.ВремяВыполнения = Форма.ВремяВыполненияВсего * Форма.Количество / Форма.КоличествоВсего;
		
		Форма.ВремяВыполненияНов = Форма.ВремяВыполненияВсего * Форма.КоличествоНов / Форма.КоличествоВсего;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыРЦ()
	
	РеквизитыРЦ = ОперативныйУчетПроизводства.РеквизитыРабочегоЦентра(
		?(ЗначениеЗаполнено(РабочийЦентр), РабочийЦентр, ВидРабочегоЦентра));
		
	РеквизитыРЦНов = ОперативныйУчетПроизводства.РеквизитыРабочегоЦентра(
		?(ЗначениеЗаполнено(РабочийЦентрНов), РабочийЦентрНов, ВидРабочегоЦентраНов));
	
КонецПроцедуры

#КонецОбласти
