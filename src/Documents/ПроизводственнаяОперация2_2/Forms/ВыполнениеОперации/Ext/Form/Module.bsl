
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Режим",                    Режим);
	Параметры.Свойство("ПроизводственнаяОперация", ПроизводственнаяОперация);
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Операция");
	СтруктураРеквизитов.Вставить("ОперацияНаименование", "Наименование");
	СтруктураРеквизитов.Вставить("ОперацияКакРаспоряжениеВыработки");
	СтруктураРеквизитов.Вставить("КоличествоКВыполнению");
	СтруктураРеквизитов.Вставить("ИдентификаторОперации");
	
	Для каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		Если Параметры.Свойство(КлючИЗначение.Ключ, ЭтотОбъект[КлючИЗначение.Ключ]) Тогда
			СтруктураРеквизитов.Удалить(КлючИЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Если СтруктураРеквизитов.Количество() > 0 Тогда
		ЗначениеРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПроизводственнаяОперация, СтруктураРеквизитов);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначениеРеквизитов);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ОперацияНаименование) Тогда
		ОперацияНаименование = Операция;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("ЕдиницаИзмеренияПредставление", ЕдиницаИзмеренияПредставление) Тогда
		ЕдиницаИзмеренияПредставление = УправлениеПроизводствомКлиентСервер.ПредставлениеЕдиницыИзмеренияОперации(
			Неопределено,
			КоличествоКВыполнению);
	КонецЕсли;
	
	ЗаполнитьСписокПричинБрака();
	
	ЗаполнитьСписокПредыдущихОпераций(Параметры);
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	РаспределитьОстаток("КоличествоГотово");
	
КонецПроцедуры

&НаКлиенте
Процедура Брак(Команда)
	
	РаспределитьОстаток("КоличествоБрак");
	
КонецПроцедуры

&НаКлиенте
Процедура Отменено(Команда)
	
	РаспределитьОстаток("КоличествоОтменено");
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	Если ПроверитьЗаполнениеРеквизитов() Тогда
		
		Результат = Новый Структура;
		Результат.Вставить("ЗаписиПротокола", Новый Массив);
		Результат.Вставить("ПересчитатьНормативы", ПересчитатьНормативы);
		
		Если КоличествоБрак > 0 Тогда
			
			Если Режим = "Выполнение" ИЛИ Режим = "ВыполнитьПроконтролировать" Тогда
				ПредыдущееСостояние = "КоличествоКВыполнению";
			ИначеЕсли Режим = "НаКонтроль" Тогда
				ПредыдущееСостояние = "КоличествоНаДоработке";
			КонецЕсли;
			
			НовоеДействие = ОперативныйУчетПроизводстваКлиентСервер.ЗаписьПротоколаКонструктор("Брак");
			НовоеДействие.ПредыдущееСостояние = ПредыдущееСостояние;
			НовоеДействие.Количество  = КоличествоБрак;
			НовоеДействие.Комментарий = ПричинаБрак;
			Результат.ЗаписиПротокола.Добавить(НовоеДействие);
			
		КонецЕсли;
		
		Если КоличествоГотово > 0 Тогда
			
			Если Режим = "Выполнение" ИЛИ Режим = "ВыполнитьПроконтролировать" Тогда
				НовоеДействие = ОперативныйУчетПроизводстваКлиентСервер.ЗаписьПротоколаКонструктор("Выполнение");
				НовоеДействие.Количество  = КоличествоГотово;
				Результат.ЗаписиПротокола.Добавить(НовоеДействие);
			КонецЕсли;
			
			Если Режим = "ВыполнитьПроконтролировать" Тогда
				НовоеДействие = ОперативныйУчетПроизводстваКлиентСервер.ЗаписьПротоколаКонструктор("Контроль");
				НовоеДействие.Количество  = КоличествоГотово;
				Результат.ЗаписиПротокола.Добавить(НовоеДействие);
			КонецЕсли;
			
			Если Режим = "НаКонтроль" Тогда
				НовоеДействие = ОперативныйУчетПроизводстваКлиентСервер.ЗаписьПротоколаКонструктор("ПредъявлениеККонтролю");
				НовоеДействие.Количество  = КоличествоГотово;
				Результат.ЗаписиПротокола.Добавить(НовоеДействие);
			КонецЕсли;
			
		КонецЕсли;
		
		Если КоличествоОтменено > 0 Тогда
			
			НовоеДействие = ОперативныйУчетПроизводстваКлиентСервер.ЗаписьПротоколаКонструктор("Отмена");
			НовоеДействие.Количество = КоличествоОтменено;
			Результат.ЗаписиПротокола.Добавить(НовоеДействие);
			
		КонецЕсли;
		
		Если ПредыдущиеОперации.Количество() > 0
			И ПредыдущиеОперацииЕстьКоличествоКВыполнению(ЭтотОбъект) > 0 Тогда
			НачатьВыполнениеПредыдущихОпераций(Результат);
		Иначе
			Закрыть(Результат);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для каждого Строка Из ПредыдущиеОперации Цикл
		Строка.Отметка = Истина;
	КонецЦикла;
	
	ПредыдущиеОперацииКоличество = ПредыдущиеОперации.Количество();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для каждого Строка Из ПредыдущиеОперации Цикл
		Строка.Отметка = Ложь;
	КонецЦикла;
	
	ПредыдущиеОперацииКоличество = 0;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	
	ПредыдущиеОперацииРассчитатьКоличествоКВыполнению(ЭтотОбъект);
	
	РассчитатьПризнакПересчитатьНормативы(ЭтотОбъект);
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПричинаПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущиеОперацииОтметкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПредыдущиеОперации.ТекущиеДанные;
	
	ПредыдущиеОперацииКоличество = ПредыдущиеОперацииКоличество + ?(ТекущиеДанные.Отметка, 1, -1);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущиеОперацииВыполнитьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПредыдущиеОперации.ТекущиеДанные; 
	
	ТекущиеДанные.КоличествоРучнойВвод = ?(ТекущиеДанные.Количество = 0, Ложь, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	Если Режим = "НаКонтроль" Тогда
		
		ЕстьПредыдущиеОперации = Ложь;
		
		Элементы.ГруппаОтменено.Видимость = Ложь;
		Элементы.Отменено.Видимость = Ложь;
		
		Заголовок = СтрШаблон(НСтр("ru = 'Предъявление к контролю операции ""%1""';
									|en = 'Present the ""%1"" operation for control'"), ОперацияНаименование);
		
	Иначе
		
		ЕстьПредыдущиеОперации = ПредыдущиеОперации.Количество() > 0;
		
		Заголовок = СтрШаблон(НСтр("ru = 'Выполнение операции ""%1""';
									|en = 'Execute the ""%1"" operation'"), ОперацияНаименование);
		
	КонецЕсли;
	
	Элементы.ГруппаТекущаяОперация.ОтображатьЗаголовок = ЕстьПредыдущиеОперации;
	Элементы.ГруппаОперацииКВыполнению.Видимость = ЕстьПредыдущиеОперации;
	
	НастроитьЭлементыОтраженияБрака();
	
	НастроитьПереключательПересчитатьНормативы();
	
	КлючСохраненияПоложенияОкна = СтрШаблон("%1%2", ИмяФормы, ?(ЕстьПредыдущиеОперации,"1","2"));
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	Если Инициализация
		Или СтруктураРеквизитов.Свойство("Количество") Тогда
			
		ОбновитьКоличествоКРаспределению(Форма);
		
		Элементы.КоличествоБрак.МаксимальноеЗначение      = Форма.КоличествоБрак + Форма.КоличествоКРаспределению;
		Элементы.КоличествоГотово.МаксимальноеЗначение    = Форма.КоличествоГотово + Форма.КоличествоКРаспределению;
		Элементы.КоличествоОтменено.МаксимальноеЗначение  = Форма.КоличествоОтменено + Форма.КоличествоКРаспределению;
		
		Элементы.ГруппаБыстрыеКоманды.Доступность         = Форма.КоличествоКРаспределению > 0;
		Элементы.ФормаПрименить.Доступность = (Форма.КоличествоГотово > 0 ИЛИ Форма.КоличествоБрак > 0 ИЛИ Форма.КоличествоОтменено > 0);
		
	КонецЕсли;
	
	Если Инициализация
		Или СтруктураРеквизитов.Свойство("ИнформационнаяПанель") Тогда
			
			Элементы.ГруппаИнформационнаяПанель.Видимость = ЗначениеЗаполнено(Элементы.ИнформационнаяПанельНадпись.Заголовок);
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыОтраженияБрака()
	
	Элементы.Брак.Доступность = ОперацияКакРаспоряжениеВыработки;
	Элементы.ГруппаБрак.Доступность = ОперацияКакРаспоряжениеВыработки;
	
	Если НЕ ОперацияКакРаспоряжениеВыработки Тогда
		Элементы.ИнформационнаяПанельНадпись.Заголовок = Документы.ПроизводственнаяОперация2_2.ТекстПредупрежденияНедоступностьОтраженияБрака();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПереключательПересчитатьНормативы()
	
	Настройки = Документы.ПроизводственнаяОперация2_2.НастройкиПереключателяПересчитатьНормативы(ПроизводственнаяОперация);
	
	Элементы.ПересчитатьНормативы.Видимость = Настройки.Видимость;
	Элементы.ПересчитатьНормативы.Заголовок = Настройки.Заголовок;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьКоличествоКРаспределению(Форма)
	
	Форма.КоличествоКРаспределению = Форма.КоличествоКВыполнению
		- Форма.КоличествоБрак
		- Форма.КоличествоГотово
		- Форма.КоличествоОтменено;
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьОстаток(ИмяРеквизита)
	
	ЭтотОбъект[ИмяРеквизита] = ЭтотОбъект[ИмяРеквизита] + КоличествоКРаспределению;
	ТекущийЭлемент = Элементы[ИмяРеквизита];
	
	ПредыдущиеОперацииРассчитатьКоличествоКВыполнению(ЭтотОбъект);
	
	РассчитатьПризнакПересчитатьНормативы(ЭтотОбъект);
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПричинБрака()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ ПЕРВЫЕ 50
	|	Таблица.Комментарий КАК Комментарий
	|ИЗ
	|	Документ.ПроизводственнаяОперация2_2.Протокол КАК Таблица
	|ГДЕ
	|	Таблица.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПротоколаПроизводственнойОперации.Брак)
	|	И Таблица.Комментарий <> """"
	|	И Таблица.Ссылка.Проведен
	|УПОРЯДОЧИТЬ ПО
	|	Таблица.Дата УБЫВ
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекущийСписок = Элементы.ПричинаБрак.СписокВыбора;
			Если ТекущийСписок.НайтиПоЗначению(Выборка.Комментарий) = Неопределено Тогда
				ТекущийСписок.Добавить(Выборка.Комментарий);
				Если ТекущийСписок.Количество() >= 5 Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеРеквизитов()
	
	ТекстПревышеноКоличество = НСтр("ru = 'Превышено доступное к выполнению количество';
									|en = 'The quantity available for execution is exceeded'");
	
	ЕстьОшибки = Ложь;
	
	Если (КоличествоБрак + КоличествоГотово + КоличествоОтменено) > КоличествоКВыполнению Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			ТекстПревышеноКоличество,,
			"КоличествоКВыполнению",,
			ЕстьОшибки);
	КонецЕсли;
	
	Для каждого Строка Из ПредыдущиеОперации Цикл
		Если Строка.Отметка
				И Строка.Количество > Строка.МожноВыполнить Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ТекстПревышеноКоличество,,
				СтрШаблон("ПредыдущиеОперации[%1].Количество", ПредыдущиеОперации.Индекс(Строка)),,
				ЕстьОшибки);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Строка Из ПредыдущиеОперации Цикл

	КонецЦикла;
	
	Возврат НЕ ЕстьОшибки;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокПредыдущихОпераций(Параметры) 
	
	Если Режим = "НаКонтроль"
			ИЛИ Параметры.ИспользоватьСменныеЗадания Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыТекстаЗапроса = РегистрыСведений.ОчередьПроизводственныхОпераций.ПараметрыТекстаЗапросаОчередьОпераций();
	ПараметрыТекстаЗапроса.ПоказателиВыполнения = Истина;
	ПараметрыТекстаЗапроса.РеквизитыОперацииРасширенные = Истина;
	ПараметрыТекстаЗапроса.ИмяВыходнойТаблицы = "ВТОчередь";
	ПараметрыТекстаЗапроса.Отборы.Добавить("Очередь.Этап = &Этап");
	
	ТекстЗапроса = РегистрыСведений.ОчередьПроизводственныхОпераций.ТекстЗапросаОчередьОпераций(ПараметрыТекстаЗапроса) + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	Т.Требуется                                    КАК Требуется,
	|	Т.Запланировано - Т.ТребуетПовторения          КАК Запланировано,
	|	&Обработано                                    КАК Обработано
	|
	|ИЗ
	|	ВТОчередь КАК Т
	|ГДЕ
	|	Т.ИдентификаторОперации = &ИдентификаторОперации
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Операция                                          КАК Операция,
	|	Т.ПроизводственнаяОперация                          КАК ПроизводственнаяОперация,
	|	Т.ИдентификаторОперации                             КАК ИдентификаторОперации,
	|	Т.Наименование                                      КАК Наименование,
	|	Т.НомерОперации                                     КАК НомерОперации,
	|
	|	&Этап                                               КАК Этап,
	|	&Подразделение                                      КАК Подразделение,
	|	&Исполнитель                                        КАК Исполнитель,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Т.Операция.Участок, ЗНАЧЕНИЕ(Справочник.ПроизводственныеУчастки.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.ПроизводственныеУчастки.ПустаяСсылка) ТОГДА
	|			Т.Операция.Участок
	|		ИНАЧЕ &Участок
	|	КОНЕЦ                                               КАК Участок,
	|
	|	Т.Требуется                                         КАК Требуется,
	|	Т.Запланировано - Т.ТребуетПовторения               КАК Запланировано,
	|	&Обработано                                         КАК Обработано,
	|	&МожноВыполнить                                     КАК МожноВыполнить
	|
	|ИЗ
	|	ВТОчередь КАК Т
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОчередь КАК ТекущаяОперация
	|	ПО ТекущаяОперация.ИдентификаторОперации = &ИдентификаторОперации
	|		И Т.НомерОперации < ТекущаяОперация.НомерОперации
	|
	|ГДЕ
	|	&МожноВыполнить > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Т.НомерОперации
	|";
	
	ТекстМожноВыполнить = "(Т.Запланировано - Т.ТребуетПовторения) - &Обработано";
	ТекстОбработано     = "(Т.Выполнено + Т.Брак + Т.НаКонтроле + Т.НаДоработке + Т.Пропущено)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&МожноВыполнить", ТекстМожноВыполнить);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Обработано", ТекстОбработано);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Операция",              Операция);
	Запрос.УстановитьПараметр("ИдентификаторОперации", ИдентификаторОперации);
	Запрос.УстановитьПараметр("Этап",                  Параметры.Этап);
	Запрос.УстановитьПараметр("Подразделение",         Параметры.Подразделение);
	Запрос.УстановитьПараметр("Исполнитель",           Параметры.Исполнитель);
	Запрос.УстановитьПараметр("Участок",               Параметры.Участок);
	Запрос.УстановитьПараметр("КоличествоКВыполнению", КоличествоКВыполнению);
	
	Результаты = Запрос.ВыполнитьПакет();
	
	Выборка = Результаты[Результаты.ВГраница()-1].Выбрать();
	Выборка.Следующий();
	ДанныеТекущейОперации = Новый Структура("Требуется,Запланировано,Обработано");
	ЗаполнитьЗначенияСвойств(ДанныеТекущейОперации, Выборка);
	
	ПредыдущиеОперации.Загрузить(Результаты[Результаты.ВГраница()].Выгрузить());
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПредыдущиеОперацииРассчитатьКоличествоКВыполнению(Форма)
	
	Если НЕ Форма.ЕстьПредыдущиеОперации Тогда
		Возврат;
	КонецЕсли;
	
	Коэффициент = (Форма.ДанныеТекущейОперации.Обработано + Форма.КоличествоГотово + Форма.КоличествоБрак) / Форма.ДанныеТекущейОперации.Запланировано;
	
	Для каждого Строка Из Форма.ПредыдущиеОперации Цикл
		Если НЕ Строка.КоличествоРучнойВвод Тогда
			КоэффициентСтроки = Строка.Обработано/Строка.Запланировано;
			Если Коэффициент > КоэффициентСтроки Тогда
				Строка.Количество = Мин(Строка.Запланировано * (Коэффициент-КоэффициентСтроки), Строка.МожноВыполнить);
			Иначе
				Строка.Количество = 0;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредыдущиеОперацииЕстьКоличествоКВыполнению(Форма)
	
	Для каждого Строка Из Форма.ПредыдущиеОперации Цикл
		Если Строка.Отметка И Строка.Количество > 0 Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура НачатьВыполнениеПредыдущихОпераций(РезультатВыполнения)
	
	Если ДлительнаяОперация <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НачатьВыполнениеПредыдущихОперацийНаСервере();
	
	Если ДлительнаяОперация <> Неопределено Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ВыполнениеПредыдущихОперацийЗавершение",
			ЭтотОбъект,
			РезультатВыполнения);
			
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выполняются предыдущие операции...';
												|en = 'Previous operations are being executed...'");
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			ДлительнаяОперация,
			ОписаниеОповещения,
			ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НачатьВыполнениеПредыдущихОперацийНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ЗапуститьВФоне              = Истина;
	ПараметрыВыполнения.ОжидатьЗавершение           = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Выполнение последовательности предыдущих операций.';
															|en = 'Perform a sequence of previous operations.'");
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ТаблицаОпераций", ПредыдущиеОперации.Выгрузить());
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Документы.ПроизводственнаяОперация2_2.ВыполнитьПоследовательностьПредыдущихОперацийВФоне",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеПредыдущихОперацийЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ДлительнаяОперация = Неопределено;
	
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыполнениеПредыдущихОперацийЗавершениеНаСервере(Результат.АдресРезультата) Тогда
	
		Закрыть(ДополнительныеПараметры);
		
	Иначе
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Ошибка выполнения производственных операций.
								|См. подробности в журнале регистрации.';
								|en = 'An error occurred while performing routing operations.
								|For more information, see the event log.'");
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВыполнениеПредыдущихОперацийЗавершениеНаСервере(АдресХранилища)
	
	Возврат ПолучитьИзВременногоХранилища(АдресХранилища);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьПризнакПересчитатьНормативы(Форма)
	
	Если НЕ Форма.Элементы.ПересчитатьНормативы.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	Если Форма.КоличествоБрак ИЛИ Форма.КоличествоОтменено Тогда
		Форма.ПересчитатьНормативы = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти