
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Заголовок  = Заголовок + " " + Параметры.Заголовок;
	Количество = Параметры.Количество;
	Контроль   = Параметры.Контроль;
	Протокол.Загрузить(Параметры.Протокол.Выгрузить());
	
	Параметры.Свойство("ПроизводственнаяОперация", ПроизводственнаяОперация);
	Параметры.Свойство("Операция", Операция);
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	МожноОтменить       = Истина;
	Для Индекс = -(Протокол.Количество() - 1) По 0 Цикл
		
		ТекущаяСтрока = Протокол[-Индекс];
		
		МожноОтменить = Мин(МожноОтменить, ТекущийПользователь = ТекущаяСтрока.Ответственный);
		ТекущаяСтрока.МожноОтменить = МожноОтменить;
		
	КонецЦикла;
	
	ЕдиницаИзмеренияПредставление = УправлениеПроизводствомКлиентСервер.ПредставлениеЕдиницыИзмеренияОперации(
		Неопределено,
		Количество);
	
	Документы.ПроизводственнаяОперация2_2.ПересчитатьИтоги(ЭтотОбъект);
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект),
			НСтр("ru = 'Протокол был изменен. Сохранить изменения?';
				|en = 'The protocol was changed. Do you want to save the changes?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПротокол

&НаКлиенте
Процедура ПротоколОтменитьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Протокол.ТекущиеДанные;
	
	Начало = ?(ТекущиеДанные.Отметка, Протокол.Индекс(ТекущиеДанные) + 1, 0);
	Конец  = ?(ТекущиеДанные.Отметка, Протокол.Количество() - 1, Протокол.Индекс(ТекущиеДанные) - 1);
	
	Для Индекс = Начало По Конец Цикл
		Протокол[Индекс].Отметка = ТекущиеДанные.Отметка;
	КонецЦикла;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	УстановитьСнятьОтметки(ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	УстановитьСнятьОтметки(ЭтотОбъект, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьВыбранные(Команда)
	
	УдалитьВыбранныеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	Модифицированность = Ложь;
	
	СохранитьИЗакрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПротоколОтменить.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Протокол.МожноОтменить");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	Настройки = Документы.ПроизводственнаяОперация2_2.НастройкиПереключателяПересчитатьНормативы(ПроизводственнаяОперация);
	
	Элементы.ПересчитатьНормативы.Видимость      = Настройки.Видимость;
	Элементы.ПересчитатьНормативы.Заголовок      = Настройки.Заголовок;
	Элементы.ПересчитатьНормативы.ТолькоПросмотр = ТолькоПросмотр;

	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	Если СтруктураРеквизитов.Свойство("Отметки")
		ИЛИ Инициализация Тогда
		
		ЕстьДоступныеФлажки     = ЕстьДоступныеФлажки(Форма);
		ЕстьУстановленныеФлажки = ЕстьУстановленныеФлажки(Форма);
		
		Элементы.ФормаУстановитьФлажки.Доступность = НЕ Форма.ТолькоПросмотр И ЕстьДоступныеФлажки;
		Элементы.ФормаСнятьФлажки.Доступность      = НЕ Форма.ТолькоПросмотр И ЕстьДоступныеФлажки;
		Элементы.ФормаУдалитьВыбранные.Доступность = НЕ Форма.ТолькоПросмотр И ЕстьУстановленныеФлажки;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСнятьОтметки(Форма, Значение)
	
	Для каждого Строка Из Форма.Протокол Цикл
		Если НЕ Строка.МожноОтменить Тогда
			Продолжить;
		КонецЕсли;
		Строка.Отметка = Значение;
	КонецЦикла;
	
	НастроитьЗависимыеЭлементыФормы(Форма, "Отметки");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЕстьДоступныеФлажки(Форма)
	
	Возврат Форма.Протокол.НайтиСтроки(Новый Структура("МожноОтменить", Истина)).Количество() > 0;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЕстьУстановленныеФлажки(Форма)
	
	Возврат Форма.Протокол.НайтиСтроки(Новый Структура("Отметка", Истина)).Количество() > 0;
	
КонецФункции

&НаСервере
Процедура УдалитьВыбранныеНаСервере()
	
	КоличествоЗаписей = Протокол.Количество();
	
	Для Индекс = -КоличествоЗаписей + 1 По 0 Цикл
		ЗаписьПротокола = Протокол[-Индекс];
		Если ЗаписьПротокола.Отметка Тогда
			Если Элементы.ПересчитатьНормативы.Видимость
				И (ЗаписьПротокола.Событие = Перечисления.СобытияПротоколаПроизводственнойОперации.Отмена
					ИЛИ ЗаписьПротокола.Событие = Перечисления.СобытияПротоколаПроизводственнойОперации.Брак) Тогда
				ПересчитатьНормативы = Истина;
			КонецЕсли;
			Протокол.Удалить(-Индекс);
		КонецЕсли;
	КонецЦикла;
	
	Если КоличествоЗаписей <> Протокол.Количество() Тогда
		Документы.ПроизводственнаяОперация2_2.ПересчитатьИтоги(ЭтотОбъект);
		Модифицированность = Истина;
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "Отметки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		СохранитьИЗакрыть();
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть()
	
	Результат = Новый Структура;
	Результат.Вставить("Протокол", Протокол);
	Результат.Вставить("ПересчитатьНормативы", ПересчитатьНормативы);
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти