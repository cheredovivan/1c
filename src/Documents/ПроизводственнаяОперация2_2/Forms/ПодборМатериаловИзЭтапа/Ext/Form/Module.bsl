
#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, "Этап");
	
	ЗаполнитьТаблицуМатериалов();
	
	Элементы.ГруппаОтсутствуютОстатки.Видимость = (ТаблицаМатериалы.Количество() = 0);
	
	ПодборТоваровКлиентСервер.СформироватьЗаголовокФормыПодбора(Заголовок, Этап);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ОповещениеСохранитьИЗакрыть = Новый ОписаниеОповещения(
		"ПередЗакрытиемСохранитьИЗакрыть", ЭтотОбъект);
	
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(
		ОповещениеСохранитьИЗакрыть, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаМатериалы

&НаКлиенте
Процедура ТаблицаМатериалыКоличествоУпаковокПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаМатериалы.ТекущиеДанные;
	ПересчитатьКоличествоЕдиниц(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаМатериалыУпаковкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаМатериалы.ТекущиеДанные;
	ПересчитатьКоличествоЕдиниц(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьСтроки(Команда)
	
	ВыбратьВсеСтрокиНаСервере(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьСтроки(Команда)
	
	ВыбратьВсеСтрокиНаСервере(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	Если ЕстьВыбранныеСтроки() Тогда
		
		ПеренестиВыбранныеСтрокиВДокумент();
		
	Иначе
		
		ОчиститьСообщения();
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Необходимо выбрать строки для переноса';
				|en = 'Select lines for transfer'"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ПараметрыУсловногоОформления = НоменклатураСервер.НовыеПараметрыУсловногоОформленияЕдиницИзмерения();
	ПараметрыУсловногоОформления.ИмяПоляЕдиницаИзмерения = "ТаблицаМатериалыНоменклатураЕдиницаИзмерения";
	ПараметрыУсловногоОформления.ПутьКПолюУпаковка = "ТаблицаМатериалы.Упаковка";
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтаФорма, ПараметрыУсловногоОформления);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуМатериалов()
	
	ТаблицаМатериалы.Загрузить(
		Документы.ЭтапПроизводства2_2.ОстаткиОбеспечиваемыхМатериалов(Этап, "Характеристика, Серия"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьКоличествоЕдиниц(СтрокаТабличнойЧасти)

	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", ПроизводствоКлиентСервер.ПараметрыПересчетаКоличестваЕдиниц());
	
	ПакетнаяОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтрокаТабличнойЧасти, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьВсеСтрокиНаСервере(ЗначениеВыбора = Истина)
	
	НайденныеСтроки = ТаблицаМатериалы.НайтиСтроки(Новый Структура("СтрокаВыбрана", Не ЗначениеВыбора));
	
	Для Каждого СтрокаТаблицы Из НайденныеСтроки Цикл
		
		СтрокаТаблицы.СтрокаВыбрана = ЗначениеВыбора;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВыбранныеСтрокиВДокумент()
	
	Результат = ПоместитьВыбранныеСтрокиВХранилище();
	
	Модифицированность = Ложь;
	Закрыть();
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьВыбранныеСтрокиВХранилище()
	
	ТаблицаВыбранныхСтрок = ТаблицаМатериалы.Выгрузить(Новый Структура("СтрокаВыбрана", Истина));
	
	Возврат ПоместитьВоВременноеХранилище(ТаблицаВыбранныхСтрок);
	
КонецФункции

&НаКлиенте
Процедура ПередЗакрытиемСохранитьИЗакрыть(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ПеренестиВыбранныеСтрокиВДокумент();
	
КонецПроцедуры

&НаСервере
Функция ЕстьВыбранныеСтроки()
	
	ВыбранныеСтроки = ТаблицаМатериалы.НайтиСтроки(Новый Структура("СтрокаВыбрана", Истина));
	
	Возврат НЕ(ВыбранныеСтроки.Количество() = 0);
	
КонецФункции

#КонецОбласти
