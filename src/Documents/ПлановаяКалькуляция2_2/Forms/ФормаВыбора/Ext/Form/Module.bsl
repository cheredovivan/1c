#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	#Область Отборы
	
	СтруктураБыстрогоОтбора = Неопределено;
	СтруктураОтбора = Неопределено;
	Параметры.Свойство("СтруктураОтбора", СтруктураОтбора);
	
	Если СтруктураОтбора <> Неопределено Тогда
		
		ИспользоватьОтборОрганизация = ЗначениеЗаполнено(СтруктураОтбора.Организация);
		ИспользоватьОтборПодразделениеДиспетчер = ЗначениеЗаполнено(СтруктураОтбора.ПодразделениеДиспетчер);
		ИспользоватьОтборМаксимальнаяДатаПлановыхКалькуляций = ЗначениеЗаполнено(
			СтруктураОтбора.Дата);
		ПоказыватьТолькоПоследниеПлановыеКалькуляции = ЗначениеЗаполнено(СтруктураОтбора.Дата);
		ОтключитьОтборПоОбъектамКалькуляции = СтруктураОтбора.Свойство("ОтключитьОтборПоОбъектамКалькуляции")
			И СтруктураОтбора.ОтключитьОтборПоОбъектамКалькуляции;
		ИспользоватьОтборОбъектыКалькуляции = Не ОтключитьОтборПоОбъектамКалькуляции
			И СтруктураОтбора.ОбъектыКалькуляции.Количество() > 0;
		ИспользоватьОтборПродукцияИлиПолуфабрикат = ЗначениеЗаполнено(СтруктураОтбора.Изделие);
		ИспользоватьОтборХарактеристика = ЗначениеЗаполнено(СтруктураОтбора.Характеристика);
		
		// Отбор по назначению по умолчанию не включаем, но поле отбора заполняем
		Назначение = СтруктураОтбора.Назначение;
		
		// Устанавливаем отборы
		Организация = СтруктураОтбора.Организация;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Организация", Организация,
			ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Организация) И ИспользоватьОтборОрганизация);
		
		ПодразделениеДиспетчер = СтруктураОтбора.ПодразделениеДиспетчер;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПодразделениеДиспетчер",
			ПодразделениеДиспетчер, ВидСравненияКомпоновкиДанных.Равно,,
			ЗначениеЗаполнено(ПодразделениеДиспетчер) И ИспользоватьОтборПодразделениеДиспетчер);
		
		МаксимальнаяДатаПлановыхКалькуляций = СтруктураОтбора.Дата;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
			"Дата", КонецДня(МаксимальнаяДатаПлановыхКалькуляций),
			ВидСравненияКомпоновкиДанных.МеньшеИлиРавно,,
			ЗначениеЗаполнено(МаксимальнаяДатаПлановыхКалькуляций) И ИспользоватьОтборМаксимальнаяДатаПлановыхКалькуляций);
		
		ОбъектыКалькуляции.ЗагрузитьЗначения(СтруктураОтбора.ОбъектыКалькуляции);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
			"ОбъектыКалькуляции.Объект", ОбъектыКалькуляции, ВидСравненияКомпоновкиДанных.ВСписке,,
			ЗначениеЗаполнено(ОбъектыКалькуляции) И ИспользоватьОтборОбъектыКалькуляции);
			
		// Устанавливаем параметры
		ПродукцияИлиПолуфабрикат = СтруктураОтбора.Изделие;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
			"ИзделиеОтбор", Истина, ВидСравненияКомпоновкиДанных.Равно,,
			ЗначениеЗаполнено(ПродукцияИлиПолуфабрикат) И ИспользоватьОтборПродукцияИлиПолуфабрикат);
			
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
			"Изделие", ПродукцияИлиПолуфабрикат,
			ЗначениеЗаполнено(ПродукцияИлиПолуфабрикат) И ИспользоватьОтборПродукцияИлиПолуфабрикат);
			
		Характеристика = СтруктураОтбора.Характеристика;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
			"ХарактеристикаОтбор", Истина, ВидСравненияКомпоновкиДанных.Равно,,
			ЗначениеЗаполнено(Характеристика) И ИспользоватьОтборХарактеристика);
			
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
			"Характеристика", Характеристика,
			ЗначениеЗаполнено(Характеристика) И ИспользоватьОтборХарактеристика);
			
		ТолькоРассчитанные = Истина;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
			"Проведен", ТолькоРассчитанные,
			ВидСравненияКомпоновкиДанных.Равно,
			ТолькоРассчитанные);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
			"РассчитанаОтбор", ТолькоРассчитанные,
			ВидСравненияКомпоновкиДанных.Равно,,
			ТолькоРассчитанные);
	КонецЕсли;
	
	#КонецОбласти
	
	#Область УниверсальныеМеханизмы
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ГлобальныеКоманды);
	// Конец ИнтеграцияС1СДокументооборотом

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	ТекущиеДелаПереопределяемый.ПередЗагрузкойДанныхИзНастроекНаСервере(ЭтаФорма, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Перем СтруктураОтборов;
	
	Если Параметры.Свойство("СтруктураОтборов", СтруктураОтборов) Тогда
		
		Если СтруктураОтборов.Свойство("ПодразделениеДиспетчер") Тогда
			Настройки.Вставить("ПодразделениеДиспетчер", СтруктураОтборов.ПодразделениеДиспетчер)
		КонецЕсли;
		
		Если СтруктураОтборов.Свойство("Организация") Тогда
			Настройки.Вставить("Организация", СтруктураОтборов.Организация)
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПодразделениеДиспетчерОтборПриИзменении(Элемент)	
	Если Элемент.Имя = "ПодразделениеОтбор" И ЗначениеЗаполнено(ПодразделениеДиспетчер) Тогда
		ИспользоватьОтборПодразделениеДиспетчер = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПодразделениеДиспетчер",
		ПодразделениеДиспетчер, ВидСравненияКомпоновкиДанных.Равно,,
		ЗначениеЗаполнено(ПодразделениеДиспетчер) И ИспользоватьОтборПодразделениеДиспетчер);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОтборПриИзменении(Элемент)
	Если Элемент.Имя = "ОрганизацияОтбор" И ЗначениеЗаполнено(Организация) Тогда
		ИспользоватьОтборОрганизация = Истина;
	КонецЕсли;
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Организация", Организация,
		ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Организация) И ИспользоватьОтборОрганизация);
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыКалькуляцииПриИзменении(Элемент)
	Если Элемент.Имя = "ОбъектыКалькуляции" И ОбъектыКалькуляции.Количество() > 0 Тогда
		ИспользоватьОтборОбъектыКалькуляции = Истина;
	КонецЕсли;
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"ОбъектыКалькуляции.Объект", ОбъектыКалькуляции, ВидСравненияКомпоновкиДанных.ВСписке,,
		ЗначениеЗаполнено(ОбъектыКалькуляции) И ИспользоватьОтборОбъектыКалькуляции);
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияИлиПолуфабрикатПриИзменении(Элемент)
	Если Элемент.Имя = "ПродукцияИлиПолуфабрикат" И ЗначениеЗаполнено(ПродукцияИлиПолуфабрикат) Тогда
		ИспользоватьОтборПродукцияИлиПолуфабрикат = Истина;
	КонецЕсли;
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"ИзделиеОтбор", Истина, ВидСравненияКомпоновкиДанных.Равно,,
		ЗначениеЗаполнено(ПродукцияИлиПолуфабрикат) И ИспользоватьОтборПродукцияИлиПолуфабрикат);
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
		"Изделие", ПродукцияИлиПолуфабрикат,
		ЗначениеЗаполнено(ПродукцияИлиПолуфабрикат) И ИспользоватьОтборПродукцияИлиПолуфабрикат);
КонецПроцедуры

&НаКлиенте
Процедура ХарактеристикаПриИзменении(Элемент)
	Если Элемент.Имя = "Характеристика" И ЗначениеЗаполнено(Характеристика) Тогда
		ИспользоватьОтборХарактеристика = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"ХарактеристикаОтбор", Истина, ВидСравненияКомпоновкиДанных.Равно,,
		ЗначениеЗаполнено(Характеристика) И ИспользоватьОтборХарактеристика);
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
		"Характеристика", Характеристика,
		ЗначениеЗаполнено(Характеристика) И ИспользоватьОтборХарактеристика);
КонецПроцедуры

&НаКлиенте
Процедура НазначениеПриИзменении(Элемент)
	Если Элемент.Имя = "Назначение" И ЗначениеЗаполнено(Назначение) Тогда
		ИспользоватьОтборНазначение = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"ОбъектыКалькуляции.Назначение", Назначение, ВидСравненияКомпоновкиДанных.Равно,,
		ЗначениеЗаполнено(Назначение) И ИспользоватьОтборНазначение);
КонецПроцедуры

&НаКлиенте
Процедура МаксимальнаяДатаПлановыхКалькуляцийПриИзменении(Элемент)
	Если Элемент.Имя = "МаксимальнаяДатаПлановыхКалькуляций" И ЗначениеЗаполнено(МаксимальнаяДатаПлановыхКалькуляций) Тогда
		ИспользоватьОтборМаксимальнаяДатаПлановыхКалькуляций = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"Дата", КонецДня(МаксимальнаяДатаПлановыхКалькуляций),
		ВидСравненияКомпоновкиДанных.МеньшеИлиРавно,,
		ЗначениеЗаполнено(МаксимальнаяДатаПлановыхКалькуляций) И ИспользоватьОтборМаксимальнаяДатаПлановыхКалькуляций);
КонецПроцедуры

&НаКлиенте
Процедура ТолькоРассчитанныеПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"Проведен",
		ТолькоРассчитанные,
		ВидСравненияКомпоновкиДанных.Равно,
		Неопределено,
		ТолькоРассчитанные);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"РассчитанаОтбор",
		ТолькоРассчитанные,
		ВидСравненияКомпоновкиДанных.Равно,
		Неопределено,
		ТолькоРассчитанные);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийСписка

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область Универсальные

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	СтандартныеПодсистемыСервер.УстановитьУсловноеОформлениеПоляДата(ЭтотОбъект, "Список", "Дата");

КонецПроцедуры

#КонецОбласти
