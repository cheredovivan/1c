
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует описание реквизитов объекта, заполняемых по статистике их использования.
//
// Параметры:
//  ОписаниеРеквизитов - Структура - описание реквизитов, для которых необходимо получить значения по статистике
//
//
Процедура ЗадатьОписаниеЗаполняемыхРеквизитовПоСтатистике(ОписаниеРеквизитов) Экспорт
	
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов,
		"Организация");
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Организация";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов,
		"ВидЦены", Параметры);
		
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьТолькоЗаполненные = "Организация";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(
		ОписаниеРеквизитов, "Ответственный", Параметры);
	
КонецПроцедуры

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * ТаблицаИмяРегистра - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.ПлановаяКалькуляция2_2") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаМножителиПартийИПолуфабрикатов(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

Процедура ЗаполнитьДокументУстановкаЦенНоменклатуры(ДокументУстановки, ДанныеЗаполнения) Экспорт
	
	НаборВалют = Константы.СоздатьНабор("ВалютаРегламентированногоУчета, ВалютаУправленческогоУчета");
	НаборВалют.Прочитать();
	
	ПлановыйВидЦены = Справочники.ВидыЦен.ВидЦеныПлановойСтоимостиТМЦ();
	ВалютаПлана = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПлановыйВидЦены, "ВалютаЦены");
	ИспользуетсяЦенообразование25 = ПолучитьФункциональнуюОпцию("ИспользуетсяЦенообразование25");
	
	Если ИспользуетсяЦенообразование25 Тогда
		
		ТабличнаяЧастьТовары = "Товары2_5";
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ОбъектыКалькуляции.Номенклатура КАК Номенклатура,
			|	ОбъектыКалькуляции.Характеристика.ХарактеристикаНоменклатурыДляЦенообразования КАК ХарактеристикаЦО,
			|	ОбъектыКалькуляции.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
			|	&ПлановыйВидЦены КАК ВидЦены,
			|	ВЫРАЗИТЬ(
			|		СУММА(ВЫБОР
			|			КОГДА &ИспользоватьСуммуУпр
			|				ТОГДА МножителиПартийИПолуфабрикатов.Стоимость
			|			КОГДА &ИспользоватьСуммуУпрБезНДС
			|				ТОГДА МножителиПартийИПолуфабрикатов.СтоимостьБезНДС 
			|			КОГДА &ИспользоватьСуммуРегл
			|				ТОГДА МножителиПартийИПолуфабрикатов.СтоимостьРегл
			|			ИНАЧЕ 0
			|		КОНЕЦ) 
			|		/ СУММА(МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката)
			|	КАК ЧИСЛО(31, 2)) КАК Цена
			|ИЗ
			|	Документ.ПлановаяКалькуляция2_2.ОбъектыКалькуляции КАК ОбъектыКалькуляции
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
			|		ПО ОбъектыКалькуляции.Ссылка = МножителиПартийИПолуфабрикатов.Калькуляция
			|		И ОбъектыКалькуляции.Номенклатура = МножителиПартийИПолуфабрикатов.Продукция
			|		И ОбъектыКалькуляции.Характеристика = МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
			|		И ОбъектыКалькуляции.Назначение = МножителиПартийИПолуфабрикатов.Назначение
			|		И МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
			|ГДЕ
			|	ОбъектыКалькуляции.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	ОбъектыКалькуляции.Номенклатура,
			|	ОбъектыКалькуляции.Характеристика.ХарактеристикаНоменклатурыДляЦенообразования,
			|	ОбъектыКалькуляции.Номенклатура.ЕдиницаИзмерения
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	МножителиПартийИПолуфабрикатов.Полуфабрикат КАК Номенклатура,
			|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката.ХарактеристикаНоменклатурыДляЦенообразования КАК
			|		ХарактеристикаЦО,
			|	МножителиПартийИПолуфабрикатов.Полуфабрикат.ЕдиницаИзмерения КАК Упаковка,
			|	&ПлановыйВидЦены КАК ВидЦены,
			|	ВЫРАЗИТЬ(
			|		СУММА(ВЫБОР
			|				КОГДА &ИспользоватьСуммуУпр
			|					ТОГДА МножителиПартийИПолуфабрикатов.Стоимость
			|				КОГДА &ИспользоватьСуммуУпрБезНДС
			|					ТОГДА МножителиПартийИПолуфабрикатов.СтоимостьБезНДС
			|				КОГДА &ИспользоватьСуммуРегл
			|					ТОГДА МножителиПартийИПолуфабрикатов.СтоимостьРегл
			|				ИНАЧЕ 0
			|		КОНЕЦ)
			|		/ СУММА(МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката)
			|	КАК ЧИСЛО(31, 2)) КАК Цена
			|ИЗ
			|	Документ.ПлановаяКалькуляция2_2.ОбъектыКалькуляции КАК ОбъектыКалькуляции
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
			|		ПО ОбъектыКалькуляции.Ссылка = МножителиПартийИПолуфабрикатов.Калькуляция
			|		И ОбъектыКалькуляции.Номенклатура = МножителиПартийИПолуфабрикатов.Продукция
			|		И ОбъектыКалькуляции.Характеристика = МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
			|		И ОбъектыКалькуляции.Назначение = МножителиПартийИПолуфабрикатов.Назначение
			|		И МножителиПартийИПолуфабрикатов.Полуфабрикат <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
			|ГДЕ
			|	ОбъектыКалькуляции.Ссылка = &Ссылка
			|	И МножителиПартийИПолуфабрикатов.Полуфабрикат <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
			|
			|СГРУППИРОВАТЬ ПО
			|	МножителиПартийИПолуфабрикатов.Полуфабрикат,
			|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката.ХарактеристикаНоменклатурыДляЦенообразования,
			|	МножителиПартийИПолуфабрикатов.Полуфабрикат.ЕдиницаИзмерения";
	
	Иначе
	
		ТабличнаяЧастьТовары = "Товары";
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ОбъектыКалькуляции.Номенклатура КАК Номенклатура,
			|	ОбъектыКалькуляции.Характеристика КАК Характеристика,
			|	ОбъектыКалькуляции.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
			|	&ПлановыйВидЦены КАК ВидЦены,
			|	ВЫРАЗИТЬ(
			|		СУММА(ВЫБОР
			|				КОГДА &ИспользоватьСуммуУпр
			|					ТОГДА МножителиПартийИПолуфабрикатов.Стоимость
			|				КОГДА &ИспользоватьСуммуУпрБезНДС
			|					ТОГДА МножителиПартийИПолуфабрикатов.СтоимостьБезНДС
			|				КОГДА &ИспользоватьСуммуРегл
			|					ТОГДА МножителиПартийИПолуфабрикатов.СтоимостьРегл
			|				ИНАЧЕ 0
			|		КОНЕЦ)
			|		/ СУММА(МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката)
			|	КАК ЧИСЛО(31, 2)) КАК Цена
			|ИЗ
			|	Документ.ПлановаяКалькуляция2_2.ОбъектыКалькуляции КАК ОбъектыКалькуляции
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
			|		ПО ОбъектыКалькуляции.Ссылка = МножителиПартийИПолуфабрикатов.Калькуляция
			|			И ОбъектыКалькуляции.Номенклатура = МножителиПартийИПолуфабрикатов.Продукция
			|			И ОбъектыКалькуляции.Характеристика = МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
			|			И ОбъектыКалькуляции.Назначение = МножителиПартийИПолуфабрикатов.Назначение
			|			И (МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
			|ГДЕ
			|	ОбъектыКалькуляции.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	ОбъектыКалькуляции.Номенклатура,
			|	ОбъектыКалькуляции.Характеристика,
			|	ОбъектыКалькуляции.Номенклатура.ЕдиницаИзмерения
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	МножителиПартийИПолуфабрикатов.Полуфабрикат КАК Номенклатура,
			|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката КАК Характеристика,
			|	МножителиПартийИПолуфабрикатов.Полуфабрикат.ЕдиницаИзмерения КАК Упаковка,
			|	&ПлановыйВидЦены КАК ВидЦены,
			|	ВЫРАЗИТЬ(
			|		СУММА(ВЫБОР
			|				КОГДА &ИспользоватьСуммуУпр
			|					ТОГДА МножителиПартийИПолуфабрикатов.Стоимость
			|				КОГДА &ИспользоватьСуммуУпрБезНДС
			|					ТОГДА МножителиПартийИПолуфабрикатов.СтоимостьБезНДС
			|				КОГДА &ИспользоватьСуммуРегл
			|					ТОГДА МножителиПартийИПолуфабрикатов.СтоимостьРегл
			|				ИНАЧЕ 0
			|		КОНЕЦ)
			|		/ СУММА(МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката)
			|	КАК ЧИСЛО(31, 2)) КАК Цена
			|ИЗ
			|	Документ.ПлановаяКалькуляция2_2.ОбъектыКалькуляции КАК ОбъектыКалькуляции
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
			|		ПО ОбъектыКалькуляции.Ссылка = МножителиПартийИПолуфабрикатов.Калькуляция
			|			И ОбъектыКалькуляции.Номенклатура = МножителиПартийИПолуфабрикатов.Продукция
			|			И ОбъектыКалькуляции.Характеристика = МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
			|			И ОбъектыКалькуляции.Назначение = МножителиПартийИПолуфабрикатов.Назначение
			|			И (МножителиПартийИПолуфабрикатов.Полуфабрикат <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
			|ГДЕ
			|	ОбъектыКалькуляции.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	МножителиПартийИПолуфабрикатов.Полуфабрикат,
			|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката,
			|	МножителиПартийИПолуфабрикатов.Полуфабрикат.ЕдиницаИзмерения";
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("ПлановыйВидЦены", ПлановыйВидЦены);
	Запрос.УстановитьПараметр("ИспользоватьСуммуРегл", 
		(ВалютаПлана = НаборВалют.ВалютаРегламентированногоУчета));
	ЦенаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПлановыйВидЦены, "ЦенаВключаетНДС") = Истина;
	Запрос.УстановитьПараметр("ИспользоватьСуммуУпр", ВалютаПлана = НаборВалют.ВалютаУправленческогоУчета 
		И ЦенаВключаетНДС);
	Запрос.УстановитьПараметр("ИспользоватьСуммуУпрБезНДС", ВалютаПлана = НаборВалют.ВалютаУправленческогоУчета 
		И Не ЦенаВключаетНДС);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не требуется вводить установку цен номенклатуры на основании документа %1';
				|en = 'It is not required to enter pricing of products based on document %1'"),
			ДанныеЗаполнения);
		ВызватьИсключение Текст;
		
	ИначеЕсли Не ЗначениеЗаполнено(ПлановыйВидЦены) Тогда
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не установлен плановый вид цены.';
				|en = 'Planned price type not set.'"),
			ДанныеЗаполнения);
		ВызватьИсключение Текст;
		
	Иначе
		
		ДанныеТовары = РезультатЗапроса.Выгрузить();
		ДокументУстановки.ДокументОснование = ДанныеЗаполнения;
		ДокументУстановки[ТабличнаяЧастьТовары].Загрузить(РезультатЗапроса.Выгрузить());
		
		ДокументУстановки.ВидыЦен.Загрузить(ДанныеТовары);
		Если ДанныеТовары.Найти(ПлановыйВидЦены, "ВидЦены") = Неопределено Тогда
			НовыйВидЦены = ДокументУстановки.ВидыЦен.Добавить();
			НовыйВидЦены.ВидЦены = ПлановыйВидЦены;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура ЗапускВыполненияФоновогоЗадания необходима для запуска в фоне проведения или отмены проведения плановой калькуляции.
// Параметры:
//   ДокументСсылка - ДокументСсылка.ПлановаяКалькуляция2_2 - сслыка на плановую калькуляцию.
//   ЭтоПроведение - булево - флаг обозначающий какое фоновое задание необходимо запустить.
//
Процедура ЗапускВыполненияФоновогоЗадания(ДокументСсылка, ЭтоПроведение) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Уид = ДокументСсылка.УникальныйИдентификатор();
	КлючПроведение = СтрШаблон("%1 ЭтоПроведение = %2", Уид, Истина);
	КлючОтменаПроведения = СтрШаблон("%1 ЭтоПроведение = %2", Уид, Ложь);
	
	ОтборПроведение = Новый Структура();
	ОтборПроведение.Вставить("Ключ", КлючПроведение);
	ОтборПроведение.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	АктивныеЗаданияПроведения = ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборПроведение);
	
	ОтборОтменаПроведения = Новый Структура();
	ОтборОтменаПроведения.Вставить("Ключ", КлючОтменаПроведения);
	ОтборОтменаПроведения.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	АктивныеЗаданияОтменыПроведения = ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборОтменаПроведения);
	
	Если ЭтоПроведение Тогда
		Если АктивныеЗаданияПроведения.Количество() > 0 Тогда
			МассивЗаданий = ФоновыеЗадания.ОжидатьЗавершенияВыполнения(АктивныеЗаданияПроведения, 5);
			Если ЕстьАктивныеФоновыеЗадания(МассивЗаданий) Тогда
				ВызватьИсключение НСтр("ru = 'Выполняется фоновое задание проведения документа, для отмены фонового задания проведения документа, необходимо распровести документ.';
										|en = 'The document posting background job is in progress. To cancel the background job, unpost the document.'");
			КонецЕсли;
		КонецЕсли;
		
		Если АктивныеЗаданияОтменыПроведения.Количество() > 0 Тогда
			МассивЗаданий = ФоновыеЗадания.ОжидатьЗавершенияВыполнения(АктивныеЗаданияОтменыПроведения, 5);
			Если ЕстьАктивныеФоновыеЗадания(МассивЗаданий) Тогда
				ВызватьИсключение НСтр("ru = 'Выполняется фоновое задание отмены проведения, перед проведением необходимо дождаться отмены проведения.';
										|en = 'The posting cancellation background job is in progress. Before posting, wait for the posting to be canceled.'");
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли АктивныеЗаданияПроведения.Количество() > 0 Или АктивныеЗаданияОтменыПроведения.Количество() > 0 Тогда
		// В случаях, когда происходит отмена проведения и в этот момент активно задание проведения или отмены проведения,
		// повторный запуск фонового задания не нужен, достаточно регистрации задачи отмены проведения,
		// которая пишется в регистр сведений ОчередьРасчетаПлановыхКалькуляций, перед запуском этой процедуры.
		Возврат;
	КонецЕсли;
	
	НаименованиеЗадания = НСтр("ru = 'Проведение документа: %Ссылка%';
								|en = 'Posting the document: %Ссылка%'");
	НаименованиеЗадания = СтрЗаменить(НаименованиеЗадания, "%Ссылка%", ДокументСсылка);
	
	ИмяЭкспортнойПроцедуры = "РегистрыСведений.МножителиПартийИПолуфабрикатов.ВыполнитьПроведение";
	
	ПараметрыЭкспортнойПроцедуры = Новый Массив();
	ПараметрыЭкспортнойПроцедуры.Добавить(Новый Структура("Калькуляция, ЭтоПроведение", ДокументСсылка, ЭтоПроведение));
	
	ПараметрыЗадания = Новый Массив();
	ПараметрыЗадания.Добавить(ИмяЭкспортнойПроцедуры);
	ПараметрыЗадания.Добавить(ПараметрыЭкспортнойПроцедуры);
	
	Ключ = ?(ЭтоПроведение, КлючПроведение, КлючОтменаПроведения);
	ФоновыеЗадания.Выполнить("ОбщегоНазначения.ВыполнитьМетодКонфигурации", ПараметрыЗадания, Ключ, НаименованиеЗадания);

КонецПроцедуры

#Область АудитСостоянияСистемы

// Добавляет проверку для регистрации ошибок.
// 
// Параметры:
// 	ТаблицаГруппПроверок - см. АудитСостоянияСистемы.ТаблицаГруппПроверокСостоянияСистемы
//
Процедура ЗаполнитьГруппыПроверокДляРегистрации(ТаблицаГруппПроверок) Экспорт
	
	ОписаниеГруппыПроверок = ТаблицаГруппПроверок.Добавить();
	ОписаниеГруппыПроверок.ИдентификаторРодителя = "";
	ОписаниеГруппыПроверок.Наименование = НСтр("ru = 'Калькуляции продукции';
												|en = 'Product costings'");
	ОписаниеГруппыПроверок.КонтекстПроверокВеденияУчета = Документы.ПлановаяКалькуляция2_2.ПустаяСсылка();
	ОписаниеГруппыПроверок.Идентификатор = "КалькуляцииПродукции";
	
КонецПроцедуры

// Заполняет проверки, выполняемые после расчета плановой калькуляции.
// 
// Параметры:
// 	ТаблицаПроверок - см. АудитСостоянияСистемы.ТаблицаПроверокСостоянияСистемы
//
Процедура ЗаполнитьПроверкиДляРегистрации(ТаблицаПроверок) Экспорт
	
	// Наличие движений с нулевой стоимостью.
	ОписаниеПроверки = ДобавитьОписаниеНовойПроверки(ТаблицаПроверок,
		"ДвиженияСНулевойСтоимостью",
		Документы.ПлановаяКалькуляция2_2.ПустаяСсылка(),
		Перечисления.МоментЗапускаПроверкиОперацииЗакрытияМесяца.ПослеРасчета,
		"Документы.ПлановаяКалькуляция2_2.ПроверкаДвиженийСНулевойСтоимостью");
	
	ЗакрытиеМесяцаСервер.ЗаполнитьПредставлениеНовойПроверки(ОписаниеПроверки,
		НСтр("ru = 'Наличие движений с нулевой стоимостью.';
			|en = 'There are movements with zero cost.'", ОбщегоНазначения.КодОсновногоЯзыка()),
		НСтр("ru = 'Для всей номенклатуры и видов работ должны быть назначены плановые цены.';
			|en = 'Planned prices should be assigned for all products and activity kinds.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
КонецПроцедуры

// Выполняет проверку на коррректность расчета плановой кальклуяции.
// 
// Параметры:
// 	ПараметрыПроверки - см. АудитСостоянияСистемы.ИнициализироватьПараметрыПроверки
//
Процедура ПроверкаДвиженийСНулевойСтоимостью(ПараметрыПроверки) Экспорт
	
	ДополнительныеПараметры = ПараметрыПроверки.ДополнительныеПараметры; // Структура
	Калькуляции = ДополнительныеПараметры.Калькуляции;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	РегистрыСведений.МножителиПартийИПолуфабрикатов.ПолучитьЦеныНоменклатуры(Калькуляции, МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивПлановыхКалькуляций", Калькуляции);
	
	МассивТекстовЗапроса = Новый Массив();
	Запрос.УстановитьПараметр("ИспользоватьРазрядыРасценокРаботСотрудников",
		ПолучитьФункциональнуюОпцию("ИспользоватьРазрядыРасценокРаботСотрудников"));
	//++ Локализация
	Запрос.УстановитьПараметр("ДополнительныйРазрезТарифнойСетки",
		Константы.ДополнительныйРазрезТарифнойСетки.Получить());
	//-- Локализация
	МассивТекстовЗапроса.Добавить(РегистрыНакопления.ПлановыеТрудозатраты.ТекстЗапросаРасценкиТрудозатрат(Ложь));
	
	ТекстЗапроса = "ВЫБРАТЬ
		|	ПлановыеМатериальныеЗатраты.Калькуляция КАК Калькуляция,
		|	""Материальные"" КАК ТипЗатраты,
		|	СУММА(1) КАК КоличествоСтрок
		|ПОМЕСТИТЬ Ошибки
		|ИЗ
		|	РегистрНакопления.ПлановыеМатериальныеЗатраты КАК ПлановыеМатериальныеЗатраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО ПлановыеМатериальныеЗатраты.Калькуляция = ЦеныНоменклатуры.Калькуляция
		|			И ПлановыеМатериальныеЗатраты.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|			И ПлановыеМатериальныеЗатраты.Характеристика = ЦеныНоменклатуры.Характеристика
		|			И ПлановыеМатериальныеЗатраты.Период = ЦеныНоменклатуры.Период
		|ГДЕ
		|	НЕ ПлановыеМатериальныеЗатраты.ЭтоПолуфабрикат
		|	И ПлановыеМатериальныеЗатраты.Калькуляция В (&МассивПлановыхКалькуляций)
		|	И ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлановыеМатериальныеЗатраты.Калькуляция
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПлановыеТрудозатраты.Калькуляция,
		|	""Трудозатраты"",
		|	СУММА(1)
		|ИЗ
		|	РегистрНакопления.ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|	ЛЕВОЕ СОЕДИНЕНИЕ Расценки КАК Расценки
		|	ПО ПлановыеТрудозатраты.Калькуляция = Расценки.Калькуляция
		|		И ПлановыеТрудозатраты.ВидРабот = Расценки.ВидРабот
		|		И ПлановыеТрудозатраты.Период = Расценки.Период
		|		И ПлановыеТрудозатраты.Подразделение = Расценки.Подразделение
		|ГДЕ
		|	ПлановыеТрудозатраты.Калькуляция В (&МассивПлановыхКалькуляций)
		|	И ЕСТЬNULL(Расценки.Расценка, 0) = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлановыеТрудозатраты.Калькуляция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВидыРабот
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Расценки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Ошибки.Калькуляция КАК Калькуляция,
		|	Ошибки.ТипЗатраты КАК ТипЗатраты,
		|	Ошибки.КоличествоСтрок КАК КоличествоСтрок,
		|	Ошибки.Калькуляция.Организация КАК Организация
		|ИЗ
		|	Ошибки КАК Ошибки
		|ИТОГИ ПО
		|	Организация";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	Результат = Запрос.Выполнить();
	ВыборкаПоОрганизациям = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	КалькуляцииСОшибкамиРасчета = Новый Массив;;
	
	Пока ВыборкаПоОрганизациям.Следующий() Цикл
		
		АудитСостоянияСистемы.ДобавитьСтрокуВТаблицуВыявленныхПроблем(
			ПараметрыПроверки, 
			ВыборкаПоОрганизациям.Организация, 
			'00010101', 
			НСтр("ru = 'Присутствуют движения по материалам или видам работы с нулевой стоимостью.';
				|en = 'There are movements of materials and activity kinds with zero value.'"));
		
		ВыборкаДетальная = ВыборкаПоОрганизациям.Выбрать();
		Пока ВыборкаДетальная.Следующий() Цикл
			
			Если ВыборкаДетальная.ТипЗатраты = "Материальные" Тогда
				Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не заданы плановые цены для %1 позиций номенклатуры.';
						|en = 'Scheduled prices for the %1 items of products are not specified.'"),
					ВыборкаДетальная.КоличествоСтрок);
			Иначе
				Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не заданы плановые расценки для %1 видов работ.';
						|en = 'Planned rates for the %1 activity kinds are not specified.'"),
					ВыборкаДетальная.КоличествоСтрок);
				КонецЕсли;
				
			АудитСостоянияСистемы.ДобавитьОбъектКЗафиксированнойПроблеме(
				ПараметрыПроверки,
				ВыборкаДетальная.Калькуляция,
				Представление);
				
			Если КалькуляцииСОшибкамиРасчета.Найти(ВыборкаДетальная.Калькуляция) = Неопределено Тогда
				КалькуляцииСОшибкамиРасчета.Добавить(ВыборкаДетальная.Калькуляция);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ДополнительныеПараметры.Вставить("КалькуляцииСОшибкамиРасчета", КалькуляцииСОшибкамиРасчета);
	
КонецПроцедуры

#КонецОбласти

#Область Команды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.УстановкаЦенНоменклатуры.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
КонецПроцедуры

// Заполняет список команд создания на основании.
//
// Параметры:
//  КомандыСоздатьНаОсновании - ТаблицаЗначений - описание в СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании
// Возвращаемое значение:
// 	СтрокаТаблицыЗначений - описание в СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании 
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт

	Если ПравоДоступа("Добавление", Метаданные.Документы.ПлановаяКалькуляция2_2) Тогда
		
		ТолькоПроизводство22 = ПолучитьФункциональнуюОпцию("ИспользуетсяТолькоУправлениеПроизводством22");
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.ПлановаяКалькуляция2_2.ПолноеИмя();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПлановаяКалькуляция2_2.ПолноеИмя();
		КомандаСоздатьНаОсновании.МножественныйВыбор = Истина;
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьУправлениеПроизводством2_2";
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПлановаяКалькуляция2_2)
			+ ?(ТолькоПроизводство22, "", " 2.2");

		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

// Заполняет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ПлановаяСебестоимостьПродукции) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер			= Метаданные.Отчеты.ПлановаяСебестоимостьПродукции.ПолноеИмя();
		КомандаОтчет.Представление		= НСтр("ru = 'Плановая себестоимость продукции';
												|en = 'Product standard cost'");
		КомандаОтчет.ВидимостьВФормах	= "ФормаДокумента, ФормаСписка";
		КомандаОтчет.КлючВарианта 		= "ПлановаяСебестоимостьПродукцииКонтекст";
		КомандаОтчет.Порядок            = 1;
		КомандаОтчет.Важность 			= "Важное";
		КомандаОтчет.МножественныйВыбор = Ложь;
		
	КонецЕсли;
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ОшибкиРасчетаКалькуляцииПродукции) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер               = Метаданные.Отчеты.ОшибкиРасчетаКалькуляцииПродукции.ПолноеИмя();
		КомандаОтчет.Представление               = НСтр("ru = 'Протокол расчета';
														|en = 'Calculation protocol'");
		КомандаОтчет.ВидимостьВФормах            = "ФормаДокумента, ФормаСписка";
		КомандаОтчет.Порядок                     = 1;
		КомандаОтчет.Важность = "Важное";
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - См. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПлановаяКалькуляцияСводно";
	КомандаПечати.Представление = НСтр("ru = 'Сводная калькуляция';
										|en = 'Summary cost estimate'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПлановаяКалькуляцияПодробно";
	КомандаПечати.Представление = НСтр("ru = 'Подробная калькуляция';
										|en = 'Detailed cost estimate'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПротоколОшибок";
	КомандаПечати.Порядок = 60;
	КомандаПечати.Представление = НСтр("ru = 'Протокол ошибок';
										|en = 'Error log'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

// Инициализирует параметры, обслуживающие выбор назначений в формах документа.
//
//  Возвращаемое значение:
//  См. Справочники.Назначения.МакетФормыВыбораНазначений
//
Функция МакетФормыВыбораНазначений() Экспорт
	
	МакетФормы = Справочники.Назначения.МакетФормыВыбораНазначений();
	ОписаниеКолонок = Справочники.Назначения.ДобавитьОписаниеКолонок(МакетФормы, "ВсеНазначения", Истина, "Объект.ОбъектыКалькуляции.Назначение");
	
	Возврат МакетФормы;
	
КонецФункции

#КонецОбласти

// Возвращает параметры выбора спецификаций для изделий, указанных в документе.
//
// Параметры:
//   Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров выбора спецификаций.
//
// Возвращаемое значение:
//   Структура - Структура, переопределяющая умолчания, заданные в функции УправлениеДаннымиОбИзделияхКлиентСервер.ПараметрыВыбораСпецификаций().
//
Функция ПараметрыВыбораСпецификаций(Объект) Экспорт
	
	ПараметрыВыбораСпецификаций = УправлениеДаннымиОбИзделияхКлиентСервер.ПараметрыВыбораСпецификаций();
	
	ПараметрыВыбораСпецификаций.ДоступныеТипы.Добавить(Перечисления.ТипыПроизводственныхПроцессов.Сборка);
	ПараметрыВыбораСпецификаций.ДоступныеТипы.Добавить(Перечисления.ТипыПроизводственныхПроцессов.Ремонт);
	
	ПараметрыВыбораСпецификаций.ДоступныеСтатусы.Добавить(Перечисления.СтатусыСпецификаций.Действует);
	
	СвязиПараметровВыбора = Новый Структура(УправлениеДаннымиОбИзделияхКлиентСервер.ПоляСтруктурыДанныхОбИзделииДляВыбораСпецификации());
	СвязиПараметровВыбора.Номенклатура            = "Объект.ОбъектыКалькуляции.Номенклатура";
	СвязиПараметровВыбора.Характеристика          = "Объект.ОбъектыКалькуляции.Характеристика";
	СвязиПараметровВыбора.НачалоПроизводства      = "Объект.Дата";
	СвязиПараметровВыбора.ПодразделениеДиспетчер  = "Объект.ПодразделениеДиспетчер";
	
	ПараметрыВыбораСпецификаций.СвязиПараметровВыбора.Вставить("Объект.ОбъектыКалькуляции.Объект", СвязиПараметровВыбора);
	
	Возврат ПараметрыВыбораСпецификаций;
	
КонецФункции

// Имена реквизитов, от значений которых зависят параметры выбора спецификаций
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую.
//
Функция ИменаРеквизитовДляЗаполненияПараметровВыбораСпецификаций() Экспорт
	
	ИменаРеквизитов = "";
	Возврат ИменаРеквизитов;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецОбласти

#Область ЗаполнениеДокументов

Функция ОписаниеПараметраЗаполненияЦенВКоллекции() Экспорт
	
	Описание = Новый Структура;
	Описание.Вставить("ДатаДокумента", Дата(1, 1, 1));
	Описание.Вставить("ВалютаДокумента", Справочники.Валюты.ПустаяСсылка());
	Описание.Вставить("Калькуляция", Документы.ПлановаяКалькуляция2_2.ПустаяСсылка());
	
	Возврат Описание;
	
КонецФункции

Процедура ЗаполнитьЦеныПоКалькуляцииВКоллекции(Коллекция, НаборСтрок, ПараметрыЗаполнения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Строки.Номенклатура КАК Номенклатура,
		|	Строки.Характеристика КАК Характеристика,
		|	Строки.Назначение КАК Назначение,
		|	Строки.Количество КАК Количество,
		|	&Калькуляция КАК Калькуляция
		|ПОМЕСТИТЬ ТаблицаПродукции
		|ИЗ
		|	&Коллекция КАК Строки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Назначение,
		|	Калькуляция
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПродукции.Номенклатура КАК Номенклатура,
		|	ТаблицаПродукции.Характеристика КАК Характеристика,
		|	ТаблицаПродукции.Назначение КАК Назначение,
		|	ТаблицаПродукции.Калькуляция КАК Калькуляция,
		|	Множители.Стоимость КАК Стоимость,
		|	Множители.КоличествоПолуфабриката КАК КоличествоПолуфабриката,
		|	1 КАК Приоритет
		|ПОМЕСТИТЬ ЦеныПродукции
		|ИЗ
		|	ТаблицаПродукции КАК ТаблицаПродукции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК Множители
		|		ПО ТаблицаПродукции.Калькуляция = Множители.Калькуляция
		|		И ТаблицаПродукции.Номенклатура = Множители.Продукция
		|		И ТаблицаПродукции.Характеристика = Множители.ХарактеристикаПродукции
		|		И ТаблицаПродукции.Назначение = Множители.Назначение
		|		И Множители.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаПродукции.Номенклатура,
		|	ТаблицаПродукции.Характеристика,
		|	ТаблицаПродукции.Назначение,
		|	ТаблицаПродукции.Калькуляция,
		|	Множители.Стоимость,
		|	Множители.КоличествоПолуфабриката,
		|	2 КАК Приоритет
		|ИЗ
		|	ТаблицаПродукции КАК ТаблицаПродукции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК Множители
		|		ПО ТаблицаПродукции.Калькуляция = Множители.Калькуляция
		|		И ТаблицаПродукции.Номенклатура = Множители.Продукция
		|		И ТаблицаПродукции.Характеристика = Множители.ХарактеристикаПродукции
		|		И Множители.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И Множители.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаПродукции.Номенклатура,
		|	ТаблицаПродукции.Характеристика,
		|	ТаблицаПродукции.Назначение,
		|	ТаблицаПродукции.Калькуляция,
		|	Множители.Стоимость,
		|	Множители.КоличествоПолуфабриката,
		|	3 КАК Приоритет
		|ИЗ
		|	ТаблицаПродукции КАК ТаблицаПродукции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК Множители
		|		ПО ТаблицаПродукции.Калькуляция = Множители.Калькуляция
		|		И ТаблицаПродукции.Номенклатура = Множители.Продукция
		|		И ТаблицаПродукции.Характеристика = Множители.ХарактеристикаПродукции
		|		И Множители.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Множители.Полуфабрикат КАК Номенклатура,
		|	Множители.ХарактеристикаПолуфабриката КАК Характеристика,
		|	ТаблицаПродукции.Назначение КАК Назначение,
		|	ТаблицаПродукции.Калькуляция КАК Калькуляция,
		|	Множители.Стоимость,
		|	Множители.КоличествоПолуфабриката,
		|	4 КАК Приоритет
		|ИЗ
		|	ТаблицаПродукции КАК ТаблицаПродукции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК Множители
		|		ПО ТаблицаПродукции.Калькуляция = Множители.Калькуляция
		|		И ТаблицаПродукции.Номенклатура = Множители.Полуфабрикат
		|		И ТаблицаПродукции.Характеристика = Множители.ХарактеристикаПолуфабриката
		|		И ТаблицаПродукции.Назначение = Множители.Назначение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Множители.Полуфабрикат КАК Номенклатура,
		|	Множители.ХарактеристикаПолуфабриката КАК Характеристика,
		|	ТаблицаПродукции.Назначение,
		|	ТаблицаПродукции.Калькуляция,
		|	Множители.Стоимость,
		|	Множители.КоличествоПолуфабриката,
		|	5 КАК Приоритет
		|ИЗ
		|	ТаблицаПродукции КАК ТаблицаПродукции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК Множители
		|		ПО ТаблицаПродукции.Калькуляция = Множители.Калькуляция
		|		И ТаблицаПродукции.Номенклатура = Множители.Полуфабрикат
		|		И ТаблицаПродукции.Характеристика = Множители.ХарактеристикаПолуфабриката
		|		И Множители.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Множители.Полуфабрикат КАК Номенклатура,
		|	Множители.ХарактеристикаПолуфабриката КАК Характеристика,
		|	ТаблицаПродукции.Назначение,
		|	ТаблицаПродукции.Калькуляция,
		|	Множители.Стоимость,
		|	Множители.КоличествоПолуфабриката,
		|	6 КАК Приоритет
		|ИЗ
		|	ТаблицаПродукции КАК ТаблицаПродукции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК Множители
		|		ПО ТаблицаПродукции.Калькуляция = Множители.Калькуляция
		|		И ТаблицаПродукции.Номенклатура = Множители.Полуфабрикат
		|		И ТаблицаПродукции.Характеристика = Множители.ХарактеристикаПолуфабриката
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЦеныПродукции.Номенклатура,
		|	ЦеныПродукции.Характеристика,
		|	ЦеныПродукции.Назначение,
		|	ЦеныПродукции.Калькуляция,
		|	СУММА(ЦеныПродукции.Стоимость) / СУММА(ЦеныПродукции.КоличествоПолуфабриката) КАК Цена,
		|	ЦеныПродукции.Приоритет КАК Приоритет
		|ИЗ
		|	ЦеныПродукции КАК ЦеныПродукции
		|
		|СГРУППИРОВАТЬ ПО
		|	ЦеныПродукции.Номенклатура,
		|	ЦеныПродукции.Характеристика,
		|	ЦеныПродукции.Назначение,
		|	ЦеныПродукции.Калькуляция,
		|	ЦеныПродукции.Приоритет
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
	
	Запрос.УстановитьПараметр("Коллекция", Коллекция.Выгрузить(НаборСтрок));
	Запрос.УстановитьПараметр("Калькуляция", ПараметрыЗаполнения.Калькуляция);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	ВалютаУпр = Константы.ВалютаУправленческогоУчета.Получить();
	ТребуетсяПересчет = Не (ПараметрыЗаполнения.ВалютаДокумента = ВалютаУпр);
	Если ТребуетсяПересчет Тогда
		
		ПараметрыКурсаКалькуляции = РаботаСКурсамиВалютУТ.ПолучитьКурсВалюты(ВалютаУпр, 
			ПараметрыЗаполнения.ДатаДокумента);
		ПараметрыКурсаДокумента = РаботаСКурсамиВалютУТ.ПолучитьКурсВалюты(ПараметрыЗаполнения.ВалютаДокумента, 
			ПараметрыЗаполнения.ДатаДокумента); 
		
	КонецЕсли;
	
	КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	ОбрабатываемыеСтроки = Новый Массив();
	
	Для Каждого ДанныеСтроки Из НаборСтрок Цикл
		
		ОтборСтрок = Новый Структура;
		ОтборСтрок.Вставить("Номенклатура", ДанныеСтроки.Номенклатура);
		ОтборСтрок.Вставить("Характеристика", ДанныеСтроки.Характеристика);
		Если ЗначениеЗаполнено(ДанныеСтроки.Назначение) Тогда
			// Ищем цену с назначением, только если в документе приемнике цены оно заполнено.
			ОтборСтрок.Вставить("Назначение", ДанныеСтроки.Назначение);
		КонецЕсли;

		Выборка.Сбросить();
		Если Не Выборка.НайтиСледующий(ОтборСтрок) Тогда
			Если ОтборСтрок.Свойство("Назначение") Тогда
				// В документе приемнике назначение указано, но нет цены с таким назначением,
				// пробуем найти цену без отбора по назначению.
				ОтборСтрок.Удалить("Назначение");
				Выборка.Сбросить();
				Если Не Выборка.НайтиСледующий(ОтборСтрок) Тогда
					Продолжить;
				КонецЕсли;
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если ТребуетсяПересчет Тогда
			ДанныеСтроки.Цена = РаботаСКурсамиВалютУТ.ПересчитатьПоКурсу(Выборка.Цена, 
				ПараметрыКурсаКалькуляции, ПараметрыКурсаДокумента);
		Иначе
			ДанныеСтроки.Цена = Выборка.Цена;
		КонецЕсли;
			
		ОбрабатываемыеСтроки.Добавить(ДанныеСтроки);

	КонецЦикла;
		
	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
		ОбрабатываемыеСтроки,
		СтруктураДействий,
		Коллекция,
		КэшированныеЗначения);
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;
	
	Возврат ИсточникиДанных; 
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	РеквизитыДокументов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Дата,Организация");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Период", КонецДня(РеквизитыДокументов.Дата));
	Запрос.УстановитьПараметр("ВидЦены", Справочники.ВидыЦен.ВидЦеныПлановойСтоимостиТМЦ());
	Запрос.УстановитьПараметр("НеРассчитанныйПериодПоОрганизации",
		РегистрыСведений.ЗаданияКРасчетуСебестоимости.ПолучитьНеРассчитанныйПериодПоОрганизации(РеквизитыДокументов.Организация));
	
КонецПроцедуры

Функция ТекстыЗапросаДляФормированияДвижений(Запрос, ТекстыЗапроса)
	
	УстановитьВыходныеИзделияПоСпецификации(Запрос);
	ТекстЗапросаВТОбъектыКалькуляции(Запрос, ТекстыЗапроса);
	
	Возврат ТекстыЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаМножителиПартийИПолуфабрикатов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МножителиПартийИПолуфабрикатов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТОбъектыКалькуляции", ТекстыЗапроса) Тогда
		ТекстыЗапросаДляФормированияДвижений(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПустойУИД", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ВЗ.Период КАК Период,
		|	ВЗ.Калькуляция КАК Калькуляция,
		|	ВЗ.Продукция КАК Продукция,
		|	ВЗ.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ВЗ.Назначение КАК Назначение,
		|	ВЗ.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	ВЗ.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	ВЗ.КлючПартия КАК КлючПартия,
		|	ВЗ.Полуфабрикат КАК Полуфабрикат,
		|	ВЗ.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	ВЗ.Порядок КАК Порядок,
		|	СУММА(ВЗ.Числитель) КАК Числитель,
		|	ВЗ.Знаменатель КАК Знаменатель,
		|	СУММА(ВЗ.КоличествоПолуфабриката) КАК КоличествоПолуфабриката,
		|	ВЗ.РасчетЗавершен КАК РасчетЗавершен,
		|	ВЗ.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	ВЗ.ПартияВыпущена КАК ПартияВыпущена
		|ИЗ
		|(
		|	ВЫБРАТЬ
		|		НАЧАЛОПЕРИОДА(ВыходныеИзделияПоЗаказу.ДатаПроизводства, ДЕНЬ) КАК Период,
		|		&Ссылка КАК Калькуляция,
		|		ВТОбъектыКалькуляции.Номенклатура КАК Продукция,
		|		ВТОбъектыКалькуляции.Характеристика КАК ХарактеристикаПродукции,
		|		ВТОбъектыКалькуляции.Назначение КАК Назначение,
		|		ВЫБОР КОГДА ВыходныеИзделияПоЗаказу.ПроизводствоНаСтороне И НЕ ВыходныеИзделияПоЗаказу.ПартияВыпущена
		|			ТОГДА
		|				ВыходныеИзделияПоЗаказу.Спецификация
		|			ИНАЧЕ
		|				ВыходныеИзделияПоЗаказу.ВыпускающийЭтап
		|		КОНЕЦ КАК ПартияСпецификацияПродукции,
		|		НЕОПРЕДЕЛЕНО КАК ПартияСпецификацияПолуфабриката,
		|		УНИКАЛЬНЫЙИДЕНТИФИКАТОР(ВТОбъектыКалькуляции.Характеристика) КАК КлючПартия,
		|		ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Полуфабрикат,
		|		ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаПолуфабриката,
		|		ВЫБОР
		|			КОГДА ВыходныеИзделияПоЗаказу.ПартияВыпущена
		|				ТОГДА -100
		|			КОГДА ВыходныеИзделияПоЗаказу.СписатьНаРасходы
		|				ТОГДА -1
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК Порядок,
		|		ВыходныеИзделияПоЗаказу.ДоляСтоимости КАК Числитель,
		|		ВЫБОР
		//	Комментарий 1045.
		//	В одном документе ЭтапПроизводства2_2 продукция может быть частично выпущена:
		//		1. Часть строк с галкой Произведено, часть без галки.
		//		2. Разные даты выпуска:
		//			2.1 Какие-то даты выпуска попали в закрытый период, какие-то нет.
		//			2.2 Какие-то даты выпуска до даты ПК, какие-то после.
		|			КОГДА ВыходныеИзделияПоЗаказу.ПартияВыпущена
		//	Для верного расчета затрат по выпущенным партиям знаменатель должен быть посчитан только по строкам,
		//		для которых ПартияВыпущена = ИСТИНА.
		|				ТОГДА ВсегоДолейСтоимости.ДоляПоВыпущеннымПартиям
		//	Для верного расчета по НЕ выпущенным партиям, знаменатель должен быть в целом по всему документу.
		|			ИНАЧЕ ВсегоДолейСтоимости.ВсегоДолей
		|		КОНЕЦ КАК Знаменатель,
		|		ВыходныеИзделияПоЗаказу.Количество КАК КоличествоПолуфабриката,
		|		ЛОЖЬ КАК РасчетЗавершен,
		|		ВТОбъектыКалькуляции.Объект КАК ЗаказНаПроизводство,
		|		ВыходныеИзделияПоЗаказу.ПартияВыпущена КАК ПартияВыпущена
		|	ИЗ
		|		ВТОбъектыКалькуляции КАК ВТОбъектыКалькуляции
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыходныеИзделияПоЗаказу КАК ВыходныеИзделияПоЗаказу
		|			ПО ВТОбъектыКалькуляции.Объект = ВыходныеИзделияПоЗаказу.Объект
		|				И ВТОбъектыКалькуляции.Номенклатура = ВыходныеИзделияПоЗаказу.Номенклатура
		|				И ВТОбъектыКалькуляции.Характеристика = ВыходныеИзделияПоЗаказу.Характеристика
		|				И ВТОбъектыКалькуляции.Назначение = ВыходныеИзделияПоЗаказу.Назначение
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсегоДолейСтоимости КАК ВсегоДолейСтоимости
		|			ПО (ВыходныеИзделияПоЗаказу.ВыпускающийЭтап = ВсегоДолейСтоимости.Объект)
		|	ГДЕ
		|		НЕ (ВыходныеИзделияПоЗаказу.ПартияВыпущена И ВыходныеИзделияПоЗаказу.СписатьНаРасходы)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		&Период КАК Период,
		|		&Ссылка КАК Калькуляция,
		|		ВТОбъектыКалькуляции.Номенклатура КАК Продукция,
		|		ВТОбъектыКалькуляции.Характеристика КАК ХарактеристикаПродукции,
		|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|		ВыходныеИзделияПоСпецификации.Спецификация КАК ПартияСпецификацияПродукции,
		|		НЕОПРЕДЕЛЕНО КАК ПартияСпецификацияПолуфабриката,
		|		УНИКАЛЬНЫЙИДЕНТИФИКАТОР(ВТОбъектыКалькуляции.Характеристика) КАК КлючПартия,
		|		ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Полуфабрикат,
		|		ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаПолуфабриката,
		|		0 КАК Порядок,
		|		ВыходныеИзделияПоСпецификации.ДоляСтоимости КАК Числитель,
		|		ВЫБОР 
		|			КОГДА ВсегоДолейСтоимости.ВсегоДолей = 0
		|				ТОГДА 1
		|			ИНАЧЕ
		|				ВсегоДолейСтоимости.ВсегоДолей
		|		КОНЕЦ КАК Знаменатель,
		|		ВыходныеИзделияПоСпецификации.Количество КАК КоличествоПолуфабриката,
		|		ЛОЖЬ КАК РасчетЗавершен,
		|		НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|		ЛОЖЬ КАК ПартияВыпущена
		|	ИЗ
		|		ВТОбъектыКалькуляции КАК ВТОбъектыКалькуляции
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыходныеИзделияПоСпецификации КАК ВыходныеИзделияПоСпецификации
		|			ПО ВТОбъектыКалькуляции.Объект = ВыходныеИзделияПоСпецификации.Спецификация
		|				И ВТОбъектыКалькуляции.Номенклатура = ВыходныеИзделияПоСпецификации.Номенклатура
		|				И (ВТОбъектыКалькуляции.Характеристика = ВыходныеИзделияПоСпецификации.Характеристика 
		|					ИЛИ ВыходныеИзделияПоСпецификации.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
		|
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсегоДолейСтоимости КАК ВсегоДолейСтоимости
		|			ПО (ВыходныеИзделияПоСпецификации.Спецификация = ВсегоДолейСтоимости.Объект)
		|
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
		|			ПО ВТОбъектыКалькуляции.Объект = РесурсныеСпецификации.Ссылка
		|				И (РесурсныеСпецификации.ТипПроизводственногоПроцесса В (
		|					ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Сборка), 
		|					ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Ремонт))
		|					)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		&Период КАК Период,
		|		&Ссылка КАК Калькуляция,
		|		ВТОбъектыКалькуляции.Номенклатура КАК Продукция,
		|		ВТОбъектыКалькуляции.Характеристика КАК ХарактеристикаПродукции,
		|		ВТОбъектыКалькуляции.Назначение КАК Назначение,
		|		ВЫБОР КОГДА ДД.ВыпускающийЭтап ССЫЛКА Документ.ЭтапПроизводства2_2
		|			ТОГДА ДД.ВыпускающийЭтап
		|			ИНАЧЕ ДД.Спецификация
		|		КОНЕЦ КАК ПартияСпецификацияПродукции,
		|		НЕОПРЕДЕЛЕНО КАК ПартияСпецификацияПолуфабриката,
		|		ДД.КлючПартия КАК КлючПартия,
		|		ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Полуфабрикат,
		|		ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаПолуфабриката,
		// Признак дин. структуры Порядок = -1024 см. ОбработатьПродукциюПоЗаказу.ОбработатьПродукциюПоЗаказу
		|		-1024 КАК Порядок,
		|		ДД.ДоляСтоимости КАК Числитель,
		|		1 КАК Знаменатель,
		|		ДД.Количество КАК КоличествоПолуфабриката,
		|		ЛОЖЬ КАК РасчетЗавершен,
		|		ДД.Объект КАК ЗаказНаПроизводство,
		|		ЛОЖЬ КАК ПартияВыпущена
		|	ИЗ
		|		ВТОбъектыКалькуляции КАК ВТОбъектыКалькуляции
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыходныеИзделияПоЗаказуДинамическаяСтруктура КАК ДД
		|			ПО ВТОбъектыКалькуляции.Объект = ДД.Объект
		|				И ВТОбъектыКалькуляции.Номенклатура = ДД.Номенклатура
		|				И ВТОбъектыКалькуляции.Характеристика = ДД.Характеристика
		|				И ВТОбъектыКалькуляции.Назначение = ДД.Назначение
		|) КАК ВЗ
		|СГРУППИРОВАТЬ ПО
		|	ВЗ.Период,
		|	ВЗ.Калькуляция,
		|	ВЗ.Продукция,
		|	ВЗ.ХарактеристикаПродукции,
		|	ВЗ.Назначение,
		|	ВЗ.ПартияСпецификацияПродукции,
		|	ВЗ.ПартияСпецификацияПолуфабриката,
		|	ВЗ.КлючПартия,
		|	ВЗ.Полуфабрикат,
		|	ВЗ.ХарактеристикаПолуфабриката,
		|	ВЗ.Порядок,
		|	ВЗ.Знаменатель,
		|	ВЗ.РасчетЗавершен,
		|	ВЗ.ЗаказНаПроизводство,
		|	ВЗ.ПартияВыпущена
		|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

// Текст запроса выходные изделия по заказу динамическая структура, необходим при старте расчета ПК,
// для стартового сбора данных необходимых для расчета.
// 
// Возвращаемое значение:
//  Строка - Текст запроса выходные изделия по заказу динамическая структура
Функция ТекстЗапроса_ВыходныеИзделияПоЗаказуДинамическаяСтруктура()
	Возврат "
	|ВЫБРАТЬ
	|	ОбъектыКРазузлованию.Объект КАК Объект,
	|	СтруктураЗаказа.Спецификация КАК Спецификация,
	|	СтруктураЗаказа.КлючПартия КАК КлючПартия,
	|	СтруктураЗаказа.Номенклатура КАК Номенклатура,
	|	СтруктураЗаказа.Характеристика КАК Характеристика,
	|	СтруктураЗаказа.Назначение КАК Назначение,
	|	СтруктураЗаказа.Этап КАК ВыпускающийЭтап,
	|	1 КАК ДоляСтоимости,
	|	СУММА(СтруктураЗаказа.Запланировано) КАК Количество
	|ПОМЕСТИТЬ ВыходныеИзделияПоЗаказуДинамическаяСтруктура
	|ИЗ
	|	ОбъектыКРазузлованию КАК ОбъектыКРазузлованию
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтруктураЗаказа КАК СтруктураЗаказа
	|		ПО ОбъектыКРазузлованию.ДинамическаяСтруктура
	|			И ОбъектыКРазузлованию.Объект = СтруктураЗаказа.ЗаказНаПроизводство
	|			И СтруктураЗаказа.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПроизводствоПродукции)
	|			И СтруктураЗаказа.Этап Ссылка Справочник.ЭтапыПроизводства
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбъектыКРазузлованию.Объект,
	|	СтруктураЗаказа.Спецификация,
	|	СтруктураЗаказа.КлючПартия,
	|	СтруктураЗаказа.Назначение,
	|	СтруктураЗаказа.Номенклатура,
	|	СтруктураЗаказа.Характеристика,
	|	СтруктураЗаказа.Этап
	|	
	|ИМЕЮЩИЕ
	|	СУММА(СтруктураЗаказа.Запланировано) > 0
	|";
	
КонецФункции

Процедура ТекстЗапросаВТОбъектыКалькуляции(Запрос, ТекстыЗапроса)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОбъектыКалькуляции.Номенклатура,
		|	ОбъектыКалькуляции.Характеристика,
		|	ОбъектыКалькуляции.Назначение,
		|	ОбъектыКалькуляции.Объект
		|ПОМЕСТИТЬ ВТОбъектыКалькуляции
		|ИЗ
		|	Документ.ПлановаяКалькуляция2_2.ОбъектыКалькуляции КАК ОбъектыКалькуляции
		|ГДЕ
		|	ОбъектыКалькуляции.Ссылка = &Ссылка";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТОбъектыКалькуляции");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОбъектыКалькуляции.Объект КАК Объект,
		|	ОбъектыКалькуляции.Объект ССЫЛКА Документ.ЗаказНаПроизводство2_2
		|		И ВЫРАЗИТЬ(ОбъектыКалькуляции.Объект КАК Документ.ЗаказНаПроизводство2_2).ДинамическаяСтруктура КАК ДинамическаяСтруктура
		|ПОМЕСТИТЬ ОбъектыКРазузлованию
		|ИЗ
		|	ВТОбъектыКалькуляции КАК ОбъектыКалькуляции
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Объект";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТОбъектыКалькуляции");
	
	ТекстЗапроса = "ВЫБРАТЬ
		|	ОбъектыКРазузлованию.Объект КАК Объект,
		|	Реквизиты.ПроизводствоНаСтороне
		|		ИЛИ Реквизиты.ПроизводствоНаСтороне2_5
		|		ИЛИ Реквизиты.ВнутренняяПереработка КАК ПроизводствоНаСтороне,
		|	Реквизиты.Спецификация КАК Спецификация,
		|	Изделия.Номенклатура КАК Номенклатура,
		|	Изделия.Характеристика КАК Характеристика,
		|	Изделия.Назначение КАК Назначение,
		|	Изделия.Ссылка КАК ВыпускающийЭтап,
		|	Изделия.СписатьНаРасходы КАК СписатьНаРасходы,
		|	ВЫБОР
		|		КОГДА Реквизиты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
		|				ИЛИ Изделия.Произведено
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.ПроизводствоОднойДатой
		|						ТОГДА Реквизиты.ДатаПроизводства <= &Период
		|								И Реквизиты.ДатаПроизводства < &НеРассчитанныйПериодПоОрганизации
		|					ИНАЧЕ Изделия.ДатаПроизводства <= &Период
		|							И Изделия.ДатаПроизводства < &НеРассчитанныйПериодПоОрганизации
		|				КОНЕЦ
		|			ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПартияВыпущена,
		|	ВЫБОР
		|		КОГДА Реквизиты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
		|				ИЛИ Изделия.Произведено
		|			ТОГДА ВЫБОР
		|					КОГДА Реквизиты.ПроизводствоОднойДатой
		|						ТОГДА Реквизиты.ДатаПроизводства
		|					ИНАЧЕ Изделия.ДатаПроизводства
		|				КОНЕЦ
		|		ИНАЧЕ Реквизиты.Дата
		|	КОНЕЦ КАК ДатаПроизводства,
		|	СУММА(ВЫБОР
		|			КОГДА Изделия.ДоляСтоимости = 0
		|				ТОГДА Изделия.Количество
		|			ИНАЧЕ Изделия.ДоляСтоимости
		|		КОНЕЦ) КАК ДоляСтоимости,
		|	СУММА(Изделия.Количество) КАК Количество
		|
		|ПОМЕСТИТЬ ВыходныеИзделияПоЗаказу
		|ИЗ
		|	ОбъектыКРазузлованию КАК ОбъектыКРазузлованию
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК Изделия
		|		ПО &УсловияСоединения
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК Реквизиты
		|		ПО Изделия.Ссылка = Реквизиты.Ссылка
		|			И Реквизиты.Проведен
		|
		|СГРУППИРОВАТЬ ПО
		|	ОбъектыКРазузлованию.Объект,
		|	Реквизиты.Дата,
		|	Реквизиты.Спецификация,
		|	Реквизиты.ПроизводствоНаСтороне,
		|	Реквизиты.ПроизводствоНаСтороне2_5,
		|	Реквизиты.ВнутренняяПереработка,
		|	Изделия.Назначение,
		|	Изделия.Номенклатура,
		|	Изделия.Характеристика,
		|	Изделия.Ссылка,
		|	Изделия.СписатьНаРасходы,
		|	Реквизиты.Статус,
		|	Реквизиты.ПроизводствоОднойДатой,
		|	Реквизиты.ДатаПроизводства,
		|	Изделия.Произведено,
		|	Изделия.ДатаПроизводства
		|";
	
	ВыбираемыеПоля = Новый Структура;
	ВыбираемыеПоля.Вставить("Номенклатура",   "ВыходныеИзделия.Номенклатура");
	ВыбираемыеПоля.Вставить("Характеристика", "ВыходныеИзделия.Характеристика");
	ВыбираемыеПоля.Вставить("Назначение",     "ВыходныеИзделия.Назначение");
	ВыбираемыеПоля.Вставить("ДоляСтоимости",  "ВыходныеИзделия.ДоляСтоимости");
	ВыбираемыеПоля.Вставить("Количество",     "ВыходныеИзделия.Количество");
	
	ТекстЗапроса = Документы.ЭтапПроизводства2_2.ПодставитьВыбираемыеПоля(ТекстЗапроса, ВыбираемыеПоля);
	ТекстЗапроса = Документы.ЭтапПроизводства2_2.ПодставитьСоединениеДляПолученияВыходныхИзделий(ТекстЗапроса, 
		"УсловияСоединения",
		Новый Структура("Распоряжение", "ОбъектыКРазузлованию.Объект"));
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВыходныеИзделияПоЗаказу");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ИзделияСпецификаций.Номенклатура КАК Номенклатура,
		|	ИзделияСпецификаций.Характеристика КАК Характеристика,
		|	ИзделияСпецификаций.Спецификация КАК Спецификация,
		|	ИзделияСпецификаций.ДоляСтоимости КАК ДоляСтоимости,
		|	ИзделияСпецификаций.ВсегоДолей КАК ВсегоДолей,
		|	ИзделияСпецификаций.КоличествоИзделие КАК Количество
		|ПОМЕСТИТЬ РезультатыРазузлования
		|ИЗ
		|	&ИзделияСпецификаций КАК ИзделияСпецификаций";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "РезультатыРазузлования");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВТОбъектыКалькуляции.Номенклатура КАК Номенклатура,
		|	ВТОбъектыКалькуляции.Характеристика КАК Характеристика,
		|	ВТОбъектыКалькуляции.Объект КАК Спецификация,
		|	МАКСИМУМ(РезультатыРазузлования.ДоляСтоимости) КАК ДоляСтоимости,
		|	МАКСИМУМ(РезультатыРазузлования.Количество) КАК Количество
		|ПОМЕСТИТЬ ВыходныеИзделияПоСпецификации
		|ИЗ
		|	ВТОбъектыКалькуляции КАК ВТОбъектыКалькуляции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РезультатыРазузлования КАК РезультатыРазузлования
		|		ПО ВТОбъектыКалькуляции.Номенклатура = РезультатыРазузлования.Номенклатура
		|			И ВТОбъектыКалькуляции.Характеристика = РезультатыРазузлования.Характеристика
		|			И ВТОбъектыКалькуляции.Объект = РезультатыРазузлования.Спецификация
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТОбъектыКалькуляции.Номенклатура,
		|	ВТОбъектыКалькуляции.Характеристика,
		|	ВТОбъектыКалькуляции.Объект";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВыходныеИзделияПоСпецификации");
	
	ТекстыЗапроса.Добавить(
		ТекстЗапроса_ВыходныеИзделияПоЗаказуДинамическаяСтруктура(),
		"ВыходныеИзделияПоЗаказуДинамическаяСтруктура"
	);
	
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ОбъектыКРазузлованию.Объект КАК Объект,
		|	МАКСИМУМ(РезультатыРазузлования.ВсегоДолей) КАК ВсегоДолей,
		|	0 ДоляПоВыпущеннымПартиям
		|ПОМЕСТИТЬ ВсегоДолейСтоимости
		|ИЗ
		|	ОбъектыКРазузлованию КАК ОбъектыКРазузлованию
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РезультатыРазузлования КАК РезультатыРазузлования
		|		ПО ОбъектыКРазузлованию.Объект = РезультатыРазузлования.Спецификация
		|
		|СГРУППИРОВАТЬ ПО
		|	ОбъектыКРазузлованию.Объект
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВыходныеИзделияПоЗаказу.ВыпускающийЭтап КАК ВыпускающийЭтап,
		|	СУММА(ВыходныеИзделияПоЗаказу.ДоляСтоимости) КАК ВсегоДолей,
		|	СУММА(ВЫБОР
		|				КОГДА ВыходныеИзделияПоЗаказу.ПартияВыпущена
		|					ТОГДА ВыходныеИзделияПоЗаказу.ДоляСтоимости
		|				ИНАЧЕ
		|					0
		|	КОНЕЦ) КАК ДоляПоВыпущеннымПартиям
		|ИЗ
		|	ВыходныеИзделияПоЗаказу КАК ВыходныеИзделияПоЗаказу
		|
		|СГРУППИРОВАТЬ ПО
		|	ВыходныеИзделияПоЗаказу.ВыпускающийЭтап
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Объект";
	
	ПараметрыПодстановки = ПроизводствоСервер.ПараметрыПодстановкиАлгоритмаРасчетаДолейСтоимости("Изделия", "Реквизиты.СпособРаспределенияЗатратНаВыходныеИзделия");
	ПараметрыПодстановки.ДоляСтоимости = "&ДоляСтоимости";
	ПараметрыПодстановки.СоединениеДоляСтоимости = "И &СоединениеДоляСтоимости";
	ПараметрыПодстановки.Дата = "&Период";
	
	ПроизводствоСервер.ВыполнитьПодстановкуАлгоритмаРасчетаДолейСтоимости(ТекстЗапроса, ПараметрыПодстановки);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВсегоДолейСтоимости");
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

Функция ЕстьАктивныеФоновыеЗадания(МассивЗаданий)

	Для Каждого Задание Из МассивЗаданий Цикл
		Если Задание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
		
КонецФункции

Процедура УстановитьВыходныеИзделияПоСпецификации(Запрос)
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Объекты.Номенклатура КАК Номенклатура,
		|	Объекты.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	Объекты.Характеристика КАК Характеристика,
		|	Объекты.Количество КАК Количество,
		|	Объекты.Объект КАК Спецификация,
		|	Реквизиты.Дата КАК НачалоПроизводства,
		|	Реквизиты.ПодразделениеДиспетчер КАК ПодразделениеДиспетчер,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности
		|ИЗ
		|	Документ.ПлановаяКалькуляция2_2.ОбъектыКалькуляции КАК Объекты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлановаяКалькуляция2_2 КАК Реквизиты
		|		ПО Объекты.Ссылка = Реквизиты.Ссылка
		|ГДЕ
		|	Реквизиты.Ссылка = &Ссылка
		|	И Реквизиты.ОбъектКалькуляции = ЗНАЧЕНИЕ(Перечисление.ОбъектыКалькуляции.РесурснаяСпецификация)";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	СписокНоменклатуры = Справочники.РесурсныеСпецификации.СписокНоменклатуры();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = СписокНоменклатуры.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
	КЧ = Новый КвалификаторыЧисла(10, 3);
	Изделия = Новый ТаблицаЗначений;
	Изделия.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Изделия.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Изделия.Колонки.Добавить("Спецификация", Новый ОписаниеТипов("СправочникСсылка.РесурсныеСпецификации"));
	Изделия.Колонки.Добавить("ДоляСтоимости", Новый ОписаниеТипов("Число", КЧ));
	Изделия.Колонки.Добавить("ВсегоДолей", Новый ОписаниеТипов("Число", КЧ));
	Изделия.Колонки.Добавить("КоличествоИзделие", Новый ОписаниеТипов("Число", КЧ));
	
	ПараметрыВыборки = Справочники.РесурсныеСпецификации.ПараметрыВыборкиДанных("ВыходныеИзделия");
	ПараметрыВыборки.ПереопределениеНастройкиПартииВыпуска.Использовать = Истина;
	ПараметрыВыборки.ОкруглятьКоличествоШтучныхТоваров = Ложь;
	ПараметрыВыборки.РассчитыватьДолиСтоимостиВыходныхИзделий = Истина;
	
	ПараметрыВыборки.Вставить("ОтключитьРасчетПараметрическихФормул", Истина);
	
	ДанныеСпецификаций = Справочники.РесурсныеСпецификации.ДанныеСпецификацииПоСпискуНоменклатуры(СписокНоменклатуры, ПараметрыВыборки);
	
	Индекс = 0;
	Пока Индекс < ДанныеСпецификаций.Количество() Цикл
		
		ДанныеСпецификации = ДанныеСпецификаций[Индекс];
		ДанныеПоНоменклатуре = СписокНоменклатуры[Индекс];
		Индекс = Индекс + 1;
		
		ВыходныеИзделия = ДанныеСпецификации.ВыходныеИзделия;
		КлючПоиска = Новый Структура("Номенклатура, Характеристика");
		ЗаполнитьЗначенияСвойств(КлючПоиска, ДанныеПоНоменклатуре);
		
		ДанныеПоИзделию = ВыходныеИзделия.НайтиСтроки(КлючПоиска);
		Если ДанныеПоИзделию.Количество() = 0 Тогда
			
			КлючПоиска.Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
			ДанныеПоИзделию = ВыходныеИзделия.НайтиСтроки(КлючПоиска);
			
			Если ДанныеПоИзделию.Количество() = 0 Тогда
				
				КлючПоиска = Новый Структура("ВидНоменклатуры");
				ЗаполнитьЗначенияСвойств(КлючПоиска, ДанныеПоНоменклатуре);
				
				ДанныеПоИзделию = ВыходныеИзделия.НайтиСтроки(КлючПоиска);
				Если ДанныеПоИзделию.Количество() = 0 Тогда
					Продолжить;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		НовоеИзделие = Изделия.Добавить();
		ЗаполнитьЗначенияСвойств(НовоеИзделие, ДанныеПоНоменклатуре);
		Для Каждого Изделие Из ДанныеПоИзделию Цикл
			
			НовоеИзделие.КоличествоИзделие = НовоеИзделие.КоличествоИзделие
				+ Изделие.КоличествоУпаковокНаЕдиницуПартииВыпуска * Изделие.ДанныеУпаковки.Числитель / Изделие.ДанныеУпаковки.Знаменатель;
			Если Изделие.ДоляСтоимостиНаЕдиницу = 0 Тогда
				НовоеИзделие.ДоляСтоимости = 1;
			Иначе
				НовоеИзделие.ДоляСтоимости = НовоеИзделие.ДоляСтоимости + Изделие.ДоляСтоимостиНаЕдиницу;
			КонецЕсли;
			
		КонецЦикла;
		
		Для каждого Изделие Из ВыходныеИзделия Цикл
			НовоеИзделие.ВсегоДолей = НовоеИзделие.ВсегоДолей + Изделие.ДоляСтоимостиНаЕдиницу
				* Изделие.КоличествоУпаковокНаЕдиницуПартииВыпуска * Изделие.ДанныеУпаковки.Числитель / Изделие.ДанныеУпаковки.Знаменатель;
		КонецЦикла;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ИзделияСпецификаций", Изделия);
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - см. УправлениеПечатьюПереопределяемый.ПриПечати.МассивОбъектов
//  ПараметрыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыПечати
//  КоллекцияПечатныхФорм - см. УправлениеПечатьюПереопределяемый.ПриПечати.КоллекцияПечатныхФорм
//  ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
//  ПараметрыВывода - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыВывода
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПлановаяКалькуляцияСводно") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПлановаяКалькуляцияСводно", НСтр("ru = 'Калькуляция продукции (сводно)';
																														|en = 'Product costing (summary)'"), СформироватьПечатнуюФормуКалькуляции(МассивОбъектов, ОбъектыПечати, Истина));
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПлановаяКалькуляцияПодробно") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПлановаяКалькуляцияПодробно", НСтр("ru = 'Калькуляция продукции (подробно)';
																														|en = 'Product costing (detailed)'"), СформироватьПечатнуюФормуКалькуляции(МассивОбъектов, ОбъектыПечати, Ложь));
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПротоколОшибок") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПротоколОшибок", НСтр("ru = 'Протокол ошибок';
																											|en = 'Error log'"), СформироватьПечатнуюФормуПротоколаОшибок(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
	ФормированиеПечатныхФорм.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуКалькуляции(МассивОбъектов, ОбъектыПечати, Сводно)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПлановаяКалькуляция2_2.ПФ_MXL_ПлановаяКалькуляция");
	
	Запрос = Новый Запрос;
	ТекстыЗапросов = Новый Массив();
	ТекстЗапроса = "ВЫБРАТЬ
		|	Реквизиты.Организация КАК Организация,
		|	Реквизиты.Ссылка КАК Ссылка,
		|	Реквизиты.ОбъектКалькуляции КАК ОбъектКалькуляции,
		|	ПлановаяКалькуляция.Номенклатура КАК Номенклатура,
		|	ПлановаяКалькуляция.Характеристика КАК Характеристика,
		|	ПлановаяКалькуляция.Назначение КАК Назначение,
		|	ПлановаяКалькуляция.Объект КАК Объект,
		|	ПлановаяКалькуляция.Упаковка КАК Упаковка,
		|	ПлановаяКалькуляция.Количество КАК Количество,
		|	ПлановаяКалькуляция.КоличествоУпаковок КАК КоличествоУпаковок,
		|	Реквизиты.Номер КАК Номер,
		|	Реквизиты.Дата КАК Дата,
		|	Реквизиты.Ответственный.ФизическоеЛицо КАК ФизЛицо
		|ПОМЕСТИТЬ ШапкаТаблицы
		|ИЗ
		|	Документ.ПлановаяКалькуляция2_2.ОбъектыКалькуляции КАК ПлановаяКалькуляция
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлановаяКалькуляция2_2 КАК Реквизиты
		|		ПО ПлановаяКалькуляция.Ссылка = Реквизиты.Ссылка
		|ГДЕ
		|	Реквизиты.Ссылка В(&ПлановыеКалькуляции)
		|";
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	ТекстыЗапросов.Добавить(Отчеты.ПлановаяСебестоимостьПродукции.ТекстЗапроса_ДанныеПлановыхКалькуляций(Истина));
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ШапкаТаблицы.Организация КАК Организация,
		|	ШапкаТаблицы.Ссылка КАК Калькуляция,
		|	ШапкаТаблицы.ОбъектКалькуляции КАК ОбъектКалькуляции,
		|	ШапкаТаблицы.Номенклатура КАК Продукция,
		|	ШапкаТаблицы.Характеристика КАК Характеристика,
		|	ШапкаТаблицы.Назначение КАК Назначение,
		|	ШапкаТаблицы.Объект КАК Объект,
		|	ВЫБОР
		|		КОГДА ШапкаТаблицы.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|			ТОГДА ПРЕДСТАВЛЕНИЕ(ШапкаТаблицы.Номенклатура.ЕдиницаИзмерения)
		|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ШапкаТаблицы.Упаковка)
		|	КОНЕЦ КАК ЕдиницаИзмерения,
		|	ШапкаТаблицы.КоличествоУпаковок КАК Количество,
		|	ШапкаТаблицы.Номер КАК Номер,
		|	ШапкаТаблицы.Дата КАК Дата,
		|	ШапкаТаблицы.ФизЛицо КАК ФизЛицо,
		|	ПРЕДСТАВЛЕНИЕ(ШапкаТаблицы.Ссылка) КАК Представление
		|ИЗ
		|	ШапкаТаблицы КАК ШапкаТаблицы
		|
		|УПОРЯДОЧИТЬ ПО
		|	Калькуляция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Калькуляция КАК Калькуляция,
		|	ВложенныйЗапрос.Объект КАК Объект,
		|	ВложенныйЗапрос.Продукция КАК Продукция,
		|	ВложенныйЗапрос.ХарактеристикаПродукции КАК Характеристика,
		|	ВложенныйЗапрос.Назначение КАК Назначение,
		|	ВложенныйЗапрос.ТипЗатрат КАК ТипЗатраты,
		|	ВложенныйЗапрос.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ВложенныйЗапрос.Номенклатура КАК Затрата,
		|	ВложенныйЗапрос.НоменклатураЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВложенныйЗапрос.Характеристика КАК ХарактеристикаЗатраты,
		|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
		|	СУММА(ВложенныйЗапрос.Стоимость) КАК Сумма,
		|	СУММА(ВложенныйЗапрос.СтоимостьЗабалансовая) КАК СуммаЗабалансовая,
		|	ВЫБОР
		|		КОГДА СУММА(ВложенныйЗапрос.Количество) = 0
		|				ТОГДА 0
		|			ИНАЧЕ СУММА(ВложенныйЗапрос.Стоимость) / СУММА(ВложенныйЗапрос.Количество)
		|		КОНЕЦ КАК Цена,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВложенныйЗапрос.Номенклатура) КАК ЗатратаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВложенныйЗапрос.Характеристика) КАК ХарактеристикаЗатратыПредставление
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПлановыеЗатраты.Калькуляция КАК Калькуляция,
		|		ПлановыеЗатраты.ОбъектКалькуляции КАК Объект,
		|		ПлановыеЗатраты.Продукция КАК Продукция,
		|		ПлановыеЗатраты.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|		ПлановыеЗатраты.Назначение КАК Назначение,
		|		ПлановыеЗатраты.ТипЗатрат КАК ТипЗатрат,
		|		ПлановыеЗатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|		ПлановыеЗатраты.Номенклатура КАК Номенклатура,
		|		ПлановыеЗатраты.Номенклатура.ЕдиницаИзмерения КАК НоменклатураЕдиницаИзмерения,
		|		ПлановыеЗатраты.Характеристика КАК Характеристика,
		|		ПлановыеЗатраты.Количество КАК Количество,
		|		ВЫБОР КОГДА ПлановыеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА 0
		|			ИНАЧЕ ПлановыеЗатраты.Стоимость
		|		КОНЕЦ КАК Стоимость,
		|		ВЫБОР КОГДА ПлановыеЗатраты.ЭтоПолуфабрикат 
		|			ТОГДА 0
		|			ИНАЧЕ ПлановыеЗатраты.Забалансовая
		|		КОНЕЦ КАК СтоимостьЗабалансовая
		|	ИЗ
		|		Данные КАК ПлановыеЗатраты
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ФактическиеЗатраты.Калькуляция,
		|		ФактическиеЗатраты.ОбъектКалькуляции,
		|		ФактическиеЗатраты.Продукция,
		|		ФактическиеЗатраты.ХарактеристикаПродукции,
		|		ФактическиеЗатраты.Назначение,
		|		ФактическиеЗатраты.ТипЗатрат,
		|		ФактическиеЗатраты.СтатьяКалькуляции,
		|		ФактическиеЗатраты.Номенклатура,
		|		ФактическиеЗатраты.НоменклатураЕдиницаИзмерения,
		|		ФактическиеЗатраты.Характеристика,
		|		ВЫБОР
		|			КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
		|				ТОГДА ФактическиеЗатраты.Количество
		|			ИНАЧЕ ОпределениеПолуфабрикатов.Количество * ФактическиеЗатраты.Числитель / ФактическиеЗатраты.Знаменатель
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
		|				ТОГДА ФактическиеЗатраты.Стоимость
		|			КОГДА НЕ ОпределениеПолуфабрикатов.ФиксСтоимость
		|				ТОГДА 0
		|			ИНАЧЕ ОпределениеПолуфабрикатов.Стоимость * ФактическиеЗатраты.Числитель / ФактическиеЗатраты.Знаменатель
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
		|				ТОГДА ФактическиеЗатраты.Забалансовая
		|			КОГДА НЕ ОпределениеПолуфабрикатов.ФиксСтоимость
		|				ТОГДА 0
		|			ИНАЧЕ ОпределениеПолуфабрикатов.СтоимостьЗабалансовая * ФактическиеЗатраты.Числитель / ФактическиеЗатраты.Знаменатель
		|		КОНЕЦ
		|	ИЗ
		|		ФактическиеЗатраты КАК ФактическиеЗатраты
		|			ЛЕВОЕ СОЕДИНЕНИЕ ОпределениеПолуфабрикатов КАК ОпределениеПолуфабрикатов
		|			ПО ФактическиеЗатраты.АналитикаУчетаНоменклатуры = ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры
		|				И (ВЫБОР
		|					КОГДА ФактическиеЗатраты.ЭтоПродукция
		|						ТОГДА ФактическиеЗатраты.ПартияСпецификацияПродукции = ОпределениеПолуфабрикатов.ПартияСпецификацияПродукции
		|					ИНАЧЕ ФактическиеЗатраты.ПартияПолуфабриката = ОпределениеПолуфабрикатов.ПартияСпецификацияПродукции
		|					КОНЕЦ)
		|	) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.СтатьяКалькуляции,
		|	ВложенныйЗапрос.Объект,
		|	ВложенныйЗапрос.НоменклатураЕдиницаИзмерения,
		|	ВложенныйЗапрос.Назначение,
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Продукция,
		|	ВложенныйЗапрос.Калькуляция,
		|	ВложенныйЗапрос.ТипЗатрат,
		|	ВложенныйЗапрос.ХарактеристикаПродукции,
		|	ВложенныйЗапрос.Характеристика
		|
		|ИМЕЮЩИЕ
		|	НЕ СУММА(ВложенныйЗапрос.Количество) = 0
		|		ИЛИ НЕ СУММА(ВложенныйЗапрос.Стоимость) = 0";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	Запрос.Текст = СтрСоединить(ТекстыЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос.УстановитьПараметр("ПлановыеКалькуляции", МассивОбъектов);
	Запрос.УстановитьПараметр("ДанныеПоСебестоимости", 3);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ПолеСлева = 5;
	ТабличныйДокумент.ПолеСправа = 5;
	ТабличныйДокумент.РазмерКолонтитулаСверху = 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу = 0;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПлановаяКалькуляция";
	
	Запрос.Текст = Документы.ЭтапПроизводства2_2.ПодставитьВыбираемыеПоля(Запрос.Текст, 
		Новый Структура("Количество, КодСтроки", 
			"ЭтапПроизводства2_2ВыходныеИзделия.Количество", "ЭтапПроизводства2_2ВыходныеИзделия.КодСтроки"));
	
	ПоляСоединения = Новый Структура;
	ПоляСоединения.Вставить("Этап", "ОбщиеМножители.ПартияСпецификацияПродукции");
	ПоляСоединения.Вставить("Номенклатура", "ОбщиеМножители.Продукция");
	ПоляСоединения.Вставить("Характеристика", "ОбщиеМножители.ХарактеристикаПродукции");
	ПоляСоединения.Вставить("Назначение", "ОбщиеМножители.Назначение");
	Запрос.Текст = Документы.ЭтапПроизводства2_2.ПодставитьСоединениеДляПолученияВыходныхИзделий(Запрос.Текст, 
		"УсловияСоединения",
		ПоляСоединения);
	
	МассивРезультатов  = Запрос.ВыполнитьПакет();
	
	ДанныеШапки = МассивРезультатов[МассивРезультатов.Количество() - 2].Выбрать();
	ДетальныеЗаписи = МассивРезультатов[МассивРезультатов.Количество() - 1].Выгрузить(); // ТаблицаЗначений
	Если Сводно Тогда
		ДетальныеЗаписи.Свернуть("Продукция, Характеристика, Назначение, Калькуляция, Объект, ТипЗатраты, СтатьяКалькуляции", "Сумма, СуммаЗабалансовая");
	КонецЕсли;
	
	ДетальныеЗаписи.Индексы.Добавить("Калькуляция, Объект, Продукция, Характеристика, Назначение");
	
	ПервыйДокумент = Истина;
		
	Пока ДанныеШапки.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
					
		// Вывод общих реквизитов шапки.
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		
		ПараметрыОбласти = Новый Структура("ТекстЗаголовка");
		ПараметрыОбласти.ТекстЗаголовка = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(
			ДанныеШапки, НСтр("ru = 'Калькуляция продукции';
								|en = 'Product costing'", ОбщегоНазначения.КодОсновногоЯзыка()));
		ОбластьМакета.Параметры.Заполнить(ПараметрыОбласти);
		
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьМакета, ДанныеШапки.Калькуляция);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ПервыйДокумент    = Ложь;
		КалькуляцияЗаказа = ДанныеШапки.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.ЗаказНаПроизводство;

		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок_Организация");
		ПараметрыОбласти = Новый Структура("Организация", ДанныеШапки.Организация);
		ОбластьМакета.Параметры.Заполнить(ПараметрыОбласти);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		Если КалькуляцияЗаказа Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Заголовок_Заказ");
		Иначе
			ОбластьМакета = Макет.ПолучитьОбласть("Заголовок_Спецификация");
		КонецЕсли;
		
		ПараметрыОбласти = Новый Структура("Количество", 
			Формат(ДанныеШапки.Количество, "ЧЦ=10; ЧДЦ=3") + " " + ДанныеШапки.ЕдиницаИзмерения);
		ОбластьМакета.Параметры.Заполнить(ДанныеШапки);
		ОбластьМакета.Параметры.Заполнить(ПараметрыОбласти);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		СтруктураПоиска = Новый Структура("Калькуляция, Объект, Продукция, Характеристика, Назначение");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ДанныеШапки);
		
		ОтборБезЗабаланса = Новый Структура("СуммаЗабалансовая", 0);
		
		ДетальныеЗаписиНабора = ДетальныеЗаписи.Скопировать(СтруктураПоиска);
		Если ДетальныеЗаписиНабора.Количество() - ДетальныеЗаписиНабора.НайтиСтроки(ОтборБезЗабаланса).Количество() > 0 Тогда
			ШапкаБезДетализации = "ШапкаБезДетализацииСЗабалансом";
			СтрокиТаблицыСводно = "СтрокиТаблицыСводноСЗабалансом";
			СтрокиТаблицыСводноТипЗатраты = "СтрокиТаблицыСводноТипЗатратыСЗабалансом";
			ИтогиТаблицыСводно = "ИтогиТаблицыСводноСЗабалансом";
			ШапкаПодробно = "ШапкаПодробноСЗабалансом";
			СтрокиТаблицыПодробно = "СтрокиТаблицыПодробноСЗабалансом";
			СтрокиТаблицыПодробноТипЗатраты = "СтрокиТаблицыПодробноТипЗатратыСЗабалансом";
			СтрокиТаблицыПодробноСтатьяКалькуляции = "СтрокиТаблицыПодробноСтатьяКалькуляцииСЗабалансом";
			ИтогиТаблицыПодробно = "ИтогиТаблицыПодробноСЗабалансом";
		Иначе
			ШапкаБезДетализации = "ШапкаБезДетализации";
			СтрокиТаблицыСводно = "СтрокиТаблицыСводно";
			СтрокиТаблицыСводноТипЗатраты = "СтрокиТаблицыСводноТипЗатраты";
			ИтогиТаблицыСводно = "ИтогиТаблицыСводно";
			ШапкаПодробно = "ШапкаПодробно";
			СтрокиТаблицыПодробно = "СтрокиТаблицыПодробно";
			СтрокиТаблицыПодробноТипЗатраты = "СтрокиТаблицыПодробноТипЗатраты";
			СтрокиТаблицыПодробноСтатьяКалькуляции = "СтрокиТаблицыПодробноСтатьяКалькуляции";
			ИтогиТаблицыПодробно = "ИтогиТаблицыПодробно";
		КонецЕсли;
		
		Если Сводно Тогда
			
			ОбластьШапки = Макет.ПолучитьОбласть(ШапкаБезДетализации);
			ОбластьСтрок = Макет.ПолучитьОбласть(СтрокиТаблицыСводно);
			ОбластьТипЗатрат = Макет.ПолучитьОбласть(СтрокиТаблицыСводноТипЗатраты);
			ОбластьСтатьяКалькуляции = Неопределено;
			ОбластьИтогов = Макет.ПолучитьОбласть(ИтогиТаблицыСводно);
			
		Иначе
			
			ОбластьШапки = Макет.ПолучитьОбласть(ШапкаПодробно);
			ОбластьСтрок = Макет.ПолучитьОбласть(СтрокиТаблицыПодробно);
			ОбластьТипЗатрат = Макет.ПолучитьОбласть(СтрокиТаблицыПодробноТипЗатраты);
			ОбластьСтатьяКалькуляции = Макет.ПолучитьОбласть(СтрокиТаблицыПодробноСтатьяКалькуляции);
			ОбластьИтогов = Макет.ПолучитьОбласть(ИтогиТаблицыПодробно);
			
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьШапки);
		
		Если Сводно Тогда
			ДетальныеЗаписиНабора.Сортировать("ТипЗатраты, СтатьяКалькуляции");
		Иначе
			ДетальныеЗаписиНабора.Сортировать("ТипЗатраты, СтатьяКалькуляции, Затрата");
		КонецЕсли;
		
		Материальные = 0;
		ВозвратныеОтходы = 0;
		МатериальныеЗабаланс = 0;
		ОплатаТруда = 0;

		ПрочиеПеременные = 0;
		ПрочиеПостоянные = 0;
		
		ТипЗатраты = "";
		СтатьяКалькуляции = Неопределено;
		
		Для Каждого ДетальнаяЗапись Из ДетальныеЗаписиНабора Цикл
			
			Если Не ТипЗатраты = ДетальнаяЗапись.ТипЗатраты Тогда
				
				СтатьяКалькуляции = Неопределено;
				ИтогоПоТипуЗатрат = 0;
				ИтогоПоТипуЗатратЗабаланс = 0;
				СтрокиПоТипуЗатрат = ДетальныеЗаписиНабора.НайтиСтроки(Новый Структура("ТипЗатраты", ДетальнаяЗапись.ТипЗатраты));
				
				Для Каждого СтрокаПоТипуЗатрат Из СтрокиПоТипуЗатрат Цикл
					
					ИтогоПоТипуЗатрат = ИтогоПоТипуЗатрат + СтрокаПоТипуЗатрат.Сумма;
					ИтогоПоТипуЗатратЗабаланс = ИтогоПоТипуЗатратЗабаланс + СтрокаПоТипуЗатрат.СуммаЗабалансовая;
					
				КонецЦикла;
				
				Если ДетальнаяЗапись.ТипЗатраты = "Материальные" Тогда
					
					ПредставлениеТипаЗатрат = НСтр("ru = 'Материальные';
													|en = 'Material'", ОбщегоНазначения.КодОсновногоЯзыка());
					Материальные = ИтогоПоТипуЗатрат;
					МатериальныеЗабаланс = ИтогоПоТипуЗатратЗабаланс;
					
				ИначеЕсли ДетальнаяЗапись.ТипЗатраты = "ВозвратныеОтходы" Тогда
					
					ПредставлениеТипаЗатрат = НСтр("ru = 'Возвратные отходы/Побочный выпуск';
													|en = 'Scrap and spoilage/Side release'", ОбщегоНазначения.КодОсновногоЯзыка());
					ВозвратныеОтходы = ИтогоПоТипуЗатрат;
					
				ИначеЕсли ДетальнаяЗапись.ТипЗатраты = "Трудозатраты" Тогда
					
					ПредставлениеТипаЗатрат = НСтр("ru = 'Оплата труда';
													|en = 'Payroll'", ОбщегоНазначения.КодОсновногоЯзыка());
					ОплатаТруда = ИтогоПоТипуЗатрат;
					
				ИначеЕсли ДетальнаяЗапись.ТипЗатраты = "ПрочиеПеременные" Тогда
					ПредставлениеТипаЗатрат = НСтр("ru = 'Постатейные переменные';
													|en = 'Variable costs'", ОбщегоНазначения.КодОсновногоЯзыка());
					ПрочиеПеременные = ИтогоПоТипуЗатрат;
					
				ИначеЕсли ДетальнаяЗапись.ТипЗатраты = "ПрочиеПостоянные" Тогда
					
					ПредставлениеТипаЗатрат = НСтр("ru = 'Постатейные постоянные';
													|en = 'Itemized fixed'", ОбщегоНазначения.КодОсновногоЯзыка());
					ПрочиеПостоянные = ИтогоПоТипуЗатрат;
					
				КонецЕсли;
				
				ПараметрыОбласти = Новый Структура;
				ПараметрыОбласти.Вставить("ТипЗатраты", ПредставлениеТипаЗатрат);
				ПараметрыОбласти.Вставить("Сумма", ИтогоПоТипуЗатрат);
				ПараметрыОбласти.Вставить("СуммаЗабалансовая", ИтогоПоТипуЗатратЗабаланс);
				ОбластьТипЗатрат.Параметры.Заполнить(ПараметрыОбласти);
				
				ТабличныйДокумент.Вывести(ОбластьТипЗатрат);
				
			КонецЕсли;
			
			Если Не ОбластьСтатьяКалькуляции = Неопределено И Не СтатьяКалькуляции = ДетальнаяЗапись.СтатьяКалькуляции Тогда
				
				ОтборСтрок = Новый Структура("ТипЗатраты, СтатьяКалькуляции", 
					ДетальнаяЗапись.ТипЗатраты, ДетальнаяЗапись.СтатьяКалькуляции);
					
				СтрокиПоСтатьейКалькуляции = ДетальныеЗаписиНабора.НайтиСтроки(ОтборСтрок);
				ИтогоПоСтатье = 0;
				ИтогоПоСтатьеЗабаланс = 0;
				
				Для Каждого СтрокаПоСтатье Из СтрокиПоСтатьейКалькуляции Цикл
					
					ИтогоПоСтатье = ИтогоПоСтатье + СтрокаПоСтатье.Сумма;
					ИтогоПоСтатьеЗабаланс = ИтогоПоСтатьеЗабаланс + СтрокаПоСтатье.СуммаЗабалансовая;
					
				КонецЦикла;
				
				ПараметрыОбласти = Новый Структура;
				ПараметрыОбласти.Вставить("СтатьяКалькуляции", ДетальнаяЗапись.СтатьяКалькуляции);
				ПараметрыОбласти.Вставить("Сумма", ИтогоПоСтатье);
				ПараметрыОбласти.Вставить("СуммаЗабалансовая", ИтогоПоСтатьеЗабаланс);
				ОбластьСтатьяКалькуляции.Параметры.Заполнить(ПараметрыОбласти);
				
				ТабличныйДокумент.Вывести(ОбластьСтатьяКалькуляции);
				
			КонецЕсли;
			
			ТипЗатраты = ДетальнаяЗапись.ТипЗатраты;
			СтатьяКалькуляции = ДетальнаяЗапись.СтатьяКалькуляции;
			
			ОбластьСтрок.Параметры.Заполнить(ДетальнаяЗапись);
			Если Не Сводно И ЗначениеЗаполнено(ДетальнаяЗапись.ХарактеристикаЗатраты) Тогда
				
				ПараметрыОбласти = Новый Структура("Затрата", 
					ДетальнаяЗапись.ЗатратаПредставление + ", " + ДетальнаяЗапись.ХарактеристикаЗатратыПредставление);
				ОбластьСтрок.Параметры.Заполнить(ПараметрыОбласти);
				
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьСтрок);
			
		КонецЦикла;
		
		ПараметрыОбласти = Новый Структура;
		ПараметрыОбласти.Вставить("ВсегоМатериальные", Материальные);
		ПараметрыОбласти.Вставить("ВсегоВозвратныеОтходы", ВозвратныеОтходы);
		ПараметрыОбласти.Вставить("ВсегоТрудозатрат", ОплатаТруда);
		ПараметрыОбласти.Вставить("ВсегоПостатейные", ПрочиеПеременные + ПрочиеПостоянные);
		ПараметрыОбласти.Вставить("Итого",
			Материальные + ОплатаТруда + ПрочиеПеременные + ПрочиеПостоянные + ВозвратныеОтходы);
		ПараметрыОбласти.Вставить("ИтогоЗабаланс", МатериальныеЗабаланс);
		ПараметрыОбласти.Вставить("Валюта", Строка(ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ДанныеШапки.Организация)));
		ОбластьИтогов.Параметры.Заполнить(ПараметрыОбласти);
		
		ТабличныйДокумент.Вывести(ОбластьИтогов);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
		ПараметрыОбласти = Новый Структура("Ответственный",
			ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(ДанныеШапки.ФизЛицо, ДанныеШапки.Дата));
		ОбластьМакета.Параметры.Заполнить(ПараметрыОбласти);
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция СформироватьПечатнуюФормуПротоколаОшибок(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПлановаяКалькуляция2_2.ПФ_MXL_ПротоколОшибок");
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	РегистрыСведений.МножителиПартийИПолуфабрикатов.ПолучитьЦеныНоменклатуры(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивПлановыхКалькуляций", МассивОбъектов);
	
	МассивТекстовЗапроса = Новый Массив();
	Запрос.УстановитьПараметр("ИспользоватьРазрядыРасценокРаботСотрудников",
		ПолучитьФункциональнуюОпцию("ИспользоватьРазрядыРасценокРаботСотрудников"));
	//++ Локализация
	Запрос.УстановитьПараметр("ДополнительныйРазрезТарифнойСетки",
		Константы.ДополнительныйРазрезТарифнойСетки.Получить());
	//-- Локализация
	МассивТекстовЗапроса.Добавить(РегистрыНакопления.ПлановыеТрудозатраты.ТекстЗапросаРасценкиТрудозатрат(Ложь));
	
	ТекстЗапроса = "ВЫБРАТЬ
		|	ПлановыеМатериальныеЗатраты.Калькуляция КАК Регистратор,
		|	ПлановыеМатериальныеЗатраты.Номенклатура КАК Затрата,
		|	ПлановыеМатериальныеЗатраты.Характеристика,
		|	""Материальные"" КАК ТипЗатраты
		|ПОМЕСТИТЬ Затраты
		|ИЗ
		|	РегистрНакопления.ПлановыеМатериальныеЗатраты КАК ПлановыеМатериальныеЗатраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО ПлановыеМатериальныеЗатраты.Калькуляция = ЦеныНоменклатуры.Калькуляция
		|			И ПлановыеМатериальныеЗатраты.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|			И ПлановыеМатериальныеЗатраты.Характеристика = ЦеныНоменклатуры.Характеристика
		|			И ПлановыеМатериальныеЗатраты.Период = ЦеныНоменклатуры.Период
		|ГДЕ
		|	НЕ ПлановыеМатериальныеЗатраты.ЭтоПолуфабрикат
		|	И ПлановыеМатериальныеЗатраты.Калькуляция В (&МассивПлановыхКалькуляций)
		|	И ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлановыеМатериальныеЗатраты.Калькуляция,
		|	ПлановыеМатериальныеЗатраты.Номенклатура,
		|	ПлановыеМатериальныеЗатраты.Характеристика
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПлановыеТрудозатраты.Калькуляция КАК Регистратор,
		|	ПлановыеТрудозатраты.ВидРабот,
		|	НЕОПРЕДЕЛЕНО,
		|	""Трудозатраты""
		|ИЗ
		|	РегистрНакопления.ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|	ЛЕВОЕ СОЕДИНЕНИЕ Расценки КАК Расценки
		|	ПО ПлановыеТрудозатраты.Калькуляция = Расценки.Калькуляция
		|		И ПлановыеТрудозатраты.ВидРабот = Расценки.ВидРабот
		|		И ПлановыеТрудозатраты.Период = Расценки.Период
		|		И ПлановыеТрудозатраты.Подразделение = Расценки.Подразделение
		|ГДЕ
		|	ПлановыеТрудозатраты.Калькуляция В (&МассивПлановыхКалькуляций)
		|	И ЕСТЬNULL(Расценки.Расценка, 0) = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлановыеТрудозатраты.Калькуляция,
		|	ПлановыеТрудозатраты.ВидРабот
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Затраты.Затрата КАК Затрата,
		|	Затраты.Характеристика КАК Характеристика,
		|	Затраты.Регистратор КАК Регистратор,
		|	Затраты.ТипЗатраты КАК ТипЗатраты,
		|	Реквизиты.Дата КАК Дата,
		|	Реквизиты.Номер КАК Номер
		|ИЗ
		|	Затраты КАК Затраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПлановаяКалькуляция2_2 КАК Реквизиты
		|		ПО Затраты.Регистратор = Реквизиты.Ссылка
		|ИТОГИ ПО
		|	Регистратор";
		
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	Затраты = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ПолеСлева = 5;
	ТабличныйДокумент.ПолеСправа = 5;
	ТабличныйДокумент.РазмерКолонтитулаСверху = 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу = 0;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПротоколОшибок";
	
	ПервыйДокумент = Истина;
	
	Для Каждого СтрокаРегистратора Из Затраты.Строки Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		// Вывод общих реквизитов шапки.
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьМакета, СтрокаРегистратора.Регистратор);
		ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Протокол ошибок %1';
				|en = 'Error log %1'", ОбщегоНазначения.КодОсновногоЯзыка()),
			ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(СтрокаРегистратора, НСтр("ru = 'калькуляции продукции';
																									|en = 'product costings'", ОбщегоНазначения.КодОсновногоЯзыка())));
		ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ПервыйДокумент = Ложь;
		
		СтрокиЗатрат = СтрокаРегистратора.Строки;
		Если СтрокиЗатрат.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Материалы = СтрокиЗатрат.НайтиСтроки(Новый Структура("ТипЗатраты", "Материальные"));
		Трудозатраты = СтрокиЗатрат.НайтиСтроки(Новый Структура("ТипЗатраты", "Трудозатраты"));
		
		Если Материалы.Количество() >= Трудозатраты.Количество() Тогда
			
			ИмяОбласти = "Материалы";
			ТаблицаОбхода = Материалы;
			ТаблицаДополнения = Трудозатраты;
			
			ПолеОбласти = "Номенклатура";
			ПолеДополнения = "ВидРаботы";
			
		Иначе
			
			ИмяОбласти = "Работы";
			ТаблицаОбхода = Трудозатраты;
			ТаблицаДополнения = Материалы;
			
			ПолеОбласти = "ВидРаботы";
			ПолеДополнения = "Номенклатура";
			
		КонецЕсли;
		
		ОбластьДополнения = "";
		Если Материалы.Количество() > 0 И Трудозатраты.Количество() > 0 Тогда
			ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ИначеЕсли Материалы.Количество() = 0 И Трудозатраты.Количество() > 0 Тогда
			
			ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаРаботы");
			ОбластьДополнения = "|КолонкаРаботы";
			
		Иначе 
			
			ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаМатериалы");
			ОбластьДополнения = "|КолонкаМатериалы";
			
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		Индекс = 0;
		Для Каждого СтрокаЗатраты Из ТаблицаОбхода Цикл
			
			СтрокаДополнения = Неопределено;
			Если Индекс < ТаблицаДополнения.Количество() Тогда
				
				ОбластьМакета = Макет.ПолучитьОбласть("МатериалыРаботы");
				СтрокаДополнения = ТаблицаДополнения[Индекс];
				
			Иначе
				ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти + ОбластьДополнения);
			КонецЕсли;
			
			ОбластьМакета.Параметры.Заполнить(СтрокаЗатраты);
			ОбластьМакета.Параметры[ПолеОбласти] = СтрокаЗатраты.Затрата;
			
			Если Не СтрокаДополнения = Неопределено Тогда
				
				ОбластьМакета.Параметры.Заполнить(СтрокаДополнения);
				ОбластьМакета.Параметры[ПолеДополнения] = СтрокаДополнения.Затрата;
				
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла;
		
		СтруктураИтогов = Новый Структура();
		СтруктураИтогов.Вставить("КоличествоМатериалов", Материалы.Количество());
		СтруктураИтогов.Вставить("КоличествоРабот", Трудозатраты.Количество());
		
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал" + ОбластьДополнения);
		ОбластьМакета.Параметры.Заполнить(СтруктураИтогов);
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

#Область АудитСостоянияСистемы

// Добавляет новую проверку состояния системы в таблицу проверок и заполняет ее ключевые поля.
//
// Параметры:
//	ТаблицаПроверок 			 - см. АудитСостоянияСистемы.ТаблицаПроверокСостоянияСистемы
//	Идентификатор 				 - Строка - уникальный строковый идентификатор (код) проверки
//	КонтекстПроверокВеденияУчета 			 - СправочникСсылка.ОперацииЗакрытияМесяца - этап закрытия месяца (владелец), к которому относится проверка
//	УточнениеКонтекстаПроверокВеденияУчета - ПеречислениеСсылка.МоментЗапускаПроверкиОперацииЗакрытияМесяца - когда надо выполнять проверку
//	Обработчик 					 - Строка - полное имя экспортной процедуры-обработчика, вызываемого для выполнения проверки
//	ВажностьПроблемы 					 - ПеречислениеСсылка.ВажностьПроблемыУчета - статус по умолчанию для результатов выполнения проверки.
// 
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - добавленная строка таблицы проверок.
//
Функция ДобавитьОписаниеНовойПроверки(ТаблицаПроверок, Идентификатор, КонтекстПроверокВеденияУчета,
			УточнениеКонтекстаПроверокВеденияУчета, Обработчик, ВажностьПроблемы = Неопределено) Экспорт
	
	ОписаниеПроверки = ТаблицаПроверок.Добавить();
	
	ОписаниеПроверки.Идентификатор 				  = Идентификатор;
	ОписаниеПроверки.ИдентификаторРодителя 		 = "КалькуляцииПродукции";
	ОписаниеПроверки.КонтекстПроверокВеденияУчета 		  = КонтекстПроверокВеденияУчета;
	ОписаниеПроверки.УточнениеКонтекстаПроверокВеденияУчета = УточнениеКонтекстаПроверокВеденияУчета;
	ОписаниеПроверки.Обработчик 				  = Обработчик;
	ОписаниеПроверки.ВажностьПроблемы 					  =
		?(ЗначениеЗаполнено(ВажностьПроблемы), ВажностьПроблемы, Перечисления.ВажностьПроблемыУчета.Ошибка);
		
	ОписаниеПроверки.ВыполняетсяТолькоВКонтексте = Истина;
	ОписаниеПроверки.ВозможноИзменениеВажности   = Ложь;
	
	ОписаниеПроверки.ДетализацияДоОрганизации    = Истина;
	ОписаниеПроверки.ДетализацияДоПериода        = Ложь;
	ОписаниеПроверки.Использование			     = Истина;
	
	Возврат ОписаниеПроверки;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
