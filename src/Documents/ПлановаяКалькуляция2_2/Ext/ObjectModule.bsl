#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокумент();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Массив") Тогда
		
		Если ДанныеЗаполнения.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ПроверитьВозможностьВводаНаОсновании(ДанныеЗаполнения);
		
		Если ТипЗнч(ДанныеЗаполнения[0]) = Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
			ЗаполнитьПоСпецификациям(ДанныеЗаполнения);
		ИначеЕсли ТипЗнч(ДанныеЗаполнения[0]) = Тип("ДокументСсылка.ЗаказНаПроизводство2_2") Тогда
			ЗаполнитьПоЗаказу(ДанныеЗаполнения, Ложь);
		ИначеЕсли ТипЗнч(ДанныеЗаполнения[0]) = Тип("ДокументСсылка.ЭтапПроизводства2_2") Тогда
			ЗаполнитьПоЭтапу(ДанныеЗаполнения);
		ИначеЕсли ТипЗнч(ДанныеЗаполнения[0]) = Тип("ДокументСсылка.ПланПроизводства") Тогда
			ЗаполнитьПоПлануПроизводства(ДанныеЗаполнения);
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнениеОбъектовПоСтатистике.ЗаполнитьРеквизитыОбъекта(ЭтотОбъект, ДанныеЗаполнения);
	
	Если Не ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверки.ИмяТЧ = "ОбъектыКалькуляции";
	
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(
		ЭтотОбъект,
		МассивНепроверяемыхРеквизитов,
		Отказ,
		ПараметрыПроверки);
		
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияКоличества();
	ПараметрыПроверки.ИмяТЧ = "ОбъектыКалькуляции";
	НоменклатураСервер.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, ПараметрыПроверки);
	
	Если ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.ЗаказНаПроизводство Тогда
		ЗаказыНаПроизводство = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОбъектыКалькуляции.ВыгрузитьКолонку("Объект"));
		ПроверитьВозможностьРасчетаПлановойКалькуляции(Ссылка, ЗаказыНаПроизводство,
			Отказ);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
	Если ПроведениеДокументов.СвойстваДокумента(ЭтотОбъект).РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		ЭтоПроведение = Ложь;
		ЗаписатьОчередьФоновогоПроведения(ЭтоПроведение);
		ЗапуститьФоновуюОбработкуДвижений(Ссылка, ЭтоПроведение);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	ЭтоПроведение = Истина;
	ЗаписатьОчередьФоновогоПроведения(ЭтоПроведение);
	ЗапуститьФоновуюОбработкуДвижений(Ссылка, ЭтоПроведение);
		
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет возможность расчета плановой калькуляции по заказам на производство
// 
// Параметры:
//  ПлановаяКалькуляцияСсылка - ДокументСсылка.ПлановаяКалькуляция2_2
//  ЗаказыНаПроизводство - Массив из ДокументСсылка.ЗаказНаПроизводство2_2 - массив ссылок на заказы на производство, 
//   по которым необходимо проверить готовность данных к расчету плановой калькуляции.
//  Отказ - Булево
// 
Процедура ПроверитьВозможностьРасчетаПлановойКалькуляции(ПлановаяКалькуляцияСсылка, ЗаказыНаПроизводство, Отказ) Экспорт

	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("МассивСсылок", ЗаказыНаПроизводство);
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказНаПроизводство2_2.Ссылка КАК Ссылка,
	|	ЗаказНаПроизводство2_2.ДинамическаяСтруктура КАК ДинамическаяСтруктура,
	|	ЗаказНаПроизводство2_2.Проведен КАК Проведен,
	|	ЗаказНаПроизводство2_2.Статус КАК Статус
	|ПОМЕСТИТЬ ЗаказыНаПроизводство
	|ИЗ
	|	Документ.ЗаказНаПроизводство2_2 КАК ЗаказНаПроизводство2_2
	|ГДЕ
	|	ЗаказНаПроизводство2_2.Ссылка В(&МассивСсылок)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДинамическаяСтруктура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(Таблица.ДинамическаяСтруктура) КАК ЕстьЗаказыДинамическаяСтруктура,
	|	МАКСИМУМ(НЕ Таблица.ДинамическаяСтруктура) КАК ЕстьЗаказыСтатическаяСтруктура
	|ИЗ
	|	ЗаказыНаПроизводство КАК Таблица
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(Таблица.ДинамическаяСтруктура) ЕСТЬ НЕ NULL
	|";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ТекстСообщения = НСтр("ru = 'Для выполнения расчета плановой калькуляции необходимо выбрать как минимум один заказ на производство, в табличной части ""Заказы на производство"".';
								|en = 'To calculate the product standard cost, select at least one production order in the ""Production orders"" table.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ПлановаяКалькуляцияСсылка, , , Отказ);
		Возврат;
	КонецЕсли;
	
	ВыборкаТипыЗаказов = РезультатЗапроса.Выбрать();
	ВыборкаТипыЗаказов.Следующий();
	
	ТекстыЗапроса = Новый Массив();
	ТекстыЗапроса.Добавить(Документы.ЗаказНаПроизводство2_2.ТекстЗапросаВТТребуетсяСформироватьЭтапы(ВыборкаТипыЗаказов));
	// Параметры для запроса выше
	Запрос.УстановитьПараметр("СтатусРабочийГрафик",
		РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик());
	ИспользуетсяГрафикПроизводства = УправлениеПроизводствомПовтИсп.ИспользуетсяГрафикПроизводства();
	Запрос.УстановитьПараметр("ИспользуетсяГрафикПроизводства", ИспользуетсяГрафикПроизводства);
	Запрос.УстановитьПараметр("ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров",
		Константы.ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров.Получить());

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДД.Ссылка КАК ЗаказНаПроизводство,
	|	1 КАК КодОшибки,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектОшибки
	|ИЗ ВтТребуетсяСформироватьЭтапы КАК ДД
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказыНаПроизводство.Ссылка КАК ЗаказНаПроизводство,
	|	2 КАК КодОшибки,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектОшибки
	|ИЗ
	|	ЗаказыНаПроизводство КАК ЗаказыНаПроизводство
	|ГДЕ
	|	ИСТИНА В (ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ Документ.ЭтапПроизводства2_2 КАК Этапы
	|			ГДЕ ЗаказыНаПроизводство.Проведен
	|				И Этапы.Распоряжение = ЗаказыНаПроизводство.Ссылка
	|				И Этапы.Проведен
	|				И Этапы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Формируется)
	|			)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказыНаПроизводство.Ссылка КАК ЗаказНаПроизводство,
	|	3 КАК КодОшибки,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектОшибки
	|ИЗ
	|	ЗаказыНаПроизводство КАК ЗаказыНаПроизводство
	|ГДЕ
	|	ЗаказыНаПроизводство.ДинамическаяСтруктура
	|	И ЗаказыНаПроизводство.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство2_2.Формируется)
	|	И ЗаказыНаПроизводство.Проведен
	|	И ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			РегистрСведений.ПротоколРасчетаСтруктурыЗаказа КАК ПротоколРасчетаСтруктурыЗаказа
	|		ГДЕ
	|			ЗаказыНаПроизводство.Ссылка = ПротоколРасчетаСтруктурыЗаказа.ЗаказНаПроизводство
	|			И ПротоколРасчетаСтруктурыЗаказа.ТипОшибки В (&ПроверяемыеТипыОшибокДинЗаказовНаПроизводство)
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.Ссылка КАК ЗаказНаПроизводство,
	|	4 КАК КодОшибки,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектОшибки
	|ИЗ ЗаказыНаПроизводство КАК ДД
	|ГДЕ
	|	ДД.Проведен
	|	И ДД.ДинамическаяСтруктура
	|	И (
	|		ИСТИНА В (ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|				ИЗ
	|					РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаСпецификации КАК Т
	|				ГДЕ
	|					Т.ЗаказНаПроизводство = ДД.Ссылка)
	|		ИЛИ ИСТИНА В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					ИСТИНА
	|				ИЗ
	|					РегистрСведений.БуферЗаданийКРасчетуСтруктурыЗаказаСпецификации КАК Т
	|				ГДЕ
	|					Т.ЗаказНаПроизводство = ДД.Ссылка)
	|		ИЛИ ИСТИНА В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					ИСТИНА
	|				ИЗ
	|					РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаРаспределениеЗапасов КАК Т
	|				ГДЕ
	|					Т.ЗаказНаПроизводство = ДД.Ссылка)
	|		ИЛИ ИСТИНА В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					ИСТИНА
	|				ИЗ
	|					РегистрСведений.БуферЗаданийКРасчетуСтруктурыЗаказаРаспределениеЗапасов КАК Т
	|				ГДЕ
	|					Т.ЗаказНаПроизводство = ДД.Ссылка)
	|		ИЛИ ИСТИНА В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					ИСТИНА
	|				ИЗ
	|					РегистрСведений.ЗаданияКОчисткеСтруктурыЗаказа КАК Т
	|				ГДЕ
	|					Т.ЗаказНаПроизводство = ДД.Ссылка)
	|		ИЛИ &ИспользуютсяГрафикиПРоизводства
	|	)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.Ссылка КАК ЗаказНаПроизводство,
	|	5 КАК КодОшибки,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектОшибки
	|ИЗ ЗаказыНаПроизводство КАК ДД
	|ГДЕ
	|	НЕ ДД.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.Ссылка КАК ЗаказНаПроизводство,
	|	6 КАК КодОшибки,
	|	НЕОПРЕДЕЛЕНО КАК ОбъектОшибки
	|ИЗ ЗаказыНаПроизводство КАК ДД
	|ГДЕ
	|	ДД.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство2_2.Формируется)
	|";
	
	Если ИспользуетсяГрафикПроизводства Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИЛИ &ИспользуютсяГрафикиПРоизводства", "");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИЛИ &ИспользуютсяГрафикиПРоизводства", "ИЛИ ИСТИНА В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства КАК Т
		|				ГДЕ
		|					Т.ЗаказНаПроизводство = ДД.Ссылка)");
	КонецЕсли;
	
	КодыОшибок = Новый Соответствие();
	КодыОшибок.Вставить(1, НСтр("ru = 'Для расчета плановой калькуляции, требуется запланировать документ %1 - создать этапы производства.';
								|en = 'To calculate the product standard cost, schedule the %1 document: create production stages.'"));
	КодыОшибок.Вставить(2, НСтр("ru = 'В структуре документа %1, есть этап/этапы в статусе ""Формируется"", расчет плановой калькуляции может быть некорректен.';
								|en = 'The structure of the ""%1"" document contains a stage/stages with the ""Created"" status. The product standard cost may be calculated incorrectly.'"));
	КодыОшибок.Вставить(3, НСтр("ru = 'Есть ошибки в расчете динамической структуры документа %1, расчет плановой калькуляции может быть некорректен.';
								|en = 'There are errors in calculating the dynamic structure of the ""%1"" document. The product standard cost may be calculated incorrectly.'"));
	КодыОшибок.Вставить(4, НСтр("ru = 'Для расчета плановой калькуляции, требуется рассчитать динамическую структуру документа %1.';
								|en = 'To calculate the product standard cost, calculate the dynamic structure of the ""%1"" document.'"));
	КодыОшибок.Вставить(5, НСтр("ru = 'Для расчета плановой калькуляции, документ %1, должен быть проведен.';
								|en = 'To calculate the product standard cost, post the ""%1"" document.'"));
	КодыОшибок.Вставить(6, НСтр("ru = 'Для расчета плановой калькуляции, документ %1, должен быть в статусе ""К производству"" или ""Закрыт"".';
								|en = 'To calculate the product standard cost, the ""%1"" document status must be set to ""For production"" or ""Closed"".'"));
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	Запрос.Текст = УправлениеПроизводством.ОбъединитьТекстыЗапросаВПакет(ТекстыЗапроса);
	
	ПроверяемыеТипыОшибокДинЗаказовНаПроизводство = Новый Массив();
	ТипыОшибок = Перечисления.ТипыОшибокРасчетаСтруктурыЗаказаНаПроизводство;
	ПроверяемыеТипыОшибокДинЗаказовНаПроизводство.Добавить(ТипыОшибок.ОшибкаРазузлования);
	ПроверяемыеТипыОшибокДинЗаказовНаПроизводство.Добавить(ТипыОшибок.ОшибкаЗаполненияДанныхСпецификации);
	ПроверяемыеТипыОшибокДинЗаказовНаПроизводство.Добавить(ТипыОшибок.ОшибкаПодбораСпецификаций);
	Запрос.УстановитьПараметр("ПроверяемыеТипыОшибокДинЗаказовНаПроизводство",
		ПроверяемыеТипыОшибокДинЗаказовНаПроизводство);
	
	РезЗапроса = Запрос.Выполнить();
	Если РезЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			КодыОшибок.Получить(Выборка.КодОшибки),
			Выборка.ЗаказНаПроизводство,
			Выборка.ОбъектОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ПлановаяКалькуляцияСсылка, , , Отказ);
	КонецЦикла;

	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ИнициализироватьДокумент()
	
	Организация 	= ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	Автор			= Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ЗаписатьОчередьФоновогоПроведения(ЭтоПроведение)
	
	Очередь = Новый ТаблицаЗначений;
	Очередь.Колонки.Добавить("Калькуляция", Новый ОписаниеТипов("ДокументСсылка.ПлановаяКалькуляция2_2"));
	Очередь.Колонки.Добавить("ОтменаПроведения", Новый ОписаниеТипов("Булево"));
	
	Если ЭтоПроведение Тогда
		РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.ОчиститьОчередь(Ссылка);
	КонецЕсли;
	СтрокаОчередь = Очередь.Добавить();
	СтрокаОчередь.Калькуляция = Ссылка;
	СтрокаОчередь.ОтменаПроведения = Не ЭтоПроведение;
	
	РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.ЗаписатьОчередь(Очередь, Ссылка);
	НовоеСостояние = ?(ЭтоПроведение,
			Перечисления.СостоянияРасчетаПлановойКалькуляции.Рассчитывается,
			Перечисления.СостоянияРасчетаПлановойКалькуляции.НеРассчитана);
	РегистрыСведений.СостоянияПлановыхКалькуляций.УстановитьСостояние(Ссылка, НовоеСостояние);
	
КонецПроцедуры

Процедура ЗапуститьФоновуюОбработкуДвижений(Калькуляция, ЭтоПроведение)
	
	Документы.ПлановаяКалькуляция2_2.ЗапускВыполненияФоновогоЗадания(Калькуляция, ЭтоПроведение);
	
КонецПроцедуры

Процедура ЗаполнитьПоСпецификациям(МассивОбъектов)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Изделия.Номенклатура КАК Номенклатура,
		|	Изделия.Характеристика КАК Характеристика,
		|	СУММА(ВЫРАЗИТЬ(Изделия.КоличествоУпаковок * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК ЧИСЛО(15,3))) КАК Количество,
		|	СУММА(Изделия.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	Изделия.Упаковка КАК Упаковка,
		|	Изделия.Ссылка КАК Объект
		|ИЗ
		|	Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК Изделия
		|ГДЕ
		|	Изделия.Ссылка В(&МассивОбъектов)
		|
		|СГРУППИРОВАТЬ ПО
		|	Изделия.Номенклатура,
		|	Изделия.Упаковка,
		|	Изделия.Характеристика,
		|	Изделия.Ссылка";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"Изделия.Упаковка",
			"Изделия.Номенклатура"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	ОбъектыКалькуляции.Загрузить(Запрос.Выполнить().Выгрузить());
	Основание = МассивОбъектов[0];
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказу(ДанныеЗаполнения, ЗаполнениеПоЭтапу)
	
	ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.ЗаказНаПроизводство;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказНаПроизводство2_2Продукция.Номенклатура КАК Номенклатура,
		|	ЗаказНаПроизводство2_2Продукция.Характеристика КАК Характеристика,
		|	ЗаказНаПроизводство2_2Продукция.Упаковка КАК Упаковка,
		|	СУММА(ЗаказНаПроизводство2_2Продукция.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(ЗаказНаПроизводство2_2Продукция.Количество) КАК Количество,
		|	ЗаказНаПроизводство2_2.Ссылка КАК Объект,
		|	ЗаказНаПроизводство2_2.Подразделение КАК ПодразделениеДиспетчер,
		|	ЗаказНаПроизводство2_2Продукция.Назначение КАК Назначение,
		|	ЗаказНаПроизводство2_2.Организация КАК Организация
		|ИЗ
		|	Документ.ЗаказНаПроизводство2_2.Продукция КАК ЗаказНаПроизводство2_2Продукция
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство2_2 КАК ЗаказНаПроизводство2_2
		|		ПО ЗаказНаПроизводство2_2Продукция.Ссылка = ЗаказНаПроизводство2_2.Ссылка
		|			И (НЕ ЗаказНаПроизводство2_2Продукция.Отменено)
		|ГДЕ
		|	ЗаказНаПроизводство2_2.Ссылка В(&МассивОбъектов)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказНаПроизводство2_2Продукция.Характеристика,
		|	ЗаказНаПроизводство2_2Продукция.Упаковка,
		|	ЗаказНаПроизводство2_2.Ссылка,
		|	ЗаказНаПроизводство2_2Продукция.Номенклатура,
		|	ЗаказНаПроизводство2_2Продукция.Назначение,
		|	ЗаказНаПроизводство2_2.Подразделение,
		|	ЗаказНаПроизводство2_2.Организация";
	
	Запрос.УстановитьПараметр("МассивОбъектов", ДанныеЗаполнения);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ПодразделениеДиспетчер = Выборка.ПодразделениеДиспетчер;
		Организация = Выборка.Организация;
		НовыйОбъект = ОбъектыКалькуляции.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйОбъект, Выборка);
		
	КонецЦикла;
	
	Если Не ЗаполнениеПоЭтапу Тогда
		Основание = ДанныеЗаполнения[0];
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоЭтапу(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭтапПроизводства2_2.Распоряжение
		|ИЗ
		|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
		|ГДЕ
		|	ЭтапПроизводства2_2.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", ДанныеЗаполнения);
	
	ЗаполнитьПоЗаказу(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Распоряжение"), Истина);
	Основание = ДанныеЗаполнения[0];
	
КонецПроцедуры

Процедура ЗаполнитьПоПлануПроизводства(ДанныеЗаполнения)
	
	ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.РесурснаяСпецификация;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДПП.Номенклатура КАК Номенклатура,
	|	ДПП.Характеристика КАК Характеристика,
	|	ДПП.Упаковка КАК Упаковка,
	|	СУММА(ДПП.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(ДПП.Количество) КАК Количество,
	|	ДД.Подразделение КАК ПодразделениеДиспетчер,
	|	ДД.Назначение КАК Назначение,
	|	ДПП.Спецификация КАК Объект
	|ИЗ
	|	Документ.ПланПроизводства КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПланПроизводства.Продукция КАК ДПП
	|		ПО ДД.Ссылка В (&МассивОбъектов)
	|			И ДД.Ссылка = ДПП.Ссылка
	|			И НЕ ДПП.Полуфабрикат
	|			И НЕ ДПП.Отменено
	|
	|СГРУППИРОВАТЬ ПО
	|	ДПП.Номенклатура,
	|	ДПП.Характеристика,
	|	ДПП.Упаковка,
	|	ДД.Подразделение,
	|	ДД.Назначение,
	|	ДПП.Спецификация";
	
	Запрос.УстановитьПараметр("МассивОбъектов", ДанныеЗаполнения);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ПодразделениеДиспетчер = Выборка.ПодразделениеДиспетчер;
		НовыйОбъект = ОбъектыКалькуляции.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйОбъект, Выборка);
		
	КонецЦикла;

	ШаблонСообщения = НСтр("ru = 'Из документа %1, перенесена только продукция, количество и стоимость полуфабрикатов, будет рассчитано на основании ресурсных спецификаций продукции.';
							|en = 'Only manufactured products are transferred from the ""%1"" document. The quantity and cost of semi-finished products will be calculated based on the product BOMs.'");
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонСообщения,
		ДанныеЗаполнения[0]);
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка);
	Основание = ДанныеЗаполнения[0];
	
КонецПроцедуры

Процедура ПроверитьВозможностьВводаНаОсновании(Основания)
	
	Если ТипЗнч(Основания[0]) = Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
		ТекстЗапроса = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Реквизиты.Ссылка КАК Ссылка,
			|	ВЫБОР
			|		КОГДА Реквизиты.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Разборка)
			|			ТОГДА 0
			|		ИНАЧЕ
			|			1
			|	КОНЕЦ КАК НомерОшибки
			|ИЗ
			|	Справочник.РесурсныеСпецификации КАК Реквизиты
			|ГДЕ
			|	(Реквизиты.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Разборка)
			|		ИЛИ Реквизиты.ВариантНазначения = ЗНАЧЕНИЕ(Перечисление.ВариантыНазначенияСпецификации.ВидНоменклатуры))
			|	И Реквизиты.Ссылка В(&Основания)";
	ИначеЕсли ТипЗнч(Основания[0]) = Тип("ДокументСсылка.ЗаказНаПроизводство2_2") Тогда
		ТекстЗапроса = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ЗаказНаПроизводство2_2.Ссылка КАК Ссылка,
			|	0 КАК НомерОшибки
			|ИЗ
			|	Документ.ЗаказНаПроизводство2_2 КАК ЗаказНаПроизводство2_2
			|ГДЕ
			|	ЗаказНаПроизводство2_2.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Разборка)
			|	И ЗаказНаПроизводство2_2.Ссылка В(&Основания)";
	Иначе
		ТекстЗапроса = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ЗаказНаПроизводство2_2.Ссылка КАК Ссылка,
			|	0 КАК НомерОшибки
			|ИЗ
			|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство2_2 КАК ЗаказНаПроизводство2_2
			|		ПО ЭтапПроизводства2_2.Распоряжение = ЗаказНаПроизводство2_2.Ссылка
			|ГДЕ
			|	ЗаказНаПроизводство2_2.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Разборка)
			|	И ЭтапПроизводства2_2.Ссылка В(&Основания)";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Основания", Основания);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Если Выборка.НомерОшибки = 0 Тогда
		ТекстИсключения = НСтр("ru = 'Калькуляция разборки, утилизации не поддерживается.';
								|en = 'Reverse kitting and disposal costing is not supported.'");
	Иначе
		ТекстИсключения = НСтр("ru = 'На основании спецификации для вида номенклатуры ввод калькуляции недоступен.';
								|en = 'Costing entry based on bill of materials is not available for the item kind.'");
	КонецЕсли;
	
	ВызватьИсключение ТекстИсключения;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
