#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("ИнтеграцияБЗК");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов - таблиц значений - данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.НачислениеЕНП") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос        = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаПрочиеАктивыПассивы(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРасчетыНалоговыхАгентовСБюджетомПоНДФЛ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРасчетыСФондамиПоСтраховымВзносам(Запрос, ТекстыЗапроса, Регистры);
		//++ НЕ УТКА
		ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры);
		//-- НЕ УТКА
		ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРасчетыПоНалогамНаЕдиномНалоговомСчете(ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

#Область ПроводкиРеглУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	Проводки = Новый Массив;
	
	#Область НачислениеСовокупнойНалоговойОбязанности // (Дт <68.Х, 69.Х> :: Кт <68.90>)
	Проводки.Добавить(
	// Начисление совокупной налоговой обязанности по уплате налогов (Дт <68.Х, 69.Х> :: Кт <68.90>)
	"ВЫБРАТЬ
	|
	|	Налоги.Регистратор КАК Ссылка,
	|	Налоги.Период КАК Период,
	|	Налоги.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Налоги.СуммаРегл КАК Сумма,
	|	Налоги.СуммаУпр КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	&ВалютаРеглУчета КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиДт,
	|
	|	Строки.СчетУчета КАК СчетДт,
	|	Строки.Субконто1 КАК СубконтоДт1,
	|	Строки.Субконто2 КАК СубконтоДт2,
	|	Строки.Субконто3 КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|
	|	""Начисление совокупной налоговой обязанности по уплате налогов"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДвиженияПоПрочимАктивамПассивам КАК Налоги
	|	ПО ДокументыКОтражению.Ссылка = Налоги.Регистратор
	|		И Налоги.Активность И НЕ Налоги.Сторно
	|		И Налоги.ДебетКредит = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет)
	|		И Налоги.НастройкаХозяйственнойОперации =
	|			ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеСовокупнойНалоговойОбязанности)
	|		И Налоги.ИдентификаторФинЗаписи <> """"
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.НачислениеЕНП.РасшифровкаНалогов КАК Строки
	|	ПО ДокументыКОтражению.Ссылка = Строки.Ссылка
	|		И Налоги.ИдентификаторФинЗаписи = Строки.ИдентификаторСтроки");
	#КонецОбласти
	
	#Область ПереносСовокупнойНалоговойОбязанностиИзФилиала // (Дт <68.90> :: Кт <79.02> / Дт <79.02> :: Кт <68.90>)
	Проводки.Добавить(
	// Перенос совокупной налоговой обязанности на головную организацию (Дт <68.90> :: Кт <79.02> / Дт <79.02> :: Кт <68.90>)
	"ВЫБРАТЬ
	|
	|	ДокументыКОтражению.Ссылка КАК Ссылка,
	|	РасчетыСФилиалами.Период КАК Период,
	|	РасчетыСФилиалами.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	РасчетыСФилиалами.СуммаРегл КАК Сумма,
	|	РасчетыСФилиалами.СуммаУпр КАК СуммаУУ,
	|
	|	ВЫБОР КОГДА Налоги.ДебетКредит = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	&ВалютаРеглУчета КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиДт,
	|
	|	ВЫБОР КОГДА РасчетыСФилиалами.ДебетКредит = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет)
	|		ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоТекущимОперациям)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет)
	|	КОНЕЦ КАК СчетДт,
	|	ВЫБОР КОГДА РасчетыСФилиалами.ДебетКредит = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет)
	|		ТОГДА РасчетыСФилиалами.Аналитика
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ВЫБОР КОГДА Налоги.ДебетКредит = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиКт,
	|
	|	ВЫБОР КОГДА РасчетыСФилиалами.ДебетКредит = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит)
	|		ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоТекущимОперациям)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет)
	|	КОНЕЦ КАК СчетКт,
	|	ВЫБОР КОГДА РасчетыСФилиалами.ДебетКредит = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит)
	|		ТОГДА РасчетыСФилиалами.Аналитика
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|
	|	""Перенос совокупной налоговой обязанности"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДвиженияПоПрочимАктивамПассивам КАК РасчетыСФилиалами
	|	ПО ДокументыКОтражению.Ссылка = РасчетыСФилиалами.Регистратор
	|		И РасчетыСФилиалами.Активность И НЕ РасчетыСФилиалами.Сторно
	|		И РасчетыСФилиалами.НастройкаХозяйственнойОперации =
	|			ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПереносСовокупнойНалоговойОбязанностиИзФилиала)
	|		И РасчетыСФилиалами.Статья = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыСФилиалами)
	|		И РасчетыСФилиалами.ИдентификаторФинЗаписи <> """"
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДвиженияПоПрочимАктивамПассивам КАК Налоги
	|	ПО ДокументыКОтражению.Ссылка = Налоги.Регистратор
	|		И Налоги.Активность И НЕ Налоги.Сторно
	|		И Налоги.НастройкаХозяйственнойОперации =
	|			ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПереносСовокупнойНалоговойОбязанностиИзФилиала)
	|		И Налоги.Статья = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги)
	|		И Налоги.ИдентификаторФинЗаписи = РасчетыСФилиалами.ИдентификаторФинЗаписи
	|		И Налоги.ДебетКредит <> РасчетыСФилиалами.ДебетКредит
	|		И Налоги.Организация = РасчетыСФилиалами.Организация");
	#КонецОбласти
	
	Возврат СтрСоединить(Проводки, ОбщегоНазначения.ТекстОбъединитьВсе());
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - текст запроса создания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат "";
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Добавляет команду создания документа "Начисление ЕНП".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
// 	Неопределено - Описание
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.НачислениеЕНП) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.НачислениеЕНП.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.НачислениеЕНП);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область Заполнение

Функция ПолучитьНалогиПоДаннымЗарплаты(ПараметрыОтбора) Экспорт
	
	ТаблицаНалогов = ПустаяСсылка().РасшифровкаНалогов.ВыгрузитьКолонки();
	
	Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтбора, "Организация");
	ДатаДокумента = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтбора, "ДатаДокумента", '00010101');
	
	Если НЕ (ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(ДатаДокумента)) Тогда
		Возврат ТаблицаНалогов;
	КонецЕсли;
	
	ПараметрыЗаполнения = ПараметрыЗаполненияНДФЛНалоговогоАгента(ДатаДокумента);
	УдержаноНДФЛЗаПериод = УчетНДФЛ.УдержаноНалогаЗаПериод(
		Организация, ПараметрыЗаполнения.ДатаНачала, ПараметрыЗаполнения.ДатаОкончания);
	
	ВидыНалоговПоКБК = ВидыНалоговПоКБК(ДатаДокумента);
	
	Для Каждого СтрокаТаблицы Из УдержаноНДФЛЗаПериод Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Сумма) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаНалогов.Добавить();
		
		НоваяСтрока.ТипНалога = ВидыНалоговПоКБК.Получить(СтрокаТаблицы.КБК);
		Если Не ЗначениеЗаполнено(НоваяСтрока.ТипНалога) Тогда
			НоваяСтрока.ТипНалога = Справочники.ВидыНалоговВзносов.НДФЛ;
		КонецЕсли;
		
		НоваяСтрока.КодБК = СтрокаТаблицы.КБК;
		НоваяСтрока.Сумма = СтрокаТаблицы.Сумма;
		
		Если ДатаДокумента >= ЕдиныйНалоговыйСчет.ДатаВступленияВСилу539ФЗ() Тогда
			
			Если СтрокаТаблицы.ДатаУдержания < ПараметрыЗаполнения.ГраницаПериодов Тогда
				НоваяСтрока.НачалоПериода = НачалоМесяца(ПараметрыЗаполнения.ОтчетныйПериод);
				НоваяСтрока.КонецПериода = ПараметрыЗаполнения.ГраницаПериодов - 1;
			Иначе
				НоваяСтрока.НачалоПериода = ПараметрыЗаполнения.ГраницаПериодов;
				НоваяСтрока.КонецПериода = КонецМесяца(ПараметрыЗаполнения.ОтчетныйПериод);
			КонецЕсли;
			
		ИначеЕсли ДатаДокумента >= ЕдиныйНалоговыйСчет.ДатаНачалаПрименения() Тогда
			
			Если ПараметрыЗаполнения.ОтчетныйПериод < КонецГода(ДатаДокумента) Тогда
				НоваяСтрока.НачалоПериода = НачалоМесяца(ПараметрыЗаполнения.ОтчетныйПериод);
			Иначе
				НоваяСтрока.НачалоПериода = ПараметрыЗаполнения.ГраницаПериодов;
			КонецЕсли;
			
			НоваяСтрока.КонецПериода = КонецМесяца(ПараметрыЗаполнения.ОтчетныйПериод);
			
		Иначе
			
			НоваяСтрока.НачалоПериода = НачалоМесяца(ПараметрыЗаполнения.ОтчетныйПериод);
			НоваяСтрока.КонецПериода = КонецМесяца(ПараметрыЗаполнения.ОтчетныйПериод);
			
		КонецЕсли;
		
		НоваяСтрока.СрокУплаты = СтрокаТаблицы.СрокПеречисленияНалога;
		
		НоваяСтрока.СчетУчета = РеглУчетСервер.СчетУчетаПоТипуНалога(НоваяСтрока.ТипНалога);
		НоваяСтрока.Субконто1 = Перечисления.ВидыПлатежейВГосБюджет.Налог;
		НоваяСтрока.Субконто2 = СтрокаТаблицы.РегистрацияВНалоговомОргане;
		
	КонецЦикла;
	
	ВычестьУжеЗарегистрированныеДанныеПоНДФЛ(ТаблицаНалогов, ПараметрыОтбора);
	
	ОтражениеЗарплатыВУчете.СвернутьТаблицу(ТаблицаНалогов);
	
	Возврат ТаблицаНалогов;
	
КонецФункции

Функция ПолучитьНалогиПоДаннымДекларацийИРасчетов(ПараметрыОтбора) Экспорт
	
	ТаблицаНалогов = ПустаяСсылка().РасшифровкаНалогов.ВыгрузитьКолонки();
	
	Организация = Неопределено;
	ДатаДокумента = '00010101';
	
	Если НЕ (ПараметрыОтбора.Свойство("Организация", Организация) И ЗначениеЗаполнено(Организация)
		И ПараметрыОтбора.Свойство("ДатаДокумента", ДатаДокумента) И ЗначениеЗаполнено(ДатаДокумента)) Тогда
		Возврат ТаблицаНалогов;
	КонецЕсли;
	
	Если ПараметрыОтбора.Свойство("ТипыНалогов") Тогда
		ОтборПоТипамНалогов = ПараметрыОтбора.ТипыНалогов;
	Иначе
		ОтборПоТипамНалогов = Неопределено;
	КонецЕсли;
	
	МесяцНачисления = Месяц(ДатаДокумента);
	ДействующиеРегистрацииВНалоговомОргане = ПолучитьДействующиеРегистрацииВНалоговомОргане(Организация);
	
	Если ОтборПоТипамНалогов = Неопределено
		Или ОтборПоТипамНалогов.Получить(Справочники.ВидыНалоговВзносов.НДС) <> Неопределено Тогда
		
		ДанныеДеклараций = ДанныеДляУплатыНалогаНДС(Организация, ДействующиеРегистрацииВНалоговомОргане, ДатаДокумента);
		ДобавитьВТаблицуНалогов(ТаблицаНалогов, ДанныеДеклараций, ПараметрыОтбора);
		
	КонецЕсли;
	
	Если ОтборПоТипамНалогов = Неопределено
		Или ОтборПоТипамНалогов.Получить(Справочники.ВидыНалоговВзносов.НалогНаПрибыль_РегиональныйБюджет) <> Неопределено
		Или ОтборПоТипамНалогов.Получить(Справочники.ВидыНалоговВзносов.НалогНаПрибыль_ФедеральныйБюджет) <> Неопределено Тогда
		
		ДанныеДеклараций = ДанныеДляУплатыНалогаНаПрибыль(Организация, ДействующиеРегистрацииВНалоговомОргане, ДатаДокумента);
		ДобавитьВТаблицуНалогов(ТаблицаНалогов, ДанныеДеклараций, ПараметрыОтбора);
		
	КонецЕсли;
	
	Если (ОтборПоТипамНалогов = Неопределено
		Или ОтборПоТипамНалогов.Получить(Справочники.ВидыНалоговВзносов.Акцизы) <> Неопределено)
		И (Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "КрупнейшийНалогоплательщик")
			Или ЗначениеЗаполнено(Организация.ГоловнаяОрганизация)
			И Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация.ГоловнаяОрганизация, "КрупнейшийНалогоплательщик")) Тогда
		
		ДанныеДеклараций = ДанныеДляУплатыКосвенныхНалогов(Организация, ДействующиеРегистрацииВНалоговомОргане, ДатаДокумента);
		ДобавитьВТаблицуНалогов(ТаблицаНалогов, ДанныеДеклараций, ПараметрыОтбора);
		
	КонецЕсли;
	
	Если ОтборПоТипамНалогов = Неопределено
		Или ОтборПоТипамНалогов.Получить(Справочники.ВидыНалоговВзносов.ЗемельныйНалог) <> Неопределено Тогда
		
		Если МесяцНачисления % 3 = 1 Тогда
			
			ПериодНачисления = КонецКвартала(ДобавитьМесяц(ДатаДокумента,-3));
			ДанныеРасчета = ДанныеДляУплатыЗемельногоНалога(Организация, ПериодНачисления);
			ДобавитьВТаблицуНалогов(ТаблицаНалогов, ДанныеРасчета, ПараметрыОтбора);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОтборПоТипамНалогов = Неопределено
		Или ОтборПоТипамНалогов.Получить(Справочники.ВидыНалоговВзносов.ТранспортныйНалог) <> Неопределено Тогда
		
		Если МесяцНачисления % 3 = 1 Тогда
			
			ПериодНачисления = КонецКвартала(ДобавитьМесяц(ДатаДокумента,-3));
			ДанныеРасчета = ДанныеДляУплатыТранспортногоНалога(Организация, ПериодНачисления);
			ДобавитьВТаблицуНалогов(ТаблицаНалогов, ДанныеРасчета, ПараметрыОтбора);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОтборПоТипамНалогов = Неопределено
		Или ОтборПоТипамНалогов.Получить(Справочники.ВидыНалоговВзносов.НалогНаИмущество) <> Неопределено Тогда
		
		Если МесяцНачисления % 3 = 1 Тогда
			
			ПериодНачисления = КонецКвартала(ДобавитьМесяц(ДатаДокумента,-3));
			ДанныеРасчета = ДанныеДляУплатыНалогаНаИмущество(Организация, ПериодНачисления);
			ДобавитьВТаблицуНалогов(ТаблицаНалогов, ДанныеРасчета, ПараметрыОтбора);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОтборПоТипамНалогов = Неопределено
		Или ОтборПоТипамНалогов.Получить(Справочники.ВидыНалоговВзносов.УСН_Доходы) <> Неопределено
		Или ОтборПоТипамНалогов.Получить(Справочники.ВидыНалоговВзносов.УСН_ДоходыМинусРасходы) <> Неопределено
		Или ОтборПоТипамНалогов.Получить(Справочники.ВидыНалоговВзносов.УСН_МинимальныйНалог) <> Неопределено Тогда
		
		Если МесяцНачисления % 3 = 1 Тогда
			
			ПериодНачисления = КонецКвартала(ДобавитьМесяц(ДатаДокумента,-3));
			ДанныеРасчета = ДанныеДляУплатыНалогаУСН(Организация, ПериодНачисления);
			ДобавитьВТаблицуНалогов(ТаблицаНалогов, ДанныеРасчета, ПараметрыОтбора);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОтборПоТипамНалогов = Неопределено
		Или ОтборПоТипамНалогов.Получить(Справочники.ВидыНалоговВзносов.ТорговыйСбор) <> Неопределено Тогда
		
		ПериодНачисления = КонецКвартала(ДобавитьМесяц(ДатаДокумента,-3));
		
		Если МесяцНачисления % 3 = 1
			И ТорговыйСбор.УплачиваетсяТорговыйСбор(Организация, ПериодНачисления) Тогда
			
			ДанныеРасчета = ДанныеДляУплатыТорговогоСбора(Организация, ПериодНачисления);
			ДобавитьВТаблицуНалогов(ТаблицаНалогов, ДанныеРасчета, ПараметрыОтбора);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОтборПоТипамНалогов <> Неопределено
		И ОтборПоТипамНалогов.Получить(Справочники.ВидыНалоговВзносов.НДС_НалоговыйАгент) <> Неопределено Тогда
		
		ДополнитьТаблицуНалоговДаннымиНА(Организация, ДатаДокумента, ТаблицаНалогов);
		
	КонецЕсли;
	
	Если ОтборПоТипамНалогов <> Неопределено Тогда
		ДополнитьТаблицуНалоговПрочимиНалогами(Организация, ДатаДокумента, ОтборПоТипамНалогов, ТаблицаНалогов);
	КонецЕсли;
	
	Возврат ТаблицаНалогов;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка          КАК Ссылка,
	|	ДанныеДокумента.Дата            КАК Период,
	|	ДанныеДокумента.Номер           КАК Номер,
	|	ДанныеДокумента.Организация     КАК Организация,
	|	ДанныеДокумента.СуммаДокумента  КАК СуммаДокумента,
	|	ДанныеДокумента.Проведен        КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Ответственный   КАК Ответственный,
	|	ДанныеДокумента.Комментарий     КАК Комментарий
	|ИЗ
	|	Документ.НачислениеЕНП КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Реквизиты.Владелец().Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Для Каждого Параметр Из ПараметрыПроведения(Реквизиты) Цикл
		Запрос.УстановитьПараметр(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	
КонецПроцедуры

Функция ПараметрыПроведения(Реквизиты)
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ИдентификаторДокумента", Реквизиты.Ссылка.УникальныйИдентификатор());
	
	ВалютаРеглУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Реквизиты.Организация);
	КоэффициентыПересчета = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентыПересчетаВалюты(
		ВалютаРеглУчета, ВалютаРеглУчета, Реквизиты.Период, Реквизиты.Организация);
	Параметры.Вставить("КоэффициентПересчетаВВалютуУпр", КоэффициентыПересчета.КоэффициентПересчетаВВалютуУпр);
	
	Параметры.Вставить("ИспользоватьНачислениеЗарплаты",            ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты"));
	Параметры.Вставить("ИспользоватьУчетПрочихАктивовПассивов",     ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихАктивовПассивов"));
	Параметры.Вставить("ИспользоватьРаспределениеОплатИНачислений", Реквизиты.Период >= РеглУчетСервер.ДатаНачалаОбязательногоПримененияЕНП());
	
	РегистрацияВНалоговомОргане = Справочники.РегистрацииВНалоговомОргане.РегистрацияВНалоговомОргане(Реквизиты.Организация, Реквизиты.Период);
	Параметры.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	ГоловнаяОрганизация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Реквизиты.Организация, "ГоловнаяОрганизация");
	Параметры.Вставить("ГоловнаяОрганизация", ?(ЗначениеЗаполнено(ГоловнаяОрганизация), ГоловнаяОрганизация, Реквизиты.Организация));
	
	Параметры.Вставить("НДФЛНалоговогоАгента", ВидыНалоговВзносовЛокализация.НДФЛНалоговогоАгента());
	
	Возврат Параметры;
	
КонецФункции

#Область ТекстыЗапросаВременныхТаблиц

Процедура ДобавитьТекстЗапросаВТРасшифровкаНалогов(ТекстыЗапроса)
	
	ИмяТаблицы = "ВТРасшифровкаНалогов";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Строки.ТипНалога КАК ВидНалогаВзносов,
	|	ВЫБОР
	|		КОГДА Строки.Субконто1 ССЫЛКА Справочник.РегистрацииВНалоговомОргане
	|				И Строки.Субконто1 <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|			ТОГДА Строки.Субконто1
	|		КОГДА Строки.Субконто2 ССЫЛКА Справочник.РегистрацииВНалоговомОргане
	|				И Строки.Субконто2 <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|			ТОГДА Строки.Субконто2
	|		КОГДА Строки.Субконто3 ССЫЛКА Справочник.РегистрацииВНалоговомОргане
	|				И Строки.Субконто3 <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|			ТОГДА Строки.Субконто3
	|		ИНАЧЕ &РегистрацияВНалоговомОргане
	|	КОНЕЦ КАК РегистрацияВНалоговомОргане,
	|	Строки.СрокУплаты КАК СрокУплаты,
	|	Строки.СчетУчета КАК СчетУчета,
	|	Строки.КодБК КАК КодБК,
	|	Строки.Сумма КАК Сумма,
	|	ВЫРАЗИТЬ(Строки.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК СуммаУУ,
	|	Строки.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТРасшифровкаНалогов
	|ИЗ
	|	Документ.НачислениеЕНП.РасшифровкаНалогов КАК Строки
	|
	|ГДЕ
	|	Строки.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаВТВидыСтраховыхВзносов(ТекстыЗапроса)
	
	ИмяТаблицы = "ВТВидыСтраховыхВзносов";
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = ИнтеграцияБЗК.ТекстЗапросаВидыСтраховыхВзносов(ИмяТаблицы);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

#КонецОбласти

Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеАктивыПассивы";
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТРасшифровкаНалогов(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                             КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)              КАК ВидДвижения,
	|	&Организация                                        КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                        КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК Статья,
	|	Налоги.ВидНалогаВзносов                             КАК Аналитика,
	|	СУММА(Налоги.СуммаУУ)                               КАК Сумма
	|ИЗ
	|	ВТРасшифровкаНалогов КАК Налоги
	|
	|СГРУППИРОВАТЬ ПО
	|	Налоги.ВидНалогаВзносов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                             КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)              КАК ВидДвижения,
	|	&Организация                                        КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                        КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК Статья,
	|	ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.ЕдиныйНалоговыйПлатеж) КАК Аналитика,
	|	СУММА(Налоги.СуммаУУ)                               КАК Сумма
	|ИЗ
	|	ВТРасшифровкаНалогов КАК Налоги
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                             КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)              КАК ВидДвижения,
	|	&Организация                                        КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                        КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК Статья,
	|	ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.ЕдиныйНалоговыйПлатеж) КАК Аналитика,
	|	СУММА(Налоги.СуммаУУ)                               КАК Сумма
	|ИЗ
	|	ВТРасшифровкаНалогов КАК Налоги
	|
	|ГДЕ
	|	&Организация <> &ГоловнаяОрганизация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                             КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)              КАК ВидДвижения,
	|	&Организация                                        КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                        КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыСФилиалами) КАК Статья,
	|	&ГоловнаяОрганизация                                КАК Аналитика,
	|	СУММА(Налоги.СуммаУУ)                               КАК Сумма
	|ИЗ
	|	ВТРасшифровкаНалогов КАК Налоги
	|
	|ГДЕ
	|	&Организация <> &ГоловнаяОрганизация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                             КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)              КАК ВидДвижения,
	|	&ГоловнаяОрганизация                                КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                        КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыСФилиалами) КАК Статья,
	|	&Организация                                        КАК Аналитика,
	|	СУММА(Налоги.СуммаУУ)                               КАК Сумма
	|ИЗ
	|	ВТРасшифровкаНалогов КАК Налоги
	|
	|ГДЕ
	|	&Организация <> &ГоловнаяОрганизация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                             КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)              КАК ВидДвижения,
	|	&ГоловнаяОрганизация                                КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                        КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК Статья,
	|	ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.ЕдиныйНалоговыйПлатеж) КАК Аналитика,
	|	СУММА(Налоги.СуммаУУ)                               КАК Сумма
	|ИЗ
	|	ВТРасшифровкаНалогов КАК Налоги
	|
	|ГДЕ
	|	&Организация <> &ГоловнаяОрганизация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТРасшифровкаНалогов(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫБОР КОГДА Налоги.СрокУплаты >= &Период
	|		ТОГДА Налоги.СрокУплаты
	|		ИНАЧЕ &Период
	|	КОНЕЦ                                               КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|	&Организация                                        КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                        КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК Статья,
	|	Налоги.ВидНалогаВзносов                             КАК Аналитика,
	|	Налоги.Сумма                                        КАК СуммаРегл,
	|	Налоги.СуммаУУ                                      КАК СуммаБезНДС,
	|	Налоги.СуммаУУ                                      КАК СуммаСНДС,
	|	Налоги.СуммаУУ                                      КАК СуммаУпр,
	|	Налоги.ИдентификаторСтроки                          КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеСовокупнойНалоговойОбязанности) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВТРасшифровкаНалогов КАК Налоги
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА Налоги.СрокУплаты >= &Период
	|		ТОГДА Налоги.СрокУплаты
	|		ИНАЧЕ &Период
	|	КОНЕЦ                                               КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|	&Организация                                        КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                        КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК Статья,
	|	ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.ЕдиныйНалоговыйПлатеж) КАК Аналитика,
	|	Налоги.Сумма                                        КАК СуммаРегл,
	|	Налоги.СуммаУУ                                      КАК СуммаБезНДС,
	|	Налоги.СуммаУУ                                      КАК СуммаСНДС,
	|	Налоги.СуммаУУ                                      КАК СуммаУпр,
	|	Налоги.ИдентификаторСтроки                          КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеСовокупнойНалоговойОбязанности) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВТРасшифровкаНалогов КАК Налоги
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА Налоги.СрокУплаты >= &Период
	|		ТОГДА Налоги.СрокУплаты
	|		ИНАЧЕ &Период
	|	КОНЕЦ                                               КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|	&Организация                                        КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                        КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК Статья,
	|	ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.ЕдиныйНалоговыйПлатеж) КАК Аналитика,
	|	СУММА(Налоги.Сумма)                                 КАК СуммаРегл,
	|	СУММА(Налоги.СуммаУУ)                               КАК СуммаБезНДС,
	|	СУММА(Налоги.СуммаУУ)                               КАК СуммаСНДС,
	|	СУММА(Налоги.СуммаУУ)                               КАК СуммаУпр,
	|	&ИдентификаторДокумента                             КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПереносСовокупнойНалоговойОбязанностиИзФилиала) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВТРасшифровкаНалогов КАК Налоги
	|
	|ГДЕ
	|	&Организация <> &ГоловнаяОрганизация
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР КОГДА Налоги.СрокУплаты >= &Период
	|		ТОГДА Налоги.СрокУплаты
	|		ИНАЧЕ &Период
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА Налоги.СрокУплаты >= &Период
	|		ТОГДА Налоги.СрокУплаты
	|		ИНАЧЕ &Период
	|	КОНЕЦ                                               КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|	&Организация                                        КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                        КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыСФилиалами) КАК Статья,
	|	&ГоловнаяОрганизация                                КАК Аналитика,
	|	СУММА(Налоги.Сумма)                                 КАК СуммаРегл,
	|	СУММА(Налоги.СуммаУУ)                               КАК СуммаБезНДС,
	|	СУММА(Налоги.СуммаУУ)                               КАК СуммаСНДС,
	|	СУММА(Налоги.СуммаУУ)                               КАК СуммаУпр,
	|	&ИдентификаторДокумента                             КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПереносСовокупнойНалоговойОбязанностиИзФилиала) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВТРасшифровкаНалогов КАК Налоги
	|
	|ГДЕ
	|	&Организация <> &ГоловнаяОрганизация
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР КОГДА Налоги.СрокУплаты >= &Период
	|		ТОГДА Налоги.СрокУплаты
	|		ИНАЧЕ &Период
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА Налоги.СрокУплаты >= &Период
	|		ТОГДА Налоги.СрокУплаты
	|		ИНАЧЕ &Период
	|	КОНЕЦ                                               КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|	&ГоловнаяОрганизация                                КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                        КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыСФилиалами) КАК Статья,
	|	&Организация                                        КАК Аналитика,
	|	СУММА(Налоги.Сумма)                                 КАК СуммаРегл,
	|	СУММА(Налоги.СуммаУУ)                               КАК СуммаБезНДС,
	|	СУММА(Налоги.СуммаУУ)                               КАК СуммаСНДС,
	|	СУММА(Налоги.СуммаУУ)                               КАК СуммаУпр,
	|	&ИдентификаторДокумента                             КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПереносСовокупнойНалоговойОбязанностиИзФилиала) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВТРасшифровкаНалогов КАК Налоги
	|
	|ГДЕ
	|	&Организация <> &ГоловнаяОрганизация
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР КОГДА Налоги.СрокУплаты >= &Период
	|		ТОГДА Налоги.СрокУплаты
	|		ИНАЧЕ &Период
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА Налоги.СрокУплаты >= &Период
	|		ТОГДА Налоги.СрокУплаты
	|		ИНАЧЕ &Период
	|	КОНЕЦ                                               КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|	&ГоловнаяОрганизация                                КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                        КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) КАК Статья,
	|	ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.ЕдиныйНалоговыйПлатеж) КАК Аналитика,
	|	СУММА(Налоги.Сумма)                                 КАК СуммаРегл,
	|	СУММА(Налоги.СуммаУУ)                               КАК СуммаБезНДС,
	|	СУММА(Налоги.СуммаУУ)                               КАК СуммаСНДС,
	|	СУММА(Налоги.СуммаУУ)                               КАК СуммаУпр,
	|	&ИдентификаторДокумента                             КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПереносСовокупнойНалоговойОбязанностиИзФилиала) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВТРасшифровкаНалогов КАК Налоги
	|
	|ГДЕ
	|	&Организация <> &ГоловнаяОрганизация
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР КОГДА Налоги.СрокУплаты >= &Период
	|		ТОГДА Налоги.СрокУплаты
	|		ИНАЧЕ &Период
	|	КОНЕЦ";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыНалоговыхАгентовСБюджетомПоНДФЛ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыНалоговыхАгентовСБюджетомПоНДФЛ";
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТРасшифровкаНалогов(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Ссылка,
	|	&Организация КАК Организация,
	|	Налоги.ВидНалогаВзносов КАК ТипНалога,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК МесяцНалоговогоПериода,
	|	"""" КАК ПлатежноеПоручениеНомер,
	|	Налоги.СрокУплаты КАК ПлатежноеПоручениеДата,
	|	Налоги.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	""0"" КАК ПоказательОснования,
	|	""0"" КАК ПоказательПериода,
	|	Налоги.Сумма КАК Сумма
	|ИЗ
	|	ВТРасшифровкаНалогов КАК Налоги
	|
	|ГДЕ
	|	&ИспользоватьНачислениеЗарплаты
	|	И НЕ &ИспользоватьРаспределениеОплатИНачислений
	|	И Налоги.ВидНалогаВзносов В (&НДФЛНалоговогоАгента)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСФондамиПоСтраховымВзносам(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыСФондамиПоСтраховымВзносам";
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТРасшифровкаНалогов(ТекстыЗапроса);
	ДобавитьТекстЗапросаВТВидыСтраховыхВзносов(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	СтраховыеВзносы.ВидСтраховыхВзносов КАК ВидОбязательногоСтрахованияСотрудников,
	|	Налоги.СрокУплаты КАК ПлатежноеПоручениеДата,
	|	"""" КАК ПлатежноеПоручениеНомер,
	|	ИСТИНА КАК ЭтоСтраховыеВзносы,
	|	0 КАК ГодЗадолженностиПоЕСН,
	|	Налоги.Сумма КАК Сумма
	|ИЗ
	|	ВТРасшифровкаНалогов КАК Налоги
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВидыСтраховыхВзносов КАК СтраховыеВзносы
	|		ПО Налоги.ВидНалогаВзносов = СтраховыеВзносы.ВидНалога
	|
	|ГДЕ
	|	&ИспользоватьНачислениеЗарплаты
	|	И НЕ &ИспользоватьРаспределениеОплатИНачислений";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыПоНалогамНаЕдиномНалоговомСчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыПоНалогамНаЕдиномНалоговомСчете";
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТРасшифровкаНалогов(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	&Организация КАК Организация,
	|	Налоги.СчетУчета КАК СчетУчета,
	|	Налоги.ВидНалогаВзносов КАК Налог,
	|	Налоги.КодБК КАК КодБК,
	|	Налоги.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЕСТЬNULL(Регистрации.КодПоОКТМО, """") КАК КодПоОКТМО,
	|	НЕОПРЕДЕЛЕНО КАК ПлатежныйДокумент,
	|	Налоги.СрокУплаты КАК СрокУплаты,
	|	Налоги.Сумма КАК Сумма
	|ИЗ
	|	ВТРасшифровкаНалогов КАК Налоги
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РегистрацииВНалоговомОргане КАК Регистрации
	|		ПО Налоги.РегистрацияВНалоговомОргане = Регистрации.Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

//++ НЕ УТКА
Функция ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВМеждународномУчете";
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТРасшифровкаНалогов(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	Организации.Ссылка КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка В (&ГоловнаяОрганизация, &Организация)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период,
	|	Организации.Ссылка,
	|	НАЧАЛОПЕРИОДА(Налоги.СрокУплаты, ДЕНЬ)
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасшифровкаНалогов КАК Налоги
	|		ПО Организации.Ссылка В (&ГоловнаяОрганизация, &Организация)
	|			И Налоги.СрокУплаты > &Период";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции
//-- НЕ УТКА

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДобавитьТекстЗапросаВТРасшифровкаНалогов(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	Организации.Ссылка КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка В (&Организация, &ГоловнаяОрганизация)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период,
	|	Организации.Ссылка,
	|	НАЧАЛОПЕРИОДА(Налоги.СрокУплаты, ДЕНЬ)
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасшифровкаНалогов КАК Налоги
	|		ПО Организации.Ссылка В (&Организация, &ГоловнаяОрганизация)
	|			И Налоги.СрокУплаты > &Период";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Заполнение

Функция ПараметрыЗаполненияНДФЛНалоговогоАгента(Знач ДатаДокумента)
	
	ГраницаОтбора = КонецДня(ДатаДокумента);
	ОтчетныйПериод = НачалоМесяца(ДатаДокумента);
	
	Если ДатаДокумента >= ЕдиныйНалоговыйСчет.ДатаВступленияВСилу539ФЗ() Тогда
		
		// С 1 января 2024 года налоговые агенты перечисляют удержанный НДФЛ дважды в месяц
		// За период с 1-го по 22-е число текущего месяца - 28-е число текущего месяца
		// С 23-го числа по последнее число текущего месяца - 5-е число следующего месяца
		
		НачалоСведений = НачалоМесяца(ГраницаОтбора);
		ГраницаПериодов = НачалоСведений + 22 * 86400; // 23-е число текущего месяца
		
	ИначеЕсли ДатаДокумента >= ЕдиныйНалоговыйСчет.ДатаНачалаПрименения() Тогда
		
		// С 1 января 2023 года, при подаче уведомлений об исчисленных суммах налогов, данные по НДФЛ удержанному
		// в отчетном периоде предоставляются с 23 числа предыдущего месяца по 22 число текущего
		
		Если День(ДатаДокумента) < 23 Тогда
			НачалоСведений = ДобавитьМесяц(ОтчетныйПериод, -1) + 22 * 86400; // 23-е число предыдущего месяца
		ИначеЕсли Месяц(ДатаДокумента) = 12 Тогда
			НачалоСведений = ОтчетныйПериод + 22 * 86400; // 23-е декабря
			ОтчетныйПериод = КонецГода(ДатаДокумента);
		Иначе
			НачалоСведений = НачалоМесяца(ДатаДокумента) + 22 * 86400; // 23-е число текущего месяца
			ОтчетныйПериод = ДобавитьМесяц(ОтчетныйПериод, 1);
		КонецЕсли;
		
		Если ДатаДокумента >= УчетНДФЛ.НачалоПередачиЧастичныхУведомлений() Тогда
			ГраницаПериодов = НачалоМесяца(ОтчетныйПериод) + 8 * 86400; // 9-е число отчетного месяца
		Иначе
			ГраницаПериодов = НачалоСведений;
		КонецЕсли;
		
	Иначе
		
		НачалоСведений = НачалоМесяца(ГраницаОтбора);
		ГраницаПериодов = НачалоСведений;
		
	КонецЕсли;
	
	НачалоСведений = Макс(НачалоСведений, НачалоГода(ДатаДокумента));
	ГраницаПериодов = Макс(ГраницаПериодов, НачалоСведений);
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ДатаНачала", НачалоСведений);
	ПараметрыОтбора.Вставить("ДатаОкончания", ГраницаОтбора);
	ПараметрыОтбора.Вставить("ГраницаПериодов", ГраницаПериодов);
	ПараметрыОтбора.Вставить("ОтчетныйПериод", ОтчетныйПериод);
	
	Возврат Новый ФиксированнаяСтруктура(ПараметрыОтбора);
	
КонецФункции

Процедура ВычестьУжеЗарегистрированныеДанныеПоНДФЛ(ТаблицаНалогов, ПараметрыЗаполнения)
	
	Организация         = ПараметрыЗаполнения.Организация;
	ДатаДокумента       = ПараметрыЗаполнения.ДатаДокумента;
	ИсключаемыйДокумент = ПараметрыЗаполнения.Ссылка;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(ДатаДокумента));
	Запрос.УстановитьПараметр("ИсключаемыйДокумент", ИсключаемыйДокумент);
	Запрос.УстановитьПараметр("ТаблицаНДФЛ", ТаблицаНалогов);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДФЛ.КодБК КАК КодБК,
	|	НДФЛ.ТипНалога КАК ТипНалога,
	|	НДФЛ.НачалоПериода КАК НачалоПериода,
	|	НДФЛ.КонецПериода КАК КонецПериода,
	|	НДФЛ.СрокУплаты КАК СрокУплаты,
	|	НДФЛ.СчетУчета КАК СчетУчета,
	|	НДФЛ.Субконто1 КАК Субконто1,
	|	НДФЛ.Субконто2 КАК Субконто2
	|ПОМЕСТИТЬ ТаблицаНДФЛ
	|ИЗ
	|	&ТаблицаНДФЛ КАК НДФЛ
	|;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Налоги.КодБК КАК КодБК,
	|	Налоги.ТипНалога КАК ТипНалога,
	|	Налоги.НачалоПериода КАК НачалоПериода,
	|	Налоги.КонецПериода КАК КонецПериода,
	|	Налоги.СрокУплаты КАК СрокУплаты,
	|	Налоги.СчетУчета КАК СчетУчета,
	|	Налоги.Субконто1 КАК Субконто1,
	|	Налоги.Субконто2 КАК Субконто2,
	|	-Налоги.Сумма КАК Сумма
	|ИЗ
	|	Документ.НачислениеЕНП.РасшифровкаНалогов КАК Налоги
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНДФЛ КАК НДФЛ
	|		ПО Налоги.ТипНалога = НДФЛ.ТипНалога
	|			И Налоги.НачалоПериода = НДФЛ.НачалоПериода
	|			И Налоги.КонецПериода = НДФЛ.КонецПериода
	|			И Налоги.СрокУплаты = НДФЛ.СрокУплаты
	|			И Налоги.Субконто1 = НДФЛ.Субконто1
	|			И Налоги.Субконто2 = НДФЛ.Субконто2
	|			И Налоги.КодБК = НДФЛ.КодБК
	|
	|ГДЕ
	|	Налоги.Ссылка.Проведен
	|	И Налоги.Ссылка <> &ИсключаемыйДокумент
	|	И Налоги.Ссылка.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(Налоги.Ссылка.Дата, МЕСЯЦ) = &ПериодРегистрации
	|	И Налоги.ТипНалога В (
	|		ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.НДФЛ),
	|		ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.НДФЛСПревышения),
	|		ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.НДФЛДивиденды),
	|		ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.НДФЛДивидендыСПревышения))";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаНалогов.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

Функция НовыйТаблицаПлатежиПоДаннымДекларации()
	
	Платежи = ИнтерфейсыВзаимодействияБРО.НовыйТаблицаДанныхРеглОтчета();
	Платежи.Колонки.Добавить("РегистрацияВНалоговомОргане",
		Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	Платежи.Колонки.Добавить("ТипНалога", Новый ОписаниеТипов("СправочникСсылка.ВидыНалоговВзносов"));
	Платежи.Колонки.Добавить("СчетУчета", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	Платежи.Колонки.Добавить("НачалоПериода", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	Платежи.Колонки.Добавить("КонецПериода", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	Платежи.Колонки.Добавить("СрокУплаты", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	
	Возврат Платежи;
	
КонецФункции

Функция ДанныеДляУплатыНалогаНДС(Организация, РегистрацииВНалоговыхОрганах, ДатаДокумента)
	
	Платежи = НовыйТаблицаПлатежиПоДаннымДекларации();
	
	ПериодДекларации = КонецКвартала(ДобавитьМесяц(ДатаДокумента,-3));
	ПериодыПлатежей  = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КонецМесяца(ДатаДокумента));
	
	Для Каждого РегистрацияВНалоговомОргане Из РегистрацииВНалоговыхОрганах Цикл
		
		ДанныеРеглОтчета = ИнтерфейсыВзаимодействияБРО.ДанныеРеглОтчетаДляУплатыНалога(
			"НДС", Организация, РегистрацияВНалоговомОргане.Ссылка, ПериодДекларации);
		
		Если ДанныеРеглОтчета <> Неопределено
			И ЗначениеЗаполнено(ДанныеРеглОтчета.Ссылка) Тогда
			ДанныеРеглОтчета.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане.Ссылка);
			
			Для Каждого СтрокаТаблицы Из ДобавитьПлатежиПоДаннымДекларации(Платежи, ДанныеРеглОтчета, ПериодыПлатежей) Цикл
				СтрокаТаблицы.СрокУплаты = СрокУплатыНалогаВзносов(СтрокаТаблицы.ТипНалога, СтрокаТаблицы.Период);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Платежи.Сортировать("ТипНалога Возр, Период Возр");
	
	Возврат Платежи;
	
КонецФункции

Функция ДанныеДляУплатыНалогаНаПрибыль(Организация, РегистрацииВНалоговыхОрганах, ДатаДокумента)
	
	Платежи = НовыйТаблицаПлатежиПоДаннымДекларации();
	
	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату(
		"НастройкиУчетаНалогаНаПрибыль", Организация, ДатаДокумента, Ложь);
	
	Если ПараметрыУчетнойПолитики = Неопределено Тогда
		Возврат Платежи;
	КонецЕсли;
	
	МесяцНачисления = Месяц(ДатаДокумента);
	ПериодыДекларацийИПлатежей = Новый Соответствие;
	
	Если Перечисления.ПорядокУплатыАвансовПоНалогуНаПрибыль.ПоФактическойПрибыли
		= ПараметрыУчетнойПолитики.ПорядокУплатыАвансовПоНалогуНаПрибыль Тогда
		
		// данные для заполнения берем:
		// * в январе - не заполняем,
		// * в остальные месяцы - из декларации за предыдущий месяц,
		// * в марте - дополнительно из декларации за прошлый год.
		
		Если МесяцНачисления >= 2 Тогда
			ПериодДекларации = КонецМесяца(ДобавитьМесяц(ДатаДокумента, -1));
			ПериодыДекларацийИПлатежей.Вставить(ПериодДекларации);
		КонецЕсли;
		
		Если МесяцНачисления = 3 Тогда
			ПериодДекларации = КонецГода(ДобавитьМесяц(ДатаДокумента, -3));
			ПериодыДекларацийИПлатежей.Вставить(ПериодДекларации);
		КонецЕсли;
		
	КонецЕсли;
	Если Перечисления.ПорядокУплатыАвансовПоНалогуНаПрибыль.Ежеквартально
		= ПараметрыУчетнойПолитики.ПорядокУплатыАвансовПоНалогуНаПрибыль Тогда
	
		// данные для заполнения берем:
		// * в марте - из годовой декларации предыдущего года,
		// * в апреле, июле, октябре - из декларации предыдущего квартала.
		
		Если МесяцНачисления = 3
			Или (МесяцНачисления > 1 И МесяцНачисления % 3 = 1) Тогда
			ПериодДекларации = КонецКвартала(ДобавитьМесяц(ДатаДокумента, -3));
			ПериодыДекларацийИПлатежей.Вставить(ПериодДекларации);
		КонецЕсли;
		
	КонецЕсли;
	Если Перечисления.ПорядокУплатыАвансовПоНалогуНаПрибыль.Ежемесячно
		= ПараметрыУчетнойПолитики.ПорядокУплатыАвансовПоНалогуНаПрибыль Тогда
		
		// данные для заполнения берем:
		// * в январе, феврале и марте - из декларации за 9 месяцев предыдущего года (с отбором по месяцу начисления),
		// * остальные месяцы - из декларации предыдущего квартала (с отбором по месяцу начисления),
		// * в марте - дополнительно все платежи из декларации за прошлый год.
		
		Если МесяцНачисления <= 3 Тогда
			ПериодДекларации = ДобавитьМесяц(КонецГода(ДобавитьМесяц(ДатаДокумента, -3)), -3);
			ПериодыПлатежей = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КонецМесяца(ДатаДокумента));
			ПериодыДекларацийИПлатежей.Вставить(ПериодДекларации, ПериодыПлатежей);
		Иначе
			ПериодДекларации = КонецКвартала(ДобавитьМесяц(ДатаДокумента, -3));
			ПериодыПлатежей = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КонецМесяца(ДатаДокумента));
			Если МесяцНачисления % 3 = 1 Тогда
				ПериодыПлатежей.Добавить(КонецМесяца(ПериодДекларации));
			КонецЕсли;
			ПериодыДекларацийИПлатежей.Вставить(ПериодДекларации, ПериодыПлатежей);
		КонецЕсли;
		
		Если МесяцНачисления = 3 Тогда
			ПериодДекларации = КонецГода(ДобавитьМесяц(ДатаДокумента, -3));
			ПериодыДекларацийИПлатежей.Вставить(ПериодДекларации);
		КонецЕсли;
		
	КонецЕсли;
	
	ОсновнаяРегистрация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "РегистрацияВНалоговомОргане");
	
	Для Каждого КлючИЗначение Из ПериодыДекларацийИПлатежей Цикл
		ПериодДекларации = КлючИЗначение.Ключ;
		ПериодыПлатежей  = КлючИЗначение.Значение;
		Для Каждого РегистрацияВНалоговомОргане Из РегистрацииВНалоговыхОрганах Цикл
			
			ДанныеРеглОтчета = ИнтерфейсыВзаимодействияБРО.ДанныеРеглОтчетаДляУплатыНалога(
				"НалогНаПрибыль", Организация, РегистрацияВНалоговомОргане.Ссылка, ПериодДекларации);
			
			Если ДанныеРеглОтчета <> Неопределено
				И ЗначениеЗаполнено(ДанныеРеглОтчета.Ссылка) Тогда
				ДанныеРеглОтчета.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане.Ссылка);
				
				Если РегистрацияВНалоговомОргане.КрупнейшийНалогоплательщик = Истина Тогда
					ДанныеРеглОтчета.РегистрацияВНалоговомОргане = ОсновнаяРегистрация;
				КонецЕсли;
				
				ДобавитьПлатежиПоДаннымДекларации(Платежи, ДанныеРеглОтчета, ПериодыПлатежей);
				
			КонецЕсли;
			
			ПоОбособленномуПодразделению = Новый Структура("ОбособленноеПодразделение", Истина);
			ДанныеРеглОтчета = ИнтерфейсыВзаимодействияБРО.ДанныеРеглОтчетаДляУплатыНалога(
				"НалогНаПрибыль", Организация, РегистрацияВНалоговомОргане.Ссылка, ПериодДекларации, ПоОбособленномуПодразделению);
			
			Если ДанныеРеглОтчета <> Неопределено
				И ЗначениеЗаполнено(ДанныеРеглОтчета.Ссылка) Тогда
				ДанныеРеглОтчета.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане.Ссылка);
				
				ДобавитьПлатежиПоДаннымДекларации(Платежи, ДанныеРеглОтчета, ПериодыПлатежей);
				
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	Платежи.Сортировать("ТипНалога Возр, Период Возр");
	
	Возврат Платежи;
	
КонецФункции

Функция ДанныеДляУплатыКосвенныхНалогов(Организация, РегистрацииВНалоговыхОрганах, ДатаДокумента)
	
	Платежи = НовыйТаблицаПлатежиПоДаннымДекларации();
	
	ПериодДекларации = КонецМесяца(ДобавитьМесяц(ДатаДокумента, -1));
	ПериодыПлатежей  = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПериодДекларации);
	
	ОсновнаяРегистрация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "РегистрацияВНалоговомОргане");
	
	Для Каждого РегистрацияВНалоговомОргане Из РегистрацииВНалоговыхОрганах Цикл
		
		// Акцизы при импорте товаров из ЕАЭС
		
		ДанныеРеглОтчета = ИнтерфейсыВзаимодействияБРО.ДанныеРеглОтчетаДляУплатыНалога(
			"КосвенныеНалогиТамСоюз.2010_Декларация", Организация, РегистрацияВНалоговомОргане.Ссылка, ПериодДекларации);
		
		Если ДанныеРеглОтчета <> Неопределено
			И ЗначениеЗаполнено(ДанныеРеглОтчета.Ссылка) Тогда
			ДанныеРеглОтчета.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане.Ссылка);
			ДанныеРеглОтчета.Вставить("ВидНалоговВзносов", Справочники.ВидыНалоговВзносов.Акцизы);
			
			Если РегистрацияВНалоговомОргане.КрупнейшийНалогоплательщик = Истина Тогда
				ДанныеРеглОтчета.РегистрацияВНалоговомОргане = ОсновнаяРегистрация;
			КонецЕсли;
			
			Отбор = Новый Структура("ВидНалога", Перечисления.ВидыНалогов.НДС_ВвозимыеТовары);
			Для Каждого СтрокаТаблицы Из ДанныеРеглОтчета.Платежи.НайтиСтроки(Отбор) Цикл
				ДанныеРеглОтчета.Платежи.Удалить(СтрокаТаблицы);
			КонецЦикла;
			
			СрокУплаты = СрокУплатыНалогаВзносов(ДанныеРеглОтчета.ВидНалоговВзносов, ДатаДокумента, 20);
			Для Каждого СтрокаТаблицы Из ДобавитьПлатежиПоДаннымДекларации(Платежи, ДанныеРеглОтчета, ПериодыПлатежей) Цикл
				СтрокаТаблицы.СрокУплаты = СрокУплаты;
			КонецЦикла;
			
		КонецЕсли;
		
		// Акцизы на алкогольную продукцию
		
		ДанныеРеглОтчета = ИнтерфейсыВзаимодействияБРО.ДанныеРеглОтчетаДляУплатыНалога(
			"Акцизы.2016_ДекларацияАлкоголь", Организация, РегистрацияВНалоговомОргане.Ссылка, ПериодДекларации);
		
		Если ДанныеРеглОтчета <> Неопределено
			И ЗначениеЗаполнено(ДанныеРеглОтчета.Ссылка) Тогда
			ДанныеРеглОтчета.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане.Ссылка);
			ДанныеРеглОтчета.Вставить("ВидНалоговВзносов", Справочники.ВидыНалоговВзносов.Акцизы);
			
			Если РегистрацияВНалоговомОргане.КрупнейшийНалогоплательщик = Истина Тогда
				ДанныеРеглОтчета.РегистрацияВНалоговомОргане = ОсновнаяРегистрация;
			КонецЕсли;
			
			СрокУплаты = СрокУплатыНалогаВзносов(ДанныеРеглОтчета.ВидНалоговВзносов, ДатаДокумента);
			Для Каждого СтрокаТаблицы Из ДобавитьПлатежиПоДаннымДекларации(Платежи, ДанныеРеглОтчета, ПериодыПлатежей) Цикл
				СтрокаТаблицы.СрокУплаты = СрокУплаты;
			КонецЦикла;
			
		КонецЕсли;
		
		// Акцизы на ГСМ, природный газ, автомобили и мотоциклы
		
		ДанныеРеглОтчета = ИнтерфейсыВзаимодействияБРО.ДанныеРеглОтчетаДляУплатыНалога(
			"Акцизы.2016_ДекларацияИныеТовары", Организация, РегистрацияВНалоговомОргане.Ссылка, ПериодДекларации);
		
		Если ДанныеРеглОтчета <> Неопределено
			И ЗначениеЗаполнено(ДанныеРеглОтчета.Ссылка) Тогда
			ДанныеРеглОтчета.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане.Ссылка);
			ДанныеРеглОтчета.Вставить("ВидНалоговВзносов", Справочники.ВидыНалоговВзносов.Акцизы);
			
			Если РегистрацияВНалоговомОргане.КрупнейшийНалогоплательщик = Истина Тогда
				ДанныеРеглОтчета.Удалить("РегистрацияВНалоговомОргане");
				ДанныеРеглОтчета.Платежи.Колонки.Добавить(
					"РегистрацияВНалоговомОргане", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
				Отбор = Новый Структура("КрупнейшийНалогоплательщик, КодПоОКТМО", Ложь, "");
				Для Каждого СтрокаТаблицы Из ДанныеРеглОтчета.Платежи Цикл
					Если ПустаяСтрока(СтрокаТаблицы.ОКАТО) Тогда
						Продолжить;
					КонецЕсли;
					Отбор.КодПоОКТМО = СтрокаТаблицы.ОКАТО;
					НайденныеСтроки = РегистрацииВНалоговыхОрганах.НайтиСтроки(Отбор);
					Если НайденныеСтроки.ВГраница() = 0 Тогда
						СтрокаТаблицы.РегистрацияВНалоговомОргане = НайденныеСтроки[0].Ссылка;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			СрокУплаты = СрокУплатыНалогаВзносов(ДанныеРеглОтчета.ВидНалоговВзносов, ДатаДокумента);
			Для Каждого СтрокаТаблицы Из ДобавитьПлатежиПоДаннымДекларации(Платежи, ДанныеРеглОтчета, ПериодыПлатежей) Цикл
				СтрокаТаблицы.СрокУплаты = СрокУплаты;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Платежи.Сортировать("ТипНалога Возр, Период Возр");
	
	Возврат Платежи;
	
КонецФункции

Функция ДобавитьПлатежиПоДаннымДекларации(Платежи, ДанныеРеглОтчета, Периоды)
	
	НовыеСтроки = Новый Массив;
	
	ДанныеДекларации = Новый Структура;
	Если ДанныеРеглОтчета.Свойство("РегистрацияВНалоговомОргане") Тогда
		ДанныеДекларации.Вставить("РегистрацияВНалоговомОргане", ДанныеРеглОтчета.РегистрацияВНалоговомОргане);
	КонецЕсли;
	
	ПериодРеглОтчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеРеглОтчета.Ссылка, "ДатаНачала, ДатаОкончания");
	ДанныеДекларации.Вставить("НачалоПериода", ПериодРеглОтчета.ДатаНачала);
	ДанныеДекларации.Вставить("КонецПериода", КонецДня(ПериодРеглОтчета.ДатаОкончания));
	
	ВидыНалоговПоКБК = ВидыНалоговПоКБК(ПериодРеглОтчета.ДатаОкончания);
	ВидНалогаДекларации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеРеглОтчета, "ВидНалоговВзносов");
	
	Для Каждого Платеж Из ДанныеРеглОтчета.Платежи Цикл
		
		Если Платеж.Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если Периоды <> Неопределено
			И Периоды.Найти(КонецМесяца(Платеж.Период)) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Платежи.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Платеж);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДекларации);
		
		ВидНалогаВзносов = ?(ЗначениеЗаполнено(ВидНалогаДекларации),
			ВидНалогаДекларации, ВидыНалоговПоКБК.Получить(Платеж.КБК));
		Если Не ЗначениеЗаполнено(ВидНалогаВзносов) Тогда
			ВидНалогаВзносов = Справочники.ВидыНалоговВзносов.ПрочиеНалогиИСборы;
		ИначеЕсли ВидНалогаВзносов = Справочники.ВидыНалоговВзносов.НДС И Платеж.Агент Тогда
			ВидНалогаВзносов = Справочники.ВидыНалоговВзносов.НДС_НалоговыйАгент;
		КонецЕсли;
		
		НоваяСтрока.СчетУчета =
			РеглУчетСервер.СчетУчетаПоТипуНалога(ВидНалогаВзносов);
		НоваяСтрока.ТипНалога = ВидНалогаВзносов;
		
		НовыеСтроки.Добавить(НоваяСтрока);
		
	КонецЦикла;
	
	Возврат НовыеСтроки;
	
КонецФункции

Функция ДанныеДляУплатыЗемельногоНалога(Организация, ОкончаниеОтчетногоПериодаДляРасчета)
	
	ВидНалогаВзносов = Справочники.ВидыНалоговВзносов.ЗемельныйНалог;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ВидНалогаВзносов", ВидНалогаВзносов);
	Запрос.УстановитьПараметр("ПериодРасчета", НачалоДня(ОкончаниеОтчетногоПериодаДляРасчета));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(ОкончаниеОтчетногоПериодаДляРасчета));
	
	Запрос.УстановитьПараметр("НачалоПримененияНовогоРасчета", 
								РасчетИмущественныхНалоговКлиентСервер.НачалоПримененияНовогоРасчетаЗемельногоНалога());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&ВидНалогаВзносов КАК ТипНалога,
	|	Расчет.НачалоПериода КАК НачалоПериода,
	|	Расчет.КонецПериода КАК КонецПериода,
	|	Расчет.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СУММА(Расчет.Сумма) КАК Сумма,
	|	Расчет.КБК КАК КБК
	|	
	|ИЗ (
	|	ВЫБРАТЬ
	|		&НачалоПериода КАК НачалоПериода,
	|		&ПериодРасчета КАК КонецПериода,
	|		РасчетЗемельногоНалога.ИФНС КАК РегистрацияВНалоговомОргане,
	|		СУММА(РасчетЗемельногоНалога.СуммаНалогаКУплате) КАК Сумма,
	|		РасчетЗемельногоНалога.КБК КАК КБК
	|	ИЗ
	|		РегистрСведений.РасчетЗемельногоНалога КАК РасчетЗемельногоНалога
	|	ГДЕ
	|		РасчетЗемельногоНалога.ПериодРасчета = &ПериодРасчета
	|		И РасчетЗемельногоНалога.Организация = &Организация
	|		И РасчетЗемельногоНалога.ПериодРасчета >= &НачалоПримененияНовогоРасчета
	|		И РасчетЗемельногоНалога.Активность
	|	СГРУППИРОВАТЬ ПО
	|		РасчетЗемельногоНалога.КБК,
	|		РасчетЗемельногоНалога.ИФНС
	|	ИМЕЮЩИЕ
	|		СУММА(РасчетЗемельногоНалога.СуммаНалогаКУплате) > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&НачалоПериода КАК НачалоПериода,
	|		&ПериодРасчета КАК КонецПериода,
	|		РасчетЗемельногоНалогаДо2020.ИФНС КАК РегистрацияВНалоговомОргане,
	|		СУММА(РасчетЗемельногоНалогаДо2020.СуммаНалогаКУплате) КАК Сумма,
	|		РасчетЗемельногоНалогаДо2020.КБК КАК КБК
	|	ИЗ
	|		РегистрСведений.РасчетЗемельногоНалогаДо2020 КАК РасчетЗемельногоНалогаДо2020
	|	ГДЕ
	|		РасчетЗемельногоНалогаДо2020.ПериодРасчета = &ПериодРасчета
	|		И РасчетЗемельногоНалогаДо2020.Организация = &Организация
	|		И РасчетЗемельногоНалогаДо2020.ПериодРасчета < &НачалоПримененияНовогоРасчета
	|		И РасчетЗемельногоНалогаДо2020.Активность
	|	СГРУППИРОВАТЬ ПО
	|		РасчетЗемельногоНалогаДо2020.КБК,
	|		РасчетЗемельногоНалогаДо2020.ИФНС
	|	ИМЕЮЩИЕ
	|		СУММА(РасчетЗемельногоНалогаДо2020.СуммаНалогаКУплате) > 0
	|	) КАК Расчет
	|	
	|СГРУППИРОВАТЬ ПО
	|	Расчет.КБК,
	|	Расчет.РегистрацияВНалоговомОргане,
	|	Расчет.НачалоПериода,
	|	Расчет.КонецПериода"; 
	
	ДанныеРасчета = Запрос.Выполнить().Выгрузить(); 
	ДанныеРасчета.Колонки.Добавить("СчетУчета", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("СчетУчета", РеглУчетСервер.СчетУчетаПоТипуНалога(ВидНалогаВзносов));
	
	Для Каждого Платеж Из ДанныеРасчета Цикл
		ЗаполнитьЗначенияСвойств(Платеж, ЗначенияЗаполнения);
	КонецЦикла; 
	
	Возврат ДанныеРасчета;
	
КонецФункции

Функция ДанныеДляУплатыТранспортногоНалога(Организация, ОкончаниеОтчетногоПериодаДляРасчета)
	
	ВидНалогаВзносов = Справочники.ВидыНалоговВзносов.ТранспортныйНалог;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ВидНалогаВзносов", ВидНалогаВзносов);
	Запрос.УстановитьПараметр("ПериодРасчета", НачалоДня(ОкончаниеОтчетногоПериодаДляРасчета));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(ОкончаниеОтчетногоПериодаДляРасчета));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&ВидНалогаВзносов КАК ТипНалога,
	|	&НачалоПериода КАК НачалоПериода,
	|	&ПериодРасчета КАК КонецПериода,
	|	РасчетТранспортногоНалога.ИФНС КАК РегистрацияВНалоговомОргане,
	|	СУММА(РасчетТранспортногоНалога.СуммаНалогаКУплате) КАК Сумма
	|ИЗ
	|	РегистрСведений.РасчетТранспортногоНалога КАК РасчетТранспортногоНалога
	|ГДЕ
	|	РасчетТранспортногоНалога.ПериодРасчета = &ПериодРасчета
	|	И РасчетТранспортногоНалога.Организация = &Организация
	|	И РасчетТранспортногоНалога.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетТранспортногоНалога.ИФНС
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасчетТранспортногоНалога.СуммаНалогаКУплате) > 0"; 
	
	ДанныеРасчета = Запрос.Выполнить().Выгрузить(); 
	ДанныеРасчета.Колонки.Добавить("КБК", ОбщегоНазначения.ОписаниеТипаСтрока(20));
	ДанныеРасчета.Колонки.Добавить("СчетУчета", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("КБК", КБКПоВидуНалогаВзносов(ВидНалогаВзносов, ОкончаниеОтчетногоПериодаДляРасчета));
	ЗначенияЗаполнения.Вставить("СчетУчета", РеглУчетСервер.СчетУчетаПоТипуНалога(ВидНалогаВзносов));
	
	Для Каждого Платеж Из ДанныеРасчета Цикл 
		ЗаполнитьЗначенияСвойств(Платеж, ЗначенияЗаполнения);
	КонецЦикла; 
	
	Возврат ДанныеРасчета;
	
КонецФункции

Функция ДанныеДляУплатыНалогаНаИмущество(Организация, ОкончаниеОтчетногоПериодаДляРасчета)
	
	ВидНалогаВзносов = Справочники.ВидыНалоговВзносов.НалогНаИмущество;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ВидНалогаВзносов", ВидНалогаВзносов);
	Запрос.УстановитьПараметр("ПериодРасчета", НачалоДня(ОкончаниеОтчетногоПериодаДляРасчета));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(ОкончаниеОтчетногоПериодаДляРасчета));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&ВидНалогаВзносов                              КАК ТипНалога,
	|	НалогНаИмущество.НачалоПериода                 КАК НачалоПериода,
	|	НалогНаИмущество.КонецПериода                  КАК КонецПериода,
	|	НалогНаИмущество.РегистрацияВНалоговомОргане   КАК РегистрацияВНалоговомОргане,
	|	НалогНаИмущество.КБК                           КАК КБК,
	|	СУММА(НалогНаИмущество.Сумма)                  КАК Сумма
	|ИЗ
	|(
	|	ВЫБРАТЬ
	|		&НачалоПериода                                    КАК НачалоПериода,
	|		&ПериодРасчета                                    КАК КонецПериода,
	|		РасчетНалогаНаИмущество.ИФНС                      КАК РегистрацияВНалоговомОргане,
	|		РасчетНалогаНаИмущество.КБК                       КАК КБК,
	|		СУММА(РасчетНалогаНаИмущество.СуммаНалогаКУплате) КАК Сумма
	|	ИЗ
	|		РегистрСведений.РасчетНалогаНаИмущество КАК РасчетНалогаНаИмущество
	|	ГДЕ
	|		РасчетНалогаНаИмущество.ПериодРасчета = &ПериодРасчета
	|		И РасчетНалогаНаИмущество.Организация = &Организация
	|		И РасчетНалогаНаИмущество.Активность
	|	СГРУППИРОВАТЬ ПО
	|		РасчетНалогаНаИмущество.КБК,
	|		РасчетНалогаНаИмущество.ИФНС
	|	ИМЕЮЩИЕ
	|		СУММА(РасчетНалогаНаИмущество.СуммаНалогаКУплате) > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&НачалоПериода                                        КАК НачалоПериода,
	|		&ПериодРасчета                                        КАК КонецПериода,
	|		РасчетНалогаНаИмуществоПоКС.ИФНС                      КАК РегистрацияВНалоговомОргане,
	|		ЕСТЬNULL(ПараметрыНачисленияННИ.КБК, """")            КАК КБК,
	|		СУММА(РасчетНалогаНаИмуществоПоКС.СуммаНалогаКУплате) КАК Сумма
	|	ИЗ
	|		РегистрСведений.РасчетНалогаНаИмуществоПоКадастровойСтоимости КАК РасчетНалогаНаИмуществоПоКС
	|			
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыНачисленияНалогаНаИмущество.СрезПоследних(&ПериодРасчета,
	|				Организация = &Организация) КАК ПараметрыНачисленияННИ
	|			ПО РасчетНалогаНаИмуществоПоКС.Организация = ПараметрыНачисленияННИ.Организация
	|				И РасчетНалогаНаИмуществоПоКС.ОсновноеСредство = ПараметрыНачисленияННИ.ОсновноеСредство
	|				И РасчетНалогаНаИмуществоПоКС.КодПоОКТМО = ПараметрыНачисленияННИ.КодПоОКТМО
	|	ГДЕ
	|		РасчетНалогаНаИмуществоПоКС.ПериодРасчета = &ПериодРасчета
	|		И РасчетНалогаНаИмуществоПоКС.Организация = &Организация
	|		И РасчетНалогаНаИмуществоПоКС.Активность
	|	СГРУППИРОВАТЬ ПО
	|		РасчетНалогаНаИмуществоПоКС.ИФНС,
	|		ЕСТЬNULL(ПараметрыНачисленияННИ.КБК, """")
	|	ИМЕЮЩИЕ
	|		СУММА(РасчетНалогаНаИмуществоПоКС.СуммаНалогаКУплате) > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&НачалоПериода                                         КАК НачалоПериода,
	|		&ПериодРасчета                                         КАК КонецПериода,
	|		РасчетНалогаНаИмуществоПоСГС.ИФНС                      КАК РегистрацияВНалоговомОргане,
	|		РасчетНалогаНаИмуществоПоСГС.КБК                       КАК КБК,
	|		СУММА(РасчетНалогаНаИмуществоПоСГС.СуммаНалогаКУплате) КАК Сумма
	|	ИЗ
	|		РегистрСведений.РасчетНалогаНаИмуществоПоСреднегодовойСтоимости КАК РасчетНалогаНаИмуществоПоСГС
	|	ГДЕ
	|		РасчетНалогаНаИмуществоПоСГС.ПериодРасчета = &ПериодРасчета
	|		И РасчетНалогаНаИмуществоПоСГС.Организация = &Организация
	|		И РасчетНалогаНаИмуществоПоСГС.Активность
	|	СГРУППИРОВАТЬ ПО
	|		РасчетНалогаНаИмуществоПоСГС.КБК,
	|		РасчетНалогаНаИмуществоПоСГС.ИФНС
	|	ИМЕЮЩИЕ
	|		СУММА(РасчетНалогаНаИмуществоПоСГС.СуммаНалогаКУплате) > 0
	|) КАК НалогНаИмущество
	|
	|СГРУППИРОВАТЬ ПО
	|	НалогНаИмущество.НачалоПериода,
	|	НалогНаИмущество.КонецПериода,
	|	НалогНаИмущество.РегистрацияВНалоговомОргане,
	|	НалогНаИмущество.КБК
	|	";
	
	ДанныеРасчета = Запрос.Выполнить().Выгрузить();
	ДанныеРасчета.Колонки.Добавить("СчетУчета", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("КБК", КБКПоВидуНалогаВзносов(ВидНалогаВзносов, ОкончаниеОтчетногоПериодаДляРасчета));
	ЗначенияЗаполнения.Вставить("СчетУчета", РеглУчетСервер.СчетУчетаПоТипуНалога(ВидНалогаВзносов));
	
	Для Каждого Платеж Из ДанныеРасчета Цикл 
		
		Если Не ЗначениеЗаполнено(Платеж.КБК) Тогда
			ЗаполнитьЗначенияСвойств(Платеж, ЗначенияЗаполнения, "КБК");
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Платеж, ЗначенияЗаполнения, "СчетУчета");
		
	КонецЦикла;
	
	Возврат ДанныеРасчета;
	
КонецФункции

Функция ДанныеДляУплатыНалогаУСН(Организация, ОкончаниеОтчетногоПериодаДляРасчета)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРасчета", НачалоДня(ОкончаниеОтчетногоПериодаДляРасчета));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоГода(ОкончаниеОтчетногоПериодаДляРасчета));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&НачалоПериода КАК НачалоПериода,
	|	&ПериодРасчета КАК КонецПериода,
	|	ВЫБОР РасчетНалогаУСН.ОбъектНалогообложения
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ОбъектыНалогообложенияПоУСН.Доходы)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.УСН_Доходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.УСН_ДоходыМинусРасходы)
	|	КОНЕЦ КАК ТипНалога,
	|	РасчетНалогаУСН.ОбъектНалогообложения КАК ОбъектНалогообложения,
	|	СУММА(РасчетНалогаУСН.НалогКУплате) КАК Сумма,
	|	СУММА(РасчетНалогаУСН.МинимальныйНалог) КАК МинимальныйНалог,
	|	СУММА(РасчетНалогаУСН.НалогИсчисленныйВсего) КАК НалогИсчисленныйВсего,
	|	СУММА(РасчетНалогаУСН.АвансовыеПлатежи) КАК АвансовыеПлатежи
	|ИЗ
	|	РегистрСведений.РасчетНалогаУСН КАК РасчетНалогаУСН
	|ГДЕ
	|	РасчетНалогаУСН.Организация = &Организация
	|	И РасчетНалогаУСН.ПериодРасчета = &ПериодРасчета
	|	И РасчетНалогаУСН.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетНалогаУСН.ОбъектНалогообложения
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасчетНалогаУСН.НалогКУплате) > 0";
	
	ДанныеРасчета = Запрос.Выполнить().Выгрузить();
	ДанныеРасчета.Колонки.Добавить("КБК", ОбщегоНазначения.ОписаниеТипаСтрока(20));
	ДанныеРасчета.Колонки.Добавить("СчетУчета", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ДанныеРасчета.Колонки.Добавить("РегистрацияВНалоговомОргане", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("РегистрацияВНалоговомОргане",
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "РегистрацияВНалоговомОргане"));
	
	Для Каждого Платеж Из ДанныеРасчета Цикл 
		
		Если Платеж.ОбъектНалогообложения = Перечисления.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы
			И КонецГода(ОкончаниеОтчетногоПериодаДляРасчета) = ОкончаниеОтчетногоПериодаДляРасчета
			И Платеж.НалогИсчисленныйВсего < Платеж.МинимальныйНалог Тогда
			Платеж.ТипНалога = Справочники.ВидыНалоговВзносов.УСН_МинимальныйНалог;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Платеж, ЗначенияЗаполнения);
		
		Платеж.КБК = КБКПоВидуНалогаВзносов(Платеж.ТипНалога, ОкончаниеОтчетногоПериодаДляРасчета);
		Платеж.СчетУчета = РеглУчетСервер.СчетУчетаПоТипуНалога(Платеж.ТипНалога);
		
	КонецЦикла;
	
	Возврат ДанныеРасчета;
	
КонецФункции

Функция ДанныеДляУплатыТорговогоСбора(Организация, ОкончаниеОтчетногоПериодаДляРасчета)
	
	ВидНалогаВзносов = Справочники.ВидыНалоговВзносов.ТорговыйСбор;
	
	РасчетПоСбору = ТорговыйСбор.СуммаТорговогоСбора(Организация, НачалоКвартала(ОкончаниеОтчетногоПериодаДляРасчета), ОкончаниеОтчетногоПериодаДляРасчета);
	
	ДанныеРасчета = Новый ТаблицаЗначений;
	ДанныеРасчета.Колонки.Добавить("ТипНалога", Новый ОписаниеТипов("СправочникСсылка.ВидыНалоговВзносов"));
	ДанныеРасчета.Колонки.Добавить("КБК", ОбщегоНазначения.ОписаниеТипаСтрока(20));
	ДанныеРасчета.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(15,2)); 
	ДанныеРасчета.Колонки.Добавить("НачалоПериода", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ДанныеРасчета.Колонки.Добавить("КонецПериода", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ДанныеРасчета.Колонки.Добавить("СчетУчета", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ДанныеРасчета.Колонки.Добавить("РегистрацияВНалоговомОргане", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	
	Если РасчетПоСбору.Количество() = 0 Тогда
		Возврат ДанныеРасчета;
	КонецЕсли;
	
	Платеж = ДанныеРасчета.Добавить();
	Платеж.ТипНалога = ВидНалогаВзносов;
	Платеж.КБК = КБКПоВидуНалогаВзносов(ВидНалогаВзносов, ОкончаниеОтчетногоПериодаДляРасчета);
	Платеж.Сумма = РасчетПоСбору.ВыгрузитьКолонку("Сумма");
	Платеж.НачалоПериода = НачалоКвартала(ОкончаниеОтчетногоПериодаДляРасчета);
	Платеж.КонецПериода = ОкончаниеОтчетногоПериодаДляРасчета;
	Платеж.СчетУчета = РеглУчетСервер.СчетУчетаПоТипуНалога(ВидНалогаВзносов); 
	Платеж.РегистрацияВНалоговомОргане = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "РегистрацияВНалоговомОргане");
	
	Возврат ДанныеРасчета;
	
КонецФункции

Функция КБКПоВидуНалогаВзносов(ВидНалогаВзносов, Знач Период = '00010101')
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	СписокКБК = ЕдиныйНалоговыйСчет.КБКПоВидамНалогов(Период).Получить(ВидНалогаВзносов);
	Если СписокКБК = Неопределено ИЛИ СписокКБК.Количество() > 1 Тогда
		Возврат "";
	КонецЕсли;
	
	КБК = СписокКБК.Получить(0).Значение;
	Для Каждого ПодвидДохода Из КБК.ПодвидыДохода Цикл
		Если ПодвидДохода.Значение.ВидПлатежа = Перечисления.ВидыПлатежейВГосБюджет.Налог Тогда
			Возврат КБК.Администратор + КБК.ВидДохода + ПодвидДохода.Ключ + КБК.КОСГУ;
		КонецЕсли;
	КонецЦикла;
	
	Возврат "";
	
КонецФункции

Функция ВидыНалоговПоКБК(Знач ОтчетныйПериод) Экспорт
	
	Если Не ЗначениеЗаполнено(ОтчетныйПериод)
		ИЛИ ТипЗнч(ОтчетныйПериод) <> Тип("Дата") Тогда
		ОтчетныйПериод = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Соответствие = Новый Соответствие;
	
	Соответствие.Вставить("18210602010021000110", Справочники.ВидыНалоговВзносов.НалогНаИмущество);
	Соответствие.Вставить("18210101012021000110", Справочники.ВидыНалоговВзносов.НалогНаПрибыль_РегиональныйБюджет);
	Соответствие.Вставить("18210101011011000110", Справочники.ВидыНалоговВзносов.НалогНаПрибыль_ФедеральныйБюджет);
	Соответствие.Вставить("18210606031031000110", Справочники.ВидыНалоговВзносов.ЗемельныйНалог);
	Соответствие.Вставить("18210604011021000110", Справочники.ВидыНалоговВзносов.ТранспортныйНалог);
	Соответствие.Вставить("18210301000011000110", Справочники.ВидыНалоговВзносов.НДС);
	
	ДатаЗакона176ФЗ = УчетНДФЛ.ДатаЗакона176ФЗ();
	Если ОтчетныйПериод < ДатаЗакона176ФЗ Тогда
		КБКпоНДФЛ = УчетНДФЛ.КБКпоНДФЛ(ОтчетныйПериод);
		Соответствие.Вставить(КБКпоНДФЛ.ПоОблигациям,         Справочники.ВидыНалоговВзносов.НДФЛ);
		Соответствие.Вставить(КБКпоНДФЛ.ДивидендыСПревышения, Справочники.ВидыНалоговВзносов.НДФЛДивидендыСПревышения);
		Соответствие.Вставить(КБКпоНДФЛ.Дивиденды,            Справочники.ВидыНалоговВзносов.НДФЛДивиденды);
		Соответствие.Вставить(КБКпоНДФЛ.СПревышения,          Справочники.ВидыНалоговВзносов.НДФЛСПревышения);
		Соответствие.Вставить(КБКпоНДФЛ.Основной,             Справочники.ВидыНалоговВзносов.НДФЛ);
	КонецЕсли;
	
	Возврат Новый ФиксированноеСоответствие(Соответствие);
	
КонецФункции

Процедура ДополнитьТаблицуНалоговДаннымиНА(Организация, ДатаОстатков, ТаблицаНалогов)
	
	ВидНалогаВзносов = Справочники.ВидыНалоговВзносов.НДС_НалоговыйАгент;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаОстатков", ДатаОстатков);
	Запрос.УстановитьПараметр("ВидНалогаВзносов", ВидНалогаВзносов);
	Запрос.УстановитьПараметр("СчетНалогов", РеглУчетСервер.СчетУчетаПоТипуНалога(ВидНалогаВзносов));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&ВидНалогаВзносов КАК ТипНалога,
	|	ХозрасчетныйОстатки.Счет КАК СчетУчета,
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
	|	ХозрасчетныйОстатки.Субконто3 КАК Субконто3,
	|	СУММА(-ХозрасчетныйОстатки.СуммаОстаток) КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаОстатков,
	|			Счет В ИЕРАРХИИ (&СчетНалогов),,
	|			Организация = &Организация
	|		) КАК ХозрасчетныйОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстатки.Счет,
	|	ХозрасчетныйОстатки.Субконто1,
	|	ХозрасчетныйОстатки.Субконто2,
	|	ХозрасчетныйОстатки.Субконто3";
	
	ТаблицаНалоговНА = Запрос.Выполнить().Выгрузить();
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("КодБК", КБКПоВидуНалогаВзносов(ВидНалогаВзносов, ДатаОстатков));
	ЗначенияЗаполнения.Вставить("НачалоПериода", НачалоКвартала(ДобавитьМесяц(ДатаОстатков,-3)));
	ЗначенияЗаполнения.Вставить("КонецПериода", КонецКвартала(ДобавитьМесяц(ДатаОстатков,-3)));
	ЗначенияЗаполнения.Вставить("СрокУплаты", СрокУплатыНалогаВзносов(ВидНалогаВзносов, ДатаОстатков));
	
	Для Каждого СтрокаНалога Из ТаблицаНалоговНА Цикл
		
		НоваяСтрока = ТаблицаНалогов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНалога);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗначенияЗаполнения);
		
	КонецЦикла;
	
	ТаблицаНалогов.Сортировать("ТипНалога Возр, СрокУплаты Возр");
	
КонецПроцедуры

Процедура ДополнитьТаблицуНалоговПрочимиНалогами(Организация, ДатаДокумента, ОтборПоТипамНалогов, ТаблицаНалогов)
	
	ДатаОстатков = ДатаДокумента;
	
	ПрочиеНалоги = Новый Массив;
	СчетаПрочихНалогов = Новый Массив;
	ВидыНалоговСОсобымЗаполнением = ВидыНалоговСОсобымЗаполнением(Организация);
	Для Каждого ЭлементСписка Из ОтборПоТипамНалогов Цикл
		Если ВидыНалоговСОсобымЗаполнением.Найти(ЭлементСписка.Ключ) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ПрочиеНалоги.Добавить(ЭлементСписка.Ключ);
		Если ЭлементСписка.Ключ = Справочники.ВидыНалоговВзносов.ПрочиеНалогиИСборы Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаПрочихНалогов, ЭлементСписка.Значение, Истина);
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(ПрочиеНалоги) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПрочиеНалоги", ПрочиеНалоги);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ТипыНалогов.Ссылка КАК ТипНалога,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета
	|ИЗ
	|	Справочник.ВидыНалоговВзносов КАК ТипыНалогов
	|ГДЕ
	|	ТипыНалогов.Ссылка В (&ПрочиеНалоги)
	|	И ТипыНалогов.Ссылка <> ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.ПрочиеНалогиИСборы)";
	
	ТипыНалогов = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТаблицы Из ТипыНалогов Цикл
		СтрокаТаблицы.СчетУчета = РеглУчетСервер.СчетУчетаПоТипуНалога(СтрокаТаблицы.ТипНалога);
	КонецЦикла;
	
	Если СчетаПрочихНалогов <> Неопределено Тогда
		
		Для Каждого СчетПрочихНалогов Из СчетаПрочихНалогов Цикл 
			НоваяСтрока = ТипыНалогов.Добавить();
			НоваяСтрока.ТипНалога = Справочники.ВидыНалоговВзносов.ПрочиеНалогиИСборы;
			НоваяСтрока.СчетУчета = СчетПрочихНалогов;
		КонецЦикла;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТипыНалогов", ТипыНалогов);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаОстатков", Новый Граница(ДатаОстатков, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("СчетаНалогов", ТипыНалогов.ВыгрузитьКолонку("СчетУчета"));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.ТипНалога,
	|	Таблица.СчетУчета
	|ПОМЕСТИТЬ ТипыНалогов
	|ИЗ &ТипыНалогов КАК Таблица
	|;
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
	|	ХозрасчетныйОстатки.Субконто3 КАК Субконто3,
	|	СУММА(-ХозрасчетныйОстатки.СуммаОстаток) КАК Сумма
	|ПОМЕСТИТЬ НалогиКУплате
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаОстатков,
	|			Счет В ИЕРАРХИИ (&СчетаНалогов),,
	|			Организация = &Организация
	|		) КАК ХозрасчетныйОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстатки.Счет,
	|	ХозрасчетныйОстатки.Субконто1,
	|	ХозрасчетныйОстатки.Субконто2,
	|	ХозрасчетныйОстатки.Субконто3
	|;
	|
	|ВЫБРАТЬ
	|	ТипыНалогов.ТипНалога КАК ТипНалога,
	|	НалогиКУплате.Счет КАК СчетУчета,
	|	НалогиКУплате.Субконто1 КАК Субконто1,
	|	НалогиКУплате.Субконто2 КАК Субконто2,
	|	НалогиКУплате.Субконто3 КАК Субконто3,
	|	НалогиКУплате.Сумма
	|ИЗ
	|	НалогиКУплате КАК НалогиКУплате
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТипыНалогов КАК ТипыНалогов
	|	ПО НалогиКУплате.Счет= ТипыНалогов.СчетУчета";
	
	ТаблицаПрочихНалогов = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаНалога Из ТаблицаПрочихНалогов Цикл
		
		НоваяСтрока = ТаблицаНалогов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНалога);
		НоваяСтрока.КодБК = КБКПоВидуНалогаВзносов(НоваяСтрока.ТипНалога, ДатаДокумента);
		НоваяСтрока.НачалоПериода = НачалоМесяца(ДатаОстатков);
		НоваяСтрока.КонецПериода = КонецМесяца(ДатаОстатков);
		НоваяСтрока.СрокУплаты = СрокУплатыНалогаВзносов(НоваяСтрока.ТипНалога, ДатаДокумента);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВидыНалоговСОсобымЗаполнением(Организация)
	
	ВидыНалоговВзносов = Новый Массив;
	ВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.НДС);
	ВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.НДС_НалоговыйАгент);
	ВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.НалогНаПрибыль_РегиональныйБюджет);
	ВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.НалогНаПрибыль_ФедеральныйБюджет);
	ВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.ЗемельныйНалог);
	ВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.ТранспортныйНалог);
	ВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.НалогНаИмущество);
	ВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.ТорговыйСбор);
	ВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.УСН_Доходы);
	ВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.УСН_ДоходыМинусРасходы);
	ВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.УСН_МинимальныйНалог);
	Если Не ЗначениеЗаполнено(Организация.ГоловнаяОрганизация)
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "КрупнейшийНалогоплательщик") = Ложь 
		Или ЗначениеЗаполнено(Организация.ГоловнаяОрганизация)
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация.ГоловнаяОрганизация, "КрупнейшийНалогоплательщик") = Ложь Тогда 
		ВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.Акцизы);
	КонецЕсли;
	
	Возврат ВидыНалоговВзносов;
	
КонецФункции

Процедура ДобавитьВТаблицуНалогов(ТаблицаНалогов, Платежи, ПараметрыОтбора)
	
	Если ПараметрыОтбора.Свойство("ТипыНалогов") Тогда
		ОтборПоТипамНалогов = ПараметрыОтбора.ТипыНалогов;
	Иначе
		ОтборПоТипамНалогов = Неопределено;
	КонецЕсли;
	
	Для Каждого СтрокаНалога Из Платежи Цикл
		Если ОтборПоТипамНалогов = Неопределено Или ОтборПоТипамНалогов.Получить(СтрокаНалога.ТипНалога) <> Неопределено Тогда
			
			НоваяСтрока = ТаблицаНалогов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНалога);
			НоваяСтрока.КодБК = СтрокаНалога.КБК;
			НоваяСтрока.Субконто1 = Перечисления.ВидыПлатежейВГосБюджет.Налог;
			Если СтрокаНалога.ТипНалога = Справочники.ВидыНалоговВзносов.НалогНаПрибыль_РегиональныйБюджет Тогда
				НоваяСтрока.Субконто2 = Перечисления.УровниБюджетов.РегиональныйБюджет;
				НоваяСтрока.Субконто3 = СтрокаНалога.РегистрацияВНалоговомОргане;
				НоваяСтрока.НачалоПериода = НачалоКвартала(НоваяСтрока.КонецПериода);
			ИначеЕсли СтрокаНалога.ТипНалога = Справочники.ВидыНалоговВзносов.НалогНаПрибыль_ФедеральныйБюджет Тогда
				НоваяСтрока.Субконто2 = Перечисления.УровниБюджетов.ФедеральныйБюджет;
				НоваяСтрока.Субконто3 = СтрокаНалога.РегистрацияВНалоговомОргане;
				НоваяСтрока.НачалоПериода = НачалоКвартала(НоваяСтрока.КонецПериода);
			Иначе
				ДанныеСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаНалога.СчетУчета);
				Если ДанныеСчета.ВидСубконто2 = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РегистрацияВНалоговомОргане Тогда
					НоваяСтрока.Субконто2 = СтрокаНалога.РегистрацияВНалоговомОргане;
				ИначеЕсли ДанныеСчета.ВидСубконто3 = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РегистрацияВНалоговомОргане Тогда
					НоваяСтрока.Субконто3 = СтрокаНалога.РегистрацияВНалоговомОргане;
				КонецЕсли;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(НоваяСтрока.СрокУплаты) Тогда
				НоваяСтрока.СрокУплаты = СрокУплатыНалогаВзносов(СтрокаНалога.ТипНалога, ПараметрыОтбора.ДатаДокумента);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьДействующиеРегистрацииВНалоговомОргане(Организация)
	
	МассивОрганизаций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	Если ЗначениеЗаполнено(Организация.ГоловнаяОрганизация) Тогда
		МассивОрганизаций.Добавить(Организация.ГоловнаяОрганизация);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", МассивОрганизаций);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрацииВНалоговомОргане.Ссылка КАК Ссылка,
	|	РегистрацииВНалоговомОргане.КодПоОКТМО КАК КодПоОКТМО,
	|	РегистрацииВНалоговомОргане.КрупнейшийНалогоплательщик
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|ГДЕ
	|	РегистрацииВНалоговомОргане.Владелец В (&Организация)
	|	И НЕ РегистрацииВНалоговомОргане.ПометкаУдаления";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция СрокУплатыНалогаВзносов(ВидНалогаВзносов, Период, ДеньПлатежа = 28)
	
	Попытка
		СрокУплаты = Дата(Год(Период), Месяц(Период), ДеньПлатежа);
	Исключение
		СрокУплаты = Дата(Год(Период), Месяц(Период), 28);
	КонецПопытки;
	
	Возврат СрокУплаты;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
