#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Организация = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ТекущаяОрганизация", "");
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОперацияМеждународный")
	 ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("Основание")
		И ДанныеЗаполнения.Свойство("ПланСчетов") Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			ДокументОснование = ДанныеЗаполнения.Основание;
			МетаданныеОснования = Метаданные.НайтиПоТипу(ТипЗнч(ДокументОснование));
			ПланСчетовОснования = ДанныеЗаполнения.ПланСчетов;
		Иначе
			ДокументОснование = ДанныеЗаполнения;
			МетаданныеОснования = ДокументОснование.Метаданные();
			ПланСчетовОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ПланСчетов");
		КонецЕсли;
		
		Если Метаданные.Документы.Найти(МетаданныеОснования.Имя) <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДокументОснование,,"Дата,Номер");
			ПланСчетов = ПланСчетовОснования;
			Комментарий = "";
			
			НоваяСтрока = ЭтотОбъект.ЗаполнениеДвижений.Добавить();
			НоваяСтрока.Документ = ДокументОснование;
			
			НастройкаФормированияПроводок = МеждународныйУчетСерверПовтИсп.ИспользуемаяНастройкаФормированияПроводок(
				НачалоМесяца(ТекущаяДатаСеанса()), ПланСчетов, Организация);
			
			СпособАннулированияПроводок = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаФормированияПроводок, "СпособАннулированияПроводок");
			ВариантФормированияПроводок = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПланСчетовОснования, "ВариантФормированияПроводок");
			
			Если СпособАннулированияПроводок = Перечисления.СпособыАннулированияПроводокМеждународногоУчета.РеверсивнымиПроводками Тогда
				СодержаниеОперации = НСтр("ru = 'Реверс';
											|en = 'Reverse'", ОбщегоНазначения.КодОсновногоЯзыка()) + " " + Строка(ДокументОснование);
			Иначе
				СодержаниеОперации = НСтр("ru = 'Сторно';
											|en = 'Storno'", ОбщегоНазначения.КодОсновногоЯзыка()) + " " + Строка(ДокументОснование);
			КонецЕсли;
			
			Если ВариантФормированияПроводок = Перечисления.ВариантыФормированияПроводок.СКорреспонденцией Тогда
				ПроводкиСторно = РегистрыБухгалтерии.Международный.ПроводкиСторно(ДокументОснование,
					ДанныеЗаполнения.ПланСчетов, ТекущаяДатаСеанса());
				
				Проводки = Движения.Международный;
				Для Каждого Проводка Из ПроводкиСторно Цикл
					НоваяПроводка = Проводки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяПроводка, Проводка);
					НоваяПроводка.Активность = Ложь;
					Проводки.УстановитьСубконто(НоваяПроводка, Проводка, "Дт");
					Проводки.УстановитьСубконто(НоваяПроводка, Проводка, "Кт");
				КонецЦикла;
			Иначе
				ПроводкиСторно = РегистрыБухгалтерии.МеждународныйБезКорреспонденции.ПроводкиСторно(ДокументОснование,
					ДанныеЗаполнения.ПланСчетов, ТекущаяДатаСеанса());
				
				Проводки = Движения.МеждународныйБезКорреспонденции;
				Для Каждого Проводка Из ПроводкиСторно Цикл
					НоваяПроводка = Проводки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяПроводка, Проводка);
					НоваяПроводка.Активность = Ложь;
					Проводки.УстановитьСубконто(НоваяПроводка, Проводка);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Массив") Тогда
		
		ТипыДокументаОснования = Метаданные.НайтиПоТипу(ТипЗнч(Ссылка)).ТабличныеЧасти.ЗаполнениеДвижений.Реквизиты.Документ.Тип;
		
		Для каждого ДокументЗаполнения Из ДанныеЗаполнения Цикл
			
			Если ТипыДокументаОснования.СодержитТип(ТипЗнч(ДокументЗаполнения)) Тогда
				
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДокументЗаполнения,,"Дата,Номер,Проведен");
				НачалоСодержания = ?(СодержаниеОперации = "", НСтр("ru = 'КОРРЕКТИРОВКА:';
																	|en = 'ADJUSTMENT:'") + " ", СодержаниеОперации + ", ");
				Комментарий = "";
				Содержание = НачалоСодержания + Строка(ДокументЗаполнения);
				НоваяСтрока = ЗаполнениеДвижений.Добавить();
				НоваяСтрока.Документ = ДокументЗаполнения;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПланСчетов) Тогда
		ПланСчетов = Справочники.ПланыСчетовМеждународногоУчета.ПланСчетовПоУмолчанию();
	КонецЕсли;
	
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если ВводитьПериодПоСтрокам Тогда
		Движения.Международный.ДополнительныеСвойства.Вставить("ВводитьПериодПоСтрокам", ВводитьПериодПоСтрокам);
	КонецЕсли;
	// Заполнение реквизитов регистров Период, Организация
	Для каждого НаборЗаписей Из Движения Цикл
		
		Если НаборЗаписей.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ПустаяТаблица   = НаборЗаписей.ВыгрузитьКолонки(); // ТаблицаЗначений - 
		ЕстьПланСчетов  = ПустаяТаблица.Колонки.Найти("ПланСчетов") <> Неопределено;
		ЕстьОрганизация = ПустаяТаблица.Колонки.Найти("Организация") <> Неопределено;
		ЕстьПериод      = ПустаяТаблица.Колонки.Найти("Период") <> Неопределено;
		
		Если НЕ (ЕстьОрганизация ИЛИ ЕстьПериод ИЛИ ЕстьПланСчетов) Тогда
			Продолжить;
		КонецЕсли;
		
		ТаблицаДвижений = НаборЗаписей.Выгрузить();
		Если ЕстьПланСчетов Тогда
			ТаблицаДвижений.ЗаполнитьЗначения(ПланСчетов, "ПланСчетов");
		КонецЕсли;
		Если ЕстьОрганизация Тогда
			ТаблицаДвижений.ЗаполнитьЗначения(Организация, "Организация");
		КонецЕсли;
		Если ЕстьПериод И НЕ ВводитьПериодПоСтрокам Тогда
			ТаблицаДвижений.ЗаполнитьЗначения(Дата, "Период");
		КонецЕсли;
		НаборЗаписей.Загрузить(ТаблицаДвижений);
	
	КонецЦикла;
	
	УстановитьАктивностьДвижений(РежимЗаписи);

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ТекущаяДатаСеанса());
	Ответственный = Пользователи.ТекущийПользователь();
	
	Набор = ОбъектКопирования.Движения.Международный;
	Набор.Прочитать();
	НаборТекущегоОбъекта = Движения.Международный;
	Если Набор.Количество() > 0 Тогда
		
		Для Каждого ЗаписьНабора Из Набор Цикл
			
			НоваяЗапись = НаборТекущегоОбъекта.Добавить();
			НоваяЗапись.Период = Дата;
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ЗаписьНабора,,"Период,Регистратор,СубконтоДт,СубконтоКт");
			НоваяЗапись.Активность = Ложь;
			
			Для Каждого Субконто Из ЗаписьНабора.СубконтоДт Цикл
				НоваяЗапись.СубконтоДт[Субконто.Ключ] = Субконто.Значение;
			КонецЦикла;
			
			Для Каждого Субконто Из ЗаписьНабора.СубконтоКт Цикл
				НоваяЗапись.СубконтоКт[Субконто.Ключ] = Субконто.Значение;
			КонецЦикла;
			
		КонецЦикла;// по записям набора
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьАктивностьДвижений(РежимЗаписи)
	
	Активность = РежимЗаписи = РежимЗаписиДокумента.Проведение ИЛИ РежимЗаписи = РежимЗаписиДокумента.Запись И Проведен;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого Движение Из Движения Цикл
		
		Если Движение.Записывать = Ложь Тогда // При работе формы набор может быть уже "потроган" (прочитан, модифицирован)
			// Набор никто не трогал
			Движение.Прочитать();
		КонецЕсли;
		
		Для Каждого Строка Из Движение Цикл
			
			Если Строка.Активность = Активность Тогда
				Продолжить;
			КонецЕсли;
			
			Строка.Активность   = Активность;
			Движение.Записывать = Истина; // На случай, если набор был прочитан выше
			
		КонецЦикла;
		
		Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
			Движение.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли