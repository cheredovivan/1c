///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.Печать

// Заполняет список команд печати.
// 
// Параметры:
//  КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной
//                                                            параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной
//                                            параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании
//  Параметры                  - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
КонецПроцедуры

// Для использования в процедуре ДобавитьКомандыСозданияНаОсновании других модулей менеджеров объектов.
//  Добавляет в список команд создания на основании этот объект.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании
// 
// Возвращаемое значение:
//  СтрокаТаблицыЗначений, Неопределено - описание добавленной команды.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Возврат СозданиеНаОсновании.ДобавитьКомандуСозданияНаОсновании(КомандыСозданияНаОсновании, Метаданные());
	
КонецФункции

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ФизическоеЛицо)
	|	И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

#Область ОбновлениеИнформационнойБазы

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область СЭДО

Функция ТекстXML(Документ) Экспорт
	// Пример xml:
	//<n1:noticeResponse xmlns:p="urn:ru:ecp:integration:types:person:v3.0" xmlns:n1="urn:ru:ecp:integration:vnim:notice:response:v3.0" specVersion="3.0.1" responseOn="e0930cd4-7626-464f-93c9-64e2b7be0550" xmlns:com="urn:ru:ecp:integration:types:common:v3.0">
	//	<noticeNumber>5862</noticeNumber>
	//	<insuredInfo>
	//		<snils>13700328015</snils>
	//	</insuredInfo>
	//	<insurerInfo>
	//		<regNumSFR>1000260750</regNumSFR>
	//	</insurerInfo>
	//	<informationResponse>
	//		<information>
	//			<receivePayment>
	//				<com:bankInfo>
	//					<com:bik>044525593</com:bik>
	//					<com:accountNum>40702840102130000122</com:accountNum>
	//				</com:bankInfo>
	//			</receivePayment>
	//		</information>
	//	</informationResponse>
	//	<note>Направляем исправленные реквизиты для выплаты</note>
	//	<senderInfo>
	//		<author>
	//			<p:firstName>Андрей</p:firstName>
	//			<p:lastName>Андреев</p:lastName>
	//			<p:middleName>Андреевич</p:middleName>
	//		</author>
	//		<email>mail@mail.ru</email>
	//		<phoneNumber>+7 495 323 45 79</phoneNumber>
	//	</senderInfo>
	//</n1:noticeResponse>
	
	МенеджерXML = Обработки.ПостроительXML.Создать();
	МенеджерXML.ФорматДат = "ДФ=yyyy-MM-dd";
	
	// В корне 1 элемент: Подтверждение сведений проактивно собранных Фондом.
	КореньXML = МенеджерXML.ДобавитьУзел(МенеджерXML.ДеревоXML, "n1:noticeResponse");
	
	// Пространства имен.
	МенеджерXML.ДобавитьПространствоИмен(КореньXML, "n1", "urn:ru:ecp:integration:vnim:notice:response:v3.0");
	МенеджерXML.ДобавитьПространствоИмен(КореньXML, "common", "urn:ru:ecp:integration:types:common:v3.0");
	МенеджерXML.ДобавитьПространствоИмен(КореньXML, "person", "urn:ru:ecp:integration:types:person:v3.0");
	МенеджерXML.ДобавитьПространствоИмен(КореньXML, "organization", "urn:ru:ecp:integration:types:organization:v3.0");
	МенеджерXML.ДобавитьПространствоИмен(КореньXML, "notice", "urn:ru:ecp:integration:vnim:notice:v3.0");
	МенеджерXML.ДобавитьПространствоИмен(КореньXML, "xsi", "http://www.w3.org/2001/XMLSchema-instance");
	
	// Атрибуты.
	МенеджерXML.ДобавитьАтрибут(КореньXML, "specVersion", Документ.ВерсияСпецификации);
	МенеджерXML.ДобавитьАтрибут(КореньXML, "responseOn", Документ.ВходящийЗапросИдентификатор);
	
	// Номер запроса.
	МенеджерXML.ДобавитьУзел(КореньXML, "noticeNumber", Документ.ВходящийЗапросНомер);
	
	// Застрахованное лицо.
	УзелДанных = МенеджерXML.ДобавитьУзел(КореньXML, "insuredInfo");
	МенеджерXML.ДобавитьУзел(УзелДанных, "snils", УбратьПробелыИТире(Документ.СотрудникСНИЛС));
	
	// Страхователь.
	УзелДанных = МенеджерXML.ДобавитьУзел(КореньXML, "insurerInfo");
	МенеджерXML.ДобавитьУзел(УзелДанных, "regNumSFR", Документ.РегистрационныйНомерСФР);
	
	// Ответ на запрос информации о недостающих сведениях, необходимых для назначения и выплаты пособия.
	УзелДанных = МенеджерXML.ДобавитьУзел(КореньXML, "informationResponse");
	Если Документ.ОтветБудетВДругомДокументе Тогда
		МенеджерXML.ДобавитьУзел(УзелДанных, "responseWillBeSentNewDoc", Истина);
	ИначеЕсли Документ.ОтказВНазначенииПособия Тогда
		МенеджерXML.ДобавитьУзел(УзелДанных, "refusal", Истина);
	ИначеЕсли Документ.ОтветНеТребуется Тогда
		МенеджерXML.ДобавитьУзел(УзелДанных, "noInformation", Истина);
	Иначе
		ВыгрузитьРеквизитыДляВыплатыПособия(Документ, МенеджерXML, УзелДанных);
	КонецЕсли;
	
	// Примечание / причина.
	МенеджерXML.ДобавитьУзел(КореньXML, "note", СокрЛП(Документ.Примечание), Истина, Ложь);
	
	// Уполномоченный представитель страхователя.
	УзелПредставитель = МенеджерXML.ДобавитьУзел(КореньXML, "senderInfo");
	ВыгрузитьДанныеПредставителя(Документ, МенеджерXML, УзелПредставитель);
	
	МенеджерXML.НачатьЗаписьВСтрокуXML(Ложь);
	СтрокаXML = МенеджерXML.СтрокаXML();
	Возврат СтрокаXML;
КонецФункции

Функция Метаданные() Экспорт
	Возврат Метаданные.Документы.ОтветНаЗапросРеквизитовДляВыплатыПособия;
КонецФункции

Функция ТипСообщения(Документ = Неопределено) Экспорт
	Возврат 33;
КонецФункции

Функция ЗаголовокФормыОтправкиСведенийВФСС() Экспорт
	Возврат СтрШаблон(НСтр("ru = 'Отправка %1';
							|en = 'Send %1'"), Метаданные().ПредставлениеСписка);
КонецФункции

Процедура ПриЗаполненииПараметровОтправки(МассивСсылок, ТаблицаОтправки) Экспорт
	Синоним = Метаданные().Синоним;
	
	ИменаРеквизитовСтроки = "Ссылка, Номер, Дата, ФизическоеЛицо, Организация";
	ИменаРеквизитов = "Ссылка, Номер, Дата, ДатаОтправки, ФизическоеЛицо, Организация, ХранилищеXML, ПометкаУдаления, Проведен";
	Соответствие = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивСсылок, ИменаРеквизитов);
	Для Каждого КлючИЗначение Из Соответствие Цикл
		Документ = КлючИЗначение.Значение;
		// Добавление строки отправки и заполнение представления.
		СтрокаОтправки = ТаблицаОтправки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаОтправки, Документ, ИменаРеквизитовСтроки);
		СтрокаОтправки.ТипСообщения = ТипСообщения(Документ);
		СтрокаОтправки.ТекстXML     = Документ.ХранилищеXML.Получить();
		СтрокаОтправки.Отправить    = Ложь;
		СтрокаОтправки.ШаблонОшибки = СтрШаблон(
			НСтр("ru = 'Не удалось отправить %1 сотрудника %2:%3';
				|en = 'Cannot send %1 of the %2 employee:%3'"),
			Синоним,
			СтрокаОтправки.ФизическоеЛицо,
			"%1");
		// Проверки и обновление вторичных данных документа перед отправкой.
		Если Документ.ПометкаУдаления Тогда
			СтрокаОтправки.ИндексКартинки = СЭДОФССКлиентСервер.ИндексКартинкиУдален();
			СтрокаОтправки.Результат = НСтр("ru = 'Документ помечен на удаление';
											|en = 'The document is marked for deletion'");
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(Документ.ДатаОтправки) Тогда
			СтрокаОтправки.ИндексКартинки = СЭДОФССКлиентСервер.ИндексКартинкиОтправлен();
			Если НачалоДня(Документ.ДатаОтправки) <= ТекущаяДатаСеанса() Тогда
				Текст = НСтр("ru = 'Документ уже отправлен %1';
							|en = 'The document is already sent %1'");
				СтрокаОтправки.Результат = СтрШаблон(Текст, Формат(Документ.ДатаОтправки, "ДЛФ=D"));
			Иначе
				Текст = НСтр("ru = 'Документ уже отправлен в %1';
							|en = 'The document is already sent to %1'");
				СтрокаОтправки.Результат = СтрШаблон(Текст, Формат(Документ.ДатаОтправки, "ДФ=HH:mm"));
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		ДокументОбъект = Документ.Ссылка.ПолучитьОбъект();
		Если Не ДокументОбъект.ПроверитьЗаполнение() Тогда
			СтрокаОтправки.ИндексКартинки = СЭДОФССКлиентСервер.ИндексКартинкиПредупреждение();
			ЗаполнитьТекстОшибкиПроверкиЗаполнения(СтрокаОтправки, "");
			Если Не ДокументОбъект.ОтключитьПроверкиПроведения Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		// Проведение документа если требуется.
		ЕстьИзменения = ДокументОбъект.ОбновитьВторичныеДанные();
		СтрокаОтправки.Отправить = Истина;
		Если ЕстьИзменения Или Не ДокументОбъект.Проведен Тогда
			Попытка
				СтрокаОтправки.ИндексКартинки = СЭДОФССКлиентСервер.ИндексКартинкиПроведен();
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
				ЗаполнитьЗначенияСвойств(СтрокаОтправки, ДокументОбъект, ИменаРеквизитовСтроки);
				СтрокаОтправки.ТекстXML = ДокументОбъект.ХранилищеXML.Получить();
			Исключение
				СтрокаОтправки.Отправить = Ложь;
				СтрокаОтправки.ИндексКартинки = СЭДОФССКлиентСервер.ИндексКартинкиОстановлен();
				СтрокаОтправки.ЗначениеРасшифровки = Документ.Ссылка;
				ТекстИсключения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЗаполнитьТекстОшибкиПроверкиЗаполнения(СтрокаОтправки, ТекстИсключения);
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриПодготовкеВыбранныхДокументовКОтправке(МассивСсылок, ТаблицаОтправки) Экспорт
	// Действие не требуется.
КонецПроцедуры

// См. СЭДОФСС.ЗарегистрироватьНепосредственныйРезультатОтправкиДокумента.
Процедура ЗарегистрироватьРезультатОтправки(РезультатОтправки) Экспорт
	// Сначала записывается отправленный документ, т.к. его данные используются при обновлении регистров (кэшей).
	ДокументОбъект = РезультатОтправки.Ссылка.ПолучитьОбъект();
	ДокументОбъект.ДатаОтправки           = РезультатОтправки.ДатаОтправки;
	ДокументОбъект.ИдентификаторСообщения = РезультатОтправки.ИдентификаторСообщения;
	ДокументОбъект.Страхователь           = РезультатОтправки.Страхователь;
	ДокументОбъект.ГоловнаяОрганизация    = РезультатОтправки.ГоловнаяОрганизация;
	СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, Ложь, РежимЗаписиДокумента.Запись);
	
	// Затем записывается транспортный регистр,
	// который при записи запускает обновление остальных регистров, зависящих от состояния отправки.
	РегистрыСведений.РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий.ЗаполнитьПоДокументу(ДокументОбъект, РезультатОтправки);
	
	// Регистр СведенияОбЭЛН обновлять не требуется, т.к. организация, физлицо и пометка удаления не менялись.
	// Документ ВходящийЗапросФССДляРасчетаПособия обновлять не требуется по схожим причинам.
	
	РезультатОтправки.Измененные.Добавить(ДокументОбъект.Ссылка);
КонецПроцедуры

// См. СЭДОФСС.ЗагрузитьРезультатДоставкиСообщенияСЭДО.
Процедура ЗагрузитьРезультатДоставкиСообщения33(Страхователь, Сообщение, Результат) Экспорт
	Таблица = РегистрыСведений.РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий.НайтиПоИдентификаторуСообщения(
		Страхователь,
		Сообщение.Идентификатор,
		Результат);
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		// Сначала записывается отправленный документ, т.к. его данные используются при обновлении регистров (кэшей).
		Если Не Сообщение.ДоставленоФонду Тогда
			ДокументОбъект = СтрокаТаблицы.ИсходящийДокумент.ПолучитьОбъект();
			ДокументОбъект.ДатаОтправки           = '00010101';
			ДокументОбъект.ИдентификаторСообщения = "";
			СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, Ложь, РежимЗаписиДокумента.Запись);
		КонецЕсли;
		
		// Затем записывается транспортный регистр,
		// который при записи запускает обновление остальных регистров, зависящих от состояния отправки.
		РегистрыСведений.РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий.ЗаполнитьПоРезультатуДоставки(
			СтрокаТаблицы.ИсходящийДокумент,
			Сообщение);
		
		// Отметка для БРО и других потребителей что сообщение обработано.
		Результат.Обработано = Истина;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СоставДокументов

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
//
Функция ОписаниеСоставаОбъекта() Экспорт
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаФизическоеЛицоВШапке("ФизическоеЛицо", "Сотрудник");
КонецФункции

#КонецОбласти

#Область ФиксацияВторичныхДанныхВДокументах

Функция ПараметрыФиксацииВторичныхДанных(Документ = Неопределено) Экспорт
	Возврат ФиксацияВторичныхДанныхВДокументах.ПараметрыФиксации(ФиксируемыеРеквизиты(Документ));
КонецФункции

// Определяет по данным документа признак фиксации.
//
// Параметры:
//   Документ - ДокументОбъект.ОтветНаЗапросРеквизитовДляВыплатыПособия, ДанныеФормыСтруктура
//
// Возвращаемое значение:
//   Булево
//
Функция ОбъектЗафиксирован(Документ) Экспорт
	Возврат Не Документ.ПометкаУдаления И ЗначениеЗаполнено(Документ.ДатаОтправки);
КонецФункции

#КонецОбласти

#Область МногофункциональныеДокументы

// Возвращает метаданные разделов документа.
//
// Возвращаемое значение:
//   Соответствие, Неопределено - Описание разделов документа.
//
Функция ОписаниеРазделовДанных() Экспорт
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("МногофункциональныеДокументыБЗККлиентСервер");
		ВсеРазделы = Модуль.РазделыДанных();
		
		ОписаниеРазделовДанных = Новый Соответствие();
		
		ОписаниеРаздела = Модуль.НовыйОписаниеРазделаДанных();
		ОписаниеРазделовДанных.Вставить(ВсеРазделы.КадровыеДанные, ОписаниеРаздела);
		ОписаниеРаздела.РеквизитСостояние    = "Проведен";
		ОписаниеРаздела.РеквизитОтветсвенный = "Ответственный";
		
		ОписаниеРаздела = Модуль.НовыйОписаниеРазделаДанных();
		ОписаниеРазделовДанных.Вставить(ВсеРазделы.НачисленнаяЗарплата, ОписаниеРаздела);
		Возврат ОписаниеРазделовДанных;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

// Возвращает значения по которым будут проверяться права на документ.
//
// Параметры:
//   ДокументОбъект - ДокументОбъект, ДанныеФормыСтруктура
//
// Возвращаемое значение:
//   Структура - Значения доступа по которым будут проверяться права на документ
//
Функция ЗначенияДоступа(ДокументОбъект) Экспорт
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		МодульМногофункциональныеДокументыБЗК = ОбщегоНазначения.ОбщийМодуль("МногофункциональныеДокументыБЗК");
		Возврат МодульМногофункциональныеДокументыБЗК.ЗначенияДоступаПоСоставуДокумента(
			ДокументОбъект,
			ДокументОбъект.Организация);
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область ФиксацияВторичныхДанныхВДокументах

Функция ФиксируемыеРеквизиты(Документ = Неопределено)
	ФиксируемыеРеквизиты = Новый Соответствие;
	
	РеквизитыРучногоЗаполнения = Новый Структура("Ответственный, Комментарий, Примечание,
		|ФизическоеЛицо, ГоловнаяОрганизация"); // Физлицо и головная орг. заполняются безусловно, т.к. определяют права.
	
	Если Документ = Неопределено Тогда
		ЗаполненВходящийЗапрос = Ложь;
		ЗаполненСотрудник      = Ложь;
		ЗаполненоФизлицо       = Ложь;
	Иначе
		ЗаполненВходящийЗапрос = ЗначениеЗаполнено(Документ.ВходящийЗапрос);
		ЗаполненСотрудник      = ЗначениеЗаполнено(Документ.Сотрудник);
		ЗаполненоФизлицо       = ЗначениеЗаполнено(Документ.ФизическоеЛицо);
	КонецЕсли;
	
	// Реквизиты входящего запроса.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения      = "ВходящийЗапрос";
	Группа.ИмяГруппы                = "ВходящийЗапрос";
	Группа.ОтображатьПредупреждение = ЗаполненВходящийЗапрос;
	
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ВходящийЗапросИдентификатор");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ВходящийЗапросНомер");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Сотрудник", Ложь);
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Организация", Ложь);
	
	// Реквизиты организации.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения = "Организация";
	Группа.ИмяГруппы           = "Организация";
	
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Страхователь");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "РегистрационныйНомерСФР");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ПредставительСФР", Ложь);
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ДолжностьПредставителяСФР", Ложь);
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ОснованиеПодписиПредставителяСФР", Ложь);
	
	// Уполномоченный представитель.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения = "ПредставительСФР";
	Группа.ИмяГруппы           = "ПредставительСФР";
	
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ПредставительФамилия");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ПредставительИмя");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ПредставительОтчество");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ПредставительТелефон");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ПредставительЭлектроннаяПочта");
	
	// Физлицо.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения      = "Сотрудник";
	Группа.ИмяГруппы                = "Сотрудник";
	Группа.ОтображатьПредупреждение = ЗаполненСотрудник;
	
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "КодСтраныГражданства");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "СотрудникФамилия");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "СотрудникИмя");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "СотрудникОтчество");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "СотрудникДатаРождения");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "СотрудникСНИЛС");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживания");
	
	// Способ выплаты пособия.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения = "Сотрудник";
	Группа.ИмяГруппы           = "СпособВыплатыПособия";
	Группа.ФиксацияГруппы      = Истина;
	
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "СпособВыплатыПособия");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "КартаМИР");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Банк");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "НомерСчета");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияВид");
	
	// Способ выплаты пособия: Сведения о банке.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения = "Банк";
	Группа.ИмяГруппы           = "РеквизитыБанка";
	Группа.ФиксацияГруппы      = Истина;
	
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "БанкБИК");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "БанкКоррСчет");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "БанкНаименование");
	
	// Способ выплаты пособия: реквизиты адреса проживания.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения = "АдресПроживания";
	Группа.ИмяГруппы           = "АдресПроживания";
	Группа.ФиксацияГруппы      = Истина;
	
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияЗаПределамиРФ");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияДом");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияИндекс");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияКвартира");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияСтроение");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияКорпус");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияФИАСДом");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияФИАСРО");
	
	// Способ выплаты пособия: Сведения о банке.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения      = "ПервичныйДокумент";
	Группа.ИмяГруппы                = "ИнаяОрганизация";
	Группа.ФиксацияГруппы           = Истина;
	Группа.ОтображатьПредупреждение = Ложь;
	
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ИнаяОрганизацияБИК");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ИнаяОрганизацияКБК");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ИнаяОрганизацияЛицевойСчетОрганизации");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ИнаяОрганизацияЛицевойСчетСотрудника");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ИнаяОрганизацияНаименование");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ИнаяОрганизацияНомерСчетаСтрахователя");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ИнаяОрганизацияРегистрационныйНомерСФР");
	
	// Добавление всех оставшихся реквизитов одной группой "по умолчанию".
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ИмяГруппы                = "СлужебныеРеквизиты";
	Группа.ОтображатьПредупреждение = Ложь;
	Для Каждого Реквизит Из Метаданные().Реквизиты Цикл
		Если Не РеквизитыРучногоЗаполнения.Свойство(Реквизит.Имя)
			И ФиксируемыеРеквизиты[Реквизит.Имя] = Неопределено Тогда
			ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, Реквизит.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(ФиксируемыеРеквизиты);
КонецФункции

Процедура ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, ИмяРеквизита, ОтображатьПредупреждение = Неопределено)
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(
		ФиксируемыеРеквизиты,
		Группа,
		ИмяРеквизита,
		ОтображатьПредупреждение);
КонецПроцедуры

#КонецОбласти

#Область ТекстXML

Функция ПоддерживаемыеВерсииСпецификации() Экспорт
	// В боевом проактиве СЭДО нумерация версий отличается от тестового.
	// Это может стать проблемой если версия проактива появится в xml в явном виде.
	// Пока версия не в xml - это "метафора" обозначающая алгоритм работы с данными (и отображения данных).
	// Поэтому при разработке понятнее и удобнее ориентироваться на нумерацию версий спецификации тестового СЭДО.
	Список = Новый СписокЗначений;
	Список.Добавить("3.0.1");
	Возврат Список;
КонецФункции

Процедура ВыгрузитьРеквизитыДляВыплатыПособия(Документ, МенеджерXML, УзелДанных)
	
	// Информация о недостающих сведениях, необходимых для назначения и выплаты пособия.
	УзелРеквизитов = МенеджерXML.ДобавитьУзел(УзелДанных, "information");
	
	// Способ выплаты пособия.
	УзелСпособ = МенеджерXML.ДобавитьУзел(УзелРеквизитов, "receivePayment", , Истина, Ложь);
	ВыгрузитьСпособВыплаты(Документ, МенеджерXML, УзелСпособ);
	
	// Данные о застрахованном лице.
	УзелСотрудник = МенеджерXML.ДобавитьУзел(УзелРеквизитов, "insuredInfo", , Истина, Ложь);
	
	// ФИО.
	УзелФИО = МенеджерXML.ДобавитьУзел(УзелСотрудник, "fullName", , Истина, Ложь);
	МенеджерXML.ДобавитьУзел(УзелФИО, "person:firstName",  Документ.СотрудникИмя);
	МенеджерXML.ДобавитьУзел(УзелФИО, "person:lastName",   Документ.СотрудникФамилия);
	МенеджерXML.ДобавитьУзел(УзелФИО, "person:middleName", Документ.СотрудникОтчество, Истина);
	
	// Дата рождения.
	МенеджерXML.ДобавитьУзел(УзелСотрудник, "birthDate", Документ.СотрудникДатаРождения, Истина, Ложь);
	
КонецПроцедуры

Процедура ВыгрузитьСпособВыплаты(Документ, МенеджерXML, УзелСпособ)
	
	Если Документ.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ЧерезБанк Тогда
		
		Узел = МенеджерXML.ДобавитьУзел(УзелСпособ, "common:bankInfo");
		МенеджерXML.ДобавитьУзел(Узел, "common:bik",        Документ.БанкБИК);
		МенеджерXML.ДобавитьУзел(Узел, "common:accountNum", Документ.НомерСчета);
		
	ИначеЕсли Документ.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.НаКартуМИР Тогда
		
		НомерКарты = БанковскиеКарты.ПолныйНомерКарты(Документ.КартаМИР);
		НомерКарты = СтроковыеФункцииБЗККлиентСервер.УбратьПробелы(НомерКарты);
		МенеджерXML.ДобавитьУзел(УзелСпособ, "common:cardMir", НомерКарты);
		
	ИначеЕсли Документ.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ПочтовымПереводом Тогда
		
		// Адрес доставки пособия получателю. Не применимо для пособий № 87, 88.
		УзелАдрес = МенеджерXML.ДобавитьУзел(УзелСпособ, "common:postalAddress");
		ВыгрузитьАдрес(МенеджерXML, УзелАдрес, Документ, "АдресПроживания");
		
	ИначеЕсли Документ.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ИнаяОрганизация Тогда
		
		// Сведения об иной организации. Не применимо для пособий № 87, 88.
		Узел = МенеджерXML.ДобавитьУзел(УзелСпособ, "common:otherOrg");
		МенеджерXML.ДобавитьУзел(Узел, "common:orgName",                Документ.ИнаяОрганизацияНаименование);
		МенеджерXML.ДобавитьУзел(Узел, "common:bik",                    Документ.ИнаяОрганизацияБИК);
		МенеджерXML.ДобавитьУзел(Узел, "common:accountNum",             Документ.ИнаяОрганизацияНомерСчетаСтрахователя);
		МенеджерXML.ДобавитьУзел(Узел, "common:personalOrgAccount",     Документ.ИнаяОрганизацияЛицевойСчетОрганизации);
		МенеджерXML.ДобавитьУзел(Узел, "common:personalInsuredAccount", Документ.ИнаяОрганизацияЛицевойСчетСотрудника, Истина);
		МенеджерXML.ДобавитьУзел(Узел, "common:kbk",                    Документ.ИнаяОрганизацияКБК, Истина);
		МенеджерXML.ДобавитьУзел(Узел, "common:regNumSFRCorrectionalFacility",
			Документ.ИнаяОрганизацияРегистрационныйНомерСФР);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыгрузитьАдрес(МенеджерXML, УзелАдрес, Документ, ИмяПоляАдрес)
	АдресЗаПределамиРФ = Документ[ИмяПоляАдрес + "ЗаПределамиРФ"];
	Если ЗначениеЗаполнено(АдресЗаПределамиРФ) Тогда
		// Адрес за пределами РФ.
		МенеджерXML.ДобавитьУзел(УзелАдрес, "common:foreignAddress", АдресЗаПределамиРФ);
	Иначе
		// Адрес (в представлении ГАР uuid).
		УзелГАР = МенеджерXML.ДобавитьУзел(УзелАдрес, "common:addressGar");
		Если ЗначениеЗаполнено(Документ[ИмяПоляАдрес + "ФИАСДом"]) Тогда
			// HOUSEGUID – Глобальный уникальный идентификатор дома.
			МенеджерXML.ДобавитьУзел(УзелГАР, "common:houseGuid", Документ[ИмяПоляАдрес + "ФИАСДом"]);
		Иначе
			// AOGUID - Guid записи родительского объекта (улицы, города, населенного пункта, планировочной структуры и т.п.).
			МенеджерXML.ДобавитьУзел(УзелГАР, "common:guid",     Документ[ИмяПоляАдрес + "ФИАСРО"]);
			МенеджерXML.ДобавитьУзел(УзелГАР, "common:house",    Документ[ИмяПоляАдрес + "Дом"], Истина);
			МенеджерXML.ДобавитьУзел(УзелГАР, "common:stroenie", Документ[ИмяПоляАдрес + "Строение"], Истина);
			МенеджерXML.ДобавитьУзел(УзелГАР, "common:building", Документ[ИмяПоляАдрес + "Корпус"], Истина);
		КонецЕсли;
		МенеджерXML.ДобавитьУзел(УзелГАР, "common:flat", Документ[ИмяПоляАдрес + "Квартира"], Истина);
		МенеджерXML.ДобавитьУзел(УзелАдрес, "common:postalCode", Документ[ИмяПоляАдрес + "Индекс"]);
	КонецЕсли;
КонецПроцедуры

Процедура ВыгрузитьДанныеПредставителя(Документ, МенеджерXML, УзелПредставитель)
	
	// ФИО отправителя.
	УзелФИО = МенеджерXML.ДобавитьУзел(УзелПредставитель, "author", , Истина, Ложь);
	МенеджерXML.ДобавитьУзел(УзелФИО, "person:firstName",  Документ.ПредставительИмя);
	МенеджерXML.ДобавитьУзел(УзелФИО, "person:lastName",   Документ.ПредставительФамилия);
	МенеджерXML.ДобавитьУзел(УзелФИО, "person:middleName", Документ.ПредставительОтчество, Истина);
	
	// Адрес электронной почты отправителя.
	МенеджерXML.ДобавитьУзел(УзелПредставитель, "email", Документ.ПредставительЭлектроннаяПочта);
	
	// Номер телефона отправителя.
	МенеджерXML.ДобавитьУзел(УзелПредставитель, "phoneNumber", Документ.ПредставительТелефон);
	
КонецПроцедуры

Функция УбратьПробелыИТире(Строка)
	Возврат СтроковыеФункцииБЗККлиентСервер.УбратьПробелы(Строка, Истина);
КонецФункции

#КонецОбласти

#Область ПриЗаполненииПараметровОтправки

Функция ЗаполнитьТекстОшибкиПроверкиЗаполнения(СтрокаОтправки, ТекстИсключенияПриПроведении)
	СтрокаОтправки.Ошибки = ПолучитьСообщенияПользователю(Истина);
	
	Количество = СтрокаОтправки.Ошибки.Количество();
	Если Количество = 1 Тогда
		СтрокаОтправки.Результат = СтрокаОтправки.Ошибки[0].Текст;
	ИначеЕсли Количество > 1 Тогда
		СтрокаОтправки.Результат = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru = ';%1 ошибка заполнения;;%1 ошибки заполнения;%1 ошибок заполнения;';
				|en = ';%1 filling error;;%1 filling errors;%1 filling errors;'"),
			Количество);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстИсключенияПриПроведении) Тогда
		Если СтрокаОтправки.Результат = "" Тогда
			СтрокаОтправки.Результат = СтрШаблон(НСтр("ru = 'Ошибка проведения: %1';
														|en = 'Posting error: %1'"), ТекстИсключенияПриПроведении);
		Иначе
			СтрокаОтправки.Результат = СтрокаОтправки.Результат + ";" + Символы.ПС + СтрШаблон(НСтр("ru = 'И ошибка проведения: %1';
																									|en = 'And posting error: %1'"), ТекстИсключенияПриПроведении);
		КонецЕсли;
	КонецЕсли;
КонецФункции

#КонецОбласти

Функция КоличествоОжидаемыхСообщений(ГоловнаяОрганизация, Организации) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	1 КАК Поле1
	|ИЗ
	|	РегистрСведений.РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий КАК Регистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО Регистрации.ГоловнаяОрганизация = Организации.Ссылка
	|ГДЕ
	|	Регистрации.Состояние = &Отправлен
	|	И Регистрации.ДатаОтправки > &ДатаНачалаАктуальности
	|	И Регистрации.ГоловнаяОрганизация = &ГоловнаяОрганизация
	|	И Организации.Ссылка В(&Организации)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1
	|ИЗ
	|	РегистрСведений.РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий КАК Регистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО Регистрации.ГоловнаяОрганизация = Организации.Ссылка
	|ГДЕ
	|	Регистрации.Состояние = &ОтправленОператору
	|	И Регистрации.ДатаОтправкиОператору > &ДатаНачалаАктуальности
	|	И Регистрации.ГоловнаяОрганизация = &ГоловнаяОрганизация
	|	И Организации.Ссылка В(&Организации)";
	
	Запрос.УстановитьПараметр("Отправлен", Перечисления.СостоянияДокументаСЭДОФСС.Отправлен);
	Запрос.УстановитьПараметр("ОтправленОператору", Перечисления.СостоянияДокументаСЭДОФСС.ОтправленОператору);
	Запрос.УстановитьПараметр("ДатаНачалаАктуальности", ДатаНачалаАктуальности());
	
	Если ЗначениеЗаполнено(ГоловнаяОрганизация) Тогда
		Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Регистрации.ГоловнаяОрганизация = &ГоловнаяОрганизация", "");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организации) Тогда
		Запрос.УстановитьПараметр("Организации", Организации);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПО Регистрации.ГоловнаяОрганизация = Организации.Ссылка", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Организации.Ссылка В(&Организации)", "");
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выбрать().Количество();
КонецФункции

Функция ДатаНачалаАктуальности()
	Возврат НачалоДня(ДобавитьМесяц(ТекущаяДатаСеанса(), -1));
КонецФункции

Функция ОписаниеПодписейДокумента() Экспорт 
	ОписаниеПодписей = ПодписиДокументов.ОписаниеТаблицыПодписей();
	ПодписиДокументов.ДобавитьОписаниеПодписейОрганизации(ОписаниеПодписей, "ПредставительСФР");
	Возврат ОписаниеПодписей;
КонецФункции

Функция НайтиПоИдентификатору(ИдентификаторСообщения, Количество = Неопределено) Экспорт
	Настройки = ЗапросыБЗК.НастройкиЗапросаКТаблице();
	Если Количество <> Неопределено Тогда
		Настройки.Количество = Количество;
	КонецЕсли;
	Настройки.Порядок = "ДатаОтправки УБЫВ";
	ЗапросыБЗК.ДобавитьОтбор(Настройки.Отбор, "ИдентификаторСообщения", "=", ИдентификаторСообщения);
	Поля = "Ссылка, Дата, ПометкаУдаления, Организация, Страхователь";
	Запрос = ЗапросыБЗК.ЗапросКТаблице(Метаданные(), Поля, Настройки);
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

#КонецОбласти

#КонецЕсли
