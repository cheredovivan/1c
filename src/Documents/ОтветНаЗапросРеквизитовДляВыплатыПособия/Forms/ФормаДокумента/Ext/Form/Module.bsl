///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		ПриПолученииДанныхНаСервере();
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	КонецЕсли;
	// Конец ПроцессыОбработкиДокументов
	
	Если Параметры.ПоказатьОшибкиОтправки Тогда
		Объект.ОтключитьПроверкиПроведения = Ложь;
		ПроверитьЗаполнение();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ТекстЗапросаXML = ТекущийОбъект.ХранилищеXML.Получить();
	
	ПриПолученииДанныхНаСервере(ТекущийОбъект);
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ИмяСобытия = "Запись_НастройкиПрямыхВыплатФСССотрудников"
		И Параметр.ФизическоеЛицо = Объект.ФизическоеЛицо Тогда
		ПодключитьОбработчикОжиданияПрочитатьОбновитьВторичныеДанные();
		
	ИначеЕсли ИмяСобытия = "Запись_ОтветНаЗапросРеквизитовДляВыплатыПособия"
		И (Источник = Объект.Ссылка Или Не ЗначениеЗаполнено(Источник)) Тогда
		ПодключитьОбработчикОжиданияПрочитатьОбновитьВторичныеДанные();
		
	ИначеЕсли ИмяСобытия = "Запись_РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий"
		И (Источник = Объект.Ссылка Или Не ЗначениеЗаполнено(Источник)) Тогда
		ПодключитьОбработчикОжиданияПрочитатьОбновитьВторичныеДанные();
		
	ИначеЕсли ИмяСобытия = "Запись_СведенияОЗастрахованномЛицеФСС" Тогда
		ПодключитьОбработчикОжиданияПрочитатьОбновитьВторичныеДанные();
		
	ИначеЕсли ИмяСобытия = "Запись_НастройкиПрямыхВыплатФССОрганизаций"
		И Параметр.ГоловнаяОрганизация = Объект.ГоловнаяОрганизация Тогда
		ПодключитьОбработчикОжиданияПрочитатьОбновитьВторичныеДанные();
		
	ИначеЕсли ИмяСобытия = "Запись_БанковскиеСчетаКонтрагентов" Тогда
		ПодключитьОбработчикОжиданияПрочитатьОбновитьВторичныеДанные();
		
	ИначеЕсли ИмяСобытия = "Запись_БанковскиеКартыКонтрагентов"
		И Источник = Объект.КартаМИР Тогда
		ПодключитьОбработчикОжиданияПрочитатьОбновитьВторичныеДанные();
		
	ИначеЕсли ИмяСобытия = "ИзменениеДанныхФизическогоЛица"
		И Источник = Объект.ФизическоеЛицо Тогда
		ПодключитьОбработчикОжиданияПрочитатьОбновитьВторичныеДанные();
		
	ИначеЕсли ИмяСобытия = "Запись_ФизическиеЛица"
		И Источник = Объект.ФизическоеЛицо Тогда
		ПодключитьОбработчикОжиданияПрочитатьОбновитьВторичныеДанные();
		
	ИначеЕсли ИмяСобытия = СЭДОФССКлиент.ИмяСобытияПослеПолученияСообщенийОтФСС()
		Или ИмяСобытия = СЭДОФССКлиент.ИмяСобытияПослеОтправкиПодтвержденияПолучения() Тогда
		ПодключитьОбработчикОжиданияПрочитатьОбновитьВторичныеДанные();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	СЭДОФСС.СвертываемыеГруппыОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
	// До проверки объекта создаем его, дозаполняем и сами проверяем.
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	Если Не ТекущийОбъект.ПроверитьЗаполнение() Тогда
		Если Не Объект.ОтключитьПроверкиПроведения Тогда
			Отказ = Истина;
		КонецЕсли;
		ОбработатьСообщенияПользователю();
	КонецЕсли;
	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Объект"));
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекстЗапросаXML = ТекущийОбъект.ХранилищеXML.Получить();
	
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.СохранитьРеквизитыФормыФикс(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи, Отказ);
	КонецЕсли;
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ФиксацияВторичныхДанныхВДокументахФормы.УстановитьМодифицированность(ЭтотОбъект, Ложь);
	ФиксацияВторичныхДанныхВДокументахФормы.УстановитьОбъектЗафиксирован(ЭтотОбъект);
	ФиксацияВторичныхДанныхВДокументахФормы.ЗаполнитьРеквизитыФормыФикс(ЭтотОбъект, ЭтотОбъект.Объект);
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбновитьФорму(ЭтотОбъект);
	
	ОбновитьЭлементыФормы();
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец ПроцессыОбработкиДокументов
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ПослеЗаписиНаСервере(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ОтветНаЗапросРеквизитовДляВыплатыПособия", ПараметрыЗаписи, Объект.Ссылка);
	СЭДОФССКлиент.ОповеститьОНеобходимостиОбновитьТекущиеДела();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОрганизацияПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	СотрудникПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВходящийЗапросПриИзменении(Элемент)
	ВходящийЗапросПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СпособВыплатыПособияПриИзменении(Элемент)
	СпособВыплатыПособияПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура БанкПриИзменении(Элемент)
	БанкПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура АдресПроживанияВидПриИзменении(Элемент)
	АдресПроживанияВидПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ИнаяОрганизацияБИКПриИзменении(Элемент)
	ИнаяОрганизацияБИКПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ИнаяОрганизацияБанкПриИзменении(Элемент)
	ИнаяОрганизацияБанкПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура АдресПроживанияЗаПределамиРФНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СЭДОФССКлиент.ПоказатьФормуРедактированияКомментария(Элемент, ЭтотОбъект, "Объект." + Элемент.Имя,
		НСтр("ru = 'Адрес проживания за пределами РФ';
			|en = 'Residential address outside the Russian Federation'"));
КонецПроцедуры

&НаКлиенте
Процедура ПримечаниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СЭДОФССКлиент.ПоказатьФормуРедактированияКомментария(Элемент, ЭтотОбъект, "Объект." + Элемент.Имя,
		Элемент.ПодсказкаВвода);
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СЭДОФССКлиент.ПоказатьФормуРедактированияКомментария(Элемент, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура НадписьДокументПроведенИОтправленОбработкаНавигационнойСсылки(Элемент, Адрес, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Измерения = Новый Структура("ИсходящийДокумент", Объект.Ссылка);
	ПараметрыЗаписи = Новый Массив(1);
	ПараметрыЗаписи[0] = Измерения;
	КлючРегистра = Новый(Тип("РегистрСведенийКлючЗаписи.РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий"), ПараметрыЗаписи);
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", КлючРегистра);
	ОткрытьФорму(
		"РегистрСведений.РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий.ФормаЗаписи",
		ПараметрыФормы,
		,
		,
		,
		,
		,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

#Область ФиксацияВторичныхДанныхВДокументах

&НаКлиенте
Процедура ОбновитьВторичныеДанные(Команда)
	ПрочитатьОбновитьВторичныеДанныеНаСервере();
	Надпись = Элементы.Найти("ПредупреждениеОбновленияВторичныхДанных");
	Если Надпись <> Неопределено И Надпись.Видимость Тогда
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВсеИсправления(Команда) 
	ОтменитьВсеИсправленияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьИзмененияСпособаВыплатыПособия(Команда)
	ОтменитьИзмененияСпособаВыплатыПособияНаСервере();
КонецПроцедуры

#КонецОбласти

#Область Сотрудники

&НаКлиенте
Процедура Подключаемый_ПояснениеНажатие(Элемент, СтандартнаяОбработка = Ложь)
	СотрудникиКлиент.ПояснениеНажатие(Элемент, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ИзменитьАдресПроживания(Команда)
	Если ЗначениеЗаполнено(Объект.ФизическоеЛицо) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", Объект.ФизическоеЛицо);
		ПараметрыФормы.Вставить("ВводАдреса", Истина);
		ОткрытьФорму("Справочник.ФизическиеЛица.ФормаОбъекта", ПараметрыФормы);
	Иначе
		Подключаемый_ПолеВводаКонтактнойИнформацииНачалоВыбора(Элементы.АдресПроживанияПредставление, Неопределено, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСпособВыплатыПособия(Команда)
	УчетПособийСоциальногоСтрахованияКлиент.НастроитьСпособПрямыхВыплатФизическогоЛица(
		Объект.Организация,
		Объект.ФизическоеЛицо,
		ЭтотОбъект,
		Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьПроверкиПроведения(Команда)
	Объект.ОтключитьПроверкиПроведения = Не Объект.ОтключитьПроверкиПроведения;
	ОбновитьЭлементыФормы();
КонецПроцедуры

&НаКлиенте
Процедура ВопросВПоддержку(Команда)
	СЭДОФССКлиент.ОткрытьВопросВПоддержку(ПодготовитьВопросВПоддержку());
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Если Записать() Тогда
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВключитьВозможностьРедактирования(Команда)
	ВключитьВозможностьРедактированияНаСервере();
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область Свойства

// СтандартныеПодсистемы.Свойства
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область ФиксацияВторичныхДанныхВДокументах

&НаСервере
Функция ПараметрыФиксацииВторичныхДанных() Экспорт
	ПараметрыФиксации = Документы.ОтветНаЗапросРеквизитовДляВыплатыПособия.ПараметрыФиксацииВторичныхДанных(Объект);
	ПараметрыФиксации.Вставить("ОписаниеФормы", ФиксацияВторичныхДанныхВДокументахФормы.ОписаниеФормы());
	
	ОписаниеЭлементовФормы = Новый Соответствие;
	ОписаниеПутиКРеквизитамОбъекта = ФиксацияВторичныхДанныхВДокументахФормы.ОписаниеЭлементаФормы();
	ОписаниеПутиКРеквизитамОбъекта.ПрефиксПути = "Объект";
	Для Каждого ОписаниеФиксацииРеквизита Из ПараметрыФиксации.ОписаниеФиксацииРеквизитов Цикл
		ОписаниеЭлементовФормы.Вставить(ОписаниеФиксацииРеквизита.Ключ, ОписаниеПутиКРеквизитамОбъекта);
	КонецЦикла;
	ОписаниеЭлементовФормы.Вставить("ИнаяОрганизацияБанк", ОписаниеПутиКРеквизитамОбъекта);
	
	// Адреса редактируется через реквизит формы, поэтому для них указываются пустой префикс пути к объекту.
	ОписаниеПутиКРеквизитамФормы = ФиксацияВторичныхДанныхВДокументахФормы.ОписаниеЭлементаФормы();
	ИнициализироватьПоляВводаКонтактнойИнформации();
	Для Каждого КлючИЗначение Из ПоляВводаКонтактнойИнформации Цикл
		ОписаниеЭлементовФормы.Вставить(КлючИЗначение.Ключ, ОписаниеПутиКРеквизитамФормы);
	КонецЦикла;
	
	ПараметрыФиксации.ОписаниеФормы.ОписаниеЭлементовФормы = ОписаниеЭлементовФормы;
	Возврат ПараметрыФиксации;
КонецФункции

&НаСервере
Функция ФиксацияБыстрыйПоискРеквизитов()
	БыстрыйПоискРеквизитов = Новый Соответствие; // Ключ - имя элемента, значение - имя реквизита.
	ПараметрыФиксации = ЭтотОбъект["ПараметрыФиксацииВторичныхДанных"];
	Для Каждого КлючИЗначение Из ПараметрыФиксации.ОписаниеФиксацииРеквизитов Цикл
		ИмяРеквизита = КлючИЗначение.Значение.ИмяРеквизита;
		// Поиск элементов по имени.
		Если Элементы.Найти(ИмяРеквизита) <> Неопределено Тогда
			БыстрыйПоискРеквизитов.Вставить(ИмяРеквизита, ИмяРеквизита);
		КонецЕсли;
	КонецЦикла;
	БыстрыйПоискРеквизитов.Вставить("ИнаяОрганизацияБанк", "ИнаяОрганизацияБИК");
	Возврат БыстрыйПоискРеквизитов;
КонецФункции

&НаСервере
Процедура ФиксацияСброситьФиксациюИзмененийПоОснованиюЗаполнения(ОснованиеЗаполнения, ТекущаяСтрока = 0)
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.СброситьФиксациюИзмененийРеквизитовПоОснованиюЗаполнения(ЭтотОбъект, ОснованиеЗаполнения, ТекущаяСтрока);
КонецПроцедуры

&НаСервере
Процедура ФиксацияЗаполнитьРеквизитыОбъекта(Знач ИмяСобытия = "")
	Если ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбъектФормыЗафиксирован(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ЗаполнитьИдентификаторыСтрок(ЭтотОбъект);
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.СохранитьРеквизитыФормыФикс(ЭтотОбъект, ДокументОбъект);
	
	ЕстьИзменения = Ложь;
	Если ИмяСобытия = "ОтменитьВсеИсправления" И ДокументОбъект.ФиксацияИзменений.Количество() > 0 Тогда
		ДокументОбъект.ФиксацияИзменений.Очистить();
		ЕстьИзменения = Истина;
	КонецЕсли;
	Если ДокументОбъект.ОбновитьВторичныеДанные(ЭтотОбъект.ПараметрыФиксацииВторичныхДанных) Тогда
		ЕстьИзменения = Истина;
	КонецЕсли;
	
	Если ЕстьИзменения Тогда
		Если ТолькоПросмотр Или Не ПравоДоступа("Изменение", ДокументОбъект.Метаданные()) Тогда
			ФиксацияВторичныхДанныхВДокументахФормы.ВывестиПредупреждениеОНаличииИзмененийВИсходныхДанныхКоторыеНельзяПрименить(
				ЭтотОбъект);
		Иначе
			Если Не ДокументОбъект.ЭтоНовый() Тогда
				ФиксацияВторичныхДанныхВДокументахФормы.УстановитьМодифицированность(ЭтотОбъект, Истина);
			КонецЕсли;
			ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
			ФиксацияВторичныхДанныхВДокументахФормы.ЗаполнитьРеквизитыФормыФикс(ЭтотОбъект, Объект);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗафиксироватьИзменениеРеквизитаВФорме(Элемент, СтандартнаяОбработка = Ложь) Экспорт
	Подключаемый_ЗафиксироватьИзменениеРеквизитаНаСервере(Элемент.Имя);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ЗафиксироватьИзменениеРеквизитаНаСервере(ИмяЭлемента, ОбновлятьВторичныеДанные = Ложь, СброситьФиксациюДочерних = Ложь)
	Элемент = Элементы[ИмяЭлемента];
	Если Элемент = Элементы.ОтказВНазначенииПособия Тогда
		Объект.ОтветБудетВДругомДокументе = Ложь;
		Объект.ОтветНеТребуется = Ложь;
	ИначеЕсли Элемент = Элементы.ОтветБудетВДругомДокументе Тогда
		Объект.ОтказВНазначенииПособия = Ложь;
		Объект.ОтветНеТребуется = Ложь;
	ИначеЕсли Элемент = Элементы.ОтветНеТребуется Тогда
		Объект.ОтказВНазначенииПособия = Ложь;
		Объект.ОтветБудетВДругомДокументе = Ложь;
	КонецЕсли;
	ОписаниеЭлементов = ФиксацияБыстрыйПоискРеквизитов();
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.Подключаемый_ЗафиксироватьИзменениеРеквизитаВФорме(ЭтотОбъект, Элемент, ОписаниеЭлементов);
	Если ОбновлятьВторичныеДанные Тогда
		Если СброситьФиксациюДочерних Тогда
			ИмяРеквизита = ОписаниеЭлементов.Получить(Элемент.Имя);
			ФиксацияСброситьФиксациюИзмененийПоОснованиюЗаполнения(ИмяРеквизита);
		КонецЕсли;
		ОбновитьВторичныеДанныеНаСервере();
	Иначе
		ОбновитьЭлементыФормы();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ОбъектЗафиксирован() Экспорт
	ЗаполнитьФлажокДокументЗафиксирован();
	Возврат ДокументЗафиксирован;
КонецФункции

&НаСервере
Процедура ЗаполнитьФлажокДокументЗафиксирован()
	ДокументЗафиксирован = Документы.ОтветНаЗапросРеквизитовДляВыплатыПособия.ОбъектЗафиксирован(Объект);
КонецПроцедуры

#КонецОбласти

#Область ПодписиДокументов

// ПодписиДокументов
&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементПриИзменении(Элемент)
	ПодписиДокументовКлиент.ПриИзмененииПодписывающегоЛица(ЭтотОбъект, Элемент.Имя);
	ПредставительПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементНажатие(Элемент)
	ПодписиДокументовКлиент.РасширеннаяПодсказкаНажатие(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры
// Конец ПодписиДокументов

#КонецОбласти

#Область ПроцессыОбработкиДокументов

// ПроцессыОбработкиДокументов
&НаКлиенте
Процедура Подключаемый_ВыполнитьЗадачуПоОбработкеДокумента(Команда)
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.ВыполнитьЗадачу(ЭтотОбъект, Команда, Объект)
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьЗадачуПоОбработкеДокументаОповещение(Контекст, ДополнительныеПараметры) Экспорт
	ВыполнитьЗадачуПоОбработкеДокументаНаСервере(Контекст);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры, Контекст);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗадачуПоОбработкеДокументаНаСервере(Контекст)
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ВыполнитьЗадачу(ЭтотОбъект, Контекст, Объект);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомментарийНаправившегоОткрытие(Элемент, СтандартнаяОбработка)
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.КомментарийНаправившегоОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомментарийСледующемуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.КомментарийСледующемуНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры
// Конец ПроцессыОбработкиДокументов

#КонецОбласти

#Область КонтактнаяИнформация

&НаСервере
Процедура ОбновитьЭлементыКонтактнойИнформации()
	ИнициализироватьПоляВводаКонтактнойИнформации();
	Для Каждого КлючИЗначение Из ПоляВводаКонтактнойИнформации Цикл
		ИмяРеквизитаФормы = КлючИЗначение.Ключ;
		Если ИмяРеквизитаФормы = "БыстрыйПоискРеквизитовФормы" Тогда
			Продолжить;
		КонецЕсли;
		Реквизит          = КлючИЗначение.Значение;
		Если Реквизит.Тип = Перечисления.ТипыКонтактнойИнформации.Адрес Тогда
			КонтактнаяИнформацияБЗК.ОбновитьПолеВводаАдреса(ЭтотОбъект, ИмяРеквизитаФормы, Объект[Реквизит.Имя]);
		ИначеЕсли Реквизит.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
			КонтактнаяИнформацияБЗК.ОбновитьПолеВводаТелефона(ЭтотОбъект, ИмяРеквизитаФормы, Объект[Реквизит.Имя]);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьПоляВводаКонтактнойИнформации()
	Если ПоляВводаКонтактнойИнформации <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	// ПоляВводаКонтактнойИнформации - Структура
	//   * Ключ     - Строка    - Имя реквизита формы.
	//   * Значение - Структура - Свойства реквизита объекта.
	//       * Имя - Строка - Имя реквизита объекта хранящего значение контактной информации.
	//       * Тип - ПеречислениеСсылка.ТипыКонтактнойИнформации - Тип контактной информации.
	//       * Вид - Строка - Имя предопределенного вида контактной информации.
	ПоляВводаКонтактнойИнформации = Новый Структура;
	БыстрыйПоискРеквизитовФормы = Новый Структура;
	
	Реквизит = ОписаниеРеквизитаКонтактнойИнформации();
	Реквизит.Имя = "АдресПроживания";
	Реквизит.Тип = Перечисления.ТипыКонтактнойИнформации.Адрес;
	Реквизит.Вид = "Справочник.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица";
	ПоляВводаКонтактнойИнформации.Вставить(Реквизит.Имя + "Представление", Реквизит);
	БыстрыйПоискРеквизитовФормы.Вставить(Реквизит.Имя, Реквизит.Имя + "Представление");
	
	ПоляВводаКонтактнойИнформации.Вставить("БыстрыйПоискРеквизитовФормы", БыстрыйПоискРеквизитовФормы);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеРеквизитаКонтактнойИнформации()
	Возврат Новый Структура("Имя, Тип, Вид");
КонецФункции

&НаСервере
Процедура АдаптироватьСообщениеПользователюКПолямВводаКонтактнойИнформации(Сообщение)
	Если ЗначениеЗаполнено(Сообщение.Поле)
		И СтроковыеФункцииКлиентСервер.ТолькоКириллицаВСтроке(Сообщение.Поле)
		И ПоляВводаКонтактнойИнформации.БыстрыйПоискРеквизитовФормы.Свойство(Сообщение.Поле) Тогда
		Сообщение.Поле = ПоляВводаКонтактнойИнформации.БыстрыйПоискРеквизитовФормы[Сообщение.Поле];
		Сообщение.ПутьКДанным = "";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеВводаКонтактнойИнформацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Реквизит = ПоляВводаКонтактнойИнформации[Элемент.Имя];
	Оповещение = Новый ОписаниеОповещения("ПолеВводаКонтактнойИнформацииЗавершениеВыбора", ЭтотОбъект, Реквизит);
	ПараметрыОткрытия = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
		ПредопределенноеЗначение(Реквизит.Вид),
		Объект[Реквизит.Имя]);
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, ЭтотОбъект, Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ПолеВводаКонтактнойИнформацииЗавершениеВыбора(РезультатВыбора, Реквизит) Экспорт
	Если ТипЗнч(РезультатВыбора) = Тип("Структура")Тогда
		Объект[Реквизит.Имя] = РезультатВыбора.Значение;
		ФиксацияВторичныхДанныхВДокументахКлиентСервер.ЗафиксироватьИзменениеРеквизита(ЭтотОбъект, Реквизит.Имя);
		ФиксацияЗаполнитьРеквизитыОбъекта();
		ОбновитьЭлементыФормы();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПредставлениеАдреса(Префикс)
	ЗаПределамиРФ = ОбщегоНазначенияБЗК.ЗначениеСвойства(Объект, Префикс + "ЗаПределамиРФ");
	Если ЗначениеЗаполнено(ЗаПределамиРФ) И Элементы[Префикс + "ЗаПределамиРФ"].Видимость Тогда
		Возврат ЗаПределамиРФ;
	КонецЕсли;
	
	// Получение представления из JSON.
	ЗначениеАдреса = Объект[Префикс];
	Если ЗначениеЗаполнено(ЗначениеАдреса) Тогда
		Представление = СЭДОФСС.КраткоеПредставлениеАдреса(ЗначениеАдреса);
		Если ЗначениеЗаполнено(Представление) Тогда
			Возврат Представление;
		КонецЕсли;
	КонецЕсли;
	
	// Поиск адреса по идентификаторам, если они заполнены.
	ФИАСДом  = Объект[Префикс + "ФИАСДом"];
	ФИАСРО   = Объект[Префикс + "ФИАСРО"];
	Квартира = Объект[Префикс + "Квартира"];
	
	// Поиск адреса по идентификатору дома.
	Если ЗначениеЗаполнено(ФИАСДом) Тогда
		Попытка
			ПараметрыПолученияАдреса = Новый Структура;
			ПараметрыПолученияАдреса.Вставить("АдресВJSON", Истина);
			ЗначениеАдреса = РаботаСАдресами.АдресПоИдентификатору(ФИАСДом, ПараметрыПолученияАдреса);
		Исключение
			ЗначениеАдреса = "";
			Ошибка = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Текст = НСтр("ru = 'Ошибка при поиске адреса %1 по идентификатору %2: %3';
						|en = 'An error occurred when searching %1 address by %2 ID: %3'");
			Текст = СтрШаблон(Текст, Префикс, ФИАСДом, Ошибка);
			СообщенияБЗК.СообщитьОПроблеме(Текст);
		КонецПопытки;
		Если ЗначениеЗаполнено(ЗначениеАдреса) Тогда
			Представление = СЭДОФСС.КраткоеПредставлениеАдреса(ЗначениеАдреса);
			Если ЗначениеЗаполнено(Представление) И ЗначениеЗаполнено(Квартира) Тогда
				Представление = Представление + ", " + Квартира;
			КонецЕсли;
			Если ЗначениеЗаполнено(Представление) Тогда
				Возврат Представление;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Поиск адреса по идентификатору родительского объекта.
	Если ЗначениеЗаполнено(ФИАСРО) Тогда
		Попытка
			ПараметрыПолученияАдреса = Новый Структура;
			ПараметрыПолученияАдреса.Вставить("АдресВJSON", Истина);
			ЗначениеАдреса = РаботаСАдресами.АдресПоИдентификатору(ФИАСРО, ПараметрыПолученияАдреса);
		Исключение
			ЗначениеАдреса = "";
			Ошибка = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Текст = НСтр("ru = 'Ошибка при поиске адреса %1 по идентификатору %2: %3';
						|en = 'An error occurred when searching %1 address by %2 ID: %3'");
			Текст = СтрШаблон(Текст, Префикс, ФИАСРО, Ошибка);
			СообщенияБЗК.СообщитьОПроблеме(Текст);
		КонецПопытки;
		Если ЗначениеЗаполнено(ЗначениеАдреса) Тогда
			Представление = СЭДОФСС.КраткоеПредставлениеАдреса(ЗначениеАдреса);
			Если ЗначениеЗаполнено(Представление) Тогда
				Дом      = Объект[Префикс + "Дом"];
				Корпус   = Объект[Префикс + "Корпус"];
				Строение = ОбщегоНазначенияБЗК.ЗначениеСвойства(Объект, Префикс + "Строение");
				Если ЗначениеЗаполнено(Дом) Тогда
					Представление = Представление + ", " + Дом;
				КонецЕсли;
				Если ЗначениеЗаполнено(Корпус) Тогда
					Представление = Представление + ", " + Корпус;
				КонецЕсли;
				Если ЗначениеЗаполнено(Строение) Тогда
					Представление = Представление + ", " + Строение;
				КонецЕсли;
				Если ЗначениеЗаполнено(Квартира) Тогда
					Представление = Представление + ", " + Квартира;
				КонецЕсли;
				Возврат Представление;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат "";
КонецФункции

#КонецОбласти

#Область СвертываемыеГруппы

&НаСервере
Процедура ПриОпределенииСвертываемыхГрупп(Группы) Экспорт
	Группы.Добавить(Элементы.СотрудникГруппа);
	Группы.Добавить(Элементы.СпособВыплатыГруппа);
	Группы.Добавить(Элементы.СтраховательГруппа);
	Группы.Добавить(Элементы.ПредставительГруппа);
КонецПроцедуры

&НаСервере
Процедура ПослеДобавленияЭлементовСвертываемыхГрупп() Экспорт
	ГруппаСпособовВыплаты = Элементы.СпособВыплатыГруппа.Родитель;
	Элементы.Переместить(Элементы.КнопкиСвертываемойГруппыСпособВыплаты, ГруппаСпособовВыплаты.ПодчиненныеЭлементы[0]);
КонецПроцедуры

&НаСервере
Процедура ПриОпределенииСвойствСвертываемойГруппы(Группа, Представление, Заполнена, Развернута, Видимость) Экспорт
	
	Если Группа = Элементы.СотрудникГруппа Тогда
		ОбновитьЭлементыГруппыСотрудник(Группа, Представление, Заполнена, Развернута, Видимость);
		
	ИначеЕсли Группа = Элементы.СпособВыплатыГруппа Тогда
		ОбновитьЭлементыГруппыСпособВыплаты(Группа, Представление, Заполнена, Развернута, Видимость);
		
	ИначеЕсли Группа = Элементы.СтраховательГруппа Тогда
		ОбновитьЭлементыГруппыСтрахователь(Группа, Представление, Заполнена, Развернута, Видимость);
		
	ИначеЕсли Группа = Элементы.ПредставительГруппа Тогда
		ОбновитьЭлементыГруппыПредставитель(Группа, Представление, Заполнена, Развернута, Видимость);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СвернутьРазвернутьГруппу(Элемент)
	СвернутьРазвернутьГруппу(Элемент.Имя);
КонецПроцедуры

&НаСервере
Процедура СвернутьРазвернутьГруппу(ИмяЭлемента)
	СЭДОФСС.СвернутьРазвернутьГруппу(ЭтотОбъект, ИмяЭлемента);
КонецПроцедуры

#КонецОбласти

#Область Форма

&НаСервере
Процедура ПриПолученииДанныхНаСервере(ТекущийОбъект = "Объект")
	ПравоИзменения   = (Элементы.Найти("ФормаПровести") <> Неопределено);
	ПравоПолученияЭД = СЭДОФСС.ЕстьПравоПолучения();
	ПравоОтправкиЭД  = СЭДОФСС.ЕстьПравоОтправки();
	
	Права = СЭДОФСС.ПраваНаФактическиеНачисленияВДокументе(Объект);
	ПравоПросмотраФактическихНачислений = Права.Просмотр;
	ПравоИзмененияФактическихНачислений = Права.Редактирование;
	
	ПервичнаяИнициализицияФормы = (ПараметрыСвертываемыхГрупп = Неопределено);
	Если ПервичнаяИнициализицияФормы Тогда
		СЭДОФСС.СписокВыбораСпособовВыплаты(Элементы.СпособВыплатыПособия.СписокВыбора, Объект.СпособВыплатыПособия);
		// ЗарплатаКадрыПодсистемы.ПодписиДокументов
		ПодписиДокументов.ПриСозданииНаСервере(ЭтотОбъект, Документы.ОтветНаЗапросРеквизитовДляВыплатыПособия.ОписаниеПодписейДокумента());
		// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	Иначе
		Документ = ?(ТекущийОбъект = "Объект", Объект, ТекущийОбъект);
		ФиксацияВторичныхДанныхВДокументахКлиентСервер.СохранитьРеквизитыФормыФикс(ЭтотОбъект, Документ);
	КонецЕсли;
	
	ЗаполнитьИнаяОрганизацияБанк();
	
	ФиксацияВторичныхДанныхВДокументахФормы.ИнициализироватьМеханизмФиксацииРеквизитов(
		ЭтотОбъект,
		ТекущийОбъект,
		ПараметрыФиксацииВторичныхДанных());
	ФиксацияВторичныхДанныхВДокументахФормы.ПодключитьОбработчикиФиксацииИзмененийРеквизитов(
		ЭтотОбъект,
		ФиксацияБыстрыйПоискРеквизитов());
	
	ФиксацияЗаполнитьРеквизитыОбъекта();
	
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбновитьФорму(ЭтотОбъект);
	
	УстановитьФункциональныеОпцииФормы();
	
	ОбновитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьОбработчикОжиданияПрочитатьОбновитьВторичныеДанные()
	ОтключитьОбработчикОжидания("ПрочитатьОбновитьВторичныеДанные");
	ПодключитьОбработчикОжидания("ПрочитатьОбновитьВторичныеДанные", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьОбновитьВторичныеДанные()
	ПрочитатьОбновитьВторичныеДанныеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПрочитатьОбновитьВторичныеДанныеНаСервере()
	Если Не ДокументЗафиксирован И (Модифицированность Или Объект.Ссылка.Пустая()) Тогда
		ОбновитьВторичныеДанныеНаСервере();
	Иначе
		Прочитать();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьВторичныеДанныеНаСервере(Знач ИмяСобытия = "")
	ФиксацияЗаполнитьРеквизитыОбъекта(ИмяСобытия);
	ОбновитьЭлементыФормы();
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбновитьФорму(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ОтменитьВсеИсправленияНаСервере()
	Если ТолькоПросмотр Или Не ПравоДоступа("Изменение", Метаданные.Документы.ОтветНаЗапросРеквизитовДляВыплатыПособия) Тогда
		ФиксацияВторичныхДанныхВДокументахФормы.ВывестиПредупреждениеОНаличииИзмененийВИсходныхДанныхКоторыеНельзяПрименить(
			ЭтотОбъект);
		Возврат;
	КонецЕсли;
	Модифицированность = Истина;
	ОбновитьВторичныеДанныеНаСервере("ОтменитьВсеИсправления");
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыФормы()
	
	ЗаполнитьФлажокДокументЗафиксирован();
	ВыгружатьРеквизитыДляВыплатыПособия = Не Объект.ОтветБудетВДругомДокументе
		И Не Объект.ОтказВНазначенииПособия И Не Объект.ОтветНеТребуется;
	
	ТолькоПросмотр = ДокументЗафиксирован;
	
	ОбновитьКнопкиФормы();
	
	ОбновитьПанельСостоянияДокумента();
	
	ОбновитьЭлементыКонтактнойИнформации();
	
	СЭДОФСС.ОбновитьСвертываемыеГруппы(ЭтотОбъект);
	
	ОбновитьПрочиеЭлементыФормы();
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументовКлиентСервер.УстановитьПредставлениеПодписей(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКнопкиФормы()
	
	Элементы.ФормаОбновитьВторичныеДанные.Видимость = Не ДокументЗафиксирован;
	Элементы.ФормаОтменитьВсеИсправления.Видимость  = Не ДокументЗафиксирован;
	
	Если ПравоОтправкиЭД Тогда
		ДокументОтправлен = Не Объект.ПометкаУдаления И ЗначениеЗаполнено(Объект.ДатаОтправки);
		Элементы.ОтправитьВФонд.ТолькоВоВсехДействиях = ДокументОтправлен;
	КонецЕсли;
	
	Элементы.ФормаОтключитьПроверкиПроведения.Пометка = Объект.ОтключитьПроверкиПроведения;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПанельСостоянияДокумента()
	ПоложениеКнопкиЗакрыть     = ПоложениеКнопкиВКоманднойПанели.ВДополнительномПодменю;
	ПоложениеКнопкиСкопировать = ПоложениеКнопкиВКоманднойПанели.ВДополнительномПодменю;
	ВидимостьКнопкиПроверить   = Ложь;
	КнопкаПоУмолчанию          = Неопределено;
	Если Объект.ПометкаУдаления Тогда
		ТекущаяСтраница = Элементы.СтраницаДокументПомеченНаУдаление;
		Если ПравоИзменения Тогда
			КнопкаПоУмолчанию = Элементы.ФормаЗаписать;
		КонецЕсли;
	ИначеЕсли Не Объект.Проведен Тогда
		ТекущаяСтраница = Элементы.СтраницаДокументНеПроведен;
		Если ПравоИзменения Тогда
			КнопкаПоУмолчанию = Элементы.ФормаПровести;
		КонецЕсли;
	ИначеЕсли Не ЗначениеЗаполнено(Объект.ДатаОтправки) Тогда
		ТекущаяСтраница = Элементы.СтраницаДокументПроведенИНеОтправлен;
		Если ПравоОтправкиЭД Тогда
			КнопкаПоУмолчанию = Элементы.ОтправитьВФонд;
		КонецЕсли;
	Иначе
		СостояниеОтправки = СостояниеОтправки();
		Если СостояниеОтправки.ЕстьОшибки Тогда
			ТекущаяСтраница = Элементы.СтраницаДокументПроведенИЕстьОшибки;
			Элементы.НадписьДокументПроведенИЕстьОшибки.Заголовок = СостояниеОтправки.ЗаголовокНадписи;
			ПоложениеКнопкиСкопировать = ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанелиИВДополнительномПодменю;
			Если ПравоИзменения Тогда
				КнопкаПоУмолчанию = Элементы.КнопкаСкопировать;
			КонецЕсли;
		ИначеЕсли СостояниеОтправки.ЕстьПредупреждения Тогда
			ТекущаяСтраница = Элементы.СтраницаДокументПроведенИЕстьПредупреждения;
			Элементы.НадписьДокументПроведенИЕстьПредупреждения.Заголовок = СостояниеОтправки.ЗаголовокНадписи;
			ПоложениеКнопкиСкопировать = ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанелиИВДополнительномПодменю;
			Если ПравоИзменения Тогда
				КнопкаПоУмолчанию = Элементы.КнопкаСкопировать;
			КонецЕсли;
		Иначе
			ТекущаяСтраница = Элементы.СтраницаДокументПроведенИОтправлен;
			Элементы.НадписьДокументПроведенИОтправлен.Заголовок = СостояниеОтправки.ЗаголовокНадписи;
			ПоложениеКнопкиЗакрыть = ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанелиИВДополнительномПодменю;
			ВидимостьКнопкиПроверить = Истина;
			КнопкаПоУмолчанию = Элементы.ФормаЗакрыть;
		КонецЕсли;
	КонецЕсли;
	Если КнопкаПоУмолчанию <> Неопределено Тогда
		КнопкаПоУмолчанию.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	Элементы.СтраницыСостоянияДокумента.ТекущаяСтраница = ТекущаяСтраница;
	Если ПравоИзменения Тогда
		Элементы.КнопкаСкопировать.ПоложениеВКоманднойПанели = ПоложениеКнопкиСкопировать;
	КонецЕсли;
	Элементы.ФормаЗакрыть.ПоложениеВКоманднойПанели = ПоложениеКнопкиЗакрыть;
	Если ПравоПолученияЭД Тогда
		Элементы.ПроверитьНаличиеОтвета.Видимость = ВидимостьКнопкиПроверить;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция СостояниеОтправки()
	РегистрСведенийМенеджерЗаписи = РеквизитФормыВЗначение("РегистрацияСведений");
	РегистрСведенийМенеджерЗаписи.ИсходящийДокумент = Объект.Ссылка;
	РегистрСведенийМенеджерЗаписи.Прочитать();
	ЗначениеВРеквизитФормы(РегистрСведенийМенеджерЗаписи, "РегистрацияСведений");
	
	Состояние          = РегистрацияСведений.Состояние;
	ВариантыСостояний  = Перечисления.СостоянияДокументаСЭДОФСС;
	ЕстьОшибки         = Ложь;
	ЕстьПредупреждения = Ложь;
	Если Состояние = ВариантыСостояний.ПодготовленКОтправке Тогда
		Представление = НСтр("ru = 'Проведен и <a href=""1"">готов к отправке</a>';
							|en = 'Posted and <a href=""1"">ready for sending</a>'");
	ИначеЕсли Состояние = ВариантыСостояний.ОшибкаПриОтправке Тогда
		Представление = НСтр("ru = 'При отправке <a href=""1"">возникла ошибка</a>';
							|en = 'An <a href=""1"">error occurred</a> when sending'");
	ИначеЕсли Состояние = ВариантыСостояний.ОтправленОператору Тогда
		Представление = НСтр("ru = '<a href=""1"">Отправлен оператору</a>';
							|en = '<a href=""1"">Sent to provider</a>'");
	ИначеЕсли Состояние = ВариантыСостояний.Отправлен Тогда
		Представление = НСтр("ru = '<a href=""1"">Отправлен в Фонд</a>';
							|en = '<a href=""1"">Sent to SSF</a>'");
	ИначеЕсли Состояние = ВариантыСостояний.ОшибкаЛогическогоКонтроля Тогда
		ЕстьОшибки = Истина;
		Представление = НСтр("ru = 'Не принят, см. <a href=""1"">ошибки логического контроля</a>';
							|en = 'Not accepted, see <a href=""1"">logical control errors</a>'");
	ИначеЕсли Состояние = ВариантыСостояний.Принят Тогда
		Представление = НСтр("ru = '<a href=""1"">Успешно принят СФР</a>';
							|en = '<a href=""1"">Accepted by the Social Insurance Fund of Russia</a>'");
	ИначеЕсли Состояние = ВариантыСостояний.НеПринят Тогда
		ЕстьОшибки = Истина;
		Представление = НСтр("ru = 'Документ не принят, см. <a href=""1"">протокол</a>';
							|en = 'The document is not accepted, see <a href=""1"">the log</a>'");
	Иначе
		СостояниеСтрокой = Строка(Состояние);
		СостояниеСтрокой = НРег(Лев(СостояниеСтрокой, 1)) + Сред(СостояниеСтрокой, 2);
		Представление = СтрШаблон(НСтр("ru = 'Проведен и <a href=""1"">%1</a>';
										|en = 'Posted and <a href=""1"">%1</a>'"), СостояниеСтрокой);
	КонецЕсли;
	
	Результат = Новый Структура("Состояние, ЗаголовокНадписи, ЕстьОшибки, ЕстьПредупреждения");
	Результат.Состояние          = Состояние;
	Результат.ЗаголовокНадписи   = СтроковыеФункции.ФорматированнаяСтрока(Представление);
	Результат.ЕстьОшибки         = ЕстьОшибки;
	Результат.ЕстьПредупреждения = ЕстьПредупреждения;
	
	Возврат Результат;
КонецФункции

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	УстановитьПривилегированныйРежим(Истина);
	
	УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация", Объект.Организация));
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьСообщенияПользователю()
	Сообщения = ПолучитьСообщенияПользователю(Истина);
	Если Объект.ОтключитьПроверкиПроведения Тогда
		Возврат;
	КонецЕсли;
	Для Каждого Сообщение Из Сообщения Цикл
		// Привязка сообщений к объекту.
		Если Не ЗначениеЗаполнено(Сообщение.КлючДанных)
			И Не ЗначениеЗаполнено(Сообщение.ПутьКДанным)
			И Не СтрНачинаетсяС(Сообщение.Поле, "Объект.") Тогда
			Сообщение.ПутьКДанным = "Объект";
		КонецЕсли;
		// Адаптация сообщений к особенностям формы.
		Если ЗначениеЗаполнено(Сообщение.Поле) Тогда
			АдаптироватьСообщениеПользователюКПолямВводаКонтактнойИнформации(Сообщение);
			СЭДОФСС.СвертываемыеГруппыАдаптироватьСообщениеПользователю(ЭтотОбъект, Сообщение);
		КонецЕсли;
		Сообщение.Сообщить();
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ОбновитьПрочиеЭлементыФормы()
	Элементы.АдресПроживанияЗаПределамиРФ.Видимость = ЗначениеЗаполнено(Объект.АдресПроживанияЗаПределамиРФ)
		Или (Объект.КодСтраныГражданства <> "643" И Не ЗначениеЗаполнено(Объект.АдресПроживания));
	Элементы.СотрудникСНИЛСВШапке.Видимость = Не ВыгружатьРеквизитыДляВыплатыПособия;
КонецПроцедуры

#КонецОбласти

#Область Организация

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Объект.Организация);
	Иначе
		Объект.ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Объект.Страхователь);
	КонецЕсли;
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ЗаполнитьПодписиПоОрганизации(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ФиксацияСброситьФиксациюИзмененийПоОснованиюЗаполнения("Организация");
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ЗафиксироватьИзменениеРеквизита(ЭтотОбъект, "Организация");
	УстановитьФункциональныеОпцииФормы();
	ФиксацияЗаполнитьРеквизитыОбъекта();
	ОбновитьЭлементыФормы();
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбновитьФорму(ЭтотОбъект);
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыГруппыСтрахователь(Группа, Представление, Заполнена, Развернута, Видимость)
	ЕстьСтрахователь = ЗначениеЗаполнено(Объект.Страхователь);
	ЕстьРегномер = ЗначениеЗаполнено(Объект.РегистрационныйНомерСФР);
	Заполнена = ЕстьСтрахователь Или ЕстьРегномер;
	Если Развернута Тогда
		// Видимостью элементов группы управлять не требуется.
	Иначе
		Представления = Новый Массив;
		Если ЕстьСтрахователь Тогда
			Представления.Добавить(Строка(Объект.Страхователь));
		КонецЕсли;
		Если ЕстьРегномер Тогда
			Представления.Добавить(НСтр("ru = 'рег. номер';
										|en = 'reg. number'") + " " + Объект.РегистрационныйНомерСФР);
		КонецЕсли;
		Представление = СтрСоединить(Представления, ", ");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ВходящийЗапрос

&НаСервере
Процедура ВходящийЗапросПриИзмененииНаСервере()
	ФиксацияСброситьФиксациюИзмененийПоОснованиюЗаполнения("*");
	Если ЗначениеЗаполнено(Объект.ВходящийЗапрос) Тогда
		ДокуменОбъект = РеквизитФормыВЗначение("Объект");
		ДокуменОбъект.Заполнить(Объект.ВходящийЗапрос);
		ЗначениеВРеквизитФормы(ДокуменОбъект, "Объект");
	КонецЕсли;
	ПриПолученииДанныхНаСервере("Объект");
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область Сотрудник

&НаСервере
Процедура ОбновитьЭлементыГруппыСотрудник(Группа, Представление, Заполнена, Развернута, Видимость)
	Видимость = ВыгружатьРеквизитыДляВыплатыПособия;
	Если Не Видимость Тогда
		Возврат;
	КонецЕсли;
	ЕстьФИО = ЗначениеЗаполнено(Объект.СотрудникФамилия);
	ЕстьСНИЛС = ЗначениеЗаполнено(Объект.СотрудникСНИЛС);
	ЕстьДатаРождения = ЗначениеЗаполнено(Объект.СотрудникДатаРождения);
	Заполнена = ЕстьФИО Или ЕстьСНИЛС Или ЕстьДатаРождения;
	Если Развернута Тогда
		// Видимостью элементов группы управлять не требуется.
	Иначе
		Представления = Новый Массив;
		Если ЕстьФИО Тогда
			Представления.Добавить(СокрЛП(Объект.СотрудникФамилия +" "+ Объект.СотрудникИмя +" "+ Объект.СотрудникОтчество));
		КонецЕсли;
		Если ЕстьСНИЛС Тогда
			Представления.Добавить(СтрШаблон(НСтр("ru = 'СНИЛС %1';
													|en = 'SNILS: %1'"), Объект.СотрудникСНИЛС));
		КонецЕсли;
		Если ЕстьДатаРождения Тогда
			Представления.Добавить(НСтр("ru = 'Дата рождения';
										|en = 'Date of birth'") + " " + Формат(Объект.СотрудникДатаРождения, "ДЛФ=D"));
		КонецЕсли;
		Представление = СтрСоединить(Представления, ", ");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СотрудникПриИзмененииНаСервере()
	Если ЗначениеЗаполнено(Объект.Сотрудник) Тогда
		Объект.ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Сотрудник, "ФизическоеЛицо");
	Иначе
		Объект.ФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
	КонецЕсли;
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ЗафиксироватьИзменениеРеквизита(ЭтотОбъект, "Сотрудник");
	ФиксацияСброситьФиксациюИзмененийПоОснованиюЗаполнения("Сотрудник");
	ФиксацияСброситьФиксациюИзмененийПоОснованиюЗаполнения("ФизическоеЛицо");
	ФиксацияСброситьФиксациюИзмененийПоОснованиюЗаполнения("Банк");
	ФиксацияЗаполнитьРеквизитыОбъекта();
	ОбновитьЭлементыФормы();
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбновитьФорму(ЭтотОбъект);
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область СпособВыплаты

&НаСервере
Процедура ОбновитьЭлементыГруппыСпособВыплаты(Группа, Представление, Заполнена, Развернута, Видимость)
	Видимость = ВыгружатьРеквизитыДляВыплатыПособия;
	Элементы.КнопкиСвертываемойГруппыСпособВыплаты.Видимость = Видимость;
	Если Не Видимость Тогда
		Возврат;
	КонецЕсли;
	// Кнопки отображаются даже когда группа свернута, поэтому обновляются безусловно.
	ОбновитьКнопкиСпособаВыплатыПособия();
	СпособВыплаты = Объект.СпособВыплатыПособия;
	Если Развернута Тогда
		Если СпособВыплаты = Перечисления.СпособыВыплатыПособия.НаКартуМИР Тогда
			УстановитьСтраницу(Элементы.СтраницыСпособовВыплатыПособия, Элементы.СтраницаВыплатитьНаКартуМИР);
		ИначеЕсли СпособВыплаты = Перечисления.СпособыВыплатыПособия.ЧерезБанк Тогда
			УстановитьСтраницу(Элементы.СтраницыСпособовВыплатыПособия, Элементы.СтраницаВыплатитьЧерезБанк);
			// Результат проверки номера счета.
			РезультатПроверки = ПроверкиБЗК.РезультатПроверкиНомераСчета(
				Объект.НомерСчета,
				Объект.БанкБИК,
				Объект.БанкКоррСчет);
			Элементы.РезультатПроверкиНомераСчета.Заголовок = РезультатПроверки.Пояснение;
			Если РезультатПроверки.Успех Тогда
				Элементы.РезультатПроверкиНомераСчета.ЦветФона = ЦветаСтиля.ЦветФонаУспешнойОперацииБЗК;
			Иначе
				Элементы.РезультатПроверкиНомераСчета.ЦветФона = ЦветаСтиля.ЦветФонаПредупрежденияБЗК;
			КонецЕсли;
		ИначеЕсли СпособВыплаты = Перечисления.СпособыВыплатыПособия.ПочтовымПереводом Тогда
			УстановитьСтраницу(Элементы.СтраницыСпособовВыплатыПособия, Элементы.СтраницаВыплатитьПочтовымПереводом);
		ИначеЕсли СпособВыплаты = Перечисления.СпособыВыплатыПособия.ИнаяОрганизация Тогда
			УстановитьСтраницу(Элементы.СтраницыСпособовВыплатыПособия, Элементы.СтраницаВыплатитьИнаяОрганизация);
		Иначе
			УстановитьСтраницу(Элементы.СтраницыСпособовВыплатыПособия, Неопределено);
		КонецЕсли;
	Иначе
		Если СпособВыплаты = Перечисления.СпособыВыплатыПособия.ПочтовымПереводом Тогда
			Представление = ПредставлениеАдреса("АдресПроживания");
		Иначе
			Представление = СЭДОФСС.ПредставлениеСпособаВыплаты(Объект);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьКнопкиСпособаВыплатыПособия()
	// Команда "Изменить способ выплаты пособия".
	Элементы.ИзменитьСпособВыплатыПособия.Видимость = ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.ФизическоеЛицо);
	Если ЭтотОбъект["СпособВыплатыПособияФикс"]
		Или ЭтотОбъект["БанкФикс"]
		Или ЭтотОбъект["АдресПроживанияВидФикс"] Тогда
		Элементы.ИзменитьСпособВыплатыПособия.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
	Иначе
		Элементы.ИзменитьСпособВыплатыПособия.ЦветТекста = Новый Цвет;
	КонецЕсли;
	// Команда "Отменить изменения способа выплаты пособия".
	Элементы.ОтменитьИзмененияСпособаВыплатыПособия.Видимость = Не ДокументЗафиксирован
		И (ЭтотОбъект["СпособВыплатыПособияФикс"]
		Или ЭтотОбъект["КартаМИРФикс"]
		Или ЭтотОбъект["БанкФикс"]
		Или ЭтотОбъект["БанкБИКФикс"]
		Или ЭтотОбъект["НомерСчетаФикс"]
		Или ЭтотОбъект["АдресПроживанияВидФикс"]
		Или ЭтотОбъект["АдресПроживанияФикс"]
		Или ЭтотОбъект["АдресПроживанияЗаПределамиРФФикс"]
		Или ЭтотОбъект["АдресПроживанияФИАСДомФикс"]
		Или ЭтотОбъект["АдресПроживанияФИАСРОФикс"]);
КонецПроцедуры

&НаСервере
Процедура ОтменитьИзмененияСпособаВыплатыПособияНаСервере()
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.СброситьФиксациюИзмененийРеквизитовПоИмениГруппы(
		ЭтотОбъект,
		"СпособВыплатыПособия");
	ФиксацияЗаполнитьРеквизитыОбъекта();
	ОбновитьЭлементыФормы();
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбновитьФорму(ЭтотОбъект);
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура СпособВыплатыПособияПриИзмененииНаСервере()
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ЗафиксироватьИзменениеРеквизита(ЭтотОбъект, "СпособВыплатыПособия");
	ФиксацияСброситьФиксациюИзмененийПоОснованиюЗаполнения("СпособВыплатыПособия");
	ФиксацияЗаполнитьРеквизитыОбъекта();
	ОбновитьЭлементыФормы();
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбновитьФорму(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура БанкПриИзмененииНаСервере()
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ЗафиксироватьИзменениеРеквизита(ЭтотОбъект, "Банк");
	ФиксацияСброситьФиксациюИзмененийПоОснованиюЗаполнения("Банк");
	ФиксацияЗаполнитьРеквизитыОбъекта();
	ОбновитьЭлементыФормы();
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбновитьФорму(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура АдресПроживанияВидПриИзмененииНаСервере()
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ЗафиксироватьИзменениеРеквизита(ЭтотОбъект, "АдресПроживанияВид");
	ФиксацияСброситьФиксациюИзмененийПоОснованиюЗаполнения("АдресПроживанияВид");
	ФиксацияЗаполнитьРеквизитыОбъекта();
	ОбновитьЭлементыФормы();
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбновитьФорму(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ИнаяОрганизацияБИКПриИзмененииНаСервере()
	ЗаполнитьИнаяОрганизацияБанк();
	ОбновитьЭлементыФормы();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнаяОрганизацияБанк()
	Если Объект.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ИнаяОрганизация Тогда
		ИнаяОрганизацияБанк = СЭДОФСС.НайтиБанкПоБИК(Объект.ИнаяОрганизацияБИК);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ИнаяОрганизацияБанкПриИзмененииНаСервере()
	Если ЗначениеЗаполнено(ИнаяОрганизацияБанк) Тогда
		Объект.ИнаяОрганизацияБИК = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ИнаяОрганизацияБанк, "Код");
	Иначе
		Объект.ИнаяОрганизацияБИК = "";
	КонецЕсли;
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ЗафиксироватьИзменениеРеквизита(ЭтотОбъект, "ИнаяОрганизацияБИК");
КонецПроцедуры

#КонецОбласти

#Область Представитель

&НаСервере
Процедура ОбновитьЭлементыГруппыПредставитель(Группа, Представление, Заполнена, Развернута, Видимость)
	ЕстьФИО = ЗначениеЗаполнено(Объект.ПредставительФамилия);
	ЕстьТелефон = ЗначениеЗаполнено(Объект.ПредставительТелефон);
	ЕстьПочта = ЗначениеЗаполнено(Объект.ПредставительЭлектроннаяПочта);
	Заполнена = ЕстьФИО Или ЕстьТелефон Или ЕстьПочта;
	Если Развернута Тогда
		// Видимостью элементов группы управлять не требуется.
	Иначе
		Массив = Новый Массив;
		Если ЕстьФИО Тогда
			Массив.Добавить(СокрЛП(Объект.ПредставительФамилия + " " + Объект.ПредставительИмя + " " + Объект.ПредставительОтчество));
		КонецЕсли;
		Если ЕстьТелефон Тогда
			Массив.Добавить(Объект.ПредставительТелефон);
		КонецЕсли;
		Если ЕстьПочта Тогда
			Массив.Добавить(Объект.ПредставительЭлектроннаяПочта);
		КонецЕсли;
		Представление = СтрСоединить(Массив, ", ");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПредставительПриИзмененииНаСервере()
	ФиксацияСброситьФиксациюИзмененийПоОснованиюЗаполнения("Представитель");
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ЗафиксироватьИзменениеРеквизита(ЭтотОбъект, "Представитель");
	ФиксацияЗаполнитьРеквизитыОбъекта();
	ОбновитьЭлементыФормы();
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбновитьФорму(ЭтотОбъект);
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбщегоНазначения

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСтраницу(Страницы, ТекущаяСтраница)
	Для Каждого Страница Из Страницы.ПодчиненныеЭлементы Цикл
		Страница.Видимость = (Страница = ТекущаяСтраница);
	КонецЦикла;
	Если ТекущаяСтраница <> Неопределено Тогда
		Страницы.ТекущаяСтраница = ТекущаяСтраница;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область Команды

&НаСервере
Процедура ВключитьВозможностьРедактированияНаСервере()
	ВозможностьРедактирования = Не Элементы.ВключитьВозможностьРедактирования.Пометка;
	Элементы.ВключитьВозможностьРедактирования.Пометка = ВозможностьРедактирования;
	Элементы.СлужебныеПоляГруппа.ТолькоПросмотр        = Не ВозможностьРедактирования;
	Если ТолькоПросмотр И ВозможностьРедактирования Тогда
		ТолькоПросмотр = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область Поддержка

&НаСервере
Функция ПодготовитьВопросВПоддержку()
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Вложения = Новый Массив;
	Возврат СЭДОФСС.ПодготовитьВопросВПоддержку(ДокументОбъект, Вложения);
КонецФункции

#КонецОбласти

#КонецОбласти
