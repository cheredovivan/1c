// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Возвращает типы электронных документов, для которых доступно формирование на основании объекта учета.
// 
// Возвращаемое значение:
// 	Структура:
// * АктВыполненныхРабот - ПеречислениеСсылка.ТипыДокументовЭДО
// * АктНаПередачуПрав - ПеречислениеСсылка.ТипыДокументовЭДО
// * АктОРасхождениях - ПеречислениеСсылка.ТипыДокументовЭДО
// * АктСверкиВзаиморасчетов - ПеречислениеСсылка.ТипыДокументовЭДО
// * ДоговорныйДокумент - ПеречислениеСсылка.ТипыДокументовЭДО
// * ЗаказТовара - ПеречислениеСсылка.ТипыДокументовЭДО
// * ЗапросКоммерческихПредложений - ПеречислениеСсылка.ТипыДокументовЭДО
// * КаталогТоваров - ПеречислениеСсылка.ТипыДокументовЭДО
// * КоммерческоеПредложение - ПеречислениеСсылка.ТипыДокументовЭДО
// * КорректировочныйСчетФактура - ПеречислениеСсылка.ТипыДокументовЭДО
// * ОтветНаЗаказ - ПеречислениеСсылка.ТипыДокументовЭДО
// * ОтчетОПродажахКомиссионногоТовара - ПеречислениеСсылка.ТипыДокументовЭДО
// * ОтчетОСписанииКомиссионногоТовара - ПеречислениеСсылка.ТипыДокументовЭДО
// * ПрайсЛист - ПеречислениеСсылка.ТипыДокументовЭДО
// * СоглашениеОбИзмененииСтоимости - ПеречислениеСсылка.ТипыДокументовЭДО
// * СчетНаОплату - ПеречислениеСсылка.ТипыДокументовЭДО
// * СчетФактура - ПеречислениеСсылка.ТипыДокументовЭДО
// * ТоварнаяНакладная - ПеречислениеСсылка.ТипыДокументовЭДО
// * РеквизитыОрганизации - ПеречислениеСсылка.ТипыДокументовЭДО
// * УПД - ПеречислениеСсылка.ТипыДокументовЭДО
// * УКД - ПеречислениеСсылка.ТипыДокументовЭДО
// * АктПриемкиСтроительныхРаботУслуг - ПеречислениеСсылка.ТипыДокументовЭДО
// * КонтрактЕИС - ПеречислениеСсылка.ТипыДокументовЭДО
// * ДокументПриемкиЕИСУПД - ПеречислениеСсылка.ТипыДокументовЭДО
// * ДокументПриемкиЕИСУКД - ПеречислениеСсылка.ТипыДокументовЭДО
// * РезультатРассмотренияАктаПриемкиЕИСУПД - ПеречислениеСсылка.ТипыДокументовЭДО
// * РезультатРассмотренияАктаПриемкиЕИСУКД - ПеречислениеСсылка.ТипыДокументовЭДО
// * СведенияОРеализацииКомиссионером - ПеречислениеСсылка.ТипыДокументовЭДО
// * СведенияОЗакупкеКомиссионером - ПеречислениеСсылка.ТипыДокументовЭДО
// * КорректировкаСведенийОРеализацииКомиссионером - ПеречислениеСсылка.ТипыДокументовЭДО
// * КорректировкаСведенийОЗакупкеКомиссионером - ПеречислениеСсылка.ТипыДокументовЭДО
// * ВозвратТоваровМеждуОрганизациями - ПеречислениеСсылка.ТипыДокументовЭДО
// * ПередачаТоваровМеждуОрганизациями - ПеречислениеСсылка.ТипыДокументовЭДО
// * МашиночитаемаяДоверенность - ПеречислениеСсылка.ТипыДокументовЭДО
// * ДокументПредприятия - ПеречислениеСсылка.ТипыДокументовЭДО
//
Функция СтандартныеТипыДокументов() Экспорт
	
	ТипыДокументов = Новый Структура;

	ТипыДокументов.Вставить("АктВыполненныхРабот", АктВыполненныхРабот);
	ТипыДокументов.Вставить("АктНаПередачуПрав", АктНаПередачуПрав);
	ТипыДокументов.Вставить("АктОРасхождениях", АктОРасхождениях);
	ТипыДокументов.Вставить("АктСверкиВзаиморасчетов", АктСверкиВзаиморасчетов);
	ТипыДокументов.Вставить("ДоговорныйДокумент", ДоговорныйДокумент);
	ТипыДокументов.Вставить("ЗаказТовара", ЗаказТовара);
	ТипыДокументов.Вставить("ЗапросКоммерческихПредложений", ЗапросКоммерческихПредложений);
	ТипыДокументов.Вставить("КаталогТоваров", КаталогТоваров);
	ТипыДокументов.Вставить("КоммерческоеПредложение", КоммерческоеПредложение);
	ТипыДокументов.Вставить("КорректировочныйСчетФактура", КорректировочныйСчетФактура);
	ТипыДокументов.Вставить("ОтветНаЗаказ", ОтветНаЗаказ);
	ТипыДокументов.Вставить("ОтчетОПродажахКомиссионногоТовара", ОтчетОПродажахКомиссионногоТовара);
	ТипыДокументов.Вставить("ОтчетОСписанииКомиссионногоТовара", ОтчетОСписанииКомиссионногоТовара);
	ТипыДокументов.Вставить("ПрайсЛист", ПрайсЛист);
	ТипыДокументов.Вставить("СоглашениеОбИзмененииСтоимости", СоглашениеОбИзмененииСтоимости);
	ТипыДокументов.Вставить("СчетНаОплату", СчетНаОплату);
	ТипыДокументов.Вставить("СчетФактура", СчетФактура);
	ТипыДокументов.Вставить("ТоварнаяНакладная", ТоварнаяНакладная);
	ТипыДокументов.Вставить("РеквизитыОрганизации", РеквизитыОрганизации);
	ТипыДокументов.Вставить("УПД", УПД);
	ТипыДокументов.Вставить("УКД", УКД);
	ТипыДокументов.Вставить("АктПриемкиСтроительныхРаботУслуг", АктПриемкиСтроительныхРаботУслуг);

	ТипыДокументов.Вставить("КонтрактЕИС", КонтрактЕИС);
	ТипыДокументов.Вставить("ДокументПриемкиЕИСУПД", ДокументПриемкиЕИСУПД);
	ТипыДокументов.Вставить("ДокументПриемкиЕИСУКД", ДокументПриемкиЕИСУКД);
	ТипыДокументов.Вставить("РезультатРассмотренияАктаПриемкиЕИСУПД", РезультатРассмотренияАктаПриемкиЕИСУПД);
	ТипыДокументов.Вставить("РезультатРассмотренияАктаПриемкиЕИСУКД", РезультатРассмотренияАктаПриемкиЕИСУКД);

	ТипыДокументов.Вставить("СведенияОРеализацииКомиссионером", СведенияОРеализацииКомиссионером);
	ТипыДокументов.Вставить("СведенияОЗакупкеКомиссионером", СведенияОЗакупкеКомиссионером);
	ТипыДокументов.Вставить("КорректировкаСведенийОРеализацииКомиссионером", КорректировкаСведенийОРеализацииКомиссионером);
	ТипыДокументов.Вставить("КорректировкаСведенийОЗакупкеКомиссионером", КорректировкаСведенийОЗакупкеКомиссионером);

	ТипыДокументов.Вставить("ВозвратТоваровМеждуОрганизациями", ВозвратТоваровМеждуОрганизациями);
	ТипыДокументов.Вставить("ПередачаТоваровМеждуОрганизациями",ПередачаТоваровМеждуОрганизациями);
	ТипыДокументов.Вставить("МашиночитаемаяДоверенность", МашиночитаемаяДоверенность);

	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСГИСЭПД") Тогда
		МодульОбменСГИСЭПД = ОбщегоНазначения.ОбщийМодуль("ОбменСГИСЭПД");
		МодульОбменСГИСЭПД.ПриОпределенииСтандартныхТиповДокументов(ТипыДокументов);
	КонецЕсли;
	
	ТипыДокументов.Вставить("ДокументПредприятия", ДокументПредприятия);

	Возврат ТипыДокументов;
	
КонецФункции

// Возвращает доступные типы для электронного документа произвольного формата.
// 
// Возвращаемое значение:
// 	Массив из ПеречислениеСсылка.ТипыДокументовЭДО
Функция ТипыДокументовПроизвольногоФормата() Экспорт
	ТипыДокументов = Новый Массив; // См. ТипыДокументовПроизвольногоФормата
	ТипыДокументов.Добавить(АктВзаимозачета);
	ТипыДокументов.Добавить(АктВыполненныхРабот);
	ТипыДокументов.Добавить(АктСверки);
	ТипыДокументов.Добавить(Ведомость);
	ТипыДокументов.Добавить(ГарантийноеПисьмо);
	ТипыДокументов.Добавить(Договор);
	ТипыДокументов.Добавить(ДополнительноеСоглашение);
	ТипыДокументов.Добавить(КС11);
	ТипыДокументов.Добавить(КС2);
	ТипыДокументов.Добавить(КС3);
	ТипыДокументов.Добавить(Отчет);
	ТипыДокументов.Добавить(ПлатежноеПоручение);
	ТипыДокументов.Добавить(ПриложениеКАкту);
	ТипыДокументов.Добавить(Прочее);
	ТипыДокументов.Добавить(СоглашениеОбЭДО);
	ТипыДокументов.Добавить(Спецификация);
	ТипыДокументов.Добавить(СчетНаОплату);
	ТипыДокументов.Добавить(ТоварнаяНакладная);
	ТипыДокументов.Добавить(Уведомление);
	ТипыДокументов.Добавить(МашиночитаемаяДоверенность);
	ТипыДокументов.Добавить(ДоговорныйДокумент);
	Возврат ТипыДокументов;
КонецФункции

// Типы документов прямого обмена для фильтрации в алгоритмах.
// 
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ТипыДокументовЭДО
Функция ТипыДокументовПрямогоОбмена() Экспорт
	ТипыДокументов = Новый Массив; // См. ТипыДокументовПрямогоОбмена
	ТипыДокументов.Добавить(АктВыполненныхРабот);
	ТипыДокументов.Добавить(АктНаПередачуПрав);
	ТипыДокументов.Добавить(АктОРасхождениях);
	ТипыДокументов.Добавить(ДоговорныйДокумент);
	ТипыДокументов.Добавить(ЗаказТовара);
	ТипыДокументов.Добавить(ЗапросКоммерческихПредложений);
	ТипыДокументов.Добавить(КаталогТоваров);
	ТипыДокументов.Добавить(КоммерческоеПредложение);
	ТипыДокументов.Добавить(ОтветНаЗаказ);
	ТипыДокументов.Добавить(ОтчетОПродажахКомиссионногоТовара);
	ТипыДокументов.Добавить(ОтчетОСписанииКомиссионногоТовара);
	ТипыДокументов.Добавить(ПрайсЛист);
	ТипыДокументов.Добавить(СоглашениеОбИзмененииСтоимости);
	ТипыДокументов.Добавить(СчетНаОплату);
	ТипыДокументов.Добавить(ТоварнаяНакладная);
	ТипыДокументов.Добавить(КаталогТоваров);
	ТипыДокументов.Добавить(АктСверкиВзаиморасчетов);
	Возврат ТипыДокументов;
КонецФункции

// Типы документов прямого обмена для фильтрации в алгоритмах.
// 
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ТипыДокументовЭДО
Функция ТипыДокументовИнтеркампани() Экспорт
	ТипыДокументов = Новый Массив; // См. ТипыДокументовИнтеркампани
	ТипыДокументов.Добавить(ВозвратТоваровМеждуОрганизациями);
	ТипыДокументов.Добавить(ПередачаТоваровМеждуОрганизациями);
	Возврат ТипыДокументов;
КонецФункции

// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - ПеречислениеСсылка.ТипыДокументовЭДО
//  * Значение - ПеречислениеСсылка.ТипыДокументовЭДО
Функция КорректируемыеТипыДокументов() Экспорт
	
	Результат = Новый Соответствие;
	
	Результат.Вставить(УПД, УКД);
	Результат.Вставить(УПД, УКД);
	
	Результат.Вставить(ТоварнаяНакладная, СоглашениеОбИзмененииСтоимости);
	Результат.Вставить(АктВыполненныхРабот, СоглашениеОбИзмененииСтоимости);
	Результат.Вставить(АктНаПередачуПрав, СоглашениеОбИзмененииСтоимости);
	Результат.Вставить(СоглашениеОбИзмененииСтоимости, СоглашениеОбИзмененииСтоимости);
	
	Результат.Вставить(СчетФактура, КорректировочныйСчетФактура);
	Результат.Вставить(КорректировочныйСчетФактура,КорректировочныйСчетФактура);
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоСтандартный(ТипДокумента) Экспорт
	Для Каждого СтандартныйТип Из СтандартныеТипыДокументов() Цикл
		Если СтандартныйТип.Значение = ТипДокумента Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	Возврат Ложь;
КонецФункции

// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоПрикладной(ТипДокумента) Экспорт
	Возврат ТипДокумента = Прикладной;
КонецФункции

// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоИнтеркампани(ТипДокумента) Экспорт
	Возврат ТипДокумента = ПередачаТоваровМеждуОрганизациями
		ИЛИ ТипДокумента = ВозвратТоваровМеждуОрганизациями
КонецФункции

// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоСлужебный(ТипДокумента) Экспорт
	Возврат ТипДокумента = ПодтверждениеОператораЭДО
		ИЛИ ТипДокумента = ИзвещениеОПолучении
		ИЛИ ТипДокумента = УведомлениеОбУточнении
		ИЛИ ТипДокумента = ПредложениеОбАннулировании;
КонецФункции

// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоИсправляющий(ТипДокумента) Экспорт
	Возврат ТипДокумента = УПД
		Или ТипДокумента = ТоварнаяНакладная
		Или ТипДокумента = АктВыполненныхРабот
		Или ТипДокумента = АктНаПередачуПрав
		Или ТипДокумента = СчетФактура
		Или ТипДокумента = СведенияОРеализацииКомиссионером
		Или ТипДокумента = СведенияОЗакупкеКомиссионером
		Или ТипДокумента = АктОРасхождениях;
КонецФункции

// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоКорректирующий(ТипДокумента) Экспорт
	Возврат ТипДокумента = УКД
		ИЛИ ТипДокумента = СоглашениеОбИзмененииСтоимости
		ИЛИ ТипДокумента = КорректировочныйСчетФактура
		ИЛИ ТипДокумента = КорректировкаСведенийОРеализацииКомиссионером
		ИЛИ ТипДокумента = КорректировкаСведенийОЗакупкеКомиссионером;
КонецФункции

// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
// 
// Возвращаемое значение:
//  Строка
Функция КраткоеНаименование(ТипДокумента) Экспорт
	//АПК:1036-выкл отключение проверки орфографии
	Наименование = "";
	
	Если ТипДокумента = УПД Тогда
		Наименование = НСтр("ru = 'УПД';
							|en = 'UTD'");
	ИначеЕсли ТипДокумента = УКД Тогда
		Наименование = НСтр("ru = 'УКД';
							|en = 'UAD'");
	ИначеЕсли ТипДокумента = АктСверки Тогда
		Наименование = НСтр("ru = 'Сверка';
							|en = 'Reconciliation'");
	ИначеЕсли ТипДокумента = АктНаПередачуПрав Тогда
		Наименование = НСтр("ru = 'Акт ПП';
							|en = 'Acceptance certificate'");
	ИначеЕсли ТипДокумента = АктВыполненныхРабот Тогда
		Наименование = НСтр("ru = 'Акт';
							|en = 'Certificate'");
	ИначеЕсли ТипДокумента = АктОРасхождениях Тогда
		Наименование = НСтр("ru = 'Расхождения';
							|en = 'Discrepancies'");
	ИначеЕсли ТипДокумента = ТоварнаяНакладная
		ИЛИ ТипДокумента = ПередачаТоваровМеждуОрганизациями Тогда
		Наименование = НСтр("ru = 'Накладная';
							|en = 'Invoice'");
	ИначеЕсли ТипДокумента = СчетФактура Тогда
		Наименование = НСтр("ru = 'СФ';
							|en = 'Tax inv'");
	ИначеЕсли ТипДокумента = СоглашениеОбИзмененииСтоимости Тогда
		Наименование = НСтр("ru = 'Корректировка';
							|en = 'Adjustment'");
	ИначеЕсли ТипДокумента = КорректировочныйСчетФактура Тогда
		Наименование = НСтр("ru = 'Кор. СФ';
							|en = 'Tax invoice adjustment'");
	ИначеЕсли ТипДокумента = КаталогТоваров Тогда
		Наименование = НСтр("ru = 'Каталог';
							|en = 'Directory'");
	ИначеЕсли ТипДокумента = СчетНаОплату Тогда
		Наименование = НСтр("ru = 'Счет';
							|en = 'Account'");
	ИначеЕсли ТипДокумента = ПрайсЛист Тогда
		Наименование = НСтр("ru = 'Прайс';
							|en = 'Price list'");
	ИначеЕсли ТипДокумента = ЗаказТовара Тогда
		Наименование = НСтр("ru = 'Заказ';
							|en = 'Order'");
	ИначеЕсли ТипДокумента = ОтветНаЗаказ Тогда
		Наименование = НСтр("ru = 'Заказ покупателя';
							|en = 'Sales order'");
	ИначеЕсли ТипДокумента = АктВзаимозачета Тогда
		Наименование = НСтр("ru = 'Взаимозачет';
							|en = 'Offset'");
	ИначеЕсли ТипДокумента = ГарантийноеПисьмо Тогда
		Наименование = НСтр("ru = 'Гар. письмо';
							|en = 'Guarantee letter'");
	ИначеЕсли ТипДокумента = ДополнительноеСоглашение Тогда
		Наименование = НСтр("ru = 'Доп. согл.';
							|en = 'Additional agreement'");
	ИначеЕсли ТипДокумента = ПлатежноеПоручение Тогда
		Наименование = НСтр("ru = 'Пл. поруч.';
							|en = 'Payment order'");
	ИначеЕсли ТипДокумента = ПриложениеКАкту Тогда
		Наименование = НСтр("ru = 'Акт (прил.)';
							|en = 'Certificate (annex)'");
	ИначеЕсли ТипДокумента = СоглашениеОбЭДО Тогда
		Наименование = НСтр("ru = 'Соглашение';
							|en = 'Agreement'");
	ИначеЕсли ТипДокумента = ИзвещениеОПолучении Тогда
		Наименование = НСтр("ru = 'Извещение';
							|en = 'Notification'");
	ИначеЕсли ТипДокумента = ПредложениеОбАннулировании Тогда
		Наименование = НСтр("ru = 'ПОА';
							|en = 'CO'");
	ИначеЕсли ТипДокумента = УведомлениеОбУточнении Тогда
		Наименование = НСтр("ru = 'УОУ';
							|en = 'AMO'");
	ИначеЕсли ТипДокумента = ОтчетОПродажахКомиссионногоТовара Тогда
		Наименование = НСтр("ru = 'Продажа (комиссия)';
							|en = 'Sales (fee)'");
	ИначеЕсли ТипДокумента = ОтчетОСписанииКомиссионногоТовара Тогда
		Наименование = НСтр("ru = 'Списание (комиссия)';
							|en = 'Write-off (fee)'");
	ИначеЕсли ТипДокумента = ВозвратТоваровМеждуОрганизациями Тогда
		Наименование = НСтр("ru = 'Возврат';
							|en = 'Return'");
	ИначеЕсли ТипДокумента = ЗапросКоммерческихПредложений Тогда
		Наименование = НСтр("ru = 'Запрос ком. пред.';
							|en = 'Quotation request'");
	ИначеЕсли ТипДокумента = ПодтверждениеОператораЭДО Тогда
		Наименование = НСтр("ru = 'Подтв. оператора';
							|en = 'Confirm EDI service provider'");
	ИначеЕсли ТипДокумента = СведенияОРеализацииКомиссионером Тогда
		Наименование = НСтр("ru = 'Реализ. комиссионером';
							|en = 'Sold by commission agent'");
	ИначеЕсли ТипДокумента = СведенияОЗакупкеКомиссионером Тогда
		Наименование = НСтр("ru = 'Закупка комиссионером';
							|en = 'Purchase by commission agent'");
	ИначеЕсли ТипДокумента = КорректировкаСведенийОРеализацииКомиссионером Тогда
		Наименование = НСтр("ru = 'Корр. реализ. ком-ром';
							|en = 'Adjustment of commission agent sale'");
	ИначеЕсли ТипДокумента = КорректировкаСведенийОЗакупкеКомиссионером Тогда
		Наименование = НСтр("ru = 'Корр. закуп. ком-ром';
							|en = 'Adjustment of commission agent purchase'");
	ИначеЕсли ТипДокумента = МашиночитаемаяДоверенность Тогда
		Наименование = НСтр("ru = 'МЧД';
							|en = 'Machine-readable authorization letter'");
	ИначеЕсли ТипДокумента = АктСверкиВзаиморасчетов Тогда
		Наименование = НСтр("ru = 'Сверка взаиморасч.';
							|en = 'AR/AP reconciliation'");
	ИначеЕсли ТипДокумента = АктПриемкиСтроительныхРаботУслуг Тогда
		Наименование = НСтр("ru = 'Акт прием. строит. работ.';
							|en = 'Acceptance certificate for construction works'");
	Иначе
		Наименование = Строка(ТипДокумента);
	КонецЕсли;
	
	Возврат Наименование;
	//АПК:1036-вкл включение проверки орфографии
КонецФункции

// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
// Возвращаемое значение:
//  Число - приоритет отображения типа электронного документа.
Функция ПриоритетПриОтображенииВСписке(ТипДокумента) Экспорт
	
	Приоритет = 100;
	
	Если ТипДокумента = АктНаПередачуПрав Тогда
		Приоритет = 1;
	ИначеЕсли ТипДокумента = АктВыполненныхРабот Тогда
		Приоритет = 2;
	ИначеЕсли ТипДокумента = ТоварнаяНакладная Тогда
		Приоритет = 3;
	ИначеЕсли ТипДокумента = СчетФактура Тогда
		Приоритет = 4;
	ИначеЕсли ТипДокумента = УПД Тогда
		Приоритет = 5;
	ИначеЕсли ТипДокумента = УКД Тогда
		Приоритет = 6;
	ИначеЕсли ТипДокумента = СоглашениеОбИзмененииСтоимости Тогда
		Приоритет = 7;
	ИначеЕсли ТипДокумента = КорректировочныйСчетФактура Тогда
		Приоритет = 8;
	ИначеЕсли ТипДокумента = КаталогТоваров Тогда
		Приоритет = 9;
	ИначеЕсли ТипДокумента = СчетНаОплату Тогда
		Приоритет = 10;
	ИначеЕсли ТипДокумента = ПрайсЛист Тогда
		Приоритет = 11;
	ИначеЕсли ТипДокумента = ЗаказТовара Тогда
		Приоритет = 12;
	ИначеЕсли ТипДокумента = ОтветНаЗаказ Тогда
		Приоритет = 13;
	ИначеЕсли ТипДокумента = АктВзаимозачета Тогда
		Приоритет = 14;
	ИначеЕсли ТипДокумента = АктСверки Тогда
		Приоритет = 15;
	ИначеЕсли ТипДокумента = Ведомость Тогда
		Приоритет = 16;
	ИначеЕсли ТипДокумента = ГарантийноеПисьмо Тогда
		Приоритет = 17;
	ИначеЕсли ТипДокумента = Договор Тогда
		Приоритет = 18;
	ИначеЕсли ТипДокумента = ДополнительноеСоглашение Тогда
		Приоритет = 19;
	ИначеЕсли ТипДокумента = КС11 Тогда
		Приоритет = 20;
	ИначеЕсли ТипДокумента = КС2 Тогда
		Приоритет = 21;
	ИначеЕсли ТипДокумента = КС3 Тогда
		Приоритет = 22;
	ИначеЕсли ТипДокумента = Отчет Тогда
		Приоритет = 23;
	ИначеЕсли ТипДокумента = ПлатежноеПоручение Тогда
		Приоритет = 24;
	ИначеЕсли ТипДокумента = ПриложениеКАкту Тогда
		Приоритет = 25;
	ИначеЕсли ТипДокумента = Прочее Тогда
		Приоритет = 26;
	ИначеЕсли ТипДокумента = СоглашениеОбЭДО Тогда
		Приоритет = 27;
	ИначеЕсли ТипДокумента = Спецификация Тогда
		Приоритет = 28;
	ИначеЕсли ТипДокумента = Уведомление Тогда
		Приоритет = 29;
	ИначеЕсли ТипДокумента = ЗапросКоммерческихПредложений Тогда
		Приоритет = 30;
	ИначеЕсли ТипДокумента = АктОРасхождениях Тогда
		Приоритет = 31;
	ИначеЕсли ТипДокумента = АктСверкиВзаиморасчетов Тогда
		Приоритет = 32;
	ИначеЕсли ТипДокумента = АктПриемкиСтроительныхРаботУслуг Тогда
		Приоритет = 33;
	ИначеЕсли ТипДокумента = Прикладной Тогда
		Приоритет = 40;
	ИначеЕсли ТипДокумента = ДоговорныйДокумент Тогда
		Приоритет = 101;
	КонецЕсли;
	
	Возврат Приоритет;
	
КонецФункции

// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
// 
// Возвращаемое значение:
//  Структура:
//  * Группа - Строка
//  * ПриоритетГруппы - Число
Функция ПараметрыГруппыПриОтображенииВСписке(ТипДокумента) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Группа", "");
	Параметры.Вставить("ПриоритетГруппы", 0);
	
	Если ТипДокумента = АктНаПередачуПрав 
		Или ТипДокумента = АктВыполненныхРабот 
		Или ТипДокумента = ТоварнаяНакладная 
		Или ТипДокумента = СчетФактура
		Или ТипДокумента = УПД
		Или ТипДокумента = УКД
		Или ТипДокумента = СоглашениеОбИзмененииСтоимости
		Или ТипДокумента = КорректировочныйСчетФактура
		Или ТипДокумента = ОтчетОСписанииКомиссионногоТовара
		Или ТипДокумента = ОтчетОПродажахКомиссионногоТовара
		Или ТипДокумента = АктОРасхождениях
		Или ТипДокумента = АктСверкиВзаиморасчетов
		Или ТипДокумента = СведенияОРеализацииКомиссионером
		Или ТипДокумента = СведенияОЗакупкеКомиссионером
		Или ТипДокумента = КорректировкаСведенийОРеализацииКомиссионером
		Или ТипДокумента = КорректировкаСведенийОЗакупкеКомиссионером 
		Или ТипДокумента = АктПриемкиСтроительныхРаботУслуг Тогда
		Параметры.Группа = НСтр("ru = 'Первичные документы';
								|en = 'Source documents'");
		Параметры.ПриоритетГруппы = 1;
	ИначеЕсли ТипДокумента = КаталогТоваров
		Или ТипДокумента = СчетНаОплату
		Или ТипДокумента = ПрайсЛист
		Или ТипДокумента = ЗаказТовара
		Или ТипДокумента = ОтветНаЗаказ
		Или ТипДокумента = ЗапросКоммерческихПредложений
		Или ТипДокумента = КоммерческоеПредложение
		Или ТипДокумента = Прикладной Тогда
		Параметры.Группа = НСтр("ru = 'Оперативные документы';
								|en = 'Operational documents'");
		Параметры.ПриоритетГруппы = 2;
	Иначе
		Параметры.Группа = НСтр("ru = 'Прочие';
								|en = 'Other'");
		Параметры.ПриоритетГруппы = 3;
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
// 
// Возвращаемое значение:
//  Булево - Если истина документа поддерживает загрузку из файла
Функция ПоддерживаетЗагрузкуИзФайлаВФормализованномВиде(ТипДокумента) Экспорт
	ПоддерживаемыеТипы = Новый Массив; // Массив из ПеречислениеСсылка.ТипыДокументовЭДО
	ПоддерживаемыеТипы.Добавить(УПД);
	ПоддерживаемыеТипы.Добавить(УКД);
	ПоддерживаемыеТипы.Добавить(СчетФактура);
	ПоддерживаемыеТипы.Добавить(КорректировочныйСчетФактура);
	ПоддерживаемыеТипы.Добавить(АктВыполненныхРабот);
	ПоддерживаемыеТипы.Добавить(ТоварнаяНакладная);
	ПоддерживаемыеТипы.Добавить(АктОРасхождениях);
	ПоддерживаемыеТипы.Добавить(АктНаПередачуПрав);
	ПоддерживаемыеТипы.Добавить(СоглашениеОбИзмененииСтоимости);
	Возврат ПоддерживаемыеТипы.Найти(ТипДокумента) <> Неопределено;
КонецФункции

#КонецОбласти

#КонецЕсли