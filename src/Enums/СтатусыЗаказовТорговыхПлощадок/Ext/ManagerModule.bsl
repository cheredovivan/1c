#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Возвращает представление схемы работы для учетной записи маркетплейса.
//
// Параметры:
//   Представление    - Строка - представление статуса отправления на торговой площадке;
//   Уточнение        - Строка - представление субстатуса отправления на торговой площадке;
//   ТорговаяПлощадка - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//
// Возвращаемое значение:
//   ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок - статус заказа в учетной системе.
//
Функция ПолучитьПоПредставлению(Знач Представление, Знач Уточнение = "", ТорговаяПлощадка = Неопределено) Экспорт
	
	Результат     = Перечисления.СтатусыЗаказовТорговыхПлощадок.ПустаяСсылка();
	Представление = ВРег(Представление);
	Уточнение     = ВРег(Уточнение);
	
	Если ТорговаяПлощадка = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		Если Представление = ВРег("acceptance_in_progress") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.ПринимаетсяВДоставку;
		ИначеЕсли Представление = ВРег("arbitration") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.Спор;
		ИначеЕсли Представление = ВРег("awaiting_approve") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки;
		ИначеЕсли Представление = ВРег("awaiting_deliver") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке;
		ИначеЕсли Представление = ВРег("awaiting_packaging") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки;
		ИначеЕсли Представление = ВРег("awaiting_registration") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки;
		ИначеЕсли Представление = ВРег("awaiting_verification") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки;
		ИначеЕсли Представление = ВРег("cancelled") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.Отменен;
		ИначеЕсли Представление = ВРег("cancelled_from_split_pending") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.Отменен;
		ИначеЕсли Представление = ВРег("client_arbitration") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.Доставлен;
		ИначеЕсли Представление = ВРег("delivering") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.Доставляется;
		ИначеЕсли Представление = ВРег("driver_pickup") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.Доставляется;
		ИначеЕсли Представление = ВРег("delivered") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.Доставлен;
		ИначеЕсли Представление = ВРег("not_accepted") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.Доставляется;
		ИначеЕсли Представление = ВРег("sent_by_seller") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.Доставляется; 
		КонецЕсли;
	
		Если Уточнение = ВРег("posting_in_pickup_point") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.ВПунктеВыдачи;
		ИначеЕсли Уточнение = ВРег("posting_in_carriage")Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.ПередаетсяВДоставку;
		ИначеЕсли Уточнение = ВРег("posting_not_in_carriage")Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.НеПринятВДоставку;
		КонецЕсли;
	
	
	ИначеЕсли  ТорговаяПлощадка = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		
		Если Представление = ВРег("PROCESSING") Тогда
			
			Если Уточнение = ВРег("STARTED") Тогда
				Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки; 
			ИначеЕсли Уточнение = ВРег("READY_TO_SHIP") Тогда
				Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке; 
			КонецЕсли;
			
		ИначеЕсли Представление = ВРег("UNPAID") Тогда	
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетОплату; 
			
		ИначеЕсли Представление = ВРег("CANCELLED") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.Отменен;
			
		ИначеЕсли Представление = ВРег("DELIVERY ") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.Доставляется;
			
		ИначеЕсли Представление = ВРег("PICKUP") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.ВПунктеВыдачи;
			
		ИначеЕсли Представление = ВРег("DELIVERED") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.Доставляется;
			
		ИначеЕсли Представление = ВРег("PENDING") Тогда
			
		ИначеЕсли Представление = ВРег("PARTIALLY_RETURNED") Тогда	
			
		ИначеЕсли Представление = ВРег("RETURNED") Тогда
			Результат = Перечисления.СтатусыЗаказовТорговыхПлощадок.Возвращен;
			
		ИначеЕсли Представление = ВРег("UNKNOWN ") Тогда
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Заполняет список выбора статусами заказов.
// Параметры:
//   СписокВыбора           - СписокЗначений Из ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок - список для  
//                              заполнения доступных значений выбора. 
//   ТорговаяПлощадка 		- ПеречислениеСсылка.ВидыМаркетплейсов - торговая площадка.
//   ДобавитьПустоеЗначение - Булево - признак добавления значения "Все".  
//	 ТолькоЗаказыФизЛиц		- Булево - признак, что в список должны попасть только статусы, актуальные для физических лиц.
//
Процедура ЗаполнитьСписокВыбора(СписокВыбора, ТорговаяПлощадка, ДобавитьПустоеЗначение = Истина, ТолькоЗаказыФизЛиц = Ложь) Экспорт
	
	СписокВыбора.Очистить();
	
	Если ДобавитьПустоеЗначение Тогда
		СписокВыбора.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.ПустаяСсылка(), НСтр("ru = 'Все';
																								|en = 'All'"));
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СтатусыЗаказовТорговыхПлощадок.Ссылка КАК Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(СтатусыЗаказовТорговыхПлощадок.Ссылка) КАК Представление
		|ИЗ
		|	Перечисление.СтатусыЗаказовТорговыхПлощадок КАК СтатусыЗаказовТорговыхПлощадок 
		|ГДЕ 
		|   НЕ (СтатусыЗаказовТорговыхПлощадок.Ссылка В(&СписокИсключений))  
		|
		|УПОРЯДОЧИТЬ ПО
		|	СтатусыЗаказовТорговыхПлощадок.Порядок"; 
	
	СписокИсключений = Новый Массив;
	Если ТорговаяПлощадка = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon ИЛИ ТолькоЗаказыФизЛиц Тогда
		СписокИсключений.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетОплату); 
	КонецЕсли;	
	Если ТорговаяПлощадка = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда	
		СписокИсключений.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.Спор);	
	КонецЕсли;  
	Запрос.Параметры.Вставить("СписокИсключений",СписокИсключений);
	
	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	Пока ВыборкаДанных.Следующий() Цикл
		СписокВыбора.Добавить(ВыборкаДанных.Ссылка, ВыборкаДанных.Представление);
	КонецЦикла;   
		
КонецПроцедуры

// Возвращает список конечных статусов.
//
// Возвращаемое значение:
//   Массив из ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок - список статусов.
//
Функция СтатусыЗаказовКонечные() Экспорт

	Статусы = Новый Массив;
	Статусы.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.Отменен);
	Статусы.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.Доставлен);
	Статусы.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.Возвращен);
	
	Возврат Статусы;

КонецФункции

// Возвращает список рабочих статусов.
//
// Параметры:
//   ИспользоватьОграничения - Булево - признак получения с учетом следующих параметров.
//   ДоПередачиВДоставку     - Булево - признак получения статусов до передачи в доставку.
//   ПередачаВДоставку       - Булево - признак получения статуса передачи в доставку.
//
// Возвращаемое значение:
//   Массив из ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок - список статусов.
//
Функция СтатусыЗаказовРаботе(ИспользоватьОграничения = Ложь,
		ДоПередачиВДоставку = Истина, ПередачаВДоставку = Ложь) Экспорт

	Статусы = Новый Массив;
	
	Если ИспользоватьОграничения Тогда
		Если ДоПередачиВДоставку Тогда
			Статусы.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки);
			Статусы.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке);
		КонецЕсли;
		
		Если ПередачаВДоставку Тогда
			Статусы.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.ПередаетсяВДоставку);
		КонецЕсли;
	Иначе
		Завершенные = СтатусыЗаказовКонечные();
		
		Для Каждого Статус Из Метаданные.Перечисления.СтатусыЗаказовТорговыхПлощадок.ЗначенияПеречисления Цикл
			Если Завершенные.Найти(Перечисления.СтатусыЗаказовТорговыхПлощадок[Статус.Имя]) = Неопределено Тогда
				Статусы.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок[Статус.Имя]);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Статусы;

КонецФункции

// Возвращает список статусов в доставке.
//
// Параметры:
//   ВключаяОжидающиеОтгрузки - Булево - признак включения статуса ГотовКОтгрузке.
//
// Возвращаемое значение:
//   Массив из ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок - список статусов.
//
Функция СтатусыЗаказовВДоставке(ВключаяОжидающиеОтгрузки = Истина) Экспорт

	Статусы = Новый Массив;
	
	Если ВключаяОжидающиеОтгрузки Тогда
		Статусы.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке);
	КонецЕсли;
	
	Статусы.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.ПередаетсяВДоставку);
	Статусы.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.ПринимаетсяВДоставку);
	Статусы.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.Доставляется);
	
	Возврат Статусы;

КонецФункции

#КонецОбласти

#КонецЕсли