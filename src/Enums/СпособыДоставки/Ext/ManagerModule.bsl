//@strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет представление переданного способа доставки.
//
// Параметры:
//   СпособДоставки - ПеречислениеСсылка.СпособыДоставки - способ доставки для получения представления.
//
// Возвращаемое значение:
//   Строка - представление способа доставки.
//
Функция ПредставлениеСпособаДоставки(СпособДоставки) Экспорт

	ПредставленияСпособовДоставки = ПредставленияСпособовДоставки();
	
	ПредставлениеСпособаДоставки = ПредставленияСпособовДоставки[СпособДоставки];
	Если ПредставлениеСпособаДоставки = Неопределено Тогда
		ПредставлениеСпособаДоставки = Строка(СпособДоставки);
	КонецЕсли;
	
	Возврат ПредставлениеСпособаДоставки;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Конструктор представлений способов доставки.
//
// Возвращаемое значение:
//   Соответствие - список представлений:
//    * Ключ - ПеречислениеСсылка.СпособыДоставки - способ доставки.
//    * Значение - Строка - представление способа доставки.
//
Функция ПредставленияСпособовДоставки()

	ПредставленияСпособовДоставки = Новый Соответствие;
	
	ПредставленияСпособовДоставки.Вставить(Перечисления.СпособыДоставки.Самовывоз,
											НСтр("ru = 'Самовывоз (с нашего склада)';
												|en = 'Pickup from warehouse'"));
	ПредставленияСпособовДоставки.Вставить(Перечисления.СпособыДоставки.ДоКлиента,
											НСтр("ru = 'Наша транспортная служба до клиента';
												|en = 'In-house carrier'"));
	ПредставленияСпособовДоставки.Вставить(Перечисления.СпособыДоставки.СиламиПеревозчикаПоАдресу,
											НСтр("ru = 'Наша транспортная служба до перевозчика';
												|en = 'In-house carrier (delivery to shipping company)'"));
	ПредставленияСпособовДоставки.Вставить(Перечисления.СпособыДоставки.СиламиПеревозчика,
											НСтр("ru = 'Перевозчик (с нашего склада)';
												|en = 'Outsourced carrier'"));
	ПредставленияСпособовДоставки.Вставить(Перечисления.СпособыДоставки.КПолучателюОпределяетСлужбаДоставки,
											НСтр("ru = 'На усмотрение транспортной службы';
												|en = 'Define later'"));
	
	Возврат ПредставленияСпособовДоставки;

КонецФункции

#КонецОбласти

#КонецЕсли