
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
	
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = ДанныеВыбораПоФункциональнымОпциям();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Убирает из полного списка разделов те, функционал ведения учета по которым не включен.
//
// Возвращаемое значение:
//  СписокЗначений - список, куда добавлены все возможные (из используемых) разделы учета.
//
Функция ДанныеВыбораПоФункциональнымОпциям(ТолькоДляДокументаИнвентаризацияОбъектовУчета = Ложь) Экспорт
	
	СписокРазделов = Новый СписокЗначений;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	РазделыУчетаДляИнвентаризации.Ссылка КАК СсылкаЗначение,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(РазделыУчетаДляИнвентаризации.Ссылка) КАК СсылкаПредставление
	|ИЗ
	|	Перечисление.РазделыУчетаДляИнвентаризации КАК РазделыУчетаДляИнвентаризации
	|
	|УПОРЯДОЧИТЬ ПО
	|	РазделыУчетаДляИнвентаризации.Порядок");
	Выборка = Запрос.Выполнить().Выбрать();
	ИспользуетсяУправлениеВНА = ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА(); 
	РазделыИсключения = Новый Массив;
	РазделыИсключения.Добавить(ОсновныеСредства);
	РазделыИсключения.Добавить(НематериальныеАктивы);
	РазделыИсключения.Добавить(КапитальныеВложенияПоОС);
	РазделыИсключения.Добавить(КомпонентыОСИОборудованиеКУстановке);

	Пока Выборка.Следующий() Цикл
		
		Раздел = Выборка.СсылкаЗначение;
		Если РазделыИсключения.Найти(Раздел) <> Неопределено
			И НЕ ИспользуетсяУправлениеВНА Тогда
			Продолжить;
		КонецЕсли;
		
		СписокРазделов.Добавить(Выборка.СсылкаЗначение, Выборка.СсылкаПредставление);
	
	КонецЦикла;
	
	Возврат СписокРазделов;
	
КонецФункции

#КонецОбласти	
	
#КонецЕсли