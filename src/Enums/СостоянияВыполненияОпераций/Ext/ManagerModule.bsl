
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Порядок состояния в списках
// 
// Параметры:
//  Состояние - ПеречислениеСсылка.СостоянияВыполненияОпераций
// 
// Возвращаемое значение:
//  Число
Функция ПорядокВСписке(Состояние) Экспорт
	
	Результат = 0;
	
	Если Состояние = Перечисления.СостоянияВыполненияОпераций.Выполнена
		ИЛИ Состояние = Перечисления.СостоянияВыполненияОпераций.Отменена
		ИЛИ Состояние = Перечисления.СостоянияВыполненияОпераций.Пропущена Тогда
		Результат = 7;
	ИначеЕсли Состояние = Перечисления.СостоянияВыполненияОпераций.Выполняется
		ИЛИ Состояние = Перечисления.СостоянияВыполненияОпераций.НаКонтроле
		ИЛИ Состояние = Перечисления.СостоянияВыполненияОпераций.НаДоработке Тогда
		Результат = 6;
	ИначеЕсли Состояние = Перечисления.СостоянияВыполненияОпераций.КВыполнению Тогда
		Результат = 5;
	ИначеЕсли Состояние = Перечисления.СостоянияВыполненияОпераций.Назначена Тогда
		Результат = 4;
	ИначеЕсли Состояние = Перечисления.СостоянияВыполненияОпераций.КНазначению Тогда
		Результат = 3;
	ИначеЕсли Состояние = Перечисления.СостоянияВыполненияОпераций.НачатаПредшествующая Тогда
		Результат = 2;
	ИначеЕсли Состояние = Перечисления.СостоянияВыполненияОпераций.ОжиданиеПредшествующих Тогда
		Результат = 1;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Устанавливает условное оформления поля "Состояния" в списках
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ИмяПоля - Строка - Имя поля
//  ПутьКПолюСостояние - Строка - Путь к полю состояние
//  ПутьКПолюСоздано - Строка - Путь к полю создано
//  
Процедура УстановитьУсловноеОформлениеСостояния(Форма, ИмяПоля = "Состояние", ПутьКПолюСостояние = "Операции.Состояние", ПутьКПолюСоздано = Неопределено) Экспорт
	
	Элемент = Форма.УсловноеОформление.Элементы.Добавить();
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы[ИмяПоля].Имя);
	
	ОжидающиеСостояния = Новый СписокЗначений();
	ОжидающиеСостояния.Добавить(Перечисления.СостоянияВыполненияОпераций.НачатаПредшествующая);
	ОжидающиеСостояния.Добавить(Перечисления.СостоянияВыполненияОпераций.ОжиданиеПредшествующих);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюСостояние);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ОтборЭлемента.ПравоеЗначение = ОжидающиеСостояния;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.СерыйЦветТекста1);
	
	Если ПутьКПолюСоздано <> Неопределено Тогда
		
		Для каждого ЭлементСписка Из ОжидающиеСостояния Цикл
			
			Элемент = Форма.УсловноеОформление.Элементы.Добавить();
			Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы[ИмяПоля].Имя);
			
			ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюСостояние);
			ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ОтборЭлемента.ПравоеЗначение = ЭлементСписка.Значение;
			
			ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюСоздано);
			ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ОтборЭлемента.ПравоеЗначение = Истина;
			
			ТекстПоля = СтрШаблон(НСтр("ru = '%1 (уже назначена)';
										|en = '%1 (already assigned)'"), Строка(ЭлементСписка.Значение));
			Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ТекстПоля);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли