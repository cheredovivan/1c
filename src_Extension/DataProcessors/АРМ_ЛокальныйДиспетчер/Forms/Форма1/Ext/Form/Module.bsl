//Перем КэшированныеЗначения;

#Область ОбработчикиПриИзменении

&НаКлиенте
Функция ПолучитьСтруктуруДействий()
	
	Ответ = Новый Структура();
	Ответ.Вставить("Изделие", Ложь); 
	Ответ.Вставить("Спецификация", Ложь); 
	Ответ.Вставить("Маршрут", Ложь); 
	Ответ.Вставить("Цехозаход", Ложь); 
	Ответ.Вставить("Цех", Ложь); 
	Ответ.Вставить("Этапы", Ложь); 
	Ответ.Вставить("Итоги", Ложь); 
	
	Возврат Ответ;
	
КонецФункции

&НаСервере
Процедура ВыполнитьДействияПриИзмененииНаСервере( СтруктураДействий )
	
	РеалО = РеквизитФормыВЗначение("Объект");
	РеалД = РеквизитФормыВЗначение("ДеревоЭтапыНазначения");
	
	Если СтруктураДействий.Изделие = Истина
	Тогда
		Если ЗначениеЗаполнено(Объект.Номенклатура)
		Тогда
			Элементы.Номенклатура.Заголовок = "Изделие " + Объект.Номенклатура.Артикул;
		Иначе
			Элементы.Номенклатура.Заголовок = "Изделие";
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураДействий.Спецификация = Истина
	Тогда
		ЗаполнитьСписокСпецификаций( РеалО, РеалД );
	КонецЕсли;
	
	Если СтруктураДействий.Маршрут = Истина
	Тогда
		ЗаполнитьМаршрут( РеалО );
	КонецЕсли;
	
	Если СтруктураДействий.Цех = Истина
	Тогда
		ПроверитьЗаполнениеПоляЦех( РеалО );
	КонецЕсли;
	
	Если СтруктураДействий.Цехозаход = Истина
	Тогда
		ЗаполнитьСписокЦехозаходов( РеалО );
	КонецЕсли;
	
	Если СтруктураДействий.Этапы = Истина
	Тогда
		Если Не ЗначениеЗаполнено(Объект.Номенклатура)
		Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю( "Выберите изделие",,"Номенклатура");
		Иначе
			ЗаполнитьСписокЭтапов( РеалО, РеалД  );
		КонецЕсли;
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(РеалД, "ДеревоЭтапыНазначения");	
	ЗначениеВРеквизитФормы(РеалО, "Объект");	
	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДействияПриИзменении( СтруктураДействий )
	
	ВыполнитьДействияПриИзмененииНаСервере( СтруктураДействий );
	
	// Действия, которые нужно выполнить на клиенте
	Если СтруктураДействий.Этапы = Истина
	Тогда
		РазвернутьДерево();
	КонецЕсли;
	
	Если СтруктураДействий.Итоги = Истина
	Тогда
		ОбновитьИтоги();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	СтруктураДействий = ПолучитьСтруктуруДействий();
	СтруктураДействий.Изделие = Истина;
	СтруктураДействий.Спецификация = Истина;
	СтруктураДействий.Маршрут = Истина;
	СтруктураДействий.Цехозаход = Истина;
	СтруктураДействий.Цех = Истина;
	СтруктураДействий.Этапы = Истина;
	СтруктураДействий.Итоги = Истина;
	
	ВыполнитьДействияПриИзменении( СтруктураДействий );
	
КонецПроцедуры

&НаКлиенте
Процедура СпецификацияПриИзменении(Элемент)
	
	СтруктураДействий = ПолучитьСтруктуруДействий();
	СтруктураДействий.Маршрут = Истина;
	СтруктураДействий.Цехозаход = Истина;
	СтруктураДействий.Цех = Истина;
	СтруктураДействий.Этапы = Истина;
	СтруктураДействий.Итоги = Истина;
	
	ВыполнитьДействияПриИзменении( СтруктураДействий );
	
КонецПроцедуры

&НаКлиенте
Процедура НомерЦехозаходаПриИзменении(Элемент)
	
	СтруктураДействий = ПолучитьСтруктуруДействий();
	СтруктураДействий.Этапы = Истина;
	СтруктураДействий.Итоги = Истина;
	
	ВыполнитьДействияПриИзменении( СтруктураДействий );
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	СтруктураДействий = ПолучитьСтруктуруДействий();
	СтруктураДействий.Спецификация = Истина;
	СтруктураДействий.Маршрут = Истина;
	СтруктураДействий.Цехозаход = Истина;
	СтруктураДействий.Этапы = Истина;
	СтруктураДействий.Итоги = Истина;
	
	ВыполнитьДействияПриИзменении( СтруктураДействий );
	
КонецПроцедуры


#КонецОбласти


&НаКлиенте
Процедура ОтобратьЭтапы(Команда)
	
	СтруктураДействий = ПолучитьСтруктуруДействий();
	СтруктураДействий.Этапы = Истина;
	СтруктураДействий.Итоги = Истина;
	
	ВыполнитьДействияПриИзменении( СтруктураДействий );
	
КонецПроцедуры

&НаСервере
Функция РаспределитьПоЭтапамНаСервере()
	РеалД = РеквизитФормыВЗначение("ДеревоЭтапыНазначения");
	
	ВсегоНедоделов = 0;
	Для Каждого СДЗЭтап Из РеалД.Строки
	Цикл
		Для Каждого СДЗНазн Из СДЗЭтап.Строки
		Цикл
			ВсегоНедоделов = ВсегоНедоделов + СДЗНазн.Недодел;
		КонецЦикла;
	КонецЦикла;
	Если ВсегоНедоделов < Объект.Количество
	Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.Количество;
		Возврат "Количество!";
	КонецЕсли;
	
	Для Каждого СДЗЭтап Из РеалД.Строки
	Цикл
		СДЗЭтап.Распределено = 0;
		Для Каждого СДЗНазн Из СДЗЭтап.Строки
		Цикл
			СДЗНазн.Распределено = 0;
		КонецЦикла;
	КонецЦикла;
	
	ОсталосьРаспределить = Объект.Количество;
	
	// Распределяем сначала на военную продукцию
	Для Каждого СДЗЭтап Из РеалД.Строки
	Цикл
		РаспределеноНаЭтап = 0;
		Для Каждого СДЗНазн Из СДЗЭтап.Строки
	        Цикл
			Если Не ЗначениеЗаполнено(СДЗНазн.Назначение)
		        Тогда
				Продолжить;
			КонецЕсли;
			Если ОсталосьРаспределить > СДЗНазн.Недодел
		        Тогда
				СДЗНазн.Распределено = СДЗНазн.Недодел;
				ОсталосьРаспределить = ОсталосьРаспределить - СДЗНазн.Недодел;
			Иначе
				СДЗНазн.Распределено = ОсталосьРаспределить;
				ОсталосьРаспределить = 0;
			КонецЕсли;
			РаспределеноНаЭтап = РаспределеноНаЭтап + СДЗНазн.Распределено;
		КонецЦикла;
		СДЗЭтап.Распределено = СДЗЭтап.Распределено + РаспределеноНаЭтап;
	КонецЦикла;
	
	// .. а потом распределяем на гражданскую продукцию
	Для Каждого СДЗЭтап Из РеалД.Строки
	Цикл
		РаспределеноНаЭтап = 0;
		Для Каждого СДЗНазн Из СДЗЭтап.Строки
	        Цикл
			Если ЗначениеЗаполнено(СДЗНазн.Назначение)
		        Тогда
				Продолжить;
			КонецЕсли;
			Если ОсталосьРаспределить > СДЗНазн.Недодел
		        Тогда
				СДЗНазн.Распределено = СДЗНазн.Недодел;
				ОсталосьРаспределить = ОсталосьРаспределить - СДЗНазн.Недодел;
			Иначе
				СДЗНазн.Распределено = ОсталосьРаспределить;
				ОсталосьРаспределить = 0;
			КонецЕсли;
			РаспределеноНаЭтап = РаспределеноНаЭтап + СДЗНазн.Распределено;
		КонецЦикла;
		СДЗЭтап.Распределено = СДЗЭтап.Распределено + РаспределеноНаЭтап;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(РеалД, "ДеревоЭтапыНазначения");
	
	Возврат Истина;
КонецФункции

&НаКлиенте
Процедура РаспределитьПоЭтапам(Команда)
	ВыполнитьРаспределениеПоЭтапам();
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "НаименованиеЭтапа" 
	Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, Элемент.ТекущиеДанные.Этап);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокЦехозаходов( РеалО )
	
	Элементы.НомерЦехозахода.СписокВыбора.Очистить();
	Ном = 0;
	Для Каждого СТЗ Из Маршрут
	Цикл
		Если СТЗ.Подразделение = РеалО.Подразделение
		Тогда
			Элементы.НомерЦехозахода.СписокВыбора.Добавить( Ном, Строка(Ном+1) );
			//Элементы.НомерЦехозахода.СписокВыбора.Добавить( Ном, Строка(СТЗ.НомерЭтапа) );
			Ном = Ном + 1;
		КонецЕсли;
	КонецЦикла;
	
	РеалО.НомерЦехозахода = 0;
	
	Если Ном > 1
	Тогда
		Элементы.НомерЦехозахода.Доступность = Истина;
	Иначе
		Элементы.НомерЦехозахода.Доступность = Ложь;
	КонецЕсли;
		
	
КонецПроцедуры

Функция ПолучитьСписокЭтаповМаршрутовДляЦехозахода( РеалО, СообщениеОбОшибке )
	
	Ответ = Новый СписокЗначений;
	
	//****ОЕ -- 23.05.2018
	// В Элементы.Спецификация уже содержится спецификация, по которым
	// есть сформированные документы ЭтапПроизводства2_2 для заданного подразделения
	// Дальше собираем элементы справочника ЭтапыПроизводства
	// Подразделения в документе ЭтапПроизводства и в соответсвующем элементе справочника
	// ЭтапыПроизводства должны совпадать
	// Если не совпадают - то это ошибка 
	//****ОЕ_конец
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЭтапыПроизводства.Ссылка КАК ЭтапПроизводства,
	               |	ЭтапыПроизводства.НомерЭтапа КАК НомерЭтапа,
	               |	ЭтапыПроизводства.НомерСледующегоЭтапа КАК НомерСледующегоЭтапа,
	               |	ЭтапыПроизводства.Подразделение КАК Подразделение
	               |ИЗ
	               |	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	               |ГДЕ
	               |	ЭтапыПроизводства.Владелец = &Спецификация
	               |	И НЕ ЭтапыПроизводства.ПометкаУдаления
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НомерЭтапа"; //почему-то было УБЫВ
	Запрос.УстановитьПараметр( "Спецификация", РеалО.Спецификация );
	ВыбЭтап = Запрос.Выполнить().Выбрать();
	Инд = 0;
	Исполнение = РеалО.НомерЦехозахода;
	Пока ВыбЭтап.Следующий()
	Цикл
		Инд = Инд + 1;
		Если Маршрут[Инд-1].Подразделение <> ВыбЭтап.Подразделение
		Тогда
			Ответ.Очистить();
			СообщениеОбОшибке.Текст = "Подразделение в спецификации отличается: " + Маршрут[Инд-1].Подразделение + " <> " + ВыбЭтап.Подразделение;
			СообщениеОбОшибке.КлючДанных = РеалО.Спецификация;
			Возврат Ответ;
		КонецЕсли;
		Если ВыбЭтап.Подразделение = РеалО.Подразделение
		Тогда
			Если Исполнение = 0 Тогда //если нужны все этапы цеха, закоментир это условие
				Ответ.Добавить( ВыбЭтап.ЭтапПроизводства );
			КонецЕсли;
			Исполнение = Исполнение - 1;
		КонецЕсли;
	КонецЦикла;
	
	Если Исполнение >= 0
	Тогда
		Ответ.Очистить();
		Если РеалО.НомерЦехозахода = 0
		Тогда
			СообщениеОбОшибке.Текст = "В маршруте нет подразделения  " + РеалО.Подразделение;
			СообщениеОбОшибке.КлючДанных = РеалО.Спецификация;
		Иначе
			СообщениеОбОшибке.Текст = "В спецификации число цехозаходов в " + РеалО.Подразделение + " меньше " + Строка(РеалО.НомерЦехозахода+1);
			СообщениеОбОшибке.КлючДанных = РеалО.Спецификация;
		КонецЕсли;
	КонецЕсли;
	Если Инд <> Маршрут.Количество()
	Тогда
		Ответ.Очистить();
		СообщениеОбОшибке.Текст = "Число этапов в спецификации отличается: " + Инд + " <> " + Маршрут.Количество();
		СообщениеОбОшибке.КлючДанных = РеалО.Спецификация;
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Процедура ЗаполнитьСписокЭтапов( РеалО, РеалД )

	РеалД.Строки.Очистить();
	Если Элементы.Спецификация.СписокВыбора.Количество() = 0
	Тогда
		Возврат;
	КонецЕсли;                                                                                                     
	Если Не ЗначениеЗаполнено(РеалО.Подразделение)
	Тогда
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(РеалО.НомерЦехозахода)
	Тогда
		РеалО.НомерЦехозахода = 0;
	КонецЕсли;
	
        СообщениеОбОшибке = Новый СообщениеПользователю;
	ЭтапыМаршрутов = ПолучитьСписокЭтаповМаршрутовДляЦехозахода( РеалО, СообщениеОбОшибке );
	Если ЭтапыМаршрутов.Количество() = 0
	Тогда
		СообщениеОбОшибке.Сообщить();
		Если ТипЗнч(СообщениеОбОшибке.КлючДанных) = Тип("СправочникСсылка.РесурсныеСпецификации")
		Тогда
			ВывестиВСообщенияВсеСформированныеЭтапыПоСпецификации( СообщениеОбОшибке.КлючДанных );
		КонецЕсли;	
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр( "ЭтапыМаршрутов", ЭтапыМаршрутов );
	Если ЭтапыМаршрутов.НайтиПоЗначению( Маршрут[Маршрут.Количество()-1].Этап ) = Неопределено Тогда		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЭтапПроизводства2_2.Ссылка КАК Этап,
			|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
			|	ЭтапПроизводства2_2.КоличествоУпаковокФакт КАК Выполнено,
			|	ЭтапПроизводства2_2.КоличествоУпаковокПлан КАК Запланировано,
			|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика
			|ИЗ
			|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
			|ГДЕ
			|	ЭтапПроизводства2_2.Этап В(&ЭтапыМаршрутов)
			|	И ЭтапПроизводства2_2.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
			|	И ЭтапПроизводства2_2.Проведен
			|
			|УПОРЯДОЧИТЬ ПО
			|	ЭтапПроизводства2_2.Дата
			|ИТОГИ
			|	СУММА(Выполнено),
			|	СУММА(Запланировано)
			|ПО
			|	Этап";
		Запрос.УстановитьПараметр( "ЭтапыМаршрутов", ЭтапыМаршрутов );
	Иначе
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЭтапПроизводства2_2ВыходныеИзделия.Ссылка КАК Этап,
			|	ЭтапПроизводства2_2ВыходныеИзделия.Назначение КАК Назначение,
			|	СУММА(ВЫБОР
			|			КОГДА ЭтапПроизводства2_2ВыходныеИзделия.Произведено
			|				ТОГДА ЭтапПроизводства2_2ВыходныеИзделия.Количество
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК Выполнено,
			|	СУММА(ЭтапПроизводства2_2ВыходныеИзделия.Количество) КАК Запланировано,
			|	ЭтапПроизводства2_2ВыходныеИзделия.Характеристика КАК Характеристика
			|ИЗ
			|	Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ЭтапПроизводства2_2ВыходныеИзделия
			|ГДЕ
			|	НЕ ЭтапПроизводства2_2ВыходныеИзделия.Отменено
			|	И ЭтапПроизводства2_2ВыходныеИзделия.Ссылка.Этап В(&ЭтапыМаршрутов)
			|	И ЭтапПроизводства2_2ВыходныеИзделия.Ссылка.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
			|	И ЭтапПроизводства2_2ВыходныеИзделия.Ссылка.Проведен
			|
			|СГРУППИРОВАТЬ ПО
			|	ЭтапПроизводства2_2ВыходныеИзделия.Ссылка,
			|	ЭтапПроизводства2_2ВыходныеИзделия.Назначение,
			|	ЭтапПроизводства2_2ВыходныеИзделия.Характеристика
			|
			|УПОРЯДОЧИТЬ ПО
			|	ЭтапПроизводства2_2ВыходныеИзделия.Ссылка.Дата,
			|	ЭтапПроизводства2_2ВыходныеИзделия.Назначение УБЫВ
			|ИТОГИ
			|	СУММА(Выполнено),
			|	СУММА(Запланировано)
			|ПО
			|	Этап";
	КонецЕсли;
	
	ВыбЭтапы = Запрос.Выполнить().Выбрать( ОбходРезультатаЗапроса.ПоГруппировкам );
	Пока ВыбЭтапы.Следующий()
	Цикл
		СДЗЭтап = РеалД.Строки.Добавить();
		СДЗЭтап.Этап = ВыбЭтапы.Этап;
		СДЗЭтап.Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		СДЗЭтап.Запланировано = ВыбЭтапы.Запланировано;
		СДЗЭтап.Недодел = ВыбЭтапы.Запланировано - ВыбЭтапы.Выполнено;
		СДЗЭтап.Распределено = 0;
		СДЗЭтап.СтрокаЭтапа = Истина;
		СДЗЭтап.ЕстьНазначение = Ложь;             
		
		ВыбНазн = ВыбЭтапы.Выбрать();
		Пока ВыбНазн.Следующий()
		Цикл
			СДЗНазн = СДЗЭтап.Строки.Добавить();
			СДЗНазн.Назначение = ВыбНазн.Назначение;
			СДЗНазн.Характеристика = ВыбНазн.Характеристика;
			СДЗНазн.Запланировано = ВыбНазн.Запланировано;
			СДЗНазн.Недодел = ВыбНазн.Запланировано - ВыбНазн.Выполнено;
			СДЗНазн.Распределено = 0;
			
			СДЗНазн.СтрокаЭтапа = Ложь;
			СДЗНазн.ЕстьНазначение = ЗначениеЗаполнено(ВыбНазн.Назначение);
		КонецЦикла;
	
	КонецЦикла;	
	
	РеалО.Количество = 0;
	
КонецПроцедуры

Процедура ОчиститьСписокСпецификаций( РеалО )
	РеалО.Спецификация = Справочники.РесурсныеСпецификации.ПустаяСсылка();
	Элементы.Спецификация.СписокВыбора.Очистить();;
	Маршрут.Очистить();
	ОчиститьСписокЦехозаходов( РеалО );
КонецПроцедуры	

Процедура ОчиститьСписокЦехозаходов( РеалО )
	РеалО.НомерЦехозахода = 0;
	Элементы.НомерЦехозахода.СписокВыбора.Очистить();
	ДеревоЭтапыНазначения.ПолучитьЭлементы().Очистить();
КонецПроцедуры

Процедура ЗаполнитьСписокСпецификаций( РеалО, РеалД )
	
	Если Не ЗначениеЗаполнено(РеалО.Номенклатура)
	Тогда
		ОчиститьСписокСпецификаций( РеалО );
		РеалД.Строки.Очистить();
		РеалО.Количество = 0;
		Возврат;
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	ЭтапПроизводства2_2.Спецификация КАК Спецификация
		               |ИЗ
		               |	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК РесурсныеСпецификацииВыходныеИзделия
		               |		ПО ЭтапПроизводства2_2.Спецификация = РесурсныеСпецификацииВыходныеИзделия.Ссылка
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВозвратныеОтходы КАК РесурсныеСпецификацииВозвратныеОтходы
		               |		ПО ЭтапПроизводства2_2.Спецификация = РесурсныеСпецификацииВозвратныеОтходы.Ссылка
		               |ГДЕ
		               |	ЭтапПроизводства2_2.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
		               |	И (РесурсныеСпецификацииВыходныеИзделия.Номенклатура = &Номенклатура
		               |			ИЛИ РесурсныеСпецификацииВозвратныеОтходы.Номенклатура = &Номенклатура)
		               |	И (&Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		               |			ИЛИ &Подразделение = ЭтапПроизводства2_2.Подразделение)
		               |	И НЕ ЭтапПроизводства2_2.ПометкаУдаления";
		Запрос.УстановитьПараметр( "Подразделение", РеалО.Подразделение );
		Запрос.УстановитьПараметр( "Номенклатура", РеалО.Номенклатура );
		ТЗ = Запрос.Выполнить().Выгрузить();
		
		Ном = 0;
		Пока Ном < Элементы.Спецификация.СписокВыбора.Количество()
		Цикл
			Если ТЗ.Найти( Элементы.Спецификация.СписокВыбора[Ном] ) = Неопределено
		        Тогда
				Элементы.Спецификация.СписокВыбора.Удалить( Ном );
			Иначе
				Ном = Ном + 1;
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого СТЗ Из ТЗ
		Цикл
			Если Элементы.Спецификация.СписокВыбора.НайтиПоЗначению( СТЗ.Спецификация ) = Неопределено
			Тогда
				Элементы.Спецификация.СписокВыбора.Добавить( СТЗ.Спецификация );
			КонецЕсли;
		КонецЦикла;
		
		Если Элементы.Спецификация.СписокВыбора.НайтиПоЗначению( РеалО.Спецификация ) = Неопределено И
			Элементы.Спецификация.СписокВыбора.Количество() > 0
		Тогда
			РеалО.Спецификация = Элементы.Спецификация.СписокВыбора[0].Значение;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


Процедура ЗаполнитьМаршрут( РеалО )
	
	ЗначениеВРеквизитФормы( ЭТП.ПолучитьТехнологическийМаршрут( РеалО.Спецификация ), "Маршрут" );
	
КонецПроцедуры
	

Функция ВыгрузитьТЧОбеспечениеМатериаламиИРаботами( ДокСсылка )
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка КАК Ссылка,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.НомерСтроки КАК НомерСтроки,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура КАК Номенклатура,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Характеристика КАК Характеристика,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Серия КАК Серия,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Упаковка КАК Упаковка,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Количество КАК Количество,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.ВариантОбеспечения КАК ВариантОбеспечения,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.ДатаОтгрузки КАК ДатаОтгрузки,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Склад КАК Склад,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Производится КАК Производится,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Спецификация КАК Спецификация,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.СтатусУказанияСерийОтправитель КАК СтатусУказанияСерийОтправитель,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.СтатусУказанияСерийПолучатель КАК СтатусУказанияСерийПолучатель,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.КодСтроки КАК КодСтроки,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.ИспользуетсяАвтовыбор КАК ИспользуетсяАвтовыбор,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.СведенияАвтовыбора КАК СведенияАвтовыбора,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.ПрименениеМатериала КАК ПрименениеМатериала,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.КлючСвязиСпецификация КАК КлючСвязиСпецификация,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура.ЕдиницаИзмерения КАК НоменклатураЕдиницаИзмерения,
	|	ПРЕДСТАВЛЕНИЕ(ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура.ЕдиницаИзмерения) КАК НоменклатураЕдиницаИзмеренияПредставление,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура.ЕдиницаИзмерения.ЕдиницаИзмерения КАК НоменклатураБазоваяЕдиницаИзмерения
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами
	|ГДЕ
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр( "Ссылка", ДокСсылка );
	Ответ = Запрос.Выполнить().Выгрузить();

	Возврат Ответ;
	
КонецФункции

Функция ВыгрузитьТЧВыходныеИзделия( ДокСсылка )
	
	Запрос = Новый Запрос;
	//Запрос.Текст = "ВЫБРАТЬ
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.Ссылка КАК Ссылка,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.НомерСтроки КАК НомерСтроки,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.Номенклатура КАК Номенклатура,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.Характеристика КАК Характеристика,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.КоличествоУпаковок КАК КоличествоУпаковок,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.Количество КАК Количество,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.Упаковка КАК Упаковка,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.Получатель КАК Получатель,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.Назначение КАК Назначение,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.Серия КАК Серия,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.Произведено КАК Произведено,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.ДатаПроизводства КАК ДатаПроизводства,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.СтатусУказанияСерий КАК СтатусУказанияСерий,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.СтатусУказанияСерийОтправитель КАК СтатусУказанияСерийОтправитель,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.СтатусУказанияСерийПолучатель КАК СтатусУказанияСерийПолучатель,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.ДоляСтоимости КАК ДоляСтоимости,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.СписатьНаРасходы КАК СписатьНаРасходы,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.СтатьяРасходов КАК СтатьяРасходов,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.АналитикаРасходов КАК АналитикаРасходов,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.ВидЗапасов КАК ВидЗапасов,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.ИдентификаторСтроки КАК ИдентификаторСтроки,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.КодСтроки КАК КодСтроки,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.СчетУчета КАК СчетУчета,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.Субконто1 КАК Субконто1,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.Субконто2 КАК Субконто2,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.Субконто3 КАК Субконто3,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.ПередатьДавальцу КАК ПередатьДавальцу,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.ЭтапПотребитель КАК ЭтапПотребитель,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.Номенклатура.ЕдиницаИзмерения КАК НоменклатураЕдиницаИзмерения,
	//               |	ПРЕДСТАВЛЕНИЕ(ЭтапПроизводства2_2ВыходныеИзделия.Номенклатура.ЕдиницаИзмерения) КАК НоменклатураЕдиницаИзмеренияПредставление,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.Номенклатура.ЕдиницаИзмерения.ЕдиницаИзмерения КАК НоменклатураБазоваяЕдиницаИзмерения,
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.Отменено КАК Отменено
	//               |ИЗ
	//               |	Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ЭтапПроизводства2_2ВыходныеИзделия
	//               |ГДЕ
	//               |	ЭтапПроизводства2_2ВыходныеИзделия.Ссылка = &Ссылка";
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЭтапПроизводства2_2ВыходныеИзделия.*,
	               |	ЭтапПроизводства2_2ВыходныеИзделия.Номенклатура.ЕдиницаИзмерения КАК НоменклатураЕдиницаИзмерения,
	               |	ПРЕДСТАВЛЕНИЕ(ЭтапПроизводства2_2ВыходныеИзделия.Номенклатура.ЕдиницаИзмерения) КАК НоменклатураЕдиницаИзмеренияПредставление,
	               |	ЭтапПроизводства2_2ВыходныеИзделия.Номенклатура.ЕдиницаИзмерения.ЕдиницаИзмерения КАК НоменклатураБазоваяЕдиницаИзмерения
	               |ИЗ
	               |	Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ЭтапПроизводства2_2ВыходныеИзделия
	               |ГДЕ
	               |	ЭтапПроизводства2_2ВыходныеИзделия.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр( "Ссылка", ДокСсылка);
	Ответ = Запрос.Выполнить().Выгрузить();

	Возврат Ответ;
	
КонецФункции

&НаСервере
Процедура ПланИНедоделыНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЭтапПроизводства2_2.Ссылка КАК ДокЭтап,
	               |	ЭтапПроизводства2_2.ИдентификаторЦепочки КАК ИдентификаторЦепочки,
	               |	ЭтапПроизводства2_2.Назначение КАК Назначение,
	               |	ЭтапПроизводства2_2ВыходныеИзделия.Номенклатура КАК Номенклатура,
	               |	НАЧАЛОПЕРИОДА(ЭтапПроизводства2_2.Распоряжение.ДатаПотребности, МЕСЯЦ) КАК МесяцПлана,
	               |	ЭтапПроизводства2_2.Этап.НомерСледующегоЭтапа КАК ЭтапНомерСледующегоЭтапа
	               |ПОМЕСТИТЬ ВТЭтапы
	               |ИЗ
	               |	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ЭтапПроизводства2_2ВыходныеИзделия
	               |		ПО ЭтапПроизводства2_2.Ссылка = ЭтапПроизводства2_2ВыходныеИзделия.Ссылка
	               |ГДЕ
	               |	ЭтапПроизводства2_2.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
	               |	И НЕ ЭтапПроизводства2_2.ПометкаУдаления
	               |	И ЭтапПроизводства2_2.Подразделение = &Подразделение
	               |	И ЭтапПроизводства2_2.Этап.НомерСледующегоЭтапа = 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТЭтапы.ДокЭтап КАК ДокЭтап,
	               |	ВТЭтапы.ИдентификаторЦепочки КАК ИдентификаторЦепочки,
	               |	ВТЭтапы.Назначение КАК Назначение,
	               |	ВТЭтапы.Номенклатура КАК Номенклатура,
	               |	ВТЭтапы.МесяцПлана КАК МесяцПлана,
	               |	ВТЭтапы.ЭтапНомерСледующегоЭтапа КАК ЭтапНомерСледующегоЭтапа,
	               |	ВЫБОР
	               |		КОГДА НЕ ЭтапПроизводства2_2ВыходныеИзделия.Произведено И НЕ ЭтапПроизводства2_2ВыходныеИзделия.Отменено
	               |			ТОГДА ЭтапПроизводства2_2ВыходныеИзделия.Количество
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Количество
	               |ИЗ
	               |	ВТЭтапы КАК ВТЭтапы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ЭтапПроизводства2_2ВыходныеИзделия
	               |		ПО ВТЭтапы.ДокЭтап = ЭтапПроизводства2_2ВыходныеИзделия.Ссылка
	               |ГДЕ
	               |	ВТЭтапы.ЭтапНомерСледующегоЭтапа = 0
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТЭтапы.ДокЭтап,
	               |	ВТЭтапы.ИдентификаторЦепочки,
	               |	ВТЭтапы.Назначение,
	               |	ВТЭтапы.Номенклатура,
	               |	ВТЭтапы.МесяцПлана,
	               |	ВТЭтапы.ЭтапНомерСледующегоЭтапа,
	               |	ВЫБОР
	               |		КОГДА НЕ ЭтапПроизводства2_2Трудозатраты.Выполнено
	               |				И ЭтапПроизводства2_2Трудозатраты.ВидРабот = &ПереданоНаСледЭтап
	               |			ТОГДА ЭтапПроизводства2_2Трудозатраты.Количество
	               |		ИНАЧЕ 0
	               |	КОНЕЦ
	               |ИЗ
	               |	ВТЭтапы КАК ВТЭтапы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.Трудозатраты КАК ЭтапПроизводства2_2Трудозатраты
	               |		ПО ВТЭтапы.ДокЭтап = ЭтапПроизводства2_2Трудозатраты.Ссылка
	               |ГДЕ
	               |	ВТЭтапы.ЭтапНомерСледующегоЭтапа <> 0";
	//Запрос.УстановитьПараметр( "", );
	Выб = Запрос.Выполнить().Выбрать();
	Пока Выб.Следующий()
	Цикл
	
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура ПланИНедоделы(Команда)
	ПланИНедоделыНаСервере();
КонецПроцедуры

&НаСервере
Процедура СписокНоменклатурыКПроизводствуНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЭтапПроизводства2_2ВыходныеИзделия.Номенклатура.Артикул КАК Артикул,
	|	ЭтапПроизводства2_2ВыходныеИзделия.Номенклатура КАК Номенклатура
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ЭтапПроизводства2_2ВыходныеИзделия
	|		ПО ЭтапПроизводства2_2.ИдентификаторЦепочки = ЭтапПроизводства2_2ВыходныеИзделия.Ссылка
	|ГДЕ
	|	ЭтапПроизводства2_2.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
	|	И ЭтапПроизводства2_2.Подразделение = &Подразделение
	|	И НЕ ЭтапПроизводства2_2.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура
	|АВТОУПОРЯДОЧИВАНИЕ";
	Запрос.УстановитьПараметр( "Подразделение", Объект.Подразделение );
	ТЗ = Запрос.Выполнить().Выгрузить();
	
	СТЗ = ТЗ.ВыбратьСтроку();
	
	
	Если СТЗ <> Неопределено
	Тогда
		Объект.Номенклатура = СТЗ.Номенклатура;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыКПроизводству(Команда)
	//СписокНоменклатурыКПроизводствуНаСервере();
	
	СПФ = Новый Структура;
	СПФ.Вставить( "Подразделение", Объект.Подразделение );
	
	Оповещение = Новый ОписаниеОповещения("ОбработкаОповещенияОВыборе", ЭтотОбъект);
	
	ОткрытьФорму( "ВнешняяОбработка.АРМ_ЛокальныйДиспетчер.Форма.ФормаПодбораНоменклатуры", СПФ, Объект,,,,Оповещение );
	
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаОповещенияОВыборе(РезультатЗакрытия,ДопПараметры) Экспорт

	Если ЗначениеЗаполнено(РезультатЗакрытия)
	Тогда
		Объект.Номенклатура = РезультатЗакрытия;
		НоменклатураПриИзменении(Элементы.Номенклатура);
		Объект.Количество = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДерево()
 	КоллекцияЭлементовДерева = ДеревоЭтапыНазначения.ПолучитьЭлементы();
	Для Каждого Строка Из КоллекцияЭлементовДерева 
	Цикл    
		ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
		Элементы.Этапы.Развернуть(ИдентификаторСтроки);
	КонецЦикла;   	
КонецПроцедуры

//&НаКлиенте
//Процедура КоличествоПриИзменении(Элемент)
//	ЭтаФорма.ТекущийЭлемент = ЭтаФорма.Элементы.Количество;
//	ВыполнитьРаспределениеПоЭтапам();
//КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьРаспределениеПоЭтапам()
	Если Элементы.Спецификация.СписокВыбора.Количество() <> 0
	Тогда
		Ответ = РаспределитьПоЭтапамНаСервере();
	КонецЕсли;       
	РазвернутьДерево();
	ОбновитьИтоги();
	Если Ответ = Истина
	Тогда
		Объект.Количество = 0;
	ИначеЕсли Ответ = "Количество!"
	Тогда
		ЭтаФорма.ТекущийЭлемент = ЭтаФорма.Элементы.Количество;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыРаспределеноПриИзменении(Элемент)

	ПересчитатьРаспределенноеКоличество();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИтоги()
	
	Запланировано = 0;
	Недодел = 0;
	Распределено = 0;
	Для Каждого СДЗ Из ДеревоЭтапыНазначения.ПолучитьЭлементы()
	Цикл
		Запланировано = Запланировано + СДЗ.Запланировано;
		Недодел = Недодел + СДЗ.Недодел;
		Распределено = Распределено + СДЗ.Распределено;
	КонецЦикла;
	
	Элементы.ЭтапыЗапланировано.ТекстПодвала = Формат( Запланировано, "ЧЦ=15; ЧДЦ=2" );
	Элементы.ЭтапыНедодел.ТекстПодвала = Формат( Недодел, "ЧЦ=15; ЧДЦ=2" );
	Элементы.ЭтапыРаспределено.ТекстПодвала = Формат( Распределено, "ЧЦ=15; ЧДЦ=2" );
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьРаспределенноеКоличество()
	
	ТекДанные = Элементы.Этапы.ТекущиеДанные;
	Если ТекДанные.СтрокаЭтапа = Истина
	Тогда
		// Восстанавливаем количество, в этой строке это поле менять нельзя
	        Распределено = 0;
		Для Каждого СДЗНазн Из ТекДанные.ПолучитьЭлементы()
		Цикл
	        	Распределено = Распределено + СДЗНазн.Распределено;
		КонецЦикла;
		ТекДанные.Распределено = Распределено;
		Возврат;
	ИначеЕсли ТекДанные.Распределено < 0
	Тогда
		ТекДанные.Распределено = 0;
	КонецЕсли;
	
	РодСтрока = ТекДанные.ПолучитьРодителя();
	Распределено = 0;
	Для Каждого СДЗНазн Из РодСтрока.ПолучитьЭлементы()
	Цикл
		Распределено = Распределено + СДЗНазн.Распределено;
	КонецЦикла;
	РодСтрока.Распределено = Распределено;
	ОбновитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыПередУдалением(Элемент, Отказ)
	ТекДанные = Элементы.Этапы.ТекущиеДанные;
	Если ТекДанные.СтрокаЭтапа = Ложь
	Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыРаспределеноИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыПередНачаломИзменения(Элемент, Отказ)
	ТекДанные = Элементы.Этапы.ТекущиеДанные;
	Если ТекДанные.СтрокаЭтапа = Истина И 
		Элементы.Этапы.ТекущийЭлемент.Имя = "ЭтапыРаспределено"
	Тогда
		Отказ = Истина;
	ИначеЕсли ТекДанные.СтрокаЭтапа = Ложь И 
		ТекДанные.Недодел = 0
	Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПереместитьНаСервере( ДанныеФормы )
	
	Штука = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду("796");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура.Артикул КАК Артикул,
	|	ТоварыНаСкладахОстатки.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладахОстатки.Характеристика КАК Характеристика,
	|	ТоварыНаСкладахОстатки.Номенклатура.ВидНоменклатуры.ИспользоватьХарактеристики КАК ХарактеристикиИспользуются,
	|	ТоварыНаСкладахОстатки.Назначение КАК Назначение,
	|	ТоварыНаСкладахОстатки.Назначение КАК НазначениеОтправителя,
	|	ТоварыНаСкладахОстатки.ВНаличииОстаток КАК Количество,
	|	ТоварыНаСкладахОстатки.ВНаличииОстаток КАК КоличествоУпаковок
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад = &СкладОтправитель) КАК ТоварыНаСкладахОстатки
	|ГДЕ
	|	ТоварыНаСкладахОстатки.ВНаличииОстаток > 0";
	Если Объект.Подразделение.Наименование = "14 цех"
	Тогда
		СкладОтправитель = Справочники.Склады.НайтиПоНаименованию("14 цех кладовая ГП");
	Иначе
		СкладОтправитель = Справочники.Склады.НайтиПоНаименованию( СокрП(Объект.Подразделение.Наименование) + " кладовая Д" );
	КонецЕсли;
	ДанныеФормы.СкладОтправитель = СкладОтправитель;
	ДанныеФормы.Дата = ДатаРасходаИВыпуска;
	
	Запрос.УстановитьПараметр( "СкладОтправитель",  СкладОтправитель);
	Выб = Запрос.Выполнить().Выбрать();
	Пока Выб.Следующий()
	Цикл
		СТЧ = ДанныеФормы.Товары.Добавить();
		ЗаполнитьЗначенияСвойств( СТЧ, Выб );
		//СТЧ.Упаковка = Штука;
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура Переместить(Команда)
	
	Форма = ПолучитьФорму("Документ.ПеремещениеТоваров.Форма.ФормаДокумента" );
	ДанныеФормы = Форма.Объект;
	ПереместитьНаСервере( ДанныеФормы );
	
	КопироватьДанныеФормы(ДанныеФормы, Форма.Объект);
	
	Форма.Открыть();

КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыОткрытияВедомостиТоваровПоСкладам()
	
	ОтчетОбъект = Отчеты.ВедомостьПоТоварамНаСкладах.Создать();
	КомпоновщикНастроек = ОтчетОбъект.КомпоновщикНастроек;
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	Если ЗначениеЗаполнено(ДатаРасходаИВыпуска)
	Тогда
		ПериодОтчета = Новый СтандартныйПериод( НачалоМесяца(ДатаРасходаИВыпуска), КонецМесяца(ДатаРасходаИВыпуска) );
	Иначе
		ПериодОтчета =Новый СтандартныйПериод( ВариантСтандартногоПериода.ЭтотМесяц );
	КонецЕсли;
	Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Период", ПериодОтчета );
	КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	ПользовательскиеНастройки = КомпоновщикНастроек.ПользовательскиеНастройки;
	
	КлючВарианта = Неопределено;
	СписокВариантов = ХранилищеВариантовОтчетов.ПолучитьСписок("Отчет.ВедомостьПоТоварамНаСкладах");	
	Для Каждого Вариант Из СписокВариантов
	Цикл
		Если Вариант.Представление = "Ведомость по товарам на складах - Подробно"
		Тогда
			КлючВарианта = Вариант.Значение;
		КонецЕсли;
	КонецЦикла;
	Если КлючВарианта = Неопределено
	Тогда 
		Сооб = Новый СообщениеПользователю;
		Сооб.Текст = "Не найден вариант отчета ""Ведомость по товарам на складах - Подробно""";
		Сооб.Сообщить();
	КонецЕсли;
		
	Если Объект.Подразделение.Наименование <> "14 цех"
	Тогда
		ФиксированныйОтбор = Новый Структура( "Склад", 
			Справочники.Склады.НайтиПоНаименованию( Объект.Подразделение.Наименование + " кладовая Д") );
	Иначе
		МассивСкладов = Новый Массив;
		МассивСкладов.Добавить( Справочники.Склады.НайтиПоНаименованию( Объект.Подразделение.Наименование + " кладовая Д") );
		МассивСкладов.Добавить( Справочники.Склады.НайтиПоНаименованию( Объект.Подразделение.Наименование + " кладовая ГП") );
		ФиксированныйОтбор = Новый Структура( "Склад", МассивСкладов );
	КонецЕсли;
	
	
	
	Ответ = Новый Структура(
		"СформироватьПриОткрытии, Отбор, КлючВарианта, ПользовательскиеНастройки", 
			Истина, 
			ФиксированныйОтбор,
			КлючВарианта,
			ПользовательскиеНастройки);
	
	Возврат Ответ;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьВедомостьПоТоварамНаСкладах(Команда)
	
	ОткрытьФорму("Отчет.ВедомостьПоТоварамНаСкладах.Форма", 
				ПолучитьПараметрыОткрытияВедомостиТоваровПоСкладам(),
				ЭтаФорма,
				"АРМ_ЛокальныйДиспетчер");
	
КонецПроцедуры

&НаСервере
Процедура ВывестиВСообщенияВсеСформированныеЭтапыПоСпецификации( Спец )
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЭтапПроизводства2_2.Ссылка КАК Этап,
	|	ЭтапПроизводства2_2.НаименованиеЭтапа КАК ЭтапПредставление,
	|	ЭтапПроизводства2_2.Подразделение.Наименование КАК ЭтапПодразделениеПредставление
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	|ГДЕ
	|	ЭтапПроизводства2_2.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
	|	И НЕ ЭтапПроизводства2_2.ПометкаУдаления
	|	И ЭтапПроизводства2_2.Спецификация = &Спецификация
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтапПроизводства2_2.Спецификация,
	|	ЭтапПроизводства2_2.Этап.НомерЭтапа";
	Запрос.УстановитьПараметр( "Спецификация", Спец );
	Выб = Запрос.Выполнить().Выбрать();
	Пока Выб.Следующий()
	Цикл
		Сооб = Новый СообщениеПользователю;
		Сооб.Текст = "--  " + Выб.ЭтапПредставление + ", " + Выб.ЭтапПодразделениеПредставление;
		Сооб.КлючДанных = Выб.Этап;
		Сооб.Сообщить();
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПроверитьЗаполнениеПоляЦех( РеалО )
	
	Найдено = Ложь;
	Для Каждого СТЗ Из Маршрут
	Цикл
		Если СТЗ.Подразделение = РеалО.Подразделение
		Тогда
			Найдено = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не Найдено
	Тогда
		Если РеалО.Подразделение = Справочники.СтруктураПредприятия.ПустаяСсылка() И
			Маршрут.Количество() = 1
		Тогда
			РеалО.Подразделение = Маршрут[0].Подразделение;
		Иначе
			РеалО.Подразделение = Справочники.СтруктураПредприятия.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОформитьБезТрудозатрат = Истина;    
	ДатаРасходаИВыпуска = НачалоДня(ТекущаяДатаСеанса());   
КонецПроцедуры

&НаКлиенте
Процедура Оформить(Команда)
	Если Не ЗначениеЗаполнено(ДатаРасходаИВыпуска)
	Тогда
		ЭтаФорма.ТекущийЭлемент = ЭтаФорма.Элементы.ДатаОперации;
		Возврат;
	КонецЕсли;
	
	Если Элементы.Этапы.ТекущиеДанные.СтрокаЭтапа
	Тогда
		СДЗ = Элементы.Этапы.ТекущиеДанные;
	Иначе
		СДЗ = Элементы.Этапы.ТекущиеДанные.ПолучитьРодителя();
	КонецЕсли;

	П = ОформитьНаСервере(СДЗ.ПолучитьИдентификатор());
	Если П = Неопределено тогда
		Возврат;
	ИначеЕсли ТипЗнч(П) = Тип("Структура") тогда	
		Форма = ОткрытьФорму("Документ.ЭтапПроизводства2_2.ФормаОбъекта", П );
	Иначе       
		УстановитьСохранено(Ложь);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ОформитьНаСервере( ИдентификаторСтрокиДерева )
	СоответствиеНазначений = Новый Соответствие;
	СДЗЭтап = ДеревоЭтапыНазначения.НайтиПоИдентификатору( ИдентификаторСтрокиДерева );
	Распределено = 0;
	Для Каждого СДЗНазн Из СДЗЭтап.ПолучитьЭлементы()
	Цикл
		Распределено = Распределено + СДЗНазн.Распределено;
		СоответствиеНазначений.Вставить(СДЗНазн.Назначение, СДЗНазн.Распределено);
	КонецЦикла;
	Если Распределено = 0
	Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Док = СДЗЭтап.Этап.ПолучитьОбъект();
	Если Док.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.Завершен
	Тогда
		Сооб = Новый СообщениеПользователю;
		Сооб.Текст = "Этап завершен, расход уже не корректируется";
		Сооб.Сообщить();
		Возврат Неопределено;
	КонецЕсли;
	
	Если Док.НомерСледующегоЭтапа = 0 Тогда
		КВыпуску = СоответствиеНазначений;
	Иначе
		КВыпуску = Распределено;
	КонецЕсли; 
	Объ = РеквизитФормыВЗначение("Объект");
	СборникТаблиц = Обработки.АРМ_ЛокальныйДиспетчер.ОформитьВыпуск(Док, ДатаРасходаИВыпуска, КВыпуску, ОформитьБезТрудозатрат, Объ);
	ЗаполнитьКолонкиТаблиц(СборникТаблиц); 
	ЗначениеВРеквизитФормы(Объ, "Объект");
	
	Если НЕ Док.Модифицированность() тогда
		Возврат Неопределено;
	ИначеЕсли СборникТаблиц.ТаблицаЭтапов.Количество() = 1 тогда
		П = Новый Структура;
		П.Вставить("НесохраненныйДокумент",ЭТП.СохранитьОбъектВХранилище(Док));
		П.Вставить("Ключ", Док.Ссылка);
		Возврат П;
	Иначе
		Возврат СборникТаблиц.ТаблицаЭтапов.Количество();
	КонецЕсли;		
КонецФункции

&НаСервере
Процедура ЗаполнитьКолонкиТаблиц(СборникТаблиц)  
	МасРекв = Новый Массив;
	Для каждого Стр из СборникТаблиц.ТаблТабл цикл
		ПолноеИмя = "Объект." + Стр.Имя;
		Если ПолучитьРеквизиты(ПолноеИмя).Количество() = 0 тогда  
			Для каждого Кол из Стр.Таблица.Колонки цикл   
				Рекв = Новый РеквизитФормы(Кол.Имя, Кол.ТипЗначения, ПолноеИмя);
				МасРекв.Добавить(Рекв);
			КонецЦикла;
		КонецЕсли; 
	КонецЦикла;
	Если МасРекв.Количество() > 0 тогда  
		ИзменитьРеквизиты(МасРекв);
	КонецЕсли;   
КонецПроцедуры   

&НаСервере
Функция Таблица1ЭтапОткрытиеНаСервере(ЭтапСсылка)
	Док = ЭтапСсылка.ПолучитьОбъект(); 
	СборникТаблиц = Обработки.АРМ_ЛокальныйДиспетчер.СоздатьСборникТаблиц(Объект);
	Обработки.АРМ_ЛокальныйДиспетчер.ТаблицыВЭтап(Док, СборникТаблиц);
	П = Новый Структура;
	П.Вставить("НесохраненныйДокумент",ЭТП.СохранитьОбъектВХранилище(Док));
	П.Вставить("Ключ", ЭтапСсылка);
	П.Вставить("АРМ_ЛокальныйДиспетчер", Истина);
	Возврат П;
КонецФункции

&НаКлиенте
Процедура Таблица1ЭтапОткрытие(Элемент, СтандартнаяОбработка)  
	СтандартнаяОбработка = Ложь;
	ОткрытьЭтап(Неопределено);
КонецПроцедуры

&НаСервере
Функция ПровестиНаСервере(РеальноПроводить)
	Возврат Обработки.АРМ_ЛокальныйДиспетчер.ПровестиЭтапыФоново(РеквизитФормыВЗначение("Объект"), УникальныйИдентификатор, РеальноПроводить);
КонецФункции

&НаКлиенте
Процедура ПопытатьсяПровести(Команда) 
	Провести1(Ложь);
КонецПроцедуры  

&НаКлиенте
Процедура Провести1(РеальноПроводить) 
	ФП = ПровестиНаСервере(РеальноПроводить);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
    ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
    ПараметрыОжидания.Интервал = 2;
    ДлительныеОперацииКлиент.ОжидатьЗавершение(ФП, 
		Новый ОписаниеОповещения("ПроведениеФоновоВыполнено", ЭтотОбъект, РеальноПроводить), ПараметрыОжидания);
КонецПроцедуры  

&НаКлиенте
Процедура ПроведениеФоновоВыполнено(Результат, ДополнительныеПараметры) Экспорт
	Успешно = Ложь;
    Если Результат = Неопределено Тогда
        Возврат;
    ИначеЕсли Результат.Статус = "Ошибка" Тогда
        ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
	ИначеЕсли Результат.Статус = "Выполнено" Тогда 
		Успешно = Истина;
        //ОбработатьРезультат(Результат.АдресРезультата);
	КонецЕсли;
	Если ДополнительныеПараметры тогда
		Заг = "Результат проведения"; 
		Стр = "Этапы успешно проведены";
	Иначе
		Заг = "Результат проверки"; 
		Стр = "Этапы успешно проводятся";
	КонецЕсли;
	Если НЕ Успешно тогда
		Стр = "Ошибка проведения этапов";
	КонецЕсли;
    ПоказатьПредупреждение(,Стр,,Заг);
	Если ДополнительныеПараметры И Успешно тогда
		УстановитьСохранено(Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСохранено(Зн)
	Элементы.КнопкаПопытатьсяПровести.Доступность = НЕ Зн;
	Элементы.КнопкаПровести.Доступность = НЕ Зн;
	Если Зн тогда
		
	Иначе
		Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.Результат;	
		Элементы.Группа1.Доступность = Зн;
		Элементы.Группа2.Доступность = Зн;
	КонецЕсли;
	Сохранено = Зн;
КонецПроцедуры 

&НаСервере
Процедура ОбработкаОповещенияНаСервере(Параметр)
	Док = Параметр.Ссылка.ПолучитьОбъект();
	ЭТП.ВзятьОбъектИзХранилища(Док, Параметр.Адрес);
	СборникТаблиц = Обработки.АРМ_ЛокальныйДиспетчер.СоздатьСборникТаблиц(Объект);
	Обработки.АРМ_ЛокальныйДиспетчер.ЭтапВТаблицы(Док, СборникТаблиц); 
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)  
	Если ИмяСобытия = "АРМ_ЛокальныйДиспетчерИзменениеДокумента" тогда
		ОбработкаОповещенияНаСервере(Параметр); 
		УстановитьСохранено(Ложь);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Провести(Команда)
	Провести1(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьСохранено(Истина);
КонецПроцедуры


&НаКлиенте
Процедура ЗакрытьРезультат(Команда)
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.КОформлению;	
	Элементы.Группа1.Доступность = Истина;
	Элементы.Группа2.Доступность = Истина;
	УстановитьСохранено(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЭтап(Команда)
	П = Таблица1ЭтапОткрытиеНаСервере(Элементы.Таблица1.ТекущиеДанные.Этап);   
	Форма = ОткрытьФорму("Документ.ЭтапПроизводства2_2.ФормаОбъекта", П, ЭтаФорма, Истина);  
	Форма.ЗакрыватьПриЗакрытииВладельца = Истина;
КонецПроцедуры

