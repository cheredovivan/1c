
Функция ОформитьВыпуск(Док, ДатаВып, КВыпуску, ОформитьБезТрудозатрат = Истина, Объект) Экспорт 
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	СборникТаблиц = СоздатьСборникТаблиц(Объект);
	ЭтапыПоследователи = ЗагрузитьЭтапыПоследователи(Док.Ссылка);
	Если Док.НомерСледующегоЭтапа = 0 Тогда
		ОформитьВыполнениеЭтапаПоНазначениям(Док, ДатаВып, КВыпуску, СборникТаблиц.ТаблицаЭтапов, НЕ ОформитьБезТрудозатрат, 
			ЭтапыПоследователи, КэшированныеЗначения);
	Иначе
		ОформитьВыполнениеЭтапа(Док, ДатаВып, Док.КоличествоУпаковокФакт + КВыпуску, СборникТаблиц.ТаблицаЭтапов, НЕ ОформитьБезТрудозатрат, 
			ЭтапыПоследователи, КэшированныеЗначения);
	КонецЕсли;  
	ЗаполнитьТаблицыСборника(СборникТаблиц); 
	
	Объект.Этапы.Очистить();
	Для каждого СтрТ из СборникТаблиц.ТаблицаЭтапов цикл
		Стр = Объект.Этапы.Добавить();
		Стр.Этап = СтрТ.Ссылка;
	КонецЦикла;
	
	Возврат СборникТаблиц;
КонецФункции

//Изделия только в штуках!
Процедура ОформитьВыполнениеЭтапаПоНазначениям(Этап, ДатаВып, СоответствиеНазначений, ТаблицаЭтапов, ДелитьТрудозатраты = Ложь, 
		ЭтапыПоследователи, КэшированныеЗначения)   
	СтруктураДействий = Новый Структура("ПересчитатьКоличествоУпаковок");
	//ВыходныеИзделия     
	СтрКол = "Подразделение, Номенклатура, Характеристика";
	ТаблицаПроверка = Этап.ВыходныеИзделия.Выгрузить(,СтрКол);
	ТаблицаПроверка.Свернуть(СтрКол);
	Если ТаблицаПроверка.Количество() > 1 тогда
		ЭТП.СообщитьОшибку("Неправильная структура выпуска, "+Этап.Ссылка, Этап.Ссылка);
	КонецЕсли;  
	Запланировано = Этап.ВыходныеИзделия.Итог("Количество");
	Произведено = 0;
	Для каждого Строка Из Этап.ВыходныеИзделия Цикл
		Если Строка.Произведено тогда
			Произведено = Произведено + Строка.Количество;
		КонецЕсли;
	КонецЦикла;                     
	
	ПарамОтбора = Новый Структура("Произведено, Отменено, Назначение", Ложь, Ложь);
	ПарамОтбораСДатой = Новый Структура("Произведено, Отменено, ДатаПроизводства, Назначение", Истина, Ложь, ДатаВып);
	ПроизвестиВсего = 0;  
	
	Для каждого Пара из СоответствиеНазначений цикл 
		Произвести = Пара.Значение;  
		ПроизвестиОстаток = Произвести; 
		ПарамОтбора["Назначение"] = Пара.Ключ;
		ОтборИзделия = Этап.ВыходныеИзделия.НайтиСтроки(ПарамОтбора);
		Для каждого СтрИзделия из ОтборИзделия цикл
			ОстатокИзделий = СтрИзделия.Количество - ПроизвестиОстаток;
			Если ОстатокИзделий < 0 тогда
				Этап.ВыходныеИзделия.Удалить(СтрИзделия);
				ПроизвестиОстаток = -ОстатокИзделий;
			Иначе
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ОстатокИзделий < 0 тогда
			ЭТП.СообщитьОшибку("К выполнению назначено больше, чем запланировано по назначению " + Пара.Ключ, Этап.Ссылка);
		Иначе
			ПарамОтбораСДатой["Назначение"] = Пара.Ключ;
			ОтборИзделия = Этап.ВыходныеИзделия.НайтиСтроки(ПарамОтбораСДатой);
			Если ОтборИзделия.Количество()  > 0 тогда
				СтрНов = ОтборИзделия[0];
				СтрНов.Количество = СтрНов.Количество + Произвести;
			Иначе
				СтрНов = Этап.ВыходныеИзделия.Добавить();
				ЗаполнитьЗначенияСвойств(СтрНов, СтрИзделия,, "КодСтроки");
				СтрНов.Количество = Произвести;
			КонецЕсли;
			СтрНов.Произведено = Истина;
			СтрНов.ДатаПроизводства = ДатаВып;
			ПроизвестиВсего = ПроизвестиВсего + Произвести;
			ЭТП.ПересчитатьКоличествоУпаковокВСтрокеТЧ1(СтрНов, СтруктураДействий, КэшированныеЗначения);
			Если ОстатокИзделий = 0 тогда 
				Этап.ВыходныеИзделия.Удалить(СтрИзделия);
			Иначе
				СтрИзделия.Количество = ОстатокИзделий;
				ЭТП.ПересчитатьКоличествоУпаковокВСтрокеТЧ1(СтрИзделия, СтруктураДействий, КэшированныеЗначения);
				ПроизводствоКлиентСервер.ПересчитатьДолюСтоимостиПриРазбиенииСтроки(СтрНов, СтрИзделия, Этап.СпособРаспределенияЗатратНаВыходныеИзделия);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Запланировано > 0 И ПроизвестиВсего > 0 тогда
		Выполнено = (Произведено + ПроизвестиВсего) *Этап.КоличествоУпаковокПлан /Запланировано;
		//Выполнено = ПроизвестиВсего *Этап.Запланировано /Выб.Запланировано + Этап.Выполнено;
		ОформитьВыполнениеЭтапа(Этап, ДатаВып, Выполнено, ТаблицаЭтапов, ДелитьТрудозатраты, ЭтапыПоследователи, КэшированныеЗначения);
	КонецЕсли;
КонецПроцедуры

Процедура ОформитьВыполнениеЭтапа(Этап, ДатаВып, КоличествоУпаковокФакт, ТаблицаЭтапов, ДелитьТрудозатраты = Ложь, 
		ЭтапыПоследователи, КэшированныеЗначения)
	//Этап = Документы.ЭтапПроизводства2_2.ПустаяСсылка();	
	СтруктураДействий = Новый Структура("ПересчитатьКоличествоУпаковок");

	Если Этап.КоличествоУпаковокФакт >= КоличествоУпаковокФакт тогда
		Возврат;
	КонецЕсли;
	Если Этап.КоличествоУпаковокПлан < КоличествоУпаковокФакт тогда
		ЭТП.СообщитьОшибку("К выполнению больше, чем запланировано в этапе " + Этап.Ссылка, Этап.Ссылка);
	КонецЕсли;
	
	КоэфОстЧ = Этап.КоличествоУпаковокПлан - КоличествоУпаковокФакт;
	КоэфОстЗ = Этап.КоличествоУпаковокПлан;
	
	Если Этап.НеОтгружатьЧастями тогда
		Этап.НеОтгружатьЧастями = Ложь;
	КонецЕсли;    
	Если Этап.ПроизводствоОднойДатой тогда
		Этап.ПроизводствоОднойДатой = Ложь;
	КонецЕсли;
	Если Этап.КоличествоУпаковокФакт <> КоличествоУпаковокФакт тогда
		Этап.КоличествоУпаковокФакт = КоличествоУпаковокФакт;
	КонецЕсли;
	//Этап.ОтразитьКоличествоФактВТЧ();
	ЭТП.ОтразитьКоличествоФактВТЧ1(Этап, КэшированныеЗначения, ДелитьТрудозатраты);
	ЭТП.СжатьТабличнуюЧастьСегодня(Этап.РасходМатериаловИРабот, "ДатаРасхода", ДатаВып, 
		"Подразделение, Номенклатура, Характеристика, ДатаРасхода, Упаковка, СтатьяКалькуляции", СтруктураДействий, КэшированныеЗначения);
	ЭТП.СжатьТабличнуюЧастьСегодня(Этап.ПобочныеИзделия, "ДатаПроизводства", ДатаВып, 
		"Подразделение, Номенклатура, Характеристика, Назначение, Произведено, Отменено, ДатаПроизводства, ПричинаОтмены, Упаковка, СтатьяКалькуляции",
		СтруктураДействий, КэшированныеЗначения);
	
	ТаблицаРасход = Этап.РасходМатериаловИРабот.Выгрузить(, "Подразделение, Номенклатура, Характеристика, Количество");  
	ТаблицаРасход.Свернуть("Подразделение, Номенклатура, Характеристика", "Количество");
	ОтборОбесп = Новый Структура("ВариантОбеспечения", Перечисления.ВариантыОбеспечения.Отгрузить);
	ТаблицаОтгружено = Этап.ОбеспечениеМатериаламиИРаботами.Выгрузить(ОтборОбесп, "Подразделение, Номенклатура, Характеристика, Количество");
	ЭТП.ВычестьСтрокиИзТаблицы(ТаблицаРасход, ТаблицаОтгружено, Новый Структура("Подразделение, Номенклатура, Характеристика"), Ложь);
	ОтборОбесп = Новый Структура("ВариантОбеспечения, Подразделение, Номенклатура, Характеристика",Перечисления.ВариантыОбеспечения.КОбеспечению);
	Для каждого Стр1 из ТаблицаРасход цикл 
		ЗаполнитьЗначенияСвойств(ОтборОбесп, Стр1);
		МасСтр = Этап.ОбеспечениеМатериаламиИРаботами.НайтиСтроки(ОтборОбесп);
		Если МасСтр.Количество() = 0 тогда
			Продолжить;
		КонецЕсли;
		СтрОсталось = МасСтр[0];    
		Расход = Стр1.Количество - СтрОсталось.Количество;
		Если Расход < 0 тогда
			СтрОтгружено = Этап.ОбеспечениеМатериаламиИРаботами.Добавить();
			ЗаполнитьЗначенияСвойств(СтрОтгружено, СтрОсталось,, "КодСтроки");
			СтрОсталось.Количество = СтрОсталось.Количество - Стр1.Количество;
			ЭТП.ПересчитатьКоличествоУпаковокВСтрокеТЧ1(СтрОсталось, СтруктураДействий, КэшированныеЗначения);
		Иначе     
			СтрОтгружено = СтрОсталось;
		КонецЕсли;
		СтрОтгружено.Количество = Стр1.Количество;
		СтрОтгружено.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.Отгрузить;
		СтрОтгружено.ДатаОтгрузки = ДатаВып;
		ЭТП.ПересчитатьКоличествоУпаковокВСтрокеТЧ1(СтрОтгружено, СтруктураДействий, КэшированныеЗначения);
		
		НСтрМас = 1;
		Пока Расход > 0 И МасСтр.Количество() > НСтрМас цикл
			//в норм ситуации не должно вып, на всяк случай
			СтрОсталось = МасСтр[НСтрМас]; 
			Если СтрОсталось.Количество > Расход тогда
				СтрОсталось.Количество = СтрОсталось.Количество - Расход;
				ЭТП.ПересчитатьКоличествоУпаковокВСтрокеТЧ1(СтрОсталось, СтруктураДействий, КэшированныеЗначения);
				Прервать;
			Иначе     
				Расход = Расход - СтрОсталось.Количество;
				НСтрМас = НСтрМас + 1;
				Этап.ОбеспечениеМатериаламиИРаботами.Удалить(СтрОсталось);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	ЭТП.СжатьТабличнуюЧастьСегодня(Этап.ОбеспечениеМатериаламиИРаботами, "ДатаОтгрузки", ДатаВып, 
		"Подразделение, Номенклатура, Характеристика, ДатаОтгрузки, ВариантОбеспечения, Обособленно, Упаковка, СтатьяКалькуляции",
		СтруктураДействий, КэшированныеЗначения);
	
	//Статус этапа
	СтатусБыл = Этап.Статус;
	Если УправлениеПроизводствомКлиентСервер.СравнениеСтатусовЭтапа( Этап.Статус, Перечисления.СтатусыЭтаповПроизводства2_2.Начат) <0 тогда
		Этап.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.Начат;
		Этап.ФактическоеНачалоЭтапа = ДатаВып;
	КонецЕсли;                    
	Если Этап.КоличествоУпаковокФакт = Этап.КоличествоУпаковокПлан тогда
		Этап.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.Завершен; //!Добавить Обработку!
		Этап.ФактическоеОкончаниеЭтапа = ДатаВып;
	КонецЕсли;
	Если Этап.Статус <> СтатусБыл тогда
		ДанныеЗаполнения = Новый Структура("ДатаСобытия,ПлановаяДатаПоступления");
		ДанныеЗаполнения.ДатаСобытия = ДатаВып;
		ДанныеЗаполнения.ПлановаяДатаПоступления = Документы.ЭтапПроизводства2_2.ПлановаяДатаПоступления(Этап.Ссылка);
	    УправлениеПроизводством.ЗаполнитьРеквизитыЭтапаПриИзмененииСтатуса(Этап, СтатусБыл, ДанныеЗаполнения);
	КонецЕсли;
	Если Этап.Модифицированность() тогда 
		Стр = ТаблицаЭтапов.Найти(Этап.Ссылка, "Ссылка");
		Если Стр = Неопределено тогда
			Стр = ТаблицаЭтапов.Добавить();
			Стр.Ссылка = Этап.Ссылка;
			Стр.Объект = Этап;       
		Иначе
			ТаблицаЭтапов.Сдвинуть(Стр, ТаблицаЭтапов.Количество() - ТаблицаЭтапов.Индекс(Стр) - 1); 
		КонецЕсли;
		ЗакрытьЭтапыПредшественники(Этап, ДатаВып, ТаблицаЭтапов, ЭтапыПоследователи, КэшированныеЗначения);
	КонецЕсли;
КонецПроцедуры  

Процедура ЗакрытьЭтапыПредшественники(Этап, ДатаВып, ТаблицаЭтапов, ЭтапыПоследователи, КэшированныеЗначения)
	//Этап = Документы.ЭтапПроизводства2_2.ПустаяСсылка();
	ИмяДопРеквизита = "ЭтапЗакрыватьАвтоматически";
	
	ОтборПредшественники = ЭтапыПоследователи.НайтиСтроки(Новый Структура("Этап", Этап.Ссылка));
	Для каждого Предшественник из ОтборПредшественники цикл
		РезП = ТаблицаЭтапов.Найти(Предшественник.Ссылка);
		Если РезП = Неопределено тогда
			Этап0 = Предшественник.Ссылка.ПолучитьОбъект();
		Иначе
			Этап0 = РезП.Объект;
		КонецЕсли;
		Если Предшественник.ТипСвязи = 0 ИЛИ Этап0.ВыходныеИзделия.Количество() = 0 тогда //в т.ч. этапы с побочным выпуском
			КоличествоУпаковокФакт0 = Этап0.КоличествоУпаковокПлан * Этап.КоличествоУпаковокФакт/Этап.КоличествоУпаковокПлан;  
			Если Этап0.КоличествоУпаковокФакт < КоличествоУпаковокФакт0 тогда
				Если Этап.Подразделение = Этап0.Подразделение ИЛИ ЭТП.ДопРеквизит(Этап0.Этап, ИмяДопРеквизита) = Истина тогда
					ОформитьВыполнениеЭтапа(Этап0, ДатаВып, КоличествоУпаковокФакт0, ТаблицаЭтапов,,ЭтапыПоследователи, КэшированныеЗначения);
				Иначе
					ЭТП.СообщитьОшибку("Требуется выполнение этапа другого цеха " + Строка(Предшественник.Ссылка), Предшественник.Ссылка);
				КонецЕсли;	
			КонецЕсли;
		Иначе
			Требуется = Этап.ОбеспечениеМатериаламиИРаботами.Выгрузить(Новый Структура(
				"ВариантОбеспечения, Обособленно, Номенклатура, Назначение", Перечисления.ВариантыОбеспечения.Отгрузить,
				Истина, Этап0.ВыходныеИзделия[0].Номенклатура, Этап.Назначение), "Количество").Итог("Количество");
			Если Требуется = Неопределено тогда
				Требуется = 0;
			КонецЕсли;  
			КоличествоПроизведено = Этап0.ВыходныеИзделия.Выгрузить(Новый Структура("Произведено, Назначение", Истина, Этап.Назначение), 
				"Количество").Итог("Количество");
			Если КоличествоПроизведено = Неопределено тогда
				КоличествоПроизведено = 0;
			КонецЕсли; 
			Произвести = Требуется - КоличествоПроизведено;
			Если Произвести > 0 тогда
				Если Этап.Подразделение = Этап0.Подразделение ИЛИ ЭТП.ДопРеквизит(Этап0.Этап,ИмяДопРеквизита)=Истина тогда
					СоответствиеНазначений = Новый Соответствие;
					СоответствиеНазначений.Вставить(Этап.Назначение, Произвести);
					ОформитьВыполнениеЭтапаПоНазначениям(Этап0, ДатаВып, СоответствиеНазначений, ТаблицаЭтапов,,
						ЭтапыПоследователи, КэшированныеЗначения);
				Иначе
					ЭТП.СообщитьОшибку("Требуется выполнение этапа другого цеха " + Строка(Предшественник.Ссылка), Предшественник.Ссылка);
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;    
КонецПроцедуры     

Функция ЗагрузитьЭтапыПоследователи(знач СсылкаЭтап)
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Табл.Ссылка КАК Ссылка,
	|	Табл.Этап КАК Этап,
	|	МИНИМУМ(Табл.ТипСвязи) КАК ТипСвязи
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.Последователи КАК Табл
	|ГДЕ
	|	Табл.Ссылка.Распоряжение = ВЫРАЗИТЬ(&Этап КАК Документ.ЭтапПроизводства2_2).Распоряжение
	|	И Табл.Ссылка.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	Табл.Ссылка,
	|	Табл.Этап"); //если 0 и 1, то 0
	Запрос.УстановитьПараметр("Этап", СсылкаЭтап);
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции   

Функция СоздатьСборникТаблиц(Объект) Экспорт
	СборникТаблиц = Новый Структура;
	ТаблицаЭтапов = Новый ТаблицаЗначений;
	ТаблицаЭтапов.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ДокументСсылка.ЭтапПроизводства2_2"));
	ТаблицаЭтапов.Колонки.Добавить("Объект", Новый ОписаниеТипов("ДокументОбъект.ЭтапПроизводства2_2"));
	СборникТаблиц.Вставить("ТаблицаЭтапов", ТаблицаЭтапов);                  
	
	ТаблТабл = Новый ТаблицаЗначений; 
	ТаблТабл.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(40)));
	//Масс = Новый Массив;
	//Масс.Добавить(ТипЗнч(Объект.ЭтапыСвойства));
	ТаблТабл.Колонки.Добавить("Таблица");//, Новый ОписаниеТипов(Масс));
	Стр = ТаблТабл.Добавить();
	Стр.Имя = "ЭтапыСвойства";
	Стр.Таблица = Объект.ЭтапыСвойства;
	Стр = ТаблТабл.Добавить();
	Стр.Имя = "ВыходныеИзделия";
	Стр.Таблица = Объект.ВыходныеИзделия;
	Стр = ТаблТабл.Добавить();
	Стр.Имя = "ПобочныеИзделия";
	Стр.Таблица = Объект.ПобочныеИзделия;
	Стр = ТаблТабл.Добавить();
	Стр.Имя = "ОбеспечениеМатериаламиИРаботами";
	Стр.Таблица = Объект.ОбеспечениеМатериаламиИРаботами;
	Стр = ТаблТабл.Добавить();
	Стр.Имя = "РасходМатериаловИРабот";
	Стр.Таблица = Объект.РасходМатериаловИРабот;
	Стр = ТаблТабл.Добавить();
	Стр.Имя = "Трудозатраты";
	Стр.Таблица = Объект.Трудозатраты;
	СборникТаблиц.Вставить("ТаблТабл", ТаблТабл);                  
	
	Для каждого Стр из ТаблТабл цикл
		СборникТаблиц.Вставить(Стр.Имя, Стр.Таблица);                  
	КонецЦикла;
	Возврат СборникТаблиц;
КонецФункции

Функция СоздатьСборникТаблицСоСсылкамиЭтапов(Объект)  
	СбТ = СоздатьСборникТаблиц(Объект);
	Для каждого Стр из Объект.Этапы цикл
		СтрН = СбТ.ТаблицаЭтапов.Добавить();
		СтрН.Ссылка = Стр.Этап;
	КонецЦикла;
	Возврат СбТ;
КонецФункции

Процедура ЗаполнитьКолонкиТаблиц(СбТ)
	НадоЗаполнять = Ложь;
	Для каждого Стр из СбТ.ТаблТабл цикл
		Если Стр.Таблица.Колонки.Количество() = 0 тогда
			НадоЗаполнять = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;   
	Если НЕ НадоЗаполнять тогда
		Возврат;
	КонецЕсли;
	ВГран = СбТ.ТаблТабл.Количество() - 1;
	МассивЭтапов = СбТ.ТаблицаЭтапов.ВыгрузитьКолонку("Ссылка");

	Запрос = Новый Запрос;
	Текст0 = "ВЫБРАТЬ * ИЗ Документ.ЭтапПроизводства2_2 КАК Табл ГДЕ Табл.Ссылка В(&Этапы);
	|";
	Текст1 = "ВЫБРАТЬ * ИЗ Документ.ЭтапПроизводства2_2.&ИмяТаблицы КАК Табл ГДЕ Табл.Ссылка В(&Этапы);
	|";
	ВГран = СбТ.ТаблТабл.Количество() - 1;
	Для Сч = 1 по ВГран цикл
		Текст0 = Текст0 + СтрЗаменить(Текст1, "&ИмяТаблицы", СбТ.ТаблТабл[Сч].Имя);
	КонецЦикла;
	Запрос.Текст = Текст0;
	Запрос.УстановитьПараметр("Этапы", МассивЭтапов);
	Рез = Запрос.ВыполнитьПакет();
	
	Для Сч = 0 по ВГран цикл
		Если СбТ.ТаблТабл[Сч].Таблица.Колонки.Количество() = 0 тогда  
			Выгрузка = Рез[Сч].Выгрузить();
			Для каждого Кол из Выгрузка.Колонки цикл   
				Если Кол.ТипЗначения.СодержитТип(Тип("ТаблицаЗначений")) тогда
					Продолжить;
				КонецЕсли;
				НоваяКолонка = СбТ.ТаблТабл[Сч].Таблица.Колонки.Добавить(,Кол.ТипЗначения);
				ЗаполнитьЗначенияСвойств(НоваяКолонка, Кол);
			КонецЦикла;
		КонецЕсли; 
	КонецЦикла;
КонецПроцедуры

Процедура ЭтапВТаблицы(Док, СбТ) Экспорт
	ПарамОтбора = Новый Структура("Ссылка", Док.Ссылка);
	Масс = СбТ.ЭтапыСвойства.НайтиСтроки(ПарамОтбора);
	Если Масс.Количество() = 0 тогда
		Стр = СбТ.ЭтапыСвойства.Добавить();
	Иначе     
		Стр = Масс[0];
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(Стр, Док);
	ВГран = СбТ.ТаблТабл.Количество() - 1;
	Для Сч = 1 по ВГран цикл
		Табл = СбТ.ТаблТабл[Сч].Таблица;
		Масс = Табл.НайтиСтроки(ПарамОтбора);
		Для каждого Стр из Масс цикл
			Табл.Удалить(Стр);
		КонецЦикла;	          
		Для каждого СтрД из Док[СбТ.ТаблТабл[Сч].Имя] цикл
			Стр = Табл.Добавить();              
			Стр.Ссылка = Док.Ссылка;
			ЗаполнитьЗначенияСвойств(Стр, СтрД);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура ТаблицыВЭтап(Док, СбТ) Экспорт
	ПарамОтбора = Новый Структура("Ссылка", Док.Ссылка);
	Масс = СбТ.ЭтапыСвойства.НайтиСтроки(ПарамОтбора);
	Если Масс[0].ВерсияДанных <> Док.ВерсияДанных тогда
		ЭТП.СообщитьОшибку("Этап изменил другой пользователь, "+Док.Ссылка, Док.Ссылка);
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(Док, Масс[0]); //переписать, чтобы не портить Модифицированность (возможно)
	ВГран = СбТ.ТаблТабл.Количество() - 1;  
	Для Сч = 1 по ВГран цикл  
		Табл = ЭТП.Выгрузить1(СбТ.ТаблТабл[Сч].Таблица, ПарамОтбора);
		Док[СбТ.ТаблТабл[Сч].Имя].Загрузить(Табл);
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьТаблицыСборника(СбТ)
	ЗаполнитьКолонкиТаблиц(СбТ);	
	Для каждого Стр из СбТ.ТаблицаЭтапов цикл
		ЭтапВТаблицы(Стр.Объект, СбТ);
	КонецЦикла;	
КонецПроцедуры  

Функция ПровестиЭтапыФоново(знач Объект, ИДФормы, РеальноПроводить) Экспорт 
	СбТ = СоздатьСборникТаблицСоСсылкамиЭтапов(Объект);
	//СбТХран = ПоместитьСборникТаблицВХранилище(Объект, ИДФормы);
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, "Обработки.АРМ_ЛокальныйДиспетчер.ПровестиЭтапы", СбТ, РеальноПроводить);
КонецФункции

Процедура ПровестиЭтапы(СбТ, РеальноПроводить) Экспорт
	//Результат = Ложь;     
	//СбТ = ЗагрузитьСборникТаблицИзХранилища(СбТХран);
	КоличествоЭтапов = СбТ.ТаблицаЭтапов.Количество();
	ПроцВып = 0;          
	ПроцВыпБыл = 0;
	НачатьТранзакцию(); 
	Попытка
		Для Сч= 1 - КоличествоЭтапов по 0 цикл
			Док = СбТ.ТаблицаЭтапов[-Сч].Ссылка.ПолучитьОбъект();
			ТаблицыВЭтап(Док, СбТ);
			Док.Записать(РежимЗаписиДокумента.Проведение);   
			ПроцВып = Цел((1+Сч/КоличествоЭтапов)*100);
			Если ПроцВып <> ПроцВыпБыл тогда
				ДлительныеОперации.СообщитьПрогресс(ПроцВып);
				ПроцВыпБыл = ПроцВып;
			КонецЕсли;
		КонецЦикла;  
		//Результат = Истина;
		Если РеальноПроводить тогда
			ЗафиксироватьТранзакцию();
		Иначе
			ОтменитьТранзакцию();
		КонецЕсли;
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	//Возврат Результат;
КонецПроцедуры 

//Функция ПоместитьСборникТаблицВХранилище(знач Объект, ИДФормы)
//	СбТ = СоздатьСборникТаблицСоСсылкамиЭтапов(Объект);
//	ЭтоДанныеФормы = (ТипЗнч(СбТ.ЭтапыСвойства) = Тип("ДанныеФормыКоллекция"));
//	Для каждого Стр из СбТ.ТаблТабл цикл
//		Если ЭтоДанныеФормы тогда 
//			ТабЗнач = Стр.Таблица.Выгрузить();
//		Иначе
//			ТабЗнач = Стр.Таблица;
//		КонецЕсли;
//		Хран = ПоместитьВоВременноеХранилище(ТабЗнач, ИДФормы);
//		Стр.Таблица = Хран;
//	КонецЦикла;   
//	Хран = ПоместитьВоВременноеХранилище(СбТ.ТаблицаЭтапов, ИДФормы);
//	СбТ.ТаблицаЭтапов = Хран;
//	Хран = ПоместитьВоВременноеХранилище(СбТ.ТаблТабл, ИДФормы);
//	СбТ.ТаблТабл = Хран;
//	Возврат ПоместитьВоВременноеХранилище(СбТ, ИДФормы);
//КонецФункции  

//Функция ЗагрузитьСборникТаблицИзХранилища(СбТХран)
//	СбТ = ПолучитьИзВременногоХранилища(СбТХран);
//	ТабЗнач = ПолучитьИзВременногоХранилища(СбТ.ТаблТабл);
//	СбТ.ТаблТабл = ТабЗнач;
//	ТабЗнач = ПолучитьИзВременногоХранилища(СбТ.ТаблицаЭтапов);
//	СбТ.ТаблицаЭтапов = ТабЗнач;
//	Для каждого Стр из СбТ.ТаблТабл цикл
//		ТабЗнач = ПолучитьИзВременногоХранилища(Стр.Таблица);
//		Стр.Таблица = ТабЗнач;
//		СбТ.Вставить(Стр.Имя, ТабЗнач);
//	КонецЦикла;
//	Возврат СбТ;
//КонецФункции  


