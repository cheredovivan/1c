
&НаСервере
Процедура НайтиСпецификацииНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РесурсныеСпецификацииВыходныеИзделия.Ссылка КАК Спецификация,
	|	РесурсныеСпецификацииВыходныеИзделия.Номенклатура КАК Изделие, 
	|	ИСТИНА КАК Пометка,	
	|	РесурсныеСпецификацииМатериалыИУслуги.Упаковка КАК Упаковка,
	|	РесурсныеСпецификацииМатериалыИУслуги.КоличествоУпаковок КАК КоличествоУпаковок,	
	|	РесурсныеСпецификацииВыходныеИзделия.Номенклатура.Артикул КАК Артикул
	|ИЗ 			
	|	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК РесурсныеСпецификацииМатериалыИУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК РесурсныеСпецификацииВыходныеИзделия
	|		ПО РесурсныеСпецификацииМатериалыИУслуги.Ссылка = РесурсныеСпецификацииВыходныеИзделия.Ссылка
	|ГДЕ
	|	РесурсныеСпецификацииМатериалыИУслуги.Номенклатура = &Номенклатура
	|	И НЕ РесурсныеСпецификацииМатериалыИУслуги.Ссылка.ПометкаУдаления
	|	И РесурсныеСпецификацииМатериалыИУслуги.Ссылка.Статус.Порядок <> 2 
	|УПОРЯДОЧИТЬ ПО
	|	РесурсныеСпецификацииВыходныеИзделия.Номенклатура.Артикул"; 
		
	Запрос.УстановитьПараметр("Номенклатура",Объект.МатериалБыло); 	
	Объект.Спецификации.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

&НаКлиенте
Процедура НайтиСпецификации(Команда)
	НайтиСпецификацииНаСервере();
	Элементы.Заменить.Доступность = Объект.Спецификации.Количество()>0;
КонецПроцедуры

&НаКлиенте
Процедура Поле1ПриИзменении(Элемент)
	Элементы.НайтиСпец.Доступность = ЗначениеЗаполнено(Объект.МатериалБыло);
	Объект.Спецификации.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура Поле2ПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.МатериалСтало) тогда
		Элементы.Заменить.Заголовок = "Заменить материал!";
	Иначе
		Элементы.Заменить.Заголовок = "Удалить материал!";
	КонецЕсли;
КонецПроцедуры

	
&НаСервере
Процедура ЗаменитьНаСервере()
	Если Объект.МатериалБыло = Объект.МатериалСтало тогда
		ВызватьИсключение "Старый и новый материал совпадают";
	КонецЕсли;       
	КоэффициентУпаковки = Неопределено;
	//Запрос = Новый Запрос;
	//Запрос.Текст =
	//"ВЫБРАТЬ
	//|	&ТекстЗапросаКоэффициентУпаковки КАК КоэффициентУпаковки
	//|ИЗ
	//|	Справочник.УпаковкиЕдиницыИзмерения КАК Упаковка";     
	//
	//Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
	//Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
	//Объект.МатериалБыло.ЕдиницаИзмерения,
	//Объект.МатериалСтало));	
	//
	//Выборка = Запрос.Выполнить().Выбрать();
	//Если Выборка.Следующий() Тогда
	//	КоэффициентУпаковки = Выборка.КоэффициентУпаковки;
	//КонецЕсли;		
	ДанныеУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентВесОбъемПрочиеРеквизитыУпаковки(Объект.МатериалБыло.ЕдиницаИзмерения, Объект.МатериалСтало);
	КоэффициентУпаковки = ДанныеУпаковки.Коэффициент;	 
	Если КоэффициентУпаковки = 1 Тогда
		Если НЕ Объект.МатериалБыло.ЕдиницаИзмерения = Объект.МатериалСтало.ЕдиницаИзмерения Тогда 
			ВызватьИсключение "Невозможно пересчитать количество!";
		КонецЕсли;
	КонецЕсли;	
	Если КоэффициентУпаковки = Неопределено Тогда
		ВызватьИсключение "Невозможно пересчитать количество!";
	КонецЕсли;	                                   
	
	Для каждого Строка из Объект.Спецификации цикл
		Если Строка.Пометка тогда
			Спецификация = Строка.Спецификация.ПолучитьОбъект();
			Номер = Спецификация.МатериалыИУслуги.Количество()-1;
			Пока Номер>=0 цикл
				Если Спецификация.МатериалыИУслуги[Номер].Номенклатура = Объект.МатериалБыло тогда
					Если ЗначениеЗаполнено(Объект.МатериалСтало) тогда  
						Если НЕ ЗначениеЗаполнено(Спецификация.МатериалыИУслуги[Номер].Упаковка) Тогда
							Спецификация.МатериалыИУслуги[Номер].КоличествоУпаковок = Спецификация.МатериалыИУслуги[Номер].КоличествоУпаковок * КоэффициентУпаковки; 
						КонецЕсли;	
						Спецификация.МатериалыИУслуги[Номер].Номенклатура = Объект.МатериалСтало;					
					Иначе
						Спецификация.МатериалыИУслуги.Удалить(Номер);
					КонецЕсли;
				КонецЕсли;
				Номер = Номер-1;
			КонецЦикла;
			Если Спецификация.Модифицированность() тогда
				Спецификация.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Заменить(Команда)
	ЗаменитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВключитьВсе(Команда)
	Для каждого Стр из Объект.Спецификации цикл
		Стр.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВыключитьВсе(Команда)
	Для каждого Стр из Объект.Спецификации цикл
		Стр.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры
