#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Печать

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	СтруктураТипов = ОбщегоНазначенияУТ.СоответствиеМассивовПоТипамОбъектов(МассивОбъектов);
	ПараметрыПечати 	  = Новый Структура("ВыводитьУслуги, ПечатьВВалюте", Истина, Ложь );

	//++ НЕ УТ
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МХ18") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"МХ18",
			НСтр("ru='Накладная на передачу готовой продукции (МХ-18)'"),
			СформироватьПечатнуюФормуМХ18(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
		
	КонецЕсли;
	//-- НЕ УТ
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М11") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"М11",
			НСтр("ru='Требование-накладная (М-11)'"),
			СформироватьПечатнуюФормуМ11(
				СтруктураТипов,
				ОбъектыПечати,
				ПараметрыПечати));
		
	КонецЕсли;
	
	ФормированиеПечатныхФорм.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, СтруктураТипов, КоллекцияПечатныхФорм);
	
КонецПроцедуры


#Область Печать_СчетФактура


Функция СтруктураИтоговыеСуммы()
	
	Структура = Новый Структура;
	
	СтруктураРесурсовДляИтогов = СтруктураРесурсовДляИтогов();
	
	Для Каждого Элемент Из СтруктураРесурсовДляИтогов Цикл
		Структура.Вставить("Итого"+Элемент.Ключ+"НаСтранице", 0);
		Структура.Вставить("Итого"+Элемент.Ключ, 0);
	КонецЦикла;
	
	Возврат Структура;
	
КонецФункции

Процедура ОбнулитьИтогиПоСтранице(ИтоговыеСуммы)
	
	СтруктураРесурсовДляИтогов = СтруктураРесурсовДляИтогов();
	
	Для Каждого Элемент Из СтруктураРесурсовДляИтогов Цикл
		ИтоговыеСуммы.Вставить("Итого"+Элемент.Ключ+"НаСтранице", 0);
	КонецЦикла;
	
КонецПроцедуры

Процедура РассчитатьИтоговыеСуммы(ИтоговыеСуммы, СтрокаТовары)
	
	СтруктураСуммПоСтроке = СтруктураРесурсовДляИтогов();
	
	ЗаполнитьЗначенияСвойств(СтруктураСуммПоСтроке, СтрокаТовары);
	ОкруглитьДоЦелого(СтруктураСуммПоСтроке.КоличествоМест);
	Для Каждого Элемент Из СтруктураСуммПоСтроке Цикл
		Если ЗначениеЗаполнено(Элемент.Значение) Тогда
			ИтоговыеСуммы["Итого"+Элемент.Ключ+"НаСтранице"] = ИтоговыеСуммы["Итого"+Элемент.Ключ+"НаСтранице"] + Элемент.Значение;
			ИтоговыеСуммы["Итого"+Элемент.Ключ] = ИтоговыеСуммы["Итого"+Элемент.Ключ] + Элемент.Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьИтоговыеДанныеПодвала(ИтоговыеСуммы, ВсегоНомеров, ВалютаРегламентированногоУчета)
	
	ИтоговыеСуммы.Вставить("КоличествоПорядковыхНомеровЗаписейПрописью", ЧислоПрописью(ВсегоНомеров, ,",,,,,,,,0"));
	ИтоговыеСуммы.Вставить("СуммаПрописью", РаботаСКурсамиВалют.СформироватьСуммуПрописью(ИтоговыеСуммы.ИтогоСуммаСНДС, ВалютаРегламентированногоУчета));
	//****ОЕ -- 20.01.2018
	ИтоговыеСуммы.Вставить("КоличествоПорядковыхНомеровЗаписей", ВсегоНомеров );
	//****ОЕ_конец
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыСтрокиТовара(ДанныеПечати, СтрокаТовары, ОбластьМакета, НомерСтроки, ПредставлениеНоменклатуры2, ВыводитьКодНоменклатуры = Истина, ВыводитьКодТНВД = Неопределено, СчетФактураНаАванс = Ложь)
	
	ИспользоватьНаборы = ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(СтрокаТовары, "ЭтоНабор");
	
	ПрефиксИПостфикс = НаборыСервер.ПолучитьПрефиксИПостфикс(СтрокаТовары, ИспользоватьНаборы);
	
	Если ИспользоватьНаборы
		И СтрокаТовары.ЭтоКомплектующие
		И СтрокаТовары.ВариантПредставленияНабораВПечатныхФормах = Перечисления.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие
		И (СтрокаТовары.ВариантРасчетаЦеныНабора = Перечисления.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоДолям
		   ИЛИ СтрокаТовары.ВариантРасчетаЦеныНабора = Перечисления.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоЦенам) Тогда
		// Область должна остаться незаполненной
		ОбластьМакета.Параметры.Заполнить(НаборыСервер.ПустыеДанные());
	ИначеЕсли ИспользоватьНаборы
		И СтрокаТовары.ЭтоНабор
		И СтрокаТовары.ВариантПредставленияНабораВПечатныхФормах = Перечисления.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие
		И (СтрокаТовары.ВариантРасчетаЦеныНабора = Перечисления.ВариантыРасчетаЦенНаборов.РассчитываетсяИзЦенКомплектующих
			ИЛИ СчетФактураНаАванс) Тогда
		// Область должна остаться незаполненной
		ОбластьМакета.Параметры.Заполнить(НаборыСервер.ПустыеДанные());
	Иначе
		ОбластьМакета.Параметры.Заполнить(СтрокаТовары);
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("КоличествоМест, КоличествоВОдномМесте, НоменклатураКод,КодТНВЭД", 0, 0, "", "--");
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, СтрокаТовары);
	ОкруглитьДоЦелого(СтруктураПараметров.КоличествоМест);
	СтруктураПараметров.Вставить("НомерСтроки", НомерСтроки);
	
	ДополнительныеПараметрыПолученияНаименованияДляПечати = НоменклатураКлиентСервер.ДополнительныеПараметрыПредставлениеНоменклатурыДляПечати();
	ДополнительныеПараметрыПолученияНаименованияДляПечати.ВозвратнаяТара = СтрокаТовары.ЭтоВозвратнаяТара;	
	Если ВыводитьКодТНВД <> Неопределено Тогда
		ДополнительныеПараметрыПолученияНаименованияДляПечати.КодТНВЭД = ?(НЕ ВыводитьКодТНВД.ВыводитьВКолонке И ВыводитьКодТНВД.ВыводитьВСтроке, СтрокаТовары.КодТНВЭД, "");
		Если НЕ ВыводитьКодТНВД.ВыводитьВКолонке Тогда
			СтруктураПараметров.КодТНВЭД = "--";
		КонецЕсли;
	КонецЕсли;
	
	ПредставлениеНоменклатуры =  ПрефиксИПостфикс.Префикс
		+ НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
			Строка(СтрокаТовары.НоменклатураНаименование),
			,
			,
			,
			ДополнительныеПараметрыПолученияНаименованияДляПечати)
		+ ПрефиксИПостфикс.Постфикс;
		
	ПредставлениеНоменклатуры2=""; //СС
	ДокументОснование = СтрокаТовары.ДокументОснование;
	Если ЗначениеЗаполнено(ДокументОснование) тогда
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЭтапПроизводства2_2") тогда
			ПредставлениеНоменклатуры2 = "Этап "+ ДокументОснование.Номер+ " от "+ Формат(ДокументОснование.Дата, "ДФ=дд.ММ.гг");
		КонецЕсли;
		НаправлениеДеятельности = ДокументОснование.НаправлениеДеятельности;
	Иначе	
		НаправлениеДеятельности = СтрокаТовары.НаправлениеДеятельности;
	КонецЕсли;
	ЗаказКлиента = СтрокаТовары.ЗаказКлиента;
	//ЗаказКлиента = "";
	
	//Если ЗначениеЗаполнено(НаправлениеДеятельности)
	//Тогда
	//	Если НЕ ЗначениеЗаполнено(ЗаказКлиента) Тогда
	//		//СТЧ = НаправлениеДеятельности.ДополнительныеРеквизиты.Найти(ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "НаправлениеДеятельностиДоговор") );
	//		//Если СТЧ = Неопределено
	//		//Тогда
	//			Запрос = Новый Запрос;
	//			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	//			               |	ЗаказКлиентаТовары.Ссылка КАК Ссылка
	//			               |ИЗ
	//			               |	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	//			               |ГДЕ
	//			               |	ЗаказКлиентаТовары.Номенклатура = &Номенклатура
	//			               |	И ЗаказКлиентаТовары.Ссылка.НаправлениеДеятельности = &НаправлениеДеятельности
	//			               |	И ЗаказКлиентаТовары.Ссылка.Дата <= &Дата
	//			               |
	//			               |УПОРЯДОЧИТЬ ПО
	//			               |	ЗаказКлиентаТовары.Ссылка.Дата УБЫВ";
	//			Запрос.УстановитьПараметр("Номенклатура", СтрокаТовары.Номенклатура);
	//			Запрос.УстановитьПараметр("НаправлениеДеятельности", НаправлениеДеятельности);
	//			Запрос.УстановитьПараметр("Дата", ДанныеПечати.ДатаДокумента);
	//			Выб = Запрос.Выполнить().Выбрать();
	//			Если Выб.Следующий() тогда
	//				ЗаказКлиента = Выб.Ссылка;	
	//			КонецЕсли;
	//		//Иначе
	//		//	Договор = СТЧ.Значение;
	//		//КонецЕсли;
	//	КонецЕсли;
	//КонецЕсли;
	ВремСтр = ДогСпецГКДляПечати(ЗаказКлиента, НаправлениеДеятельности);
	Если ЗначениеЗаполнено(ВремСтр) тогда
		Если ЗначениеЗаполнено(ПредставлениеНоменклатуры2) тогда
			ПредставлениеНоменклатуры2 = ПредставлениеНоменклатуры2 + ", ";
		КонецЕсли;
		ПредставлениеНоменклатуры2 = ПредставлениеНоменклатуры2 + ВремСтр;
	КонецЕсли;
	
	//Если ЗначениеЗаполнено(Договор) тогда
	//	ДоговорПеч = Договор.НаименованиеДляПечати;
	//	Если Найти( ДоговорПеч, "Дог") <> 1 Тогда
	//		ДоговорПеч = "Дог. " + ДоговорПеч;
	//	КонецЕсли;
	//	ПредставлениеНоменклатуры2 = ДоговорПеч+" "+ПредставлениеНоменклатуры2;
	//КонецЕсли; //СС конец
	
//	ПредставлениеНоменклатуры =СтрокаТовары.Номенклатура.Артикул+" "+ПредставлениеНоменклатуры; //СС 11.07.18
	//****ОЕ_конец
	//CC	
	СтруктураПараметров.Вставить("ПредставлениеНоменклатуры", ПредставлениеНоменклатуры);
	СтруктураПараметров.Вставить("Характеристика", СтрокаТовары.ХарактеристикаНаименование);
	//СтруктураПараметров.Вставить("Назначение", Неопределено);
		
	Если Не ВыводитьКодНоменклатуры Тогда
		СтруктураПараметров.НоменклатураКод = "";
	КонецЕсли;
	ОбластьМакета.Параметры.Заполнить(СтруктураПараметров);
	
КонецПроцедуры

#КонецОбласти

#Область Печать_М11

// Функция формирует печатную форму "М-11"
//
Функция СформироватьПечатнуюФормуМ11(СтруктураТипов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_М11";
	
	НомерТипаДокумента = 0;
	
	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл
		
		НомерТипаДокумента = НомерТипаДокумента + 1;
		Если НомерТипаДокумента > 1 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		МенеджерОбъекта = ПолучитьМенеджер(СтруктураОбъектов.Ключ);
		ДанныеДляПечати = МенеджерОбъекта.ПолучитьДанныеДляПечатнойФормыМ11(
			ПараметрыПечати,
			СтруктураОбъектов.Значение);
		
		ЗаполнитьТабличныйДокументМ11(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФормуСчетНаОплату()

// Процедура заполняет табличный документ "М-11"
//
Процедура ЗаполнитьТабличныйДокументМ11(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	ШаблонОшибкиТовары = НСтр("ru = 'В документе %1 отсутствуют товары. Печать М-11 не требуется'");
	
	ТоварКод = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(ТоварКод) Тогда
		ТоварКод = "Код";
	КонецЕсли;
	
	//Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьМ11.ПФ_MXL_М11");
	Макет = ПолучитьМакет("М11");
	
	ПервыйДокумент = Истина;
	ВыборкаДокументы = ДанныеДляПечати.РезультатПоШапке.Выбрать();
	ВыборкаПоТабличнымЧастям = ДанныеДляПечати.РезультатПоТабличнойЧасти.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	СтруктураОтбора = Новый Структура("Ссылка");
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		СтруктураОтбора.Ссылка = ВыборкаДокументы.Ссылка;
		ВыборкаПоТабличнымЧастям.Сбросить();
		Если НЕ ВыборкаПоТабличнымЧастям.НайтиСледующий(СтруктураОтбора) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонОшибкиТовары,
					ВыборкаДокументы.Ссылка
				),
				ВыборкаДокументы.Ссылка);
			Продолжить;
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВыборкаПоСкладам = ВыборкаПоТабличнымЧастям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПоСкладам.Следующий() Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			НомерСтраницы   = 1;
			НомерСтроки = 0;
			
			// Создаем массив для проверки вывода
			МассивВыводимыхОбластей = Новый Массив;
			
			ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
			
			// Вывод шапки.
			ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
			ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьШапка, ВыборкаДокументы.Ссылка);
			ОбластьШапка.Параметры.Заголовок = НСтр("ru = 'ТРЕБОВАНИЕ-НАКЛАДНАЯ №'") + ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаДокументы.Номер, Ложь, Истина);
			ОбластьШапка.Параметры.Заполнить(ВыборкаДокументы);
			ОбластьШапка.Параметры.Заполнить(ВыборкаПоСкладам);
			ОбластьШапка.Параметры.ПредставлениеПодразделения = ВыборкаДокументы.Подразделение;
			
			
			//Организация
			ДопРек = ВыборкаДокументы.Ссылка.ДополнительныеРеквизиты.Выгрузить();
			ДопРекП = ДопРек.Найти( ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ПеремещениеТоваровОрганизация") );
			Если ДопРекП = Неопределено
			Тогда
				СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ВыборкаДокументы.Организация, ВыборкаДокументы.ДатаДокумента);
			Иначе
				СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДопРекП.Значение, ВыборкаДокументы.ДатаДокумента);
			КонецЕсли;			
	        	
			//СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ВыборкаДокументы.Организация, ВыборкаДокументы.ДатаДокумента);
			//ОбластьШапка.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации);
			ОбластьШапка.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации,"ПолноеНаименование,ИНН,ФактическийАдрес,Телефоны");
			ОбластьШапка.Параметры.КодОКПО = СведенияОбОрганизации.КодПоОКПО;
			ТабличныйДокумент.Вывести(ОбластьШапка);
			
			// Выводим заголовок таблицы
			ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
			ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
			
			ВыборкаПоСтрокам = ВыборкаПоСкладам.Выбрать();
			КоличествоСтрок = ВыборкаПоСтрокам.Количество();
			Пока ВыборкаПоСтрокам.Следующий() Цикл
				
				Область = Макет.ПолучитьОбласть("Строка");
				Область.Параметры.Заполнить(ВыборкаПоСтрокам);
				
				НомерСтроки = НомерСтроки + 1;
				
				МассивВыводимыхОбластей.Очистить();
				МассивВыводимыхОбластей.Добавить(Область);
				
				Если НомерСтроки = КоличествоСтрок Тогда
					МассивВыводимыхОбластей.Добавить(ОбластьПодвал);
				КонецЕсли;
				
				Если ТоварКод = "Артикул" Тогда
					Область.Параметры.НоменклатурныйНомер = ВыборкаПоСтрокам.НоменклатурныйНомерАртикул;
				Иначе
					Область.Параметры.НоменклатурныйНомер = ВыборкаПоСтрокам.НоменклатурныйНомерКод;
				КонецЕсли;
				
				Область.Параметры.МатериалНаименование = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
					СокрЛП(ВыборкаПоСтрокам.НоменклатураНаименование),
					СокрЛП(ВыборкаПоСтрокам.Характеристика));
				
				Если НЕ ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
					НомерСтраницы = НомерСтраницы + 1;
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					СтруктураПараметров = Новый Структура;
					СтруктураПараметров.Вставить("НомерСтраницы", "Страница " + НомерСтраницы);
					ЗаголовокТаблицы.Параметры.Заполнить(СтруктураПараметров);
					ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
				КонецЕсли;
				
				ТабличныйДокумент.Вывести(Область);
				
			КонецЦикла;
			
			// Вывод подвала.
			ОбластьПодвал.Параметры.ДолжностьОтправителя = ВыборкаПоСкладам.ДолжностьКладовщикаОтправителя;
			ОбластьПодвал.Параметры.ФИООтправителя = ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(ВыборкаПоСкладам.КладовщикОтправитель, ВыборкаДокументы.ДатаДокумента);
			ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ

#Область Печать_МХ18

Функция СформироватьПечатнуюФормуМХ18(СтруктураТипов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_МХ18";
	
	НомерТипаДокумента = 0;
	
	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл
		
		НомерТипаДокумента = НомерТипаДокумента + 1;
		Если НомерТипаДокумента > 1 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		//МенеджерОбъекта = ПолучитьМенеджер(СтруктураОбъектов.Ключ);
		//
		//ДанныеДляПечати = МенеджерОбъекта.ПолучитьДанныеДляПечатнойФормыМХ18(ПараметрыПечати, СтруктураОбъектов.Значение);
		Если СтруктураОбъектов.Ключ = "Документ.ПеремещениеТоваров" тогда
			ДанныеДляПечати = ПолучитьДанныеДляПечатнойФормыМХ18(ПараметрыПечати, СтруктураОбъектов.Значение);
		ИначеЕсли СтруктураОбъектов.Ключ = "Документ.ДвижениеПродукцииИМатериалов" тогда
			ДанныеДляПечати = ПолучитьДанныеДляПечатнойФормыМХ18_Д(ПараметрыПечати, СтруктураОбъектов.Значение);
		ИначеЕсли СтруктураОбъектов.Ключ = "Документ.ЭтапПроизводства2_2" тогда
			ДанныеДляПечати = ПолучитьДанныеДляПечатнойФормыМХ18_Э(ПараметрыПечати, СтруктураОбъектов.Значение);
		Иначе
			ВызватьИсключение "МХ18: Неверный тип документа для печати";
		КонецЕсли;
			
		
		ЗаполнитьТабличныйДокументМХ18(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ЗаполнитьРеквизитыШапкиМХ18(ДанныеПечати, ВыборкаПоСкладам, Макет, ТабличныйДокумент)
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	
	//Дополнительные реквизиты
	ДопРек = ДанныеПечати.Ссылка.ДополнительныеРеквизиты.Выгрузить();
	ДопРекП = ДопРек.Найти( ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ПеремещениеТоваровКодОперации") );
	Если ДопРекП <> Неопределено
	Тогда
		УстановитьПараметр(ОбластьМакета, "КодОперации", ДопРекП.Значение);
	КонецЕсли;			
	ДопРекП = ДопРек.Найти( ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ПеремещениеТоваровОрганизация") );
	Если ДопРекП = Неопределено
	Тогда
		СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Организация, ДанныеПечати.Дата);
	Иначе
		СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДопРекП.Значение, ДанныеПечати.Дата);
	КонецЕсли;			
	
	
	// Выводим общие реквизиты шапки
	//СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Организация, ДанныеПечати.Дата);
	
	ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(
		ТабличныйДокумент,
		Макет,
		ОбластьМакета,
		ДанныеПечати.Ссылка);
	
	ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
	УстановитьПараметр(ОбластьМакета, "ПредставлениеОрганизации", ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации,"ПолноеНаименование,ИНН,ФактическийАдрес,Телефоны"));
	УстановитьПараметр(ОбластьМакета, "ОрганизацияПоОКПО",        СведенияОбОрганизации.КодПоОКПО);
	УстановитьПараметр(ОбластьМакета, "ДатаДокумента",            ДанныеПечати.ДатаДокумента);
	УстановитьПараметр(ОбластьМакета, "НомерДокумента",           ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеПечати.Номер));
	УстановитьПараметр(ОбластьМакета, "Получатель",               ВыборкаПоСкладам.Получатель);
	
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыПодвалаМХ18(ОбластьПодвала, ДанныеПечати, ИтоговыеСуммы)
	
	ОбластьПодвала.Параметры.Заполнить(ДанныеПечати);
	
	УстановитьПараметр(ОбластьПодвала,
		"КоличествоПорядковыхНомеровЗаписейПрописью",
		ИтоговыеСуммы.КоличествоПорядковыхНомеровЗаписейПрописью);
	
КонецПроцедуры

Процедура ЗаполнитьТабличныйДокументМХ18(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	//Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьОбщихФорм.ПФ_MXL_МХ18");
	Макет = ПолучитьМакет("МХ18");

	
	ДанныеПечати        = ДанныеДляПечати.РезультатПоШапке.Выбрать();
	ВыборкаПоДокументам = ДанныеДляПечати.РезультатПоТабличнойЧасти.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	Пока ДанныеПечати.Следующий() Цикл
		
		СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);
		ВыборкаПоДокументам.Сбросить();
		Если НЕ ВыборкаПоДокументам.НайтиСледующий(СтруктураПоиска) Тогда
			
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В документе %1 отсутствуют товары. Печать накладной не требуется'"),
				ДанныеПечати.Ссылка);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				ДанныеПечати.Ссылка);
			
			Продолжить;
			
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВыборкаПоСкладам = ВыборкаПоДокументам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПоСкладам.Следующий() Цикл
			
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ПервыйДокумент = Ложь;
			
			
			// Выводим общие реквизиты шапки
			ЗаполнитьРеквизитыШапкиМХ18(ДанныеПечати, ВыборкаПоСкладам, Макет, ТабличныйДокумент);
			
			// Выводим заголовок таблицы
			ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
			ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
			
			НомерСтраницы   = 1;
			
			// Инициализация итогов в документе
			ИтоговыеСуммы = СтруктураИтоговыеСуммы();
			
			// Создаем массив для проверки вывода
			МассивВыводимыхОбластей = Новый Массив;
			
			// Выводим многострочную часть документа
			ОбластьМакета  = Макет.ПолучитьОбласть("Строка");
			ОбластьПодвала = Макет.ПолучитьОбласть("Подвал");
			ОбластьВсего   = Макет.ПолучитьОбласть("Всего");
			
			ОбластьМакета2  = Макет.ПолучитьОбласть("Строка2");
			
			НомерСтроки = 0;
			ДобавитьИтоговыеДанныеПодвала(ИтоговыеСуммы, НомерСтроки, ВалютаРегламентированногоУчета);
			ЗаполнитьРеквизитыПодвалаМХ18(ОбластьПодвала, ДанныеПечати, ИтоговыеСуммы);
			
			СтрокаТовары = ВыборкаПоСкладам.Выбрать();
			КоличествоСтрок = СтрокаТовары.Количество();
			Пока СтрокаТовары.Следующий() Цикл
				
				НомерСтроки = НомерСтроки + 1;
				Строка2="";
				ЗаполнитьРеквизитыСтрокиТовара(ДанныеПечати, СтрокаТовары, ОбластьМакета, НомерСтроки, Строка2);
				
				МассивВыводимыхОбластей.Очистить();
				МассивВыводимыхОбластей.Добавить(ОбластьМакета);
				Если Строка2<>"" тогда
					СтруктураПараметров = Новый Структура();
					СтруктураПараметров.Вставить("ПредставлениеНоменклатуры2", Строка2);
					ОбластьМакета2.Параметры.Заполнить(СтруктураПараметров);
					МассивВыводимыхОбластей.Добавить(ОбластьМакета2);
				КонецЕсли;
				
				Если НомерСтроки = КоличествоСтрок Тогда
					МассивВыводимыхОбластей.Добавить(ОбластьВсего);
					МассивВыводимыхОбластей.Добавить(ОбластьПодвала);
				КонецЕсли;
				
				Если НЕ ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
					НомерСтраницы = НомерСтраницы + 1;
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					УстановитьПараметр(ЗаголовокТаблицы, "НомерСтраницы", "Страница " + НомерСтраницы);
					ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
				КонецЕсли;
				
				ТабличныйДокумент.Вывести(ОбластьМакета);
				Если Строка2<>"" тогда
					ТабличныйДокумент.Вывести(ОбластьМакета2);
				КонецЕсли;
				РассчитатьИтоговыеСуммы(ИтоговыеСуммы, СтрокаТовары);
				
			КонецЦикла;
			
			// Выводим итоги по документу в целом
			ОбластьВсего.Параметры.Заполнить(ИтоговыеСуммы);
			ТабличныйДокумент.Вывести(ОбластьВсего);
			
			// Выводим итоги по документу
			ДобавитьИтоговыеДанныеПодвала(ИтоговыеСуммы, НомерСтроки, ВалютаРегламентированногоУчета);
			ЗаполнитьРеквизитыПодвалаМХ18(ОбластьПодвала, ДанныеПечати, ИтоговыеСуммы);
			ТабличныйДокумент.Вывести(ОбластьПодвала);
			
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
			ТабличныйДокумент,
			НомерСтрокиНачало,
			ОбъектыПечати,
			ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
КонецПроцедуры

//взято из ПеремещениеТоваровЛокализация
// Функция получает данные для формирования печатной формы МХ - 18
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.ПеремещениеТоваров - Массив ссылок на документы, 
//																по которым необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
//
Функция ПолучитьДанныеДляПечатнойФормыМХ18(ПараметрыПечати, МассивОбъектов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	//КолонкаКодов			= ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	//Если НЕ ЗначениеЗаполнено(КолонкаКодов) Тогда
	//	КолонкаКодов = "Код";
	//КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка						КАК Ссылка,
	|	ЕСТЬNULL(ДанныеДокумента.ИсправляемыйДокумент.Номер, ДанныеДокумента.Номер) КАК Номер,
	|	ЕСТЬNULL(ДанныеДокумента.ИсправляемыйДокумент.Дата, ДанныеДокумента.Дата) КАК Дата,
	|	ЕСТЬNULL(ДанныеДокумента.ИсправляемыйДокумент.Дата, ДанныеДокумента.Дата) КАК ДатаДокумента,
	|	ДанныеДокумента.Организация					КАК Организация,
	|	ДанныеДокумента.Организация.Префикс			КАК Префикс,
	|	ДанныеДокумента.СкладОтправитель.Представление	КАК ПредставлениеПодразделения
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокВыпуск.Ссылка       КАК Ссылка,
	|	ДокВыпуск.Дата         КАК Дата,
	|	ДокВыпуск.Организация  КАК Организация,
	|	ДокВыпуск.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|ПОМЕСТИТЬ ВТДанныеДокументовВыпуска
	|ИЗ 
	|	Документ.ПеремещениеТоваров КАК ДокВыпуск
	|ГДЕ 
	|	ДокВыпуск.Ссылка В (&МассивДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таб.Ссылка,
	|	РСУчетнаяПолитика.УчетГотовойПродукцииПоПлановойСтоимости КАК ИспользоватьПлановыеЦены
	|ПОМЕСТИТЬ ВТИспользованиеПлановыхЦен
	|ИЗ (
	|	ВЫБРАТЬ
	|		ДокументыВыпуска.Ссылка,
	|		ДокументыВыпуска.Дата,
	|		ДокументыВыпуска.Организация,
	|		ДокументыВыпуска.ГоловнаяОрганизация,
	|		МАКСИМУМ (РСУчетнаяПолитика.Период) КАК УчетнаяПолитикаПериод
	|
	|	ИЗ ВТДанныеДокументовВыпуска КАК ДокументыВыпуска
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК РСУчетнаяПолитика
	|		ПО РСУчетнаяПолитика.Организация = ДокументыВыпуска.ГоловнаяОрганизация
	|		И  РСУчетнаяПолитика.Период <= ДокументыВыпуска.Дата
	|	СГРУППИРОВАТЬ ПО
	|		ДокументыВыпуска.Ссылка,
	|		ДокументыВыпуска.Дата,
	|		ДокументыВыпуска.Организация,
	|		ДокументыВыпуска.ГоловнаяОрганизация
	|	) КАК Таб
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК РСУчетнаяПолитика
	|	ПО Таб.ГоловнаяОрганизация = РСУчетнаяПолитика.Организация
	|	И  Таб.УчетнаяПолитикаПериод = РСУчетнаяПолитика.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ТАБЛИЧНЫМ ЧАСТЯМ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка														КАК Ссылка,
	|	ТаблицаТовары.Номенклатура													КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное								КАК НоменклатураНаименование,
	|	ТаблицаТовары.Номенклатура.Код								КАК НоменклатураКод,
	|	ТаблицаТовары.Характеристика.НаименованиеПолное								КАК ХарактеристикаНаименование,
	|	ТаблицаТовары.Характеристика												КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения 											КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения 													КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения 											КАК ВидУпаковки,
	|	ВЫБОР КОГДА ТаблицаТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) ТОГДА
	|		1
	|	ИНАЧЕ
	|		ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)
	|	КОНЕЦ																				КАК КоличествоВОдномМесте,
	|	СУММА(ТаблицаТовары.КоличествоУпаковок * &ТекстЗапросаВес)					КАК МассаБрутто,
	|	СУММА(ТаблицаТовары.КоличествоУпаковок)										КАК Количество,
	|	СУММА(ТаблицаТовары.КоличествоУпаковок) 									КАК КоличествоМест,
	|	МИНИМУМ(ТаблицаТовары.НомерСтроки)											КАК НомерСтроки,
	|	ЛОЖЬ																				КАК ЭтоВозвратнаяТара,
	|	СРЕДНЕЕ(ВЫБОР
	|			КОГДА ВТИспользованиеПлановыхЦен.ИспользоватьПлановыеЦены ЕСТЬ NULL
	|				  ИЛИ НЕ ВТИспользованиеПлановыхЦен.ИспользоватьПлановыеЦены
	|			ТОГДА 0
	|			ИНАЧЕ 0
	|			КОНЕЦ)																		КАК Цена,
	|	СУММА(ВЫБОР
	|			КОГДА ВТИспользованиеПлановыхЦен.ИспользоватьПлановыеЦены ЕСТЬ NULL
	|				  ИЛИ НЕ ВТИспользованиеПлановыхЦен.ИспользоватьПлановыеЦены
	|			ТОГДА 0
	|			ИНАЧЕ 0
	|		КОНЕЦ)																			КАК Сумма,
	|	ТаблицаТовары.Ссылка.СкладПолучатель.Представление                                       КАК Получатель,
	|	ТаблицаТовары.Назначение КАК Назначение,
	|	ТаблицаТовары.Назначение.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаТовары.ЗаказНаПеремещение КАК ДокументОснование,
	|	ВЫРАЗИТЬ(ТаблицаТовары.ЗаказНаПеремещение.ДокументОснование КАК Документ.ЗаказКлиента) КАК ЗаказКлиента
	|
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ТаблицаТовары
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТИспользованиеПлановыхЦен КАК ВТИспользованиеПлановыхЦен
	|	ПО ТаблицаТовары.Ссылка = ВТИспользованиеПлановыхЦен.Ссылка
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка В(&МассивДокументов)
	|	И ТаблицаТовары.Номенклатура.ТипНоменклатуры В (
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|															)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Упаковка,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения,
	|	&ТекстЗапросаКодЕдиницыИзмерения,
	|	ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1),
	|	ТаблицаТовары.Назначение,
	|	ТаблицаТовары.ЗаказНаПеремещение
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка,
	|	Получатель";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаВес",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
			
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
			
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	РезультатПоШапке = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[3];
	
	СтруктураДанныхДляПечати = Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		РезультатПоШапке,
		РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

//взято из ДвижениеПродукцииИМатериаловЛокализация
// Функция получает данные для формирования печатной формы МХ - 18
//
Функция ПолучитьДанныеДляПечатнойФормыМХ18_Д(ПараметрыПечати, МассивОбъектов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	//КолонкаКодов			= ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	//Если НЕ ЗначениеЗаполнено(КолонкаКодов) Тогда
	//	КолонкаКодов = "Код";
	//КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Товары.Ссылка                            КАК Ссылка,
	|	МИНИМУМ(Товары.НомерСтроки)              КАК НомерСтроки,
	|	Товары.Номенклатура                      КАК Номенклатура,
	|	Товары.Характеристика                    КАК Характеристика,
	|	Товары.Упаковка                          КАК Упаковка,
	|	СУММА(Товары.КоличествоУпаковок)         КАК КоличествоУпаковок,
	|	КОНЕЦПЕРИОДА(Товары.Ссылка.Дата, ДЕНЬ)   КАК ДатаПолученияЦены,
	|	&ВидЦены                                 КАК ВидЦены,
	|	&ВалютаЦены                              КАК ВалютаЦены,
	|	Товары.Назначение КАК Назначение,
	|	Товары.Распоряжение КАК Распоряжение
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ДвижениеПродукцииИМатериалов.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка В(&МассивДокументов)
	|	И Товары.Ссылка.ХозяйственнаяОперация В
	|		(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства),
	|		 ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзКладовой))
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Ссылка,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Упаковка,
	|	Товары.Назначение,
	|	Товары.Распоряжение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыЦенНоменклатуры.Ссылка,
	|	ПериодыЦенНоменклатуры.НомерСтроки,
	|	ЦеныНоменклатуры.Цена,
	|	ЦеныНоменклатуры.Упаковка
	|ПОМЕСТИТЬ Цены
	|ИЗ
	|	(ВЫБРАТЬ
	|		Товары.Ссылка КАК Ссылка,
	|		Товары.НомерСтроки КАК НомерСтроки,
	|		ЦеныНоменклатуры.ВидЦены КАК ВидЦены,
	|		ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
	|		ЦеныНоменклатуры.Характеристика КАК Характеристика,
	|		МАКСИМУМ(ЦеныНоменклатуры.Период) КАК Период
	|	ИЗ
	|		Товары КАК Товары
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|			ПО Товары.ВидЦены = ЦеныНоменклатуры.ВидЦены
	|				И Товары.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|				И Товары.Характеристика = ЦеныНоменклатуры.Характеристика
	|				И Товары.ДатаПолученияЦены >= ЦеныНоменклатуры.Период
	|				И Товары.ВалютаЦены = ЦеныНоменклатуры.Валюта
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Товары.Ссылка,
	|		Товары.НомерСтроки,
	|		ЦеныНоменклатуры.ВидЦены,
	|		ЦеныНоменклатуры.Номенклатура,
	|		ЦеныНоменклатуры.Характеристика) КАК ПериодыЦенНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|		ПО ПериодыЦенНоменклатуры.Период = ЦеныНоменклатуры.Период
	|			И ПериодыЦенНоменклатуры.ВидЦены = ЦеныНоменклатуры.ВидЦены
	|			И ПериодыЦенНоменклатуры.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|			И ПериодыЦенНоменклатуры.Характеристика = ЦеныНоменклатуры.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыКурсовВалютПоДокументам.Ссылка,
	|	КурсыВалют.Валюта,
	|	КурсыВалют.Курс,
	|	КурсыВалют.Кратность
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТабДокументы.Ссылка КАК Ссылка,
	|		МАКСИМУМ(КурсыВалют.Период) КАК Период,
	|		КурсыВалют.Валюта КАК Валюта
	|	ИЗ
	|		Документ.ДвижениеПродукцииИМатериалов КАК ТабДокументы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|			ПО &ВалютаЦены = КурсыВалют.Валюта
	|				И ТабДокументы.Дата >= КурсыВалют.Период
	|	ГДЕ
	|		ТабДокументы.Ссылка В(&МассивДокументов)
	|		И ТабДокументы.ХозяйственнаяОперация В
	|		(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства),
	|		 ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзКладовой))
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТабДокументы.Ссылка,
	|		КурсыВалют.Валюта) КАК ПериодыКурсовВалютПоДокументам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|		ПО ПериодыКурсовВалютПоДокументам.Период = КурсыВалют.Период
	|			И ПериодыКурсовВалютПоДокументам.Валюта = КурсыВалют.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ШАПКЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка						КАК Ссылка,
	|	ЕСТЬNULL(ДанныеДокумента.ИсправляемыйДокумент.Номер, ДанныеДокумента.Номер) КАК Номер,
	|	ЕСТЬNULL(ДанныеДокумента.ИсправляемыйДокумент.Дата, ДанныеДокумента.Дата) КАК Дата,
	|	ЕСТЬNULL(ДанныеДокумента.ИсправляемыйДокумент.Дата, ДанныеДокумента.Дата) КАК ДатаДокумента,
	|	ДанныеДокумента.Организация					КАК Организация,
	|	ДанныеДокумента.Организация.Префикс			КАК Префикс,
	|	ДанныеДокумента.Отправитель.Представление	КАК ПредставлениеПодразделения
	|ИЗ
	|	Документ.ДвижениеПродукцииИМатериалов КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивДокументов)
	|	И ДанныеДокумента.ХозяйственнаяОперация В
	|		(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства),
	|		 ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзКладовой))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ТАБЛИЧНЫМ ЧАСТЯМ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка												КАК Ссылка,
	|	ТаблицаТовары.Номенклатура											КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное						КАК НоменклатураНаименование,
	|	ТаблицаТовары.Номенклатура.Код						КАК НоменклатураКод,
	|	ТаблицаТовары.Характеристика.НаименованиеПолное						КАК ХарактеристикаНаименование,
	|	ТаблицаТовары.Характеристика										КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения 							КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения 									КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения 							КАК ВидУпаковки,
	|	ВЫБОР КОГДА ТаблицаТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) ТОГДА
	|		1
	|	ИНАЧЕ
	|		ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1)
	|	КОНЕЦ																КАК КоличествоВОдномМесте,
	|	ТаблицаТовары.КоличествоУпаковок * &ТекстЗапросаВес					КАК МассаБрутто,
	|	ТаблицаТовары.КоличествоУпаковок									КАК Количество,
	|	ТаблицаТовары.КоличествоУпаковок 									КАК КоличествоМест,
	|	ТаблицаТовары.НомерСтроки											КАК НомерСтроки,
	|	ЛОЖЬ																КАК ЭтоВозвратнаяТара,
	|	ВЫРАЗИТЬ(ЕСТЬNULL(Цены.Цена, 0) 
	|		/ ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1) 
	|		* ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) 
	|		* ЕСТЬNULL(КурсыВалют.Курс, 1)
	|		/ ЕСТЬNULL(КурсыВалют.Кратность, 1) КАК ЧИСЛО(31,2))			КАК Цена,
	|	(ВЫРАЗИТЬ(ЕСТЬNULL(Цены.Цена, 0) 
	|		/ ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1) 
	|		* ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) 
	|		* ЕСТЬNULL(КурсыВалют.Курс, 1)
	|		/ ЕСТЬNULL(КурсыВалют.Кратность, 1) КАК ЧИСЛО(31,2))) * ТаблицаТовары.КоличествоУпаковок КАК Сумма,
	|	ТаблицаТовары.Ссылка.Получатель.Представление                       КАК Получатель,
	|	ТаблицаТовары.Назначение КАК Назначение,
	|	ТаблицаТовары.Назначение.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаТовары.Распоряжение КАК ДокументОснование,
	|	ВЫРАЗИТЬ(ВЫРАЗИТЬ(ТаблицаТовары.Распоряжение КАК Документ.ЭтапПроизводства2_2).Распоряжение.ДокументОснование КАК Документ.ЗаказКлиента) КАК ЗаказКлиента
	|
	|ИЗ
	|	Товары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|		ПО ТаблицаТовары.Ссылка = КурсыВалют.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Цены КАК Цены
	|		ПО ТаблицаТовары.Ссылка = Цены.Ссылка
	|			И ТаблицаТовары.НомерСтроки = Цены.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка,
	|	Получатель";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаВес",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
			
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"Цены.Упаковка",
			"ТаблицаТовары.Номенклатура"));
			
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
	
	Запрос.Текст = ТекстЗапроса;
	
	ВидЦены = Константы.ВидЦеныПлановойСтоимостиМатериаловРабот.Получить();
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("ВидЦены", ВидЦены);
	Запрос.УстановитьПараметр("ВалютаЦены", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидЦены, "ВалютаЦены"));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	// Пакет запросов:
	// 		[0] - Временная таблица по табличной части документа
	// 		[1] - Временная таблица по ценам номенклатуры табличной части
	// 		[2] - Временная таблица по курсам валют документов
	// 		[3] - Выборка по шапкам документов
	// 		[4] - Выборка по табличным частям документов.

	РезультатПоШапке = МассивРезультатов[3];
	РезультатПоТабличнойЧасти = МассивРезультатов[4];
	
	СтруктураДанныхДляПечати = Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		РезультатПоШапке,
		РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Функция получает данные для формирования печатной формы МХ-18 из документа ЭтапПроизводства2_2
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.ЭтапПроизводства2_2
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
//
Функция ПолучитьДанныеДляПечатнойФормыМХ18_Э(ПараметрыПечати, МассивОбъектов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Товары.Ссылка                            КАК Ссылка,
	|	МИНИМУМ(Товары.НомерСтроки)              КАК НомерСтроки,
	|	Товары.Номенклатура                      КАК Номенклатура,
	|	Товары.Характеристика                    КАК Характеристика,
	|	Товары.Упаковка                          КАК Упаковка,
	|	СУММА(Товары.КоличествоУпаковок)         КАК КоличествоУпаковок,
	|	КОНЕЦПЕРИОДА(Товары.Ссылка.Дата, ДЕНЬ)   КАК ДатаПолученияЦены,
	|	&ВидЦены                                 КАК ВидЦены,
	|	&ВалютаЦены                              КАК ВалютаЦены,
	|	Товары.Назначение                        КАК Назначение,
	|	Товары.Получатель                        КАК Получатель
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК Товары
	|ГДЕ
	|	Товары.Ссылка В(&МассивДокументов)
	|	И Товары.Произведено
	|	И НЕ Товары.СписатьНаРасходы
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Ссылка,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Упаковка,
	|	Товары.Назначение,
	|	Товары.Получатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыЦенНоменклатуры.Ссылка,
	|	ПериодыЦенНоменклатуры.НомерСтроки,
	|	ЦеныНоменклатуры.Цена,
	|	ЦеныНоменклатуры.Упаковка
	|ПОМЕСТИТЬ Цены
	|ИЗ
	|	(ВЫБРАТЬ
	|		Товары.Ссылка КАК Ссылка,
	|		Товары.НомерСтроки КАК НомерСтроки,
	|		ЦеныНоменклатуры.ВидЦены КАК ВидЦены,
	|		ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
	|		ЦеныНоменклатуры.Характеристика КАК Характеристика,
	|		МАКСИМУМ(ЦеныНоменклатуры.Период) КАК Период
	|	ИЗ
	|		Товары КАК Товары
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|			ПО Товары.ВидЦены = ЦеныНоменклатуры.ВидЦены
	|				И Товары.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|				И Товары.Характеристика = ЦеныНоменклатуры.Характеристика
	|				И Товары.ДатаПолученияЦены >= ЦеныНоменклатуры.Период
	|				И Товары.ВалютаЦены = ЦеныНоменклатуры.Валюта
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Товары.Ссылка,
	|		Товары.НомерСтроки,
	|		ЦеныНоменклатуры.ВидЦены,
	|		ЦеныНоменклатуры.Номенклатура,
	|		ЦеныНоменклатуры.Характеристика) КАК ПериодыЦенНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|		ПО ПериодыЦенНоменклатуры.Период = ЦеныНоменклатуры.Период
	|			И ПериодыЦенНоменклатуры.ВидЦены = ЦеныНоменклатуры.ВидЦены
	|			И ПериодыЦенНоменклатуры.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|			И ПериодыЦенНоменклатуры.Характеристика = ЦеныНоменклатуры.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыКурсовВалютПоДокументам.Ссылка,
	|	КурсыВалют.Валюта,
	|	КурсыВалют.Курс,
	|	КурсыВалют.Кратность
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТабДокументы.Ссылка КАК Ссылка,
	|		МАКСИМУМ(КурсыВалют.Период) КАК Период,
	|		КурсыВалют.Валюта КАК Валюта
	|	ИЗ
	|		Документ.ЭтапПроизводства2_2 КАК ТабДокументы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|			ПО &ВалютаЦены = КурсыВалют.Валюта
	|				И ТабДокументы.Дата >= КурсыВалют.Период
	|	ГДЕ
	|		ТабДокументы.Ссылка В(&МассивДокументов)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТабДокументы.Ссылка,
	|		КурсыВалют.Валюта) КАК ПериодыКурсовВалютПоДокументам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|		ПО ПериодыКурсовВалютПоДокументам.Период = КурсыВалют.Период
	|			И ПериодыКурсовВалютПоДокументам.Валюта = КурсыВалют.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ШАПКЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка						КАК Ссылка,
	|	ДанныеДокумента.Номер                       КАК Номер,
	|	ДанныеДокумента.Дата                        КАК Дата,
	|	ДанныеДокумента.Дата                        КАК ДатаДокумента,
	|	ДанныеДокумента.Организация					КАК Организация,
	|	ДанныеДокумента.Организация.Префикс			КАК Префикс,
	|	ДанныеДокумента.Подразделение.Представление	КАК ПредставлениеПодразделения
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ТАБЛИЧНЫМ ЧАСТЯМ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка												КАК Ссылка,
	|	ТаблицаТовары.Номенклатура											КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное						КАК НоменклатураНаименование,
	|	ТаблицаТовары.Номенклатура.Код						                КАК НоменклатураКод,
	|	ТаблицаТовары.Характеристика.НаименованиеПолное						КАК ХарактеристикаНаименование,
	|	ТаблицаТовары.Характеристика										КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения 							КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения 									КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения 							КАК ВидУпаковки,
	|	ВЫБОР КОГДА ТаблицаТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) ТОГДА
	|		1
	|	ИНАЧЕ
	|		ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1)
	|	КОНЕЦ																КАК КоличествоВОдномМесте,
	|	ТаблицаТовары.КоличествоУпаковок * &ТекстЗапросаВес					КАК МассаБрутто,
	|	ТаблицаТовары.КоличествоУпаковок									КАК Количество,
	|	ТаблицаТовары.КоличествоУпаковок 									КАК КоличествоМест,
	|	ТаблицаТовары.НомерСтроки											КАК НомерСтроки,
	|	ЛОЖЬ																КАК ЭтоВозвратнаяТара,
	|	ВЫРАЗИТЬ(ЕСТЬNULL(Цены.Цена, 0) 
	|		/ ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1) 
	|		* ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) 
	|		* ЕСТЬNULL(КурсыВалют.Курс, 1)
	|		/ ЕСТЬNULL(КурсыВалют.Кратность, 1) КАК ЧИСЛО(31,2))			КАК Цена,
	|	(ВЫРАЗИТЬ(ЕСТЬNULL(Цены.Цена, 0) 
	|		/ ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1) 
	|		* ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) 
	|		* ЕСТЬNULL(КурсыВалют.Курс, 1)
	|		/ ЕСТЬNULL(КурсыВалют.Кратность, 1) КАК ЧИСЛО(31,2))) * ТаблицаТовары.КоличествоУпаковок КАК Сумма,
	|	ТаблицаТовары.Получатель.Представление                              КАК Получатель,
	|	ТаблицаТовары.Назначение КАК Назначение,
	|	ТаблицаТовары.Назначение.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаТовары.Ссылка КАК ДокументОснование,
	|	ВЫРАЗИТЬ(ТаблицаТовары.Ссылка.Распоряжение.ДокументОснование КАК Документ.ЗаказКлиента) КАК ЗаказКлиента
	|
	|ИЗ
	|	Товары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|		ПО ТаблицаТовары.Ссылка = КурсыВалют.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Цены КАК Цены
	|		ПО ТаблицаТовары.Ссылка = Цены.Ссылка
	|			И ТаблицаТовары.НомерСтроки = Цены.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка,
	|	Получатель";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаВес",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
			
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"Цены.Упаковка",
			"ТаблицаТовары.Номенклатура"));
			
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
	
	Запрос.Текст = ТекстЗапроса;
	
	ВидЦены = Константы.ВидЦеныПлановойСтоимостиМатериаловРабот.Получить();
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("ВидЦены", ВидЦены);
	Запрос.УстановитьПараметр("ВалютаЦены", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидЦены, "ВалютаЦены"));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	// Пакет запросов:
	// 		[0] - Временная таблица по табличной части документа
	// 		[1] - Временная таблица по ценам номенклатуры табличной части
	// 		[2] - Временная таблица по курсам валют документов
	// 		[3] - Выборка по шапкам документов
	// 		[4] - Выборка по табличным частям документов.

	РезультатПоШапке = МассивРезультатов[3];
	РезультатПоТабличнойЧасти = МассивРезультатов[4];
	
	СтруктураДанныхДляПечати = Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		РезультатПоШапке,
		РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции


#КонецОбласти

//-- НЕ УТ

#КонецОбласти

#Область Прочее

Процедура УстановитьПараметр(ОбластьМакета, ИмяПараметра, ЗначениеПараметра)
	ОбластьМакета.Параметры.Заполнить(Новый Структура(ИмяПараметра, ЗначениеПараметра));
КонецПроцедуры

// Формирует строку с информацией о договоре, спецификации и госконтракте для печати
Функция ДогСпецГКДляПечати(знач ЗаказКлиента = Неопределено, знач НаправлениеДеятельности = Неопределено)
	
	РезСтр = "";
	
	Если ЗначениеЗаполнено(ЗаказКлиента) Тогда
		ДоговорНаименование = Строка(ЗаказКлиента.Договор);
		Если ЗначениеЗаполнено(ДоговорНаименование) Тогда
			РезСтр = ДоговорНаименование;
			Если СтрНайти(РезСтр, "Дог") <> 1 Тогда	
				РезСтр = "Дог. " + РезСтр;
			КонецЕсли;
		Иначе
			РезСтр = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 № %2 от %3'"),
				"Счет", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ЗаказКлиента.Номер, Истина, Истина),
				Формат(ЗаказКлиента.Дата, "ДЛФ=DD"));
		КонецЕсли;
	
		Специф = ЗаказКлиента.ДополнительныеРеквизиты.Найти(
			ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ЗаказКлиентаСпецификацияДоговора"), "Свойство");
		Если Специф <> Неопределено Тогда
			Если РезСтр <> "" Тогда
				РезСтр = РезСтр + ", ";
			КонецЕсли;
			РезСтр = РезСтр + "Спецификация №" + Специф.Значение;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(НаправлениеДеятельности) Тогда
			НаправлениеДеятельности = ЗаказКлиента.НаправлениеДеятельности;
		КонецЕсли;
	КонецЕсли;

	Если ЗначениеЗаполнено(НаправлениеДеятельности) Тогда
		ТЗДР = НаправлениеДеятельности.ДополнительныеРеквизиты.Выгрузить();
		СТЧ = ТЗДР.Найти(ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "НаправлениеДеятельностиПолноеНаименование"));
		Если РезСтр <> "" Тогда
			РезСтр = РезСтр + ", ";
		КонецЕсли;
		Если СТЧ = Неопределено Тогда
			ТекстГК = НаправлениеДеятельности.Наименование;
		Иначе
			ТекстГК = СТЧ.Значение;
			Если СтрНайти(ТекстГК, "ГК") <> 1 Тогда	
				ТекстГК = "ГК " + ТекстГК;
			КонецЕсли;
		КонецЕсли;	
		РезСтр = РезСтр + ТекстГК;
		
		СТЧ = ТЗДР.Найти(ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "НаправлениеДеятельностиИГК"));
		Если СТЧ <> Неопределено Тогда
			РезСтр = РезСтр + ", ИГК " + СТЧ.Значение;
		КонецЕсли;			
		
	ИначеЕсли ЗначениеЗаполнено(ЗаказКлиента) И ЗначениеЗаполнено(ЗаказКлиента.БанковскийСчетКонтрагента) Тогда
		ГосКонтракт = ЗаказКлиента.БанковскийСчетКонтрагента.ГосударственныйКонтракт;
		Если ЗначениеЗаполнено(ГосКонтракт) Тогда
			РезСтр = РезСтр + ", ГК " + ГосКонтракт.Наименование + ", ИГК " + ГосКонтракт.Код;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезСтр;
	
КонецФункции

Функция СтруктураРесурсовДляИтогов()
	
	Структура = Новый Структура;
	
	Структура.Вставить("СуммаБезНДС",       0);
	Структура.Вставить("СуммаНДС",          0);
	Структура.Вставить("СуммаСНДС",         0);
	Структура.Вставить("Количество",        0);
	Структура.Вставить("КоличествоМест",    0);
	Структура.Вставить("КоличествоПринято", 0);
	Структура.Вставить("МассаБрутто",       0);
	Структура.Вставить("МассаНетто",        0);
	Структура.Вставить("Сумма",             0);
	
	Структура.Вставить("РазницаБезНДСУвеличение", 0);
	Структура.Вставить("РазницаБезНДСУменьшение", 0);
	Структура.Вставить("РазницаНДСУвеличение",    0);
	Структура.Вставить("РазницаНДСУменьшение",    0);
	Структура.Вставить("РазницаСНДСУвеличение",   0);
	Структура.Вставить("РазницаСНДСУменьшение",   0);
	
	Возврат Структура;
	
КонецФункции

Функция ПараметрыМассыПрописью(Масса, КоэффициентПересчетаВТонны)
	
	МассаТонны = Масса * КоэффициентПересчетаВТонны;
	Если МассаТонны > 1 Тогда
		Коэффициент = 1;
		СтрокаФормат = "т, т, т, ж, кг, кг, кг, м, " + ?(Окр(МассаТонны) = МассаТонны, "0", "3");
	ИначеЕсли МассаТонны * 1000 > 1 Тогда
		Коэффициент = 1000;
		СтрокаФормат = "кг, кг, кг, м, г, г, г, м, " + ?(Окр(МассаТонны * Коэффициент) = МассаТонны * Коэффициент, "0", "3");
	Иначе
		Коэффициент = 1000000;
		СтрокаФормат = "г, г, г, м, г, г, г, м, 0";
	КонецЕсли;
	
	Возврат Новый Структура("Формат, Коэффициент", СтрокаФормат, Коэффициент*КоэффициентПересчетаВТонны);
	
КонецФункции

Функция СведенияОГрузоотправителе(ДанныеПечати)
	
	Если ТипЗнч(ДанныеПечати.Грузоотправитель) = Тип("СправочникСсылка.РегистрацииВНалоговомОргане") Тогда
		СведенияОГрузоотправителе = Справочники.РегистрацииВНалоговомОргане.СведенияОПодразделении(ДанныеПечати.Грузоотправитель, ДанныеПечати.Дата);
	Иначе
		СведенияОГрузоотправителе = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Грузоотправитель, ДанныеПечати.Дата);
	КонецЕсли;
	
	Возврат СведенияОГрузоотправителе;
	
КонецФункции

Процедура ПроставитьПрочеркиВПустыеПоляСтроки(ОбластьМакета)

	Для Сч = 0 По ОбластьМакета.Параметры.Количество() - 1 Цикл
		
		ТекПараметр = ОбластьМакета.Параметры.Получить(Сч);
		
		Если НЕ ЗначениеЗаполнено(ТекПараметр) Тогда
			ОбластьМакета.Параметры.Установить(Сч, "--");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОкруглитьДоЦелого(ОкругляемоеЧисло)
	Если ЗначениеЗаполнено(ОкругляемоеЧисло) Тогда
		Если ОкругляемоеЧисло <> Цел(ОкругляемоеЧисло) Тогда
			ОкругляемоеЧисло = Цел(ОкругляемоеЧисло) + 1;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ТаблицаКонтрагентовСчетаФактуры(ДанныеПечати, ДанныеКонтрагентов)
	
	ТаблицаКонтрагентов = Новый ТаблицаЗначений;
	ТаблицаКонтрагентов.Колонки.Добавить("СведенияОПокупателе");
	ТаблицаКонтрагентов.Колонки.Добавить("СведенияОГрузополучателе");
	ТаблицаКонтрагентов.Колонки.Добавить("КПП");
	ТаблицаКонтрагентов.Колонки.Добавить("ИНН");
	
	Если ДанныеКонтрагентов <> Неопределено Тогда
		
		СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);
		ДанныеКонтрагентов.НайтиСледующий(СтруктураПоиска);
		ВыборкаКонтрагентов = ДанныеКонтрагентов.Выбрать();
		
		Пока ВыборкаКонтрагентов.Следующий() Цикл
			
			СтрокаКонтрагента = ТаблицаКонтрагентов.Добавить();
			СтрокаКонтрагента.СведенияОПокупателе = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(
				ВыборкаКонтрагентов.Контрагент, ДанныеПечати.Дата);
			СтрокаКонтрагента.СведенияОГрузополучателе = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(
				ВыборкаКонтрагентов.Грузополучатель, ДанныеПечати.Дата);
				
			Если ПустаяСтрока(ВыборкаКонтрагентов.КПППокупателя) Тогда
				СтрокаКонтрагента.КПП = СтрокаКонтрагента.СведенияОПокупателе.КПП
			Иначе
				СтрокаКонтрагента.КПП = ВыборкаКонтрагентов.КПППокупателя;
			КонецЕсли;
			
			СтрокаКонтрагента.ИНН = ВыборкаКонтрагентов.ИННПокупателя;
			
		КонецЦикла;
		
	Иначе
		
		СтрокаКонтрагента = ТаблицаКонтрагентов.Добавить();
		СтрокаКонтрагента.СведенияОПокупателе = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(
			ДанныеПечати.Контрагент, ДанныеПечати.Дата);
		СтрокаКонтрагента.СведенияОГрузополучателе = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(
			ДанныеПечати.Грузополучатель, ДанныеПечати.Дата);
		Если ПустаяСтрока(ДанныеПечати.КПППокупателя) Тогда
			СтрокаКонтрагента.КПП = СтрокаКонтрагента.СведенияОПокупателе.КПП
		Иначе
			СтрокаКонтрагента.КПП = ДанныеПечати.КПППокупателя;
		КонецЕсли;
		СтрокаКонтрагента.ИНН = ДанныеПечати.ИННПокупателя;
		
	КонецЕсли;
	
	Возврат ТаблицаКонтрагентов;
	
КонецФункции

Функция ВыводитьКодыТНВЭД(ДанныеПечати, ДействуетПостановление981)
	ПараметрыВывода = Новый Структура("ВыводитьВСтроке, ВыводитьВКолонке");
	
	ПараметрыВывода.ВыводитьВСтроке = ТипЗнч(ДанныеПечати.Ссылка) = Тип("ДокументСсылка.СчетФактураВыданный")
		И УчетНДСУП.СтранаЯвляетсяЧленомТаможенногоСоюза(ДанныеПечати.СтранаРегистрации, ДанныеПечати.Дата)
		И ДанныеПечати.Дата >= УчетНДСУП.НастройкиУчета().ДатаРазделенияЭкспорта;
	ПараметрыВывода.ВыводитьВКолонке = ПараметрыВывода.ВыводитьВСтроке И ДействуетПостановление981;
	
	Возврат ПараметрыВывода
	
КонецФункции

Функция ВедетсяУчетНДСПоФЗ56(ДатаДокумента, ДатаИсправления)
	
	НачалоПримененияФЗ56 = '20170701';
	
	Дата = ?(ЗначениеЗаполнено(ДатаИсправления),ДатаИсправления,ДатаДокумента);
	
	Если Дата < НачалоПримененияФЗ56 Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

Функция ДействуетПостановление981(ДатаДокумента, ДатаИсправления)
	
	НачалоПрименения = '20171001';
	
	Дата = ?(ЗначениеЗаполнено(ДатаИсправления),ДатаИсправления,ДатаДокумента);
	
	Если Дата < НачалоПрименения Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
/////////////////////////////////////////////////////////////////////////////
// ОПИСАНИЕ ИНТЕРФЕЙСОВ

// Интерфейс для регистрации обработки.
// Вызывается при добавлении обработки в справочник "ВнешниеОбработки"
//
// Возвращаемое значение:
// Структура:
// Вид - строка - возможные значения:	"ДополнительнаяОбработка"
//										"ДополнительныйОтчет"
//										"ЗаполнениеОбъекта"
//										"Отчет"
//										"ПечатнаяФорма"
//										"СозданиеСвязанныхОбъектов"
//
// Назначение - массив строк имен объектов метаданных в формате:
//			<ИмяКлассаОбъектаМетаданного>.[ * | <ИмяОбъектаМетаданных>]
//			Например, "Документ.СчетЗаказ" или "Справочник.*"
//			Прим. параметр имеет смысл только для назначаемых обработок
//
// Наименование - строка - наименование обработки, которым будет заполнено
//						наименование справочника по умолчанию - краткая строка для
//						идентификации обработки администратором
//
// Версия - строка - версия обработки в формате <старший номер>.<младший номер>
//					используется при загрузке обработок в информационную базу
// БезопасныйРежим – Булево – Если истина, обработка будет запущена в безопасном режиме.
//							Более подбробная информация в справке.
//
// Информация - Строка- краткая информация по обработке, описание обработки
//
// Команды - ТаблицаЗначений - команды, поставляемые обработкой, одная строка таблицы соотвествует
//							одной команде
//				колонки: 
//				 - Представление - строка - представление команды конечному пользователю
//				 - Идентификатор - строка - идентефикатор команды. В случае печатных форм
//											перечисление через запятую списка макетов
//				 - Использование - строка - варианты запуска обработки:
//						"ОткрытиеФормы" - открыть форму обработки
//						"ВызовКлиентскогоМетода" - вызов клиентского экспортного метода из формы обработки
//						"ВызовСерверногоМетода" - вызов серверного экспортного метода из модуля объекта обработки
//				 - ПоказыватьОповещение – Булево – если Истина, требуется оказывать оповещение при начале
//								и при окончании запуска обработки. Прим. Имеет смысл только
//								при запуске обработки без открытия формы.
//				 - Модификатор – строка - для печатных форм MXL, которые требуется
//										отображать в форме ПечатьДокументов подсистемы Печать
//										требуется установить как "ПечатьMXL"
//
Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = Новый Структура;
	
	ПараметрыРегистрации.Вставить("Вид", "ПечатнаяФорма");
	ПараметрыРегистрации.Вставить("Назначение", ПолучитьНазначениеОбработки());
	ПараметрыРегистрации.Вставить("Наименование", НСтр("ru = 'МХ18'"));
	ПараметрыРегистрации.Вставить("Версия", "1.0");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Истина);
	ПараметрыРегистрации.Вставить("Информация", НСтр("ru = 'Внешние печатные формы МХ18'"));
	
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	
	ДобавитьКоманду(ТаблицаКоманд,
					НСтр("ru = 'Накладная на передачу готовой продукции МХ-18 (внешняя)'"),
					"МХ18",
					"ВызовСерверногоМетода",
					Истина,
					"ПечатьMXL");
				
	ДобавитьКоманду(ТаблицаКоманд,
					НСтр("ru = 'Требование-накладная М-11 (внешняя)'"),
					"М11",
					"ВызовСерверногоМетода",
					Истина,
					"ПечатьMXL");
					
					ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	
	Возврат ПараметрыРегистрации;
	
КонецФункции


Функция ПолучитьНазначениеОбработки()
	
	Назначение = Новый Массив;
	
	//Назначение.Добавить("Документ.ОтчетКомиссионера");
	//Назначение.Добавить("Документ.ВозвратТоваровПоставщику");
	//Назначение.Добавить("Документ.СчетФактураВыданныйАванс");
	//Назначение.Добавить("Документ.ОтчетКомитенту");
	//Назначение.Добавить("Документ.РеализацияТоваровУслуг");
	//Назначение.Добавить("Документ.СчетФактураВыданный");
	//Назначение.Добавить("Документ.АктВыполненныхРабот");
	//Назначение.Добавить("Документ.ВозвратТоваровМеждуОрганизациями");
	//Назначение.Добавить("Документ.ПередачаТоваровМеждуОрганизациями");
	//Назначение.Добавить("Документ.ОтчетПоКомиссииМеждуОрганизациями");
	//Назначение.Добавить("Документ.ЗаписьКнигиПродаж");
	Назначение.Добавить("Документ.ПеремещениеТоваров");
	Назначение.Добавить("Документ.ДвижениеПродукцииИМатериалов");
	Назначение.Добавить("Документ.ЭтапПроизводства2_2");
	
	Возврат Назначение;
	
КонецФункции

Функция ПолучитьТаблицуКоманд()
	
	Команды = Новый ТаблицаЗначений;
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	
	Возврат Команды;
	
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры


Функция ПолучитьОрганизациюДляПечати( РеализацияИлиСчетФактура )
	
	Если ТипЗнч(РеализацияИлиСчетФактура) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
	Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ДокРеализацияТоваровУслуг.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА ДокРеализацияТоваровУслуг.Грузоотправитель
		|		ИНАЧЕ ДокЗаказКлиента.Грузоотправитель
		|	КОНЕЦ КАК Грузоотправитель,
		|	ВЫБОР
		|		КОГДА ДокРеализацияТоваровУслуг.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА ДокРеализацияТоваровУслуг.БанковскийСчетГрузоотправителя
		|		ИНАЧЕ ДокЗаказКлиента.БанковскийСчетГрузоотправителя
		|	КОНЕЦ КАК БанковскийСчетГрузоотправителя,
		|	ВЫБОР
		|		КОГДА ДокРеализацияТоваровУслуг.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА ДокРеализацияТоваровУслуг.БанковскийСчетГрузоотправителя.НомерСчета
		|		ИНАЧЕ ДокЗаказКлиента.БанковскийСчетГрузоотправителя.НомерСчета
		|	КОНЕЦ КАК БанковскийСчетГрузоотправителяНомерСчета,
		|	ВЫБОР
		|		КОГДА ДокРеализацияТоваровУслуг.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА ДокРеализацияТоваровУслуг.БанковскийСчетГрузоотправителя.Банк.Код
		|		ИНАЧЕ ДокЗаказКлиента.БанковскийСчетГрузоотправителя.Банк.Код
		|	КОНЕЦ КАК БанковскийСчетГрузоотправителяБанкКод,
		|	ВЫБОР
		|		КОГДА ДокРеализацияТоваровУслуг.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА ДокРеализацияТоваровУслуг.БанковскийСчетГрузоотправителя.Банк.Наименование
		|		ИНАЧЕ ДокЗаказКлиента.БанковскийСчетГрузоотправителя.Банк.Наименование
		|	КОНЕЦ КАК БанковскийСчетГрузоотправителяБанкНаименование,
		|	ВЫБОР
		|		КОГДА ДокРеализацияТоваровУслуг.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА ДокРеализацияТоваровУслуг.БанковскийСчетГрузоотправителя.Банк.КоррСчет
		|		ИНАЧЕ ДокЗаказКлиента.БанковскийСчетГрузоотправителя.Банк.КоррСчет
		|	КОНЕЦ КАК БанковскийСчетГрузоотправителяБанкКоррСчет,
		|	ВЫБОР
		|		КОГДА ДокРеализацияТоваровУслуг.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА ДокРеализацияТоваровУслуг.БанковскийСчетГрузоотправителя.Банк.Город
		|		ИНАЧЕ ДокЗаказКлиента.БанковскийСчетГрузоотправителя.Банк.Город
		|	КОНЕЦ КАК БанковскийСчетГрузоотправителяБанкГород,
		|	ВЫБОР
		|		КОГДА ДокРеализацияТоваровУслуг.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА ДокРеализацияТоваровУслуг.БанковскийСчетГрузоотправителя.Банк.Адрес
		|		ИНАЧЕ ДокЗаказКлиента.БанковскийСчетГрузоотправителя.Банк.Адрес
		|	КОНЕЦ КАК БанковскийСчетГрузоотправителяБанкАдрес,
		|	ВЫБОР
		|		КОГДА ДокРеализацияТоваровУслуг.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА ДокРеализацияТоваровУслуг.Грузоотправитель.ИНН
		|		ИНАЧЕ ДокЗаказКлиента.Грузоотправитель.ИНН
		|	КОНЕЦ КАК ГрузоотправительИНН,
		|	ВЫБОР
		|		КОГДА ДокРеализацияТоваровУслуг.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА ДокРеализацияТоваровУслуг.Грузоотправитель.КПП
		|		ИНАЧЕ ДокЗаказКлиента.Грузоотправитель.КПП
		|	КОНЕЦ КАК ГрузоотправительКПП,
		|	ВЫБОР
		|		КОГДА ДокРеализацияТоваровУслуг.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА ДокРеализацияТоваровУслуг.Грузоотправитель.Наименование
		|		ИНАЧЕ ДокЗаказКлиента.Грузоотправитель.Наименование
		|	КОНЕЦ КАК ГрузоотправительНаименование,
		|	ВЫБОР
		|		КОГДА ДокРеализацияТоваровУслуг.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА ДокРеализацияТоваровУслуг.Грузоотправитель.НаименованиеПолное
		|		ИНАЧЕ ДокЗаказКлиента.Грузоотправитель.НаименованиеПолное
		|	КОНЕЦ КАК ГрузоотправительНаименованиеПолное,
		|	ПРЕДСТАВЛЕНИЕ(ДокРеализацияТоваровУслуг.Договор) КАК ДоговорПредставление
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК ДокРеализацияТоваровУслуг
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДокЗаказКлиента
		|		ПО ДокРеализацияТоваровУслуг.ЗаказКлиента = ДокЗаказКлиента.Ссылка
		|ГДЕ
		|	ДокРеализацияТоваровУслуг.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр( "Ссылка", РеализацияИлиСчетФактура );
		Выб = Запрос.Выполнить().Выбрать();
		Выб.Следующий();
		
		Ответ = Новый Структура( "Грузоотправитель, Наименование, ОфициальноеНаименование, БанковскийСчетГрузоотправителя, НомерСчета, НаименованиеБанка, БИК, КоррСчетБанка, ГородБанка, ИНН, КПП, ДоговорПредставление", 
					Выб.Грузоотправитель,
					Выб.ГрузоотправительНаименование,
					Выб.ГрузоотправительНаименованиеПолное,
					Выб.БанковскийСчетГрузоотправителя,
					Выб.БанковскийСчетГрузоотправителяНомерСчета,
					Выб.БанковскийСчетГрузоотправителяБанкНаименование,
					Выб.БанковскийСчетГрузоотправителяБанкКод,
					Выб.БанковскийСчетГрузоотправителяБанкКоррСчет,
					Выб.БанковскийСчетГрузоотправителяБанкГород,
					Выб.ГрузоотправительИНН,
					Выб.ГрузоотправительКПП,
					СокрЛП(СтрЗаменить(Выб.ДоговорПредставление, "Договор", ""))
					);
					
	ИначеЕсли ТипЗнч(РеализацияИлиСчетФактура) = Тип("ДокументСсылка.СчетФактураВыданный")
	Тогда
		
		Ответ =  ПолучитьОрганизациюДляПечати( РеализацияИлиСчетФактура.ДокументОснование );
		
	Иначе
		Отивет = Новый Структура();
	КонецЕсли;
					
	Возврат Ответ;
		
КонецФункции

//СС 09.01.19 для изменений в 2.4.6
Функция ПолучитьМенеджер(Ключ)
	МенеджерОбъекта = ОбщегоНазначенияУТ.ПолучитьМодульЛокализации(Ключ);
	Если МенеджерОбъекта = Неопределено Тогда
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Ключ);
	КонецЕсли;
	Возврат МенеджерОбъекта;
КонецФункции
