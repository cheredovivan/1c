&После("ПередЗаписью")  
Процедура Расш1_ДобавитьИзделиеВКомментарий(Отказ)  
	Попытка
		Документ = ЭтотОбъект; 
		КоличествоСтрокТЧ = Документ.ОбъектыКалькуляции.Количество(); 
		
		// Проверка КонецДействия спецификаций одним запросом (без разыменований в цикле)
		СпецификацииДляПроверки = Новый Массив;
		Для Каждого СтрокаТЧ Из Документ.ОбъектыКалькуляции Цикл
			Если ТипЗнч(СтрокаТЧ.Объект) = Тип("СправочникСсылка.РесурсныеСпецификации") 
				И ЗначениеЗаполнено(СтрокаТЧ.Объект) Тогда
				СпецификацииДляПроверки.Добавить(СтрокаТЧ.Объект);
			КонецЕсли;
		КонецЦикла;
		
		Если СпецификацииДляПроверки.Количество() > 0 Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
				|	РесурсныеСпецификации.Ссылка КАК Спецификация,
				|	РесурсныеСпецификации.КонецДействия КАК КонецДействия
				|ИЗ
				|	Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
				|ГДЕ
				|	РесурсныеСпецификации.Ссылка В (&СпецификацииДляПроверки)
				|	И РесурсныеСпецификации.КонецДействия <> ДАТАВРЕМЯ(1, 1, 1)
				|	И РесурсныеСпецификации.КонецДействия < &ДатаДокумента";
			Запрос.УстановитьПараметр("СпецификацииДляПроверки", СпецификацииДляПроверки);
			Запрос.УстановитьПараметр("ДатаДокумента", Документ.Дата);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					"Дата документа (" + Формат(Документ.Дата, "ДЛФ=D") + ") больше даты окончания действия спецификации «" 
					+ Выборка.Спецификация + "» (" + Формат(Выборка.КонецДействия, "ДЛФ=D") + ")!");
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		// Соответствие для отслеживания уже добавленных номенклатур (включая те, что уже в комментарии)
		ДобавленныеНоменклатуры = Новый Соответствие;
		
		// Сначала собираем номенклатуры, которые уже есть в комментарии
		Если НЕ ПустаяСтрока(Документ.Комментарий) Тогда
			СтрокиКомментария = СтрРазделить(Документ.Комментарий, Символы.ПС);
			Для Каждого СтрокаКомментария Из СтрокиКомментария Цикл
				СтрокаКомментария = СокрЛП(СтрокаКомментария);
				Если НЕ ПустаяСтрока(СтрокаКомментария) Тогда
					ДобавленныеНоменклатуры.Вставить(СтрокаКомментария, Истина);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Для Счетчик= -(КоличествоСтрокТЧ - 1) по 0 Цикл
			СтрокаТЧ = Документ.ОбъектыКалькуляции[-Счетчик]; 
			Номенклатура = СтрокаТЧ.Номенклатура;
			
			// Пропускаем, если номенклатура не заполнена
			Если Номенклатура = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			// Проверяем, не была ли эта номенклатура уже добавлена (в текущем цикле или в комментарии)
			КлючНоменклатуры = СокрЛП(Строка(Номенклатура));   
			ЕстьТакаяСтрока = ДобавленныеНоменклатуры.Получить(КлючНоменклатуры);
			Если ЕстьТакаяСтрока = Неопределено Тогда
				Документ.Комментарий = Документ.Комментарий + Номенклатура + Символы.ПС;
				ДобавленныеНоменклатуры.Вставить(КлючНоменклатуры, Истина);
			КонецЕсли;
		КонецЦикла;
		
		// Заполняем регистр спецификаций в калькуляциях
		Расш1_ОбщийМодульКалькуляция.ЗаполнитьРегистрСпецификацийВКалькуляциях(Документ.Ссылка);
		
	Исключение
		Сооб = Новый СообщениеПользователю;
		Сооб.Текст =  "Не удалось обработать документ";
		Сооб.Сообщить();
	КонецПопытки;
КонецПроцедуры   

&ИзменениеИКонтроль("ЗапуститьФоновуюОбработкуДвижений")
Процедура Расш1_ЗапуститьФоновуюОбработкуДвижений(Калькуляция, ЭтоПроведение)
   #Удаление
	Документы.ПлановаяКалькуляция2_2.ЗапускВыполненияФоновогоЗадания(Калькуляция, ЭтоПроведение);
   #КонецУдаления
КонецПроцедуры
