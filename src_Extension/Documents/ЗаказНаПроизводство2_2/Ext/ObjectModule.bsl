
&После("ПередЗаписью")  
Процедура Расш1_ДобавитьМаркерПроверкиВНазваниеСпецификаций()  
	Результат = Расш1_ОбщийМодуль1.СпецификацияТребуетПроверкиСлужбамиСКБиОГТ(Ссылка);
	
КонецПроцедуры


&ИзменениеИКонтроль("ЗаполнитьПоЗаказуКлиента")
Процедура Расш1_ЗаполнитьПоЗаказуКлиента(ДанныеЗаполнения)
	
	ЗаказКлиента = ДанныеЗаполнения.Основание;
	#Удаление
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗаказКлиента.Приоритет          КАК Приоритет,
		|	ЗаказКлиента.Организация        КАК Организация,
		|	ЗаказКлиента.НалогообложениеНДС КАК НалогообложениеНДС,
		|	ВЫБОР 
		|		КОГДА ЗаказКлиента.НаправлениеДеятельности.УчетЗатрат
		|			ТОГДА ЗаказКлиента.НаправлениеДеятельности
		|	КОНЕЦ КАК НаправлениеДеятельности
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|ГДЕ
		|	ЗаказКлиента.Ссылка = &ЗаказКлиента");
	#КонецУдаления
	#Вставка
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗаказКлиента.Приоритет          КАК Приоритет,
		|	ЗаказКлиента.Организация        КАК Организация,
		|	ЗаказКлиента.НалогообложениеНДС КАК НалогообложениеНДС,
		|	ЗаказКлиента.Комментарий        КАК Комментарий,
		|	ВЫБОР 
		|		КОГДА ЗаказКлиента.НаправлениеДеятельности.УчетЗатрат
		|			ТОГДА ЗаказКлиента.НаправлениеДеятельности
		|	КОНЕЦ КАК НаправлениеДеятельности
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|ГДЕ
		|	ЗаказКлиента.Ссылка = &ЗаказКлиента");
	#КонецВставки
	
	Запрос.УстановитьПараметр("ЗаказКлиента", ЗаказКлиента);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	// Заполнение шапки
	Организация             = Реквизиты.Организация;
	ДокументОснование       = ЗаказКлиента;
	ЗаказПодДеятельность    = Реквизиты.НалогообложениеНДС;
	Приоритет               = Реквизиты.Приоритет;
	НаправлениеДеятельности = Реквизиты.НаправлениеДеятельности;
	#Вставка
	Комментарий = "Проверено: ___, ___, ___" + Символы.ПС + Реквизиты.Комментарий;
	// Заполняем подразделение-диспетчера из последнего этапа приоритетной спецификации первого товара
	ЗапросПодразделения = Новый Запрос;
	ЗапросПодразделения.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЭтапыПроизводства.Подразделение КАК Подразделение
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК ВыходныеИзделия
	|		ПО ВыходныеИзделия.Номенклатура = ЗаказКлиентаТовары.Номенклатура
	|			И ВыходныеИзделия.Ссылка.ВариантПодбораВДокументы = ЗНАЧЕНИЕ(Перечисление.ВариантыПодбораСпецификацииВДокументы.Автоматически)
	|			И ВыходныеИзделия.Ссылка.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Сборка)
	|			И ВыходныеИзделия.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.Действует)
	|			И (ВыходныеИзделия.Ссылка.НачалоДействия = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ ВыходныеИзделия.Ссылка.НачалоДействия <= &ТекущаяДата)
	|			И (ВыходныеИзделия.Ссылка.КонецДействия = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ ВыходныеИзделия.Ссылка.КонецДействия > &ТекущаяДата)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|		ПО ЭтапыПроизводства.Владелец = ВыходныеИзделия.Ссылка
	|			И ЭтапыПроизводства.НомерСледующегоЭтапа = 0
	|ГДЕ
	|	ЗаказКлиентаТовары.Ссылка = &ЗаказКлиента
	|	И НЕ ЗаказКлиентаТовары.Отменено
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказКлиентаТовары.НомерСтроки,
	|	ВыходныеИзделия.Ссылка.Код УБЫВ";
	ЗапросПодразделения.УстановитьПараметр("ЗаказКлиента", ЗаказКлиента);
	ЗапросПодразделения.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	ВыборкаПодразделения = ЗапросПодразделения.Выполнить().Выбрать();
	Если ВыборкаПодразделения.Следующий() Тогда
		Подразделение = ВыборкаПодразделения.Подразделение;
	КонецЕсли;
	#КонецВставки
	
	// Заполнение табличной части
	ЗаполнитьПоТаблицеТоварыИзХранилища(ДанныеЗаполнения.АдресТовары, "ДатаОтгрузки");
	
КонецПроцедуры

