&После("ПередЗаписью")
Процедура Расш1_ПередЗаписью(Отказ, РежимЗаписи)
	ЭтотОбъект.ДополнительныеСвойства.Вставить("Расш1_РежимЗаписи", РежимЗаписи);

	// Очистка табличных частей и установка статуса для спецификаций из группы "Ремонт"
	// Только если статус не "Начат" и не "Завершен"
	Если ЭтотОбъект.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.Начат
		ИЛИ ЭтотОбъект.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.Завершен Тогда
		Возврат;
	КонецЕсли;
	
	// Ранний выход если нет спецификации
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.Спецификация) Тогда
		Возврат;
	КонецЕсли;
	
	// Проверяем группу спецификации
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЭтоРемонт
	|ИЗ
	|	Справочник.РесурсныеСпецификации КАК Спецификация
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации КАК ГруппаРемонт
	|	ПО Спецификация.Родитель = ГруппаРемонт.Ссылка
	|ГДЕ
	|	Спецификация.Ссылка = &Спецификация
	|	И ГруппаРемонт.Наименование = ""Ремонт""
	|	И ГруппаРемонт.ЭтоГруппа";
	Запрос.УстановитьПараметр("Спецификация", ЭтотОбъект.Спецификация);
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		ЭтотОбъект.ОбеспечениеМатериаламиИРаботами.Очистить();
		ЭтотОбъект.ПобочныеИзделия.Очистить();
		ЭтотОбъект.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.КВыполнению;
	КонецЕсли;
КонецПроцедуры   

&После("ПриЗаписи")
Процедура Подписка_ЭтапПроизводства_ПриПроведении(Отказ) Экспорт
	Попытка  
        Расш1_ОбщийМодуль1.ОбновитьМатСоставИзделийПоДокументу(ЭтотОбъект, Отказ);
    Исключение
        Отказ = Истина;
		ТекстОшибки = "МатСоставИзделий: ошибка при записи движений: " + ОписаниеОшибки();
        Сообщить(ТекстОшибки);
		// Прямая отправка в Telegram для диагностики
		Попытка
			СообщениеВТГ = "Этап " + СокрЛП(ЭтотОбъект.Номер) + ": " + ТекстОшибки;
			Расш1_ОбщийМодуль1.ОтправитьВTelegram(СообщениеВТГ);
		Исключение
			// Ошибка отправки в Telegram не должна прерывать работу
			Сообщить("Не удалось отправить в Telegram: " + ОписаниеОшибки());
		КонецПопытки;
    КонецПопытки;
КонецПроцедуры   

