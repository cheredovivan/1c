Процедура ЗаписатьДополнительноеСвойство(Свойство, Значение )
	Запись = РегистрыСведений.ДополнительныеСведения.СоздатьМенеджерЗаписи();
	Запись.Объект = Ссылка;
	Запись.Свойство = Свойство;
	Если ЗначениеЗаполнено(Значение)
	Тогда
		Запись.Значение = Значение;
		Запись.Записать(Истина);
	Иначе
		Запись.Удалить();
	КонецЕсли;
КонецПроцедуры

Процедура УстановитьКемИКогдаУстановленыСтатусы( ПредСтатус )
	
	Рекв_КемПереданНаУтверждение = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("План производства передан на утверждение (Планы производства)", Истина ); 
	Рекв_КогдаПереданНаУтверждение = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("План производства передан на утверждение, дата (Планы производства)", Истина ); 
	Рекв_КемУтвержден = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("План производства утвержден (Планы производства)", Истина ); 
	Рекв_КогдаУтвержден = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("План производства утвержден, дата (Планы производства)", Истина ); 
		
	Если Статус = Перечисления.СтатусыПланов.Утвержден И 
		ПредСтатус <> Перечисления.СтатусыПланов.Утвержден
	Тогда
		ЗаписатьДополнительноеСвойство(Рекв_КемУтвержден, ПараметрыСеанса.ТекущийПользователь);
		ЗаписатьДополнительноеСвойство(Рекв_КогдаУтвержден,ТекущаяДата());
	ИначеЕсли ПредСтатус = Перечисления.СтатусыПланов.Утвержден И 
		Статус <> Перечисления.СтатусыПланов.Утвержден
	Тогда
		ЗаписатьДополнительноеСвойство(Рекв_КемУтвержден, Справочники.Пользователи.ПустаяСсылка());
		ЗаписатьДополнительноеСвойство(Рекв_КогдаУтвержден, Дата(1,1,1) );
	КонецЕсли;
		
	Если Статус = Перечисления.СтатусыПланов.НаУтверждении И 
		ПредСтатус <> Перечисления.СтатусыПланов.Утвержден
	Тогда
		ЗаписатьДополнительноеСвойство(Рекв_КемПереданНаУтверждение, ПараметрыСеанса.ТекущийПользователь);
		ЗаписатьДополнительноеСвойство(Рекв_КогдаПереданНаУтверждение,ТекущаяДата());
		
	ИначеЕсли (ПредСтатус = Перечисления.СтатусыПланов.НаУтверждении Или ПредСтатус = Перечисления.СтатусыПланов.Утвержден) 
		И (Статус = Перечисления.СтатусыПланов.ВПодготовке Или Статус = Перечисления.СтатусыПланов.Отменен)
	Тогда
		ЗаписатьДополнительноеСвойство(Рекв_КемПереданНаУтверждение, Справочники.Пользователи.ПустаяСсылка());
		ЗаписатьДополнительноеСвойство(Рекв_КогдаПереданНаУтверждение, Дата(1,1,1) );
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьПраваНаУстановкуСтатуса( Отказ, ПредСтатус, МожетУстанавливатьСтатусНаУтверждении, МожетУстанавливатьСтатусУтвержден )

	Если ПредСтатус <> Статус
	Тогда
		Если Статус = Перечисления.СтатусыПланов.Отменен И
			Не МожетУстанавливатьСтатусНаУтверждении И
			Не МожетУстанавливатьСтатусУтвержден
	        Тогда
			Сооб = Новый СообщениеПользователю;
			Сооб.Текст = "Вам не разрешено устанавливать статус ""Отменен"" для плана производства";
			Сооб.Сообщить();
			Отказ = Истина;
		КонецЕсли;
	
		Если Статус = Перечисления.СтатусыПланов.Утвержден И
			ПредСтатус <> Перечисления.СтатусыПланов.НаУтверждении
		Тогда
			Сооб = Новый СообщениеПользователю;
			Сооб.Текст = "Статус ""Утвержден"" разрешено устанавливать только из статуса ""На утверждении""";
			Сооб.Сообщить();
			Отказ = Истина;
		КонецЕсли;
	
		Если ПредСтатус = Перечисления.СтатусыПланов.Утвержден Или
			Статус = Перечисления.СтатусыПланов.Утвержден
		Тогда
			Если Не  МожетУстанавливатьСтатусУтвержден
		        Тогда
				Сооб = Новый СообщениеПользователю;
				Сооб.Текст = "Вам не разрешено устанавливать/изменять статус ""Утвержден"" для плана производства";
				Сооб.Сообщить();
				Отказ = Истина;
			КонецЕсли;
			
		ИначеЕсли Статус = Перечисления.СтатусыПланов.НаУтверждении
		Тогда
			Если ПредСтатус = Перечисления.СтатусыПланов.Утвержден
			Тогда
				Если Не  МожетУстанавливатьСтатусУтвержден
			        Тогда
					Сооб = Новый СообщениеПользователю;
					Сооб.Текст = "Вам не разрешено изменять статус ""Утвержден"" для плана производства";
					Сооб.Сообщить();
					Отказ = Истина;
				КонецЕсли;
			Иначе
				Если Не  МожетУстанавливатьСтатусНаУтверждении
			        Тогда
					Сооб = Новый СообщениеПользователю;
					Сооб.Текст = "Вам не разрешено устанавливать статус ""На утверждении"" для плана производства";
					Сооб.Сообщить();
					Отказ = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&Вместо("ПередЗаписью")
Процедура Расш1_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	// Вставить содержимое обработчика.
	//Если ЭтоНовый()
	//Тогда
	//	ПродолжитьВызов(Отказ, РежимЗаписи, РежимПроведения);
	//КонецЕсли;
	
	Если (Проведен И РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения) 
		Или РежимЗаписи = РежимЗаписиДокумента.Проведение
	Тогда
	        ДеньДаты = День(Дата);
		Если ДеньДаты <> 25 И
			ДеньДаты <> 5 И
			ДеньДаты <> 15
		Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Дата документа ожидается 25, 5 или 15 числа");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		Если ДеньДаты = 25
		Тогда
			МесяцПланирования = ДобавитьМесяц(НачалоМесяца(Дата), 1);
		Иначе
			МесяцПланирования = НачалоМесяца(Дата);
		КонецЕсли;
		НачалоПериода = МесяцПланирования;
		ОкончаниеПериода = КонецМесяца(МесяцПланирования);
		Для каждого Стр из Продукция цикл
			Стр.ДатаЗапуска = НачалоПериода;
			Стр.ДатаВыпуска = ОкончаниеПериода;
		КонецЦикла;
		//Если НачалоПериода <> МесяцПланирования Или
		//	КонецМесяца(ОкончаниеПериода) <> КонецМесяца(МесяцПланирования)
		//Тогда
		//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Период планирования ожидается " + НРег(Формат(МесяцПланирования,"ДФ='ММММ гггг ""г.""'")));
		//	Отказ = Истина;
		//	Возврат;
		//КонецЕсли;			
	КонецЕсли;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Пользователь,
	|	ВЫБОР
	|		КОГДА ПланПроизводстваНаУтверждении.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК МожетУстанавливатьСтатусНаУтверждении,
	|	ВЫБОР
	|		КОГДА ПланПроизводстваУтвержден.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК МожетУстанавливатьСтатусУтвержден
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ГруппыПользователейСостав.Пользователь КАК Пользователь,
	|			ГруппыПользователейСостав.Ссылка КАК Ссылка
	|		ИЗ
	|			Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	|		ГДЕ
	|			ГруппыПользователейСостав.Пользователь = &Пользователь
	|			И ГруппыПользователейСостав.Ссылка.Наименование = &ПланПроизводстваНаУтверждении) КАК ПланПроизводстваНаУтверждении
	|		ПО Пользователи.Ссылка = ПланПроизводстваНаУтверждении.Пользователь
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ГруппыПользователейСостав.Пользователь КАК Пользователь,
	|			ГруппыПользователейСостав.Ссылка КАК Ссылка
	|		ИЗ
	|			Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	|		ГДЕ
	|			ГруппыПользователейСостав.Пользователь = &Пользователь
	|			И ГруппыПользователейСостав.Ссылка.Наименование = &ПланПроизводстваУтвержден) КАК ПланПроизводстваУтвержден
	|		ПО Пользователи.Ссылка = ПланПроизводстваУтвержден.Пользователь
	|ГДЕ
	|	Пользователи.Ссылка = &Пользователь";
	Запрос.УстановитьПараметр( "Пользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр( "ПланПроизводстваУтвержден", "План производства - Утвержден");
	Запрос.УстановитьПараметр( "ПланПроизводстваНаУтверждении", "План производства - На утверждении");
	Выб = Запрос.Выполнить().Выбрать();
	Если Не Выб.Следующий()
	Тогда
		МожетУстанавливатьСтатусНаУтверждении = Ложь;
		МожетУстанавливатьСтатусУтвержден = Ложь;
	Иначе
		МожетУстанавливатьСтатусНаУтверждении = Выб.МожетУстанавливатьСтатусНаУтверждении;
		МожетУстанавливатьСтатусУтвержден = Выб.МожетУстанавливатьСтатусУтвержден;
	КонецЕсли;
	
	Если Статус = Перечисления.СтатусыПланов.ВПодготовке
	Тогда
		РазрешеноРедактироватьТабличнуюЧасть = Истина;
	ИначеЕсли Статус = Перечисления.СтатусыПланов.НаУтверждении
	Тогда
		РазрешеноРедактироватьТабличнуюЧасть = ?(МожетУстанавливатьСтатусНаУтверждении, Истина, Ложь );
	Иначе
		РазрешеноРедактироватьТабличнуюЧасть = Ложь;
	КонецЕсли;		
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПланПроизводства.Ссылка КАК Ссылка,
	|	ПланПроизводства.ВерсияДанных КАК ВерсияДанных,
	|	ПланПроизводства.ПометкаУдаления КАК ПометкаУдаления,
	|	ПланПроизводства.Номер КАК Номер,
	|	ПланПроизводства.Дата КАК Дата,
	|	ПланПроизводства.Проведен КАК Проведен,
	|	ПланПроизводства.Статус КАК Статус,
	|	ПланПроизводства.Продукция.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		Упаковка КАК Упаковка,
	|		Количество КАК Количество,
	|		КоличествоУпаковок КАК КоличествоУпаковок,
	|		ДатаЗапуска КАК ДатаЗапуска,
	|		ДатаВыпуска КАК ДатаВыпуска,
	|		Спецификация КАК Спецификация,
	|		ДополнительныеПараметры КАК ДополнительныеПараметры,
	|		Отменено КАК Отменено,
	|		Комментарий КАК Комментарий,
	|		Полуфабрикат КАК Полуфабрикат,
	|		Назначение КАК Назначение,
	|		Замещен КАК Замещен,
	|		ДатаВыпускаПродукцииПолуфабриката КАК ДатаВыпускаПродукцииПолуфабриката
	|	) КАК Продукция,
	|	ПланПроизводства.ДополнительныеРеквизиты.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Свойство КАК Свойство,
	|		Значение КАК Значение,
	|		ТекстоваяСтрока КАК ТекстоваяСтрока
	|	) КАК ДополнительныеРеквизиты
	|ИЗ
	|	Документ.ПланПроизводства КАК ПланПроизводства
	|ГДЕ
	|	ПланПроизводства.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр( "Ссылка", Ссылка );
	Выб = Запрос.Выполнить().Выбрать();
	Выб.Следующий();
	ПредСтатус  = Выб.Статус;
	
	ПроверитьПраваНаУстановкуСтатуса( Отказ, ПредСтатус, МожетУстанавливатьСтатусНаУтверждении, МожетУстанавливатьСтатусУтвержден );
	Если Не Отказ
	Тогда
		УстановитьКемИКогдаУстановленыСтатусы( ПредСтатус );
	КонецЕсли;
	
	Если Не Отказ И Не РазрешеноРедактироватьТабличнуюЧасть
	Тогда
		ПредПродукция = Выб.Продукция.Выгрузить();
		Для Каждого Товар из Продукция
		Цикл
			ПараметрыОтбора = Новый Структура("Номенклатура, КоличествоУпаковок, ДатаВыпуска, Назначение, Спецификация", 
					Товар.Номенклатура,
					Товар.КоличествоУпаковок,
					Товар.ДатаВыпуска,
					Товар.Назначение,
					Товар.Спецификация);
			МассивСтрок = ПредПродукция.НайтиСтроки( ПараметрыОтбора );
			Если МассивСтрок.Количество() <> 1
			Тогда
				Сооб = Новый СообщениеПользователю;
				Сооб.Текст = "Изменять состав плана производства не разрешено (недостаточно прав)";
				Сооб.Сообщить();
				Отказ = Истина;
				Прервать ;
			КонецЕсли;
			ПредПродукция.Удалить( МассивСтрок[0] );
		КонецЦикла;
		
		Если ПредПродукция.Количество() <> 0
		Тогда
			Сооб = Новый СообщениеПользователю;
			Сооб.Текст = "Изменять состав плана производства не разрешено (недостаточно прав)";
			Сооб.Сообщить();
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ПродолжитьВызов(Отказ, РежимЗаписи, РежимПроведения);
	
	
КонецПроцедуры
