//Использование 
//Отчет Расш1_Плановая_калькуляция_сквозная.МодульОбъекта 

Функция ВычислитьАлгоритмРасчетаКоличества( АлгоритмРасчетаКоличества, КоличествоИзделий ) Экспорт

	Расчетноеколичество = КоличествоИзделий;
	
	Формула  = СтрЗаменить( АлгоритмРасчетаКоличества, "[#Продукция.Количество#]", Формат(Расчетноеколичество,"ЧРД=.; ЧГ=0") );
	Формула  = СтрЗаменить( Формула, "#Продукция.Количество#", Формат(Расчетноеколичество,"ЧРД=.; ЧГ=0") );
	Формула  = СтрЗаменить( Формула, "[Продукция.Количество]", Формат(Расчетноеколичество,"ЧРД=.; ЧГ=0") );
	Формула  = СтрЗаменить( Формула, "[ВыходныеИзделия[1].Количество]", Формат(Расчетноеколичество,"ЧРД=.; ЧГ=0") );
	Формула  = СтрЗаменить( Формула, "[ОсновноеИзделие.Количество]", Формат(Расчетноеколичество,"ЧРД=.; ЧГ=0") );
	Количество = Вычислить( Формула );
	                                                                                    
	Возврат Количество;
	
КонецФункции

Функция ТаблицыМатериаловИТрудозатратИтоговые(ДокументПлановаяКалькуляция) Экспорт
	
	Результат = Новый Структура;
	
	ДатаДокументаПлановаяКалькуляция = ДатаДокументаПлановаяКалькуляция(ДокументПлановаяКалькуляция); 
	ВидЦены = ВидЦеныДокументаПлановаяКалькуляция(ДокументПлановаяКалькуляция);
	
	ИменаРеквизитов = Новый Массив;
	ИменаРеквизитов.Добавить("ПлановыйПроцентПремии");
	ИменаРеквизитов.Добавить("ПлановыйПроцентЗаВредность");
	Структура = ПлановыеПроцентыТрудозатрат(ИменаРеквизитов);   	

	ПлановыйПроцентЗаВредность = Структура.ПлановыйПроцентЗаВредность;
	ПлановыйПроцентПремии = Структура.ПлановыйПроцентПремии;
	 
	
	ТаблицаМатериаловИУслуг = Новый ТаблицаЗначений;
	ТаблицаМатериаловИУслуг.Колонки.Добавить("ИзделиеМатериаловИУслуг"); 
	ТаблицаМатериаловИУслуг.Колонки.Добавить("КодИзделияМатериаловИУслуг");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("Спецификация");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("КоличествоУпаковокИзделия");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("КодМатериаловИУслуг");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("НоменклатураМатериаловИУслуг");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("УпаковкаМатериаловИУслуг");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("КоличествоУпаковокМатериаловИУслуг");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("СтатьяКалькуляцииМатериаловИУслуг");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("Цена");   
	ТаблицаМатериаловИУслуг.Колонки.Добавить("ДокументОснование");  
	ТаблицаМатериаловИУслуг.Колонки.Добавить("Стоимость");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("СтоимостьЗаЕдиницуИзделий");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("Остаток");
	
	ТаблицаТрудозатрат = Новый ТаблицаЗначений;  
	ТаблицаТрудозатрат.Колонки.Добавить("ИзделиеТрудозатрат"); 
    ТаблицаТрудозатрат.Колонки.Добавить("ВидРаботТрудозатрат");   
	ТаблицаТрудозатрат.Колонки.Добавить("КодТрудозатрат");
    ТаблицаТрудозатрат.Колонки.Добавить("КоличествоТрудозатрат");
	ТаблицаТрудозатрат.Колонки.Добавить("ПолуфабрикатТрудозатрат");
	ТаблицаТрудозатрат.Колонки.Добавить("КоличествоПолуфабрикатаТрудозатрат");
    ТаблицаТрудозатрат.Колонки.Добавить("СтатьяКалькуляцииТрудозатрат");  
    ТаблицаТрудозатрат.Колонки.Добавить("ПодразделениеТрудозатрат"); 
	ТаблицаТрудозатрат.Колонки.Добавить("Расценка");
	
	ТаблицаТрудозатрат.Колонки.Добавить("СдельнаяРасценка");
	ТаблицаТрудозатрат.Колонки.Добавить("УсловияТруда");
	ТаблицаТрудозатрат.Колонки.Добавить("Премия");
	ТаблицаТрудозатрат.Колонки.Добавить("РайонныйКоэффициент");
	ТаблицаТрудозатрат.Колонки.Добавить("СуммаТрудозатрат");
	ТаблицаТрудозатрат.Колонки.Добавить("СдельнаяРасценкаЗаЕдиницу");
	ТаблицаТрудозатрат.Колонки.Добавить("СуммаТрудозатратЗаЕдиницу");
	
	
	ТаблицаОбъектовКалькуляции = ТаблицаОбъектовКалькуляции(ДокументПлановаяКалькуляция);
	
	Для Каждого ОбъектКалькуляции Из ТаблицаОбъектовКалькуляции Цикл 

		ИзделиеОбъектаКалькуляции = ОбъектКалькуляции.Номенклатура;
		КодИзделияОбъектаКалькуляции = ОбъектКалькуляции.Код;
		СпецификацияОбъектаКалькуляции = ОбъектКалькуляции.Спецификация;			
		КоличествоОбъектаКалькуляции = ОбъектКалькуляции.КоличествоУпаковок;
		
		Структура = ТаблицыМатериаловИТрудозатратДереваСпецификации(СпецификацияОбъектаКалькуляции, КоличествоОбъектаКалькуляции, ДатаДокументаПлановаяКалькуляция);	
		
		ТаблицаМатериаловИУслугИсточник = Структура.ТаблицаМатериаловИУслуг; 	
		
		СписокНоменклатур = ТаблицаМатериаловИУслугИсточник.ВыгрузитьКолонку("Номенклатура");  
		
		ТаблицаЦен = ТаблицаЦенИДокументовОснований(СписокНоменклатур, ДатаДокументаПлановаяКалькуляция, ВидЦены);
		
		Для Каждого Строка Из ТаблицаМатериаловИУслугИсточник Цикл
			
			НоваяСтрока = ТаблицаМатериаловИУслуг.Добавить();		
			НоваяСтрока.ИзделиеМатериаловИУслуг = ИзделиеОбъектаКалькуляции;
			НоваяСтрока.КодИзделияМатериаловИУслуг = КодИзделияОбъектаКалькуляции;
			НоваяСтрока.Спецификация = СпецификацияОбъектаКалькуляции;	
			
			КоличествоУпаковокИзделия = КоличествоОбъектаКалькуляции;
			НоваяСтрока.КоличествоУпаковокИзделия = КоличествоУпаковокИзделия;   		
			НоваяСтрока.НоменклатураМатериаловИУслуг = Строка.Номенклатура;		
			НоваяСтрока.КодМатериаловИУслуг = Строка.Код;		
			НоваяСтрока.УпаковкаМатериаловИУслуг = Строка.Упаковка; 
			
			КоличествоУпаковокМатериаловИУслуг = Строка.КоличествоУпаковок;			
			НоваяСтрока.КоличествоУпаковокМатериаловИУслуг = КоличествоУпаковокМатериаловИУслуг;// * КоличествоУпаковокИзделия; 			
			НоваяСтрока.СтатьяКалькуляцииМатериаловИУслуг = Строка.СтатьяКалькуляции;
			
			НайденнаяСтрока = ТаблицаЦен.Найти(Строка.Номенклатура, "Номенклатура"); 			
			Если НайденнаяСтрока <> Неопределено Тогда
				Цена = НайденнаяСтрока.Цена;
				ДокументОснование = НайденнаяСтрока.ДокументОснование;
			Иначе
				Цена = 0;    
				ДокументОснование = Документы.ПриобретениеТоваровУслуг.ПустаяСсылка();
			КонецЕсли;			
			НоваяСтрока.Цена = Цена;   			
			НоваяСтрока.ДокументОснование = ДокументОснование;			
			НоваяСтрока.Стоимость = КоличествоУпаковокМатериаловИУслуг * Цена;			
			Если КоличествоУпаковокИзделия <> 0 Тогда
				НоваяСтрока.СтоимостьЗаЕдиницуИзделий = НоваяСтрока.Стоимость / КоличествоУпаковокИзделия;
			Иначе
				НоваяСтрока.СтоимостьЗаЕдиницуИзделий = 0;
			КонецЕсли;   
						
		КонецЦикла;                   
		
		
		ТаблицаТрудозатратИсточник = Структура.ТаблицаТрудозатрат;  
		
		СписокВидовРабот = ТаблицаТрудозатратИсточник.ВыгрузитьКолонку("ВидРаботТрудозатрат");    
		
		ТаблицаРасценкиРабот = ТаблицаРасценкиРабот(СписокВидовРабот, ДатаДокументаПлановаяКалькуляция);
		 		
		Для Каждого Строка Из ТаблицаТрудозатратИсточник Цикл  
			    			
			НоваяСтрока = ТаблицаТрудозатрат.Добавить(); 
			НоваяСтрока.ИзделиеТрудозатрат = ОбъектКалькуляции.Номенклатура;	
			НоваяСтрока.ВидРаботТрудозатрат = Строка.ВидРаботТрудозатрат;
			НоваяСтрока.КодТрудозатрат = Строка.КодТрудозатрат;
			Норма = Строка.КоличествоТрудозатрат;
			НоваяСтрока.КоличествоТрудозатрат = Норма; 
			НоваяСтрока.ПолуфабрикатТрудозатрат = Строка.ПолуфабрикатТрудозатрат;
			НоваяСтрока.КоличествоПолуфабрикатаТрудозатрат = Строка.КоличествоПолуфабрикатаТрудозатрат;
			НоваяСтрока.СтатьяКалькуляцииТрудозатрат = Строка.СтатьяКалькуляцииТрудозатрат;  
			ПодразделениеТрудозатрат = Строка.ПодразделениеТрудозатрат;
			НоваяСтрока.ПодразделениеТрудозатрат = ПодразделениеТрудозатрат;  
			
			НайденнаяСтрока = ТаблицаРасценкиРабот.Найти(Строка.ВидРаботТрудозатрат, "ВидРаботТрудозатрат"); 
			
			Если НайденнаяСтрока <> Неопределено Тогда
				Расценка = НайденнаяСтрока.Расценка;
			Иначе
				Расценка = 0;    
			КонецЕсли;			
			НоваяСтрока.Расценка = Расценка;
			
			СдельнаяРасценка = Расценка * Норма; 
			НоваяСтрока.СдельнаяРасценка = СдельнаяРасценка;
						
			ЗначениеПлановогоПроцентаЗаВредность = ПлановыйПроцентЗаВредность.Получить(ПодразделениеТрудозатрат);
			ЗначениеПлановогоПроцентаЗаВредность = ?(ЗначениеПлановогоПроцентаЗаВредность = Неопределено, 0 , ЗначениеПлановогоПроцентаЗаВредность);

			ЗначениеПлановогоПроцентаПремии = ПлановыйПроцентПремии.Получить(ПодразделениеТрудозатрат); 
			ЗначениеПлановогоПроцентаПремии = ?(ЗначениеПлановогоПроцентаПремии = Неопределено, 0 , ЗначениеПлановогоПроцентаПремии);			
			
			УсловияТруда = ЗначениеПлановогоПроцентаЗаВредность * СдельнаяРасценка / 100;
			НоваяСтрока.УсловияТруда = УсловияТруда; 
			
			Премия = ЗначениеПлановогоПроцентаПремии * СдельнаяРасценка / 100;
			НоваяСтрока.Премия = Премия; 
			
			НоваяСтрока.РайонныйКоэффициент = (СдельнаяРасценка + УсловияТруда + Премия) * 0.15;   
			
			СуммаТрудозатрат = (СдельнаяРасценка + УсловияТруда + Премия) * 1.15;
			НоваяСтрока.СуммаТрудозатрат = СуммаТрудозатрат; 
			
			Если КоличествоОбъектаКалькуляции <> 0 Тогда
				НоваяСтрока.СдельнаяРасценкаЗаЕдиницу = СдельнаяРасценка / КоличествоОбъектаКалькуляции;  
				НоваяСтрока.СуммаТрудозатратЗаЕдиницу = СуммаТрудозатрат / КоличествоОбъектаКалькуляции;
			Иначе
				НоваяСтрока.СдельнаяРасценкаЗаЕдиницу = 0;
				НоваяСтрока.СуммаТрудозатратЗаЕдиницу = 0;
			КонецЕсли;
			
			
		КонецЦикла;		
		
	КонецЦикла;	 
	
	// Получение остатков на складах с родителем "Склады ОМТСиК"
	ЗаполнитьОстаткиПоСкладамОМТСиК(ТаблицаМатериаловИУслуг, ДатаДокументаПлановаяКалькуляция);
	 		
	Результат.Вставить("ТаблицаМатериаловИУслуг", ТаблицаМатериаловИУслуг);	
	Результат.Вставить("ТаблицаТрудозатрат", ТаблицаТрудозатрат);
	
	Возврат Результат;	
		
КонецФункции

Функция ТаблицыМатериаловИТрудозатратПоПараметрам(ДокументПлановаяКалькуляция, РесурснаяСпецификация, Количество) Экспорт
	
	Результат = Новый Структура;
	
	// Получаем дату и вид цены из документа-владельца
	ДатаДокументаПлановаяКалькуляция = ДатаДокументаПлановаяКалькуляция(ДокументПлановаяКалькуляция); 
	ВидЦены = ВидЦеныДокументаПлановаяКалькуляция(ДокументПлановаяКалькуляция);
	
	ИменаРеквизитов = Новый Массив;
	ИменаРеквизитов.Добавить("ПлановыйПроцентПремии");
	ИменаРеквизитов.Добавить("ПлановыйПроцентЗаВредность");
	СтруктураПроценты = ПлановыеПроцентыТрудозатрат(ИменаРеквизитов);   	

	ПлановыйПроцентЗаВредность = СтруктураПроценты.ПлановыйПроцентЗаВредность;
	ПлановыйПроцентПремии = СтруктураПроценты.ПлановыйПроцентПремии;
	 
	
	ТаблицаМатериаловИУслуг = Новый ТаблицаЗначений;
	ТаблицаМатериаловИУслуг.Колонки.Добавить("ИзделиеМатериаловИУслуг"); 
	ТаблицаМатериаловИУслуг.Колонки.Добавить("КодИзделияМатериаловИУслуг");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("Спецификация");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("КоличествоУпаковокИзделия");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("КодМатериаловИУслуг");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("НоменклатураМатериаловИУслуг");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("УпаковкаМатериаловИУслуг");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("КоличествоУпаковокМатериаловИУслуг");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("СтатьяКалькуляцииМатериаловИУслуг");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("Цена");   
	ТаблицаМатериаловИУслуг.Колонки.Добавить("ДокументОснование");  
	ТаблицаМатериаловИУслуг.Колонки.Добавить("Стоимость");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("СтоимостьЗаЕдиницуИзделий");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("Остаток");
	
	ТаблицаТрудозатрат = Новый ТаблицаЗначений;  
	ТаблицаТрудозатрат.Колонки.Добавить("ИзделиеТрудозатрат"); 
    ТаблицаТрудозатрат.Колонки.Добавить("ВидРаботТрудозатрат");   
	ТаблицаТрудозатрат.Колонки.Добавить("КодТрудозатрат");
    ТаблицаТрудозатрат.Колонки.Добавить("КоличествоТрудозатрат");
	ТаблицаТрудозатрат.Колонки.Добавить("ПолуфабрикатТрудозатрат");
	ТаблицаТрудозатрат.Колонки.Добавить("КоличествоПолуфабрикатаТрудозатрат");
    ТаблицаТрудозатрат.Колонки.Добавить("СтатьяКалькуляцииТрудозатрат");  
    ТаблицаТрудозатрат.Колонки.Добавить("ПодразделениеТрудозатрат"); 
	ТаблицаТрудозатрат.Колонки.Добавить("Расценка");
	
	ТаблицаТрудозатрат.Колонки.Добавить("СдельнаяРасценка");
	ТаблицаТрудозатрат.Колонки.Добавить("УсловияТруда");
	ТаблицаТрудозатрат.Колонки.Добавить("Премия");
	ТаблицаТрудозатрат.Колонки.Добавить("РайонныйКоэффициент");
	ТаблицаТрудозатрат.Колонки.Добавить("СуммаТрудозатрат");
	ТаблицаТрудозатрат.Колонки.Добавить("СдельнаяРасценкаЗаЕдиницу");
	ТаблицаТрудозатрат.Колонки.Добавить("СуммаТрудозатратЗаЕдиницу");
	
	// Получаем номенклатуру и код из спецификации
	Изделие = Расш1_ОбщийМодуль1.НоменклатураПоСпецификации(РесурснаяСпецификация);
	КодИзделия = ?(ЗначениеЗаполнено(Изделие), Изделие.Код, "");
	
	// Получаем данные по спецификации
	Структура = ТаблицыМатериаловИТрудозатратДереваСпецификации(РесурснаяСпецификация, Количество, ДатаДокументаПлановаяКалькуляция);	
	
	ТаблицаМатериаловИУслугИсточник = Структура.ТаблицаМатериаловИУслуг; 	
	
	СписокНоменклатур = ТаблицаМатериаловИУслугИсточник.ВыгрузитьКолонку("Номенклатура");  
	
	ТаблицаЦен = ТаблицаЦенИДокументовОснований(СписокНоменклатур, ДатаДокументаПлановаяКалькуляция, ВидЦены);
	
	Для Каждого Строка Из ТаблицаМатериаловИУслугИсточник Цикл
		
		НоваяСтрока = ТаблицаМатериаловИУслуг.Добавить();		
		НоваяСтрока.ИзделиеМатериаловИУслуг = Изделие;
		НоваяСтрока.КодИзделияМатериаловИУслуг = КодИзделия;
		НоваяСтрока.Спецификация = РесурснаяСпецификация;	
		
		КоличествоУпаковокИзделия = Количество;
		НоваяСтрока.КоличествоУпаковокИзделия = КоличествоУпаковокИзделия;   		
		НоваяСтрока.НоменклатураМатериаловИУслуг = Строка.Номенклатура;		
		НоваяСтрока.КодМатериаловИУслуг = Строка.Код;		
		НоваяСтрока.УпаковкаМатериаловИУслуг = Строка.Упаковка; 
		
		КоличествоУпаковокМатериаловИУслуг = Строка.КоличествоУпаковок;			
		НоваяСтрока.КоличествоУпаковокМатериаловИУслуг = КоличествоУпаковокМатериаловИУслуг; 			
		НоваяСтрока.СтатьяКалькуляцииМатериаловИУслуг = Строка.СтатьяКалькуляции;
		
		НайденнаяСтрока = ТаблицаЦен.Найти(Строка.Номенклатура, "Номенклатура"); 			
		Если НайденнаяСтрока <> Неопределено Тогда
			Цена = НайденнаяСтрока.Цена;
			ДокументОснование = НайденнаяСтрока.ДокументОснование;
		Иначе
			Цена = 0;    
			ДокументОснование = Документы.ПриобретениеТоваровУслуг.ПустаяСсылка();
		КонецЕсли;			
		НоваяСтрока.Цена = Цена;   			
		НоваяСтрока.ДокументОснование = ДокументОснование;			
		НоваяСтрока.Стоимость = КоличествоУпаковокМатериаловИУслуг * Цена;			
		Если КоличествоУпаковокИзделия <> 0 Тогда
			НоваяСтрока.СтоимостьЗаЕдиницуИзделий = НоваяСтрока.Стоимость / КоличествоУпаковокИзделия;
		Иначе
			НоваяСтрока.СтоимостьЗаЕдиницуИзделий = 0;
		КонецЕсли;   
					
	КонецЦикла;                   
	
	
	ТаблицаТрудозатратИсточник = Структура.ТаблицаТрудозатрат;  
	
	СписокВидовРабот = ТаблицаТрудозатратИсточник.ВыгрузитьКолонку("ВидРаботТрудозатрат");    
    
	ТаблицаРасценкиРабот = ТаблицаРасценкиРабот(СписокВидовРабот, ДатаДокументаПлановаяКалькуляция);
	 		
	Для Каждого Строка Из ТаблицаТрудозатратИсточник Цикл  
		    			
		НоваяСтрока = ТаблицаТрудозатрат.Добавить(); 
		НоваяСтрока.ИзделиеТрудозатрат = Изделие;	
		НоваяСтрока.ВидРаботТрудозатрат = Строка.ВидРаботТрудозатрат;
		НоваяСтрока.КодТрудозатрат = Строка.КодТрудозатрат;
		Норма = Строка.КоличествоТрудозатрат;
		НоваяСтрока.КоличествоТрудозатрат = Норма; 
		НоваяСтрока.ПолуфабрикатТрудозатрат = Строка.ПолуфабрикатТрудозатрат;
		НоваяСтрока.КоличествоПолуфабрикатаТрудозатрат = Строка.КоличествоПолуфабрикатаТрудозатрат;
		НоваяСтрока.СтатьяКалькуляцииТрудозатрат = Строка.СтатьяКалькуляцииТрудозатрат;  
		ПодразделениеТрудозатрат = Строка.ПодразделениеТрудозатрат;
		НоваяСтрока.ПодразделениеТрудозатрат = ПодразделениеТрудозатрат;  
		
		НайденнаяСтрока = ТаблицаРасценкиРабот.Найти(Строка.ВидРаботТрудозатрат, "ВидРаботТрудозатрат"); 
		
		Если НайденнаяСтрока <> Неопределено Тогда
			Расценка = НайденнаяСтрока.Расценка;
		Иначе
			Расценка = 0;    
		КонецЕсли;			
		НоваяСтрока.Расценка = Расценка;
		
		СдельнаяРасценка = Расценка * Норма; 
		НоваяСтрока.СдельнаяРасценка = СдельнаяРасценка;
					
		ЗначениеПлановогоПроцентаЗаВредность = ПлановыйПроцентЗаВредность.Получить(ПодразделениеТрудозатрат);
		ЗначениеПлановогоПроцентаЗаВредность = ?(ЗначениеПлановогоПроцентаЗаВредность = Неопределено, 0 , ЗначениеПлановогоПроцентаЗаВредность);

		ЗначениеПлановогоПроцентаПремии = ПлановыйПроцентПремии.Получить(ПодразделениеТрудозатрат); 
		ЗначениеПлановогоПроцентаПремии = ?(ЗначениеПлановогоПроцентаПремии = Неопределено, 0 , ЗначениеПлановогоПроцентаПремии);			
		
		УсловияТруда = ЗначениеПлановогоПроцентаЗаВредность * СдельнаяРасценка / 100;
		НоваяСтрока.УсловияТруда = УсловияТруда; 
		
		Премия = ЗначениеПлановогоПроцентаПремии * СдельнаяРасценка / 100;
		НоваяСтрока.Премия = Премия; 
		
		НоваяСтрока.РайонныйКоэффициент = (СдельнаяРасценка + УсловияТруда + Премия) * 0.15;   
		
		СуммаТрудозатрат = (СдельнаяРасценка + УсловияТруда + Премия) * 1.15;
		НоваяСтрока.СуммаТрудозатрат = СуммаТрудозатрат; 
		
		Если Количество <> 0 Тогда
			НоваяСтрока.СдельнаяРасценкаЗаЕдиницу = СдельнаяРасценка / Количество;  
			НоваяСтрока.СуммаТрудозатратЗаЕдиницу = СуммаТрудозатрат / Количество;
		Иначе
			НоваяСтрока.СдельнаяРасценкаЗаЕдиницу = 0;
			НоваяСтрока.СуммаТрудозатратЗаЕдиницу = 0;
		КонецЕсли;
		
		
	КонецЦикла;		
	
	// Получение остатков на складах с родителем "Склады ОМТСиК"
	ЗаполнитьОстаткиПоСкладамОМТСиК(ТаблицаМатериаловИУслуг, ДатаДокументаПлановаяКалькуляция);
	 		
	Результат.Вставить("ТаблицаМатериаловИУслуг", ТаблицаМатериаловИУслуг);	
	Результат.Вставить("ТаблицаТрудозатрат", ТаблицаТрудозатрат);
	
	Возврат Результат;	
		
КонецФункции

// Заполняет колонку "Остаток" в таблице материалов остатками на складах с родителем "Склады ОМТСиК"
// Параметры:
//  ТаблицаМатериаловИУслуг - ТаблицаЗначений - таблица материалов для заполнения остатков
//  Дата - Дата - дата для получения остатков
Процедура ЗаполнитьОстаткиПоСкладамОМТСиК(ТаблицаМатериаловИУслуг, Дата)
	
	// Инициализация остатков нулями
	Для Каждого Строка Из ТаблицаМатериаловИУслуг Цикл
		Строка.Остаток = 0;
	КонецЦикла;
	
	// Поиск группы "Склады ОМТСиК"
	ГруппаСкладыОМТСиК = Справочники.Склады.НайтиПоНаименованию("Склады ОМТСиК");
	Если Не ЗначениеЗаполнено(ГруппаСкладыОМТСиК) Тогда
		Возврат;
	КонецЕсли;
	
	// Получение списка складов с родителем "Склады ОМТСиК"
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Склады.Ссылка КАК Склад
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	НЕ Склады.ЭтоГруппа
	|	И Склады.Родитель В ИЕРАРХИИ(&ГруппаСкладыОМТСиК)";
	
	Запрос.УстановитьПараметр("ГруппаСкладыОМТСиК", ГруппаСкладыОМТСиК);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	СписокСкладов = Новый Массив;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокСкладов.Добавить(Выборка.Склад);
	КонецЦикла;
	
	Если СписокСкладов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Получение уникального списка номенклатур из таблицы
	СписокНоменклатур = ТаблицаМатериаловИУслуг.ВыгрузитьКолонку("НоменклатураМатериаловИУслуг");
	Если СписокНоменклатур.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Получение остатков из регистра ТоварыНаСкладах
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Остатки.Номенклатура КАК Номенклатура,
	|	СУММА(ВЫБОР
	|		КОГДА Остатки.ВНаличииОстаток < 0
	|			ТОГДА 0
	|		ИНАЧЕ Остатки.ВНаличииОстаток
	|	КОНЕЦ) КАК Остаток
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(&Дата,
	|		Склад В (&СписокСкладов)
	|			И Номенклатура В (&СписокНоменклатур)) КАК Остатки
	|СГРУППИРОВАТЬ ПО
	|	Остатки.Номенклатура";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("СписокНоменклатур", СписокНоменклатур);
	Запрос.УстановитьПараметр("СписокСкладов", СписокСкладов);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	// Создание соответствия номенклатура -> остаток
	СоответствиеОстатков = Новый Соответствие;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СоответствиеОстатков.Вставить(Выборка.Номенклатура, Выборка.Остаток);
	КонецЦикла;
	
	// Заполнение остатков в таблице
	Для Каждого Строка Из ТаблицаМатериаловИУслуг Цикл
		Если СоответствиеОстатков.Получить(Строка.НоменклатураМатериаловИУслуг) <> Неопределено Тогда
			Строка.Остаток = СоответствиеОстатков.Получить(Строка.НоменклатураМатериаловИУслуг);
		Иначе
			Строка.Остаток = 0;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ВидЦеныДокументаПлановаяКалькуляция(ДокументПлановаяКалькуляция)
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ПлановаяКалькуляция2_2.ВидЦены КАК ВидЦены
	|ИЗ
	|	Документ.ПлановаяКалькуляция2_2 КАК ПлановаяКалькуляция2_2
	|ГДЕ
	|	ПлановаяКалькуляция2_2.Ссылка = &ДокументПлановаяКалькуляция"); 
	
	Запрос.УстановитьПараметр("ДокументПлановаяКалькуляция", ДокументПлановаяКалькуляция); 
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.ВидЦены;
	КонецЕсли;	 	
	Возврат Результат;
	
КонецФункции

// Формирует таблицу цен номенклатур с документами-основаниями.
// 
// Логика работы:
// 1. Получает цены из регистра сведений "ЦеныНоменклатуры" на указанную дату для указанного вида цены.
// 2. Вычисляет коэффициент упаковки для цен регистра (пересчет на базовую единицу измерения).
// 3. Ищет документы "ПриобретениеТоваровУслуг" с ценами, совпадающими с ценами регистра:
//    - Сравнивает цены в базовых единицах измерения (с учетом упаковки)
//    - Учитывает НДС (если цена документа включает НДС, вычитает его)
//    - Применяет допуск ±0.02 для сравнения цен
//    - Фильтрует документы по дате (только документы с датой <= указанной даты)
// 4. Из всех документов с совпадающей ценой выбирает последний:
//    - Сначала по максимальной дате
//    - При одинаковой дате - по максимальной ссылке документа
// 5. Если документ-основание не найден, использует комментарий из документа "УстановкаЦенНоменклатуры".
// 6. Возвращает цены, пересчитанные на базовую единицу измерения.
//
// Параметры:
//  СписокНоменклатур - Массив - список номенклатур для получения цен
//  Дата - Дата - дата актуальности цен
//  ВидЦены - СправочникСсылка.ВидыЦенНоменклатуры - вид цены для получения цен из регистра
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица с колонками:
//    - Номенклатура - СправочникСсылка.Номенклатура - номенклатура
//    - ЕдиницаИзмерения - СправочникСсылка.ЕдиницыИзмерения - единица измерения номенклатуры
//    - Цена - Число - цена в базовой единице измерения
//    - ДокументОснование - Строка - номер и дата документа-основания или комментарий из УстановкаЦенНоменклатуры
//
Функция ТаблицаЦенИДокументовОснований(СписокНоменклатур, Дата, ВидЦены)

    МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
    Запрос = Новый Запрос;
    Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
    Запрос.Текст = "ВЫБРАТЬ
                   |	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
                   |	ЦеныНоменклатуры.Цена КАК Цена,
                   |	ЦеныНоменклатуры.Упаковка КАК Упаковка,
                   |	ВЫБОР
                   |		КОГДА ЦеныНоменклатуры.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
                   |			ТОГДА 1
                   |		КОГДА ЦеныНоменклатуры.Упаковка = ЦеныНоменклатуры.Номенклатура.ЕдиницаИзмерения
                   |			ТОГДА 1
                   |		ИНАЧЕ &ТекстЗапросаКоэффициентУпРегистра
                   |	КОНЕЦ КАК КоэффициентУпаковки
                   |ПОМЕСТИТЬ ВТ_ЦеныНоменклатур
                   |ИЗ
                   |	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Дата, ВидЦены = &ВидЦены) КАК ЦеныНоменклатуры
                   |ГДЕ
                   |	ЦеныНоменклатуры.Номенклатура В(&СписокНоменклатур)
                   |;
                   |
                   |////////////////////////////////////////////////////////////////////////////////
                   |ВЫБРАТЬ
                   |	ТЧТовары.Номенклатура КАК Номенклатура,
                   |	ПриобретениеТоваровУслуг.Ссылка КАК СсылкаДокумент,
                   |	ПриобретениеТоваровУслуг.Дата КАК Дата
                   |ПОМЕСТИТЬ ВТ_ПоступленияПоЦене
                   |ИЗ
                   |	Документ.ПриобретениеТоваровУслуг.Товары КАК ТЧТовары
                   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
                   |		ПО ТЧТовары.Ссылка = ПриобретениеТоваровУслуг.Ссылка
                   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЦеныНоменклатур КАК ВТ_ЦеныНоменклатур
                   |		ПО ВТ_ЦеныНоменклатур.Номенклатура = ТЧТовары.Номенклатура
                   |ГДЕ
                   |	ТЧТовары.Номенклатура В(&СписокНоменклатур)
                   |	И ПриобретениеТоваровУслуг.Дата <= &Дата
                   |	И ВЫРАЗИТЬ((ВЫБОР
                   |		КОГДА ПриобретениеТоваровУслуг.ЦенаВключаетНДС
                   |			ТОГДА ТЧТовары.Цена / (1 + ЕСТЬNULL(ТЧТовары.СтавкаНДС.Ставка, 0) / 100)
                   |          	ИНАЧЕ ТЧТовары.Цена
                   |	КОНЕЦ) / ЕСТЬNULL((ВЫБОР
                   |		КОГДА ТЧТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
                   |			ТОГДА 1
                   |		КОГДА ТЧТовары.Упаковка = ТЧТовары.Номенклатура.ЕдиницаИзмерения
                   |			ТОГДА 1
                   |		ИНАЧЕ &ТекстЗапросаКоэффициентУпДокумента
                   |	КОНЕЦ), 1) КАК Число(15,2)) - ВЫРАЗИТЬ(ВТ_ЦеныНоменклатур.Цена / ЕСТЬNULL(ВТ_ЦеныНоменклатур.КоэффициентУпаковки, 1) КАК Число(15,2)) МЕЖДУ -0.02 И 0.02
                   |;
                   |
                   |////////////////////////////////////////////////////////////////////////////////
                   |ВЫБРАТЬ
                   |	ВТ_ПоступленияПоЦене.Номенклатура КАК Номенклатура,
                   |	МАКСИМУМ(ВТ_ПоступленияПоЦене.Дата) КАК МаксДата
                   |ПОМЕСТИТЬ ВТ_МаксДаты
                   |ИЗ
                   |	ВТ_ПоступленияПоЦене КАК ВТ_ПоступленияПоЦене
                   |СГРУППИРОВАТЬ ПО
                   |	ВТ_ПоступленияПоЦене.Номенклатура
                   |;
                   |
                   |////////////////////////////////////////////////////////////////////////////////
                   |ВЫБРАТЬ
                   |	ВТ_ПоступленияПоЦене.Номенклатура КАК Номенклатура,
                   |	МАКСИМУМ(ВТ_ПоступленияПоЦене.СсылкаДокумент) КАК СсылкаДокумент
                   |ПОМЕСТИТЬ ВТ_МаксДокумент
                   |ИЗ
                   |	ВТ_ПоступленияПоЦене КАК ВТ_ПоступленияПоЦене
                   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_МаксДаты КАК ВТ_МаксДаты
                   |		ПО ВТ_ПоступленияПоЦене.Номенклатура = ВТ_МаксДаты.Номенклатура
                   |			И ВТ_ПоступленияПоЦене.Дата = ВТ_МаксДаты.МаксДата
                   |СГРУППИРОВАТЬ ПО
                   |	ВТ_ПоступленияПоЦене.Номенклатура
                   |;
                   |
                   |////////////////////////////////////////////////////////////////////////////////
                   |ВЫБРАТЬ
                   |	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
                   |	ЦеныНоменклатуры.Упаковка КАК Упаковка,
                   |	ЦеныНоменклатуры.Цена КАК Цена,
                   |	ВТ_ЦеныНоменклатур.КоэффициентУпаковки КАК КоэффициентУпаковкиКБазовойЕдиницеИзмерения,
                   |	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
                   |	ДокументыМаксДата.Ссылка КАК СсылкаДокументОснование,
                   |	ДокументыМаксДата.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
                   |	ДокументыМаксДата.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
                   |	УстановкаЦен.Комментарий КАК УстановкаЦенКомментарий
                   |ИЗ
                   |	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Дата, ВидЦены = &ВидЦены) КАК ЦеныНоменклатуры
                   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЦеныНоменклатур КАК ВТ_ЦеныНоменклатур
                   |		ПО ЦеныНоменклатуры.Номенклатура = ВТ_ЦеныНоменклатур.Номенклатура
                   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
                   |		ПО ЦеныНоменклатуры.Номенклатура = СправочникНоменклатура.Ссылка
                   |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МаксДокумент КАК МаксДокумент
                   |		ПО ЦеныНоменклатуры.Номенклатура = МаксДокумент.Номенклатура
                   |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ДокументыМаксДата
                   |		ПО ДокументыМаксДата.Ссылка = МаксДокумент.СсылкаДокумент
                   |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УстановкаЦенНоменклатуры КАК УстановкаЦен
                   |		ПО ЦеныНоменклатуры.Регистратор = УстановкаЦен.Ссылка";
	//BSLLS:NestedFunctionInParameters-off
	// Подстановка для регистра (используется в ВТ_ЦеныНоменклатур и основном запросе)
	ТекстКоэффициентаРегистра = Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ЦеныНоменклатуры.Упаковка",
		"ЦеныНоменклатуры.Номенклатура");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки", ТекстКоэффициентаРегистра);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпРегистра", ТекстКоэффициентаРегистра);
	
	// Подстановка для документов
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпДокумента",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ТЧТовары.Упаковка",
		"ТЧТовары.Номенклатура"));
	//BSLLS:NestedFunctionInParameters-on
    Запрос.УстановитьПараметр("Дата", Дата); 
    Запрос.УстановитьПараметр("СписокНоменклатур", СписокНоменклатур);
    Запрос.УстановитьПараметр("ВидЦены", ВидЦены);

    Результат = Запрос.Выполнить();
	
    ТаблицаЦен = Новый ТаблицаЗначений;
    ТаблицаЦен.Колонки.Добавить("Номенклатура");
    ТаблицаЦен.Колонки.Добавить("ЕдиницаИзмерения");
    ТаблицаЦен.Колонки.Добавить("Цена");   
    ТаблицаЦен.Колонки.Добавить("ДокументОснование");

    Выборка = Результат.Выбрать(); 
    Пока Выборка.Следующий() Цикл
        НоваяСтрока = ТаблицаЦен.Добавить();        
        НоваяСтрока.Номенклатура = Выборка.Номенклатура;
        НоваяСтрока.ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
        НоваяСтрока.Цена = Выборка.Цена / Выборка.КоэффициентУпаковкиКБазовойЕдиницеИзмерения; 
        НоваяСтрока.ДокументОснование = ?(Не ЗначениеЗаполнено(Выборка.СсылкаДокументОснование), Выборка.УстановкаЦенКомментарий, Выборка.НомерВходящегоДокумента + " " + Формат(Выборка.ДатаВходящегоДокумента, "ДФ=dd.MM.yyyy"));       
    КонецЦикла;

    Возврат ТаблицаЦен;      
КонецФункции  

//не используется, вместо нее сразу ТаблицаЦенИДокументовОснований
// Параметры:
//  СписокНоменклатур - Массив - список номенклатур для получения цен
//  Дата - Дата - дата актуальности цен
//  ВидЦены - СправочникСсылка.ВидыЦенНоменклатуры - вид цены
// Возвращаемое значение:
//  ТаблицаЗначений - таблица цен номенклатур с колонками: Номенклатура, ЕдиницаИзмерения, Цена, ДокументОснование
Функция ТаблицаЦенНоменклатур(СписокНоменклатур, Дата, ВидЦены)
	
	Запрос = Новый Запрос;
	Запрос.Текст = ("ВЫБРАТЬ
	|	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЦеныНоменклатуры.Упаковка КАК Упаковка,
	|	ЦеныНоменклатуры.Цена КАК Цена, 
	|	ВЫБОР
	|		КОГДА ЦеныНоменклатуры.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ &ТекстЗапросаКоэффициентУпаковки
	|	КОНЕЦ КАК КоэффициентУпаковкиКБазовойЕдиницеИзмерения,
	|	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Дата, ВидЦены = &ВидЦены) КАК ЦеныНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ЦеныНоменклатуры.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ЦеныНоменклатуры.Номенклатура В(&СписокНоменклатур)"); 	

	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
	Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
	"ЦеныНоменклатуры.Упаковка",
	"ЦеныНоменклатуры.Номенклатура"));
	
	
	Запрос.УстановитьПараметр("Дата", Дата); 
	Запрос.УстановитьПараметр("СписокНоменклатур", СписокНоменклатур);
	Запрос.УстановитьПараметр("ВидЦены", ВидЦены);
	
	Результат = Запрос.Выполнить(); 
	ТаблицаЦен = Новый ТаблицаЗначений;
	ТаблицаЦен.Колонки.Добавить("Номенклатура");
	ТаблицаЦен.Колонки.Добавить("ЕдиницаИзмерения");
	ТаблицаЦен.Колонки.Добавить("Цена");   
	ТаблицаЦен.Колонки.Добавить("ДокументОснование");
	
	Выборка = Результат.Выбрать(); 	       
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТаблицаЦен.Добавить();        
		НоваяСтрока.Номенклатура = Выборка.Номенклатура;
		НоваяСтрока.ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
		НоваяСтрока.Цена = Выборка.Цена / Выборка.КоэффициентУпаковкиКБазовойЕдиницеИзмерения; 
		НоваяСтрока.ДокументОснование = Документы.ПриобретениеТоваровУслуг.ПустаяСсылка();		
	КонецЦикла; 	
	
	Возврат ТаблицаЦен;		
	
КонецФункции

Функция ТаблицаРасценкиРабот(СписокВидовРабот, Дата) Экспорт
	
	// Создаём объект запроса
	Запрос = Новый Запрос();

	// Создаём менеджер временных таблиц
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	// Создаём временную таблицу с последними датами расценок по каждому виду работ
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасценкиРаботСотрудниковСрезПоследних.ВидРабот КАК ВидРабот,
	|	МАКСИМУМ(РасценкиРаботСотрудниковСрезПоследних.Период) КАК ПоследнийПериод
	|ПОМЕСТИТЬ ВТ_ПоследниеПериоды
	|ИЗ
	|	РегистрСведений.РасценкиРаботСотрудников.СрезПоследних(&Дата) КАК РасценкиРаботСотрудниковСрезПоследних
	|ГДЕ
	|	РасценкиРаботСотрудниковСрезПоследних.ВидРабот В(&СписокВидовРабот)
	|СГРУППИРОВАТЬ ПО
	|	РасценкиРаботСотрудниковСрезПоследних.ВидРабот";  

	Запрос.УстановитьПараметр("Дата", Дата); 
	Запрос.УстановитьПараметр("СписокВидовРабот", СписокВидовРабот);

	// Выполняем запрос для создания временной таблицы
	Запрос.Выполнить();   

	// Теперь создаём новый запрос для выборки данных с учётом временной таблицы
	Запрос = Новый Запрос();  
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасценкиРаботСотрудниковСрезПоследних.ВидРабот КАК ВидРаботТрудозатрат,
	|	РасценкиРаботСотрудниковСрезПоследних.Расценка КАК Расценка,
	|	ВидыРаботСотрудников.Ссылка КАК Ссылка,
	|	ВидыРаботСотрудников.КвалификационныйРазряд КАК КвалификационныйРазряд,
	|	РасценкиРаботСотрудниковСрезПоследних.КвалификационныйРазряд КАК КвалификационныйРазряд1
	|ИЗ
	|	РегистрСведений.РасценкиРаботСотрудников.СрезПоследних(&Дата) КАК РасценкиРаботСотрудниковСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПоследниеПериоды КАК ПоследниеПериоды
	|		ПО РасценкиРаботСотрудниковСрезПоследних.ВидРабот = ПоследниеПериоды.ВидРабот
	|			И РасценкиРаботСотрудниковСрезПоследних.Период = ПоследниеПериоды.ПоследнийПериод
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыРаботСотрудников КАК ВидыРаботСотрудников
	|		ПО РасценкиРаботСотрудниковСрезПоследних.ВидРабот = ВидыРаботСотрудников.Ссылка
	|ГДЕ
	|	РасценкиРаботСотрудниковСрезПоследних.КвалификационныйРазряд = ВидыРаботСотрудников.КвалификационныйРазряд";  

	Запрос.УстановитьПараметр("Дата", Дата); 
	Запрос.УстановитьПараметр("СписокВидовРабот", СписокВидовРабот);

	// Выполняем основной запрос
	Результат = Запрос.Выполнить();
		
		ТаблицаРасценкиРабот = Новый ТаблицаЗначений;
		ТаблицаРасценкиРабот.Колонки.Добавить("ВидРаботТрудозатрат");
		ТаблицаРасценкиРабот.Колонки.Добавить("Расценка");
		
		Выборка = Результат.Выбрать(); 	       
		
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ТаблицаРасценкиРабот.Добавить();
			НоваяСтрока.ВидРаботТрудозатрат = Выборка.ВидРаботТрудозатрат;
			НоваяСтрока.Расценка = Выборка.Расценка;
		КонецЦикла; 	
		
		Возврат ТаблицаРасценкиРабот;		

КонецФункции

Функция ПлановыеПроцентыТрудозатрат(ИменаРеквизитов)
    
    Результат = Новый Структура;   
    
    Если ПустаяСтрока(ИменаРеквизитов) Тогда
        Возврат Результат;
    КонецЕсли;
    
    УсловияЗапроса = "";
    Для Каждого ИмяРеквизита Из ИменаРеквизитов Цикл
        Если УсловияЗапроса <> "" Тогда
            УсловияЗапроса = УсловияЗапроса + " ИЛИ ";
        КонецЕсли;
        УсловияЗапроса = УсловияЗапроса + "СтруктураПредприятияДополнительныеРеквизиты.Свойство.Имя = &" + ИмяРеквизита;
    КонецЦикла;

    // BSLLS:QueryParseError-off
    ТекстЗапроса = "ВЫБРАТЬ
    |   СтруктураПредприятияДополнительныеРеквизиты.Свойство.Имя КАК ИмяРеквизита,
    |   СтруктураПредприятияДополнительныеРеквизиты.Ссылка КАК Подразделение,
    |   ВЫРАЗИТЬ(СтруктураПредприятияДополнительныеРеквизиты.Значение КАК ЧИСЛО) КАК ЗначениеРеквизита
    |ИЗ
    |   Справочник.СтруктураПредприятия.ДополнительныеРеквизиты КАК СтруктураПредприятияДополнительныеРеквизиты
    |ГДЕ " + УсловияЗапроса;    

    Запрос = Новый Запрос(ТекстЗапроса);
    // BSLLS:QueryParseError-on    
    
    Для Каждого ИмяРеквизита Из ИменаРеквизитов Цикл
        Запрос.УстановитьПараметр(ИмяРеквизита, ИмяРеквизита);
    КонецЦикла;    
    
    РезультатЗапроса = Запрос.Выполнить();          
    Выборка = РезультатЗапроса.Выбрать(); 
    
    Пока Выборка.Следующий() Цикл 
        Если Не Результат.Свойство(Выборка.ИмяРеквизита) Тогда
            Результат.Вставить(Выборка.ИмяРеквизита, Новый Соответствие);
        КонецЕсли;
        
        КоллекцияПодразделений = Результат[Выборка.ИмяРеквизита];

        КоллекцияПодразделений.Вставить(Выборка.Подразделение, Выборка.ЗначениеРеквизита);
    КонецЦикла;
    
    Возврат Результат;        

КонецФункции

Функция ДатаДокументаПлановаяКалькуляция(ДокументПлановаяКалькуляция)
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ПлановаяКалькуляция2_2.Дата КАК Дата
	                      |ИЗ
	                      |	Документ.ПлановаяКалькуляция2_2 КАК ПлановаяКалькуляция2_2
	                      |ГДЕ
	                      |	ПлановаяКалькуляция2_2.Ссылка = &ДокументПлановаяКалькуляция"); 
	
	Запрос.УстановитьПараметр("ДокументПлановаяКалькуляция", ДокументПлановаяКалькуляция); 
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Дата;
	КонецЕсли;	 	
	Возврат Результат;
	
КонецФункции

Функция ТаблицаОбъектовКалькуляции(ДокументПлановаяКалькуляция)
	
	Результат = Новый ТаблицаЗначений;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ОбъектыКалькуляции.Номенклатура КАК Номенклатура,
	|   ОбъектыКалькуляции.Номенклатура.Код КАК Код,
	|	ОбъектыКалькуляции.Объект КАК Спецификация,
	|	ОбъектыКалькуляции.КоличествоУпаковок КАК КоличествоУпаковок	
	|ИЗ
	|	Документ.ПлановаяКалькуляция2_2.ОбъектыКалькуляции КАК ОбъектыКалькуляции
	|ГДЕ  
	|	ОбъектыКалькуляции.Ссылка = &ДокументПлановаяКалькуляция
	|	И ТИПЗНАЧЕНИЯ(ОбъектыКалькуляции.Объект) = ТИП(Справочник.РесурсныеСпецификации)"); 
	
	Запрос.УстановитьПараметр("ДокументПлановаяКалькуляция", ДокументПлановаяКалькуляция); 
	
	РезультатЗапроса = Запрос.Выполнить();      
	
	Если НЕ РезультатЗапроса.Пустой() Тогда  
		
		Результат.Колонки.Добавить("Номенклатура");
		Результат.Колонки.Добавить("Код");
		Результат.Колонки.Добавить("Спецификация");
		Результат.Колонки.Добавить("КоличествоУпаковок"); 
		
		Выборка = РезультатЗапроса.Выбрать();  
		
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = Результат.Добавить();
			НоваяСтрока.Номенклатура = Выборка.Номенклатура;
			НоваяСтрока.Код = Выборка.Код;
			НоваяСтрока.Спецификация = Выборка.Спецификация;
			НоваяСтрока.КоличествоУпаковок = Выборка.КоличествоУпаковок;
			
		КонецЦикла; 
		
	КонецЕсли;  
	
	Возврат Результат;	
		
КонецФункции

Функция ТаблицыМатериаловИТрудозатратДереваСпецификации(Спецификация, Количество, Дата = Неопределено) Экспорт
	//Структура = ТаблицыМатериаловИТрудозатратДереваСпецификации(Спецификация);
	//ТаблицаМатериалов = Структура.ТаблицаМатериаловИУслуг;
	//ТаблицаТрудозатрат = Структура.ТаблицаТрудозатрат;

	Результат = Новый Структура;  
	
	Ошибка = ""; 
	Номенклатура = Расш1_ОбщийМодуль1.НоменклатураПоСпецификации(Спецификация);  
	
	ПутьСпецификаций = Новый Массив;
	ТаблицаСпецификаций = ТаблицаПолуфабрикатовДереваСпецификацийРекурсивно(Ошибка, Спецификация, Номенклатура, Количество, "", Количество, ПутьСпецификаций, Дата);  
	
	Если ТаблицаСпецификаций.Количество() > 0 Тогда 
		
        ТаблицаСпецификацийСуммарно = Новый ТаблицаЗначений;
		ТаблицаСпецификацийСуммарно.Колонки.Добавить("Номенклатура");
		ТаблицаСпецификацийСуммарно.Колонки.Добавить("Количество");
		ТаблицаСпецификацийСуммарно.Колонки.Добавить("Спецификация");
		
		Для Каждого Строка Из ТаблицаСпецификаций Цикл
			// Формируем критерий поиска     
			Критерий = Новый Структура("Номенклатура, Спецификация", 
			Строка.Номенклатура, 
			Строка.Спецификация);
			// Ищем строки с такими же параметрами
			НайденныеСтроки = ТаблицаСпецификацийСуммарно.НайтиСтроки(Критерий);
			
			Если НайденныеСтроки.Количество() = 0 Тогда
				// Если строки не найдены, добавляем новую строку
				НоваяСтрока = ТаблицаСпецификацийСуммарно.Добавить();
				НоваяСтрока.Номенклатура = Строка.Номенклатура;
				НоваяСтрока.Количество = Строка.Количество;
				НоваяСтрока.Спецификация = Строка.Спецификация;
			Иначе
				// Если строка найдена, увеличиваем количество упаковок
				НайденнаяСтрока = НайденныеСтроки[0];
				НайденнаяСтрока.Количество = НайденнаяСтрока.Количество + Строка.Количество;
			КонецЕсли;
		КонецЦикла;		
		
		ТаблицаМатериаловИУслугСуммарно = ТаблицаМатериаловИУслугСуммарно(Ошибка, ТаблицаСпецификацийСуммарно);
		ТаблицаТрудозатратСуммарно = ТаблицаТрудозатратСуммарно(Ошибка, ТаблицаСпецификацийСуммарно);
		
	    Результат.Вставить("ТаблицаМатериаловИУслуг", ТаблицаМатериаловИУслугСуммарно);
	    Результат.Вставить("ТаблицаТрудозатрат", ТаблицаТрудозатратСуммарно);   
		
	КонецЕсли;   
	
	Возврат Результат;    
	
КонецФункции

Функция ТаблицаПолуфабрикатовДереваСпецификацийРекурсивно(Ошибка, Спецификация, Номенклатура, Количество, АлгоритмРасчетаКоличества, ОсновноеИзделиеКоличество, ПутьСпецификаций = Неопределено, Дата = Неопределено)   
	//первый раз в головной спецификации АлгоритмРасчетаКоличества = "", а ОсновноеИзделиеКоличество используется для дальнейших итераций.
	//ПутьСпецификаций - массив спецификаций, образующих путь от корня до текущей спецификации, используется для обнаружения циклических ссылок
	//Дата - дата документа для подбора приоритетной спецификации
	
	Если ПутьСпецификаций = Неопределено Тогда
		ПутьСпецификаций = Новый Массив;
	КонецЕсли;
	
	ТаблицаСпецификаций = Новый ТаблицаЗначений;               
	
	Если Спецификация <> Неопределено Тогда
		
		// Проверка на циклическую ссылку - если текущая спецификация уже есть в пути, значит мы идем по кругу
		Если ПутьСпецификаций.Найти(Спецификация) <> Неопределено Тогда
			ТекстПути = "";
			Для Каждого ЭлементПути Из ПутьСпецификаций Цикл
				Если ТекстПути <> "" Тогда
					ТекстПути = ТекстПути + " -> ";
				КонецЕсли;
				ТекстПути = ТекстПути + Строка(ЭлементПути);
			КонецЦикла;
			ТекстПути = ТекстПути + " -> " + Строка(Спецификация);
			Ошибка = "Обнаружена циклическая ссылка! Спецификация " + Спецификация + " уже встречалась в пути: " + ТекстПути;
			Сообщить(Ошибка);
			Возврат ТаблицаСпецификаций;
		КонецЕсли;
		
		// Добавляем текущую спецификацию в путь
		ПутьСпецификаций.Добавить(Спецификация);   
		
		ТаблицаСпецификаций.Колонки.Добавить("Номенклатура");
		ТаблицаСпецификаций.Колонки.Добавить("Количество");
		ТаблицаСпецификаций.Колонки.Добавить("Спецификация"); 	
		
		НоваяСтрока = ТаблицаСпецификаций.Добавить();  
		
		НоваяСтрока.Спецификация = Спецификация; 
		
		Если АлгоритмРасчетаКоличества <> "" Тогда 
			ОсновноеИзделиеКоличествоНовое = ВычислитьАлгоритмРасчетаКоличества( АлгоритмРасчетаКоличества, ОсновноеИзделиеКоличество );	
		Иначе		
			ОсновноеИзделиеКоличествоНовое = Количество;
		КонецЕсли;  
		
		НоваяСтрока.Количество = ОсновноеИзделиеКоличествоНовое; 
		
		Если Номенклатура <> Неопределено Тогда
			НоваяСтрока.Номенклатура = Номенклатура;
		Иначе
			НоваяСтрока.Номенклатура = Расш1_ОбщийМодуль1.НоменклатураПоСпецификации(Спецификация);	
		КонецЕсли; 
				

		СпособПолученияПроизводитсяНаЭтапе = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе;
		СтатьиКалькуляцииПокупных = Новый Массив;
		СтатьяКалькуляции = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул","ПокупныеМатериалы");
		Если ЗначениеЗаполнено(СтатьяКалькуляции) тогда
			СтатьиКалькуляцииПокупных.Добавить(СтатьяКалькуляции);
		КонецЕсли;
		СтатьяКалькуляции = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул","ПокупныеКомплектующие");
		Если ЗначениеЗаполнено(СтатьяКалькуляции) тогда
			СтатьиКалькуляцииПокупных.Добавить(СтатьяКалькуляции);
		КонецЕсли; 
		СтатьяКалькуляции = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул","ПолуфабрикатыПроизводимыеНаСтороне");
		Если ЗначениеЗаполнено(СтатьяКалькуляции) тогда
			СтатьиКалькуляцииПокупных.Добавить(СтатьяКалькуляции);
		КонецЕсли;
		
	    Запрос = Новый Запрос;
	    Запрос.Текст =
	    "ВЫБРАТЬ
	    |	МатериалыИУслуги.СпособПолученияМатериала КАК СпособПолученияМатериала,
	    |	МатериалыИУслуги.СтатьяКалькуляции КАК СтатьяКалькуляции, 
		|	МатериалыИУслуги.Номенклатура КАК Материал, 
		|	МатериалыИУслуги.КоличествоУпаковок КАК Количество,	
		|	МатериалыИУслуги.АлгоритмРасчетаКоличества КАК АлгоритмРасчетаКоличества,
	    |	МатериалыИУслуги.Ссылка.ОсновноеИзделиеКоличествоУпаковок КАК ОсновноеИзделиеКоличествоУпаковок,
	    |	ВЫРАЗИТЬ(МатериалыИУслуги.ИсточникПолученияПолуфабриката КАК Справочник.РесурсныеСпецификации).Ссылка КАК ИсточникПолученияПолуфабрикатаСсылка
	    |ИЗ
	    |	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК МатериалыИУслуги
	    |ГДЕ
	    |	МатериалыИУслуги.Ссылка = &Спецификация
		|	И МатериалыИУслуги.СтатьяКалькуляции НЕ В(&СтатьиКалькуляцииПокупных)
	    |	И МатериалыИУслуги.СпособПолученияМатериала <> &СпособПолученияПроизводитсяНаЭтапе";  
		//ИсточникПолученияПолуфабрикатаСсылка - это всегда спецификация, потому что полуфабрикаты, производимые на этапах исключены условием выше
	    Запрос.УстановитьПараметр("Спецификация", Спецификация);  
		Запрос.УстановитьПараметр("СтатьиКалькуляцииПокупных", СтатьиКалькуляцииПокупных);
	    Запрос.УстановитьПараметр("СпособПолученияПроизводитсяНаЭтапе", СпособПолученияПроизводитсяНаЭтапе);	
	    Выборка = Запрос.Выполнить().Выбрать();

		Пока Выборка.Следующий() Цикл 

				Если (Выборка.ИсточникПолученияПолуфабрикатаСсылка <> Null) Тогда 
					СпецификацияПолуфабриката = Выборка.ИсточникПолученияПолуфабрикатаСсылка;
				Иначе	
					СпецификацияПолуфабриката = Расш1_ОбщийМодуль1.ПриоритетнаяСпецификация(Выборка.Материал, Дата);
				КонецЕсли;

				Если (СпецификацияПолуфабриката <> Неопределено) Тогда
					Если Выборка.ОсновноеИзделиеКоличествоУпаковок <> 0 Тогда
						КоличествоПолуфабриката = Выборка.Количество / Выборка.ОсновноеИзделиеКоличествоУпаковок * ОсновноеИзделиеКоличествоНовое;
					Иначе
						КоличествоПолуфабриката = 0;
					КонецЕсли; 
					ТаблицаПодчиненных = ТаблицаПолуфабрикатовДереваСпецификацийРекурсивно(Ошибка, СпецификацияПолуфабриката, Выборка.Материал, КоличествоПолуфабриката, Выборка.АлгоритмРасчетаКоличества, ОсновноеИзделиеКоличество, ПутьСпецификаций, Дата); 
					Если ТаблицаПодчиненных.Количество() > 0 Тогда
						Для Каждого Строка Из ТаблицаПодчиненных Цикл ЗаполнитьЗначенияСвойств(ТаблицаСпецификаций.Добавить(), Строка) КонецЦикла;	
					КонецЕсли; 
				Иначе   
					Ошибка = "Ошибка! Не найдена спецификация на полуфабрикат: " + Выборка.Материал;
					Сообщить(Ошибка);
					Продолжить;	
				КонецЕсли;	

		КонецЦикла;
		
		// Удаляем текущую спецификацию из пути при выходе из функции
		Индекс = ПутьСпецификаций.Найти(Спецификация);
		Если Индекс <> Неопределено Тогда
			ПутьСпецификаций.Удалить(Индекс);
		КонецЕсли;
	КонецЕсли;    
	
	Возврат ТаблицаСпецификаций;
	
КонецФункции 

Функция ТаблицаМатериаловИУслугСуммарно(Ошибка, ТаблицаСпецификаций)     

    ТаблицаМатериаловИУслуг = ТаблицаМатериаловИУслуг(Ошибка, ТаблицаСпецификаций);

    // Создаем таблицу для суммирования
    ТаблицаМатериаловИУслугСуммарно = Новый ТаблицаЗначений;
    ТаблицаМатериаловИУслугСуммарно.Колонки.Добавить("Номенклатура"); 
    ТаблицаМатериаловИУслугСуммарно.Колонки.Добавить("Упаковка");
    ТаблицаМатериаловИУслугСуммарно.Колонки.Добавить("КоличествоУпаковок");
    ТаблицаМатериаловИУслугСуммарно.Колонки.Добавить("СтатьяКалькуляции");
	ТаблицаМатериаловИУслугСуммарно.Колонки.Добавить("Код");
	
    // Обрабатываем строки исходной таблицы
    Для Каждого Строка Из ТаблицаМатериаловИУслуг Цикл
        // Формируем критерий поиска     
        Критерий = Новый Структура("Номенклатура, Упаковка, СтатьяКалькуляции, Код", 
                                    Строка.Номенклатура, 
                                    Строка.Упаковка, 
                                    Строка.СтатьяКалькуляции,
		                            Строка.Код);
        // Ищем строки с такими же параметрами
        НайденныеСтроки = ТаблицаМатериаловИУслугСуммарно.НайтиСтроки(Критерий);

        Если НайденныеСтроки.Количество() = 0 Тогда
            // Если строки не найдены, добавляем новую строку
            НоваяСтрока = ТаблицаМатериаловИУслугСуммарно.Добавить();
            НоваяСтрока.Номенклатура = Строка.Номенклатура;
            НоваяСтрока.Упаковка = Строка.Упаковка;
            НоваяСтрока.КоличествоУпаковок = Строка.КоличествоУпаковок;
            НоваяСтрока.СтатьяКалькуляции = Строка.СтатьяКалькуляции;
			НоваяСтрока.Код = Строка.Код;
        Иначе
            // Если строка найдена, увеличиваем количество упаковок
            НайденнаяСтрока = НайденныеСтроки[0];
            НайденнаяСтрока.КоличествоУпаковок = НайденнаяСтрока.КоличествоУпаковок + Строка.КоличествоУпаковок;
        КонецЕсли;
    КонецЦикла;


    Возврат ТаблицаМатериаловИУслугСуммарно;
КонецФункции

Функция ТаблицаМатериаловИУслуг(Ошибка, ТаблицаСпецификаций)    
	
	ТаблицаМатериаловИУслуг = Новый ТаблицаЗначений;
	ТаблицаМатериаловИУслуг.Колонки.Добавить("Номенклатура");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("Упаковка");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("КоличествоУпаковок");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("СтатьяКалькуляции");
	ТаблицаМатериаловИУслуг.Колонки.Добавить("Код");

	МассивСпецификаций = ТаблицаСпецификаций.ВыгрузитьКолонку("Спецификация");	
	МассивКоличеств = ТаблицаСпецификаций.ВыгрузитьКолонку("Количество"); 
	

	СтатьиКалькуляцииПокупных = Новый Массив;
	СтатьяКалькуляции = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул","ПокупныеМатериалы");
	Если ЗначениеЗаполнено(СтатьяКалькуляции) тогда
		СтатьиКалькуляцииПокупных.Добавить(СтатьяКалькуляции);
	КонецЕсли;
	СтатьяКалькуляции = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул","ПокупныеКомплектующие");
	Если ЗначениеЗаполнено(СтатьяКалькуляции) тогда
		СтатьиКалькуляцииПокупных.Добавить(СтатьяКалькуляции);
	КонецЕсли;   
	СтатьяКалькуляции = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул","ПолуфабрикатыПроизводимыеНаСтороне");
	Если ЗначениеЗаполнено(СтатьяКалькуляции) тогда
		СтатьиКалькуляцииПокупных.Добавить(СтатьяКалькуляции);
	КонецЕсли;

	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МатериалыИУслуги.Ссылка КАК Ссылка,
	|	МатериалыИУслуги.СпособПолученияМатериала КАК СпособПолученияМатериала,
	|	МатериалыИУслуги.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	МатериалыИУслуги.Номенклатура КАК Номенклатура,
	|	МатериалыИУслуги.Упаковка КАК Упаковка,
	|	ВЫБОР
	|		КОГДА МатериалыИУслуги.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА 1
	|       КОГДА МатериалыИУслуги.Упаковка = МатериалыИУслуги.Номенклатура.ЕдиницаИзмерения
	|			ТОГДА 1	
	|		ИНАЧЕ &ТекстЗапросаКоэффициентУпаковки
	|	КОНЕЦ КАК КоэффициентУпаковкиКБазовойЕдиницеИзмерения,
	|	МатериалыИУслуги.КоличествоУпаковок КАК КоличествоУпаковок,
	|	МатериалыИУслуги.АлгоритмРасчетаКоличества КАК АлгоритмРасчетаКоличества,
	|	МатериалыИУслуги.Номенклатура.Код КАК Код,
	|	МатериалыИУслуги.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	МатериалыИУслуги.Ссылка.ОсновноеИзделиеКоличествоУпаковок КАК ОсновноеИзделиеКоличествоУпаковок
	|ИЗ
	|	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК МатериалыИУслуги
	|ГДЕ
	|	МатериалыИУслуги.Ссылка В(&МассивСпецификаций)
	|	И МатериалыИУслуги.СтатьяКалькуляции В(&СтатьиКалькуляцииПокупных)";
 
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
	Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
	"МатериалыИУслуги.Упаковка",
	"МатериалыИУслуги.Номенклатура"));

	Запрос.УстановитьПараметр("МассивСпецификаций", МассивСпецификаций);	
	Запрос.УстановитьПараметр("СтатьиКалькуляцииПокупных", СтатьиКалькуляцииПокупных);	
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 

		КоличествоУпаковок = Выборка.КоличествоУпаковок; 
		ОсновноеИзделиеКоличествоУпаковок = Выборка.ОсновноеИзделиеКоличествоУпаковок;
		КоэффициентУпаковкиКБазовойЕдиницеИзмерения = Выборка.КоэффициентУпаковкиКБазовойЕдиницеИзмерения;	   
		
		Индекс = МассивСпецификаций.Найти(Выборка.Ссылка);
		Если Индекс <> Неопределено Тогда
			КоличествоПолуфабриката = МассивКоличеств[Индекс];
		Иначе
			КоличествоПолуфабриката = 0; // Если не найдено, то умножаем на 0
		КонецЕсли;		
		
		НоваяСтрока = ТаблицаМатериаловИУслуг.Добавить();
		НоваяСтрока.Номенклатура = Выборка.Номенклатура;		
		НоваяСтрока.Упаковка = Выборка.ЕдиницаИзмерения; // Всегда единица хранения, т.к. количество уже в ЕИ хранения
		
		Если Выборка.АлгоритмРасчетаКоличества <> "" Тогда
			НоваяСтрока.КоличествоУпаковок = ВычислитьАлгоритмРасчетаКоличества(Выборка.АлгоритмРасчетаКоличества, КоличествоПолуфабриката);
		ИначеЕсли ОсновноеИзделиеКоличествоУпаковок <> 0 Тогда
			НоваяСтрока.КоличествоУпаковок = КоличествоУпаковок / КоэффициентУпаковкиКБазовойЕдиницеИзмерения * КоличествоПолуфабриката / ОсновноеИзделиеКоличествоУпаковок;
		Иначе
			НоваяСтрока.КоличествоУпаковок = 0;
		КонецЕсли;
		
		НоваяСтрока.СтатьяКалькуляции = Выборка.СтатьяКалькуляции;
		НоваяСтрока.Код = Выборка.Код;
		
	КонецЦикла;   
	
	//добавим в таблицу услуги переработчика
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	МатериалыИУслуги.Ссылка КАК Ссылка,
	|	МатериалыИУслуги.Этап КАК Этап,
	|	УслугиПереработчика.Номенклатура КАК Номенклатура,
	|	УслугиПереработчика.Количество КАК Количество,
	|	УслугиПереработчика.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	МатериалыИУслуги.Ссылка.ОсновноеИзделиеКоличествоУпаковок КАК ОсновноеИзделиеКоличествоУпаковок
	|ИЗ
	|	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК МатериалыИУслуги
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	Справочник.ЭтапыПроизводства.УслугиПереработчика КАК УслугиПереработчика
	|	ПО МатериалыИУслуги.Этап = УслугиПереработчика.Ссылка
	|ГДЕ
	|	МатериалыИУслуги.Ссылка В(&МассивСпецификаций)";
	
	
	Запрос.УстановитьПараметр("МассивСпецификаций", МассивСпецификаций);  	
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОсновноеИзделиеКоличествоУпаковок =  Выборка.ОсновноеИзделиеКоличествоУпаковок;
		Индекс = МассивСпецификаций.Найти(Выборка.Ссылка);
		Если Индекс <> Неопределено Тогда
			КоличествоПолуфабриката = МассивКоличеств[Индекс];
		Иначе
			КоличествоПолуфабриката = 1; // Если не найдено, то умножаем на 1
		КонецЕсли;
		НоваяСтрока = ТаблицаМатериаловИУслуг.Добавить();
		НоваяСтрока.Номенклатура = Выборка.Номенклатура;     
		НоваяСтрока.Упаковка = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоНаименованию("шт");
		Если ОсновноеИзделиеКоличествоУпаковок <> 0 Тогда
			НоваяСтрока.КоличествоУпаковок = Выборка.Количество * КоличествоПолуфабриката / ОсновноеИзделиеКоличествоУпаковок;
		Иначе
			НоваяСтрока.КоличествоУпаковок = 0;
		КонецЕсли;
		НоваяСтрока.СтатьяКалькуляции = Выборка.СтатьяКалькуляции;
		НоваяСтрока.Код = Неопределено;
	КонецЦикла;	

	Возврат ТаблицаМатериаловИУслуг;
	
КонецФункции

Функция ТаблицаТрудозатратСуммарно(Ошибка, ТаблицаСпецификаций) 

    ТаблицаТрудозатрат = ТаблицаТрудозатрат(Ошибка, ТаблицаСпецификаций);

    // Создаем результирующую таблицу
    ТаблицаТрудозатратСуммарно = Новый ТаблицаЗначений;
    ТаблицаТрудозатратСуммарно.Колонки.Добавить("ВидРаботТрудозатрат");   
	ТаблицаТрудозатратСуммарно.Колонки.Добавить("КодТрудозатрат");
    ТаблицаТрудозатратСуммарно.Колонки.Добавить("КоличествоТрудозатрат");
	ТаблицаТрудозатратСуммарно.Колонки.Добавить("ПолуфабрикатТрудозатрат");
	ТаблицаТрудозатратСуммарно.Колонки.Добавить("КоличествоПолуфабрикатаТрудозатрат");
    ТаблицаТрудозатратСуммарно.Колонки.Добавить("СтатьяКалькуляцииТрудозатрат");  
    ТаблицаТрудозатратСуммарно.Колонки.Добавить("ПодразделениеТрудозатрат");

    // Обрабатываем строки исходной таблицы
    Для Каждого Строка Из ТаблицаТрудозатрат Цикл
        // Формируем критерий поиска
        Критерий = Новый Структура("КодТрудозатрат, ПолуфабрикатТрудозатрат, ПодразделениеТрудозатрат", 
									Строка.КодТрудозатрат,
									Строка.ПолуфабрикатТрудозатрат,
                                    Строка.ПодразделениеТрудозатрат);
        
        // Ищем строки в результирующей таблице
        НайденныеСтроки = ТаблицаТрудозатратСуммарно.НайтиСтроки(Критерий);

		Если НайденныеСтроки.Количество() = 0 Тогда 
			
            // Если строки не найдены, добавляем новую строку
            НоваяСтрока = ТаблицаТрудозатратСуммарно.Добавить();
            НоваяСтрока.ВидРаботТрудозатрат = Строка.ВидРаботТрудозатрат;
			НоваяСтрока.КодТрудозатрат = Строка.КодТрудозатрат;
            НоваяСтрока.КоличествоТрудозатрат = Строка.КоличествоТрудозатрат; 
			НоваяСтрока.ПолуфабрикатТрудозатрат = Строка.ПолуфабрикатТрудозатрат;
			НоваяСтрока.КоличествоПолуфабрикатаТрудозатрат = Строка.КоличествоПолуфабрикатаТрудозатрат;
            НоваяСтрока.СтатьяКалькуляцииТрудозатрат = Строка.СтатьяКалькуляцииТрудозатрат;
            НоваяСтрока.ПодразделениеТрудозатрат = Строка.ПодразделениеТрудозатрат;
        Иначе
            // Если строка найдена, увеличиваем количество
            НайденнаяСтрока = НайденныеСтроки[0];
            НайденнаяСтрока.КоличествоТрудозатрат = НайденнаяСтрока.КоличествоТрудозатрат + Строка.КоличествоТрудозатрат;
        КонецЕсли;
    КонецЦикла;

    Возврат ТаблицаТрудозатратСуммарно;
КонецФункции
	
Функция ТаблицаТрудозатрат(Ошибка, ТаблицаСпецификаций)  
	
	ТаблицаТрудозатрат = Новый ТаблицаЗначений;	   
		
		ТаблицаТрудозатрат.Колонки.Добавить("ВидРаботТрудозатрат");
		ТаблицаТрудозатрат.Колонки.Добавить("КодТрудозатрат");
		ТаблицаТрудозатрат.Колонки.Добавить("КоличествоТрудозатрат");
		ТаблицаТрудозатрат.Колонки.Добавить("ПолуфабрикатТрудозатрат");
		ТаблицаТрудозатрат.Колонки.Добавить("КоличествоПолуфабрикатаТрудозатрат");
		ТаблицаТрудозатрат.Колонки.Добавить("СтатьяКалькуляцииТрудозатрат");  
		ТаблицаТрудозатрат.Колонки.Добавить("ПодразделениеТрудозатрат");
				
		МассивСпецификаций = ТаблицаСпецификаций.ВыгрузитьКолонку("Спецификация"); 
		МассивКоличеств = ТаблицаСпецификаций.ВыгрузитьКолонку("Количество");
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ 
		|	Трудозатраты.Ссылка КАК Ссылка,
		|	Трудозатраты.Этап КАК Этап,
		|	Трудозатраты.ВидРабот КАК ВидРаботТрудозатрат,
		|	Трудозатраты.ВидРабот.Код КАК КодТрудозатрат,		
		|	Трудозатраты.Количество КАК КоличествоТрудозатрат,
		|	Трудозатраты.СтатьяКалькуляции КАК СтатьяКалькуляцииТрудозатрат,
		|	Трудозатраты.Ссылка.ОсновноеИзделиеНоменклатура КАК ПолуфабрикатТрудозатрат,
		|	Трудозатраты.Ссылка.ОсновноеИзделиеКоличествоУпаковок КАК ОсновноеИзделиеКоличествоУпаковок,
		|	ЭтапыПроизводства.Подразделение КАК ПодразделениеТрудозатрат
		|ИЗ
		|	Справочник.РесурсныеСпецификации.Трудозатраты КАК Трудозатраты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
		|		ПО Трудозатраты.Этап = ЭтапыПроизводства.Ссылка
		|ГДЕ
		|	Трудозатраты.Ссылка В(&МассивСпецификаций)";   
		
		Запрос.УстановитьПараметр("МассивСпецификаций", МассивСпецификаций);		
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл 
			Индекс = МассивСпецификаций.Найти(Выборка.Ссылка);
			Если Индекс <> Неопределено Тогда
				КоличествоПолуфабриката = МассивКоличеств[Индекс];
			Иначе
				КоличествоПолуфабриката = 0; // Если не найдено, то умножаем на 1
			КонецЕсли;			
			НоваяСтрока = ТаблицаТрудозатрат.Добавить();
			НоваяСтрока.ВидРаботТрудозатрат = Выборка.ВидРаботТрудозатрат;
			НоваяСтрока.КодТрудозатрат = Выборка.КодТрудозатрат;
			Если Выборка.ОсновноеИзделиеКоличествоУпаковок <> 0 Тогда
				НоваяСтрока.КоличествоТрудозатрат = Выборка.КоличествоТрудозатрат * КоличествоПолуфабриката / Выборка.ОсновноеИзделиеКоличествоУпаковок;
			Иначе
				НоваяСтрока.КоличествоТрудозатрат = 0;
			КонецЕсли;
			НоваяСтрока.ПолуфабрикатТрудозатрат = Выборка.ПолуфабрикатТрудозатрат;
			НоваяСтрока.КоличествоПолуфабрикатаТрудозатрат = КоличествоПолуфабриката;
			НоваяСтрока.СтатьяКалькуляцииТрудозатрат = Выборка.СтатьяКалькуляцииТрудозатрат;
			НоваяСтрока.ПодразделениеТрудозатрат = Выборка.ПодразделениеТрудозатрат;
		КонецЦикла;			 
	
	Возврат ТаблицаТрудозатрат;
	
КонецФункции

// Получает трудозатраты по спецификациям с учетом конкретного этапа
// Параметры:
//  Ошибка - Строка - строка для записи ошибок
//  ТаблицаСпецификаций - ТаблицаЗначений - таблица спецификаций с количествами
//  ЭтапСпецификации - СправочникСсылка.ЭтапыПроизводства - этап спецификации для фильтрации
// Возвращаемое значение:
//  ТаблицаЗначений - таблица трудозатрат
Функция ТаблицаТрудозатратПоТаблицеСпецификаций(Ошибка, ТаблицаСпецификаций)
	
	ТаблицаТрудозатрат = Новый ТаблицаЗначений;
	
	ТаблицаТрудозатрат.Колонки.Добавить("ВидРаботТрудозатрат");
	ТаблицаТрудозатрат.Колонки.Добавить("КодТрудозатрат");
	ТаблицаТрудозатрат.Колонки.Добавить("КоличествоТрудозатрат");
	ТаблицаТрудозатрат.Колонки.Добавить("ПолуфабрикатТрудозатрат");
	ТаблицаТрудозатрат.Колонки.Добавить("КоличествоПолуфабрикатаТрудозатрат");
	ТаблицаТрудозатрат.Колонки.Добавить("СтатьяКалькуляцииТрудозатрат");
	ТаблицаТрудозатрат.Колонки.Добавить("ПодразделениеТрудозатрат");
	
	МассивСпецификаций = ТаблицаСпецификаций.ВыгрузитьКолонку("Спецификация");
	МассивКоличеств = ТаблицаСпецификаций.ВыгрузитьКолонку("Количество");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ 
	|	Трудозатраты.Ссылка КАК Ссылка,
	|	Трудозатраты.Этап КАК Этап,
	|	Трудозатраты.ВидРабот КАК ВидРаботТрудозатрат,
	|	Трудозатраты.ВидРабот.Код КАК КодТрудозатрат,
	|	Трудозатраты.Количество КАК КоличествоТрудозатрат,
	|	Трудозатраты.СтатьяКалькуляции КАК СтатьяКалькуляцииТрудозатрат,
	|	Трудозатраты.Ссылка.ОсновноеИзделиеНоменклатура КАК ПолуфабрикатТрудозатрат,
	|	Трудозатраты.Ссылка.ОсновноеИзделиеКоличествоУпаковок КАК ОсновноеИзделиеКоличествоУпаковок,
	|	ЭтапыПроизводства.Подразделение КАК ПодразделениеТрудозатрат
	|ИЗ
	|	Справочник.РесурсныеСпецификации.Трудозатраты КАК Трудозатраты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|		ПО Трудозатраты.Этап = ЭтапыПроизводства.Ссылка
	|ГДЕ
	|	Трудозатраты.Ссылка В(&МассивСпецификаций)";
	
	Запрос.УстановитьПараметр("МассивСпецификаций", МассивСпецификаций);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Индекс = МассивСпецификаций.Найти(Выборка.Ссылка);
		Если Индекс <> Неопределено Тогда
			КоличествоПолуфабриката = МассивКоличеств[Индекс];
		Иначе
			КоличествоПолуфабриката = 0;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаТрудозатрат.Добавить();
		НоваяСтрока.ВидРаботТрудозатрат = Выборка.ВидРаботТрудозатрат;
		НоваяСтрока.КодТрудозатрат = Выборка.КодТрудозатрат;
		Если Выборка.ОсновноеИзделиеКоличествоУпаковок <> 0 Тогда
			НоваяСтрока.КоличествоТрудозатрат = Выборка.КоличествоТрудозатрат * КоличествоПолуфабриката / Выборка.ОсновноеИзделиеКоличествоУпаковок;
		Иначе
			НоваяСтрока.КоличествоТрудозатрат = 0;
		КонецЕсли;
		НоваяСтрока.ПолуфабрикатТрудозатрат = Выборка.ПолуфабрикатТрудозатрат;
		НоваяСтрока.КоличествоПолуфабрикатаТрудозатрат = КоличествоПолуфабриката;
		НоваяСтрока.СтатьяКалькуляцииТрудозатрат = Выборка.СтатьяКалькуляцииТрудозатрат;
		НоваяСтрока.ПодразделениеТрудозатрат = Выборка.ПодразделениеТрудозатрат;
	КонецЦикла;
	
	Возврат ТаблицаТрудозатрат;
	
КонецФункции

// Рассчитывает трудозатраты и сдельную расценку по спецификации с учетом рекурсивного проваливания в материалы
// Параметры:
//  Спецификация - СправочникСсылка.РесурсныеСпецификации - спецификация для расчета
//  Количество - Число - количество выпущенной продукции
//  Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение-исполнитель для фильтрации
//  Дата - Дата - дата для получения расценок
// Возвращаемое значение:
//  Структура - структура с полями:
//    КоличествоТрудозатрат - Число - суммарное количество трудозатрат
//    СдельнаяРасценка - Число - суммарная сдельная расценка
Функция ТрудозатратыПоСпецификацииИПодразделению(Спецификация, Количество, Подразделение = Неопределено, Дата = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("КоличествоТрудозатрат", 0);
	Результат.Вставить("СдельнаяРасценка", 0);
	
	Если Не ЗначениеЗаполнено(Спецификация) Или Количество = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Дата = Неопределено Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Ошибка = "";
	ПутьСпецификаций = Новый Массив;
	ТаблицаСпецификаций = ТаблицаПолуфабрикатовДереваСпецификацийРекурсивноСПодразделением(
		Ошибка, 
		Спецификация, 
		Неопределено, 
		Количество, 
		"", 
		Количество, 
		ПутьСпецификаций, 
		Дата,
		Подразделение);
	
	Если ТаблицаСпецификаций.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Получение трудозатрат по всем спецификациям
	ТаблицаТрудозатрат = ТаблицаТрудозатрат(Ошибка, ТаблицаСпецификаций);  
	
	Если ТаблицаТрудозатрат.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Фильтрация по подразделению
	Если ЗначениеЗаполнено(Подразделение) Тогда
		Для Индекс = ТаблицаТрудозатрат.Количество() - 1 По 0 Цикл
			Строка = ТаблицаТрудозатрат[Индекс];
			Если Строка.ПодразделениеТрудозатрат <> Подразделение Тогда
				ТаблицаТрудозатрат.Удалить(Индекс);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Получение расценок
	СписокВидовРабот = ТаблицаТрудозатрат.ВыгрузитьКолонку("ВидРаботТрудозатрат");
	ТаблицаРасценкиРабот = ТаблицаРасценкиРабот(СписокВидовРабот, Дата);
	
	// Расчет суммарных трудозатрат и расценок
	Для Каждого Строка Из ТаблицаТрудозатрат Цикл
		
		Норма = Строка.КоличествоТрудозатрат;
		Результат.КоличествоТрудозатрат = Результат.КоличествоТрудозатрат + Норма;
		
		НайденнаяСтрока = ТаблицаРасценкиРабот.Найти(Строка.ВидРаботТрудозатрат, "ВидРаботТрудозатрат");
		Если НайденнаяСтрока <> Неопределено Тогда
			Расценка = НайденнаяСтрока.Расценка;
			СдельнаяРасценка = Расценка * Норма;
			Результат.СдельнаяРасценка = Результат.СдельнаяРасценка + СдельнаяРасценка;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Рассчитывает трудозатраты и сдельную расценку по спецификации с учетом конкретного этапа и подразделения
// Параметры:
//  Спецификация - СправочникСсылка.РесурсныеСпецификации - спецификация для расчета
//  ЭтапСпецификации - СправочникСсылка.ЭтапыПроизводства - этап спецификации, на который оформлен документ
//  Количество - Число - количество выпущенной продукции
//  Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение-исполнитель для фильтрации
//  Дата - Дата - дата для получения расценок
// Возвращаемое значение:
//  Структура - структура с полями:
//    КоличествоТрудозатрат - Число - суммарное количество трудозатрат
//    СдельнаяРасценка - Число - суммарная сдельная расценка
Функция ТрудозатратыПоСпецификацииЭтапуИПодразделению(Спецификация, ЭтапСпецификации, Количество, Подразделение = Неопределено, Дата = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("КоличествоТрудозатрат", 0);
	Результат.Вставить("СдельнаяРасценка", 0);
	
	Если Не ЗначениеЗаполнено(Спецификация) Или Количество = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Дата = Неопределено Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Ошибка = "";
	ПутьСпецификаций = Новый Массив;
	ТаблицаСпецификаций = ТаблицаПолуфабрикатовДереваСпецификацийРекурсивноСЭтапомИПодразделением(
		Ошибка, 
		Спецификация, 
		Неопределено, 
		Количество, 
		"", 
		Количество, 
		ПутьСпецификаций, 
		Дата,
		Подразделение,
		ЭтапСпецификации);
	
	Если ТаблицаСпецификаций.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Получение трудозатрат по всем спецификациям с учетом этапа
	ТаблицаТрудозатрат = ТаблицаТрудозатратПоТаблицеСпецификаций(Ошибка, ТаблицаСпецификаций);
	
	Если ТаблицаТрудозатрат.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Фильтрация по подразделению
	Если ЗначениеЗаполнено(Подразделение) Тогда
		Для Индекс = ТаблицаТрудозатрат.Количество() - 1 По 0 Цикл
			Строка = ТаблицаТрудозатрат[Индекс];
			Если Строка.ПодразделениеТрудозатрат <> Подразделение Тогда
				ТаблицаТрудозатрат.Удалить(Индекс);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Получение расценок
	СписокВидовРабот = ТаблицаТрудозатрат.ВыгрузитьКолонку("ВидРаботТрудозатрат");
	ТаблицаРасценкиРабот = ТаблицаРасценкиРабот(СписокВидовРабот, Дата);
	
	// Расчет суммарных трудозатрат и расценок
	Для Каждого Строка Из ТаблицаТрудозатрат Цикл
		
		Норма = Строка.КоличествоТрудозатрат;
		Результат.КоличествоТрудозатрат = Результат.КоличествоТрудозатрат + Норма;
		
		НайденнаяСтрока = ТаблицаРасценкиРабот.Найти(Строка.ВидРаботТрудозатрат, "ВидРаботТрудозатрат");
		Если НайденнаяСтрока <> Неопределено Тогда
			Расценка = НайденнаяСтрока.Расценка;
			СдельнаяРасценка = Расценка * Норма;
			Результат.СдельнаяРасценка = Результат.СдельнаяРасценка + СдельнаяРасценка;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Рекурсивно обходит дерево спецификаций с учетом фильтрации по подразделению последнего этапа
// Параметры:
//  Ошибка - Строка - строка для записи ошибок
//  Спецификация - СправочникСсылка.РесурсныеСпецификации - текущая спецификация
//  Номенклатура - СправочникСсылка.Номенклатура - номенклатура спецификации
//  Количество - Число - количество
//  АлгоритмРасчетаКоличества - Строка - алгоритм расчета количества
//  ОсновноеИзделиеКоличество - Число - количество основного изделия
//  ПутьСпецификаций - Массив - массив спецификаций для проверки циклов
//  Дата - Дата - дата для подбора спецификаций
//  Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение для фильтрации
// Возвращаемое значение:
//  ТаблицаЗначений - таблица спецификаций с количествами
Функция ТаблицаПолуфабрикатовДереваСпецификацийРекурсивноСПодразделением(Ошибка, Спецификация, Номенклатура, Количество, АлгоритмРасчетаКоличества, ОсновноеИзделиеКоличество, ПутьСпецификаций = Неопределено, Дата = Неопределено, Подразделение = Неопределено)
	
	Если ПутьСпецификаций = Неопределено Тогда
		ПутьСпецификаций = Новый Массив;
	КонецЕсли;
	
	ТаблицаСпецификаций = Новый ТаблицаЗначений;
	
	Если Спецификация <> Неопределено Тогда
		
		// Проверка на циклическую ссылку
		Если ПутьСпецификаций.Найти(Спецификация) <> Неопределено Тогда
			ТекстПути = "";
			Для Каждого ЭлементПути Из ПутьСпецификаций Цикл
				Если ТекстПути <> "" Тогда
					ТекстПути = ТекстПути + " -> ";
				КонецЕсли;
				ТекстПути = ТекстПути + Строка(ЭлементПути);
			КонецЦикла;
			ТекстПути = ТекстПути + " -> " + Строка(Спецификация);
			Ошибка = "Обнаружена циклическая ссылка! Спецификация " + Спецификация + " уже встречалась в пути: " + ТекстПути;
			Сообщить(Ошибка);
			Возврат ТаблицаСпецификаций;
		КонецЕсли;
		
		ПутьСпецификаций.Добавить(Спецификация);
		
		ТаблицаСпецификаций.Колонки.Добавить("Номенклатура");
		ТаблицаСпецификаций.Колонки.Добавить("Количество");
		ТаблицаСпецификаций.Колонки.Добавить("Спецификация");
		
		НоваяСтрока = ТаблицаСпецификаций.Добавить();
		НоваяСтрока.Спецификация = Спецификация;
		
		Если АлгоритмРасчетаКоличества <> "" Тогда
			ОсновноеИзделиеКоличествоНовое = ВычислитьАлгоритмРасчетаКоличества(АлгоритмРасчетаКоличества, ОсновноеИзделиеКоличество);
		Иначе
			ОсновноеИзделиеКоличествоНовое = Количество;
		КонецЕсли;
		
		НоваяСтрока.Количество = ОсновноеИзделиеКоличествоНовое;
		
		Если Номенклатура <> Неопределено Тогда
			НоваяСтрока.Номенклатура = Номенклатура;
		Иначе
			НоваяСтрока.Номенклатура = Расш1_ОбщийМодуль1.НоменклатураПоСпецификации(Спецификация);
		КонецЕсли;
		
		// Получение материалов спецификации
		СпособПолученияПроизводитсяНаЭтапе = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе;
		СпособПолученияПроизвестиПоСпецификации = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизвестиПоСпецификации;
		
		СтатьиКалькуляцииПокупных = Новый Массив;
		СтатьяКалькуляции = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул", "ПокупныеМатериалы");
		Если ЗначениеЗаполнено(СтатьяКалькуляции) Тогда
			СтатьиКалькуляцииПокупных.Добавить(СтатьяКалькуляции);
		КонецЕсли;
		СтатьяКалькуляции = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул", "ПокупныеКомплектующие");
		Если ЗначениеЗаполнено(СтатьяКалькуляции) Тогда
			СтатьиКалькуляцииПокупных.Добавить(СтатьяКалькуляции);
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	МатериалыИУслуги.СпособПолученияМатериала КАК СпособПолученияМатериала,
		|	МатериалыИУслуги.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	МатериалыИУслуги.Номенклатура КАК Материал,
		|	МатериалыИУслуги.КоличествоУпаковок КАК Количество,
		|	МатериалыИУслуги.АлгоритмРасчетаКоличества КАК АлгоритмРасчетаКоличества,
		|	МатериалыИУслуги.Ссылка.ОсновноеИзделиеКоличествоУпаковок КАК ОсновноеИзделиеКоличествоУпаковок,
		|	ВЫРАЗИТЬ(МатериалыИУслуги.ИсточникПолученияПолуфабриката КАК Справочник.РесурсныеСпецификации).Ссылка КАК ИсточникПолученияПолуфабрикатаСсылка
		|ИЗ
		|	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК МатериалыИУслуги
		|ГДЕ
		|	МатериалыИУслуги.Ссылка = &Спецификация
		|	И МатериалыИУслуги.СпособПолученияМатериала <> &СпособПолученияПроизводитсяНаЭтапе";
		
		Запрос.УстановитьПараметр("Спецификация", Спецификация);
		Запрос.УстановитьПараметр("СпособПолученияПроизводитсяНаЭтапе", СпособПолученияПроизводитсяНаЭтапе);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если СтатьиКалькуляцииПокупных.Найти(Выборка.СтатьяКалькуляции) = Неопределено Тогда
				
				// Проверяем, нужно ли проваливаться в спецификацию материала
				Если Выборка.СпособПолученияМатериала = СпособПолученияПроизвестиПоСпецификации Тогда
					
					Если Выборка.ИсточникПолученияПолуфабрикатаСсылка <> Null Тогда
						СпецификацияПолуфабриката = Выборка.ИсточникПолученияПолуфабрикатаСсылка;
					Иначе
						СпецификацияПолуфабриката = Расш1_ОбщийМодуль1.ПриоритетнаяСпецификация(Выборка.Материал, Дата);
					КонецЕсли;
					
					Если ЗначениеЗаполнено(СпецификацияПолуфабриката) Тогда
						
						// Проверяем подразделение последнего этапа спецификации
						ПодразделениеПоследнегоЭтапа = ПодразделениеПоследнегоЭтапаСпецификации(СпецификацияПолуфабриката);
						
						// Проваливаемся только если подразделение совпадает или не задано
						Если Не ЗначениеЗаполнено(Подразделение) Или ПодразделениеПоследнегоЭтапа = Подразделение Тогда
							
							Если Выборка.ОсновноеИзделиеКоличествоУпаковок <> 0 Тогда
								КоличествоПолуфабриката = Выборка.Количество / Выборка.ОсновноеИзделиеКоличествоУпаковок * ОсновноеИзделиеКоличествоНовое;
							Иначе
								КоличествоПолуфабриката = 0;
							КонецЕсли;
							
							ТаблицаПодчиненных = ТаблицаПолуфабрикатовДереваСпецификацийРекурсивноСПодразделением(
								Ошибка, 
								СпецификацияПолуфабриката, 
								Выборка.Материал, 
								КоличествоПолуфабриката, 
								Выборка.АлгоритмРасчетаКоличества, 
								ОсновноеИзделиеКоличество, 
								ПутьСпецификаций, 
								Дата,
								Подразделение);
							
							Если ТаблицаПодчиненных.Количество() > 0 Тогда
								Для Каждого Строка Из ТаблицаПодчиненных Цикл
									ЗаполнитьЗначенияСвойств(ТаблицаСпецификаций.Добавить(), Строка);
								КонецЦикла;
							КонецЕсли;
							
						КонецЕсли;
						
					Иначе
						Ошибка = "Ошибка! Не найдена спецификация на полуфабрикат: " + Выборка.Материал;
						Сообщить(Ошибка);
						Продолжить;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Индекс = ПутьСпецификаций.Найти(Спецификация);
		Если Индекс <> Неопределено Тогда
			ПутьСпецификаций.Удалить(Индекс);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТаблицаСпецификаций;
	
КонецФункции

// Получает подразделение последнего этапа спецификации (где НомерСледующегоЭтапа = 0)
// Параметры:
//  Спецификация - СправочникСсылка.РесурсныеСпецификации - спецификация
// Возвращаемое значение:
//  СправочникСсылка.СтруктураПредприятия - подразделение последнего этапа или Неопределено
Функция ПодразделениеПоследнегоЭтапаСпецификации(Спецификация) Экспорт
	
	Если Не ЗначениеЗаполнено(Спецификация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЭтапыПроизводства.Подразделение КАК Подразделение
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|ГДЕ
	|	ЭтапыПроизводства.Владелец = &Спецификация
	|	И НЕ ЭтапыПроизводства.ПометкаУдаления
	|	И ЭтапыПроизводства.НомерСледующегоЭтапа = 0
	|УПОРЯДОЧИТЬ ПО
	|	ЭтапыПроизводства.НомерЭтапа";
	
	Запрос.УстановитьПараметр("Спецификация", Спецификация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Подразделение;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Рекурсивно обходит дерево спецификаций с учетом фильтрации по этапу и подразделению последнего этапа
// Параметры:
//  Ошибка - Строка - строка для записи ошибок
//  Спецификация - СправочникСсылка.РесурсныеСпецификации - текущая спецификация
//  Номенклатура - СправочникСсылка.Номенклатура - номенклатура спецификации
//  Количество - Число - количество
//  АлгоритмРасчетаКоличества - Строка - алгоритм расчета количества
//  ОсновноеИзделиеКоличество - Число - количество основного изделия
//  ПутьСпецификаций - Массив - массив спецификаций для проверки циклов
//  Дата - Дата - дата для подбора спецификаций
//  Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение для фильтрации
//  ЭтапСпецификации - СправочникСсылка.ЭтапыПроизводства - этап спецификации для фильтрации материалов (материалы выбираются строго для этого этапа)
// Возвращаемое значение:
//  ТаблицаЗначений - таблица спецификаций с количествами
Функция ТаблицаПолуфабрикатовДереваСпецификацийРекурсивноСЭтапомИПодразделением(Ошибка, Спецификация, Номенклатура, Количество, АлгоритмРасчетаКоличества, ОсновноеИзделиеКоличество, ПутьСпецификаций = Неопределено, Дата = Неопределено, Подразделение = Неопределено, ЭтапСпецификации = Неопределено)
	
	Если ПутьСпецификаций = Неопределено Тогда
		ПутьСпецификаций = Новый Массив;
	КонецЕсли;
	
	ТаблицаСпецификаций = Новый ТаблицаЗначений;
	
	Если Спецификация <> Неопределено Тогда
		
		// Проверка на циклическую ссылку
		Если ПутьСпецификаций.Найти(Спецификация) <> Неопределено Тогда
			ТекстПути = "";
			Для Каждого ЭлементПути Из ПутьСпецификаций Цикл
				Если ТекстПути <> "" Тогда
					ТекстПути = ТекстПути + " -> ";
				КонецЕсли;
				ТекстПути = ТекстПути + Строка(ЭлементПути);
			КонецЦикла;
			ТекстПути = ТекстПути + " -> " + Строка(Спецификация);
			Ошибка = "Обнаружена циклическая ссылка! Спецификация " + Спецификация + " уже встречалась в пути: " + ТекстПути;
			Сообщить(Ошибка);
			Возврат ТаблицаСпецификаций;
		КонецЕсли;
		
		ПутьСпецификаций.Добавить(Спецификация);
		
		ТаблицаСпецификаций.Колонки.Добавить("Номенклатура");
		ТаблицаСпецификаций.Колонки.Добавить("Количество");
		ТаблицаСпецификаций.Колонки.Добавить("Спецификация");
		
		НоваяСтрока = ТаблицаСпецификаций.Добавить();
		НоваяСтрока.Спецификация = Спецификация;
		
		Если АлгоритмРасчетаКоличества <> "" Тогда
			ОсновноеИзделиеКоличествоНовое = ВычислитьАлгоритмРасчетаКоличества(АлгоритмРасчетаКоличества, ОсновноеИзделиеКоличество);
		Иначе
			ОсновноеИзделиеКоличествоНовое = Количество;
		КонецЕсли;
		
		НоваяСтрока.Количество = ОсновноеИзделиеКоличествоНовое;
		
		Если Номенклатура <> Неопределено Тогда
			НоваяСтрока.Номенклатура = Номенклатура;
		Иначе
			НоваяСтрока.Номенклатура = Расш1_ОбщийМодуль1.НоменклатураПоСпецификации(Спецификация);
		КонецЕсли;
		
		// Получение материалов спецификации с учетом этапа
		СпособПолученияПроизвестиПоСпецификации = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизвестиПоСпецификации;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	МатериалыИУслуги.Номенклатура КАК Материал,
		|	МатериалыИУслуги.КоличествоУпаковок КАК Количество,
		|	МатериалыИУслуги.АлгоритмРасчетаКоличества КАК АлгоритмРасчетаКоличества,
		|	МатериалыИУслуги.Ссылка.ОсновноеИзделиеКоличествоУпаковок КАК ОсновноеИзделиеКоличествоУпаковок,
		|	ВЫРАЗИТЬ(МатериалыИУслуги.ИсточникПолученияПолуфабриката КАК Справочник.РесурсныеСпецификации).Ссылка КАК ИсточникПолученияПолуфабрикатаСсылка
		|ИЗ
		|	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК МатериалыИУслуги
		|ГДЕ
		|	МатериалыИУслуги.Ссылка = &Спецификация
		|	И МатериалыИУслуги.СпособПолученияМатериала = &СпособПолученияПроизвестиПоСпецификации
		|	И (&ЭтапСпецификации = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
		|		ИЛИ МатериалыИУслуги.Этап = &ЭтапСпецификации)";
		
		Запрос.УстановитьПараметр("Спецификация", Спецификация);
		Запрос.УстановитьПараметр("СпособПолученияПроизвестиПоСпецификации", СпособПолученияПроизвестиПоСпецификации);
		Запрос.УстановитьПараметр("ЭтапСпецификации", ?(ЗначениеЗаполнено(ЭтапСпецификации), ЭтапСпецификации, Справочники.ЭтапыПроизводства.ПустаяСсылка()));
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ИсточникПолученияПолуфабрикатаСсылка <> Null Тогда
				СпецификацияПолуфабриката = Выборка.ИсточникПолученияПолуфабрикатаСсылка;
			Иначе
				СпецификацияПолуфабриката = Расш1_ОбщийМодуль1.ПриоритетнаяСпецификация(Выборка.Материал, Дата);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СпецификацияПолуфабриката) Тогда
				
				// Проверяем подразделение последнего этапа спецификации
				ПодразделениеПоследнегоЭтапа = ПодразделениеПоследнегоЭтапаСпецификации(СпецификацияПолуфабриката);
				
				// Проваливаемся только если подразделение совпадает или не задано
				Если Не ЗначениеЗаполнено(Подразделение) Или ПодразделениеПоследнегоЭтапа = Подразделение Тогда
					
					Если Выборка.ОсновноеИзделиеКоличествоУпаковок <> 0 Тогда
						КоличествоПолуфабриката = Выборка.Количество / Выборка.ОсновноеИзделиеКоличествоУпаковок * ОсновноеИзделиеКоличествоНовое;
					Иначе
						КоличествоПолуфабриката = 0;
					КонецЕсли;
					
					// При рекурсивном проваливании этап не передаем (Неопределено), 
					// так как для вложенной спецификации выбираются все материалы
					ТаблицаПодчиненных = ТаблицаПолуфабрикатовДереваСпецификацийРекурсивноСЭтапомИПодразделением(
						Ошибка, 
						СпецификацияПолуфабриката, 
						Выборка.Материал, 
						КоличествоПолуфабриката, 
						Выборка.АлгоритмРасчетаКоличества, 
						ОсновноеИзделиеКоличество, 
						ПутьСпецификаций, 
						Дата,
						Подразделение,
						Справочники.ЭтапыПроизводства.ПустаяСсылка());
					
					Если ТаблицаПодчиненных.Количество() > 0 Тогда
						Для Каждого Строка Из ТаблицаПодчиненных Цикл
							ЗаполнитьЗначенияСвойств(ТаблицаСпецификаций.Добавить(), Строка);
						КонецЦикла;
					КонецЕсли;
					
				КонецЕсли;
				
			Иначе
				Ошибка = "Ошибка! Не найдена спецификация на полуфабрикат: " + Выборка.Материал;
				Сообщить(Ошибка);
				Продолжить;
			КонецЕсли;
			
		КонецЦикла;
		
		Индекс = ПутьСпецификаций.Найти(Спецификация);
		Если Индекс <> Неопределено Тогда
			ПутьСпецификаций.Удалить(Индекс);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТаблицаСпецификаций;
	
КонецФункции 

// Заполняет регистр А1_СпецификацииВКалькуляциях данными о всех спецификациях дерева
// для указанной плановой калькуляции.
// 
// Параметры:
//  КалькуляцияСсылка - ДокументСсылка.ПлановаяКалькуляция2_2 - ссылка на документ калькуляции
//
Процедура ЗаполнитьРегистрСпецификацийВКалькуляциях(КалькуляцияСсылка) Экспорт
	
	// Очищаем записи регистра для данной калькуляции
	// ПлановаяКалькуляция - это ресурс, а не измерение, поэтому удаляем через запрос
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Рег.РесурснаяСпецификация КАК РесурснаяСпецификация
	|ИЗ
	|	РегистрСведений.А1_СпецификацииВКалькуляциях КАК Рег
	|ГДЕ
	|	Рег.ПлановаяКалькуляция = &ПлановаяКалькуляция";
	Запрос.УстановитьПараметр("ПлановаяКалькуляция", КалькуляцияСсылка);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.А1_СпецификацииВКалькуляциях.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.РесурснаяСпецификация = Выборка.РесурснаяСпецификация;
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписи.Удалить();
		КонецЕсли;
	КонецЦикла;
	
	ДатаДокумента = КалькуляцияСсылка.Дата;
	
	// Обходим объекты калькуляции
	Для Каждого СтрокаОК Из КалькуляцияСсылка.ОбъектыКалькуляции Цикл
		
		Если ТипЗнч(СтрокаОК.Объект) <> Тип("СправочникСсылка.РесурсныеСпецификации") 
			Или НЕ ЗначениеЗаполнено(СтрокаОК.Объект) Тогда
			Продолжить;
		КонецЕсли;
		
		КорневаяСпецификация = СтрокаОК.Объект;
		Номенклатура = СтрокаОК.Номенклатура;
		Количество = СтрокаОК.Количество;
		
		// Обрабатываем каждый объект в своём Попытка-Исключение,
		// чтобы ошибка в одном объекте не прерывала запись остальных (как в отчёте)
		Попытка
			Ошибка = "";
			ПутьСпецификаций = Новый Массив;
			ТаблицаСпецификаций = ТаблицаПолуфабрикатовДереваСпецификацийРекурсивно(
				Ошибка, 
				КорневаяСпецификация, 
				Номенклатура, 
				Количество, 
				"", 
				Количество, 
				ПутьСпецификаций, 
				ДатаДокумента);
			
			Если ТаблицаСпецификаций.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			// Записываем все спецификации из дерева в регистр
			Для Каждого СтрокаСпецификации Из ТаблицаСпецификаций Цикл
				
				Если НЕ ЗначениеЗаполнено(СтрокаСпецификации.Спецификация) Тогда
					Продолжить;
				КонецЕсли;
				
				МенеджерЗаписи = РегистрыСведений.А1_СпецификацииВКалькуляциях.СоздатьМенеджерЗаписи();
				МенеджерЗаписи.РесурснаяСпецификация = СтрокаСпецификации.Спецификация;
				МенеджерЗаписи.ПлановаяКалькуляция = КалькуляцияСсылка;
				МенеджерЗаписи.КорневаяРесурснаяСпецификация = КорневаяСпецификация;
				МенеджерЗаписи.Записать();
				
			КонецЦикла;
		Исключение
			// Ошибка в одном объекте калькуляции не должна прерывать обработку остальных
			Сообщить("Ошибка обработки спецификации " + КорневаяСпецификация + ": " + ОписаниеОшибки());
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры