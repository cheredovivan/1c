#Область Доработки     

//Использование 
//Документ.ЗаказНаПроизводство2_2.МодульОбъекта
//Документ.ЭтапПроизводства2_2.МодульМенеджера 

Функция СпецификацияТребуетПроверкиСлужбамиСКБиОГТ (ЗаказНаПроизводство) Экспорт    
	
	РезультатПроверкиНаАктуальность = РезультатПроверкиДереваСпецификаций(ЗаказНаПроизводство);  

	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатПроверкиНаАктуальность); 	

	Если РезультатПроверкиНаАктуальность <> "Все спецификации актуальны." Тогда  
		Возврат Истина;	
	Иначе
		Возврат Ложь;
	КонецЕсли;            
	
КонецФункции  

Функция РезультатПроверкиДереваСпецификаций (ЗаказНаПроизводство)    
	
	Результат = "Все спецификации актуальны.";
	
	врЗапросСпецификаций = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Продукция.Спецификация КАК Спецификация,
	|	Продукция.Характеристика КАК Характеристика
	|ИЗ
	|	Документ.ЗаказНаПроизводство2_2.Продукция КАК Продукция
	|ГДЕ  
	|	Продукция.Ссылка = &ЗаказНаПроизводство
	|	И НЕ Продукция.Спецификация = ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|	И НЕ Продукция.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)");
	врЗапросСпецификаций.УстановитьПараметр("ЗаказНаПроизводство", ЗаказНаПроизводство); 	
	РезультатЗапроса = врЗапросСпецификаций.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда 
		Выборка = РезультатЗапроса.Выбрать(); 
		Пока Выборка.Следующий() Цикл
			СпецификацияСсылка = Выборка.Спецификация; 
			СпецификацияОбъект = СпецификацияСсылка.ПолучитьОбъект();
			НаименованиеСпецификации = СпецификацияОбъект.Наименование;
			ОсновноеИзделиеНоменклатура = СпецификацияОбъект.ОсновноеИзделиеНоменклатура;
			НаименованияИзмененныхСпецификаций = ЕстьИзмененияВДеревеСпецификацийПослеПроверки(СпецификацияСсылка, ОсновноеИзделиеНоменклатура);
			Если НаименованияИзмененныхСпецификаций <> "" Тогда  
				Если Результат = "Все спецификации актуальны." Тогда 
					Результат = "Дерево спецификаций требует проверки: " + Символы.ПС + НаименованиеСпецификации 
					+ "(изменения в:" + Символы.ПС + НаименованияИзмененныхСпецификаций + ");"; 
				Иначе Результат = Результат + Символы.ПС + НаименованиеСпецификации + "(изменения в:" + Символы.ПС + НаименованияИзмененныхСпецификаций + ");";	
				КонецЕсли; 
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;  
	
	Возврат Результат;
	
КонецФункции
				
Функция ЕстьИзмененияВДеревеСпецификацийПослеПроверки(ГоловнаяСпецификация, ОсновноеИзделиеНоменклатура)   
	        	  
	Ошибка = "";		
    НаименованияИзмененныхСпецификаций = "";
	
	РеквизитДатаПроверкиСпецификации = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ПровереноОГТ_3f752df67cfb493b9bed268d425cfc60");
	ЗначениеРеквизитаДатаПроверкиСпецификации = УправлениеСвойствами.ЗначениеСвойства(ГоловнаяСпецификация, РеквизитДатаПроверкиСпецификации); 	
	ПолгодаВСекундах = 6*30*86400;
	ДатаПолгодаНазад = ТекущаяДатаСеанса() - ПолгодаВСекундах;

	Если ЗначениеРеквизитаДатаПроверкиСпецификации = Неопределено Тогда
		НаименованияИзмененныхСпецификаций = "Спецификации не проверялись. Отсутствует дата проверки."; 
	ИначеЕсли ЗначениеРеквизитаДатаПроверкиСпецификации < ДатаПолгодаНазад Тогда 
		НаименованияИзмененныхСпецификаций = "Все спецификации проверялись более полугода назад.";  
	Иначе   		  
		ДатаПроверкиСпецификацииПлюсСутки = ЗначениеРеквизитаДатаПроверкиСпецификации + 86399;    		
		ТаблицаСпецификаций = ТаблицаДереваСпецификацийРекурсивно(Ошибка, ГоловнаяСпецификация, ОсновноеИзделиеНоменклатура); 
		Если Ошибка = "" Тогда
			МассивСпецификаций = ТаблицаСпецификаций.ВыгрузитьКолонку("Спецификация");   
			
			врЗапросСпецификаций												= Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВерсииОбъектов.Объект КАК Объект,
			|	ВерсииОбъектов.ДатаВерсии КАК ДатаВерсии
			|ИЗ
			|	РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
			|ГДЕ  
			|	ВерсииОбъектов.Объект В (&МассивСпецификаций)
			|	И ВерсииОбъектов.ДатаВерсии > &ЗначениеРеквизитаДатаПроверкиСпецификации"); 
			
			врЗапросСпецификаций.УстановитьПараметр("МассивСпецификаций", МассивСпецификаций);
			врЗапросСпецификаций.УстановитьПараметр("ЗначениеРеквизитаДатаПроверкиСпецификации", ДатаПроверкиСпецификацииПлюсСутки); 
			
			Выборка = врЗапросСпецификаций.Выполнить().Выбрать();
			
			Пока Выборка.Следующий() Цикл 
				НаименованияИзмененныхСпецификаций = НаименованияИзмененныхСпецификаций + Выборка.Объект.Наименование + "; " + Символы.ПС;			
			КонецЦикла;
		Иначе
			НаименованияИзмененныхСпецификаций = Ошибка; //пока так для отладки, потом надо сделать более жестко
		КонецЕсли;
	КонецЕсли;     
	
	Возврат НаименованияИзмененныхСпецификаций;  
	
КонецФункции	

Функция НоменклатураПоСпецификации(Спецификация) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РесурсныеСпецификации.ОсновноеИзделиеНоменклатура КАК ОсновноеИзделиеНоменклатура	
	|ИЗ
	|	Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
	|ГДЕ
	|	РесурсныеСпецификации.Ссылка = &Спецификация";  
	
	Запрос.УстановитьПараметр("Спецификация", Спецификация);  	
	Выборка = Запрос.Выполнить().Выбрать();     			
	
	Если Выборка.Следующий() Тогда				
		Номенклатура = Выборка.ОсновноеИзделиеНоменклатура;
	Иначе
		Номенклатура = Справочники.Номенклатура.ПустаяСсылка(); 
	КонецЕсли;
	Возврат Номенклатура;
КонецФункции	

Функция ТаблицаДереваСпецификацийРекурсивно(Ошибка, Спецификация, Номенклатура)
	
	ТаблицаСпецификаций = Новый ТаблицаЗначений;               
	
	Если Спецификация <> Неопределено Тогда   
		
		ТаблицаСпецификаций.Колонки.Добавить("Номенклатура");
		ТаблицаСпецификаций.Колонки.Добавить("Спецификация"); 	
		
		НоваяСтрока = ТаблицаСпецификаций.Добавить();
		НоваяСтрока.Спецификация = Спецификация;	
		
		Если Номенклатура <> Неопределено Тогда
			НоваяСтрока.Номенклатура = Номенклатура;
		Иначе
			НоваяСтрока.Номенклатура = НоменклатураПоСпецификации(Спецификация);	
		КонецЕсли;
		

		СпособПолученияПроизводитсяНаЭтапе = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе;
		СтатьиКалькуляцииПокупных = Новый Массив;
		СтатьяКалькуляции = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул","ПокупныеМатериалы");
		Если ЗначениеЗаполнено(СтатьяКалькуляции) тогда
			СтатьиКалькуляцииПокупных.Добавить(СтатьяКалькуляции);
		КонецЕсли;
		СтатьяКалькуляции = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул","ПокупныеКомплектующие");
		Если ЗначениеЗаполнено(СтатьяКалькуляции) тогда
			СтатьиКалькуляцииПокупных.Добавить(СтатьяКалькуляции);
		КонецЕсли;   
		СтатьяКалькуляции = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул","РаботыСобственные");
		Если ЗначениеЗаполнено(СтатьяКалькуляции) тогда
			СтатьиКалькуляцииПокупных.Добавить(СтатьяКалькуляции);
		КонецЕсли;		                           
		СтатьяКалькуляции = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул","ДавальческоеСырье");
		Если ЗначениеЗаполнено(СтатьяКалькуляции) тогда
			СтатьиКалькуляцииПокупных.Добавить(СтатьяКалькуляции);
		КонецЕсли;			
	
		
	    Запрос = Новый Запрос;
	    Запрос.Текст =
	    "ВЫБРАТЬ РАЗЛИЧНЫЕ
	    |	МатериалыИУслуги.СпособПолученияМатериала КАК СпособПолученияМатериала,
	    |	МатериалыИУслуги.СтатьяКалькуляции КАК СтатьяКалькуляции, 
		|	МатериалыИУслуги.Номенклатура КАК Материал,
	    |	МатериалыИУслуги.ИсточникПолученияПолуфабриката КАК ИсточникПолученияПолуфабрикатаСсылка		
	    |ИЗ
	    |	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК МатериалыИУслуги
	    |ГДЕ
	    |	МатериалыИУслуги.Ссылка = &Спецификация
	    |	И МатериалыИУслуги.СпособПолученияМатериала <> &СпособПолученияПроизводитсяНаЭтапе";  
		
	    Запрос.УстановитьПараметр("Спецификация", Спецификация);  
	    Запрос.УстановитьПараметр("СпособПолученияПроизводитсяНаЭтапе", СпособПолученияПроизводитсяНаЭтапе);	
	    Выборка = Запрос.Выполнить().Выбрать();

		Пока Выборка.Следующий() Цикл 
			Если (СтатьиКалькуляцииПокупных.Найти(Выборка.СтатьяКалькуляции) = Неопределено) Тогда  
				Если (Выборка.ИсточникПолученияПолуфабрикатаСсылка <> Неопределено) Тогда 
					СпецификацияПолуфабриката = Выборка.ИсточникПолученияПолуфабрикатаСсылка;
				Иначе	
					СпецификацияПолуфабриката = ПриоритетнаяСпецификация(Выборка.Материал);
				КонецЕсли;
				Если (СпецификацияПолуфабриката <> Неопределено) Тогда			
					ТаблицаПодчиненных = ТаблицаДереваСпецификацийРекурсивно(Ошибка, СпецификацияПолуфабриката, Выборка.Материал); 
					Если ТаблицаПодчиненных.Количество() > 0 Тогда
						Для Каждого Строка Из ТаблицаПодчиненных Цикл 
							ЗаполнитьЗначенияСвойств(ТаблицаСпецификаций.Добавить(), Строка);
						КонецЦикла;	
					КонецЕсли; 
				Иначе   
					Ошибка = "Не найдена спецификация на материал: " + Выборка.Материал;
					Прервать;	
				КонецЕсли;
			КонецЕсли;	
		КонецЦикла;
	КонецЕсли;    
	
	Возврат ТаблицаСпецификаций;
	
КонецФункции 
   
Функция ПриоритетнаяСпецификация(Номенклатура, ДатаАктуальности = Неопределено) Экспорт
    Запрос = Новый Запрос;
    Запрос.Текст = 
        "ВЫБРАТЬ
        |	ВыходныеИзделия.Ссылка КАК Спецификация,
        |	ВыходныеИзделия.Номенклатура КАК Номенклатура
        |ИЗ
        |	Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК ВыходныеИзделия
        |ГДЕ
        |	ВыходныеИзделия.Номенклатура = &Номенклатура
        |	И ВыходныеИзделия.Ссылка.ВариантПодбораВДокументы = ЗНАЧЕНИЕ(Перечисление.ВариантыПодбораСпецификацииВДокументы.Автоматически)
        |	И ВыходныеИзделия.Ссылка.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Сборка)
        |	И ВыходныеИзделия.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.Действует)
        |	И (ВыходныеИзделия.Ссылка.НачалоДействия = ДАТАВРЕМЯ(1, 1, 1)
        |			ИЛИ ВыходныеИзделия.Ссылка.НачалоДействия <= &ТекущаяДата)
        |	И (ВыходныеИзделия.Ссылка.КонецДействия = ДАТАВРЕМЯ(1, 1, 1)
        |			ИЛИ ВыходныеИзделия.Ссылка.КонецДействия > &ТекущаяДата)
        |
        |УПОРЯДОЧИТЬ ПО
        |	ВыходныеИзделия.Ссылка.Код УБЫВ";

	Если ДатаАктуальности = Неопределено Тогда
		ДатаАктуальности = ТекущаяДатаСеанса();
	КонецЕсли;
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ТекущаяДата", ДатаАктуальности);
    Выборка = Запрос.Выполнить().Выбрать();


    Если Выборка.Следующий() Тогда
        Возврат Выборка.Спецификация;
    Иначе
        Возврат Неопределено;
    КонецЕсли;	
КонецФункции    

// Получает соответствие приоритетных спецификаций для списка номенклатур на указанную дату
// Параметры:
//   СписокНоменклатур - Массив из СправочникСсылка.Номенклатура - список номенклатур для поиска спецификаций
//   ДатаАктуальности - Дата - дата для определения действующей спецификации (по умолчанию - сегодня)
// Возвращаемое значение:
//   Соответствие - Ключ: СправочникСсылка.Номенклатура, Значение: СправочникСсылка.РесурсныеСпецификации
Функция ПриоритетныеСпецификацииДляСпискаНоменклатур(СписокНоменклатур, ДатаАктуальности = Неопределено) Экспорт
	
	Результат = Новый Соответствие;
	
	Если СписокНоменклатур = Неопределено ИЛИ СписокНоменклатур.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ДатаАктуальности = Неопределено Тогда
		ДатаАктуальности = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВыходныеИзделия.Ссылка КАК Спецификация,
		|	ВыходныеИзделия.Номенклатура КАК Номенклатура,
		|	ВыходныеИзделия.Ссылка.Код КАК КодСпецификации
		|ИЗ
		|	Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК ВыходныеИзделия
		|ГДЕ
		|	ВыходныеИзделия.Номенклатура В(&СписокНоменклатур)
		|	И ВыходныеИзделия.Ссылка.ВариантПодбораВДокументы = ЗНАЧЕНИЕ(Перечисление.ВариантыПодбораСпецификацииВДокументы.Автоматически)
		|	И ВыходныеИзделия.Ссылка.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Сборка)
		|	И ВыходныеИзделия.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.Действует)
		|	И (ВыходныеИзделия.Ссылка.НачалоДействия = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ ВыходныеИзделия.Ссылка.НачалоДействия <= &ДатаАктуальности)
		|	И (ВыходныеИзделия.Ссылка.КонецДействия = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ ВыходныеИзделия.Ссылка.КонецДействия >= &ДатаАктуальности)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВыходныеИзделия.Номенклатура,
		|	КодСпецификации УБЫВ";
	
	Запрос.УстановитьПараметр("СписокНоменклатур", СписокНоменклатур);
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		// Добавляем только первую найденную спецификацию для каждой номенклатуры (она приоритетная по коду)
		Если Результат.Получить(Выборка.Номенклатура) = Неопределено Тогда
			Результат.Вставить(Выборка.Номенклатура, Выборка.Спецификация);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Получает подразделение-исполнитель последнего этапа спецификации
// Параметры:
//   Спецификация - СправочникСсылка.РесурсныеСпецификации - спецификация для анализа
// Возвращаемое значение:
//   СправочникСсылка.СтруктураПредприятия или Неопределено - подразделение последнего этапа
Функция ПодразделениеПоследнегоЭтапаСпецификации(Спецификация) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Спецификация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЭтапыПроизводства.Подразделение КАК Подразделение
		|ИЗ
		|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
		|ГДЕ
		|	ЭтапыПроизводства.Владелец = &Спецификация
		|	И НЕ ЭтапыПроизводства.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭтапыПроизводства.НомерЭтапа УБЫВ";
	
	Запрос.УстановитьПараметр("Спецификация", Спецификация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Подразделение;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Получает соответствие подразделений последнего этапа для списка спецификаций
// Параметры:
//   СписокСпецификаций - Массив из СправочникСсылка.РесурсныеСпецификации - список спецификаций
// Возвращаемое значение:
//   Соответствие - Ключ: СправочникСсылка.РесурсныеСпецификации, Значение: СправочникСсылка.СтруктураПредприятия
Функция ПодразделенияПоследнегоЭтапаДляСпискаСпецификаций(СписокСпецификаций) Экспорт
	
	Результат = Новый Соответствие;
	
	Если СписокСпецификаций = Неопределено ИЛИ СписокСпецификаций.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭтапыПроизводства.Владелец КАК Спецификация,
		|	ЭтапыПроизводства.Подразделение КАК Подразделение,
		|	ЭтапыПроизводства.НомерЭтапа КАК НомерЭтапа
		|ИЗ
		|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
		|ГДЕ
		|	ЭтапыПроизводства.Владелец В(&СписокСпецификаций)
		|	И НЕ ЭтапыПроизводства.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Спецификация,
		|	НомерЭтапа УБЫВ";
	
	Запрос.УстановитьПараметр("СписокСпецификаций", СписокСпецификаций);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		// Добавляем только первое найденное подразделение для каждой спецификации (с максимальным номером этапа)
		Если Результат.Получить(Выборка.Спецификация) = Неопределено Тогда
			Результат.Вставить(Выборка.Спецификация, Выборка.Подразделение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Получает соответствие подразделений первого этапа для списка спецификаций
// Параметры:
//   СписокСпецификаций - Массив из СправочникСсылка.РесурсныеСпецификации - список спецификаций
// Возвращаемое значение:
//   Соответствие - Ключ: СправочникСсылка.РесурсныеСпецификации, Значение: СправочникСсылка.СтруктураПредприятия
Функция ПодразделенияПервогоЭтапаДляСпискаСпецификаций(СписокСпецификаций) Экспорт
	
	Результат = Новый Соответствие;
	
	Если СписокСпецификаций = Неопределено ИЛИ СписокСпецификаций.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭтапыПроизводства.Владелец КАК Спецификация,
		|	ЭтапыПроизводства.Подразделение КАК Подразделение,
		|	ЭтапыПроизводства.НомерЭтапа КАК НомерЭтапа
		|ИЗ
		|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
		|ГДЕ
		|	ЭтапыПроизводства.Владелец В(&СписокСпецификаций)
		|	И НЕ ЭтапыПроизводства.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Спецификация,
		|	НомерЭтапа";
	
	Запрос.УстановитьПараметр("СписокСпецификаций", СписокСпецификаций);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		// Добавляем только первое найденное подразделение для каждой спецификации (с минимальным номером этапа)
		Если Результат.Получить(Выборка.Спецификация) = Неопределено Тогда
			Результат.Вставить(Выборка.Спецификация, Выборка.Подразделение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

//Использование 
//Отчет Расш1_ДвижениеЗаказовНаПроизводство.МодульОбъекта

Функция ТаблицаДвиженияЗаказовНаПроизводство(Период) Экспорт

	ТаблицаДвиженияЗаказовНаПроизводство = Новый ТаблицаЗначений;	   
	
	ТаблицаДвиженияЗаказовНаПроизводство.Колонки.Добавить("ЗаказНаПроизводство");
	ТаблицаДвиженияЗаказовНаПроизводство.Колонки.Добавить("НомерЗаказаНаПроизводство");
	ТаблицаДвиженияЗаказовНаПроизводство.Колонки.Добавить("Подразделение");
	ТаблицаДвиженияЗаказовНаПроизводство.Колонки.Добавить("ДатаСтатусФормируется");  
	ТаблицаДвиженияЗаказовНаПроизводство.Колонки.Добавить("ДнейПроверки"); 
	ТаблицаДвиженияЗаказовНаПроизводство.Колонки.Добавить("ДатаСтатусКПроизводству"); 
	ТаблицаДвиженияЗаказовНаПроизводство.Колонки.Добавить("ДнейПроизводства");
	ТаблицаДвиженияЗаказовНаПроизводство.Колонки.Добавить("ДатаСтатусЗакрыт");
	
	Запрос = Новый Запрос;  
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|   ВерсииОбъектов.Объект КАК ЗаказНаПроизводство, 
	|   ЗаказыНаПроизводство.Подразделение КАК Подразделение,
	|   ЗаказыНаПроизводство.Номер КАК НомерЗаказаНаПроизводство,
	|   ВерсииОбъектов.НомерВерсии КАК НомерВерсии,
	|   ВерсииОбъектов.ДатаВерсии КАК ДатаВерсии	
	|ИЗ
	|   РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
	|   ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство2_2 КАК ЗаказыНаПроизводство
	|   ПО ВерсииОбъектов.Объект = ЗаказыНаПроизводство.Ссылка
	|ГДЕ
	|   ВерсииОбъектов.Объект ССЫЛКА Документ.ЗаказНаПроизводство2_2 
	|	И ВерсииОбъектов.ДатаВерсии > &НачалоПериода
	|   И ЗаказыНаПроизводство.Подразделение В (&СписокПодразделений)";
	
	
	СписокПодразделений = Новый СписокЗначений;
	СписокПодразделений.Добавить(Справочники.СтруктураПредприятия.НайтиПоНаименованию("14.1 Участок измерительной техники"));
	СписокПодразделений.Добавить(Справочники.СтруктураПредприятия.НайтиПоНаименованию("14.2 Участок шахтной техники"));	
	СписокПодразделений.Добавить(Справочники.СтруктураПредприятия.НайтиПоНаименованию("14.3 Участок светотехники")); 
	СписокПодразделений.Добавить(Справочники.СтруктураПредприятия.НайтиПоНаименованию("14.4 Участок ТКС"));   
	
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	//Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		// Получаем разбор версии документа
		ИнформацияОВерсии = ВерсионированиеОбъектов.РазборВерсии(Выборка.ЗаказНаПроизводство, Выборка.НомерВерсии);
		
		// Находим статус в реквизитах версии
		СтатусВерсии = ИнформацияОВерсии.Реквизиты.Найти("Статус", "НаименованиеРеквизита").ЗначениеРеквизита;
		ДатаВерсии = Выборка.ДатаВерсии;
		
		// Ищем заказ в таблице
		НоваяСтрока = ТаблицаДвиженияЗаказовНаПроизводство.Найти(Выборка.ЗаказНаПроизводство, "ЗаказНаПроизводство");
		
		Если НоваяСтрока = Неопределено Тогда
			НоваяСтрока = ТаблицаДвиженияЗаказовНаПроизводство.Добавить();
			НоваяСтрока.ЗаказНаПроизводство = Выборка.ЗаказНаПроизводство;
			НоваяСтрока.НомерЗаказаНаПроизводство = Выборка.НомерЗаказаНаПроизводство; 
			НоваяСтрока.Подразделение = Выборка.Подразделение;
		КонецЕсли;
		
		// Проставляем даты только для самого первого изменения статуса
		Если СтатусВерсии = Перечисления.СтатусыЗаказовНаПроизводство2_2.Формируется Тогда
			Если (Не ЗначениеЗаполнено(НоваяСтрока.ДатаСтатусФормируется)) ИЛИ (ДатаВерсии < НоваяСтрока.ДатаСтатусФормируется) Тогда
				НоваяСтрока.ДатаСтатусФормируется = ДатаВерсии;
			КонецЕсли;
			
		ИначеЕсли СтатусВерсии = Перечисления.СтатусыЗаказовНаПроизводство2_2.КПроизводству Тогда 
			Если (Не ЗначениеЗаполнено(НоваяСтрока.ДатаСтатусКПроизводству)) ИЛИ (ДатаВерсии < НоваяСтрока.ДатаСтатусКПроизводству) Тогда
				НоваяСтрока.ДатаСтатусКПроизводству = ДатаВерсии;
			КонецЕсли;
			
		ИначеЕсли СтатусВерсии = Перечисления.СтатусыЗаказовНаПроизводство2_2.Закрыт Тогда 
			Если (Не ЗначениеЗаполнено(НоваяСтрока.ДатаСтатусЗакрыт)) ИЛИ (ДатаВерсии < НоваяСтрока.ДатаСтатусЗакрыт) Тогда
				НоваяСтрока.ДатаСтатусЗакрыт = ДатаВерсии;
			КонецЕсли;			
		КонецЕсли;
	КонецЦикла;
  
	
	Для Каждого Строка Из ТаблицаДвиженияЗаказовНаПроизводство Цикл 
		Если ЗначениеЗаполнено(Строка.ДатаСтатусФормируется) И ЗначениеЗаполнено(Строка.ДатаСтатусКПроизводству) Тогда
			
			Если НачалоДня(Строка.ДатаСтатусФормируется) = НачалоДня(Строка.ДатаСтатусКПроизводству) Тогда
				Строка.ДнейПроверки = 1; // Даты совпадают 
			Иначе
				Строка.ДнейПроверки = (НачалоДня(Строка.ДатаСтатусКПроизводству) - НачалоДня(Строка.ДатаСтатусФормируется)) / (60 * 60 * 24); 
			КонецЕсли;
		Иначе
			Строка.ДнейПроверки = Неопределено; // или Неопределено
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.ДатаСтатусКПроизводству) И ЗначениеЗаполнено(Строка.ДатаСтатусЗакрыт) Тогда 
			Если НачалоДня(Строка.ДатаСтатусЗакрыт) = НачалоДня(Строка.ДатаСтатусКПроизводству) Тогда
				Строка.ДнейПроизводства = 1; // Даты совпадают 
			Иначе
				Строка.ДнейПроизводства = (НачалоДня(Строка.ДатаСтатусЗакрыт) - НачалоДня(Строка.ДатаСтатусКПроизводству)) / (60 * 60 * 24); 
			КонецЕсли;
		Иначе
			Строка.ДнейПроизводства = Неопределено; // или Неопределено
		КонецЕсли;
	КонецЦикла;
	
	ОтфильтрованнаяТаблица = Новый ТаблицаЗначений;
	ОтфильтрованнаяТаблица.Колонки.Добавить("ЗаказНаПроизводство"); 
	ОтфильтрованнаяТаблица.Колонки.Добавить("НомерЗаказаНаПроизводство");
	ОтфильтрованнаяТаблица.Колонки.Добавить("Подразделение");
	ОтфильтрованнаяТаблица.Колонки.Добавить("ДатаСтатусФормируется");  
	ОтфильтрованнаяТаблица.Колонки.Добавить("ДнейПроверки"); 
	ОтфильтрованнаяТаблица.Колонки.Добавить("ДатаСтатусКПроизводству"); 
	ОтфильтрованнаяТаблица.Колонки.Добавить("ДнейПроизводства");
	ОтфильтрованнаяТаблица.Колонки.Добавить("ДатаСтатусЗакрыт");

	
	Для Каждого Строка Из ТаблицаДвиженияЗаказовНаПроизводство Цикл
		Если (ЗначениеЗаполнено(Строка.ДатаСтатусФормируется) И ((ЗначениеЗаполнено(Строка.ДатаСтатусКПроизводству)) ИЛИ (ЗначениеЗаполнено(Строка.ДатаСтатусЗакрыт)))) Тогда
			Если (Строка.ДатаСтатусФормируется < Период.ДатаОкончания) Тогда	
				ЗаполнитьЗначенияСвойств(ОтфильтрованнаяТаблица.Добавить(), Строка);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОтфильтрованнаяТаблица;
	
КонецФункции

// ОбщийМодуль.РасширенияJSON
Функция СтруктураВJson(Значение, ПараметрыПреобразования = Неопределено) Экспорт
    // Инициализируем сериализатор
    ЗаписьJSON = Новый ЗаписьJSON;
    ЗаписьJSON.УстановитьСтроку();
    // Конвертация структуры (и вложенных данных) в JSON
    ЗаписатьJSON(
        ЗаписьJSON,
        Значение,
        , // имя функции обратного восстановления (можно оставить пустым)
        , // модуль, реализующий восстановление (не нужен)
        ПараметрыПреобразования
    );
    // Возвращаем итоговую JSON-строку
    Возврат ЗаписьJSON.Закрыть();
КонецФункции

Процедура ОтправитьВTelegram(СообщениеТекста) Экспорт
    // Параметры вашего бота и группы
    Токен           = "6211488387:AAFxSevvNTC8xufi3EbL2ikNgtsCrO5F7RI";
    ChatID          = "-1001948286476";  // ID группы
    MessageThreadID = 50308;         // ID темы внутри группы

    // 1. Формируем структуру и сериализуем в JSON
    Тело       = Новый Структура("chat_id, message_thread_id, text",
                   ChatID,
                   MessageThreadID,
                   СообщениеТекста);
    JSONСтрока = СтруктураВJson(Тело);

    // 2. Готовим заголовки
    Заголовки = Новый Соответствие;
    Заголовки.Вставить("Content-Type", "application/json; charset=utf-8");

    // 3. Создаём HTTP-запрос
    HTTPЗапрос = Новый HTTPЗапрос(
        "/bot" + Токен + "/sendMessage",
        Заголовки
    );
    HTTPЗапрос.УстановитьТелоИзСтроки(JSONСтрока, "UTF-8");

    // 4. Подключаемся к Telegram по HTTPS (с указанием таймаута)
    Таймаут = 15; // секунд
    HTTPСоединение = Новый HTTPСоединение(
        "api.telegram.org",
        443,
        , , ,
        Таймаут,
        Истина
    );

    // 5. Отправляем и логируем ответ
    Попытка
        Ответ = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);
        Сообщить("HTTP-код Telegram: " + Ответ.КодСостояния);
        Сообщить("Ответ Telegram: " + Ответ.ПолучитьТелоКакСтроку());
    Исключение
        Сообщить("Ошибка отправки в Telegram: " + ОписаниеОшибки());
    КонецПопытки;
КонецПроцедуры

Функция ЕстьРеквизитИлиСвойствоОбъекта(Объект, ИмяРеквизита) Экспорт

   КлючУникальности   = Новый УникальныйИдентификатор;
   СтруктураРеквизита = Новый Структура(ИмяРеквизита, КлючУникальности);
   ЗаполнитьЗначенияСвойств(СтруктураРеквизита, Объект);

   Возврат СтруктураРеквизита[ИмяРеквизита] <> КлючУникальности;

КонецФункции

//Общие.Подписки на события Расш1_ПодпискаНаСобытиеПометкиУдаленияСправочник и Расш1_ПодпискаНаСобытиеПометкиУдаленияДокумент
// Параметры:
//  ИмяРоли - Строка - имя роли для проверки
// Возвращаемое значение:
//  Булево - Истина, если пользователь имеет указанную роль, иначе Ложь
Функция ПользовательИмеетРоль(ИмяРоли) Экспорт
    // Ищем роль по полному имени в метаданных
    РольМД = Метаданные.Роли.Найти(ИмяРоли);
    Если РольМД = Неопределено Тогда
        Возврат Ложь;
	КонецЕсли;   
	РольДоступна = РольДоступна(РольМД);
    Возврат РольДоступна;
КонецФункции

Процедура ПриЗаписиЗапретПометкиУдаления(ЭтотОбъект, Отказ) Экспорт      
		
	Если ЭтотОбъект.ПометкаУдаления <> ЭтотОбъект.Ссылка.ПометкаУдаления Тогда	
		
		РольДоступна = ПользовательИмеетРоль("РазрешатьПометкуУдаления");
		Если РольДоступна Тогда
			Возврат;
		КонецЕсли;  
		
		РольДоступна = ПользовательИмеетРоль("АдминистраторСистемы");
		Если РольДоступна Тогда
			Возврат;
		КонецЕсли;

		Если ТипЗнч(ЭтотОбъект.Ссылка) = Тип("СправочникСсылка.ЭтапыПроизводства") Тогда
			Возврат;
		КонецЕсли; 	
		
		Сообщить("Ограничение на установку/снятие пометки удаления!");
		Отказ = Истина;     
		
	КонецЕсли; 
	
	Возврат;
	
КонецПроцедуры

//Документ ЭтапПроизводства модуль объекта  
// Параметры:
//  ДокОбъект - ДокументОбъект - объект документа этапа производства
// Возвращаемое значение:
//  ТаблицаЗначений - таблица выпуска с колонками: Номенклатура, Характеристика, Серия, Количество, ДатаПроизводства, Получатель, Назначение
Функция ПолучитьТаблицуВыпуска(ДокОбъект)  
	
    ТаблицаВыпуска = Новый ТаблицаЗначений;
    ТаблицаВыпуска.Колонки.Добавить("Номенклатура");
    ТаблицаВыпуска.Колонки.Добавить("Характеристика");
    ТаблицаВыпуска.Колонки.Добавить("Серия");
    ТаблицаВыпуска.Колонки.Добавить("Количество"); 
	ТаблицаВыпуска.Колонки.Добавить("ДатаПроизводства");
    ТаблицаВыпуска.Колонки.Добавить("Получатель");  
	ТаблицаВыпуска.Колонки.Добавить("Назначение");
	
	КоличествоВыпускаОбщее = 0;
	ПродукцияКонечногоЭтапа = Неопределено;
	НазначениеПродукцииЭтапа = Неопределено;
	ХарактеристикаПродукцииКонечногоЭтапа = Неопределено;
	
	Если ДокОбъект.ВыходныеИзделия.Количество() > 0 Тогда
		Изделие = Неопределено;
		Назначение = Неопределено;
		Для Каждого Стр Из ДокОбъект.ВыходныеИзделия Цикл
			//могут быть списаны на расходы или вообще не произведены
			Если Стр.Получатель <> Неопределено И Стр.Произведено Тогда
				//берем только первую встретившуюся номенклатуру выпуска - она и будет Продукцией этапа
				//берем все строки с этой номенклатурой
				Если (Стр.Номенклатура = Изделие ИЛИ Изделие = Неопределено) И (Стр.Назначение = Назначение ИЛИ Назначение = Неопределено) Тогда 
					Изделие = Стр.Номенклатура;
					Назначение = Стр.Назначение;
					НоваяСтрока = ТаблицаВыпуска.Добавить();
					НоваяСтрока.Номенклатура = Стр.Номенклатура;
					НоваяСтрока.Характеристика = Стр.Характеристика;
					НоваяСтрока.Серия = Стр.Серия;
					НоваяСтрока.Количество = Стр.Количество;
					//Тип = ТипЗнч(ДокОбъект.Ссылка);
					Если (ТипЗнч(ДокОбъект.Ссылка) = Тип("ДокументСсылка.ЭтапПроизводства2_2")) Тогда
						НоваяСтрока.ДатаПроизводства = Стр.ДатаПроизводства;
					Иначе 
						НоваяСтрока.ДатаПроизводства = ДокОбъект.Дата;
					КонецЕсли;
					НоваяСтрока.Получатель = Стр.Получатель;
					НоваяСтрока.Назначение = Стр.Назначение;
					
					// Вычисляем агрегированные значения
					КоличествоВыпускаОбщее = КоличествоВыпускаОбщее + Стр.Количество;
					Если ПродукцияКонечногоЭтапа = Неопределено Тогда
						ПродукцияКонечногоЭтапа = Стр.Номенклатура;
						НазначениеПродукцииЭтапа = ?(ЗначениеЗаполнено(Стр.Назначение), Стр.Назначение, Неопределено);
						ХарактеристикаПродукцииКонечногоЭтапа = ?(ЗначениеЗаполнено(Стр.Характеристика), Стр.Характеристика, Неопределено);
					КонецЕсли;
				КонецЕсли;	
			КонецЕсли;
		КонецЦикла;         
		
	ИначеЕсли ДокОбъект.ПобочныеИзделия.Количество() > 0 Тогда 
		Изделие = Неопределено;
		Назначение = Неопределено;
		Для Каждого Стр Из ДокОбъект.ПобочныеИзделия Цикл
			Если Стр.Получатель <> Неопределено И Стр.Произведено Тогда
				Если (Стр.Номенклатура = Изделие ИЛИ Изделие = Неопределено) И (Стр.Назначение = Назначение ИЛИ Назначение = Неопределено) Тогда 
					Изделие = Стр.Номенклатура;		
					Назначение = Стр.Назначение;
					НоваяСтрока = ТаблицаВыпуска.Добавить();
					НоваяСтрока.Номенклатура = Стр.Номенклатура;
					НоваяСтрока.Характеристика = Стр.Характеристика;
					НоваяСтрока.Серия = Стр.Серия;
					НоваяСтрока.Количество = Стр.Количество; 
					НоваяСтрока.ДатаПроизводства = Стр.ДатаПроизводства;
					НоваяСтрока.Получатель = Стр.Получатель;
					НоваяСтрока.Назначение = Стр.Назначение;
					
					// Вычисляем агрегированные значения
					КоличествоВыпускаОбщее = КоличествоВыпускаОбщее + Стр.Количество;
					Если ПродукцияКонечногоЭтапа = Неопределено Тогда
						ПродукцияКонечногоЭтапа = Стр.Номенклатура;
						НазначениеПродукцииЭтапа = ?(ЗначениеЗаполнено(Стр.Назначение), Стр.Назначение, Неопределено);
						ХарактеристикаПродукцииКонечногоЭтапа = ?(ЗначениеЗаполнено(Стр.Характеристика), Стр.Характеристика, Неопределено);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;    
		
	КонецЕсли;
	
	// Сортируем таблицу по возрастанию даты производства для корректного распределения материалов
	ТаблицаВыпуска.Сортировать("ДатаПроизводства");
	
	Результат = Новый Структура;
	Результат.Вставить("Таблица", ТаблицаВыпуска);
	Результат.Вставить("КоличествоВыпускаОбщее", КоличествоВыпускаОбщее);
	Результат.Вставить("ПродукцияКонечногоЭтапа", ПродукцияКонечногоЭтапа);
	Результат.Вставить("НазначениеПродукцииЭтапа", НазначениеПродукцииЭтапа);
	Результат.Вставить("ХарактеристикаПродукцииКонечногоЭтапа", ХарактеристикаПродукцииКонечногоЭтапа);
	
    Возврат Результат;
КонецФункции
    
Функция ПолучитьТаблицуМатериалов(ДокОбъект) 

	ГотоваяПродукция = Справочники.ВидыНоменклатуры.НайтиПоНаименованию("1. Готовая продукция");

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|    Мат.Номенклатура       КАК Номенклатура,
	|    Мат.Характеристика     КАК Характеристика,
	|    Мат.Серия              КАК Серия,
	|    СУММА(Мат.Количество)  КАК Количество,
	|    Мат.СтатьяКалькуляции  КАК СтатьяКалькуляции,
	|    Мат.Подразделение      КАК Подразделение,
	|    Мат.Назначение         КАК Назначение,
	|    Мат.ИзмеряетсяВШтуках  КАК ИзмеряетсяВШтуках
	|ИЗ
	|    (
	|        ВЫБРАТЬ
	|            ЭП.Номенклатура                         КАК Номенклатура,
	|            ЭП.Характеристика                       КАК Характеристика,
	|            ЭП.Серия                                КАК Серия,
	|            ЭП.Количество                           КАК Количество,
	|            ЭП.СтатьяКалькуляции                    КАК СтатьяКалькуляции,
	|            ЭП.Подразделение                        КАК Подразделение,
	|            ЭП.Назначение                           КАК Назначение,
	|            ЕСТЬNULL(ЭП.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины = &ТипШтук, ЛОЖЬ) КАК ИзмеряетсяВШтуках
	|        ИЗ
	|            Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ЭП
	|        ГДЕ
	|            &ДокЭтап <> ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка)
	|            И ЭП.Ссылка = &ДокЭтап
	|            И ЭП.ВариантОбеспечения = &Отгрузить
	|            И НЕ ЭП.Номенклатура.ВидНоменклатуры В ИЕРАРХИИ(&ГотоваяПродукция)
	|
	|        ОБЪЕДИНИТЬ ВСЕ
	|
	|        ВЫБРАТЬ
	|            ПБЗ.Номенклатура                         КАК Номенклатура,
	|            ПБЗ.Характеристика                       КАК Характеристика,
	|            ПБЗ.Серия                                КАК Серия,
	|            ПБЗ.Количество                           КАК Количество,
	|            ПБЗ.СтатьяКалькуляции                    КАК СтатьяКалькуляции,
	|            ПБЗ.Подразделение                        КАК Подразделение,
	|            ПБЗ.Назначение                           КАК Назначение,
	|            ЕСТЬNULL(ПБЗ.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины = &ТипШтук, ЛОЖЬ) КАК ИзмеряетсяВШтуках
	|        ИЗ
	|            Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК ПБЗ
	|        ГДЕ
	|            &ДокПБЗ <> ЗНАЧЕНИЕ(Документ.ПроизводствоБезЗаказа.ПустаяСсылка)
	|            И ПБЗ.Ссылка = &ДокПБЗ
	|            И НЕ ПБЗ.Номенклатура.ВидНоменклатуры В ИЕРАРХИИ(&ГотоваяПродукция)
	|    ) КАК Мат
	|СГРУППИРОВАТЬ ПО
	|    Мат.Номенклатура,
	|    Мат.Характеристика,
	|    Мат.Серия,
	|    Мат.СтатьяКалькуляции,
	|    Мат.Подразделение,
	|    Мат.Назначение,
	|    Мат.ИзмеряетсяВШтуках");

    // Определяем тип текущего документа и выставляем параметры
    ЭтоЭтап = ТипЗнч(ДокОбъект.Ссылка) = Тип("ДокументСсылка.ЭтапПроизводства2_2");
    ЭтоПБЗ  = ТипЗнч(ДокОбъект.Ссылка) = Тип("ДокументСсылка.ПроизводствоБезЗаказа");

    Запрос.УстановитьПараметр("ДокЭтап", ?(ЭтоЭтап, ДокОбъект.Ссылка, Документы.ЭтапПроизводства2_2.ПустаяСсылка()));
    Запрос.УстановитьПараметр("ДокПБЗ",  ?(ЭтоПБЗ,  ДокОбъект.Ссылка, Документы.ПроизводствоБезЗаказа.ПустаяСсылка()));

    Запрос.УстановитьПараметр("Отгрузить", Перечисления.ВариантыОбеспечения.Отгрузить);
    Запрос.УстановитьПараметр("ТипШтук",  Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук);
    Запрос.УстановитьПараметр("ГотоваяПродукция", ГотоваяПродукция);

    Результат = Запрос.Выполнить().Выгрузить();

	Возврат Результат;
КонецФункции

// Распределяет количество материала пропорционально строкам выпуска
// Использует метод наименьшего накопленного отклонения (sequential balancing)
// 
// Параметры:
//  ИсходноеКоличество - Число - количество материала для распределения
//  ТабВыпуска - ТаблицаЗначений - таблица выпуска (уже отсортирована по дате производства)
//  КоличествоВыпускаОбщее - Число - общее количество выпуска для расчета пропорций
//  ТребуетсяЦелое - Булево - требуется ли целое число в результате
//
// Возвращаемое значение:
//  Структура с полями:
//    Таблица - ТаблицаЗначений - таблица распределения с колонками "СтрокаВыпуска" и "Количество"
//    Распределено - Число - общее распределенное количество
//
// Алгоритм sequential balancing:
//  1. Для каждой строки вычисляется точное количество по пропорции
//  2. Если требуется целое, берется целая часть, дробная часть накапливается в переменной Отклонение
//  3. Когда накопленное отклонение >= 1, текущая строка получает +1, отклонение уменьшается на 1
//  4. Остаток распределяется автоматически в процессе прохода, без дополнительных сортировок
//  5. Если после цикла остался остаток (из-за округлений), он распределяется циклически
//
// Преимущества метода:
//  - Один проход по строкам (O(n) сложность)
//  - Минимальное использование памяти (одна переменная)
//  - Справедливое распределение остатка по мере прохода
//  - Сохраняет порядок строк (распределение на более ранние даты приоритетно)
Функция ПолучитьРаспределениеПоВыпуску(ИсходноеКоличество, ТабВыпуска, КоличествоВыпускаОбщее, ТребуетсяЦелое)
	
	ТаблицаРаспределения = Новый ТаблицаЗначений;
	ТаблицаРаспределения.Колонки.Добавить("СтрокаВыпуска");
	ТаблицаРаспределения.Колонки.Добавить("Количество");
	
	Распределено = 0;
	
	Если ИсходноеКоличество <= 0 
		ИЛИ КоличествоВыпускаОбщее = 0 
		ИЛИ ТабВыпуска.Количество() = 0 Тогда
		
		Возврат Новый Структура("Таблица, Распределено", ТаблицаРаспределения, Распределено);
	КонецЕсли;
	
	// Метод наименьшего накопленного отклонения (sequential balancing)
	// Накопленное отклонение для автоматического распределения остатка
	// Таблица выпуска уже отсортирована по дате производства в функции ПолучитьТаблицуВыпуска
	Отклонение = 0;
	
	// Проход по строкам: вычисляем целые части и автоматически распределяем остаток
	Для Индекс = 0 По ТабВыпуска.Количество() - 1 Цикл
		СтрокаВыпуска = ТабВыпуска.Получить(Индекс);
		КоэффициентКоличества = ?(КоличествоВыпускаОбщее = 0, 0, СтрокаВыпуска.Количество / КоличествоВыпускаОбщее);
		КоличествоДляСтроки = ИсходноеКоличество * КоэффициентКоличества;
		
		Если КоличествоДляСтроки <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТребуетсяЦелое Тогда
			// Добавляем дробную часть к накопленному отклонению
			Отклонение = Отклонение + (КоличествоДляСтроки - Цел(КоличествоДляСтроки));
			КоличествоДляСтроки = Цел(КоличествоДляСтроки);
			
			// Если накопленное отклонение >= 1, увеличиваем количество на 1
			Если Отклонение >= 1 Тогда
				КоличествоДляСтроки = КоличествоДляСтроки + 1;
				Отклонение = Отклонение - 1;
			КонецЕсли;
		КонецЕсли;
		
		Если КоличествоДляСтроки <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаРаспределения.Добавить();
		НоваяСтрока.СтрокаВыпуска = СтрокаВыпуска;
		НоваяСтрока.Количество = КоличествоДляСтроки;
		
		Распределено = Распределено + КоличествоДляСтроки;
	КонецЦикла;
	
	// Вычисляем остаток (должен быть минимальным или нулевым после sequential balancing)
	Остаток = ИсходноеКоличество - Распределено;
	Если ТребуетсяЦелое Тогда
		Остаток = Цел(Остаток);
	КонецЕсли;
	
	// Если остался остаток (из-за округлений или если не требуется целое), распределяем его
	Если Остаток > 0 Тогда
		Если ТаблицаРаспределения.Количество() = 0 Тогда
			// Если нет ни одной строки с положительным количеством, добавляем первую строку выпуска
			СтрокаВыпуска = ТабВыпуска.Получить(0);
			НоваяСтрока = ТаблицаРаспределения.Добавить();
			НоваяСтрока.СтрокаВыпуска = СтрокаВыпуска;
			НоваяСтрока.Количество = 0;
			ТаблицаРаспределения[0].Количество = ТаблицаРаспределения[0].Количество + Остаток;
			Распределено = Распределено + Остаток;
		Иначе
			// Распределяем остаток циклически по строкам
			ИндексСтроки = 0;
			Пока Остаток > 0 Цикл
				ТаблицаРаспределения[ИндексСтроки].Количество = ТаблицаРаспределения[ИндексСтроки].Количество + 1;
				Распределено = Распределено + 1;
				Остаток = Остаток - 1;
				ИндексСтроки = (ИндексСтроки + 1) % ТаблицаРаспределения.Количество();
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Новый Структура("Таблица, Распределено", ТаблицаРаспределения, Распределено);
КонецФункции

Функция СоздатьТаблицуНормативногоСостава()
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Материал");
	Таблица.Колонки.Добавить("ХарМат");
	Таблица.Колонки.Добавить("СерМат");
	Таблица.Колонки.Добавить("НазМат");
	Таблица.Колонки.Добавить("Статья");
	Таблица.Колонки.Добавить("Подразделение");
	Таблица.Колонки.Добавить("ИзмеряетсяВШтуках");
	Таблица.Колонки.Добавить("Удельное");
	Таблица.Колонки.Добавить("Продукция");
	
	Возврат Таблица;
	
КонецФункции

Процедура ДобавитьНормативныйСоставДоПокупныхВТаблицу(Номенклатура, ДатаАктуальности, Коэффициент, ТаблицаНормативногоСостава, СтекНоменклатуры, Спецификация = Неопределено)
	
	Если Коэффициент <= 0 Или Номенклатура = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтекНоменклатуры.Получить(Номенклатура) = Истина Тогда
		Возврат;
	КонецЕсли;
	СтекНоменклатуры.Вставить(Номенклатура, Истина);
	
	// Проверяем, не является ли материал вспомогательным
	РеквизитВспомогательныйМатериал = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ВспомогательныйМатериал_a6ab234c1428470d81365a2df40445b9");
	Если РеквизитВспомогательныйМатериал <> Неопределено Тогда
		ЗначениеРеквизитаВспомогательныйМатериал = УправлениеСвойствами.ЗначениеСвойства(Номенклатура, РеквизитВспомогательныйМатериал);
		Если ЗначениеРеквизитаВспомогательныйМатериал = Истина Тогда
			СтекНоменклатуры.Удалить(Номенклатура);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Спецификация = Неопределено Тогда
		Спецификация = ПриоритетнаяСпецификация(Номенклатура, ДатаАктуальности);
	КонецЕсли;
	
	Если Спецификация = Неопределено Тогда
		СтрокаНормативногоСостава = ТаблицаНормативногоСостава.Добавить();
		СтрокаНормативногоСостава.Материал = Номенклатура;
		СтрокаНормативногоСостава.ХарМат = Неопределено;
		СтрокаНормативногоСостава.СерМат = Неопределено;
		СтрокаНормативногоСостава.НазМат = Неопределено;
		СтрокаНормативногоСостава.Статья = Неопределено;
		СтрокаНормативногоСостава.Подразделение = Неопределено;
		СтрокаНормативногоСостава.ИзмеряетсяВШтуках = Номенклатура.ЕдиницаИзмерения <> Неопределено И Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук;
		СтрокаНормативногоСостава.Удельное = Коэффициент;
		СтрокаНормативногоСостава.Продукция = Номенклатура;
		СтекНоменклатуры.Удалить(Номенклатура);
		Возврат;
	КонецЕсли;
	
	// Получаем количество выпускаемых изделий из спецификации
	// Используем ОсновноеИзделиеКоличествоУпаковок или сумму из ВыходныеИзделия
	ЗапросКоличестваВыпуска = Новый Запрос;
	ЗапросКоличестваВыпуска.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Спецификация.ОсновноеИзделиеКоличествоУпаковок КАК КоличествоВыпуска
	|ИЗ
	|	Справочник.РесурсныеСпецификации КАК Спецификация
	|ГДЕ
	|	Спецификация.Ссылка = &Спецификация
	|	И Спецификация.ОсновноеИзделиеКоличествоУпаковок > 0";
	
	ЗапросКоличестваВыпуска.УстановитьПараметр("Спецификация", Спецификация);
	РезультатКоличестваВыпуска = ЗапросКоличестваВыпуска.Выполнить();
	КоличествоВыпускаИзделий = 0;
	ВыборкаКоличества = РезультатКоличестваВыпуска.Выбрать();
	Если ВыборкаКоличества.Следующий() Тогда
		КоличествоВыпускаИзделий = ВыборкаКоличества.КоличествоВыпуска;
	КонецЕсли;
	
	// Если ОсновноеИзделиеКоличествоУпаковок не заполнено, получаем сумму из ВыходныеИзделия
	Если КоличествоВыпускаИзделий <= 0 Тогда
		ЗапросКоличестваВыпуска.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ВыходныеИзделия.КоличествоУпаковок) КАК КоличествоВыпуска
		|ИЗ
		|	Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК ВыходныеИзделия
		|ГДЕ
		|	ВыходныеИзделия.Ссылка = &Спецификация";
		
		РезультатКоличестваВыпуска = ЗапросКоличестваВыпуска.Выполнить();
		ВыборкаКоличества = РезультатКоличестваВыпуска.Выбрать();
		Если ВыборкаКоличества.Следующий() Тогда
			Если ВыборкаКоличества.КоличествоВыпуска > 0 Тогда
				КоличествоВыпускаИзделий = ВыборкаКоличества.КоличествоВыпуска;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Если ничего не найдено, используем 1 по умолчанию
	Если КоличествоВыпускаИзделий <= 0 Тогда
		КоличествоВыпускаИзделий = 1;
	КонецЕсли;
	
	РеквизитВспомогательныйМатериал = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ВспомогательныйМатериал_a6ab234c1428470d81365a2df40445b9");
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МатериалыИУслуги.Номенклатура                КАК Материал,
	|	МатериалыИУслуги.Характеристика              КАК ХарМат,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК СерМат,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК НазМат,
	|	МатериалыИУслуги.СтатьяКалькуляции           КАК Статья,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	МатериалыИУслуги.КоличествоУпаковок                  КАК Количество,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(МатериалыИУслуги.ИсточникПолученияПолуфабриката КАК Справочник.РесурсныеСпецификации).Ссылка, ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)) КАК ИсточникПолученияПолуфабрикатаСсылка,
	|	МатериалыИУслуги.СпособПолученияМатериала    КАК СпособПолучения,
	|	ЕСТЬNULL(МатериалыИУслуги.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины = &ТипШтук, ЛОЖЬ) КАК ИзмеряетсяВШтуках
	|ИЗ
	|	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК МатериалыИУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК ДопРекв
	|		ПО ДопРекв.Ссылка = МатериалыИУслуги.Номенклатура
	|			И ДопРекв.Свойство = &РеквизитВспомогательныйМатериал
	|ГДЕ
	|	МатериалыИУслуги.Ссылка = &Спецификация
	|	И МатериалыИУслуги.СпособПолученияМатериала <> &СпособПолученияПроизводитсяНаЭтапе
	|	И (ДопРекв.Значение ЕСТЬ NULL
	|		ИЛИ ДопРекв.Значение <> ИСТИНА)");
	
	Запрос.УстановитьПараметр("Спецификация", Спецификация);
	Запрос.УстановитьПараметр("ТипШтук", Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук);
	Запрос.УстановитьПараметр("СпособПолученияПроизводитсяНаЭтапе", Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе);
	Запрос.УстановитьПараметр("РеквизитВспомогательныйМатериал", РеквизитВспомогательныйМатериал);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Материал = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// Количество материала в спецификации указано на партию выпуска
		// Нужно разделить на количество выпускаемых изделий, чтобы получить удельный расход на единицу
		// Затем умножить на коэффициент (который уже учитывает количество родительского изделия)
		Если КоличествоВыпускаИзделий > 0 Тогда
			КоличествоСтроки = Выборка.Количество / КоличествоВыпускаИзделий * Коэффициент;
		Иначе
			КоличествоСтроки = Выборка.Количество * Коэффициент;
		КонецЕсли;
		
		Если КоличествоСтроки <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СпецификацияМатериала = Выборка.ИсточникПолученияПолуфабрикатаСсылка;
		Если СпецификацияМатериала = Справочники.РесурсныеСпецификации.ПустаяСсылка() Тогда
			//СпецификацияМатериала = ПриоритетнаяСпецификация(Выборка.Материал, ДатаАктуальности); убрал временно, пока есть ошибки заполнения РС
			СпецификацияМатериала = ПриоритетнаяСпецификация(Выборка.Материал);
		КонецЕсли;
		
		Если СпецификацияМатериала = Неопределено Тогда
			СтрокаНормативногоСостава = ТаблицаНормативногоСостава.Добавить();
			СтрокаНормативногоСостава.Материал = Выборка.Материал;
			СтрокаНормативногоСостава.ХарМат = Выборка.ХарМат;
			СтрокаНормативногоСостава.СерМат = Выборка.СерМат;
			СтрокаНормативногоСостава.НазМат = Выборка.НазМат;
			СтрокаНормативногоСостава.Статья = Выборка.Статья;
			СтрокаНормативногоСостава.Подразделение = Выборка.Подразделение;
			СтрокаНормативногоСостава.ИзмеряетсяВШтуках = Выборка.ИзмеряетсяВШтуках;
			СтрокаНормативногоСостава.Удельное = КоличествоСтроки;
			СтрокаНормативногоСостава.Продукция = Номенклатура;
		Иначе
			ДобавитьНормативныйСоставДоПокупныхВТаблицу(Выборка.Материал, ДатаАктуальности, КоличествоСтроки, ТаблицаНормативногоСостава, СтекНоменклатуры, СпецификацияМатериала);
		КонецЕсли;
		
	КонецЦикла;
	
	СтекНоменклатуры.Удалить(Номенклатура);
	
КонецПроцедуры

Функция ПолучитьНормативныйСоставДоПокупных(Номенклатура, ДатаАктуальности = Неопределено)
	
	Если ДатаАктуальности = Неопределено Тогда
		ДатаАктуальности = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ТаблицаНормативногоСостава = СоздатьТаблицуНормативногоСостава();
	СтекНоменклатуры = Новый Соответствие;
	ДобавитьНормативныйСоставДоПокупныхВТаблицу(Номенклатура, ДатаАктуальности, 1, ТаблицаНормативногоСостава, СтекНоменклатуры);
	
	Если ТаблицаНормативногоСостава.Количество() = 0 Тогда
		Возврат ТаблицаНормативногоСостава;
	КонецЕсли;
	
	// Выполняем свертку - используем строки с запятыми, как в примерах проекта
	// Продукция добавляется в ключи, чтобы она сохранилась при свертке
	ТаблицаНормативногоСостава.Свернуть("Материал, ХарМат, СерМат, НазМат, Статья, Подразделение, ИзмеряетсяВШтуках, Продукция", "Удельное");
	Возврат ТаблицаНормативногоСостава;
	
КонецФункции

Процедура ОбновитьМатСоставИзделийПоДокументу(ДокОбъект, Отказ) Экспорт   
	Перем Расш1_РежимЗаписи;
	
	Если ДокОбъект = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	Набор = РегистрыНакопления.А1_МатСоставИзделий_Расш1.СоздатьНаборЗаписей();
	Набор.Отбор.Регистратор.Установить(ДокОбъект.Ссылка);    
	
	Если НЕ ДокОбъект.ДополнительныеСвойства.Свойство("Расш1_РежимЗаписи", Расш1_РежимЗаписи) Тогда 
		Возврат; 
	КонецЕсли;	 

        Если Расш1_РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
                Набор.Записать();
                Возврат;
		КонецЕсли;		
        Если Расш1_РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
                Возврат;
		КонецЕсли; 	
		
	ЗаказНаПроизводство = Документы.ЗаказНаПроизводство2_2.ПустаяСсылка(); 
	ДокументОснование = Документы.ЗаказКлиента.ПустаяСсылка(); 
	
	Если (ЕстьРеквизитИлиСвойствоОбъекта(ДокОбъект, "Распоряжение")) Тогда       
		
		ЗаказНаПроизводство = ?(ЗначениеЗаполнено(ДокОбъект.Распоряжение), ДокОбъект.Распоряжение, Документы.ЗаказНаПроизводство2_2.ПустаяСсылка());    		
		ДокументОснование = ?(ЗначениеЗаполнено(ЗаказНаПроизводство.ДокументОснование), ЗаказНаПроизводство.ДокументОснование, Документы.ЗаказКлиента.ПустаяСсылка());
		
		Если (ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказКлиента")) Тогда				
			Договор = ?(ЗначениеЗаполнено(ДокументОснование.Договор), ДокументОснование.Договор, Справочники.ДоговорыКонтрагентов.ПустаяСсылка()); 			
			ИнформацияДляПечати = ?(ЗначениеЗаполнено(ДокументОснование.ДополнительнаяИнформация), ДокументОснование.ДополнительнаяИнформация, "");			
			Если ЗначениеЗаполнено(ЗаказНаПроизводство.НаправлениеДеятельности) Тогда
				НаправлениеДеятельности = ЗаказНаПроизводство.НаправлениеДеятельности;
			ИначеЕсли ЗначениеЗаполнено(ДокументОснование.НаправлениеДеятельности) Тогда
				НаправлениеДеятельности = ДокументОснование.НаправлениеДеятельности;
			Иначе
				НаправлениеДеятельности = Справочники.НаправленияДеятельности.ПустаяСсылка();
			КонецЕсли;
		Иначе
			Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка(); 			
			ИнформацияДляПечати = "";			
			НаправлениеДеятельности = Справочники.НаправленияДеятельности.ПустаяСсылка(); 			
		КонецЕсли;
		
	КонецЕсли;		
	
    РезультатВыпуска = ПолучитьТаблицуВыпуска(ДокОбъект);
	ТабВыпуска = РезультатВыпуска.Таблица;
	Если НЕ ТабВыпуска.Количество() > 0 Тогда 
		Возврат; 
	КонецЕсли;

    ТабМатериалов = ПолучитьТаблицуМатериалов(ДокОбъект); 
    Если НЕ ТабМатериалов.Количество() > 0 Тогда 
		Возврат; 
	КонецЕсли;

	КоличествоВыпускаОбщее = РезультатВыпуска.КоличествоВыпускаОбщее;
	ПродукцияКонечногоЭтапа = РезультатВыпуска.ПродукцияКонечногоЭтапа;
	НазначениеПродукцииЭтапа = РезультатВыпуска.НазначениеПродукцииЭтапа;
	ХарактеристикаПродукцииКонечногоЭтапа = РезультатВыпуска.ХарактеристикаПродукцииКонечногоЭтапа;  
	
	Для Каждого СтрМат Из ТабМатериалов Цикл  
		
			РаспределениеМатериала = ПолучитьРаспределениеПоВыпуску(
				СтрМат.Количество,
				ТабВыпуска,
				КоличествоВыпускаОбщее,
				СтрМат.ИзмеряетсяВШтуках
			);
			
			Если РаспределениеМатериала.Таблица.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Подразделение = СтрМат.Подразделение;
			Материал = СтрМат.Номенклатура;
			ХарактеристикаМатериала = ?(ЗначениеЗаполнено(СтрМат.Характеристика), СтрМат.Характеристика, Неопределено);
			НазначениеМатериала = ?(ЗначениеЗаполнено(СтрМат.Назначение), СтрМат.Назначение, Неопределено);
			СерияМатериала = ?(ЗначениеЗаполнено(СтрМат.Серия), СтрМат.Серия, Неопределено);
			СтатьяКалькуляцииМатериала = СтрМат.СтатьяКалькуляции;

			Для Каждого СтрокаРаспределения Из РаспределениеМатериала.Таблица Цикл
				
				СтрВып = СтрокаРаспределения.СтрокаВыпуска;
				КоличествоМатКРаспределению = СтрокаРаспределения.Количество;
				
				Если НЕ КоличествоМатКРаспределению > 0 Тогда 
					Продолжить; 
				КонецЕсли;
						
				Запись = Набор.Добавить();
				Запись.Период = ДокОбъект.Дата;
				Запись.Регистратор = ДокОбъект.Ссылка; 
				Запись.Сторно                      = Ложь;
				Запись.КорректируемыйРегистратор   = Неопределено;
				Запись.ДатаПроизводства = СтрВып.ДатаПроизводства;
				Запись.СкладПолучатель = СтрВып.Получатель;  
				Запись.Подразделение = Подразделение;
				
				Запись.ПродукцияКонечногоЭтапа = ПродукцияКонечногоЭтапа;
				Запись.ХарактеристикаПродукцииКонечногоЭтапа = ХарактеристикаПродукцииКонечногоЭтапа;
				Запись.НазначениеПродукцииКонечногоЭтапа = НазначениеПродукцииЭтапа; 				         					
								
				Запись.Продукция = ПродукцияКонечногоЭтапа;
				Запись.ХарактеристикаПродукции = ХарактеристикаПродукцииКонечногоЭтапа;
				Запись.НазначениеПродукции = НазначениеПродукцииЭтапа; 
				Запись.СерияПродукции = ?(ЗначениеЗаполнено(СтрВып.Серия), СтрВып.Серия, Неопределено);
				
				Запись.Материал = Материал;
				Запись.ХарактеристикаМатериала = ХарактеристикаМатериала;
				Запись.НазначениеМатериала = НазначениеМатериала;
				Запись.СерияМатериала = СерияМатериала;
				Запись.СтатьяКалькуляцииМатериала = СтатьяКалькуляцииМатериала;
				Запись.ЗаказНаПроизводство = ЗаказНаПроизводство;
				Запись.НаправлениеДеятельности = НаправлениеДеятельности;
				Запись.Договор = Договор;
                Запись.ИнформацияДляПечати = ИнформацияДляПечати;
                Запись.ГруппаКоррекции             = Неопределено;				
				Если Набор.Метаданные().Реквизиты.Найти("ПоНормам") <> Неопределено Тогда
					Запись.ПоНормам = Ложь;
				КонецЕсли;
							

				Запись.КоличествоМатериала = КоличествоМатКРаспределению;
				Запись.КоличествоПродукции = КоличествоВыпускаОбщее; 
				Запись.КоличествоПродукцииКонечногоЭтапа = КоличествоВыпускаОбщее;
				
			КонецЦикла; 
			
		КонецЦикла;
		
		
	Набор.Записать(); 

	// Развертка полуфабрикатов перенесена в обработку перепроведения (ПерепроведениеЭтаповПоДатамПроизводства)
	// При первом проведении этапа заполняются только начальные данные,
	// развертка выполняется при перепроведении, когда все этапы партии уже проведены
	//РазвернутьПолуфабрикатыПокаЕсть(ДокОбъект, НазначениеПродукцииЭтапа, Договор, НаправлениеДеятельности, ИнформацияДляПечати, РезультатВыпуска);
	//РазвернутьПолуфабрикатПоНормам(ДокОбъект, НазначениеПродукцииЭтапа, Договор, НаправлениеДеятельности, ИнформацияДляПечати, РезультатВыпуска);

КонецПроцедуры

// Выполняет развертку полуфабрикатов для документа "Этап производства".
// Вызывается из обработки перепроведения после того, как все этапы партии проведены.
// Параметры:
//  ДокОбъект - ДокументОбъект.ЭтапПроизводства2_2 - объект документа этапа производства
Процедура РазвернутьПолуфабрикатыДокумента(ДокОбъект) Экспорт
	
	Если ДокОбъект = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	// Получаем данные о выпуске
	РезультатВыпуска = ПолучитьТаблицуВыпуска(ДокОбъект);
	ТабВыпуска = РезультатВыпуска.Таблица;
	Если НЕ ТабВыпуска.Количество() > 0 Тогда 
		Возврат; 
	КонецЕсли;
	
	НазначениеПродукцииЭтапа = РезультатВыпуска.НазначениеПродукцииЭтапа;
	
	// Получаем параметры из заказа на производство
	ЗаказНаПроизводство = Документы.ЗаказНаПроизводство2_2.ПустаяСсылка(); 
	ДокументОснование = Документы.ЗаказКлиента.ПустаяСсылка(); 
	Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	ИнформацияДляПечати = "";
	НаправлениеДеятельности = Справочники.НаправленияДеятельности.ПустаяСсылка();
	
	Если ЕстьРеквизитИлиСвойствоОбъекта(ДокОбъект, "Распоряжение") Тогда       
		ЗаказНаПроизводство = ?(ЗначениеЗаполнено(ДокОбъект.Распоряжение), ДокОбъект.Распоряжение, Документы.ЗаказНаПроизводство2_2.ПустаяСсылка());    		
		ДокументОснование = ?(ЗначениеЗаполнено(ЗаказНаПроизводство.ДокументОснование), ЗаказНаПроизводство.ДокументОснование, Документы.ЗаказКлиента.ПустаяСсылка());
		
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказКлиента") Тогда				
			Договор = ?(ЗначениеЗаполнено(ДокументОснование.Договор), ДокументОснование.Договор, Справочники.ДоговорыКонтрагентов.ПустаяСсылка()); 			
			ИнформацияДляПечати = ?(ЗначениеЗаполнено(ДокументОснование.ДополнительнаяИнформация), ДокументОснование.ДополнительнаяИнформация, "");			
			Если ЗначениеЗаполнено(ЗаказНаПроизводство.НаправлениеДеятельности) Тогда
				НаправлениеДеятельности = ЗаказНаПроизводство.НаправлениеДеятельности;
			ИначеЕсли ЗначениеЗаполнено(ДокументОснование.НаправлениеДеятельности) Тогда
				НаправлениеДеятельности = ДокументОснование.НаправлениеДеятельности;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Выполняем развертку полуфабрикатов
	РазвернутьПолуфабрикатыПокаЕсть(ДокОбъект, НазначениеПродукцииЭтапа, Договор, НаправлениеДеятельности, ИнформацияДляПечати, РезультатВыпуска);
	РазвернутьПолуфабрикатПоНормам(ДокОбъект, НазначениеПродукцииЭтапа, Договор, НаправлениеДеятельности, ИнформацияДляПечати, РезультатВыпуска);
	
КонецПроцедуры

// Создает движения в регистре А1_МатСоставИзделий_Расш1 для документа "Внутреннее потребление"
// при списании на расходы. Сторнирует записи о выпуске товаров, указанных в документе.
// Параметры:
//  ДокОбъект - ДокументОбъект - объект документа внутреннего потребления
//  Отказ - Булево - признак отказа в выполнении операции
Процедура ОбновитьМатСоставИзделийПоВнутреннемуПотреблению(ДокОбъект, Отказ) Экспорт
	Перем Расш1_РежимЗаписи;
	
	Если ДокОбъект = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	// Проверяем, что это документ "Внутреннее потребление"
	Если ТипЗнч(ДокОбъект.Ссылка) <> Тип("ДокументСсылка.ВнутреннееПотребление") Тогда
		Возврат;
	КонецЕсли;
	
	Набор = РегистрыНакопления.А1_МатСоставИзделий_Расш1.СоздатьНаборЗаписей();
	Набор.Отбор.Регистратор.Установить(ДокОбъект.Ссылка);
	
	Если НЕ ДокОбъект.ДополнительныеСвойства.Свойство("Расш1_РежимЗаписи", Расш1_РежимЗаписи) Тогда 
		Возврат; 
	КонецЕсли;
	
	Если Расш1_РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Набор.Записать();
		Возврат;
	ИначеЕсли Расш1_РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Возврат;
	//ИначеЕсли Расш1_РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		// Продолжаем обработку
	//Иначе
	//	Возврат;
	КонецЕсли;
	
	// Получаем товары из табличной части "Товары"
	Если ДокОбъект.Товары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Собираем все товары для одного запроса
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("Номенклатура");
	ТаблицаТоваров.Колонки.Добавить("Характеристика");
	ТаблицаТоваров.Колонки.Добавить("Серия");
	ТаблицаТоваров.Колонки.Добавить("Назначение");
	ТаблицаТоваров.Колонки.Добавить("Количество");
	
	Для Каждого СтрокаТовара Из ДокОбъект.Товары Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТовара.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		НоваяСтрока.Номенклатура = СтрокаТовара.Номенклатура;
		НоваяСтрока.Характеристика = ?(ЗначениеЗаполнено(СтрокаТовара.Характеристика), СтрокаТовара.Характеристика, Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
		НоваяСтрока.Серия = ?(ЗначениеЗаполнено(СтрокаТовара.Серия), СтрокаТовара.Серия, Справочники.СерииНоменклатуры.ПустаяСсылка());
		НоваяСтрока.Назначение = ?(ЗначениеЗаполнено(СтрокаТовара.Назначение), СтрокаТовара.Назначение, Справочники.Назначения.ПустаяСсылка());
		НоваяСтрока.Количество = СтрокаТовара.Количество;
	КонецЦикла;
	
	Если ТаблицаТоваров.Количество() = 0 Тогда
		Набор.Записать();
		Возврат;
	КонецЕсли;
	
	// Ищем записи о выпуске всех товаров в регистре одним запросом
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Рег.Регистратор КАК Регистратор,
	|	Рег.Период КАК Период,
	|	Рег.Продукция КАК Продукция,
	|	Рег.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	Рег.СерияПродукции КАК СерияПродукции,
	|	Рег.НазначениеПродукции КАК НазначениеПродукции,
	|	Рег.ПродукцияКонечногоЭтапа КАК ПродукцияКонечногоЭтапа,
	|	Рег.ХарактеристикаПродукцииКонечногоЭтапа КАК ХарактеристикаПродукцииКонечногоЭтапа,
	|	Рег.НазначениеПродукцииКонечногоЭтапа КАК НазначениеПродукцииКонечногоЭтапа,
	|	Рег.Материал КАК Материал,
	|	Рег.ХарактеристикаМатериала КАК ХарактеристикаМатериала,
	|	Рег.СерияМатериала КАК СерияМатериала,
	|	Рег.НазначениеМатериала КАК НазначениеМатериала,
	|	Рег.СтатьяКалькуляцииМатериала КАК СтатьяКалькуляцииМатериала,
	|	Рег.Подразделение КАК Подразделение,
	|	Рег.КоличествоПродукции КАК КоличествоПродукции,
	|	Рег.КоличествоПродукцииКонечногоЭтапа КАК КоличествоПродукцииКонечногоЭтапа,
	|	Рег.КоличествоМатериала КАК КоличествоМатериала,
	|	Рег.ДатаПроизводства КАК ДатаПроизводства,
	|	Рег.СкладПолучатель КАК СкладПолучатель,
	|	Рег.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	Рег.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Рег.Договор КАК Договор,
	|	Рег.ИнформацияДляПечати КАК ИнформацияДляПечати,
	|	Рег.ГруппаКоррекции КАК ГруппаКоррекции,
	|	Рег.ПоНормам КАК ПоНормам
	|ИЗ
	|	РегистрНакопления.А1_МатСоставИзделий_Расш1 КАК Рег
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ &ТаблицаТоваров КАК Товары
	|		ПО Рег.Продукция = Товары.Номенклатура
	|			И ЕСТЬNULL(Рег.ХарактеристикаПродукции, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|				= ЕСТЬNULL(Товары.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|			И ЕСТЬNULL(Рег.СерияПродукции, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
	|				= ЕСТЬNULL(Товары.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
	|			И ЕСТЬNULL(Рег.НазначениеПродукции, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
	|				= ЕСТЬNULL(Товары.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
	|ГДЕ
	|	Рег.Сторно = ЛОЖЬ
	|	И Рег.Активность
	|	И Рег.КоличествоПродукции > 0
	|УПОРЯДОЧИТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	Товары.Назначение,
	|	Рег.Период,
	|	Рег.Регистратор";
	
	Запрос.УстановитьПараметр("ТаблицаТоваров", ТаблицаТоваров);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	// Создаем соответствие для отслеживания остатков по товарам
	ОстаткиПоТоварам = Новый Соответствие;
	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		КлючТовара = СтрШаблон("%1|%2|%3|%4",
			Строка(СтрокаТовара.Номенклатура),
			Строка(СтрокаТовара.Характеристика),
			Строка(СтрокаТовара.Серия),
			Строка(СтрокаТовара.Назначение));
		ОстаткиПоТоварам.Вставить(КлючТовара, СтрокаТовара.Количество);
	КонецЦикла;
	
	Пока Выборка.Следующий() Цикл
		
		// Формируем ключ товара для поиска остатка
		КлючТовара = СтрШаблон("%1|%2|%3|%4",
			Строка(Выборка.Продукция),
			Строка(?(ЗначениеЗаполнено(Выборка.ХарактеристикаПродукции), Выборка.ХарактеристикаПродукции, Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка())),
			Строка(?(ЗначениеЗаполнено(Выборка.СерияПродукции), Выборка.СерияПродукции, Справочники.СерииНоменклатуры.ПустаяСсылка())),
			Строка(?(ЗначениеЗаполнено(Выборка.НазначениеПродукции), Выборка.НазначениеПродукции, Справочники.Назначения.ПустаяСсылка())));
		
		ОстатокКоличества = ОстаткиПоТоварам.Получить(КлючТовара);
		Если ОстатокКоличества = Неопределено ИЛИ ОстатокКоличества <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Определяем количество для сторнирования
		КоличествоДляСторно = Мин(ОстатокКоличества, Выборка.КоличествоПродукции);
		
		Если КоличествоДляСторно <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Коэффициент для пропорционального сторнирования материалов
		Коэффициент = ?(Выборка.КоличествоПродукции > 0, КоличествоДляСторно / Выборка.КоличествоПродукции, 0);
		
		// Создаем сторнирующую запись
		КлючПФ = КлючПФ(
			Выборка.Продукция,
			Выборка.ХарактеристикаПродукции,
			Выборка.СерияПродукции,
			Выборка.НазначениеПродукции,
			Выборка.СтатьяКалькуляцииМатериала
		);
		
		ДобавитьЗаписьМатСост(
			Набор,
			ДокОбъект.Дата,
			ДокОбъект.Ссылка,
			КлючПФ,
			Выборка.Материал,
			Выборка.ХарактеристикаМатериала,
			Выборка.СерияМатериала,
			Выборка.НазначениеМатериала,
			Выборка.СтатьяКалькуляцииМатериала,
			Выборка.Подразделение,
			-КоличествоДляСторно,
			-Выборка.КоличествоМатериала * Коэффициент,
			Истина, // Сторно
			Выборка.Регистратор, // КорректируемыйРегистратор
			Выборка.ГруппаКоррекции,
			Неопределено,
			Выборка.Продукция,
			Выборка.Договор,
			Выборка.НаправлениеДеятельности,
			Выборка.ИнформацияДляПечати,
			Выборка.ПродукцияКонечногоЭтапа,
			-Выборка.КоличествоПродукцииКонечногоЭтапа * Коэффициент,
			Выборка.ХарактеристикаПродукцииКонечногоЭтапа,
			Выборка.НазначениеПродукцииКонечногоЭтапа,
			Выборка.СкладПолучатель,
			Выборка.ДатаПроизводства,
			Выборка.ПоНормам
		);
		
		// Уменьшаем остаток
		ОстаткиПоТоварам.Установить(КлючТовара, ОстатокКоличества - КоличествоДляСторно);
		
	КонецЦикла;
	
	Набор.Записать();
	
КонецПроцедуры

Процедура РазвернутьПолуфабрикатыПокаЕсть(ДокОбъект, НазначениеПродукцииЭтапа, Договор, НаправлениеДеятельности, ИнформацияДляПечати, РезультатВыпуска)                   
		
        МинимальноеУменьшение = 0.001;
        ПредыдущиеКандидаты = Новый Соответствие;
        ПредыдущиеДанные = Неопределено;
        ПропущенныеКандидаты = Новый Соответствие;

    // Ищем первого кандидата, разворачиваем, повторяем
    Пока Истина Цикл  
        
		Кандидат = НайтиКандидатаПолуфабриката(ДокОбъект.Ссылка, ПропущенныеКандидаты);
		Если Кандидат = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		// Расчёт возможного объёма разворота = min(чистый расход в доке, сумма чистых выпусков источников)
        Источники = Кандидат.Источники; // Таблица с полями: ИсточникРег, Номенклатура, Характеристика, Серия, Назначение, ЧистыйВыпуск
        СуммаИст = 0;
        Для Каждого Стр ИЗ Источники Цикл
            СуммаИст = СуммаИст + Стр.ЧистыйВыпуск;
        КонецЦикла;

        Требуется = Мин(Кандидат.ЧистыйРасходВДоке, СуммаИст);
        Если Требуется < МинимальноеУменьшение Тогда
            Сообщить("Развертка ПФ: остаток менее " + Формат(МинимальноеУменьшение, "ЧРД=.; ЧГ=3") + ". Документ " + Строка(ДокОбъект.Ссылка));
            Прервать;
        КонецЕсли;

        Ключ = Кандидат.Ключ;
        КлючСтрока = СтрШаблон("%1|%2|%3|%4|%5",
            Строка(Ключ.Номенклатура),
            Строка(Ключ.Характеристика),
            Строка(Ключ.Серия),
            Строка(Ключ.Назначение),
            Строка(Ключ.Статья));

        ПредыдущиеДанные = ПредыдущиеКандидаты.Получить(КлючСтрока);
        Если ПредыдущиеДанные <> Неопределено Тогда
            СтароеТребуется = ПредыдущиеДанные.Требуется;
            СтароеСуммаИст = ПредыдущиеДанные.СуммаИст;
            Если (СтароеТребуется - Требуется) < МинимальноеУменьшение И Окр(СтароеСуммаИст - СуммаИст, 3) = 0 Тогда
                Сообщить("Развертка ПФ: нет прогресса для " + Строка(Ключ.Номенклатура) + ". Документ " + Строка(ДокОбъект.Ссылка) + ". Пропускаем и продолжаем.");
                ПропущенныеКандидаты.Вставить(КлючСтрока, Истина);
                Продолжить;
            КонецЕсли;
        КонецЕсли;

        ПредыдущиеКандидаты.Вставить(КлючСтрока, Новый Структура("Требуется, СуммаИст", Требуется, СуммаИст));
		
		
        РазвернутьПолуфабрикат(
			ДокОбъект, 
			Кандидат.Ключ, 
			Требуется, 
			Источники, 
			НазначениеПродукцииЭтапа, 
			Договор, 
			НаправлениеДеятельности, 
			ИнформацияДляПечати,
			РезультатВыпуска
		);
        // После записи - следующий цикл уже увидит обновлённые «чистые» остатки
	КонецЦикла; 
	
КонецПроцедуры

// Разворачивает ПФ на количество Требуется, распределяя по источникам пропорционально их ЧистомуВыпуску
// Параметры:
//  ДокОбъект - ДокументОбъект - объект документа этапа производства
//  КлючПФ - Структура - структура с полями: Номенклатура, Характеристика, Серия, Назначение, Статья
//  Требуется - Число - количество полуфабриката для разворота
//  ТабИсточников - ТаблицаЗначений - таблица источников с полями: ИсточникРег, Номенклатура, 
//	Характеристика, Серия, Назначение, ЧистыйВыпуск
//  НазначениеПродукцииЭтапа - СправочникСсылка.Назначения - назначение продукции конечного этапа
//  Договор - СправочникСсылка.ДоговорыКонтрагентов - договор для записей регистра
//  НаправлениеДеятельности - СправочникСсылка.НаправленияДеятельности - направление деятельности для записей регистра
//  ИнформацияДляПечати - Строка - дополнительная информация для печати
//  РезультатВыпуска - Структура - структура с полями: Таблица, КоличествоВыпускаОбщее, ПродукцияКонечногоЭтапа,
//	НазначениеПродукцииЭтапа, ХарактеристикаПродукцииКонечногоЭтапа
Процедура РазвернутьПолуфабрикат(
	ДокОбъект, 
	КлючПФ, 
	Требуется, 
	ТабИсточников, 
	НазначениеПродукцииЭтапа, 
	Договор, 
	НаправлениеДеятельности, 
	ИнформацияДляПечати,
	РезультатВыпуска
	)
	
	ТабВыпуска = РезультатВыпуска.Таблица;
	ПродукцияКонечногоЭтапа = РезультатВыпуска.ПродукцияКонечногоЭтапа;
	НазначениеПродукцииЭтапа = РезультатВыпуска.НазначениеПродукцииЭтапа;
	ХарактеристикаПродукцииКонечногоЭтапа = РезультатВыпуска.ХарактеристикаПродукцииКонечногоЭтапа;
	КоличествоПродукцииКонечногоЭтапа = РезультатВыпуска.КоличествоВыпускаОбщее;
	
	ОстПеренести = 0;
	ПереносПоИсточнику = 0;
    ДатаПроизводства = Неопределено;
	СкладПолучатель = Неопределено;
	ПереносПоСтрокеВыпуска = 0;	  
	
	Группа = Новый УникальныйИдентификатор();
	
	Набор = РегистрыНакопления.А1_МатСоставИзделий_Расш1.СоздатьНаборЗаписей();
	Набор.Отбор.Регистратор.Установить(ДокОбъект.Ссылка);
	Набор.Прочитать();
	
	//ДатаПроизводства = СтрВып.ДатаПроизводства;
	//СкладПолучатель = СтрВып.Получатель;		
	
	ОстПеренести = Требуется;   
	
	Для Каждого Ист Из ТабИсточников Цикл
		
		Если ОстПеренести <= 0 Тогда Прервать; КонецЕсли;  
		Если Ист.ЧистыйВыпуск <= 0 Тогда Продолжить; КонецЕсли;  //если в источнике уже нет положительного выпуска переходим к следующему источнику
		
		Если Ист = ТабИсточников[ТабИсточников.Количество()-1] Тогда
			ПереносПоИсточнику = ОстПеренести;
		Иначе
			ПереносПоИсточнику = Мин(ОстПеренести, Ист.ЧистыйВыпуск); 
		КонецЕсли;	
		
		УдельныйСостав = ПолучитьУдельныйМатСоставПоИсточнику(Ист.ИсточникРег, КлючПФ);
		Если УдельныйСостав.Количество() = 0 Тогда
			Продолжить;//ОстПеренести = ОстПеренести - ПереносПоИсточнику; Продолжить;				
		КонецЕсли;
		ГруппаДляИсточника = Группа;
		
		РаспределениеВыпуска = ПолучитьРаспределениеПоВыпуску(
		ПереносПоИсточнику,
		ТабВыпуска,
		КоличествоПродукцииКонечногоЭтапа,
		Истина
		);
		
		Если РаспределениеВыпуска.Распределено = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаМатериала Из УдельныйСостав Цикл
			//Если СтрокаМатериала.ИзмеряетсяВШтуках Тогда
			//	КолМат = Окр(СтрокаМатериала.Удельное * ПереносПоИсточнику);        
			//Иначе
			//	КолМат = СтрокаМатериала.Удельное * ПереносПоИсточнику;	
			//КонецЕсли;           
			Для Каждого СтрокаРаспределения Из РаспределениеВыпуска.Таблица Цикл   
				
				СтрВып = СтрокаРаспределения.СтрокаВыпуска;
				ДатаПроизводства = СтрВып.ДатаПроизводства;
				СкладПолучатель = СтрВып.Получатель;   
				
				ПереносПоСтрокеВыпуска = СтрокаРаспределения.Количество;   
				Если ПереносПоСтрокеВыпуска = 0 Тогда 
					Продолжить; 
				КонецЕсли;
				Если СтрокаМатериала.ИзмеряетсяВШтуках Тогда
					КолМат = Окр(СтрокаМатериала.Удельное * ПереносПоСтрокеВыпуска);        
				Иначе
					КолМат = СтрокаМатериала.Удельное * ПереносПоСтрокеВыпуска;	
				КонецЕсли;					
				// a) Добавляем расход материалов в текущем документе
				ДобавитьЗаписьМатСост
				(
				Набор,
				ДокОбъект.Дата,
				ДокОбъект.Ссылка,
				КлючПФ,
				СтрокаМатериала.Материал, 
				СтрокаМатериала.ХарМат,
				СтрокаМатериала.СерМат,
				СтрокаМатериала.НазМат,
				СтрокаМатериала.Статья, 
				СтрокаМатериала.Подразделение,
				ПереносПоСтрокеВыпуска,
				КолМат,
				Ложь, 
				Неопределено,
				ГруппаДляИсточника, 
				НазначениеПродукцииЭтапа, 
				СтрокаМатериала.Продукция,
				Договор,
				НаправлениеДеятельности,
				ИнформацияДляПечати,
				ПродукцияКонечногоЭтапа,
				КоличествоПродукцииКонечногоЭтапа,
				ХарактеристикаПродукцииКонечногоЭтапа,
				НазначениеПродукцииЭтапа,
				СкладПолучатель,
				ДатаПроизводства,
				Ложь
				);
				
				// b) Сторнируем расход материалов и выпуск ПФ у источника
				ДобавитьЗаписьМатСост
				(
				Набор,  
				ДокОбъект.Дата,     			 //Период
				ДокОбъект.Ссылка,  				 //Регистратор
				КлючПФ,                          //КлючПФ
				СтрокаМатериала.Материал,        //МатНом
				СтрокаМатериала.ХарМат,          //МатХар
				СтрокаМатериала.СерМат,          //МатСер
				СтрокаМатериала.НазМат,          //НазМат
				СтрокаМатериала.Статья,          //Статья
				СтрокаМатериала.Подразделение,   //Подразделение
				-ПереносПоСтрокеВыпуска,         //КолПродукции
				-КолМат,                         //КолМатериала
				Истина,                          //ЭтоСторно
				Ист.ИсточникРег,                 //КорректируемыйРегистратор
				ГруппаДляИсточника,              //Группа
				Неопределено,                    //НазПродукцииПереопределить
				СтрокаМатериала.Продукция,       //Продукция
				Договор,                         //Договор
				НаправлениеДеятельности,         //НаправлениеДеятельности
				ИнформацияДляПечати,             //ИнформацияДляПечати
				ПродукцияКонечногоЭтапа,         //ПродукцияКонечногоЭтапа
				КоличествоПродукцииКонечногоЭтапа, //КоличествоПродукцииКонечногоЭтапа
				ХарактеристикаПродукцииКонечногоЭтапа, //ХарактеристикаПродукцииКонечногоЭтапа
				НазначениеПродукцииЭтапа,        //НазначениеПродукцииЭтапа
				СкладПолучатель,                 //СкладПолучатель
				ДатаПроизводства,                //ДатаПроизводства
				Ложь                             //ПоНормам
				); 	
			КонецЦикла;
		КонецЦикла; 
		
		Для Каждого СтрокаРаспределения Из РаспределениеВыпуска.Таблица Цикл   
			
			СтрВып = СтрокаРаспределения.СтрокаВыпуска;
			ДатаПроизводства = СтрВып.ДатаПроизводства;
			СкладПолучатель = СтрВып.Получатель;   
			
			ПереносПоСтрокеВыпуска = СтрокаРаспределения.Количество;   
			Если ПереносПоСтрокеВыпуска = 0 Тогда 
				Продолжить; 
			КонецЕсли;			                        
			
			//3.3. Сторнируем ПФ-материал в текущем документе
			ДобавитьЗаписьМатСост
			(
			Набор, 
			ДокОбъект.Дата,
			ДокОбъект.Ссылка, 
			КлючПФ,
			КлючПФ.Номенклатура, 
			КлючПФ.Характеристика,
			КлючПФ.Серия, 
			КлючПФ.Назначение,
			КлючПФ.Статья, 
			ДокОбъект.Подразделение,
			0,
			-ПереносПоСтрокеВыпуска,
			Истина, 
			ДокОбъект.Ссылка,
			ГруппаДляИсточника,
			Неопределено, 
			Неопределено,
			Договор,
			НаправлениеДеятельности,
			ИнформацияДляПечати,
			ПродукцияКонечногоЭтапа,
			КоличествоПродукцииКонечногоЭтапа,
			ХарактеристикаПродукцииКонечногоЭтапа,
			НазначениеПродукцииЭтапа,
			СкладПолучатель,
			ДатаПроизводства,
			Ложь
			); 
		КонецЦикла;
		
		ОстПеренести = ОстПеренести - РаспределениеВыпуска.Распределено;			
		Набор.Записать();
	КонецЦикла; 
КонецПроцедуры

// Разворачивает полуфабрикаты по нормам для всех материалов, которые остались в регистре
// Параметры:
//  ДокОбъект - ДокументОбъект - объект документа этапа производства
//  НазначениеПродукцииЭтапа - СправочникСсылка.Назначения - назначение продукции конечного этапа
//  Договор - СправочникСсылка.ДоговорыКонтрагентов - договор для записей регистра
//  НаправлениеДеятельности - СправочникСсылка.НаправленияДеятельности - направление деятельности для записей регистра
//  ИнформацияДляПечати - Строка - дополнительная информация для печати
//  РезультатВыпуска - Структура - структура с полями: Таблица, КоличествоВыпускаОбщее, ПродукцияКонечногоЭтапа,
//	НазначениеПродукцииЭтапа, ХарактеристикаПродукцииКонечногоЭтапа
Процедура РазвернутьПолуфабрикатПоНормам(ДокОбъект, НазначениеПродукцииЭтапа, Договор, НаправлениеДеятельности, ИнформацияДляПечати, РезультатВыпуска)
	
	ТабВыпуска = РезультатВыпуска.Таблица;
	ПродукцияКонечногоЭтапа = РезультатВыпуска.ПродукцияКонечногоЭтапа;
	ХарактеристикаПродукцииКонечногоЭтапа = РезультатВыпуска.ХарактеристикаПродукцииКонечногоЭтапа;
	КоличествоПродукцииКонечногоЭтапа = РезультатВыпуска.КоличествоВыпускаОбщее;
	
	Набор = РегистрыНакопления.А1_МатСоставИзделий_Расш1.СоздатьНаборЗаписей();
	Набор.Отбор.Регистратор.Установить(ДокОбъект.Ссылка);
	Набор.Прочитать();
	
	Если КоличествоПродукцииКонечногоЭтапа = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитВспомогательныйМатериал = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ВспомогательныйМатериал_a6ab234c1428470d81365a2df40445b9");
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	МАКСИМУМ(ВЫБОР КОГДА НЕ Рег.Сторно ТОГДА Рег.Продукция ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КОНЕЦ)                            КАК Продукция,
		|	МАКСИМУМ(ВЫБОР КОГДА НЕ Рег.Сторно ТОГДА Рег.ХарактеристикаПродукции ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КОНЕЦ)              КАК ХарПродукции,
		|	МАКСИМУМ(ВЫБОР КОГДА НЕ Рег.Сторно ТОГДА Рег.СерияПродукции ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КОНЕЦ)                       КАК СерияПродукции,
		|	МАКСИМУМ(ВЫБОР КОГДА НЕ Рег.Сторно ТОГДА Рег.НазначениеПродукции ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КОНЕЦ)                  КАК НазначениеПродукции,
		|	МАКСИМУМ(Рег.СтатьяКалькуляцииМатериала) КАК Статья,
		|	Рег.Материал                             КАК Материал,
		|	Рег.ХарактеристикаМатериала              КАК ХарМатериала,
		|	Рег.СерияМатериала                       КАК СерМатериала,
		|	Рег.НазначениеМатериала                  КАК НазМатериала,
		|	МАКСИМУМ(Рег.Подразделение)              КАК Подразделение,
		|	МАКСИМУМ(ВЫБОР КОГДА НЕ Рег.Сторно ТОГДА Рег.КоличествоПродукции ИНАЧЕ 0 КОНЕЦ)           КАК КоличествоПродукции,
		|	СУММА(Рег.КоличествоМатериала)           КАК КоличествоМатериала,
		|	ЕСТЬNULL(Рег.Материал.ЕдиницаИзмерения.ТипИзмеряемойВеличины = &ТипШтук, ЛОЖЬ) КАК ИзмеряетсяВШтукахМатериала
		|ИЗ
		|	РегистрНакопления.А1_МатСоставИзделий_Расш1 КАК Рег
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК ДопРекв
		|		ПО ДопРекв.Ссылка = Рег.Материал
		|			И ДопРекв.Свойство = &РеквизитВспомогательныйМатериал
		|ГДЕ
		|	Рег.Регистратор = &Регистратор
		|	И Рег.ПоНормам = ЛОЖЬ
		|	И (ДопРекв.Значение ЕСТЬ NULL
		|		ИЛИ ДопРекв.Значение <> ИСТИНА)
		|	И Рег.Активность
		|СГРУППИРОВАТЬ ПО
		|	Рег.Материал,
		|	Рег.ХарактеристикаМатериала,
		|	Рег.СерияМатериала,
		|	Рег.НазначениеМатериала
		|ИМЕЮЩИЕ
		|	Окр(СУММА(Рег.КоличествоМатериала), 3) > 0");
	
	Запрос.УстановитьПараметр("Регистратор", ДокОбъект.Ссылка);
	Запрос.УстановитьПараметр("ПустаяНоменклатура", Справочники.Номенклатура.ПустаяСсылка());
	Запрос.УстановитьПараметр("ТипШтук", Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук);
	Запрос.УстановитьПараметр("РеквизитВспомогательныйМатериал", РеквизитВспомогательныйМатериал);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Материал = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если Выборка.Продукция = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		КоличествоМатериала = Выборка.КоличествоМатериала;
		Если КоличествоМатериала <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		РаспределениеВыпуска = ПолучитьРаспределениеПоВыпуску(
			КоличествоМатериала,
			ТабВыпуска,
			КоличествоПродукцииКонечногоЭтапа,
			Выборка.ИзмеряетсяВШтукахМатериала
		);
		Если РаспределениеВыпуска.Распределено = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		КлючПФ = КлючПФ(Выборка.Продукция, Выборка.ХарПродукции, Выборка.СерияПродукции, Выборка.НазначениеПродукции, Выборка.Статья);
		Группа = Новый УникальныйИдентификатор();
		НормативныеСоставыПоДатам = Новый Соответствие;
		
		Для Каждого СтрокаРаспределения Из РаспределениеВыпуска.Таблица Цикл
			
			СтрВып = СтрокаРаспределения.СтрокаВыпуска;
			ДатаПроизводства = СтрВып.ДатаПроизводства;
			Если ДатаПроизводства = Неопределено Тогда
				ДатаПроизводства = ДокОбъект.Дата;
			КонецЕсли;
			СкладПолучатель = СтрВып.Получатель;
			
			ПереносПоСтрокеВыпуска = СтрокаРаспределения.Количество;
			Если ПереносПоСтрокеВыпуска = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			НормативныйСостав = НормативныеСоставыПоДатам.Получить(ДатаПроизводства);
			Если НормативныйСостав = Неопределено Тогда
				НормативныйСостав = ПолучитьНормативныйСоставДоПокупных(Выборка.Материал, ДатаПроизводства);
				НормативныеСоставыПоДатам.Вставить(ДатаПроизводства, НормативныйСостав);
			КонецЕсли;
			
			// Проверяем, что спецификация найдена
			// Если спецификация не найдена, в нормативном составе будет только сама номенклатура с коэффициентом 1
			// Проверяем: если в составе только одна строка с самой номенклатурой и коэффициентом 1, значит спецификация не найдена
			СпецификацияНайдена = Ложь;
			Если НормативныйСостав.Количество() > 0 Тогда
				Для Каждого СтрокаМатериала Из НормативныйСостав Цикл
					// Если нашли материал, отличный от исходного, значит спецификация найдена
					Если СтрокаМатериала.Материал <> Выборка.Материал Тогда
						СпецификацияНайдена = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			// Если спецификация не найдена, ничего не делаем
			Если НЕ СпецификацияНайдена Тогда
				Продолжить;
			КонецЕсли;
			
			МатериалыРазвернуты = Ложь;
			
			Для Каждого СтрокаМатериала Из НормативныйСостав Цикл
				
				// Пропускаем саму номенклатуру (она будет сторнирована отдельно)
				Если СтрокаМатериала.Материал = Выборка.Материал Тогда
					Продолжить;
				КонецЕсли;
				
				ПодразделениеМатериала = ?(ЗначениеЗаполнено(СтрокаМатериала.Подразделение), СтрокаМатериала.Подразделение, Выборка.Подразделение);
				
				Если СтрокаМатериала.ИзмеряетсяВШтуках Тогда
					КолМат = Окр(СтрокаМатериала.Удельное * ПереносПоСтрокеВыпуска);
				Иначе
					КолМат = СтрокаМатериала.Удельное * ПереносПоСтрокеВыпуска;
				КонецЕсли;
				
				Если КолМат <= 0 Тогда
					Продолжить;
				КонецЕсли;
				
				ДобавитьЗаписьМатСост(
					Набор,
					ДокОбъект.Дата,
					ДокОбъект.Ссылка,
					КлючПФ,
					СтрокаМатериала.Материал,
					СтрокаМатериала.ХарМат,
					СтрокаМатериала.СерМат,
					СтрокаМатериала.НазМат,
					СтрокаМатериала.Статья,
					ПодразделениеМатериала,
					ПереносПоСтрокеВыпуска,
					КолМат,
					Ложь,
					Неопределено,
					Группа,
					НазначениеПродукцииЭтапа,
					СтрокаМатериала.Продукция,
					Договор,
					НаправлениеДеятельности,
					ИнформацияДляПечати,
					ПродукцияКонечногоЭтапа,
					КоличествоПродукцииКонечногоЭтапа,
					ХарактеристикаПродукцииКонечногоЭтапа,
					НазначениеПродукцииЭтапа,
					СкладПолучатель,
					ДатаПроизводства,
					Истина
				);
				
				МатериалыРазвернуты = Истина;
				
			КонецЦикла;
			
			Если НЕ МатериалыРазвернуты Тогда
				Продолжить;
			КонецЕсли;
			
			ДобавитьЗаписьМатСост(
				Набор,
				ДокОбъект.Дата,
				ДокОбъект.Ссылка,
				КлючПФ,
				Выборка.Материал,
				Выборка.ХарМатериала,
				Выборка.СерМатериала,
				Выборка.НазМатериала,
				Выборка.Статья,
				Выборка.Подразделение,
				-ПереносПоСтрокеВыпуска,
				-ПереносПоСтрокеВыпуска,
				Истина,
				ДокОбъект.Ссылка,
				Группа,
				Неопределено,
				Неопределено,
				Договор,
				НаправлениеДеятельности,
				ИнформацияДляПечати,
				ПродукцияКонечногоЭтапа,
				КоличествоПродукцииКонечногоЭтапа,
				ХарактеристикаПродукцииКонечногоЭтапа,
				НазначениеПродукцииЭтапа,
				СкладПолучатель,
				ДатаПроизводства,
				Истина
			);
			
		КонецЦикла;
		
		Набор.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

// 1А. Найти первый ПФ с положительным чистым расходом в текущем документе и
//     с наличием чистого выпуска в других регистраторах (по тем же аналитикам)
// Параметры:
//  ДокСсылка - ДокументСсылка - ссылка на документ для поиска кандидата полуфабриката
//  ПропущенныеКандидаты - Соответствие - соответствие пропущенных кандидатов (ключ - строка, значение - Истина), необязательный параметр
// Возвращаемое значение:
//  Структура - структура с полями: Ключ (Структура), ЧистыйРасходВДоке (Число), Источники (ТаблицаЗначений),
//    или Неопределено, если кандидат не найден
Функция НайтиКандидатаПолуфабриката(ДокСсылка, ПропущенныеКандидаты = Неопределено) Экспорт
	
	// Чистый расход ПФ «приписываемый» нашему документу, с учётом сторно,
	// которое могло быть создано в других регистраторах (но с КорректируемыйРегистратор = &Док).
	ЗапросПотребностьДокумента = Новый Запрос(
	"ВЫБРАТЬ
	|   Рег.Материал                        КАК Номенклатура,
	|   Рег.ХарактеристикаМатериала         КАК Характеристика,
	|   Рег.СерияМатериала                  КАК Серия,
	|   Рег.НазначениеМатериала             КАК Назначение,
	|	МАКСИМУМ(Рег.СтатьяКалькуляцииМатериала)      КАК Статья,	
	|   СУММА(Рег.КоличествоМатериала)      КАК ЧистыйРасходПФ,
	|   МАКСИМУМ(Рег.ДатаПроизводства)      КАК МаксДатаПроизводства
	|ИЗ
	|   РегистрНакопления.А1_МатСоставИзделий_Расш1 КАК Рег
	|ГДЕ
	|	Рег.Регистратор = &Док	
	|   И Рег.Активность
	|СГРУППИРОВАТЬ ПО
	|   Рег.Материал,
	|   Рег.ХарактеристикаМатериала,
	|   Рег.СерияМатериала,
	|   Рег.НазначениеМатериала
	|ИМЕЮЩИЕ
	|   Окр(СУММА(Рег.КоличествоМатериала), 3) > 0");
	
	ЗапросПотребностьДокумента.УстановитьПараметр("Док", ДокСсылка); 
	ВыборкаПотребность = ЗапросПотребностьДокумента.Выполнить().Выбрать(); 
	
	Пока ВыборкаПотребность.Следующий() Цикл
		// Чистый выпуск ПФ по «источнику» с учётом сторно в других регистраторах.
		// ИсточникРег = ВЫБОР КОГДА Ист.Сторно ТОГДА Ист.КорректируемыйРегистратор ИНАЧЕ Ист.Регистратор КОНЕЦ  
		
		//нужно переименовать ПродукцияЭтапа на ПродукцияКонечногоЭтапа

		ЗапросВыпускающиеИсточники = Новый Запрос(
		"ВЫБРАТЬ
		|   ВЫБОР
		|       КОГДА Источник.Сторно
		|           ТОГДА Источник.КорректируемыйРегистратор
		|       ИНАЧЕ Источник.Регистратор
		|   КОНЕЦ                                   КАК ИсточникРег,
		|   Источник.Продукция        КАК Продукция,        
		|   Источник.ХарактеристикаПродукции        КАК Характеристика,
		|   Источник.СерияПродукции                 КАК Серия,
		|   Источник.НазначениеПродукции            КАК Назначение,
		|   Источник.Подразделение            	    КАК Подразделение,
		|   СУММА(Источник.КоличествоПродукции)     КАК ЧистыйВыпуск,  
        |   СУММА(Источник.КоличествоМатериала)     КАК ЕстьМатериалы,
		|	МИНИМУМ(Источник.ДатаПроизводства) КАК ДатаДляСортировки
		|ИЗ
		|   РегистрНакопления.А1_МатСоставИзделий_Расш1 КАК Источник
		|ГДЕ
		|	(ВЫБОР КОГДА Источник.Сторно ТОГДА Источник.КорректируемыйРегистратор ИНАЧЕ Источник.Регистратор КОНЕЦ) <> &Док  	
		|   И Источник.Продукция = &Продукция
		|   И Источник.ХарактеристикаПродукции = &Характеристика
		|   И Источник.СерияПродукции = &Серия
		|   И Источник.НазначениеПродукции = &Назначение
		|   И Источник.Активность
		|   И Источник.ДатаПроизводства <= &МаксДатаПроизводства
		|СГРУППИРОВАТЬ ПО
		|   ВЫБОР
		|       КОГДА Источник.Сторно
		|           ТОГДА Источник.КорректируемыйРегистратор
		|       ИНАЧЕ Источник.Регистратор
		|   КОНЕЦ,
		|   Источник.Продукция,
		|   Источник.ХарактеристикаПродукции,
		|   Источник.СерияПродукции,
		|   Источник.НазначениеПродукции,
		|	Источник.Подразделение
		|ИМЕЮЩИЕ
		|	Окр(СУММА(Источник.КоличествоПродукции), 3) > 0
		|	И Окр(СУММА(Источник.КоличествоМатериала), 3) > 0
		|УПОРЯДОЧИТЬ ПО
		|	ДатаДляСортировки");   
		
		
		
		ЗапросВыпускающиеИсточники.УстановитьПараметр("Характеристика", ВыборкаПотребность.Характеристика);
		ЗапросВыпускающиеИсточники.УстановитьПараметр("Серия", ВыборкаПотребность.Серия);
		ЗапросВыпускающиеИсточники.УстановитьПараметр("Назначение", ВыборкаПотребность.Назначение); 
		ЗапросВыпускающиеИсточники.УстановитьПараметр("Док", ДокСсылка);
		ЗапросВыпускающиеИсточники.УстановитьПараметр("Продукция", ВыборкаПотребность.Номенклатура);
		ЗапросВыпускающиеИсточники.УстановитьПараметр("МаксДатаПроизводства", ВыборкаПотребность.МаксДатаПроизводства);
		
		// Ожидаемое поведение: запрос выполняется в цикле, т.к. нужно найти первого подходящего кандидата
		// и вернуть результат сразу после его обнаружения. Модификация запроса для множества значений
		// не требуется - цикл необходим для последовательной проверки кандидатов.
		// BSLLS:CreateQueryInCycle-off
		Источники = ЗапросВыпускающиеИсточники.Выполнить().Выгрузить(); 
		Если Источники.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		// BSLLS:CreateQueryInCycle-on
		// Проверяем, не является ли этот кандидат пропущенным
		Ключ = КлючПФ(
			ВыборкаПотребность.Номенклатура,
			ВыборкаПотребность.Характеристика,
			ВыборкаПотребность.Серия,
			ВыборкаПотребность.Назначение,
			ВыборкаПотребность.Статья);
		КлючСтрока = СтрШаблон("%1|%2|%3|%4|%5",
			Строка(Ключ.Номенклатура),
			Строка(Ключ.Характеристика),
			Строка(Ключ.Серия),
			Строка(Ключ.Назначение),
			Строка(Ключ.Статья));
		
		Если ПропущенныеКандидаты <> Неопределено И ПропущенныеКандидаты.Получить(КлючСтрока) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// Нашли первого подходящего
		Результат = Новый Структура;
		Результат.Вставить("Ключ", Ключ);
		Результат.Вставить("ЧистыйРасходВДоке", ВыборкаПотребность.ЧистыйРасходПФ);
		Результат.Вставить("Источники", Источники);
		Возврат Результат;
	КонецЦикла;
	
	Возврат Неопределено;
КонецФункции

// 1) Скаляр: чистый выпуск ПФ у источника по правилу назначения (строго или оба NULL).
// 2) Материалы источника по этому же ПФ (без фильтра по назначению продукции).
// Параметры:
//  ИсточникРег - ДокументСсылка - регистратор источника полуфабриката
//  КлючПФ - Структура - структура с полями: Номенклатура, Характеристика, Серия, Назначение, Статья
// Возвращаемое значение:
//  ТаблицаЗначений - таблица удельного материального состава с колонками: Материал, ХарМат, СерМат, НазМат, Статья, Подразделение, Продукция, КоличествоВыпускаПФ, ЧистыйРасходМат, ИзмеряетсяВШтуках, Удельное
Функция ПолучитьУдельныйМатСоставПоИсточнику(ИсточникРег, КлючПФ) 
	
	РеквизитВспомогательныйМатериал = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ВспомогательныйМатериал_a6ab234c1428470d81365a2df40445b9");
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ  
	|   МАКСИМУМ(Рег.КоличествоПродукцииКонечногоЭтапа)	   КАК КоличествоВыпускаПФ,	
	|   Рег.Материал,
	|   Рег.ХарактеристикаМатериала        КАК ХарМат,
	|   Рег.СерияМатериала                 КАК СерМат,
	|   Рег.НазначениеМатериала            КАК НазМат,
	|   МАКСИМУМ(Рег.СтатьяКалькуляцииМатериала) КАК Статья,
	|   Рег.Подразделение            	   КАК Подразделение,
	|   МАКСИМУМ(Рег.Продукция)            КАК Продукция,
	|   СУММА(Рег.КоличествоМатериала)     КАК ЧистыйРасходМат, 
	|   ЕСТЬNULL(Рег.Материал.ЕдиницаИзмерения.ТипИзмеряемойВеличины = &ТипШтук, ЛОЖЬ) КАК ИзмеряетсяВШтуках
	|ИЗ
	|   РегистрНакопления.А1_МатСоставИзделий_Расш1 КАК Рег
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК ДопРекв
	|		ПО ДопРекв.Ссылка = Рег.Материал
	|			И ДопРекв.Свойство = &РеквизитВспомогательныйМатериал
	|ГДЕ
	|   (ВЫБОР КОГДА Рег.Сторно ТОГДА Рег.КорректируемыйРегистратор ИНАЧЕ Рег.Регистратор КОНЕЦ) = &Источник 
	|   И Рег.ПродукцияКонечногоЭтапа = &Продукция
	|   И Рег.НазначениеПродукции = &Назначение
	|   И Рег.ХарактеристикаПродукции = &Характеристика
	|   И Рег.СерияПродукции = &Серия 
	|   И Рег.Активность
	|   И НЕ Рег.ПоНормам
	|   И (ДопРекв.Значение ЕСТЬ NULL
	|		ИЛИ ДопРекв.Значение <> ИСТИНА)
	|СГРУППИРОВАТЬ ПО
	|   Рег.Материал,
	|   Рег.ХарактеристикаМатериала,
	|   Рег.СерияМатериала,
	|   Рег.НазначениеМатериала,
	|   Рег.Подразделение,
	|	Рег.ПродукцияКонечногоЭтапа
	|ИМЕЮЩИЕ   
	|   МАКСИМУМ(Рег.КоличествоПродукцииКонечногоЭтапа) > 0
	|   И СУММА(Рег.КоличествоМатериала) > 0");
	

    Запрос.УстановитьПараметр("Источник", ИсточникРег); 
    Запрос.УстановитьПараметр("Продукция", КлючПФ.Номенклатура); 
    Запрос.УстановитьПараметр("Назначение", КлючПФ.Назначение);
    Запрос.УстановитьПараметр("Характеристика", КлючПФ.Характеристика);
    Запрос.УстановитьПараметр("Серия", КлючПФ.Серия);
    Запрос.УстановитьПараметр("ТипШтук",  Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук);
    Запрос.УстановитьПараметр("РеквизитВспомогательныйМатериал", РеквизитВспомогательныйМатериал);
	
    ТабЗапрос = Запрос.Выполнить();

	Таб= ТабЗапрос.Выгрузить();
	
	Если Таб.Количество() = 0 Тогда
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;     
	
    Если Таб.Колонки.Найти("Удельное") = Неопределено Тогда
        Таб.Колонки.Добавить("Удельное");
    КонецЕсли;

	Для Каждого Стр Из Таб Цикл
		Если Стр.КоличествоВыпускаПФ = 0 Тогда Продолжить КонецЕсли;
        Стр.Удельное = Стр.ЧистыйРасходМат / Стр.КоличествоВыпускаПФ;
     КонецЦикла;

    Возврат Таб;
КонецФункции

// Добавить запись (обычную или сторно) в наш регистр
// Параметры:
//  Набор - НаборЗаписейРегистра - набор записей регистра накопления А1_МатСоставИзделий_Расш1
//  Период - Дата - период записи в регистр
//  Регистратор - ДокументСсылка - документ-регистратор записи
//  КлючПФ - Структура - структура с полями: Номенклатура, Характеристика, Серия, Назначение, Статья
//  МатНом - СправочникСсылка.Номенклатура - номенклатура материала
//  МатХар - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика материала
//  МатСер - СправочникСсылка.СерииНоменклатуры - серия материала
//  НазМат - СправочникСсылка.Назначения - назначение материала
//  Статья - СправочникСсылка.СтатьиКалькуляции - статья калькуляции материала
//  Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение
//  КолПродукции - Число - количество продукции
//  КолМатериала - Число - количество материала
//  ЭтоСторно - Булево - признак сторнирующей записи
//  КорректируемыйРегистратор - ДокументСсылка - регистратор, который корректируется (для сторно)
//  Группа - УникальныйИдентификатор - группа коррекции
//  НазПродукцииПереопределить - СправочникСсылка.Назначения - назначение продукции для переопределения
//  Продукция - СправочникСсылка.Номенклатура - продукция (если отличается от КлючПФ.Номенклатура)
//  Договор - СправочникСсылка.ДоговорыКонтрагентов - договор
//  НаправлениеДеятельности - СправочникСсылка.НаправленияДеятельности - направление деятельности
//  ИнформацияДляПечати - Строка - дополнительная информация для печати
//  ПродукцияКонечногоЭтапа - СправочникСсылка.Номенклатура - продукция конечного этапа
//  КоличествоПродукцииКонечногоЭтапа - Число - количество продукции конечного этапа
//  ХарактеристикаПродукцииКонечногоЭтапа - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика продукции конечного этапа
//  НазначениеПродукцииЭтапа - СправочникСсылка.Назначения - назначение продукции этапа
//  СкладПолучатель - СправочникСсылка.Склады - склад-получатель
//  ДатаПроизводства - Дата - дата производства
//  ПоНормам - Булево - признак расчета по нормам
Процедура ДобавитьЗаписьМатСост(
	Набор,
	Период, 
	Регистратор, 
	КлючПФ, 
	МатНом=Неопределено, 
	МатХар=Неопределено, 
	МатСер=Неопределено, 
	НазМат=Неопределено, 
	Статья=Неопределено, 
	Подразделение=Неопределено, 
	КолПродукции=0, 
	КолМатериала=0, 
	ЭтоСторно=Ложь, 
	КорректируемыйРегистратор=Неопределено,
	Группа=Неопределено, 
	НазПродукцииПереопределить=Неопределено,
	Продукция=Неопределено,
	Договор=Неопределено,
	НаправлениеДеятельности=Неопределено,
	ИнформацияДляПечати=Неопределено,
	ПродукцияКонечногоЭтапа=Неопределено,
	КоличествоПродукцииКонечногоЭтапа=Неопределено,
	ХарактеристикаПродукцииКонечногоЭтапа=Неопределено,
	НазначениеПродукцииЭтапа=Неопределено,
	СкладПолучатель=Неопределено,
	ДатаПроизводства=Неопределено,
	ПоНормам = Ложь
	)
	
    Зп = Набор.Добавить();
    Зп.Период = Период;
    Зп.Регистратор = Регистратор;
    Зп.Активность = Истина;

    // Продукция - это наш ПФ (по ключу)
    Зп.Продукция = ?(ЗначениеЗаполнено(Продукция), Продукция, КлючПФ.Номенклатура);
    Зп.ХарактеристикаПродукции = КлючПФ.Характеристика;
    Зп.СерияПродукции = КлючПФ.Серия;
    Зп.СкладПолучатель = Неопределено;
    //Зп.НазначениеПродукции = КлючПФ.Назначение;  
	Зп.НазначениеПродукции = ?(ЗначениеЗаполнено(НазПродукцииПереопределить), НазПродукцииПереопределить, КлючПФ.Назначение);

    // Материал
    Зп.Материал = МатНом;
    Зп.ХарактеристикаМатериала = МатХар;
    Зп.СерияМатериала = МатСер;
    Зп.НазначениеМатериала = НазМат;
    Зп.СтатьяКалькуляцииМатериала = Статья;
    Зп.Подразделение = Подразделение;
	
	Зп.КоличествоПродукцииКонечногоЭтапа = КоличествоПродукцииКонечногоЭтапа;
    Зп.КоличествоПродукции = КолПродукции;
    Зп.КоличествоМатериала = КолМатериала;
	

    Зп.Сторно = ЭтоСторно;
    Зп.КорректируемыйРегистратор = КорректируемыйРегистратор;
    Зп.ГруппаКоррекции = Группа;
	Если Набор.Метаданные().Реквизиты.Найти("ПоНормам") <> Неопределено Тогда
		Зп.ПоНормам = ПоНормам;
	КонецЕсли;
	
    Зп.Договор = Договор;
    Зп.НаправлениеДеятельности = НаправлениеДеятельности;
    Зп.ИнформацияДляПечати = ИнформацияДляПечати;
	Зп.ПродукцияКонечногоЭтапа = ПродукцияКонечногоЭтапа; 
	Зп.ХарактеристикаПродукцииКонечногоЭтапа = ХарактеристикаПродукцииКонечногоЭтапа;
	Зп.НазначениеПродукцииКонечногоЭтапа = НазначениеПродукцииЭтапа;
	
    Зп.СкладПолучатель = СкладПолучатель;
	Зп.ДатаПроизводства = ДатаПроизводства;	
	
КонецПроцедуры

Функция ПустаяИлиСам(Знч, ПустаяСсылка)
    Если Знч = Неопределено Тогда Возврат ПустаяСсылка; КонецЕсли;
    Возврат Знч;
КонецФункции

Функция КлючПФ(Номенклатура, Характеристика, Серия, Назначение, Статья) 
    Рез = Новый Структура;
    Рез.Вставить("Номенклатура",   Номенклатура);
    Рез.Вставить("Характеристика", ПустаяИлиСам(Характеристика, Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка()));
    Рез.Вставить("Серия",          ПустаяИлиСам(Серия, Справочники.СерииНоменклатуры.ПустаяСсылка()));
    Рез.Вставить("Назначение",     Назначение); // при необходимости - аналогично 
	Рез.Вставить("Статья",     	   Статья);
    Возврат Рез;
КонецФункции


// === БЛОК ПОМЕСЯЧНОГО РАСПРЕДЕЛЕНИЯ (новый регистр А1_МатСоставИзделийПоПериодам) ===

// Получает таблицу материалов с датой расхода.
// Для ЭтапПроизводства2_2: связывает ОбеспечениеМатериаламиИРаботами с РасходМатериаловИРабот
// по (Номенклатура, Характеристика) для получения ДатаРасхода каждого материала.
// Для ПроизводствоБезЗаказа: берет ДатаРасхода = ДокОбъект.Дата.
//
// Возвращаемое значение:
//  ТаблицаЗначений с колонками: Номенклатура, Характеристика, Серия, Количество,
//    СтатьяКалькуляции, Подразделение, Назначение, ИзмеряетсяВШтуках, ДатаРасхода, МесяцРасхода
Функция ПолучитьТаблицуМатериаловСДатойРасхода(ДокОбъект)

	ГотоваяПродукция = Справочники.ВидыНоменклатуры.НайтиПоНаименованию("1. Готовая продукция");

	ЭтоЭтап = ТипЗнч(ДокОбъект.Ссылка) = Тип("ДокументСсылка.ЭтапПроизводства2_2");
	ЭтоПБЗ  = ТипЗнч(ДокОбъект.Ссылка) = Тип("ДокументСсылка.ПроизводствоБезЗаказа");

	Если ЭтоЭтап Тогда
		// Для этапа: берем материалы из ОбеспечениеМатериаламиИРаботами,
		// дату расхода — из РасходМатериаловИРабот (связь по Номенклатура + Характеристика)
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|    Мат.Номенклатура       КАК Номенклатура,
		|    Мат.Характеристика     КАК Характеристика,
		|    Мат.Серия              КАК Серия,
		|    СУММА(Мат.Количество)  КАК Количество,
		|    Мат.СтатьяКалькуляции  КАК СтатьяКалькуляции,
		|    Мат.Подразделение      КАК Подразделение,
		|    Мат.Назначение         КАК Назначение,
		|    Мат.ИзмеряетсяВШтуках  КАК ИзмеряетсяВШтуках,
		|    Мат.ДатаРасхода         КАК ДатаРасхода
		|ИЗ
		|    (
		|        ВЫБРАТЬ
		|            ЭП.Номенклатура                         КАК Номенклатура,
		|            ЭП.Характеристика                       КАК Характеристика,
		|            ЭП.Серия                                КАК Серия,
		|            ЭП.Количество                           КАК Количество,
		|            ЭП.СтатьяКалькуляции                    КАК СтатьяКалькуляции,
		|            ЭП.Подразделение                        КАК Подразделение,
		|            ЭП.Назначение                           КАК Назначение,
		|            ЕСТЬNULL(ЭП.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины = &ТипШтук, ЛОЖЬ) КАК ИзмеряетсяВШтуках,
		|            ВЫБОР
		|                КОГДА &РасходОднойДатой
		|                    ТОГДА &ДатаРасходаШапки
		|                КОГДА Расход.ДатаРасхода ЕСТЬ NULL
		|                    ТОГДА &ДатаДокумента
		|                ИНАЧЕ Расход.ДатаРасхода
		|            КОНЕЦ                                   КАК ДатаРасхода
		|        ИЗ
		|            Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ЭП
		|                ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.РасходМатериаловИРабот КАК Расход
		|                ПО Расход.Ссылка = ЭП.Ссылка
		|                    И Расход.Номенклатура = ЭП.Номенклатура
		|                    И Расход.Характеристика = ЭП.Характеристика
		|        ГДЕ
		|            ЭП.Ссылка = &ДокЭтап
		|            И ЭП.ВариантОбеспечения = &Отгрузить
		|            И НЕ ЭП.Номенклатура.ВидНоменклатуры В ИЕРАРХИИ(&ГотоваяПродукция)
		|    ) КАК Мат
		|СГРУППИРОВАТЬ ПО
		|    Мат.Номенклатура,
		|    Мат.Характеристика,
		|    Мат.Серия,
		|    Мат.СтатьяКалькуляции,
		|    Мат.Подразделение,
		|    Мат.Назначение,
		|    Мат.ИзмеряетсяВШтуках,
		|    Мат.ДатаРасхода");

		Запрос.УстановитьПараметр("ДокЭтап", ДокОбъект.Ссылка);
		Запрос.УстановитьПараметр("Отгрузить", Перечисления.ВариантыОбеспечения.Отгрузить);
		Запрос.УстановитьПараметр("ТипШтук", Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук);
		Запрос.УстановитьПараметр("ГотоваяПродукция", ГотоваяПродукция);
		Запрос.УстановитьПараметр("РасходОднойДатой", ДокОбъект.РасходОднойДатой);
		Запрос.УстановитьПараметр("ДатаРасходаШапки", ДокОбъект.ДатаРасхода);
		Запрос.УстановитьПараметр("ДатаДокумента", ДокОбъект.Дата);

	ИначеЕсли ЭтоПБЗ Тогда
		// Для ПроизводствоБезЗаказа: ДатаРасхода = ДатаДокумента
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|    Мат.Номенклатура       КАК Номенклатура,
		|    Мат.Характеристика     КАК Характеристика,
		|    Мат.Серия              КАК Серия,
		|    СУММА(Мат.Количество)  КАК Количество,
		|    Мат.СтатьяКалькуляции  КАК СтатьяКалькуляции,
		|    Мат.Подразделение      КАК Подразделение,
		|    Мат.Назначение         КАК Назначение,
		|    Мат.ИзмеряетсяВШтуках  КАК ИзмеряетсяВШтуках,
		|    &ДатаДокумента          КАК ДатаРасхода
		|ИЗ
		|    (
		|        ВЫБРАТЬ
		|            ПБЗ.Номенклатура                         КАК Номенклатура,
		|            ПБЗ.Характеристика                       КАК Характеристика,
		|            ПБЗ.Серия                                КАК Серия,
		|            ПБЗ.Количество                           КАК Количество,
		|            ПБЗ.СтатьяКалькуляции                    КАК СтатьяКалькуляции,
		|            ПБЗ.Подразделение                        КАК Подразделение,
		|            ПБЗ.Назначение                           КАК Назначение,
		|            ЕСТЬNULL(ПБЗ.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины = &ТипШтук, ЛОЖЬ) КАК ИзмеряетсяВШтуках
		|        ИЗ
		|            Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК ПБЗ
		|        ГДЕ
		|            ПБЗ.Ссылка = &ДокПБЗ
		|            И НЕ ПБЗ.Номенклатура.ВидНоменклатуры В ИЕРАРХИИ(&ГотоваяПродукция)
		|    ) КАК Мат
		|СГРУППИРОВАТЬ ПО
		|    Мат.Номенклатура,
		|    Мат.Характеристика,
		|    Мат.Серия,
		|    Мат.СтатьяКалькуляции,
		|    Мат.Подразделение,
		|    Мат.Назначение,
		|    Мат.ИзмеряетсяВШтуках");

		Запрос.УстановитьПараметр("ДокПБЗ", ДокОбъект.Ссылка);
		Запрос.УстановитьПараметр("ТипШтук", Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук);
		Запрос.УстановитьПараметр("ГотоваяПродукция", ГотоваяПродукция);
		Запрос.УстановитьПараметр("ДатаДокумента", ДокОбъект.Дата);
	Иначе
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;

	Результат = Запрос.Выполнить().Выгрузить();

	// Добавляем колонку МесяцРасхода
	Если Результат.Колонки.Найти("МесяцРасхода") = Неопределено Тогда
		Результат.Колонки.Добавить("МесяцРасхода");
	КонецЕсли;
	Для Каждого Стр Из Результат Цикл
		Если ЗначениеЗаполнено(Стр.ДатаРасхода) Тогда
			Стр.МесяцРасхода = НачалоМесяца(Стр.ДатаРасхода);
		Иначе
			Стр.МесяцРасхода = НачалоМесяца(ДокОбъект.Дата);
			Стр.ДатаРасхода = ДокОбъект.Дата;
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;
КонецФункции

// Добавляет запись в регистр А1_МатСоставИзделийПоПериодам (аналог ДобавитьЗаписьМатСост с заполнением ДатаРасхода)
Процедура ДобавитьЗаписьМатСостПоПериодам(
	Набор,
	Период,
	Регистратор,
	КлючПФ,
	МатНом=Неопределено,
	МатХар=Неопределено,
	МатСер=Неопределено,
	НазМат=Неопределено,
	Статья=Неопределено,
	Подразделение=Неопределено,
	КолПродукции=0,
	КолМатериала=0,
	ЭтоСторно=Ложь,
	КорректируемыйРегистратор=Неопределено,
	Группа=Неопределено,
	НазПродукцииПереопределить=Неопределено,
	Продукция=Неопределено,
	Договор=Неопределено,
	НаправлениеДеятельности=Неопределено,
	ИнформацияДляПечати=Неопределено,
	ПродукцияКонечногоЭтапа=Неопределено,
	КоличествоПродукцииКонечногоЭтапа=Неопределено,
	ХарактеристикаПродукцииКонечногоЭтапа=Неопределено,
	НазначениеПродукцииЭтапа=Неопределено,
	СкладПолучатель=Неопределено,
	ДатаПроизводства=Неопределено,
	ПоНормам = Ложь,
	ДатаРасхода = Неопределено
	)

    Зп = Набор.Добавить();
    Зп.Период = Период;
    Зп.Регистратор = Регистратор;
    Зп.Активность = Истина;

    Зп.Продукция = ?(ЗначениеЗаполнено(Продукция), Продукция, КлючПФ.Номенклатура);
    Зп.ХарактеристикаПродукции = КлючПФ.Характеристика;
    Зп.СерияПродукции = КлючПФ.Серия;
    Зп.СкладПолучатель = Неопределено;
	Зп.НазначениеПродукции = ?(ЗначениеЗаполнено(НазПродукцииПереопределить), НазПродукцииПереопределить, КлючПФ.Назначение);

    Зп.Материал = МатНом;
    Зп.ХарактеристикаМатериала = МатХар;
    Зп.СерияМатериала = МатСер;
    Зп.НазначениеМатериала = НазМат;
    Зп.СтатьяКалькуляцииМатериала = Статья;
    Зп.Подразделение = Подразделение;

	Зп.КоличествоПродукцииКонечногоЭтапа = КоличествоПродукцииКонечногоЭтапа;
    Зп.КоличествоПродукции = КолПродукции;
    Зп.КоличествоМатериала = КолМатериала;

    Зп.Сторно = ЭтоСторно;
    Зп.КорректируемыйРегистратор = КорректируемыйРегистратор;
    Зп.ГруппаКоррекции = Группа;
	Если Набор.Метаданные().Реквизиты.Найти("ПоНормам") <> Неопределено Тогда
		Зп.ПоНормам = ПоНормам;
	КонецЕсли;

    Зп.Договор = Договор;
    Зп.НаправлениеДеятельности = НаправлениеДеятельности;
    Зп.ИнформацияДляПечати = ИнформацияДляПечати;
	Зп.ПродукцияКонечногоЭтапа = ПродукцияКонечногоЭтапа;
	Зп.ХарактеристикаПродукцииКонечногоЭтапа = ХарактеристикаПродукцииКонечногоЭтапа;
	Зп.НазначениеПродукцииКонечногоЭтапа = НазначениеПродукцииЭтапа;

    Зп.СкладПолучатель = СкладПолучатель;
	Зп.ДатаПроизводства = ДатаПроизводства;
	Зп.ДатаРасхода = ДатаРасхода;

КонецПроцедуры

// Формирует начальные записи в регистр А1_МатСоставИзделийПоПериодам
// с помесячным распределением материалов по выпуску (Вариант В).
//
// Алгоритм:
//  Фаза А — помесячное: материалы с МесяцРасхода = M → распределяются по выпуску месяца M.
//  Фаза Б — fallback: материалы без совпадения месяца → распределяются по ВСЕМУ выпуску.
Процедура ОбновитьМатСоставПоПериодам(ДокОбъект) Экспорт

	Если ДокОбъект = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Набор = РегистрыНакопления.А1_МатСоставИзделийПоПериодам.СоздатьНаборЗаписей();
	Набор.Отбор.Регистратор.Установить(ДокОбъект.Ссылка);

	// Получаем параметры из заказа на производство
	ЗаказНаПроизводство = Документы.ЗаказНаПроизводство2_2.ПустаяСсылка();
	ДокументОснование = Документы.ЗаказКлиента.ПустаяСсылка();
	Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	ИнформацияДляПечати = "";
	НаправлениеДеятельности = Справочники.НаправленияДеятельности.ПустаяСсылка();

	Если ЕстьРеквизитИлиСвойствоОбъекта(ДокОбъект, "Распоряжение") Тогда
		ЗаказНаПроизводство = ?(ЗначениеЗаполнено(ДокОбъект.Распоряжение), ДокОбъект.Распоряжение, Документы.ЗаказНаПроизводство2_2.ПустаяСсылка());
		ДокументОснование = ?(ЗначениеЗаполнено(ЗаказНаПроизводство.ДокументОснование), ЗаказНаПроизводство.ДокументОснование, Документы.ЗаказКлиента.ПустаяСсылка());

		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
			Договор = ?(ЗначениеЗаполнено(ДокументОснование.Договор), ДокументОснование.Договор, Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
			ИнформацияДляПечати = ?(ЗначениеЗаполнено(ДокументОснование.ДополнительнаяИнформация), ДокументОснование.ДополнительнаяИнформация, "");
			Если ЗначениеЗаполнено(ЗаказНаПроизводство.НаправлениеДеятельности) Тогда
				НаправлениеДеятельности = ЗаказНаПроизводство.НаправлениеДеятельности;
			ИначеЕсли ЗначениеЗаполнено(ДокументОснование.НаправлениеДеятельности) Тогда
				НаправлениеДеятельности = ДокументОснование.НаправлениеДеятельности;
			Иначе
				НаправлениеДеятельности = Справочники.НаправленияДеятельности.ПустаяСсылка();
			КонецЕсли;
		Иначе
			Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
			ИнформацияДляПечати = "";
			НаправлениеДеятельности = Справочники.НаправленияДеятельности.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;

	РезультатВыпуска = ПолучитьТаблицуВыпуска(ДокОбъект);
	ТабВыпуска = РезультатВыпуска.Таблица;
	Если НЕ ТабВыпуска.Количество() > 0 Тогда
		Возврат;
	КонецЕсли;

	ТабМатериалов = ПолучитьТаблицуМатериаловСДатойРасхода(ДокОбъект);
	Если НЕ ТабМатериалов.Количество() > 0 Тогда
		Возврат;
	КонецЕсли;

	КоличествоВыпускаОбщее = РезультатВыпуска.КоличествоВыпускаОбщее;
	ПродукцияКонечногоЭтапа = РезультатВыпуска.ПродукцияКонечногоЭтапа;
	НазначениеПродукцииЭтапа = РезультатВыпуска.НазначениеПродукцииЭтапа;
	ХарактеристикаПродукцииКонечногоЭтапа = РезультатВыпуска.ХарактеристикаПродукцииКонечногоЭтапа;

	// Добавляем колонку МесяцПроизводства к выпуску
	Если ТабВыпуска.Колонки.Найти("МесяцПроизводства") = Неопределено Тогда
		ТабВыпуска.Колонки.Добавить("МесяцПроизводства");
	КонецЕсли;
	Для Каждого СтрВып Из ТабВыпуска Цикл
		Если ЗначениеЗаполнено(СтрВып.ДатаПроизводства) Тогда
			СтрВып.МесяцПроизводства = НачалоМесяца(СтрВып.ДатаПроизводства);
		Иначе
			СтрВып.МесяцПроизводства = НачалоМесяца(ДокОбъект.Дата);
		КонецЕсли;
	КонецЦикла;

	// Собираем уникальные месяцы выпуска
	МесяцыВыпуска = Новый Соответствие;
	Для Каждого СтрВып Из ТабВыпуска Цикл
		МесяцыВыпуска.Вставить(СтрВып.МесяцПроизводства, Истина);
	КонецЦикла;

	// Помечаем распределенные материалы
	Если ТабМатериалов.Колонки.Найти("Распределено") = Неопределено Тогда
		ТабМатериалов.Колонки.Добавить("Распределено");
	КонецЕсли;
	Для Каждого Стр Из ТабМатериалов Цикл
		Стр.Распределено = Ложь;
	КонецЦикла;

	// Собираем уникальные даты выпуска
	ДатыВыпуска = Новый Соответствие;
	Для Каждого СтрВып Из ТабВыпуска Цикл
		ДатыВыпуска.Вставить(НачалоДня(СтрВып.ДатаПроизводства), Истина);
	КонецЦикла;

	// === Фаза 0: точечное распределение по конкретной дате ===
	// Если ДатаРасхода материала точно совпадает с ДатаПроизводства выпуска —
	// привязываем материал только к выпуску этой даты.
	Для Каждого КлючДаты Из ДатыВыпуска Цикл
		ТекДата = КлючДаты.Ключ;

		// Выпуск этой даты
		ВыпускДаты = Новый ТаблицаЗначений;
		Для Каждого Кол Из ТабВыпуска.Колонки Цикл
			ВыпускДаты.Колонки.Добавить(Кол.Имя);
		КонецЦикла;
		КоличествоВыпускаДаты = 0;
		Для Каждого СтрВып Из ТабВыпуска Цикл
			Если НачалоДня(СтрВып.ДатаПроизводства) = ТекДата Тогда
				НоваяСтр = ВыпускДаты.Добавить();
				Для Каждого Кол Из ТабВыпуска.Колонки Цикл
					НоваяСтр[Кол.Имя] = СтрВып[Кол.Имя];
				КонецЦикла;
				КоличествоВыпускаДаты = КоличествоВыпускаДаты + СтрВып.Количество;
			КонецЕсли;
		КонецЦикла;

		Если КоличествоВыпускаДаты = 0 Тогда
			Продолжить;
		КонецЕсли;

		// Материалы с ДатаРасхода = этой дате
		Для Каждого СтрМат Из ТабМатериалов Цикл
			Если СтрМат.Распределено Тогда
				Продолжить;
			КонецЕсли;
			Если НачалоДня(СтрМат.ДатаРасхода) <> ТекДата Тогда
				Продолжить;
			КонецЕсли;

			РаспределениеМатериала = ПолучитьРаспределениеПоВыпуску(
				СтрМат.Количество,
				ВыпускДаты,
				КоличествоВыпускаДаты,
				СтрМат.ИзмеряетсяВШтуках
			);

			Если РаспределениеМатериала.Таблица.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;

			Подразделение = СтрМат.Подразделение;
			Материал = СтрМат.Номенклатура;
			ХарактеристикаМатериала = ?(ЗначениеЗаполнено(СтрМат.Характеристика), СтрМат.Характеристика, Неопределено);
			НазначениеМатериала = ?(ЗначениеЗаполнено(СтрМат.Назначение), СтрМат.Назначение, Неопределено);
			СерияМатериала = ?(ЗначениеЗаполнено(СтрМат.Серия), СтрМат.Серия, Неопределено);
			СтатьяКалькуляцииМатериала = СтрМат.СтатьяКалькуляции;

			Для Каждого СтрокаРаспределения Из РаспределениеМатериала.Таблица Цикл

				СтрВыпРасп = СтрокаРаспределения.СтрокаВыпуска;
				КоличествоМатКРаспределению = СтрокаРаспределения.Количество;

				Если НЕ КоличествоМатКРаспределению > 0 Тогда
					Продолжить;
				КонецЕсли;

				Запись = Набор.Добавить();
				Запись.Период = ДокОбъект.Дата;
				Запись.Регистратор = ДокОбъект.Ссылка;
				Запись.Сторно                      = Ложь;
				Запись.КорректируемыйРегистратор   = Неопределено;
				Запись.ДатаПроизводства = СтрВыпРасп.ДатаПроизводства;
				Запись.ДатаРасхода = СтрМат.ДатаРасхода;
				Запись.СкладПолучатель = СтрВыпРасп.Получатель;
				Запись.Подразделение = Подразделение;

				Запись.ПродукцияКонечногоЭтапа = ПродукцияКонечногоЭтапа;
				Запись.ХарактеристикаПродукцииКонечногоЭтапа = ХарактеристикаПродукцииКонечногоЭтапа;
				Запись.НазначениеПродукцииКонечногоЭтапа = НазначениеПродукцииЭтапа;

				Запись.Продукция = ПродукцияКонечногоЭтапа;
				Запись.ХарактеристикаПродукции = ХарактеристикаПродукцииКонечногоЭтапа;
				Запись.НазначениеПродукции = НазначениеПродукцииЭтапа;
				Запись.СерияПродукции = ?(ЗначениеЗаполнено(СтрВыпРасп.Серия), СтрВыпРасп.Серия, Неопределено);

				Запись.Материал = Материал;
				Запись.ХарактеристикаМатериала = ХарактеристикаМатериала;
				Запись.НазначениеМатериала = НазначениеМатериала;
				Запись.СерияМатериала = СерияМатериала;
				Запись.СтатьяКалькуляцииМатериала = СтатьяКалькуляцииМатериала;
				Запись.ЗаказНаПроизводство = ЗаказНаПроизводство;
				Запись.НаправлениеДеятельности = НаправлениеДеятельности;
				Запись.Договор = Договор;
				Запись.ИнформацияДляПечати = ИнформацияДляПечати;
				Запись.ГруппаКоррекции             = Неопределено;
				Если Набор.Метаданные().Реквизиты.Найти("ПоНормам") <> Неопределено Тогда
					Запись.ПоНормам = Ложь;
				КонецЕсли;

				Запись.КоличествоМатериала = КоличествоМатКРаспределению;
				Запись.КоличествоПродукции = КоличествоВыпускаДаты;
				Запись.КоличествоПродукцииКонечногоЭтапа = КоличествоВыпускаОбщее;

			КонецЦикла;

			СтрМат.Распределено = Истина;
		КонецЦикла;
	КонецЦикла;

	// === Фаза А: помесячное распределение нераспределённых в Фазе 0 ===
	// Материалы, у которых ДатаРасхода не совпала ни с одной датой выпуска,
	// но МесяцРасхода совпадает с месяцем выпуска — распределяем по выпуску месяца.
	Для Каждого КлючМесяца Из МесяцыВыпуска Цикл
		ТекМесяц = КлючМесяца.Ключ;

		// Выпуск этого месяца
		ВыпускМесяца = Новый ТаблицаЗначений;
		Для Каждого Кол Из ТабВыпуска.Колонки Цикл
			ВыпускМесяца.Колонки.Добавить(Кол.Имя);
		КонецЦикла;
		КоличествоВыпускаМесяца = 0;
		Для Каждого СтрВып Из ТабВыпуска Цикл
			Если СтрВып.МесяцПроизводства = ТекМесяц Тогда
				НоваяСтр = ВыпускМесяца.Добавить();
				Для Каждого Кол Из ТабВыпуска.Колонки Цикл
					НоваяСтр[Кол.Имя] = СтрВып[Кол.Имя];
				КонецЦикла;
				КоличествоВыпускаМесяца = КоличествоВыпускаМесяца + СтрВып.Количество;
			КонецЕсли;
		КонецЦикла;

		Если КоличествоВыпускаМесяца = 0 Тогда
			Продолжить;
		КонецЕсли;

		// Материалы этого месяца (только нераспределённые)
		Для Каждого СтрМат Из ТабМатериалов Цикл
			Если СтрМат.Распределено Тогда
				Продолжить;
			КонецЕсли;
			Если СтрМат.МесяцРасхода <> ТекМесяц Тогда
				Продолжить;
			КонецЕсли;

			РаспределениеМатериала = ПолучитьРаспределениеПоВыпуску(
				СтрМат.Количество,
				ВыпускМесяца,
				КоличествоВыпускаМесяца,
				СтрМат.ИзмеряетсяВШтуках
			);

			Если РаспределениеМатериала.Таблица.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;

			Подразделение = СтрМат.Подразделение;
			Материал = СтрМат.Номенклатура;
			ХарактеристикаМатериала = ?(ЗначениеЗаполнено(СтрМат.Характеристика), СтрМат.Характеристика, Неопределено);
			НазначениеМатериала = ?(ЗначениеЗаполнено(СтрМат.Назначение), СтрМат.Назначение, Неопределено);
			СерияМатериала = ?(ЗначениеЗаполнено(СтрМат.Серия), СтрМат.Серия, Неопределено);
			СтатьяКалькуляцииМатериала = СтрМат.СтатьяКалькуляции;

			Для Каждого СтрокаРаспределения Из РаспределениеМатериала.Таблица Цикл

				СтрВыпРасп = СтрокаРаспределения.СтрокаВыпуска;
				КоличествоМатКРаспределению = СтрокаРаспределения.Количество;

				Если НЕ КоличествоМатКРаспределению > 0 Тогда
					Продолжить;
				КонецЕсли;

				Запись = Набор.Добавить();
				Запись.Период = ДокОбъект.Дата;
				Запись.Регистратор = ДокОбъект.Ссылка;
				Запись.Сторно                      = Ложь;
				Запись.КорректируемыйРегистратор   = Неопределено;
				Запись.ДатаПроизводства = СтрВыпРасп.ДатаПроизводства;
				Запись.ДатаРасхода = СтрМат.ДатаРасхода;
				Запись.СкладПолучатель = СтрВыпРасп.Получатель;
				Запись.Подразделение = Подразделение;

				Запись.ПродукцияКонечногоЭтапа = ПродукцияКонечногоЭтапа;
				Запись.ХарактеристикаПродукцииКонечногоЭтапа = ХарактеристикаПродукцииКонечногоЭтапа;
				Запись.НазначениеПродукцииКонечногоЭтапа = НазначениеПродукцииЭтапа;

				Запись.Продукция = ПродукцияКонечногоЭтапа;
				Запись.ХарактеристикаПродукции = ХарактеристикаПродукцииКонечногоЭтапа;
				Запись.НазначениеПродукции = НазначениеПродукцииЭтапа;
				Запись.СерияПродукции = ?(ЗначениеЗаполнено(СтрВыпРасп.Серия), СтрВыпРасп.Серия, Неопределено);

				Запись.Материал = Материал;
				Запись.ХарактеристикаМатериала = ХарактеристикаМатериала;
				Запись.НазначениеМатериала = НазначениеМатериала;
				Запись.СерияМатериала = СерияМатериала;
				Запись.СтатьяКалькуляцииМатериала = СтатьяКалькуляцииМатериала;
				Запись.ЗаказНаПроизводство = ЗаказНаПроизводство;
				Запись.НаправлениеДеятельности = НаправлениеДеятельности;
				Запись.Договор = Договор;
				Запись.ИнформацияДляПечати = ИнформацияДляПечати;
				Запись.ГруппаКоррекции             = Неопределено;
				Если Набор.Метаданные().Реквизиты.Найти("ПоНормам") <> Неопределено Тогда
					Запись.ПоНормам = Ложь;
				КонецЕсли;

				Запись.КоличествоМатериала = КоличествоМатКРаспределению;
				Запись.КоличествоПродукции = КоличествоВыпускаМесяца;
				Запись.КоличествоПродукцииКонечногоЭтапа = КоличествоВыпускаОбщее;

			КонецЦикла;

			СтрМат.Распределено = Истина;
		КонецЦикла;
	КонецЦикла;

	// === Фаза Б: fallback — нераспределенные материалы по ВСЕМУ выпуску ===
	Для Каждого СтрМат Из ТабМатериалов Цикл
		Если СтрМат.Распределено Тогда
			Продолжить;
		КонецЕсли;

		РаспределениеМатериала = ПолучитьРаспределениеПоВыпуску(
			СтрМат.Количество,
			ТабВыпуска,
			КоличествоВыпускаОбщее,
			СтрМат.ИзмеряетсяВШтуках
		);

		Если РаспределениеМатериала.Таблица.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;

		Подразделение = СтрМат.Подразделение;
		Материал = СтрМат.Номенклатура;
		ХарактеристикаМатериала = ?(ЗначениеЗаполнено(СтрМат.Характеристика), СтрМат.Характеристика, Неопределено);
		НазначениеМатериала = ?(ЗначениеЗаполнено(СтрМат.Назначение), СтрМат.Назначение, Неопределено);
		СерияМатериала = ?(ЗначениеЗаполнено(СтрМат.Серия), СтрМат.Серия, Неопределено);
		СтатьяКалькуляцииМатериала = СтрМат.СтатьяКалькуляции;

		Для Каждого СтрокаРаспределения Из РаспределениеМатериала.Таблица Цикл

			СтрВыпРасп = СтрокаРаспределения.СтрокаВыпуска;
			КоличествоМатКРаспределению = СтрокаРаспределения.Количество;

			Если НЕ КоличествоМатКРаспределению > 0 Тогда
				Продолжить;
			КонецЕсли;

			Запись = Набор.Добавить();
			Запись.Период = ДокОбъект.Дата;
			Запись.Регистратор = ДокОбъект.Ссылка;
			Запись.Сторно                      = Ложь;
			Запись.КорректируемыйРегистратор   = Неопределено;
			Запись.ДатаПроизводства = СтрВыпРасп.ДатаПроизводства;
			Запись.ДатаРасхода = СтрМат.ДатаРасхода;
			Запись.СкладПолучатель = СтрВыпРасп.Получатель;
			Запись.Подразделение = Подразделение;

			Запись.ПродукцияКонечногоЭтапа = ПродукцияКонечногоЭтапа;
			Запись.ХарактеристикаПродукцииКонечногоЭтапа = ХарактеристикаПродукцииКонечногоЭтапа;
			Запись.НазначениеПродукцииКонечногоЭтапа = НазначениеПродукцииЭтапа;

			Запись.Продукция = ПродукцияКонечногоЭтапа;
			Запись.ХарактеристикаПродукции = ХарактеристикаПродукцииКонечногоЭтапа;
			Запись.НазначениеПродукции = НазначениеПродукцииЭтапа;
			Запись.СерияПродукции = ?(ЗначениеЗаполнено(СтрВыпРасп.Серия), СтрВыпРасп.Серия, Неопределено);

			Запись.Материал = Материал;
			Запись.ХарактеристикаМатериала = ХарактеристикаМатериала;
			Запись.НазначениеМатериала = НазначениеМатериала;
			Запись.СерияМатериала = СерияМатериала;
			Запись.СтатьяКалькуляцииМатериала = СтатьяКалькуляцииМатериала;
			Запись.ЗаказНаПроизводство = ЗаказНаПроизводство;
			Запись.НаправлениеДеятельности = НаправлениеДеятельности;
			Запись.Договор = Договор;
			Запись.ИнформацияДляПечати = ИнформацияДляПечати;
			Запись.ГруппаКоррекции             = Неопределено;
			Если Набор.Метаданные().Реквизиты.Найти("ПоНормам") <> Неопределено Тогда
				Запись.ПоНормам = Ложь;
			КонецЕсли;

			Запись.КоличествоМатериала = КоличествоМатКРаспределению;
			Запись.КоличествоПродукции = КоличествоВыпускаОбщее;
			Запись.КоличествоПродукцииКонечногоЭтапа = КоличествоВыпускаОбщее;

		КонецЦикла;
	КонецЦикла;

	Набор.Записать();

КонецПроцедуры

// Точка входа для развертки полуфабрикатов в новом регистре А1_МатСоставИзделийПоПериодам
// Аналог РазвернутьПолуфабрикатыДокумента, но работает с новым регистром.
Процедура РазвернутьПолуфабрикатыДокументаПоПериодам(ДокОбъект) Экспорт

	Если ДокОбъект = Неопределено Тогда
		Возврат;
	КонецЕсли;

	РезультатВыпуска = ПолучитьТаблицуВыпуска(ДокОбъект);
	ТабВыпуска = РезультатВыпуска.Таблица;
	Если НЕ ТабВыпуска.Количество() > 0 Тогда
		Возврат;
	КонецЕсли;

	НазначениеПродукцииЭтапа = РезультатВыпуска.НазначениеПродукцииЭтапа;

	ЗаказНаПроизводство = Документы.ЗаказНаПроизводство2_2.ПустаяСсылка();
	ДокументОснование = Документы.ЗаказКлиента.ПустаяСсылка();
	Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	ИнформацияДляПечати = "";
	НаправлениеДеятельности = Справочники.НаправленияДеятельности.ПустаяСсылка();

	Если ЕстьРеквизитИлиСвойствоОбъекта(ДокОбъект, "Распоряжение") Тогда
		ЗаказНаПроизводство = ?(ЗначениеЗаполнено(ДокОбъект.Распоряжение), ДокОбъект.Распоряжение, Документы.ЗаказНаПроизводство2_2.ПустаяСсылка());
		ДокументОснование = ?(ЗначениеЗаполнено(ЗаказНаПроизводство.ДокументОснование), ЗаказНаПроизводство.ДокументОснование, Документы.ЗаказКлиента.ПустаяСсылка());

		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
			Договор = ?(ЗначениеЗаполнено(ДокументОснование.Договор), ДокументОснование.Договор, Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
			ИнформацияДляПечати = ?(ЗначениеЗаполнено(ДокументОснование.ДополнительнаяИнформация), ДокументОснование.ДополнительнаяИнформация, "");
			Если ЗначениеЗаполнено(ЗаказНаПроизводство.НаправлениеДеятельности) Тогда
				НаправлениеДеятельности = ЗаказНаПроизводство.НаправлениеДеятельности;
			ИначеЕсли ЗначениеЗаполнено(ДокументОснование.НаправлениеДеятельности) Тогда
				НаправлениеДеятельности = ДокументОснование.НаправлениеДеятельности;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	РазвернутьПФПокаЕстьПоПериодам(ДокОбъект, НазначениеПродукцииЭтапа, Договор, НаправлениеДеятельности, ИнформацияДляПечати, РезультатВыпуска);
	РазвернутьПФПоНормамПоПериодам(ДокОбъект, НазначениеПродукцииЭтапа, Договор, НаправлениеДеятельности, ИнформацияДляПечати, РезультатВыпуска);

КонецПроцедуры

// Аналог НайтиКандидатаПолуфабриката, но ищет в А1_МатСоставИзделийПоПериодам
Функция НайтиКандидатаПФПоПериодам(ДокСсылка, ПропущенныеКандидаты = Неопределено)

	ЗапросПотребностьДокумента = Новый Запрос(
	"ВЫБРАТЬ
	|   Рег.Материал                        КАК Номенклатура,
	|   Рег.ХарактеристикаМатериала         КАК Характеристика,
	|   Рег.СерияМатериала                  КАК Серия,
	|   Рег.НазначениеМатериала             КАК Назначение,
	|	МАКСИМУМ(Рег.СтатьяКалькуляцииМатериала)      КАК Статья,
	|   СУММА(Рег.КоличествоМатериала)      КАК ЧистыйРасходПФ,
	|   МАКСИМУМ(Рег.ДатаПроизводства)      КАК МаксДатаПроизводства
	|ИЗ
	|   РегистрНакопления.А1_МатСоставИзделийПоПериодам КАК Рег
	|ГДЕ
	|	Рег.Регистратор = &Док
	|   И Рег.Активность
	|СГРУППИРОВАТЬ ПО
	|   Рег.Материал,
	|   Рег.ХарактеристикаМатериала,
	|   Рег.СерияМатериала,
	|   Рег.НазначениеМатериала
	|ИМЕЮЩИЕ
	|   Окр(СУММА(Рег.КоличествоМатериала), 3) > 0");

	ЗапросПотребностьДокумента.УстановитьПараметр("Док", ДокСсылка);
	ВыборкаПотребность = ЗапросПотребностьДокумента.Выполнить().Выбрать();

	Пока ВыборкаПотребность.Следующий() Цикл

		ЗапросВыпускающиеИсточники = Новый Запрос(
		"ВЫБРАТЬ
		|   ВЫБОР
		|       КОГДА Источник.Сторно
		|           ТОГДА Источник.КорректируемыйРегистратор
		|       ИНАЧЕ Источник.Регистратор
		|   КОНЕЦ                                   КАК ИсточникРег,
		|   Источник.Продукция        КАК Продукция,
		|   Источник.ХарактеристикаПродукции        КАК Характеристика,
		|   Источник.СерияПродукции                 КАК Серия,
		|   Источник.НазначениеПродукции            КАК Назначение,
		|   Источник.Подразделение            	    КАК Подразделение,
		|   СУММА(Источник.КоличествоПродукции)     КАК ЧистыйВыпуск,
        |   СУММА(Источник.КоличествоМатериала)     КАК ЕстьМатериалы,
		|	МИНИМУМ(Источник.ДатаПроизводства) КАК ДатаДляСортировки
		|ИЗ
		|   РегистрНакопления.А1_МатСоставИзделийПоПериодам КАК Источник
		|ГДЕ
		|	(ВЫБОР КОГДА Источник.Сторно ТОГДА Источник.КорректируемыйРегистратор ИНАЧЕ Источник.Регистратор КОНЕЦ) <> &Док
		|   И Источник.Продукция = &Продукция
		|   И Источник.ХарактеристикаПродукции = &Характеристика
		|   И Источник.СерияПродукции = &Серия
		|   И Источник.НазначениеПродукции = &Назначение
		|   И Источник.Активность
		|   И Источник.ДатаПроизводства <= &МаксДатаПроизводства
		|СГРУППИРОВАТЬ ПО
		|   ВЫБОР
		|       КОГДА Источник.Сторно
		|           ТОГДА Источник.КорректируемыйРегистратор
		|       ИНАЧЕ Источник.Регистратор
		|   КОНЕЦ,
		|   Источник.Продукция,
		|   Источник.ХарактеристикаПродукции,
		|   Источник.СерияПродукции,
		|   Источник.НазначениеПродукции,
		|	Источник.Подразделение
		|ИМЕЮЩИЕ
		|	Окр(СУММА(Источник.КоличествоПродукции), 3) > 0
		|	И Окр(СУММА(Источник.КоличествоМатериала), 3) > 0
		|УПОРЯДОЧИТЬ ПО
		|	ДатаДляСортировки");

		ЗапросВыпускающиеИсточники.УстановитьПараметр("Характеристика", ВыборкаПотребность.Характеристика);
		ЗапросВыпускающиеИсточники.УстановитьПараметр("Серия", ВыборкаПотребность.Серия);
		ЗапросВыпускающиеИсточники.УстановитьПараметр("Назначение", ВыборкаПотребность.Назначение);
		ЗапросВыпускающиеИсточники.УстановитьПараметр("Док", ДокСсылка);
		ЗапросВыпускающиеИсточники.УстановитьПараметр("Продукция", ВыборкаПотребность.Номенклатура);
		ЗапросВыпускающиеИсточники.УстановитьПараметр("МаксДатаПроизводства", ВыборкаПотребность.МаксДатаПроизводства);

		// BSLLS:CreateQueryInCycle-off
		Источники = ЗапросВыпускающиеИсточники.Выполнить().Выгрузить();
		Если Источники.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		// BSLLS:CreateQueryInCycle-on

		Ключ = КлючПФ(
			ВыборкаПотребность.Номенклатура,
			ВыборкаПотребность.Характеристика,
			ВыборкаПотребность.Серия,
			ВыборкаПотребность.Назначение,
			ВыборкаПотребность.Статья);
		КлючСтрока = СтрШаблон("%1|%2|%3|%4|%5",
			Строка(Ключ.Номенклатура),
			Строка(Ключ.Характеристика),
			Строка(Ключ.Серия),
			Строка(Ключ.Назначение),
			Строка(Ключ.Статья));

		Если ПропущенныеКандидаты <> Неопределено И ПропущенныеКандидаты.Получить(КлючСтрока) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Результат = Новый Структура;
		Результат.Вставить("Ключ", Ключ);
		Результат.Вставить("ЧистыйРасходВДоке", ВыборкаПотребность.ЧистыйРасходПФ);
		Результат.Вставить("Источники", Источники);
		Возврат Результат;
	КонецЦикла;

	Возврат Неопределено;
КонецФункции

// Аналог ПолучитьУдельныйМатСоставПоИсточнику, но читает из А1_МатСоставИзделийПоПериодам
Функция ПолучитьУдельныйМатСоставПоИсточникуПоПериодам(ИсточникРег, КлючПФ)

	РеквизитВспомогательныйМатериал = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ВспомогательныйМатериал_a6ab234c1428470d81365a2df40445b9");

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|   МАКСИМУМ(Рег.КоличествоПродукцииКонечногоЭтапа)	   КАК КоличествоВыпускаПФ,
	|   Рег.Материал,
	|   Рег.ХарактеристикаМатериала        КАК ХарМат,
	|   Рег.СерияМатериала                 КАК СерМат,
	|   Рег.НазначениеМатериала            КАК НазМат,
	|   МАКСИМУМ(Рег.СтатьяКалькуляцииМатериала) КАК Статья,
	|   Рег.Подразделение            	   КАК Подразделение,
	|   МАКСИМУМ(Рег.Продукция)            КАК Продукция,
	|   СУММА(Рег.КоличествоМатериала)     КАК ЧистыйРасходМат,
	|   ЕСТЬNULL(Рег.Материал.ЕдиницаИзмерения.ТипИзмеряемойВеличины = &ТипШтук, ЛОЖЬ) КАК ИзмеряетсяВШтуках
	|ИЗ
	|   РегистрНакопления.А1_МатСоставИзделийПоПериодам КАК Рег
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК ДопРекв
	|		ПО ДопРекв.Ссылка = Рег.Материал
	|			И ДопРекв.Свойство = &РеквизитВспомогательныйМатериал
	|ГДЕ
	|   (ВЫБОР КОГДА Рег.Сторно ТОГДА Рег.КорректируемыйРегистратор ИНАЧЕ Рег.Регистратор КОНЕЦ) = &Источник
	|   И Рег.ПродукцияКонечногоЭтапа = &Продукция
	|   И Рег.НазначениеПродукции = &Назначение
	|   И Рег.ХарактеристикаПродукции = &Характеристика
	|   И Рег.СерияПродукции = &Серия
	|   И Рег.Активность
	|   И НЕ Рег.ПоНормам
	|   И (ДопРекв.Значение ЕСТЬ NULL
	|		ИЛИ ДопРекв.Значение <> ИСТИНА)
	|СГРУППИРОВАТЬ ПО
	|   Рег.Материал,
	|   Рег.ХарактеристикаМатериала,
	|   Рег.СерияМатериала,
	|   Рег.НазначениеМатериала,
	|   Рег.Подразделение,
	|	Рег.ПродукцияКонечногоЭтапа
	|ИМЕЮЩИЕ
	|   МАКСИМУМ(Рег.КоличествоПродукцииКонечногоЭтапа) > 0
	|   И СУММА(Рег.КоличествоМатериала) > 0");

    Запрос.УстановитьПараметр("Источник", ИсточникРег);
    Запрос.УстановитьПараметр("Продукция", КлючПФ.Номенклатура);
    Запрос.УстановитьПараметр("Назначение", КлючПФ.Назначение);
    Запрос.УстановитьПараметр("Характеристика", КлючПФ.Характеристика);
    Запрос.УстановитьПараметр("Серия", КлючПФ.Серия);
    Запрос.УстановитьПараметр("ТипШтук",  Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук);
    Запрос.УстановитьПараметр("РеквизитВспомогательныйМатериал", РеквизитВспомогательныйМатериал);

	Таб = Запрос.Выполнить().Выгрузить();

	Если Таб.Количество() = 0 Тогда
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;

    Если Таб.Колонки.Найти("Удельное") = Неопределено Тогда
        Таб.Колонки.Добавить("Удельное");
    КонецЕсли;

	Для Каждого Стр Из Таб Цикл
		Если Стр.КоличествоВыпускаПФ = 0 Тогда Продолжить КонецЕсли;
        Стр.Удельное = Стр.ЧистыйРасходМат / Стр.КоличествоВыпускаПФ;
     КонецЦикла;

    Возврат Таб;
КонецФункции

// Аналог РазвернутьПолуфабрикатыПокаЕсть, но работает с А1_МатСоставИзделийПоПериодам
Процедура РазвернутьПФПокаЕстьПоПериодам(ДокОбъект, НазначениеПродукцииЭтапа, Договор, НаправлениеДеятельности, ИнформацияДляПечати, РезультатВыпуска)

        МинимальноеУменьшение = 0.001;
        ПредыдущиеКандидаты = Новый Соответствие;
        ПредыдущиеДанные = Неопределено;
        ПропущенныеКандидаты = Новый Соответствие;

    Пока Истина Цикл

		Кандидат = НайтиКандидатаПФПоПериодам(ДокОбъект.Ссылка, ПропущенныеКандидаты);
		Если Кандидат = Неопределено Тогда
			Прервать;
		КонецЕсли;

        Источники = Кандидат.Источники;
        СуммаИст = 0;
        Для Каждого Стр ИЗ Источники Цикл
            СуммаИст = СуммаИст + Стр.ЧистыйВыпуск;
        КонецЦикла;

        Требуется = Мин(Кандидат.ЧистыйРасходВДоке, СуммаИст);
        Если Требуется < МинимальноеУменьшение Тогда
            Сообщить("Развертка ПФ (по периодам): остаток менее " + Формат(МинимальноеУменьшение, "ЧРД=.; ЧГ=3") + ". Документ " + Строка(ДокОбъект.Ссылка));
            Прервать;
        КонецЕсли;

        Ключ = Кандидат.Ключ;
        КлючСтрока = СтрШаблон("%1|%2|%3|%4|%5",
            Строка(Ключ.Номенклатура),
            Строка(Ключ.Характеристика),
            Строка(Ключ.Серия),
            Строка(Ключ.Назначение),
            Строка(Ключ.Статья));

        ПредыдущиеДанные = ПредыдущиеКандидаты.Получить(КлючСтрока);
        Если ПредыдущиеДанные <> Неопределено Тогда
            СтароеТребуется = ПредыдущиеДанные.Требуется;
            СтароеСуммаИст = ПредыдущиеДанные.СуммаИст;
            Если (СтароеТребуется - Требуется) < МинимальноеУменьшение И Окр(СтароеСуммаИст - СуммаИст, 3) = 0 Тогда
                Сообщить("Развертка ПФ (по периодам): нет прогресса для " + Строка(Ключ.Номенклатура) + ". Документ " + Строка(ДокОбъект.Ссылка) + ". Пропускаем и продолжаем.");
                ПропущенныеКандидаты.Вставить(КлючСтрока, Истина);
                Продолжить;
            КонецЕсли;
        КонецЕсли;

        ПредыдущиеКандидаты.Вставить(КлючСтрока, Новый Структура("Требуется, СуммаИст", Требуется, СуммаИст));

		РазвернутьПФПоПериодам(
			ДокОбъект,
			Кандидат.Ключ,
			Требуется,
			Источники,
			НазначениеПродукцииЭтапа,
			Договор,
			НаправлениеДеятельности,
			ИнформацияДляПечати,
			РезультатВыпуска
		);
	КонецЦикла;

КонецПроцедуры

// Аналог РазвернутьПолуфабрикат, но пишет в А1_МатСоставИзделийПоПериодам
Процедура РазвернутьПФПоПериодам(
	ДокОбъект,
	КлючПФ,
	Требуется,
	ТабИсточников,
	НазначениеПродукцииЭтапа,
	Договор,
	НаправлениеДеятельности,
	ИнформацияДляПечати,
	РезультатВыпуска
	)

	ТабВыпуска = РезультатВыпуска.Таблица;
	ПродукцияКонечногоЭтапа = РезультатВыпуска.ПродукцияКонечногоЭтапа;
	НазначениеПродукцииЭтапа = РезультатВыпуска.НазначениеПродукцииЭтапа;
	ХарактеристикаПродукцииКонечногоЭтапа = РезультатВыпуска.ХарактеристикаПродукцииКонечногоЭтапа;
	КоличествоПродукцииКонечногоЭтапа = РезультатВыпуска.КоличествоВыпускаОбщее;

	ОстПеренести = 0;
	ПереносПоИсточнику = 0;
    ДатаПроизводства = Неопределено;
	СкладПолучатель = Неопределено;
	ПереносПоСтрокеВыпуска = 0;

	Группа = Новый УникальныйИдентификатор();

	Набор = РегистрыНакопления.А1_МатСоставИзделийПоПериодам.СоздатьНаборЗаписей();
	Набор.Отбор.Регистратор.Установить(ДокОбъект.Ссылка);
	Набор.Прочитать();

	ОстПеренести = Требуется;

	Для Каждого Ист Из ТабИсточников Цикл

		Если ОстПеренести <= 0 Тогда Прервать; КонецЕсли;
		Если Ист.ЧистыйВыпуск <= 0 Тогда Продолжить; КонецЕсли;

		Если Ист = ТабИсточников[ТабИсточников.Количество()-1] Тогда
			ПереносПоИсточнику = ОстПеренести;
		Иначе
			ПереносПоИсточнику = Мин(ОстПеренести, Ист.ЧистыйВыпуск);
		КонецЕсли;

		УдельныйСостав = ПолучитьУдельныйМатСоставПоИсточникуПоПериодам(Ист.ИсточникРег, КлючПФ);
		Если УдельныйСостав.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		ГруппаДляИсточника = Группа;

		РаспределениеВыпуска = ПолучитьРаспределениеПоВыпуску(
		ПереносПоИсточнику,
		ТабВыпуска,
		КоличествоПродукцииКонечногоЭтапа,
		Истина
		);

		Если РаспределениеВыпуска.Распределено = 0 Тогда
			Продолжить;
		КонецЕсли;

		Для Каждого СтрокаМатериала Из УдельныйСостав Цикл
			Для Каждого СтрокаРаспределения Из РаспределениеВыпуска.Таблица Цикл

				СтрВып = СтрокаРаспределения.СтрокаВыпуска;
				ДатаПроизводства = СтрВып.ДатаПроизводства;
				СкладПолучатель = СтрВып.Получатель;

				ПереносПоСтрокеВыпуска = СтрокаРаспределения.Количество;
				Если ПереносПоСтрокеВыпуска = 0 Тогда
					Продолжить;
				КонецЕсли;
				Если СтрокаМатериала.ИзмеряетсяВШтуках Тогда
					КолМат = Окр(СтрокаМатериала.Удельное * ПереносПоСтрокеВыпуска);
				Иначе
					КолМат = СтрокаМатериала.Удельное * ПереносПоСтрокеВыпуска;
				КонецЕсли;
				// a) Добавляем расход материалов
				ДобавитьЗаписьМатСостПоПериодам(
				Набор,
				ДокОбъект.Дата,
				ДокОбъект.Ссылка,
				КлючПФ,
				СтрокаМатериала.Материал,
				СтрокаМатериала.ХарМат,
				СтрокаМатериала.СерМат,
				СтрокаМатериала.НазМат,
				СтрокаМатериала.Статья,
				СтрокаМатериала.Подразделение,
				ПереносПоСтрокеВыпуска,
				КолМат,
				Ложь,
				Неопределено,
				ГруппаДляИсточника,
				НазначениеПродукцииЭтапа,
				СтрокаМатериала.Продукция,
				Договор,
				НаправлениеДеятельности,
				ИнформацияДляПечати,
				ПродукцияКонечногоЭтапа,
				КоличествоПродукцииКонечногоЭтапа,
				ХарактеристикаПродукцииКонечногоЭтапа,
				НазначениеПродукцииЭтапа,
				СкладПолучатель,
				ДатаПроизводства,
				Ложь
				);

				// b) Сторно у источника
				ДобавитьЗаписьМатСостПоПериодам(
				Набор,
				ДокОбъект.Дата,
				ДокОбъект.Ссылка,
				КлючПФ,
				СтрокаМатериала.Материал,
				СтрокаМатериала.ХарМат,
				СтрокаМатериала.СерМат,
				СтрокаМатериала.НазМат,
				СтрокаМатериала.Статья,
				СтрокаМатериала.Подразделение,
				-ПереносПоСтрокеВыпуска,
				-КолМат,
				Истина,
				Ист.ИсточникРег,
				ГруппаДляИсточника,
				Неопределено,
				СтрокаМатериала.Продукция,
				Договор,
				НаправлениеДеятельности,
				ИнформацияДляПечати,
				ПродукцияКонечногоЭтапа,
				КоличествоПродукцииКонечногоЭтапа,
				ХарактеристикаПродукцииКонечногоЭтапа,
				НазначениеПродукцииЭтапа,
				СкладПолучатель,
				ДатаПроизводства,
				Ложь
				);
			КонецЦикла;
		КонецЦикла;

		Для Каждого СтрокаРаспределения Из РаспределениеВыпуска.Таблица Цикл

			СтрВып = СтрокаРаспределения.СтрокаВыпуска;
			ДатаПроизводства = СтрВып.ДатаПроизводства;
			СкладПолучатель = СтрВып.Получатель;

			ПереносПоСтрокеВыпуска = СтрокаРаспределения.Количество;
			Если ПереносПоСтрокеВыпуска = 0 Тогда
				Продолжить;
			КонецЕсли;

			// Сторнируем ПФ-материал в текущем документе
			ДобавитьЗаписьМатСостПоПериодам(
			Набор,
			ДокОбъект.Дата,
			ДокОбъект.Ссылка,
			КлючПФ,
			КлючПФ.Номенклатура,
			КлючПФ.Характеристика,
			КлючПФ.Серия,
			КлючПФ.Назначение,
			КлючПФ.Статья,
			ДокОбъект.Подразделение,
			0,
			-ПереносПоСтрокеВыпуска,
			Истина,
			ДокОбъект.Ссылка,
			ГруппаДляИсточника,
			Неопределено,
			Неопределено,
			Договор,
			НаправлениеДеятельности,
			ИнформацияДляПечати,
			ПродукцияКонечногоЭтапа,
			КоличествоПродукцииКонечногоЭтапа,
			ХарактеристикаПродукцииКонечногоЭтапа,
			НазначениеПродукцииЭтапа,
			СкладПолучатель,
			ДатаПроизводства,
			Ложь
			);
		КонецЦикла;

		ОстПеренести = ОстПеренести - РаспределениеВыпуска.Распределено;
		Набор.Записать();
	КонецЦикла;
КонецПроцедуры

// Аналог РазвернутьПолуфабрикатПоНормам, но работает с А1_МатСоставИзделийПоПериодам
Процедура РазвернутьПФПоНормамПоПериодам(ДокОбъект, НазначениеПродукцииЭтапа, Договор, НаправлениеДеятельности, ИнформацияДляПечати, РезультатВыпуска)

	ТабВыпуска = РезультатВыпуска.Таблица;
	ПродукцияКонечногоЭтапа = РезультатВыпуска.ПродукцияКонечногоЭтапа;
	ХарактеристикаПродукцииКонечногоЭтапа = РезультатВыпуска.ХарактеристикаПродукцииКонечногоЭтапа;
	КоличествоПродукцииКонечногоЭтапа = РезультатВыпуска.КоличествоВыпускаОбщее;

	Набор = РегистрыНакопления.А1_МатСоставИзделийПоПериодам.СоздатьНаборЗаписей();
	Набор.Отбор.Регистратор.Установить(ДокОбъект.Ссылка);
	Набор.Прочитать();

	Если КоличествоПродукцииКонечногоЭтапа = 0 Тогда
		Возврат;
	КонецЕсли;

	РеквизитВспомогательныйМатериал = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ВспомогательныйМатериал_a6ab234c1428470d81365a2df40445b9");

	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	МАКСИМУМ(ВЫБОР КОГДА НЕ Рег.Сторно ТОГДА Рег.Продукция ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КОНЕЦ)                            КАК Продукция,
		|	МАКСИМУМ(ВЫБОР КОГДА НЕ Рег.Сторно ТОГДА Рег.ХарактеристикаПродукции ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КОНЕЦ)              КАК ХарПродукции,
		|	МАКСИМУМ(ВЫБОР КОГДА НЕ Рег.Сторно ТОГДА Рег.СерияПродукции ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КОНЕЦ)                       КАК СерияПродукции,
		|	МАКСИМУМ(ВЫБОР КОГДА НЕ Рег.Сторно ТОГДА Рег.НазначениеПродукции ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КОНЕЦ)                  КАК НазначениеПродукции,
		|	МАКСИМУМ(Рег.СтатьяКалькуляцииМатериала) КАК Статья,
		|	Рег.Материал                             КАК Материал,
		|	Рег.ХарактеристикаМатериала              КАК ХарМатериала,
		|	Рег.СерияМатериала                       КАК СерМатериала,
		|	Рег.НазначениеМатериала                  КАК НазМатериала,
		|	МАКСИМУМ(Рег.Подразделение)              КАК Подразделение,
		|	МАКСИМУМ(ВЫБОР КОГДА НЕ Рег.Сторно ТОГДА Рег.КоличествоПродукции ИНАЧЕ 0 КОНЕЦ)           КАК КоличествоПродукции,
		|	СУММА(Рег.КоличествоМатериала)           КАК КоличествоМатериала,
		|	ЕСТЬNULL(Рег.Материал.ЕдиницаИзмерения.ТипИзмеряемойВеличины = &ТипШтук, ЛОЖЬ) КАК ИзмеряетсяВШтукахМатериала
		|ИЗ
		|	РегистрНакопления.А1_МатСоставИзделийПоПериодам КАК Рег
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК ДопРекв
		|		ПО ДопРекв.Ссылка = Рег.Материал
		|			И ДопРекв.Свойство = &РеквизитВспомогательныйМатериал
		|ГДЕ
		|	Рег.Регистратор = &Регистратор
		|	И Рег.ПоНормам = ЛОЖЬ
		|	И (ДопРекв.Значение ЕСТЬ NULL
		|		ИЛИ ДопРекв.Значение <> ИСТИНА)
		|	И Рег.Активность
		|СГРУППИРОВАТЬ ПО
		|	Рег.Материал,
		|	Рег.ХарактеристикаМатериала,
		|	Рег.СерияМатериала,
		|	Рег.НазначениеМатериала
		|ИМЕЮЩИЕ
		|	Окр(СУММА(Рег.КоличествоМатериала), 3) > 0");

	Запрос.УстановитьПараметр("Регистратор", ДокОбъект.Ссылка);
	Запрос.УстановитьПараметр("ТипШтук", Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук);
	Запрос.УстановитьПараметр("РеквизитВспомогательныйМатериал", РеквизитВспомогательныйМатериал);

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл

		Если Выборка.Материал = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если Выборка.Продукция = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		КоличествоМатериала = Выборка.КоличествоМатериала;
		Если КоличествоМатериала <= 0 Тогда
			Продолжить;
		КонецЕсли;

		РаспределениеВыпуска = ПолучитьРаспределениеПоВыпуску(
			КоличествоМатериала,
			ТабВыпуска,
			КоличествоПродукцииКонечногоЭтапа,
			Выборка.ИзмеряетсяВШтукахМатериала
		);
		Если РаспределениеВыпуска.Распределено = 0 Тогда
			Продолжить;
		КонецЕсли;

		КлючПФ = КлючПФ(Выборка.Продукция, Выборка.ХарПродукции, Выборка.СерияПродукции, Выборка.НазначениеПродукции, Выборка.Статья);
		Группа = Новый УникальныйИдентификатор();
		НормативныеСоставыПоДатам = Новый Соответствие;

		Для Каждого СтрокаРаспределения Из РаспределениеВыпуска.Таблица Цикл

			СтрВып = СтрокаРаспределения.СтрокаВыпуска;
			ДатаПроизводства = СтрВып.ДатаПроизводства;
			Если ДатаПроизводства = Неопределено Тогда
				ДатаПроизводства = ДокОбъект.Дата;
			КонецЕсли;
			СкладПолучатель = СтрВып.Получатель;

			ПереносПоСтрокеВыпуска = СтрокаРаспределения.Количество;
			Если ПереносПоСтрокеВыпуска = 0 Тогда
				Продолжить;
			КонецЕсли;

			НормативныйСостав = НормативныеСоставыПоДатам.Получить(ДатаПроизводства);
			Если НормативныйСостав = Неопределено Тогда
				НормативныйСостав = ПолучитьНормативныйСоставДоПокупных(Выборка.Материал, ДатаПроизводства);
				НормативныеСоставыПоДатам.Вставить(ДатаПроизводства, НормативныйСостав);
			КонецЕсли;

			СпецификацияНайдена = Ложь;
			Если НормативныйСостав.Количество() > 0 Тогда
				Для Каждого СтрокаМатериала Из НормативныйСостав Цикл
					Если СтрокаМатериала.Материал <> Выборка.Материал Тогда
						СпецификацияНайдена = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;

			Если НЕ СпецификацияНайдена Тогда
				Продолжить;
			КонецЕсли;

			МатериалыРазвернуты = Ложь;

			Для Каждого СтрокаМатериала Из НормативныйСостав Цикл

				Если СтрокаМатериала.Материал = Выборка.Материал Тогда
					Продолжить;
				КонецЕсли;

				ПодразделениеМатериала = ?(ЗначениеЗаполнено(СтрокаМатериала.Подразделение), СтрокаМатериала.Подразделение, Выборка.Подразделение);

				Если СтрокаМатериала.ИзмеряетсяВШтуках Тогда
					КолМат = Окр(СтрокаМатериала.Удельное * ПереносПоСтрокеВыпуска);
				Иначе
					КолМат = СтрокаМатериала.Удельное * ПереносПоСтрокеВыпуска;
				КонецЕсли;

				Если КолМат <= 0 Тогда
					Продолжить;
				КонецЕсли;

				ДобавитьЗаписьМатСостПоПериодам(
					Набор,
					ДокОбъект.Дата,
					ДокОбъект.Ссылка,
					КлючПФ,
					СтрокаМатериала.Материал,
					СтрокаМатериала.ХарМат,
					СтрокаМатериала.СерМат,
					СтрокаМатериала.НазМат,
					СтрокаМатериала.Статья,
					ПодразделениеМатериала,
					ПереносПоСтрокеВыпуска,
					КолМат,
					Ложь,
					Неопределено,
					Группа,
					НазначениеПродукцииЭтапа,
					СтрокаМатериала.Продукция,
					Договор,
					НаправлениеДеятельности,
					ИнформацияДляПечати,
					ПродукцияКонечногоЭтапа,
					КоличествоПродукцииКонечногоЭтапа,
					ХарактеристикаПродукцииКонечногоЭтапа,
					НазначениеПродукцииЭтапа,
					СкладПолучатель,
					ДатаПроизводства,
					Истина
				);

				МатериалыРазвернуты = Истина;

			КонецЦикла;

			Если НЕ МатериалыРазвернуты Тогда
				Продолжить;
			КонецЕсли;

			ДобавитьЗаписьМатСостПоПериодам(
				Набор,
				ДокОбъект.Дата,
				ДокОбъект.Ссылка,
				КлючПФ,
				Выборка.Материал,
				Выборка.ХарМатериала,
				Выборка.СерМатериала,
				Выборка.НазМатериала,
				Выборка.Статья,
				Выборка.Подразделение,
				-ПереносПоСтрокеВыпуска,
				-ПереносПоСтрокеВыпуска,
				Истина,
				ДокОбъект.Ссылка,
				Группа,
				Неопределено,
				Неопределено,
				Договор,
				НаправлениеДеятельности,
				ИнформацияДляПечати,
				ПродукцияКонечногоЭтапа,
				КоличествоПродукцииКонечногоЭтапа,
				ХарактеристикаПродукцииКонечногоЭтапа,
				НазначениеПродукцииЭтапа,
				СкладПолучатель,
				ДатаПроизводства,
				Истина
			);

		КонецЦикла;

		Набор.Записать();

	КонецЦикла;

КонецПроцедуры

#КонецОбласти