//Исправление похоже бага, чтобы действительно удаляло префиксы
&ИзменениеИКонтроль("СформироватьЗаголовокДокумента")
Функция Расш1_СформироватьЗаголовокДокумента1(Шапка, знач НазваниеДокумента = "", УдалитьТолькоЛидирующиеНулиИзНомераОбъекта = Ложь) Экспорт
	
	ДанныеДокумента = Новый Структура("Номер, Дата, Представление, НомерИсправляемогоДокумента, ДатаИсправляемогоДокумента");
	ЗаполнитьЗначенияСвойств(ДанныеДокумента, Шапка);
	ИсправительныйДокумент = ЗначениеЗаполнено(ДанныеДокумента.НомерИсправляемогоДокумента) 
								И ЗначениеЗаполнено(ДанныеДокумента.ДатаИсправляемогоДокумента);
	
	// Если название документа не передано, получим название по представлению документа
	Если ПустаяСтрока(НазваниеДокумента) И ЗначениеЗаполнено(ДанныеДокумента.Представление) Тогда
		НазваниеДокумента = ПредставлениеОбъектаИзПредставленияСсылки(ДанныеДокумента);
	КонецЕсли;

	Если УдалитьТолькоЛидирующиеНулиИзНомераОбъекта Тогда
		НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(ДанныеДокумента.Номер);
		Если ИсправительныйДокумент Тогда
			НомерИсправляемогоДокументаНаПечать = ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(
				ДанныеДокумента.НомерИсправляемогоДокумента);
		КонецЕсли;
	Иначе 
		// Кроме лидирующих нулей будут удалены также префикс организации и префикс информационной базы.
		#Удаление
		НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокумента.Номер);
		#КонецУдаления
		#Вставка
		НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокумента.Номер, Истина, Истина);
		#КонецВставки
		Если ИсправительныйДокумент Тогда
			НомерИсправляемогоДокументаНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
				ДанныеДокумента.НомерИсправляемогоДокумента);
		КонецЕсли;
	КонецЕсли;
	
	Если ИсправительныйДокумент Тогда
		ЗаголовокДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 № %2 от %3 (исправление № %4 от %5)';
				|en = '%1 No. %2 dated %3 (correction No. %4 dated %5)'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			НазваниеДокумента,
			НомерИсправляемогоДокументаНаПечать,
			Формат(ДанныеДокумента.ДатаИсправляемогоДокумента, "ДЛФ=DD"),
			НомерНаПечать,
			Формат(ДанныеДокумента.Дата, "ДЛФ=DD"));
	Иначе
		ЗаголовокДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 № %2 от %3';
				|en = '%1 #%2, %3'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			НазваниеДокумента, НомерНаПечать, Формат(ДанныеДокумента.Дата, "ДЛФ=DD"));
	КонецЕсли;
	
	Возврат ЗаголовокДокумента;
	
КонецФункции
