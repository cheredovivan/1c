
//Использование:
//ПланироватьВыпускДеталей    
//ДублироватьДеревоСпецификаций
Функция НайтиСпецификацию( Номенклатура ) экспорт
	
	СписокСпецификаций = ПолучитьСписокСпецификаций(Номенклатура);
	Если СписокСпецификаций.Количество() > 0
	Тогда
		Ответ = СписокСпецификаций[0].Спецификация;
	Иначе
		Ответ = Справочники.РесурсныеСпецификации.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

//Только спецификации сборки (не ремонта или разборки)
//Использование:
//ТрудозатратыНаИзделие   
//ПлановаяКалькуляция
//ДеревоСпецификаций
Функция ПолучитьСписокСпецификаций( Номенклатура, ВключатьСпецификацииВРазработке = Ложь ) экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РесурсныеСпецификации.Ссылка КАК Спецификация,
		|	РесурсныеСпецификации.ВариантПодбораВДокументы.Порядок = 0 КАК Основная,
		|	РесурсныеСпецификации.Статус КАК Статус
		|ИЗ
		|	Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
		|ГДЕ
		|	(РесурсныеСпецификации.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.Действует)
		|			ИЛИ РесурсныеСпецификации.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.ВРазработке)
		|				И &ВключатьСпецификацииВРазработке)
		|	И НЕ РесурсныеСпецификации.ПометкаУдаления
		|	И РесурсныеСпецификации.ОсновноеИзделиеНоменклатура = &Номенклатура
		|	И РесурсныеСпецификации.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Сборка)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Основная УБЫВ,
		|	Статус УБЫВ";
	//Запрос.Текст = "ВЫБРАТЬ
	//|	ВТ.Спецификация КАК Спецификация,
	//|	МАКСИМУМ(ВТ.Основная) КАК Основная,
	//|	ВТ.Спецификация.Статус КАК Статус
	//|ИЗ
	//|	(ВЫБРАТЬ
	//|		ОсновныеСпецификации.Спецификация КАК Спецификация,
	//|		ИСТИНА КАК Основная
	//|	ИЗ
	//|		РегистрСведений.ОсновныеСпецификации КАК ОсновныеСпецификации
	//|	ГДЕ
	//|		ОсновныеСпецификации.Номенклатура = &Номенклатура
	//|	
	//|	ОБЪЕДИНИТЬ
	//|	
	//|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	//|		РесурсныеСпецификацииВыходныеИзделия.Ссылка,
	//|		ЛОЖЬ
	//|	ИЗ
	//|		Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК РесурсныеСпецификацииВыходныеИзделия
	//|	ГДЕ
	//|		РесурсныеСпецификацииВыходныеИзделия.Номенклатура = &Номенклатура
	//|		И НЕ РесурсныеСпецификацииВыходныеИзделия.Ссылка.ПометкаУдаления
	//|		И (РесурсныеСпецификацииВыходныеИзделия.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.Действует)
	//|			ИЛИ (РесурсныеСпецификацииВыходныеИзделия.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.ВРазработке) 
	//|				И &ВключатьСпецификацииВРазработке = Истина)) ) КАК ВТ
	//|СГРУППИРОВАТЬ ПО
	//|	ВТ.Спецификация

	//|УПОРЯДОЧИТЬ ПО
	//|	Основная УБЫВ,
	//|	Статус УБЫВ";
	Запрос.УстановитьПараметр( "Номенклатура", Номенклатура );
	Запрос.УстановитьПараметр( "ВключатьСпецификацииВРазработке", ВключатьСпецификацииВРазработке );
	Ответ = Запрос.Выполнить().Выгрузить();
	
	Возврат Ответ;
	
КонецФункции

Функция ЭтоМатериал( Номенклатура ) экспорт

	Возврат ЭТП.НайтиСпецификацию( Номенклатура ) = Справочники.РесурсныеСпецификации.ПустаяСсылка()

КонецФункции 

Функция ЕстьСпецификации( Номенклатура )
	Возврат Не ЭТП.НайтиСпецификацию( Номенклатура ).Пустая();
КонецФункции

//Использование:
//АРМ_ЛокальныйДиспетчер
Функция ПолучитьТехнологическийМаршрут( Спецификация ) экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЭтапыПроизводства.Ссылка КАК Ссылка,
	|	ЭтапыПроизводства.Ссылка КАК Этап,
	|	ЭтапыПроизводства.НомерЭтапа КАК НомерЭтапа,
	|	ЭтапыПроизводства.НомерСледующегоЭтапа КАК НомерСледующегоЭтапа,
	|	ЭтапыПроизводства.Подразделение КАК Подразделение,
	|	ПРЕДСТАВЛЕНИЕ(ЭтапыПроизводства.Подразделение) КАК ПодразделениеПредставление,
	|	ЭтапыПроизводства.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|ГДЕ
	|	НЕ ЭтапыПроизводства.ПометкаУдаления
	|	И ЭтапыПроизводства.Владелец = &Владелец
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерЭтапа";
	Запрос.УстановитьПараметр( "Владелец", Спецификация);
	Ответ = Запрос.Выполнить().Выгрузить();

	Возврат Ответ;
	
КонецФункции

Функция ПолучитьКладовуюЦеха( Номенклатура, Подразделение, ЭтоДеталь ) экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	1 КАК Приоритет,
	|	ВариантыОбеспеченияТоварами.Склад КАК Склад,
	|	ВариантыОбеспеченияТоварами.Склад.Подразделение КАК Подразделение,
	|	ВариантыОбеспеченияТоварами.Склад.Наименование КАК СкладНаименование
	|ИЗ
	|	РегистрСведений.ВариантыОбеспеченияТоварами КАК ВариантыОбеспеченияТоварами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО ВариантыОбеспеченияТоварами.Номенклатура = СпрНоменклатура.Ссылка
	|ГДЕ
	|	СпрНоменклатура.Ссылка = &Номенклатура
	|	И ВариантыОбеспеченияТоварами.Склад.Подразделение = &Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	Склады.Ссылка,
	|	Склады.Подразделение,
	|	Склады.Наименование
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	Склады.Наименование ПОДОБНО &КладоваяМилиД
	|	И Склады.Подразделение = &Подразделение
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	Запрос.УстановитьПараметр( "Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр( "Подразделение", Подразделение);
	Запрос.УстановитьПараметр( "КладоваяМилиД", ?(ЭтоДеталь, "% кладовая Д", "% кладовая М"));
	Выб = Запрос.Выполнить().Выбрать();
	
	Если Выб.Следующий()
	Тогда
		Ответ = Выб.Склад;
	Иначе
		Ответ = Справочники.Склады.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьСкладПолучатель( Номенклатура, Спецификация, Полуфабрикат ) экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЭтапыПроизводства.Ссылка КАК Ссылка,
	|	ЭтапыПроизводства.Подразделение КАК Подразделение,
	|	ПРЕДСТАВЛЕНИЕ(ЭтапыПроизводства.Подразделение) КАК ПодразделениеПредставление
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|ГДЕ
	|	ЭтапыПроизводства.Владелец = &РесурснаяСпецификация
	|	И НЕ ЭтапыПроизводства.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтапыПроизводства.НомерЭтапа УБЫВ";
	Запрос.УстановитьПараметр( "РесурснаяСпецификация",  Спецификация );
	ТЗ = Запрос.Выполнить().Выгрузить();
	Если ТЗ.Количество() = 0
	Тогда
		Ответ = Справочники.Склады.ПустаяСсылка();
	Иначе
		Если ТЗ[0].ПодразделениеПредставление = "Обобщенное производство"
		Тогда
			Ответ = Справочники.Склады.НайтиПоНаименованию("СДК");
			//Если Полуфабрикат
			//Тогда
			//	Ответ = Справочники.Склады.НайтиПоНаименованию("14 цех кладовая Д");
			//Иначе
			//	Ответ = Справочники.Склады.НайтиПоНаименованию("14 цех кладовая ГП");
			//КонецЕсли;
		ИначеЕсли ТЗ[0].ПодразделениеПредставление = "14 цех"
		Тогда
			Если Полуфабрикат
			Тогда
				Ответ = Справочники.Склады.НайтиПоНаименованию("14 цех кладовая Д");
			Иначе
				Ответ = Справочники.Склады.НайтиПоНаименованию("14 цех кладовая ГП");
			КонецЕсли;
		ИначеЕсли ТЗ[0].ПодразделениеПредставление = "32 цех" 
		Тогда
			Если ТЗ.Количество() = 1
			Тогда
				Ответ = Справочники.Склады.НайтиПоНаименованию("СДК");
			Иначе
				Ответ = ЭТП.ПолучитьКладовуюЦеха( Номенклатура, ТЗ[1].Подразделение, Истина );
			КонецЕсли;
		Иначе
			Ответ = ЭТП.ПолучитьКладовуюЦеха( Номенклатура, ТЗ[0].Подразделение, Истина );
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ответ;
КонецФункции

//Использование:
//ПланироватьВыпускДеталей
Функция СоздатьСтекСпецификаций() экспорт
	
	СтекСпецификация = Новый ТаблицаЗначений;
	СтекСпецификация.Колонки.Добавить("Спецификация", Новый ОписаниеТипов("СправочникСсылка.РесурсныеСпецификации")  );
	СтекСпецификация.Колонки.Добавить("КритерийДляОбъединения", Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия")  );
	
	Возврат СтекСпецификация;
КонецФункции

Функция ПолучитьКритерийДляОбъединенияСпецификаций( Спецификация, КритерийДляОбъединенияРодителя ) экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЭтапыПроизводства.Подразделение КАК Подразделение,
	|	ЭтапыПроизводства.Подразделение.Наименование КАК ПодразделениеНаименование,
	|	ЭтапыПроизводства.НомерЭтапа КАК НомерЭтапа
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|ГДЕ
	|	ЭтапыПроизводства.Владелец = &Владелец
	|	И НЕ ЭтапыПроизводства.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерЭтапа";
	Запрос.УстановитьПараметр( "Владелец", Спецификация );
	ТЗ = Запрос.Выполнить().Выгрузить();
	Если ТЗ.Количество() = 0
	Тогда
		Ответ = КритерийДляОбъединенияРодителя;
	ИначеЕсли ТЗ.Количество() = 1
	Тогда
		Если ТЗ[0].Подразделение = Справочники.СтруктураПредприятия.НайтиПоНаименованию("Обобщенное производство")
		Тогда
			Ответ = КритерийДляОбъединенияРодителя;
		Иначе 
			Ответ = ТЗ[0].Подразделение;
		КонецЕсли;
	Иначе
		Ответ = Справочники.СтруктураПредприятия.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

//Использование:
//ПланироватьВыпускДеталей
Процедура ДобавитьСпецификациюВСтек( СтекСпецификаций, Спецификация ) экспорт
	
	Если СтекСпецификаций.Количество() = 0
	Тогда
		КритерийДляОбъединенияРодителя = Справочники.СтруктураПредприятия.ПустаяСсылка();
	Иначе
		КритерийДляОбъединенияРодителя = СтекСпецификаций[0].КритерийДляОбъединения;
	КонецЕсли;
	СТЗ = СтекСпецификаций.Вставить(0);
	СТЗ.Спецификация = Спецификация;
	СТЗ.КритерийДляОбъединения = ПолучитьКритерийДляОбъединенияСпецификаций(Спецификация, КритерийДляОбъединенияРодителя );
	
КонецПроцедуры

//Использование:
//ПланироватьВыпускДеталей
Процедура УдалитьПоследнююСпецификациюИзСтека( СтекСпецификаций ) экспорт
	
	СтекСпецификаций.Удалить(0);
	
КонецПроцедуры

Функция ПолучитьЦехВключенияПоСтеку( Подразделение, СтекСпецификаций ) экспорт
	
	Если Подразделение <> Справочники.СтруктураПредприятия.ПустаяСсылка() И
		Подразделение <> Справочники.СтруктураПредприятия.НайтиПоНаименованию("Обобщенное производство")
	Тогда
		Ответ = Подразделение;
	ИначеЕсли СтекСпецификаций.Количество() > 1 И
		ЗначениеЗаполнено(СтекСпецификаций[1].КритерийДляОбъединения)
	Тогда
		Ответ = СтекСпецификаций[1].КритерийДляОбъединения;
	Иначе
		Ответ = Справочники.СтруктураПредприятия.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Ответ;
		
КонецФункции

Функция ПолучитьМассивЦеховВМаршруте( Спецификация )
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЭтапыПроизводства.Подразделение КАК Подразделение,
	|	ПРЕДСТАВЛЕНИЕ(ЭтапыПроизводства.Подразделение) КАК ПодразделениеПредставление
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|ГДЕ
	|	ЭтапыПроизводства.Владелец = &РесурснаяСпецификация
	|	И НЕ ЭтапыПроизводства.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтапыПроизводства.НомерЭтапа
	|АВТОУПОРЯДОЧИВАНИЕ";
	Запрос.УстановитьПараметр( "РесурснаяСпецификация",  Спецификация );
	Выб = Запрос.Выполнить().Выбрать();
	Ответ = Новый Массив;
	Пока Выб.Следующий()
	Цикл
		Ответ.Добавить( Выб.ПодразделениеПредставление );
	КонецЦикла;
	
	Возврат Ответ;

КонецФункции

Функция НайтиСпецификациюКудаВходитДеталь( Номенклатура )
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РесурсныеСпецификацииМатериалыИУслуги.Ссылка КАК Ссылка,
		|	РесурсныеСпецификацииМатериалыИУслуги.Ссылка.ВариантПодбораВДокументы.Порядок = 0 КАК Основная
		|ИЗ
		|	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК РесурсныеСпецификацииМатериалыИУслуги
		|ГДЕ
		|	РесурсныеСпецификацииМатериалыИУслуги.Номенклатура = &Номенклатура
		|	И НЕ РесурсныеСпецификацииМатериалыИУслуги.Ссылка.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Основная УБЫВ
		|АВТОУПОРЯДОЧИВАНИЕ";
	//Запрос.Текст = "ВЫБРАТЬ
	//|	РесурсныеСпецификацииМатериалыИУслуги.Ссылка КАК Ссылка,
	//|	ВЫБОР
	//|		КОГДА ОсновныеСпецификации.Номенклатура ЕСТЬ NULL
	//|			ТОГДА ЛОЖЬ
	//|		ИНАЧЕ ИСТИНА
	//|	КОНЕЦ КАК Основная
	//|ИЗ
	//|	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК РесурсныеСпецификацииМатериалыИУслуги
	//|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСпецификации КАК ОсновныеСпецификации
	//|		ПО РесурсныеСпецификацииМатериалыИУслуги.Ссылка = ОсновныеСпецификации.Спецификация
	//|ГДЕ
	//|	РесурсныеСпецификацииМатериалыИУслуги.Номенклатура = &Номенклатура
	//|	И НЕ РесурсныеСпецификацииМатериалыИУслуги.Ссылка.ПометкаУдаления
	//|
	//|УПОРЯДОЧИТЬ ПО
	//|	Основная УБЫВ
	//|АВТОУПОРЯДОЧИВАНИЕ";
	Запрос.УстановитьПараметр( "Номенклатура", Номенклатура );
	Выб = Запрос.Выполнить().Выбрать();
	Если Выб.Следующий()
	Тогда
		Ответ = Выб.Ссылка;
	Иначе
		Ответ = Справочники.РесурсныеСпецификации.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция СформироватьНаименованиеСпецификации( СпецификацияСсылка, Номенклатура, СтароеНаименование ) Экспорт
	
	РодительскаяСпецификация = Ложь;
	МассивЦеховВМаршруте = ПолучитьМассивЦеховВМаршруте( СпецификацияСсылка );
	Если МассивЦеховВМаршруте.Количество() = 0 Или
		МассивЦеховВМаршруте[0] = "Обобщенное производство"
	Тогда 
		Спец2 = НайтиСпецификациюКудаВходитДеталь( Номенклатура );
		Если Не Спец2.Пустая()
		Тогда
			РодительскаяСпецификация = Истина;
			МассивЦеховВМаршруте = ПолучитьМассивЦеховВМаршруте( Спец2 );
		КонецЕсли;
	КонецЕсли;
		
	Если МассивЦеховВМаршруте.Количество() > 0
	Тогда 
		Маршрут = "";
		Для Каждого СТМ Из МассивЦеховВМаршруте
	        Цикл
			Маршрут = "" + Маршрут + " ->" + СТМ;
		КонецЦикла;
			
		Маршрут  =СтрЗаменить( Маршрут, " цех", "цех" );
		Если РодительскаяСпецификация И 
			МассивЦеховВМаршруте[0] <> "Обобщенное производство"
		Тогда 
			Маршрут = Маршрут +  " *";
		КонецЕсли;
	КонецЕсли;
		
	// Старое наименование нужно беречь, т.к. там может быть информация о спецзаказе,
	// для которого была создана спецификация
	Поз = Найти( СтароеНаименование, " ->" );
	Если Поз = 0
	Тогда
		СтароеНаименование = СтароеНаименование;
	Иначе
		СтароеНаименование = Лев( СтароеНаименование, Поз - 1 );
	КонецЕсли;
	Ответ = СтароеНаименование + Маршрут;
	
	Возврат Ответ;
		
КонецФункции	

Процедура СоздатьПодэтапы( ЭтапО, Спецификация ) экспорт
	
	Запланировано = 0;
	Для Каждого СТЧ Из ЭтапО.ВыходныеИзделия
	Цикл
		Запланировано = Запланировано + СТЧ.Количество;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВидыРаботСотрудников.Ссылка КАК Ссылка,
	|	ВидыРаботСотрудников.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ВидыРаботСотрудников КАК ВидыРаботСотрудников
	|ГДЕ
	|	ВидыРаботСотрудников.Наименование ПОДОБНО ""Подэтап %""
	|	И ВидыРаботСотрудников.Родитель.Наименование = ""Фиктивные""
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	ФиктивныеВидыРабот = Запрос.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЭтапыПроизводства.Ссылка КАК Ссылка,
	|	ЭтапыПроизводства.НомерЭтапа КАК НомерЭтапа,
	|	ЭтапыПроизводства.НомерСледующегоЭтапа КАК НомерСледующегоЭтапа,
	|	ЭтапыПроизводства.Подразделение КАК Подразделение,
	|	ПРЕДСТАВЛЕНИЕ(ЭтапыПроизводства.Подразделение) КАК ПодразделениеПредставление
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|ГДЕ
	|	ЭтапыПроизводства.Владелец = &Ссылка
	|	И НЕ ЭтапыПроизводства.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерЭтапа";
	Запрос.УстановитьПараметр( "Ссылка", Спецификация );
	Маршрут = Запрос.Выполнить().Выгрузить();
	
	// Сначала удалим все фиктивные виды работ, если есть
	Инд = ЭтапО.Трудозатраты.Количество() - 1;
	Пока Инд >= 0
	Цикл 
		Если ФиктивныеВидыРабот.Найти( ЭтапО.Трудозатраты[Инд].ВидРабот ) <> Неопределено
		Тогда
			ЭтапО.Трудозатраты.Удалить( Инд );
		КонецЕсли;
		Инд = Инд - 1;
	КонецЦикла;
	
	// Теперь добавим для каждого цеха из маршрута
	Инд = 0;
	Для Каждого ТочкаМаршрута Из Маршрут
	Цикл 
		Инд = Инд + 1;
	        ФиктивныйВидРабот = ФиктивныеВидыРабот.Найти( "Подэтап " + Инд, "Наименование" ).Ссылка;
		
		СТЧ = ЭтапО.Трудозатраты.Добавить();
		СТЧ.ВидРабот = ФиктивныйВидРабот;
		СТЧ.Количество = Запланировано;
		СТЧ.НазначениеРабот = ТочкаМаршрута.ПодразделениеПредставление;
	КонецЦикла;
	
КонецПроцедуры

//Использование:
//ТОРГ12 _ERP_2_4
Функция ПолучитьДополнительныеРеквизитыГрузоотправителя( Контрагент ) экспорт
	
	Ответ = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПартнерыДополнительныеРеквизиты.Свойство.Заголовок КАК Свойство,
	|	ПартнерыДополнительныеРеквизиты.Значение КАК Значение
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры.ДополнительныеРеквизиты КАК ПартнерыДополнительныеРеквизиты
	|		ПО (Контрагенты.Партнер = ПартнерыДополнительныеРеквизиты.Ссылка)
	|ГДЕ
	|	Контрагенты.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр( "Ссылка", Контрагент);
	Выб = Запрос.Выполнить().Выбрать();
	Пока Выб.Следующий()
	Цикл
		Ответ.Вставить( Выб.Свойство, Выб.Значение );			
	КонецЦикла;
	
	Возврат Ответ;
	
КонецФункции

//Использование:
//ТрудозатратыНаИзделие
Функция НайтиИлиСоздатьПапкуФиктивныхВидовРабот() экспорт
	
	Ответ = Справочники.ВидыРаботСотрудников.НайтиПоНаименованию("Фиктивные", Истина );
	
	Если Ответ.Пустая()
	Тогда
		Папка = Справочники.ВидыРаботСотрудников.СоздатьГруппу();
		Папка.Наименование = "Фиктивные";
		Папка.Записать();
		
		Ответ = Папка.Ссылка;
	КонецЕсли;
	
	возврат Ответ;
	
КонецФункции

Функция СоздатьНовоеНазначение( НаправлениеДеятельности, Наименование ) экспорт
	
	НовНазн = Справочники.Назначения.СоздатьЭлемент();
	НовНазн.НаправлениеДеятельности = НаправлениеДеятельности;
	НовНазн.Наименование = Наименование;
	НовНазн.ТипНазначения = Перечисления.ТипыНазначений.Собственное;
	НовНазн.КонтролироватьТолькоНаличие = Истина;
	НовНазн.Записать();
	
	Возврат НовНазн.Ссылка;
	
КонецФункции


Функция ПолучитьПрефиксДляОрганизации( ИНН ) экспорт
	
	Если ИНН = "5506052891" // ЗАО ПО ЭТП
	Тогда
		Ответ = "Э";
	ИначеЕсли ИНН = "5506147085" // ЭТП-Трейдинг
	Тогда
		Ответ = "Т";
	ИначеЕсли ИНН = "5504207349" // ЭТП-ВЭН
	Тогда
		Ответ = "В";
	ИначеЕсли ИНН = "5504156912" // ТД ЭЛЕКТРОТОЧПРИБОР
	Тогда
		Ответ = "Д";
	ИначеЕсли ИНН = "5504090034" // ООО "НПО "ЭЛЕКТРОТОЧПРИБОР"
	Тогда
		Ответ = "Н";
	КонецЕсли;	

	Возврат Ответ;
	
КонецФункции

Процедура УстановитьОсновнуюСпецификацию( Номенклатура, Спецификация ) экспорт
	Если Спецификация.ПометкаУдаления ИЛИ Спецификация.Статус <> Перечисления.СтатусыСпецификаций.Действует ИЛИ
			Спецификация.ОсновноеИзделиеНоменклатура <> Номенклатура тогда
		ВызватьИсключение "Эту спецификацию нельзя установить основной!";
	КонецЕсли;
	Установить = Истина;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РесурсныеСпецификации.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
		|ГДЕ
		|	РесурсныеСпецификации.ОсновноеИзделиеНоменклатура = &ОсновноеИзделиеНоменклатура
		|	И РесурсныеСпецификации.ВариантПодбораВДокументы.Порядок = 0
		|	И НЕ РесурсныеСпецификации.ПометкаУдаления
		|	И РесурсныеСпецификации.Статус.Порядок = 1" ;
	Запрос.УстановитьПараметр("ОсновноеИзделиеНоменклатура", Номенклатура);
	Выб = Запрос.Выполнить().Выбрать();
	Пока Выб.Следующий() цикл
		Если Выб.Ссылка <> Спецификация тогда
			Спец = Выб.Ссылка.ПолучитьОбъект();
			Спец.ВариантПодбораВДокументы = Перечисления.ВариантыПодбораСпецификацииВДокументы.Вручную;
			Спец.Записать();
		Иначе
	        Установить = Ложь; //Уже установлено
		КонецЕсли;
	КонецЦикла;
	Если Установить тогда
		Спец = Спецификация.ПолучитьОбъект();
		Спец.ВариантПодбораВДокументы = Перечисления.ВариантыПодбораСпецификацииВДокументы.Автоматически;
		Спец.Записать();
	КонецЕсли;
	//Набор = РегистрыСведений.ОсновныеСпецификации.СоздатьНаборЗаписей();
	//Набор.Отбор.Подразделение.Установить(Справочники.СтруктураПредприятия.ПустаяСсылка());
	//Набор.Отбор.Номенклатура.Установить(Номенклатура);
	//Набор.Отбор.Характеристика.Установить( Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка() );
	//Набор.Прочитать();
	//Набор.Очистить();
	//Запись = Набор.Добавить();
	//Запись.Подразделение = Справочники.СтруктураПредприятия.ПустаяСсылка();
	//Запись.Номенклатура = Номенклатура;
	//Запись.Спецификация = Спецификация;
	//Набор.Записать(Истина);
КонецПроцедуры

//Использование:
//МатериалыНаСписокИзделий(0)
//ПланироватьВыпускДеталей 
//ПеремещениеИзЦехаСТрудозатратами
//ПотребностьДеталейНаПлан(0)    
//МатериалыНаВыпускПоЗКлиентов(0)
//МатериалыНаСписокИзделий01
//ПотребностьДеталейНаПлан   
//РасчетПлановыхЦен
//МатериалыНаИзделие(0)  
//ПлановаяКалькуляция 
//ДеревоСпецификаций                  
//ПотребностьДеталейНаПлан1
Функция ВыполнитьАлгоритмРасчетаКоличества( АлгоритмРасчетаКоличества, ВыходноеКоличество ) экспорт

	Расчетноеколичество = ВыходноеКоличество;
	
	Формула  = СтрЗаменить( АлгоритмРасчетаКоличества, "[#Продукция.Количество#]", Формат(Расчетноеколичество,"ЧРД=.; ЧГ=0") );
	Формула  = СтрЗаменить( Формула, "#Продукция.Количество#", Формат(Расчетноеколичество,"ЧРД=.; ЧГ=0") );
	Формула  = СтрЗаменить( Формула, "[Продукция.Количество]", Формат(Расчетноеколичество,"ЧРД=.; ЧГ=0") );
	Формула  = СтрЗаменить( Формула, "[ВыходныеИзделия[1].Количество]", Формат(Расчетноеколичество,"ЧРД=.; ЧГ=0") );
	Формула  = СтрЗаменить( Формула, "[ОсновноеИзделие.Количество]", Формат(Расчетноеколичество,"ЧРД=.; ЧГ=0") );
	Количество = Вычислить( Формула );
	                                                                                    
	Возврат Количество;
	
КонецФункции

Процедура НастроитьЭтапыПроизводстваПослеСозданияОбертка(СтруктураПараметров, АдресХранилища) Экспорт
	
	Ответ = Обработки.НастроитьЭтапыПослеСоздания.ВыполнитьНастройкуЭтапов( СтруктураПараметров );
	
	ПоместитьВоВременноеХранилище(Ответ, АдресХранилища);
	
КонецПроцедуры
 
//СС 18.12.18 Текст для Акта приёма-передачи и ТОРГ12, и МХ18
//Использование:
//ПланироватьВыпускПриборов 
//ЗаказНаПроизводство
//МатериалыНаВыпускПоЗКлиентов(0)
//ТОРГ12 _ERP_2_4 
//МХ18 _ERP_2_4
//МатериалыНаВыпускПоЗКлиентов
Функция ДогСпецГКДляПечати(знач ЗаказКлиента = Неопределено, знач НаправлениеДеятельности = Неопределено) Экспорт
	
	РезСтр="";
	
	//ГосКонтракт = ДанныеПечати.БанковскийСчетКонтрагента.ГосударственныйКонтракт;
	Если ЗначениеЗаполнено(ЗаказКлиента)
	Тогда
		ДоговорНаименование = Строка(ЗаказКлиента.Договор);
		Если ЗначениеЗаполнено(ДоговорНаименование)
		Тогда
			РезСтр = ДоговорНаименование;
			Если Найти( РезСтр, "Дог") <> 1
			Тогда	
				РезСтр = "Дог. " + РезСтр;
			КонецЕсли;
		Иначе
			//Основание = ДанныеПечати.Основание; СС 11.07.18
			РезСтр = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 № %2 от %3'"),
				"Счет", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ЗаказКлиента.Номер, Истина,Истина),
				Формат(ЗаказКлиента.Дата, "ДЛФ=DD"));
		КонецЕсли;
	
		Специф=ЗаказКлиента.ДополнительныеРеквизиты.Найти(
		ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","ЗаказКлиентаСпецификацияДоговора"),"Свойство");
		Если Специф<>Неопределено
		Тогда
			Если РезСтр<>"" тогда
				РезСтр = РезСтр + ", ";
			КонецЕсли;
			РезСтр = РезСтр+"Спецификация №"+Специф.Значение;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(НаправлениеДеятельности) тогда
			НаправлениеДеятельности = ЗаказКлиента.НаправлениеДеятельности;
		КонецЕсли;
	КонецЕсли;

	Если ЗначениеЗаполнено(НаправлениеДеятельности)
	Тогда
		ТЗДР = НаправлениеДеятельности.ДополнительныеРеквизиты.Выгрузить();
		СТЧ = ТЗДР.Найти( ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "НаправлениеДеятельностиПолноеНаименование") );
		Если РезСтр<>"" тогда
			РезСтр = РезСтр + ", ";
		КонецЕсли;
		Если СТЧ = Неопределено
		Тогда
			ТекстГК = НаправлениеДеятельности.Наименование;
		Иначе
			ТекстГК = СТЧ.Значение;
			Если Найти( ТекстГК, "ГК") <> 1
			Тогда	
				ТекстГК = "ГК " + ТекстГК;
			КонецЕсли;
		КонецЕсли;	
		РезСтр = РезСтр + ТекстГК;
		
		СТЧ = ТЗДР.Найти( ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "НаправлениеДеятельностиИГК") );
		Если СТЧ <> Неопределено
		Тогда
			РезСтр = РезСтр + ", ИГК " + СТЧ.Значение;
		КонецЕсли;			
		
		
	ИначеЕсли ЗначениеЗаполнено(ЗаказКлиента) И ЗначениеЗаполнено(ЗаказКлиента.БанковскийСчетКонтрагента) тогда
		ГосКонтракт = ЗаказКлиента.БанковскийСчетКонтрагента.ГосударственныйКонтракт;
		Если ЗначениеЗаполнено(ГосКонтракт)	Тогда
			РезСтр = РезСтр + ", ГК " + ГосКонтракт.Наименование + ", ИГК " + ГосКонтракт.Код;
		КонецЕсли;
	КонецЕсли;
	Возврат РезСтр;	
КонецФункции

Функция ЭтапВыпуска(знач Этап) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЭтапПроизводства2_2Последователи.Этап КАК Этап
		|ИЗ
		|	Документ.ЭтапПроизводства2_2.Последователи КАК ЭтапПроизводства2_2Последователи
		|ГДЕ
		|	ЭтапПроизводства2_2Последователи.Ссылка = &Ссылка
		|	И ЭтапПроизводства2_2Последователи.ТипСвязи = 0";
	Рез = Этап.Ссылка;
	Запрос.УстановитьПараметр("Ссылка",Рез);
	Выб = Запрос.Выполнить().Выбрать();
	//Пока ЗначениеЗаполнено(Этап.Этап.НомерСледующегоЭтапа) Цикл
	Пока Выб.Следующий() Цикл
		Рез = Выб.Этап;
		Запрос.УстановитьПараметр("Ссылка",Рез);
		Выб = Запрос.Выполнить().Выбрать();
	КонецЦикла;
	Возврат Рез;
КонецФункции

Процедура СообщитьОшибку(знач Текст, знач Ключ) Экспорт
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.КлючДанных = Ключ;
	Сообщение.Сообщить();
	ВызватьИсключение Текст;
КонецПроцедуры

//Изделия только в штуках!; Трудозатраты с уникальным ключом (ВидРабот+НазначениеРабот)
//Использование:
//АРМ_ЛокальныйДиспетчер
Процедура ОформитьВыполнениеЭтапаПоНазначениям(знач Этап, знач ДатаВып, знач СоответствиеНазначений, знач ДелитьТрудозатраты = Ложь) Экспорт
	
	Если ТипЗнч(Этап) = Тип("ДокументСсылка.ЭтапПроизводства2_2") тогда
		Этап = Этап.ПолучитьОбъект();
	КонецЕсли;
	
	//ВыходныеИзделия
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Этап.Ссылка);
	Запрос.УстановитьПараметр("ДатаВып", ДатаВып);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|			КОГДА НЕ ЭтапПроизводства2_2ВыходныеИзделия.Произведено
		|					И НЕ ЭтапПроизводства2_2ВыходныеИзделия.Отменено
		|				ТОГДА ЭтапПроизводства2_2ВыходныеИзделия.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Произвести,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА НЕ ЭтапПроизводства2_2ВыходныеИзделия.Произведено
		|					И НЕ ЭтапПроизводства2_2ВыходныеИзделия.Отменено
		|				ТОГДА ЭтапПроизводства2_2ВыходныеИзделия.НомерСтроки
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НомерСтроки,
		|	ЭтапПроизводства2_2ВыходныеИзделия.Назначение КАК Назначение,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЭтапПроизводства2_2ВыходныеИзделия.Произведено
		|					И НЕ ЭтапПроизводства2_2ВыходныеИзделия.Отменено
		|					И ЭтапПроизводства2_2ВыходныеИзделия.ДатаПроизводства = &ДатаВып
		|				ТОГДА ЭтапПроизводства2_2ВыходныеИзделия.НомерСтроки
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НомерСтрокиСегодня
		|ИЗ
		|	Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ЭтапПроизводства2_2ВыходныеИзделия
		|ГДЕ
		|	ЭтапПроизводства2_2ВыходныеИзделия.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ЭтапПроизводства2_2ВыходныеИзделия.Назначение
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ЭтапПроизводства2_2ВыходныеИзделия.Количество) КАК Запланировано,
		|	СУММА(ВЫБОР
		|			КОГДА ЭтапПроизводства2_2ВыходныеИзделия.Произведено
		|				ТОГДА ЭтапПроизводства2_2ВыходныеИзделия.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Произведено
		|ИЗ
		|	Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ЭтапПроизводства2_2ВыходныеИзделия
		|ГДЕ
		|	ЭтапПроизводства2_2ВыходныеИзделия.Ссылка = &Ссылка";
	Вып = Запрос.ВыполнитьПакет();
	Выб = Вып[0].Выбрать();
	ПроизвестиВсего =0;
	Пока Выб.Следующий() цикл
		Произвести = СоответствиеНазначений[Выб.Назначение];
		Если ЗначениеЗаполнено(Произвести) тогда
			СтрИзделия = Этап.ВыходныеИзделия[Выб.НомерСтроки -1];	
			ОстатокИзделий = СтрИзделия.КоличествоУпаковок - Произвести;
			Если ОстатокИзделий < 0 тогда
				СообщитьОшибку("К выполнению назначено больше, чем запланировано по назначению " + Выб.Назначение, Этап.Ссылка);
			Иначе
				
				Если Выб.НомерСтрокиСегодня >0 тогда
					СтрНов = Этап.ВыходныеИзделия[Выб.НомерСтрокиСегодня -1];
					СтрНов.КоличествоУпаковок = СтрНов.КоличествоУпаковок + Произвести;
				Иначе
					СтрНов = Этап.ВыходныеИзделия.Добавить();
					ЗаполнитьЗначенияСвойств(СтрНов, СтрИзделия,, "КодСтроки");
					СтрНов.КоличествоУпаковок = Произвести;
				КонецЕсли;
				СтрИзделия.КоличествоУпаковок = ОстатокИзделий;
				СтрНов.Количество = СтрНов.КоличествоУпаковок;
				СтрИзделия.Количество = СтрИзделия.КоличествоУпаковок;
				СтрНов.Произведено = Истина;
				СтрНов.ДатаПроизводства = ДатаВып;
				ПроизвестиВсего = ПроизвестиВсего + Произвести;
				ПроизводствоКлиентСервер.ПересчитатьДолюСтоимостиПриРазбиенииСтроки(СтрНов, СтрИзделия, Этап.СпособРаспределенияЗатратНаВыходныеИзделия);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Инд =0;
	Пока Инд < Этап.ВыходныеИзделия.Количество() цикл
		Стр1 = Этап.ВыходныеИзделия[Инд];
		Если Стр1.КоличествоУпаковок=0 И Стр1.Количество=0 тогда
			Этап.ВыходныеИзделия.Удалить(Инд);
		Иначе
			Инд = Инд+1;
		КонецЕсли;
	КонецЦикла;
	
	Выб = Вып[1].Выбрать();
	Выб.Следующий();
	Если Выб.Запланировано>0 И ПроизвестиВсего>0 тогда
		Выполнено = (Выб.Произведено + ПроизвестиВсего) *Этап.КоличествоУпаковокПлан /Выб.Запланировано;
		//Выполнено = ПроизвестиВсего *Этап.Запланировано /Выб.Запланировано + Этап.Выполнено;
		ОформитьВыполнениеЭтапа(Этап, ДатаВып, Выполнено, ДелитьТрудозатраты);
	КонецЕсли;
	
КонецПроцедуры

//Использование:
//АРМ_ЛокальныйДиспетчер
Процедура ОформитьВыполнениеЭтапа(знач Этап, знач ДатаВып, знач КоличествоУпаковокФакт, знач ДелитьТрудозатраты = Ложь) Экспорт
	//Этап = Документы.ЭтапПроизводства2_2.ПустаяСсылка();	
	Если ТипЗнч(Этап) = Тип("ДокументСсылка.ЭтапПроизводства2_2") тогда
		Этап = Этап.ПолучитьОбъект();
	КонецЕсли;
	КэшированныеЗначения = Неопределено; //ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	СтруктураДействий = Новый Структура("ПересчитатьКоличествоУпаковок");

	ИзменятьПредшественников = Истина;
	Если Этап.КоличествоУпаковокФакт > КоличествоУпаковокФакт тогда
		Возврат;
	ИначеЕсли Этап.КоличествоУпаковокФакт = КоличествоУпаковокФакт тогда
		ИзменятьПредшественников = Ложь;
	КонецЕсли;
	Если Этап.КоличествоУпаковокПлан < КоличествоУпаковокФакт тогда
		СообщитьОшибку("К выполнению больше, чем запланировано в этапе " + Этап.Ссылка, Этап.Ссылка);
	КонецЕсли;
	
	КоэфОстЧ = Этап.КоличествоУпаковокПлан - КоличествоУпаковокФакт;
	КоэфОстЗ = Этап.КоличествоУпаковокПлан;
	
	////ПобочныеИзделия
	//Запрос = Новый Запрос;
	//Запрос.УстановитьПараметр("Ссылка", Этап.Ссылка);
	//Запрос.УстановитьПараметр("ДатаВып", ДатаВып);
	//Запрос.Текст =
	//	"ВЫБРАТЬ
	//	|	СУММА(ЭтапПроизводства2_2ПобочныеИзделия.КоличествоУпаковок) КАК Запланировано,
	//	|	СУММА(ВЫБОР
	//	|			КОГДА НЕ ЭтапПроизводства2_2ПобочныеИзделия.Произведено
	//	|					И НЕ ЭтапПроизводства2_2ПобочныеИзделия.Отменено
	//	|				ТОГДА ЭтапПроизводства2_2ПобочныеИзделия.КоличествоУпаковок
	//	|			ИНАЧЕ 0
	//	|		КОНЕЦ) КАК Осталось,
	//	|	МАКСИМУМ(ВЫБОР
	//	|			КОГДА НЕ ЭтапПроизводства2_2ПобочныеИзделия.Произведено
	//	|					И НЕ ЭтапПроизводства2_2ПобочныеИзделия.Отменено
	//	|				ТОГДА ЭтапПроизводства2_2ПобочныеИзделия.НомерСтроки
	//	|			ИНАЧЕ 0
	//	|		КОНЕЦ) КАК НомерСтроки,
	//	|	МАКСИМУМ(ВЫБОР
	//	|			КОГДА ЭтапПроизводства2_2ПобочныеИзделия.Произведено
	//	|					И НЕ ЭтапПроизводства2_2ПобочныеИзделия.Отменено
	//	|					И ЭтапПроизводства2_2ПобочныеИзделия.ДатаПроизводства = &ДатаВып
	//	|				ТОГДА ЭтапПроизводства2_2ПобочныеИзделия.НомерСтроки
	//	|			ИНАЧЕ 0
	//	|		КОНЕЦ) КАК НомерСтрокиСегодня,
	//	|	ЭтапПроизводства2_2ПобочныеИзделия.Номенклатура КАК Номенклатура
	//	|ИЗ
	//	|	Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ЭтапПроизводства2_2ПобочныеИзделия
	//	|ГДЕ
	//	|	ЭтапПроизводства2_2ПобочныеИзделия.Ссылка = &Ссылка
	//	|
	//	|СГРУППИРОВАТЬ ПО
	//	|	ЭтапПроизводства2_2ПобочныеИзделия.Номенклатура
	//	|
	//	|УПОРЯДОЧИТЬ ПО
	//	|	НомерСтроки";
	//Выб = Запрос.Выполнить().Выбрать(); //По аналогии с Обеспечением материалами, поэтому названия оттуда
	//Пока Выб.Следующий() цикл
	//	Расход = Выб.Осталось - Выб.Запланировано*КоэфОстЧ/КоэфОстЗ;
	//	Если Выб.НомерСтроки >0 И  Расход>0 тогда
	//		Если КоэфОстЧ>0 тогда
	//			Расход = Цел(Расход);
	//		КонецЕсли;
	//		Если Расход>0 тогда
	//			СтрОсталось = Этап.ПобочныеИзделия[Выб.НомерСтроки -1];
	//			//Если Выб.НомерСтрокиОтгрузки >0 тогда
	//			//	СтрОтгружено = Этап.ОбеспечениеМатериаламиИРаботами[Выб.НомерСтрокиОтгрузки -1];
	//			//Иначе  Убрал, т.к. не работает, если дата этапа меньше даты запрета изменений. Лучше проверять, но пока не знаю как
	//				СтрОтгружено = Этап.ПобочныеИзделия.Добавить();
	//				ЗаполнитьЗначенияСвойств(СтрОтгружено, СтрОсталось,, "КодСтроки");
	//				СтрОтгружено.КоличествоУпаковок = 0;
	//				СтрОтгружено.Количество = 0;
	//			//КонецЕсли;
	//			СтрОсталось.КоличествоУпаковок = СтрОсталось.КоличествоУпаковок - Расход;
	//			СтрОтгружено.КоличествоУпаковок = СтрОтгружено.КоличествоУпаковок + Расход;
	//			Количество1 = СтрОсталось.КоличествоУпаковок + СтрОтгружено.КоличествоУпаковок;
	//			Количество2 = СтрОсталось.Количество + СтрОтгружено.Количество;
	//			//СтрОтгружено.Количество = Окр(СтрОтгружено.КоличествоУпаковок * Выб.Коэффициент,3);
	//			СтрОтгружено.Количество = СтрОтгружено.КоличествоУпаковок;
	//			СтрОсталось.Количество = Количество2 - СтрОтгружено.Количество;
	//			//на случай потери размерности
	//			Если СтрОтгружено.Количество =0 тогда
	//				СтрОтгружено.КоличествоУпаковок =0;
	//				СтрОсталось.КоличествоУпаковок = Количество1;
	//			ИначеЕсли СтрОсталось.Количество =0 тогда
	//				СтрОтгружено.КоличествоУпаковок = Количество1;
	//				СтрОсталось.КоличествоУпаковок = 0;
	//			КонецЕсли;
	//			СтрОтгружено.Произведено = Истина;
	//			СтрОтгружено.ДатаПроизводства = ДатаВып;
	//		КонецЕсли;
	//	КонецЕсли;
	//КонецЦикла;
	//Инд =0;
	//Пока Инд < Этап.ПобочныеИзделия.Количество() цикл
	//	Стр1 = Этап.ПобочныеИзделия[Инд];
	//	Если Стр1.КоличествоУпаковок=0 тогда // И Стр1.Количество=0 убрал
	//		Этап.ПобочныеИзделия.Удалить(Инд);
	//	Иначе
	//		Инд = Инд+1;
	//	КонецЕсли;
	//КонецЦикла;
	//
	////ОбеспечениеМатериаламиИРаботами и РасходМатериаловИРабот
	//Запрос = Новый Запрос;
	//Запрос.УстановитьПараметр("Ссылка", Этап.Ссылка);
	//Запрос.УстановитьПараметр("ДатаВып", ДатаВып);
	//Запрос.Текст =
	//	"ВЫБРАТЬ
	//	|	СУММА(ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.КоличествоУпаковок) КАК Запланировано,
	//	|	СУММА(ВЫБОР
	//	|			КОГДА ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.ВариантОбеспечения.Порядок > 1
	//	|				ТОГДА ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.КоличествоУпаковок
	//	|			ИНАЧЕ 0
	//	|		КОНЕЦ) КАК Осталось,
	//	|	МАКСИМУМ(ВЫБОР
	//	|			КОГДА ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.ВариантОбеспечения.Порядок > 1
	//	|				ТОГДА ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.НомерСтроки
	//	|			ИНАЧЕ 0
	//	|		КОНЕЦ) КАК НомерСтроки,
	//	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.КлючСвязиСпецификация КАК КлючСвязиСпецификация,
	//	|	МАКСИМУМ(ВЫБОР
	//	|			КОГДА ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.ВариантОбеспечения.Порядок < 2
	//	|					И ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.ДатаОтгрузки = &ДатаВып
	//	|				ТОГДА ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.НомерСтроки
	//	|			ИНАЧЕ 0
	//	|		КОНЕЦ) КАК НомерСтрокиОтгрузки,
	//	|	МАКСИМУМ(ВЫБОР
	//	|			КОГДА ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.ДатаОтгрузки = &ДатаВып
	//	|				ТОГДА ЕСТЬNULL(ЭтапПроизводства2_2РасходМатериаловИРабот.НомерСтроки, 0)
	//	|			ИНАЧЕ 0
	//	|		КОНЕЦ) КАК НомерСтрокиРасхода,
	//	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура КАК Номенклатура,
	//	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Упаковка КАК Упаковка
	//	|ПОМЕСТИТЬ ВР
	//	|ИЗ
	//	|	Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами
	//	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.РасходМатериаловИРабот КАК ЭтапПроизводства2_2РасходМатериаловИРабот
	//	|		ПО ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка = ЭтапПроизводства2_2РасходМатериаловИРабот.Ссылка
	//	|			И ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура = ЭтапПроизводства2_2РасходМатериаловИРабот.Номенклатура
	//	|			И ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Характеристика = ЭтапПроизводства2_2РасходМатериаловИРабот.Характеристика
	//	|			И ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.ДатаОтгрузки = ЭтапПроизводства2_2РасходМатериаловИРабот.ДатаРасхода
	//	|			И ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Упаковка = ЭтапПроизводства2_2РасходМатериаловИРабот.Упаковка
	//	|ГДЕ
	//	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка = &Ссылка
	//	|
	//	|СГРУППИРОВАТЬ ПО
	//	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.КлючСвязиСпецификация,
	//	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура,
	//	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Упаковка
	//	|;
	//	|
	//	|////////////////////////////////////////////////////////////////////////////////
	//	|ВЫБРАТЬ
	//	|	ВР.Запланировано КАК Запланировано,
	//	|	ВР.Осталось КАК Осталось,
	//	|	ВР.НомерСтроки КАК НомерСтроки,
	//	|	ВР.КлючСвязиСпецификация КАК КлючСвязиСпецификация,
	//	|	ВР.НомерСтрокиОтгрузки КАК НомерСтрокиОтгрузки,
	//	|	ВР.НомерСтрокиРасхода КАК НомерСтрокиРасхода,
	//	|	ЕСТЬNULL(&Коэффициент, 1) КАК Коэффициент
	//	|ИЗ
	//	|	ВР КАК ВР
	//	|
	//	|УПОРЯДОЧИТЬ ПО
	//	|	НомерСтроки";
	//Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Коэффициент",
	//	Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
	//	"ВР.Упаковка", "ВР.Номенклатура"));
	//Выб = Запрос.Выполнить().Выбрать();
	//Пока Выб.Следующий() цикл
	//	Расход = Выб.Осталось - Выб.Запланировано*КоэфОстЧ/КоэфОстЗ;
	//	Если Выб.НомерСтроки >0 И  Расход>0 тогда
	//		Если КоэфОстЧ>0 тогда
	//			Расход = Окр(Расход,2); //было Цел
	//		КонецЕсли;
	//		Если Расход>0 тогда
	//			СтрОсталось = Этап.ОбеспечениеМатериаламиИРаботами[Выб.НомерСтроки -1];
	//			//Если Выб.НомерСтрокиОтгрузки >0 тогда
	//			//	СтрОтгружено = Этап.ОбеспечениеМатериаламиИРаботами[Выб.НомерСтрокиОтгрузки -1];
	//			//Иначе  Убрал, т.к. не работает, если дата этапа меньше даты запрета изменений. Лучше проверять, но пока не знаю как
	//				СтрОтгружено = Этап.ОбеспечениеМатериаламиИРаботами.Добавить();
	//				ЗаполнитьЗначенияСвойств(СтрОтгружено, СтрОсталось,, "КодСтроки");
	//				СтрОтгружено.КоличествоУпаковок = 0;
	//				СтрОтгружено.Количество = 0;
	//			//КонецЕсли;
	//			СтрОсталось.КоличествоУпаковок = СтрОсталось.КоличествоУпаковок - Расход;
	//			СтрОтгружено.КоличествоУпаковок = СтрОтгружено.КоличествоУпаковок + Расход;
	//			Количество1 = СтрОсталось.КоличествоУпаковок + СтрОтгружено.КоличествоУпаковок;
	//			Количество2 = СтрОсталось.Количество + СтрОтгружено.Количество;
	//			//СтрОтгружено.Количество = Окр(СтрОтгружено.КоличествоУпаковок * Выб.Коэффициент,3);
	//			СтрОтгружено.Количество = СтрОтгружено.КоличествоУпаковок * Выб.Коэффициент;
	//			СтрОсталось.Количество = Количество2 - СтрОтгружено.Количество;
	//			//на случай потери размерности
	//			Если СтрОтгружено.Количество =0 тогда
	//				СтрОтгружено.КоличествоУпаковок =0;
	//				СтрОсталось.КоличествоУпаковок = Количество1;
	//			ИначеЕсли СтрОсталось.Количество =0 тогда
	//				СтрОтгружено.КоличествоУпаковок = Количество1;
	//				СтрОсталось.КоличествоУпаковок = 0;
	//			КонецЕсли;
	//			СтрОтгружено.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.Отгрузить;
	//			СтрОтгружено.ДатаОтгрузки = ДатаВып;
	//			
	//			Если СтрОтгружено.КоличествоУпаковок>0 тогда
	//				//Если Выб.НомерСтрокиРасхода >0 тогда
	//				//	СтрРасход = Этап.РасходМатериаловИРабот[Выб.НомерСтрокиРасхода -1];
	//				//	СтрРасход.КоличествоУпаковок = СтрОтгружено.КоличествоУпаковок;
	//				//	СтрРасход.Количество = СтрОтгружено.Количество;
	//				//Иначе   Убрал, т.к. не работает, если дата этапа меньше даты запрета изменений.
	//					СтрРасход = Этап.РасходМатериаловИРабот.Добавить();
	//					ЗаполнитьЗначенияСвойств(СтрРасход, СтрОтгружено);
	//					СтрРасход.ДатаРасхода = ДатаВып;
	//				//КонецЕсли;
	//			КонецЕсли;
	//		КонецЕсли;
	//	КонецЕсли;
	//КонецЦикла;
	//Инд =0;
	//Пока Инд < Этап.ОбеспечениеМатериаламиИРаботами.Количество() цикл
	//	Стр1 = Этап.ОбеспечениеМатериаламиИРаботами[Инд];
	//	Если Стр1.КоличествоУпаковок=0 тогда // И Стр1.Количество=0 убрал
	//		Этап.ОбеспечениеМатериаламиИРаботами.Удалить(Инд);
	//	Иначе
	//		Инд = Инд+1;
	//	КонецЕсли;
	//КонецЦикла;
	//	
	////Трудозатраты
	//НельзяДелитьТрудозатраты = Ложь;
	//Если ДелитьТрудозатраты ИЛИ КоэфОстЧ=0 тогда
	//	Запрос.Текст =
	//		"ВЫБРАТЬ
	//		|	СУММА(ЭтапПроизводства2_2Трудозатраты.Количество) КАК Запланировано,
	//		|	СУММА(ВЫБОР
	//		|			КОГДА НЕ ЭтапПроизводства2_2Трудозатраты.Выполнено
	//		|				ТОГДА ЭтапПроизводства2_2Трудозатраты.Количество
	//		|			ИНАЧЕ 0
	//		|		КОНЕЦ) КАК Осталось,
	//		|	МАКСИМУМ(ВЫБОР
	//		|			КОГДА НЕ ЭтапПроизводства2_2Трудозатраты.Выполнено
	//		|				ТОГДА ЭтапПроизводства2_2Трудозатраты.НомерСтроки
	//		|			ИНАЧЕ 0
	//		|		КОНЕЦ) КАК НомерСтроки,
	//		|	МАКСИМУМ(ВЫБОР
	//		|			КОГДА ЭтапПроизводства2_2Трудозатраты.Выполнено
	//		|					И ЭтапПроизводства2_2Трудозатраты.ДатаВыполнения = &ДатаВып
	//		|				ТОГДА ЭтапПроизводства2_2Трудозатраты.НомерСтроки
	//		|			ИНАЧЕ 0
	//		|		КОНЕЦ) КАК НомерСтрокиСегодня,
	//		|	ЭтапПроизводства2_2Трудозатраты.ВидРабот КАК ВидРабот,
	//		|	ЭтапПроизводства2_2Трудозатраты.НазначениеРабот КАК НазначениеРабот,
	//		|	КОЛИЧЕСТВО(ВЫБОР
	//		|			КОГДА НЕ ЭтапПроизводства2_2Трудозатраты.Выполнено
	//		|				ТОГДА ЭтапПроизводства2_2Трудозатраты.НомерСтроки
	//		|			ИНАЧЕ NULL
	//		|		КОНЕЦ) КАК КоличествоСтрок
	//		|ИЗ
	//		|	Документ.ЭтапПроизводства2_2.Трудозатраты КАК ЭтапПроизводства2_2Трудозатраты
	//		|ГДЕ
	//		|	ЭтапПроизводства2_2Трудозатраты.Ссылка = &Ссылка
	//		|
	//		|СГРУППИРОВАТЬ ПО
	//		|	ЭтапПроизводства2_2Трудозатраты.ВидРабот,
	//		|	ЭтапПроизводства2_2Трудозатраты.НазначениеРабот
	//		|
	//		|УПОРЯДОЧИТЬ ПО
	//		|	КоличествоСтрок УБЫВ,
	//		|	НомерСтроки";
	//	Выб = Запрос.Выполнить().Выбрать();
	//	Пока Выб.Следующий() цикл
	//		Если Выб.КоличествоСтрок>1 тогда
	//			НельзяДелитьТрудозатраты = Истина;
	//			Прервать;
	//		КонецЕсли;
	//		Расход = Выб.Осталось - Выб.Запланировано*КоэфОстЧ/КоэфОстЗ;
	//		Если Расход>0 тогда
	//			Расход = Окр(Расход,3);
	//		Иначе
	//			Расход =0;
	//		КонецЕсли;
	//		Если Выб.НомерСтроки >0 И  Расход>0 тогда
	//			СтрОсталось = Этап.Трудозатраты[Выб.НомерСтроки -1];
	//			Если Выб.НомерСтрокиСегодня >0 тогда
	//				СтрВыполнено = Этап.Трудозатраты[Выб.НомерСтрокиСегодня -1];
	//			Иначе
	//				СтрВыполнено = Этап.Трудозатраты.Добавить();
	//				ЗаполнитьЗначенияСвойств(СтрВыполнено, СтрОсталось,, "КодСтроки");
	//				СтрВыполнено.Количество = 0;
	//			КонецЕсли;
	//			СтрОсталось.Количество = СтрОсталось.Количество - Расход;
	//			СтрВыполнено.Количество = СтрВыполнено.Количество + Расход;
	//			СтрВыполнено.Выполнено = Истина;
	//			СтрВыполнено.ДатаВыполнения = ДатаВып;
	//		КонецЕсли;
	//	КонецЦикла;
	//	Если НельзяДелитьТрудозатраты тогда
	//		Если КоэфОстЧ=0 тогда
	//			Для Каждого Стр1 из Этап.Трудозатраты цикл
	//				Если НЕ Стр1.Выполнено тогда
	//					Стр1.Выполнено = Истина;
	//					Стр1.ДатаВыполнения = ДатаВып;
	//				КонецЕсли;
	//			КонецЦикла;
	//		Иначе
	//			Сообщение = Новый СообщениеПользователю;
	//			Сообщение.Текст = "Нельзя разделить трудозатраты, неправильно проставлены номера операций в спецификации";
	//			Сообщение.КлючДанных = Этап.Ссылка;
	//			Сообщение.Сообщить();
	//		КонецЕсли;
	//	Иначе
	//		Инд =0;
	//		Пока Инд < Этап.Трудозатраты.Количество() цикл
	//			Стр1 = Этап.Трудозатраты[Инд];
	//			Если Стр1.Количество=0 тогда
	//				Этап.Трудозатраты.Удалить(Инд);
	//			Иначе
	//				Инд = Инд+1;
	//			КонецЕсли;
	//		КонецЦикла;
	//	КонецЕсли;
	//КонецЕсли;	
	
	Если Этап.НеОтгружатьЧастями тогда
		Этап.НеОтгружатьЧастями = Ложь;
	КонецЕсли;    
	Если Этап.ПроизводствоОднойДатой тогда
		Этап.ПроизводствоОднойДатой = Ложь;
	КонецЕсли;
	Если Этап.КоличествоУпаковокФакт <> КоличествоУпаковокФакт тогда
		Этап.КоличествоУпаковокФакт = КоличествоУпаковокФакт;
	КонецЕсли;
	//Этап.ОтразитьКоличествоФактВТЧ();
	ЭТП.ОтразитьКоличествоФактВТЧ1(Этап, КэшированныеЗначения, ДелитьТрудозатраты);
	СжатьТабличнуюЧастьСегодня(Этап.РасходМатериаловИРабот, "ДатаРасхода", ДатаВып, 
		"Подразделение, Номенклатура, Характеристика, ДатаРасхода, Упаковка, СтатьяКалькуляции", СтруктураДействий, КэшированныеЗначения);
	СжатьТабличнуюЧастьСегодня(Этап.ПобочныеИзделия, "ДатаПроизводства", ДатаВып, 
		"Подразделение, Номенклатура, Характеристика, Назначение, Произведено, Отменено, ДатаПроизводства, ПричинаОтмены, Упаковка, СтатьяКалькуляции",
		СтруктураДействий, КэшированныеЗначения);
	
	ТаблицаРасход = Этап.РасходМатериаловИРабот.Выгрузить(, "Подразделение, Номенклатура, Характеристика, Количество");  
	ТаблицаРасход.Свернуть("Подразделение, Номенклатура, Характеристика", "Количество");
	ОтборОбесп = Новый Структура("ВариантОбеспечения", Перечисления.ВариантыОбеспечения.Отгрузить);
	ТаблицаОтгружено = Этап.ОбеспечениеМатериаламиИРаботами.Выгрузить(ОтборОбесп, "Подразделение, Номенклатура, Характеристика, Количество");
	ВычестьСтрокиИзТаблицы(ТаблицаРасход, ТаблицаОтгружено, Новый Структура("Подразделение, Номенклатура, Характеристика"), Ложь);
	ОтборОбесп = Новый Структура("ВариантОбеспечения, Подразделение, Номенклатура, Характеристика",Перечисления.ВариантыОбеспечения.КОбеспечению);
	Для каждого Стр1 из ТаблицаРасход цикл 
		ЗаполнитьЗначенияСвойств(ОтборОбесп, Стр1);
		МасСтр = Этап.ОбеспечениеМатериаламиИРаботами.НайтиСтроки(ОтборОбесп);
		Если МасСтр.Количество() = 0 тогда
			Продолжить;
		КонецЕсли;
		СтрОсталось = МасСтр[0];    
		Расход = Стр1.Количество - СтрОсталось.Количество;
		Если Расход < 0 тогда
			СтрОтгружено = Этап.ОбеспечениеМатериаламиИРаботами.Добавить();
			ЗаполнитьЗначенияСвойств(СтрОтгружено, СтрОсталось,, "КодСтроки");
			СтрОсталось.Количество = СтрОсталось.Количество - Стр1.Количество;
			ПересчитатьКоличествоУпаковокВСтрокеТЧ1(СтрОсталось, СтруктураДействий, КэшированныеЗначения);
		Иначе     
			СтрОтгружено = СтрОсталось;
		КонецЕсли;
		СтрОтгружено.Количество = Стр1.Количество;
		СтрОтгружено.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.Отгрузить;
		СтрОтгружено.ДатаОтгрузки = ДатаВып;
		ПересчитатьКоличествоУпаковокВСтрокеТЧ1(СтрОтгружено, СтруктураДействий, КэшированныеЗначения);
		
		НСтрМас = 1;
		Пока Расход > 0 И МасСтр.Количество() > НСтрМас цикл
			//в норм ситуации не должно вып, на всяк случай
			СтрОсталось = МасСтр[НСтрМас]; 
			Если СтрОсталось.Количество > Расход тогда
				СтрОсталось.Количество = СтрОсталось.Количество - Расход;
				ПересчитатьКоличествоУпаковокВСтрокеТЧ1(СтрОсталось, СтруктураДействий, КэшированныеЗначения);
				Прервать;
			Иначе     
				Расход = Расход - СтрОсталось.Количество;
				НСтрМас = НСтрМас + 1;
				Этап.ОбеспечениеМатериаламиИРаботами.Удалить(СтрОсталось);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	СжатьТабличнуюЧастьСегодня(Этап.ОбеспечениеМатериаламиИРаботами, "ДатаОтгрузки", ДатаВып, 
		"Подразделение, Номенклатура, Характеристика, ДатаОтгрузки, ВариантОбеспечения, Обособленно, Упаковка, СтатьяКалькуляции",
		СтруктураДействий, КэшированныеЗначения);
	
	//Статус этапа
	СтатусБыл = Этап.Статус;
	Если УправлениеПроизводствомКлиентСервер.СравнениеСтатусовЭтапа( Этап.Статус, Перечисления.СтатусыЭтаповПроизводства2_2.Начат) <0 тогда
		Этап.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.Начат;
		Этап.ФактическоеНачалоЭтапа = ДатаВып;
	КонецЕсли;                    
	Если Этап.КоличествоУпаковокФакт = Этап.КоличествоУпаковокПлан тогда
		Этап.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.Завершен; //!Добавить Обработку!
		Этап.ФактическоеОкончаниеЭтапа = ДатаВып;
	КонецЕсли;
	Если Этап.Статус <> СтатусБыл тогда
		ДанныеЗаполнения = Новый Структура("ДатаСобытия,ПлановаяДатаПоступления");
		ДанныеЗаполнения.ДатаСобытия = ДатаВып;
		ДанныеЗаполнения.ПлановаяДатаПоступления = Документы.ЭтапПроизводства2_2.ПлановаяДатаПоступления(Этап.Ссылка);
	    УправлениеПроизводством.ЗаполнитьРеквизитыЭтапаПриИзмененииСтатуса(
			Этап, 
			СтатусБыл, 
			ДанныеЗаполнения);
		//ИзмененныеРеквизиты = "Статус,ФактическоеНачалоЭтапа,
		//	 |ФактическоеОкончаниеЭтапа,
		//	 |НачатоВыполнениеЭтапа,
		//	 |ЗавершеноВыполнениеЭтапа,
		//	 |Выполнено";
		//ПослеИзмененияСтатусаНаСервере(ИзмененныеРеквизиты);
	КонецЕсли;     
	Если Этап.Модифицированность() И ИзменятьПредшественников тогда
		ЗадатьДопРеквизит(Этап, "ЭтапДатаПоследнегоВыпуска", ДатаВып);
	КонецЕсли;
КонецПроцедуры

Процедура ЗакрытьЭтапыПредшественники(Объект, ОтказПроведения) Экспорт
	//Объект = Документы.ЭтапПроизводства2_2.ПустаяСсылка();
	//ДатаВып = ДопРеквизит(Объект, "ЭтапДатаПоследнегоВыпуска");
	ДатаВып = Объект.ДополнительныеСвойства["ДатаПоследнегоВыпуска"];
	Если ЗначениеЗаполнено(ДатаВып) тогда
		ИмяДопРеквизита = "ЭтапЗакрыватьАвтоматически";
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ЭтапПроизводства2_2Последователи.Ссылка КАК Ссылка,
			|	ЭтапПроизводства2_2Последователи.ТипСвязи КАК ТипСвязи,
			|	ЭтапПроизводства2_2Последователи.Ссылка.КоличествоУпаковокПлан КАК КоличествоУпаковокПлан,
			|	ЭтапПроизводства2_2Последователи.Ссылка.КоличествоУпаковокФакт КАК КоличествоУпаковокФакт,
			|	ВыходныеИзделия.КоличествоПроизведено КАК КоличествоПроизведено,
			|	ВыходныеИзделия.Номенклатура КАК Номенклатура
			|ИЗ
			|	Документ.ЭтапПроизводства2_2.Последователи КАК ЭтапПроизводства2_2Последователи
			|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			|			ЭтапПроизводства2_2ВыходныеИзделия.Ссылка КАК Ссылка,
			|			СУММА(ВЫБОР
			|					КОГДА ЭтапПроизводства2_2ВыходныеИзделия.Произведено
			|						ТОГДА ЭтапПроизводства2_2ВыходныеИзделия.КоличествоУпаковок
			|					ИНАЧЕ 0
			|				КОНЕЦ) КАК КоличествоПроизведено,
			|			МАКСИМУМ(ЭтапПроизводства2_2ВыходныеИзделия.Номенклатура) КАК Номенклатура
			|		ИЗ
			|			Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ЭтапПроизводства2_2ВыходныеИзделия
			|		ГДЕ
			|			ЭтапПроизводства2_2ВыходныеИзделия.Назначение.Заказ = &Этап
			|		
			|		СГРУППИРОВАТЬ ПО
			|			ЭтапПроизводства2_2ВыходныеИзделия.Ссылка) КАК ВыходныеИзделия
			|		ПО ЭтапПроизводства2_2Последователи.Ссылка = ВыходныеИзделия.Ссылка
			|ГДЕ
			|	ЭтапПроизводства2_2Последователи.Этап = &Этап
			|	И ЭтапПроизводства2_2Последователи.Ссылка.Проведен";
		Запрос.УстановитьПараметр("Этап",Объект.Ссылка);
		Выб = Запрос.Выполнить().Выбрать(); 
		Пока Выб.Следующий() цикл
			Попытка
				Этап0 = Выб.Ссылка.ПолучитьОбъект();
				Если Выб.ТипСвязи =0 ИЛИ Выб.Номенклатура =Null тогда //в т.ч. этапы с побочным выпуском
					КоличествоУпаковокФакт0 = Выб.КоличествоУпаковокПлан * Объект.КоличествоУпаковокФакт/Объект.КоличествоУпаковокПлан;  
					Если Этап0.КоличествоУпаковокФакт < КоличествоУпаковокФакт0 тогда
						Если Объект.Подразделение = Этап0.Подразделение ИЛИ ДопРеквизит(Этап0.Этап,ИмяДопРеквизита)=Истина тогда
							ОформитьВыполнениеЭтапа(Этап0, ДатаВып, КоличествоУпаковокФакт0);
						Иначе
							Сооб = Новый СообщениеПользователю;
							Сооб.Текст =  "Требуется выполнение этапа другого цеха " + Строка(Выб.Ссылка);
							Сооб.КлючДанных = Выб.Ссылка;
							Сооб.Сообщить();  
							ОтказПроведения = Истина;
						КонецЕсли;	
					КонецЕсли;
				Иначе
					Требуется =0;
					Для каждого Стр из Объект.ОбеспечениеМатериаламиИРаботами цикл
						Если Стр.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.Отгрузить И Стр.Обособленно И
								Стр.Номенклатура = Выб.Номенклатура И Стр.Назначение = Объект.Назначение тогда
							Требуется = Требуется + Стр.КоличествоУпаковок;
						КонецЕсли;
					КонецЦикла;
					Произвести = Требуется - Выб.КоличествоПроизведено;
					Если Произвести>0 тогда
						Если Объект.Подразделение = Этап0.Подразделение ИЛИ ДопРеквизит(Этап0.Этап,ИмяДопРеквизита)=Истина тогда
							СоответствиеНазначений = Новый Соответствие;
							СоответствиеНазначений.Вставить(Объект.Назначение, Произвести);
							ОформитьВыполнениеЭтапаПоНазначениям(Этап0, ДатаВып, СоответствиеНазначений);
						Иначе
							Сооб = Новый СообщениеПользователю;
							Сооб.Текст =  "Требуется выполнение этапа другого цеха " + Строка(Выб.Ссылка);
							Сооб.КлючДанных = Выб.Ссылка;
							Сооб.Сообщить();
						КонецЕсли;	
					КонецЕсли;
				КонецЕсли;
				Если Этап0.Модифицированность() тогда
					Этап0.ОтключитьЗаполнениеНормативногоГрафикаПриЗаписи();
					Этап0.Записать(РежимЗаписиДокумента.Проведение);
				КонецЕсли;
			Исключение
				Сооб = Новый СообщениеПользователю;
				Сооб.Текст =  "Ошибка при записи этапа " + Строка(Выб.Ссылка);
				Сооб.КлючДанных = Выб.Ссылка;
				Сооб.Сообщить();
				ВызватьИсключение;
			КонецПопытки;	
		КонецЦикла;    
		//ЗадатьДопРеквизит(Объект, "ЭтапДатаПоследнегоВыпуска", Неопределено);здесь не работает, перенёс в ПередЗаписью
	КонецЕсли;
КонецПроцедуры

//Использование:
//АРМ_ЛокальныйДиспетчер
Функция СохранитьОбъектВХранилище(знач Док, знач Адрес = Неопределено) Экспорт
	Структ = Новый Структура;
	Мтд = Док.Метаданные();
	Для каждого Рекв из Мтд.Реквизиты цикл
		Структ.Вставить(Рекв.Имя);
	КонецЦикла;
	Для каждого Рекв из Мтд.СтандартныеРеквизиты цикл
		Структ.Вставить(Рекв.Имя);
	КонецЦикла;
	ЗаполнитьЗначенияСвойств(Структ, Док);
	
	ДанныеДок = Новый Структура;
	ДанныеДок.Вставить("Реквизиты", Структ);
	ТабЧ = Новый Структура;
	Для каждого Рекв из Мтд.ТабличныеЧасти цикл
		ТабЧ.Вставить(Рекв.Имя, Док[Рекв.Имя].Выгрузить());	
	КонецЦикла;
	ДанныеДок.Вставить("ТабличныеЧасти",ТабЧ);
	Возврат ПоместитьВоВременноеХранилище(ДанныеДок, Адрес);
	//П = Новый Структура;
	//П.Вставить("НесохраненныйДокумент",ПоместитьВоВременноеХранилище(ДанныеДок));
	//П.Вставить("Ключ", Док.Ссылка);
	//Возврат П;
КонецФункции

Процедура ВзятьОбъектИзХранилища(Объект, знач Хран) Экспорт
	ДанныеДок = ПолучитьИзВременногоХранилища(Хран);
	Рекв = ДанныеДок.Реквизиты;
	ЗаполнитьЗначенияСвойств(Объект, Рекв);
	Для каждого Стр из ДанныеДок.ТабличныеЧасти Цикл
		Объект[Стр.Ключ].Загрузить(Стр.Значение);
	КонецЦикла;
КонецПроцедуры

//Получить все входящие спецификации
//На входе - массив Спецификации УПД: или МенеджерВТ со спецификация-номенклатура в ВТСпец
//Возврат - менеджер временных таблиц, в таблице ВТСпец хранится список входящих спецификаций Спецификация - Номенклатура   
//Использование:
//МатериалыНаСписокИзделий(0)
//ПеремещениеИзЦехаСТрудозатратами
//ПотребностьДеталейНаПлан(0)  
//МатериалыНаВыпускПоЗКлиентов(0)   
//МатериалыНаСписокИзделий01  
//ПотребностьДеталейНаПлан  
//РасчетПлановыхЦен
//УстановитьСпособОбеспеченияВСпецификациях
//МатериалыНаИзделие(0)
//ПлановаяКалькуляция   
//ОбновитьДеталиПоВидамИзделий
////ПотребностьДеталейНаПлан1
Функция ПолучитьСписокВходящихСпецификаций1(Спецификации) Экспорт
	Если ТипЗнч(Спецификации) = Тип("МенеджерВременныхТаблиц") тогда //для совместимости
		Запрос =Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Спецификации;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ЕСТЬNULL(ВТСпец.Спецификация, ОсновныеСпецификации.Ссылка) КАК Спецификация
			|ИЗ
			|	ВТСпец КАК ВТСпец
			|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			|			РесурсныеСпецификации.Ссылка КАК Ссылка
			|		ИЗ
			|			Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
			|		ГДЕ
			|			НЕ РесурсныеСпецификации.ПометкаУдаления
			|			И РесурсныеСпецификации.Статус.Порядок = 1
			|			И РесурсныеСпецификации.ВариантПодбораВДокументы.Порядок = 0
			|			И РесурсныеСпецификации.ТипПроизводственногоПроцесса.Порядок = 0) КАК ОсновныеСпецификации
			|		ПО ВТСпец.Номенклатура = ОсновныеСпецификации.Ссылка.ОсновноеИзделиеНоменклатура
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТСпец"; 
		МасСпец = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
	Иначе
		МасСпец = Спецификации;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПолучитьСписокВходящихСпецификаций(МасСпец);
	Запрос.Текст =  
		"ВЫБРАТЬ
		|	ВТСпец.Спецификация КАК Спецификация,
		|	ВЫБОР
		|		КОГДА ВТСпец.Спецификация.ВариантПодбораВДокументы.Порядок = 0
		|			ТОГДА ВТСпец.Спецификация.ОсновноеИзделиеНоменклатура
		|		ИНАЧЕ NULL
		|	КОНЕЦ КАК Номенклатура
		|ПОМЕСТИТЬ ВТСпец1
		|ИЗ
		|	ВТСпец КАК ВТСпец"
	
		+";УНИЧТОЖИТЬ ВТСпец;ВЫБРАТЬ * ПОМЕСТИТЬ ВТСпец ИЗ ВТСпец1;УНИЧТОЖИТЬ ВТСпец1;
		|УНИЧТОЖИТЬ ПрямыеВхождения;УНИЧТОЖИТЬ ОсновныеСпецификации";
	Запрос.Выполнить();
	Возврат Запрос.МенеджерВременныхТаблиц;
// Таблицы ВТСпец
КонецФункции
Функция ПолучитьСписокВходящихСпецификаций(Спецификации) Экспорт
	МенеджерВТ = ПроверитьЗацикливаниеСпецификаций(МенеджерВТ);
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("Спецификации",Спецификации);
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РесурсныеСпецификации.Ссылка КАК Спецификация
		|ПОМЕСТИТЬ ВТСпецификации
		|ИЗ
		|	Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
		|ГДЕ
		|	РесурсныеСпецификации.Ссылка В(&Спецификации)"
		
		+";ВЫБРАТЬ * ПОМЕСТИТЬ ВТСпец ИЗ ВТСпецификации;
		|ВЫБРАТЬ ПЕРВЫЕ 1 * ИЗ ВТСпецификации";
	Пока НЕ Запрос.Выполнить().Пустой() цикл
		Запрос.Текст =   
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПрямыеВхождения.СпецЧто КАК Спецификация
			|ПОМЕСТИТЬ ВТСпецификацииМатериалов
			|ИЗ
			|	ВТСпецификации КАК ВТСпецификации
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрямыеВхождения КАК ПрямыеВхождения
			|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСпец КАК ВТСпец
			|			ПО ПрямыеВхождения.СпецЧто = ВТСпец.Спецификация
			|		ПО ВТСпецификации.Спецификация = ПрямыеВхождения.СпецКуда
			|ГДЕ
			|	ВТСпец.Спецификация.Ссылка ЕСТЬ NULL"
			
			+";УНИЧТОЖИТЬ ВТСпецификации;
			|ВЫБРАТЬ * ПОМЕСТИТЬ ВТСпецификации ИЗ ВТСпецификацииМатериалов;
			|УНИЧТОЖИТЬ ВТСпецификацииМатериалов;
			|ВЫБРАТЬ * ПОМЕСТИТЬ ВТСпец2 ИЗ ВТСпец
			|ОБЪЕДИНИТЬ ВЫБРАТЬ * ИЗ ВТСпецификации;
			|УНИЧТОЖИТЬ ВТСпец;
			|ВЫБРАТЬ * ПОМЕСТИТЬ ВТСпец ИЗ ВТСпец2;			
			|УНИЧТОЖИТЬ ВТСпец2;
			|ВЫБРАТЬ ПЕРВЫЕ 1 * ИЗ ВТСпецификации";	
	КонецЦикла;
	Запрос.Текст =  
		"УНИЧТОЖИТЬ ВТСпецификации";
	Запрос.Выполнить();                                                    
	
	ИскатьНоменклатуруСНесколькимиОсновнымиСпецификациями(МенеджерВТ);
	Возврат Запрос.МенеджерВременныхТаблиц;
// Таблицы ВТСпец, ПрямыеВхождения, ОсновныеСпецификации
КонецФункции
	
//Получить все спецификации, (включая неосновные) куда (прямо или по цепочке) входит материал
//На входе - Материал (номенклатура)
//Возврат - менеджер временных таблиц, в таблице ВТСпец хранится список спецификаций Спецификация
//Использование:
//МатериалВИзделиях
Функция ПолучитьСписокВсехСпецификацийСМатериалом(Материал) Экспорт
	МенеджерВТ = ПроверитьЗацикливаниеСпецификаций();
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РесурсныеСпецификацииМатериалыИУслуги.Ссылка КАК Спецификация
		|ПОМЕСТИТЬ ВТСпецНов
		|ИЗ
		|	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК РесурсныеСпецификацииМатериалыИУслуги
		|ГДЕ
		|	РесурсныеСпецификацииМатериалыИУслуги.Номенклатура В (&Материал)
		|	И НЕ РесурсныеСпецификацииМатериалыИУслуги.Ссылка.ПометкаУдаления
		|	И РесурсныеСпецификацииМатериалыИУслуги.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.Действует)
		|	И РесурсныеСпецификацииМатериалыИУслуги.Ссылка.ТипПроизводственногоПроцесса.Порядок = 0"
		
		+";ВЫБРАТЬ * ПОМЕСТИТЬ ВТСпец ИЗ ВТСпецНов;
		|ВЫБРАТЬ ПЕРВЫЕ 1 * ИЗ ВТСпецНов";
	Запрос.УстановитьПараметр("Материал",Материал);
	Пока НЕ Запрос.Выполнить().Пустой() цикл
		Запрос.Текст =   
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПрямыеВхождения.СпецКуда КАК Спецификация
			|ПОМЕСТИТЬ ВТСпецНов1
			|ИЗ
			|	ВТСпецНов КАК ВТСпецНов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрямыеВхождения КАК ПрямыеВхождения
			|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСпец КАК ВТСпец
			|			ПО ПрямыеВхождения.СпецКуда = ВТСпец.Спецификация
			|		ПО ВТСпецНов.Спецификация = ПрямыеВхождения.СпецЧто
			|ГДЕ
			|	ВТСпец.Спецификация.Ссылка ЕСТЬ NULL"
			
			+";УНИЧТОЖИТЬ ВТСпецНов;
			|ВЫБРАТЬ * ПОМЕСТИТЬ ВТСпецНов ИЗ ВТСпецНов1;
			|УНИЧТОЖИТЬ ВТСпецНов1;
			|ВЫБРАТЬ * ПОМЕСТИТЬ ВТСпец2 ИЗ ВТСпец
			|ОБЪЕДИНИТЬ ВЫБРАТЬ * ИЗ ВТСпецНов;
			|УНИЧТОЖИТЬ ВТСпец;
			|ВЫБРАТЬ * ПОМЕСТИТЬ ВТСпец ИЗ ВТСпец2;			
			|УНИЧТОЖИТЬ ВТСпец2;
			|ВЫБРАТЬ ПЕРВЫЕ 1 * ИЗ ВТСпецНов";	
	КонецЦикла;
	Запрос.Текст = 
		"УНИЧТОЖИТЬ ВТСпецНов";
	Запрос.Выполнить();
	
	ИскатьНоменклатуруСНесколькимиОсновнымиСпецификациями(МенеджерВТ);
	Возврат Запрос.МенеджерВременныхТаблиц;
КонецФункции

//Получить все основные спецификации, куда (прямо или по цепочке) входит материал
//На входе - Материал (номенклатура)
//Возврат - менеджер временных таблиц, в таблице ВТСпец хранится список спецификаций Спецификация - Номенклатура
Функция ПолучитьСписокСпецификацийСМатериалом(Материал) Экспорт
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РесурсныеСпецификацииМатериалыИУслуги.Ссылка КАК Спецификация,
		|	РесурсныеСпецификацииМатериалыИУслуги.Ссылка.ОсновноеИзделиеНоменклатура КАК Номенклатура
		|ПОМЕСТИТЬ ВТСпецНов
		|ИЗ
		|	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК РесурсныеСпецификацииМатериалыИУслуги
		|ГДЕ
		|	РесурсныеСпецификацииМатериалыИУслуги.Номенклатура = &Материал
		|	И РесурсныеСпецификацииМатериалыИУслуги.Ссылка.ВариантПодбораВДокументы.Порядок = 0
		|	И НЕ РесурсныеСпецификацииМатериалыИУслуги.Ссылка.ПометкаУдаления
		|	И РесурсныеСпецификацииМатериалыИУслуги.Ссылка.Статус.Порядок = 1;"+
		"
		|ВЫБРАТЬ *
		|ПОМЕСТИТЬ ВТСпец
		|ИЗ ВТСпецНов;
		|ВЫБРАТЬ * ИЗ ВТСпецНов;";
	Запрос.УстановитьПараметр("Материал",Материал);
	Пока НЕ Запрос.Выполнить().Пустой() цикл
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РесурсныеСпецификацииМатериалыИУслуги.Ссылка КАК Спецификация
			|ПОМЕСТИТЬ ВТСпецВрем
			|ИЗ
			|	ВТСпецНов КАК ВТСпецНов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК РесурсныеСпецификацииМатериалыИУслуги
			|		ПО ВТСпецНов.Номенклатура = РесурсныеСпецификацииМатериалыИУслуги.Номенклатура
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВТСпецВрем.Спецификация КАК Спецификация,
			|	ВТСпецВрем.Спецификация.ОсновноеИзделиеНоменклатура КАК Номенклатура
			|ПОМЕСТИТЬ ВТСпец0
			|ИЗ
			|	ВТСпецВрем КАК ВТСпецВрем
			|ГДЕ
			|	ВТСпецВрем.Спецификация.ВариантПодбораВДокументы.Порядок = 0
			|	И НЕ ВТСпецВрем.Спецификация.ПометкаУдаления
			|	И ВТСпецВрем.Спецификация.Статус.Порядок = 1
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТСпецВрем
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТСпецНов
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТСпец0.Спецификация КАК Спецификация,
			|	ВТСпец0.Номенклатура КАК Номенклатура
			|ПОМЕСТИТЬ ВТСпецНов
			|ИЗ
			|	ВТСпец0 КАК ВТСпец0
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСпец КАК ВТСпец
			|		ПО ВТСпец0.Спецификация = ВТСпец.Спецификация
			|			И ВТСпец0.Номенклатура = ВТСпец.Номенклатура
			|ГДЕ
			|	ВТСпец.Спецификация.Ссылка ЕСТЬ NULL
			|	И ВТСпец.Номенклатура.Ссылка ЕСТЬ NULL";
		Запрос.Выполнить();
		Запрос.Текст = 
			"УНИЧТОЖИТЬ ВТСпец0;
			|ВЫБРАТЬ *
			|ПОМЕСТИТЬ ВТСпец0
			|ИЗ ВТСпец
			|ОБЪЕДИНИТЬ
			|ВЫБРАТЬ *
			|ИЗ ВТСпецНов;
			|УНИЧТОЖИТЬ ВТСпец;
			|ВЫБРАТЬ *
			|ПОМЕСТИТЬ ВТСпец
			|ИЗ ВТСпец0;
			|УНИЧТОЖИТЬ ВТСпец0;
			|ВЫБРАТЬ * ИЗ ВТСпецНов";
	КонецЦикла;
	Запрос.Текст = 
		"УНИЧТОЖИТЬ ВТСпецНов";
	Запрос.Выполнить();
	Возврат Запрос.МенеджерВременныхТаблиц;
КонецФункции

//Возврат ЭТП.ЗарегистрироватьВнешнийОтчет(ЭтотОбъект);
// Формирует шаблон сведений о внешнем отчете или обработке для последующего заполнения.
//
// Параметры:
//   ВерсияБСП - Строка - Версия библиотеки стандартных подсистем, на которую рассчитывают механизмы внешнего объекта.
//       См. также СтандартныеПодсистемыСервер.ВерсияБиблиотеки().
//
// Возвращаемое значение:
//   ПараметрыРегистрации - Структура - Параметры внешнего объекта.
//       
//       * Вид - Строка - Вид внешнего объекта.
//           Для определения вида рекомендуется использовать функции
//           ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработки<ИмяВида>.
//           Также Вид можно указать явно:
//           ** "ПечатнаяФорма"
//           ** "ЗаполнениеОбъекта"
//           ** "СозданиеСвязанныхОбъектов"
//           ** "Отчет"
//           ** "ДополнительнаяОбработка"
//           ** "ДополнительныйОтчет"
//           ** "ШаблонСообщения".
//       
//       * Версия - Строка - Версия объекта.
//           Задается в формате: "<Старший номер>.<Младший номер>".
//       
//       * Назначение - Массив - Необязательный.
//           Объекты конфигурации, для которых предназначен этот объект.
//           ** Строка - Полное имя объекта метаданных.
//       
//       * Наименование - Строка - Необязательный. Представление для администратора (наименование элемента справочника).
//           Если не заполнено, то берется представление объекта метаданных внешнего объекта.
//       
//       * БезопасныйРежим - Булево - Необязательный. Признак подключения внешней обработки в безопасном режиме.
//           Значение по умолчанию Истина (обработка будет выполняться безопасно).
//           В безопасном режиме:
//             Игнорируется привилегированный режим.
//             Запрещены внешние по отношению к платформе 1С:Предприятия действия:
//               COM;
//               Загрузка внешних компонентов;
//               Запуск внешних приложений и команд операционной системы;
//               Доступ к файловой системе, кроме временных файлов;
//               Доступ к Интернету.
//       
//       * Разрешения - Массив - Необязательный.
//           Дополнительные разрешения, необходимые внешней обработке при работе в безопасном режиме.
//           ** ОбъектXDTO {http://www.1c.ru/1cFresh/ApplicationExtensions/Permissions/a.b.c.d}PermissionBase - Разрешение.
//               Для формирования описания разрешения рекомендуется использовать функции
//               РаботаВБезопасномРежиме.Разрешение<ВидРазрешения>(<ПараметрыРазрешения>).
//       
//       * Информация - Строка - Необязательный. Краткая информация по внешнему объекту.
//           В этом параметре для администратора рекомендуется указать описание возможностей внешнего объекта.
//           Если не заполнено, то берется комментарий объекта метаданных внешнего объекта.
//       
//       * ВерсияБСП - Строка - Необязательный. Версия библиотеки, на которую рассчитывают механизмы внешнего объекта.
//           Подробнее - см. СтандартныеПодсистемыСервер.ВерсияБиблиотеки().
//       
//       * ОпределитьНастройкиФормы - Булево - Необязательный.
//           Флажок тесной интеграции дополнительного отчета с общей формой отчета.
//           Позволяет переопределять некоторые настройки формы и подписываться на ее события.
//           Если Истина, тогда в модуле объекта отчета следует определить процедуру по шаблону:
//           
//           // Настройки общей формы отчета подсистемы "Варианты отчетов".
//           //
//           // Параметры:
//           //   Форма - УправляемаяФорма, Неопределено - Форма отчета или форма настроек отчета.
//           //       Неопределено когда вызов без контекста.
//           //   КлючВарианта - Строка, Неопределено - Имя предопределенного
//           //       или уникальный идентификатор пользовательского варианта отчета.
//           //       Неопределено когда вызов без контекста.
//           //   Настройки - Структура - см. возвращаемое значение
//           //       ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//           //
//           Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
//           	// Код процедуры.
//           КонецПроцедуры
//           
//           Внимание. Этот флажок работает только для дополнительных отчетов, подключенных к общей форме ФормаОтчета.
//           Подробнее см. в документации к подсистемам "Дополнительные отчеты и обработки" и "Варианты отчетов".
//       
//       * Команды - ТаблицаЗначений - Необязательный для отчетов. Настройки команд, поставляемых внешним объектом.
//           
//           ** Идентификатор - Строка - Внутреннее имя команды.
//               Для внешних печатных форм (когда Вид = "ПечатнаяФорма"):
//                 Идентификатор может содержать имена одной или нескольких команд печати,
//                 разделенных запятыми. Подробнее см. описание колонки Идентификатор
//                 в функции СоздатьКоллекциюКомандПечати() общего модуля УправлениеПечатью.
//           
//           ** Представление - Строка - Пользовательское представление команды.
//           
//           ** Использование - Строка - Тип команды:
//               "ВызовКлиентскогоМетода",
//               "ВызовСерверногоМетода",
//               "ЗаполнениеФормы",
//               "ОткрытиеФормы" или
//               "СценарийВБезопасномРежиме".
//               Для получения типов команд рекомендуется использовать функции
//               ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКоманды<ИмяТипа>().
//               В комментариям к этим функциям также даны шаблоны процедур-обработчиков команд.
//           
//           ** ПоказыватьОповещение - Булево - если Истина, то при запуске команды выводится оповещение "Команда выполняется...".
//              Действует для всех типов команд, кроме команд по открытию формы (Использование = "ОткрытиеФормы").
//           
//           ** Модификатор - Строка - Дополнительная классификация команды.
//               Для внешних печатных форм (когда Вид = "ПечатнаяФорма"):
//                 *** "ПечатьMXL" - для печатных форм на основе табличных макетов.
//               Для загрузки данных из файла (когда Вид = "ПечатнаяФорма" и Использование = "ЗагрузкаДанныхИзФайла"):
//                 Модификатор является обязательным для заполнения
//                 и должен содержать полное имя объекта метаданных (справочника),
//                 для которого выполняется загрузка данных.
//           
//           ** Скрыть - Булево - Необязательный. Признак того, что это служебная команда.
//               Если установить в значение Истина, то команда скрывается в карточке дополнительного объекта.
//  
Функция ЗарегистрироватьВнешнийОтчет(знач ОтчетИлиИмя, знач НаименованиеИлиБезопасныйРежим = Истина, БезопасныйРежим = Истина) Экспорт
	
	// Определяем режим вызова: объект или строка
	Если ТипЗнч(ОтчетИлиИмя) = Тип("Строка") Тогда
		// Вызов со строковыми параметрами: ЗарегистрироватьВнешнийОтчет("Имя", "Наименование", БезопасныйРежим)
		ИмяОтчета = ОтчетИлиИмя;
		Если ТипЗнч(НаименованиеИлиБезопасныйРежим) = Тип("Строка") Тогда
			ПредставлениеОтчета = НаименованиеИлиБезопасныйРежим;
		Иначе
			ПредставлениеОтчета = ИмяОтчета;
		КонецЕсли;
		КомментарийОтчета = "";
	Иначе
		// Вызов с объектом: ЗарегистрироватьВнешнийОтчет(ЭтотОбъект)
		МД = ОтчетИлиИмя.Метаданные();
		ИмяОтчета = МД.Имя;
		ПредставлениеОтчета = МД.Представление();
		КомментарийОтчета = МД.Комментарий;
	КонецЕсли;
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке(СтандартныеПодсистемыСервер.ВерсияБиблиотеки());
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительныйОтчет();
	//ПараметрыРегистрации.Назначение.Добавить("Документ.ЗаказКлиента");
	ПараметрыРегистрации.Версия = "1.0";
	//ПараметрыРегистрации.БезопасныйРежим = Ложь;
	//ПараметрыРегистрации.Наименование = "СведенияОВнешнейОбработке";
	ПараметрыРегистрации.Наименование = ПредставлениеОтчета;
	//ПараметрыРегистрации.Информация = "Сведения о внешней обработке";
	ПараметрыРегистрации.Информация = КомментарийОтчета;
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = ПредставлениеОтчета;
	НоваяКоманда.Идентификатор = ИмяОтчета;
	//НоваяКоманда.Модификатор = "ПечатьMXL";
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	НоваяКоманда.ПоказыватьОповещение = Истина;
	
	Возврат ПараметрыРегистрации;  
КонецФункции

Функция ЗарегистрироватьВнешнююОбработку(знач Имя, знач Наименование) Экспорт
    РегистрационныеДанные = Новый Структура;
    РегистрационныеДанные.Вставить("Наименование", Наименование);
    РегистрационныеДанные.Вставить("БезопасныйРежим", Истина);
    РегистрационныеДанные.Вставить("Версия", "1.0");
	//РегистрационныеДанные.Вставить("Назначение", "Производство");
  
	РегистрационныеДанные.Вставить("Вид", "ДополнительнаяОбработка");
  
    РегистрационныеДанные.Вставить("Информация", Наименование);
  
    ///////////// команды /////////////////////////
    тзКоманд = Новый ТаблицаЗначений;
    тзКоманд.Колонки.Добавить("Идентификатор");
    тзКоманд.Колонки.Добавить("Представление");
    тзКоманд.Колонки.Добавить("Модификатор");
    тзКоманд.Колонки.Добавить("ПоказыватьОповещение");
    тзКоманд.Колонки.Добавить("Использование");
  
    строкаКоманды = тзКоманд.Добавить();
    строкаКоманды.Идентификатор = Имя;
    строкаКоманды.Представление = Наименование;
    строкаКоманды.ПоказыватьОповещение = Истина;
    строкаКоманды.Модификатор = "";
    строкаКоманды.Использование = "ОткрытиеФормы";
  
    РегистрационныеДанные.Вставить("Команды", тзКоманд);
  
    ////////////// назначение (в каких объектах используется) /////////////////////////
    //для печ.форм, заполнения, ввода свазанных объектов
    //МассивНазначений = Новый Массив;
    //МассивНазначений.Добавить("Документ.*"); // все документы назначаются
    //МассивНазначений.Добавить("Документ.ПоступлениеТоваровУслуг");
    //МассивНазначений.Добавить("Документ.ЗаказПокупателя");
    //РегистрационныеДанные.Вставить("Назначение", МассивНазначений);
  
    Возврат РегистрационныеДанные;
КонецФункции

Функция ДопРеквизит(знач Объект, знач Имя) Экспорт
	ДопСвойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", Имя);
	СтрДоп = Объект.ДополнительныеРеквизиты.Найти(ДопСвойство,"Свойство");
	Если СтрДоп = Неопределено тогда
		Рез = Неопределено;
	Иначе
		Рез = СтрДоп.Значение;
	КонецЕсли;
	Возврат Рез;
КонецФункции

Процедура ЗадатьДопРеквизит(Объект, знач Имя, знач Значение) Экспорт
	ЗначПусто = НЕ ЗначениеЗаполнено(Значение);
	ДопСвойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", Имя);
	//Если не найдено - игнорируется
	Если ЗначениеЗаполнено(ДопСвойство) тогда
		СтрДоп = Объект.ДополнительныеРеквизиты.Найти(ДопСвойство,"Свойство");
		Если СтрДоп = Неопределено тогда
			Если НЕ ЗначПусто тогда
				СтрДоп = Объект.ДополнительныеРеквизиты.Добавить();
				СтрДоп.Свойство = ДопСвойство;
			КонецЕсли;
		ИначеЕсли ЗначПусто тогда
			Объект.ДополнительныеРеквизиты.Удалить(СтрДоп);
		КонецЕсли;
		Если НЕ ЗначПусто тогда
			СтрДоп.Значение = Значение;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//для СКД
Функция СтрПолучитьСтроку1(знач Стр, знач Ном) Экспорт
	Возврат СтрПолучитьСтроку(Стр, Ном);
КонецФункции

//После ПолучитьСписокВходящихСпецификаций, заполняет ВТСпецМат  
//Использование:
//МатериалВИзделиях
Процедура ЗаполнитьМатериалы(МенеджерВТ, РазузловыватьПолуфабрикатыНаСтороне = Ложь) Экспорт   
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;  
	//Добавляет в ВТСпец Номенклатуру, подразделение первого этапа и выходное количество
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НомерПервогоЭтапа.Владелец КАК Спецификация,
		|	ЭтапыПроизводства.Подразделение КАК ПодразделениеПервогоЭтапа
		|ПОМЕСТИТЬ ВТПервыйЭтап
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЭтапыПроизводства.Владелец КАК Владелец,
		|		МИНИМУМ(ЭтапыПроизводства.НомерЭтапа) КАК НомерЭтапа
		|	ИЗ
		|		Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпец КАК ВТСпец
		|			ПО ЭтапыПроизводства.Владелец = ВТСпец.Спецификация
		|	ГДЕ
		|		НЕ ЭтапыПроизводства.ПометкаУдаления
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ЭтапыПроизводства.Владелец) КАК НомерПервогоЭтапа
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
		|		ПО НомерПервогоЭтапа.Владелец = ЭтапыПроизводства.Владелец
		|			И НомерПервогоЭтапа.НомерЭтапа = ЭтапыПроизводства.НомерЭтапа
		|ГДЕ
		|	НЕ ЭтапыПроизводства.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТСпец.Спецификация КАК Спецификация,
		|	ВЫБОР
		|		КОГДА ВТСпец.Спецификация.ВариантПодбораВДокументы.Порядок = 0
		|			ТОГДА ВТСпец.Спецификация.ОсновноеИзделиеНоменклатура
		|		ИНАЧЕ NULL
		|	КОНЕЦ КАК Номенклатура,
		|	ВТПервыйЭтап.ПодразделениеПервогоЭтапа КАК ПодразделениеПервогоЭтапа,
		|	ВЫБОР
		|		КОГДА ВТСпец.Спецификация.ОсновноеИзделиеКоличествоУпаковок = 0
		|			ТОГДА 1
		|		ИНАЧЕ ВТСпец.Спецификация.ОсновноеИзделиеКоличествоУпаковок
		|	КОНЕЦ КАК ВыходноеКоличество
		|ПОМЕСТИТЬ ВТСпец0
		|ИЗ
		|	ВТСпец КАК ВТСпец
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПервыйЭтап КАК ВТПервыйЭтап
		|		ПО ВТСпец.Спецификация = ВТПервыйЭтап.Спецификация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПервыйЭтап"+";
		|УНИЧТОЖИТЬ ВТСпец;
		|ВЫБРАТЬ *
		|ПОМЕСТИТЬ ВТСпец
		|ИЗ ВТСпец0;
		|УНИЧТОЖИТЬ ВТСпец0";
	Запрос.Выполнить();
	
	//МассивПокупные = Новый Массив;
	//СтатьяКаль = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул","ПокупныеКомплектующие");
	//Если ЗначениеЗаполнено(СтатьяКаль) тогда
	//	МассивПокупные.Добавить(СтатьяКаль);
	//КонецЕсли;
	//СтатьяКаль = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул","ПолуфабрикатыПроизводимыеНаСтороне");
	//Если ЗначениеЗаполнено(СтатьяКаль) тогда
	//	МассивПокупные.Добавить(СтатьяКаль);
	//КонецЕсли;
	МассивПолуфабрикаты = Новый Массив;
	СтатьяКаль = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул","ПолуфабрикатыСобственные");
	Если ЗначениеЗаполнено(СтатьяКаль) тогда
		МассивПолуфабрикаты.Добавить(СтатьяКаль);
	КонецЕсли;
	СтатьяКаль = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул","ПолуфабрикатыПроизводимыеВПроцессе");
	Если ЗначениеЗаполнено(СтатьяКаль) тогда
		МассивПолуфабрикаты.Добавить(СтатьяКаль);
	КонецЕсли;   
	Если РазузловыватьПолуфабрикатыНаСтороне тогда
		СтатьяКаль = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул","ПолуфабрикатыПроизводимыеНаСтороне");
		Если ЗначениеЗаполнено(СтатьяКаль) тогда
			МассивПолуфабрикаты.Добавить(СтатьяКаль);
		КонецЕсли;
	КонецЕсли;
	//СтатьяКалькуляцииПокупныеМатериалы = Справочники.СтатьиКалькуляции.НайтиПоРеквизиту("ИдентификаторДляФормул","ПокупныеМатериалы");
	//СтатьяКалькуляцииЗамена = Справочники.СтатьиКалькуляции.НайтиПоНаименованию("ЗАМЕНА", Истина);
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = ТекстЗапросаМатериалы(); 
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковкиК",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ВТМатериалыИУслуги.Упаковка",
			"ВТМатериалыИУслуги.Номенклатура"));
	//Запрос.УстановитьПараметр( "СтатьяКалькуляцииЗамена", СтатьяКалькуляцииЗамена ); 
	Запрос.УстановитьПараметр( "СтатьяПолуфабрикаты", МассивПолуфабрикаты );
	Запрос.Выполнить();
КонецПроцедуры

//Использование:
//МатериалыНаИзделие
//ПлановаяКалькуляцияНаСписокИзделий(0)  
//МатериалыНаСписокИзделий    
//ПрименяемостьНаСписокИзделийПоЦеху
//МатериалыНаВыпускПоЗКлиентов
//ПлановаяКалькуляцияНаСписокИзделий
Функция РасчитатьМатериалы(знач Изделия, РазузловыватьПолуфабрикатыНаСтороне = Ложь) Экспорт
	Изделия1 = Новый ТаблицаЗначений;
	Изделия1.Колонки.Добавить("Спецификация", Новый ОписаниеТипов("СправочникСсылка.РесурсныеСпецификации"));
	Изделия1.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	Изделия1.Колонки.Добавить("Индекс", Новый ОписаниеТипов("Число"));
	Спецификации = Новый Массив;
	Для Инд = 0 по Изделия.Количество()-1 цикл
		Изд = Изделия[Инд];
		Спецификации.Добавить(Изд.Спецификация);
		Изд1 = Изделия1.Добавить();
		Изд1.Спецификация = Изд.Спецификация;
		Изд1.Количество = Изд.Количество;
		Изд1.Индекс = Инд;
	КонецЦикла;
	МенеджерВТ = ЭТП.ПолучитьСписокВходящихСпецификаций(Спецификации);
	ЗаполнитьМатериалы(МенеджерВТ, РазузловыватьПолуфабрикатыНаСтороне);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Изд1.Спецификация КАК Спецификация,
		|	Изд1.Количество КАК Количество,
		|	Изд1.Индекс КАК Индекс
		|ПОМЕСТИТЬ ВТИзд
		|ИЗ
		|	&Изд1 КАК Изд1";
	Запрос.УстановитьПараметр("Изд1", Изделия1);
	Запрос.Выполнить();
	РасчитатьМатериалы1(МенеджерВТ);
	Возврат Запрос.МенеджерВременныхТаблиц;
// Таблицы ВТСпец, ВТСпецМат, ВТМат, ВТИзд	
КонецФункции
//Использование:
//МатериалВИзделиях
Процедура РасчитатьМатериалы1(МенеджерВТ) Экспорт
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Изд.Спецификация КАК Спецификация,
		|	Изд.Спецификация КАК ГолСпец,
		|	Изд.Количество КАК Количество,
		|	Изд.Индекс КАК Индекс
		|ПОМЕСТИТЬ ВТСпец1
		|ИЗ
		|	ВТИзд КАК Изд";
	Запрос.Выполнить();
	
	Пока НЕ МенеджерВТ.Таблицы["ВТСпец1"].ПолучитьДанные().Пустой() цикл
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВТСпецМат.Номенклатура КАК Номенклатура,
			|	ВТСпецМат.СтатьяКалькуляции КАК СтатьяКалькуляции,
			|	ВТСпецМат.Подразделение КАК Подразделение,
			|	ВТСпец1.ГолСпец КАК ГолСпец,
			|	ВТСпец1.Индекс КАК Индекс,
			|	ВТСпец1.Спецификация КАК СпецификацияКуда,
			|	ВТСпецМат.Спецификация КАК СпецификацияЧто,
			|	ВТСпецМат.КоличествоУпаковок КАК КоличествоУпаковок,
			|	ВТСпецМат.КоэффициентУпаковкиКБазовойЕдиницеИзмерения КАК КоэффициентУпаковкиКБазовойЕдиницеИзмерения,
			|	ВЫБОР
			|		КОГДА ВТСпец1.Спецификация.ОсновноеИзделиеКоличествоУпаковок = 0
			|			ТОГДА 1
			|		ИНАЧЕ ВТСпец1.Спецификация.ОсновноеИзделиеКоличествоУпаковок
			|	КОНЕЦ КАК ВыходноеКоличество,
			|	ВТСпец1.Количество КАК КоличествоКуда,
			|	ВТСпецМат.АлгоритмРасчетаКоличества КАК АлгоритмРасчетаКоличества
			|ПОМЕСТИТЬ ВТМатНов1
			|ИЗ
			|	ВТСпец1 КАК ВТСпец1
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпецМат КАК ВТСпецМат
			|		ПО ВТСпец1.Спецификация = ВТСпецМат.СпецификацияВладелец
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТСпец1
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТМатНов1.Номенклатура КАК Номенклатура,
			|	ВТМатНов1.СтатьяКалькуляции КАК СтатьяКалькуляции,
			|	ВТМатНов1.Подразделение КАК Подразделение,
			|	ВТМатНов1.ГолСпец КАК ГолСпец,
			|	ВТМатНов1.Индекс КАК Индекс,
			|	ВТМатНов1.СпецификацияКуда КАК СпецификацияКуда,
			|	ВТМатНов1.СпецификацияЧто КАК СпецификацияЧто,
			|	ВТМатНов1.КоличествоУпаковок КАК КоличествоУпаковок,
			|	ВТМатНов1.КоэффициентУпаковкиКБазовойЕдиницеИзмерения КАК КоэффициентУпаковкиКБазовойЕдиницеИзмерения,
			|	ВТМатНов1.ВыходноеКоличество КАК ВыходноеКоличество,
			|	ВТМатНов1.КоличествоКуда КАК КоличествоКуда,
			|	ВТМатНов1.АлгоритмРасчетаКоличества КАК АлгоритмРасчетаКоличества,
			|	0 КАК Количество
			|ИЗ
			|	ВТМатНов1 КАК ВТМатНов1
			|ГДЕ
			|	ВТМатНов1.АлгоритмРасчетаКоличества <> """"";
		РезЗап = Запрос.Выполнить();
		ФормулНет = РезЗап.Пустой();
		
		Если ФормулНет тогда
			Запрос.Текст = "";
		Иначе
			ТабФ = РезЗап.Выгрузить();
			Для каждого СтрФ из ТабФ цикл
				СтрФ.Количество = ВыполнитьАлгоритмРасчетаКоличества(СтрФ.АлгоритмРасчетаКоличества, 
					СтрФ.КоличествоКуда) * СтрФ.КоэффициентУпаковкиКБазовойЕдиницеИзмерения;
			КонецЦикла;
			Запрос.Текст =
				"ВЫБРАТЬ
				|	ВТМатНов1.Номенклатура КАК Номенклатура,
				|	ВТМатНов1.СтатьяКалькуляции КАК СтатьяКалькуляции,
				|	ВТМатНов1.Подразделение КАК Подразделение,
				|	ВТМатНов1.ГолСпец КАК ГолСпец,
				|	ВТМатНов1.Индекс КАК Индекс,
				|	ВТМатНов1.СпецификацияКуда КАК СпецификацияКуда,
				|	ВТМатНов1.СпецификацияЧто КАК СпецификацияЧто,
				|	ВТМатНов1.КоличествоУпаковок КАК КоличествоУпаковок,
				|	ВТМатНов1.КоэффициентУпаковкиКБазовойЕдиницеИзмерения КАК КоэффициентУпаковкиКБазовойЕдиницеИзмерения,
				|	ВТМатНов1.ВыходноеКоличество КАК ВыходноеКоличество,
				|	ВТМатНов1.КоличествоКуда КАК КоличествоКуда,
				|	ВТМатНов1.АлгоритмРасчетаКоличества КАК АлгоритмРасчетаКоличества,
				|	ВТМатНов1.Количество КАК Количество
				|ПОМЕСТИТЬ ВТМатНовФ
				|ИЗ
				|	&ВТМатНов1 КАК ВТМатНов1" + "; ";
			Запрос.УстановитьПараметр("ВТМатНов1", ТабФ);
		КонецЕсли;
		Запрос.Текст = Запрос.Текст +
			"ВЫБРАТЬ
			|	ВТМатНов1.Номенклатура КАК Номенклатура,
			|	ВТМатНов1.СтатьяКалькуляции КАК СтатьяКалькуляции,
			|	ВТМатНов1.Подразделение КАК Подразделение,
			|	ВТМатНов1.ГолСпец КАК ГолСпец,
			|	ВТМатНов1.Индекс КАК Индекс,
			|	ВТМатНов1.СпецификацияКуда КАК СпецификацияКуда,
			|	ВТМатНов1.СпецификацияЧто КАК СпецификацияЧто,
			|	ВТМатНов1.КоличествоУпаковок * ВТМатНов1.КоэффициентУпаковкиКБазовойЕдиницеИзмерения / ВТМатНов1.ВыходноеКоличество * ВТМатНов1.КоличествоКуда КАК Количество
			|ПОМЕСТИТЬ ВТМатНов
			|ИЗ
			|	ВТМатНов1 КАК ВТМатНов1
			|ГДЕ
			|	ВТМатНов1.АлгоритмРасчетаКоличества = """"
			|";
		Если НЕ ФормулНет тогда
			Запрос.Текст = Запрос.Текст + "
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	ВТМатНовФ.Номенклатура,
				|	ВТМатНовФ.СтатьяКалькуляции,
				|	ВТМатНовФ.Подразделение,
				|	ВТМатНовФ.ГолСпец,
				|	ВТМатНовФ.Индекс,
				|	ВТМатНовФ.СпецификацияКуда,
				|	ВТМатНовФ.СпецификацияЧто,
				|	ВТМатНовФ.Количество
				|ИЗ
				|	ВТМатНовФ КАК ВТМатНовФ
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|УНИЧТОЖИТЬ ВТМатНовФ
				|"
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + ";
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТМатНов1
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТМатНов.СпецификацияЧто КАК Спецификация,
			|	ВТМатНов.ГолСпец КАК ГолСпец,
			|	ВТМатНов.Индекс КАК Индекс,
			|	СУММА(ВТМатНов.Количество) КАК Количество
			|ПОМЕСТИТЬ ВТСпец1
			|ИЗ
			|	ВТМатНов КАК ВТМатНов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
			|		ПО ВТМатНов.СпецификацияЧто = РесурсныеСпецификации.Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	ВТМатНов.СпецификацияЧто,
			|	ВТМатНов.ГолСпец,
			|	ВТМатНов.Индекс";
		Запрос.Текст = Запрос.Текст +
			";
			|ВЫБРАТЬ * ПОМЕСТИТЬ ВТМат1 ИЗ ВТМатНов";
		Если МенеджерВТ .Таблицы.Найти("ВТМат") <> Неопределено тогда
			Запрос.Текст = Запрос.Текст +
				"
				|ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ * ИЗ ВТМат;
				|УНИЧТОЖИТЬ ВТМат";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст +
			";
			|"+
			"УНИЧТОЖИТЬ ВТМатНов
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТМат1.Номенклатура КАК Номенклатура,
			|	ВТМат1.СтатьяКалькуляции КАК СтатьяКалькуляции,
			|	ВТМат1.Подразделение КАК Подразделение,
			|	ВТМат1.ГолСпец КАК ГолСпец,
			|	ВТМат1.Индекс КАК Индекс,
			|	ВТМат1.СпецификацияКуда КАК СпецификацияКуда,
			|	ВТМат1.СпецификацияЧто КАК СпецификацияЧто,
			|	СУММА(ВТМат1.Количество) КАК Количество
			|ПОМЕСТИТЬ ВТМат
			|ИЗ
			|	ВТМат1 КАК ВТМат1
			|
			|СГРУППИРОВАТЬ ПО
			|	ВТМат1.Номенклатура,
			|	ВТМат1.СтатьяКалькуляции,
			|	ВТМат1.Подразделение,
			|	ВТМат1.ГолСпец,
			|	ВТМат1.Индекс,
			|	ВТМат1.СпецификацияКуда,
			|	ВТМат1.СпецификацияЧто
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТМат1";
		Запрос.Выполнить();
	КонецЦикла;
	Запрос.Текст = "УНИЧТОЖИТЬ ВТСпец1";
	Запрос.Выполнить();	
КонецПроцедуры

//После РасчитатьМатериалы
//Использование:
//МатериалыНаИзделие
//ПлановаяКалькуляцияНаСписокИзделий(0)
////ПлановаяКалькуляцияНаСписокИзделий
Процедура ПолучитьСборки(МенеджерВТ) Экспорт
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВТМат.СпецификацияЧто КАК Спецификация,
		|	ВТМат.Количество КАК Количество,
		|	ВТМат.ГолСпец КАК ГолСпец,
		|	ВТМат.Индекс КАК Индекс
		|ПОМЕСТИТЬ ВТСборки0
		|ИЗ
		|	ВТМат КАК ВТМат
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТИзд.Спецификация,
		|	ВТИзд.Количество,
		|	ВТИзд.Спецификация,
		|	ВТИзд.Индекс
		|ИЗ
		|	ВТИзд КАК ВТИзд
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТСборки0.Спецификация КАК Спецификация,
		|	СУММА(ВТСборки0.Количество) КАК Количество,
		|	ВТСборки0.ГолСпец КАК ГолСпец,
		|	ВТСборки0.Индекс КАК Индекс
		|ПОМЕСТИТЬ ВТСборки1
		|ИЗ
		|	ВТСборки0 КАК ВТСборки0
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТСборки0.Спецификация,
		|	ВТСборки0.ГолСпец,
		|	ВТСборки0.Индекс
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТСборки0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТСборки1.Спецификация КАК Спецификация,
		|	ВТСборки1.Количество КАК Количество,
		|	ВТСборки1.ГолСпец КАК ГолСпец,
		|	ВТСборки1.Индекс КАК Индекс,
		|	ВТСпец.ПодразделениеПервогоЭтапа КАК ПодразделениеПервогоЭтапа,
		|	ВТСпец.ВыходноеКоличество КАК ВыходноеКоличество
		|ПОМЕСТИТЬ ВТСборки
		|ИЗ
		|	ВТСборки1 КАК ВТСборки1
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпец КАК ВТСпец
		|		ПО ВТСборки1.Спецификация = ВТСпец.Спецификация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТСборки1";
	Запрос.Выполнить();
// Таблицы ВТСпец, ВТСпецМат, ВТМат, ВТИзд, ВТСборки
КонецПроцедуры

Функция ТекстЗапросаМатериалы()
	Возврат
	"ВЫБРАТЬ
	|	РесурсныеСпецификацииМатериалыИУслуги.Ссылка КАК Ссылка,
	|	РесурсныеСпецификацииМатериалыИУслуги.Номенклатура КАК Номенклатура,
	|	РесурсныеСпецификацииМатериалыИУслуги.Характеристика КАК Характеристика,
	|	РесурсныеСпецификацииМатериалыИУслуги.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаХраненияОстатков,
	|	РесурсныеСпецификацииМатериалыИУслуги.Упаковка КАК Упаковка,
	|	РесурсныеСпецификацииМатериалыИУслуги.КоличествоУпаковок КАК КоличествоУпаковок,
	|	РесурсныеСпецификацииМатериалыИУслуги.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ВЫРАЗИТЬ(РесурсныеСпецификацииМатериалыИУслуги.АлгоритмРасчетаКоличества КАК СТРОКА(200)) КАК АлгоритмРасчетаКоличества,
	|	РесурсныеСпецификацииМатериалыИУслуги.ПроизводитсяВПроцессе КАК ПроизводитсяВПроцессе,
	|	РесурсныеСпецификацииМатериалыИУслуги.СпособПолученияМатериала КАК СпособПолученияМатериала,
	|	РесурсныеСпецификацииМатериалыИУслуги.ИсточникПолученияПолуфабриката КАК ИсточникПолученияПолуфабриката,
	|	ВЫБОР
	|		КОГДА РесурсныеСпецификацииМатериалыИУслуги.Этап = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|			ТОГДА ВТСпец.ПодразделениеПервогоЭтапа
	|		ИНАЧЕ РесурсныеСпецификацииМатериалыИУслуги.Этап.Подразделение
	|	КОНЕЦ КАК Подразделение
	|ПОМЕСТИТЬ ВТМатериалыИУслуги
	|ИЗ
	|	ВТСпец КАК ВТСпец
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК РесурсныеСпецификацииМатериалыИУслуги
	|		ПО ВТСпец.Спецификация = РесурсныеСпецификацииМатериалыИУслуги.Ссылка
	|ГДЕ
	|	РесурсныеСпецификацииМатериалыИУслуги.СпособПолученияМатериала <> ЗНАЧЕНИЕ(Перечисление.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТМатериалыИУслуги.Номенклатура КАК Номенклатура,
	|	ВТМатериалыИУслуги.Характеристика КАК Характеристика,
	|	ВТМатериалыИУслуги.Упаковка КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ВТМатериалыИУслуги.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ &ТекстЗапросаКоэффициентУпаковкиК
	|	КОНЕЦ КАК КоэффициентУпаковкиКБазовойЕдиницеИзмерения,
	|	ВТМатериалыИУслуги.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ВТМатериалыИУслуги.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ВТМатериалыИУслуги.АлгоритмРасчетаКоличества КАК АлгоритмРасчетаКоличества,
	|	ВТМатериалыИУслуги.ПроизводитсяВПроцессе КАК ПроизводитсяВПроцессе,
	|	ВТМатериалыИУслуги.СпособПолученияМатериала КАК СпособПолученияМатериала,
	|	ВТМатериалыИУслуги.ИсточникПолученияПолуфабриката КАК ИсточникПолученияПолуфабриката,
	|	ОсновныеСпецификации.Ссылка КАК ОсновнаяСпецификация,
	|	ВТМатериалыИУслуги.Подразделение КАК Подразделение,
	|	ВТМатериалыИУслуги.Ссылка КАК СпецификацияВладелец
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	ВТМатериалыИУслуги КАК ВТМатериалыИУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			РесурсныеСпецификации.Ссылка КАК Ссылка
	|		ИЗ
	|			Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
	|		ГДЕ
	|			РесурсныеСпецификации.ВариантПодбораВДокументы.Порядок = 0
	|			И НЕ РесурсныеСпецификации.ПометкаУдаления
	|			И РесурсныеСпецификации.Статус.Порядок = 1) КАК ОсновныеСпецификации
	|		ПО ВТМатериалыИУслуги.Номенклатура = ОсновныеСпецификации.Ссылка.ОсновноеИзделиеНоменклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Номенклатура КАК Номенклатура,
	|	ВТ.Характеристика КАК Характеристика,
	|	ВТ.Упаковка КАК Упаковка,
	|	ВТ.КоэффициентУпаковкиКБазовойЕдиницеИзмерения КАК КоэффициентУпаковкиКБазовойЕдиницеИзмерения,
	|	ВТ.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ВТ.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ВТ.АлгоритмРасчетаКоличества КАК АлгоритмРасчетаКоличества,
	|	ВТ.ПроизводитсяВПроцессе КАК ПроизводитсяВПроцессе,
	|	ВТ.СпособПолученияМатериала КАК СпособПолученияМатериала,
	|	ВТ.ИсточникПолученияПолуфабриката КАК ИсточникПолученияПолуфабриката,
	|	ВТ.ОсновнаяСпецификация КАК ОсновнаяСпецификация,
	|	ВТ.КоличествоУпаковок * ВТ.КоэффициентУпаковкиКБазовойЕдиницеИзмерения КАК КоличествоЕдиницХранения,
	|	ВТ.Подразделение КАК Подразделение,
	|	ВТ.СпецификацияВладелец КАК СпецификацияВладелец,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(ВТ.ИсточникПолученияПолуфабриката КАК Справочник.РесурсныеСпецификации).Ссылка, ВЫБОР
	|			КОГДА ВТ.СтатьяКалькуляции В (&СтатьяПолуфабрикаты)
	|				ТОГДА ВТ.ОсновнаяСпецификация
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК Спецификация
	|ПОМЕСТИТЬ ВТСпецМат
	|ИЗ
	|	ВТ КАК ВТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТМатериалыИУслуги";
КонецФункции

//На выходе МенеджерВременныхТаблиц с таблицей ПрямыеВхождения
Функция ПроверитьЗацикливаниеСпецификаций(ГенерироватьОшибку = Истина, ДатаАктуальности = Неопределено) Экспорт
	Запрос = Новый Запрос();                                          
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если ДатаАктуальности = Неопределено Тогда
		ДатаАктуальности = ТекущаяДатаСеанса();
	КонецЕсли;
	Запрос.УстановитьПараметр("ТекущаяДата", ДатаАктуальности);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВыходныеИзделия.Ссылка КАК Ссылка,
		|	ВыходныеИзделия.Номенклатура КАК Номенклатура,
		|	ВыходныеИзделия.Ссылка.Код КАК Код
		|ПОМЕСТИТЬ ОсновныеСпецификацииВсе
		|ИЗ
		|	Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК ВыходныеИзделия
		|ГДЕ
		|	НЕ ВыходныеИзделия.Ссылка.ПометкаУдаления
		|	И ВыходныеИзделия.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.Действует)
		|	И ВыходныеИзделия.Ссылка.ВариантПодбораВДокументы = ЗНАЧЕНИЕ(Перечисление.ВариантыПодбораСпецификацииВДокументы.Автоматически)
		|	И ВыходныеИзделия.Ссылка.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Сборка)
		|	И (ВыходныеИзделия.Ссылка.НачалоДействия = ДАТАВРЕМЯ(1, 1, 1)
		|		ИЛИ ВыходныеИзделия.Ссылка.НачалоДействия <= &ТекущаяДата)
		|	И (ВыходныеИзделия.Ссылка.КонецДействия = ДАТАВРЕМЯ(1, 1, 1)
		|		ИЛИ ВыходныеИзделия.Ссылка.КонецДействия > &ТекущаяДата)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОсновныеСпецификацииВсе.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(ОсновныеСпецификацииВсе.Код) КАК МаксКод
		|ПОМЕСТИТЬ МаксКодыСпецификаций
		|ИЗ
		|	ОсновныеСпецификацииВсе КАК ОсновныеСпецификацииВсе
		|СГРУППИРОВАТЬ ПО
		|	ОсновныеСпецификацииВсе.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОсновныеСпецификацииВсе.Ссылка КАК Ссылка,
		|	ОсновныеСпецификацииВсе.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ОсновныеСпецификации
		|ИЗ
		|	ОсновныеСпецификацииВсе КАК ОсновныеСпецификацииВсе
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксКодыСпецификаций КАК МаксКодыСпецификаций
		|		ПО ОсновныеСпецификацииВсе.Номенклатура = МаксКодыСпецификаций.Номенклатура
		|			И ОсновныеСпецификацииВсе.Код = МаксКодыСпецификаций.МаксКод
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВремТ.СпецКуда КАК СпецКуда,
		|	ВремТ.СпецЧто КАК СпецЧто
		|ПОМЕСТИТЬ ПрямыеВхождения
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		РесурсныеСпецификацииМатериалыИУслуги.Ссылка КАК СпецКуда,
		|		ЕСТЬNULL(ВЫРАЗИТЬ(РесурсныеСпецификацииМатериалыИУслуги.ИсточникПолученияПолуфабриката КАК Справочник.РесурсныеСпецификации).Ссылка, ОсновныеСпецификации.Ссылка) КАК СпецЧто
		|	ИЗ
		|		Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК РесурсныеСпецификацииМатериалыИУслуги
		|			ЛЕВОЕ СОЕДИНЕНИЕ ОсновныеСпецификации КАК ОсновныеСпецификации
		|			ПО РесурсныеСпецификацииМатериалыИУслуги.Номенклатура = ОсновныеСпецификации.Номенклатура
		|	ГДЕ
		|		НЕ РесурсныеСпецификацииМатериалыИУслуги.Ссылка.ПометкаУдаления
		|		И РесурсныеСпецификацииМатериалыИУслуги.СпособПолученияМатериала <> ЗНАЧЕНИЕ(Перечисление.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе)) КАК ВремТ
		|ГДЕ
		|	НЕ ВремТ.СпецЧто ЕСТЬ NULL" //во влож запросе включая закрытые!
		
		+";ВЫБРАТЬ * ПОМЕСТИТЬ ПолныеВхождения ИЗ ПрямыеВхождения;
		|ВЫБРАТЬ ПЕРВЫЕ 1 * ИЗ ПолныеВхождения";   
		//Глубина =0;
	Пока НЕ	Запрос.Выполнить().Пустой() цикл   
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПолныеВхождения.СпецКуда КАК Спецификация
			|ИЗ
			|	ПолныеВхождения КАК ПолныеВхождения
			|ГДЕ
			|	ПолныеВхождения.СпецКуда = ПолныеВхождения.СпецЧто";
		РезВып = Запрос.Выполнить();
		Если НЕ РезВып.Пустой() тогда
			Выб = РезВып.Выбрать();
			Пока Выб.Следующий() цикл
				Сооб = Новый СообщениеПользователю;
				Сооб.Текст =  "Зацикливание в спецификации " + Выб.Спецификация;
				Сооб.КлючДанных = Выб.Спецификация;
				Сооб.Сообщить();
			КонецЦикла;
			Если ГенерироватьОшибку тогда
				ВызватьИсключение "Обнаружено зацикливание в спецификациях!";
			Иначе
				Возврат Неопределено;
			КонецЕсли;
		КонецЕсли;
		Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПолныеВхождения.СпецКуда КАК СпецКуда,
			|	ПрямыеВхождения.СпецЧто КАК СпецЧто
			|ПОМЕСТИТЬ ПолныеВхождения1
			|ИЗ
			|	ПолныеВхождения КАК ПолныеВхождения
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрямыеВхождения КАК ПрямыеВхождения
			|		ПО ПолныеВхождения.СпецЧто = ПрямыеВхождения.СпецКуда"
			
			+";УНИЧТОЖИТЬ ПолныеВхождения;
			|ВЫБРАТЬ * ПОМЕСТИТЬ ПолныеВхождения ИЗ ПолныеВхождения1;
			|УНИЧТОЖИТЬ ПолныеВхождения1;
			|ВЫБРАТЬ ПЕРВЫЕ 1 * ИЗ ПолныеВхождения";
		//Глубина = Глубина+1;
	КонецЦикла;                                           
	Запрос.Текст =
		"УНИЧТОЖИТЬ ПолныеВхождения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОсновныеСпецификацииВсе
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ МаксКодыСпецификаций";
	Запрос.Выполнить();
	Возврат Запрос.МенеджерВременныхТаблиц;	  
	//ПрямыеВхождения
	//ОсновныеСпецификации
КонецФункции    

Процедура ИскатьНоменклатуруСНесколькимиОсновнымиСпецификациями(МенеджерВТ)
	Запрос = Новый Запрос();                                          
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТСпец.Спецификация) КАК КоличествоСпецификаций,
		|	ВТСпец.Спецификация.ОсновноеИзделиеНоменклатура КАК Номенклатура
		|ИЗ
		|	ВТСпец КАК ВТСпец
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОсновныеСпецификации КАК ОсновныеСпецификации
		|		ПО ВТСпец.Спецификация = ОсновныеСпецификации.Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТСпец.Спецификация.ОсновноеИзделиеНоменклатура
		|
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТСпец.Спецификация) > 1"; 
	Выб = Запрос.Выполнить().Выбрать();
	Пока Выб.Следующий() цикл
		Ном = Выб.Номенклатура;
		Сооб = Новый СообщениеПользователю;
		Сооб.Текст =  "Возможна ошибка! Несколько основных спецификаций у " + Строка(Ном);
		Сооб.КлючДанных = Ном;
		Сооб.Сообщить();
	КонецЦикла;
КонецПроцедуры

//из ЭтапПроизводства2_2 Модуль объекта 2.5.9.125
//для ОформитьВыполнениеЭтапа
Процедура ВычестьСтрокиИзТаблицы(Таблица1, Таблица2, УсловиеПоиска, ОбработкаВПорядкеСтрок = Истина) Экспорт
	
	Индекс2 = 0;
	
	Пока Индекс2 < Таблица2.Количество() Цикл
		
		Строка2 = Таблица2[Индекс2];
		
		ЗаполнитьЗначенияСвойств(УсловиеПоиска, Строка2);
		НайденныеСтроки = ?(ОбработкаВПорядкеСтрок,
			ОбщегоНазначенияУТ.НайтиСтрокиССохранениемПорядка(Таблица1, УсловиеПоиска, Истина),
			Таблица1.НайтиСтроки(УсловиеПоиска));
		
		Для Индекс1 = 0 По НайденныеСтроки.ВГраница() Цикл

			Строка1 = НайденныеСтроки[Индекс1];
			Количество = Мин(Строка1.Количество, Строка2.Количество);
			
			Строка1.Количество = Строка1.Количество - Количество;
			Строка2.Количество = Строка2.Количество - Количество;
			
			Если Строка2.Количество = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если Строка2.Количество = 0 Тогда
			Таблица2.Удалить(Строка2);
		Иначе
			Индекс2 = Индекс2 + 1;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

//для табличных частей этапов производства
Процедура СжатьТабличнуюЧастьСегодня(ТабличнаяЧасть, КолонкаДаты, Дата, РеквизитыГруппировки, СтруктураДействий, КэшированныеЗначения) экспорт
	Отбор1 = Новый Структура(РеквизитыГруппировки);
	Для каждого Строка из ТабличнаяЧасть.НайтиСтроки(Новый Структура(КолонкаДаты, Дата)) цикл
		ЗаполнитьЗначенияСвойств(Отбор1, Строка);
		Строка0 = ТабличнаяЧасть.НайтиСтроки(Отбор1)[0];
		Если Строка.НомерСтроки <> Строка0.НомерСтроки тогда
			Строка0.Количество = Строка0.Количество + Строка.Количество;
			ПересчитатьКоличествоУпаковокВСтрокеТЧ1(Строка0, СтруктураДействий, КэшированныеЗначения);
			ТабличнаяЧасть.Удалить(Строка);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры    

//из ЭтапПроизводства2_2 Модуль объекта 2.5.9.125 
//с заменой переменных и частично закомментирована обработка серий
Процедура ОтразитьКоличествоФактВТЧ1(Этап1, КэшированныеЗначения, ДелитьТрудозатраты = Ложь, Исполнитель = Неопределено) экспорт

	Если Этап1.КоличествоУпаковокПлан = 0 Тогда
		Возврат;
	КонецЕсли;

	// Расчет коэффициента
	Коэффициент = 0;
	Если Этап1.УпаковкаПлан = Этап1.УпаковкаФакт Тогда
		Коэффициент = Этап1.КоличествоУпаковокФакт/Этап1.КоличествоУпаковокПлан;
	Иначе
		Номенклатура = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Этап1.ПартияПроизводства, "ОсновноеИзделиеНоменклатура");
		КоэффициентПлан = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Этап1.УпаковкаПлан, Номенклатура);
		КоэффициентФакт = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Этап1.УпаковкаФакт, Номенклатура);
		Коэффициент = (Этап1.КоличествоУпаковокФакт * КоэффициентФакт) / (Этап1.КоличествоУпаковокПлан * КоэффициентПлан);
	КонецЕсли;

	// Описание переменных
	СтруктураПоиска = Новый Структура("Подразделение, Номенклатура, Характеристика");
	СтруктураПоискаСерии = Новый Структура("Подразделение, Номенклатура, Характеристика, Получатель, Назначение, Произведено, ДатаПроизводства");
	СтруктураДействий = Новый Структура("ПересчитатьКоличествоУпаковок");
	//КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	ПараметрыУказанияСерий = Документы.ЭтапПроизводства2_2.ПараметрыУказанияСерий(Этап1);

	// Расход материалов и работ
	ТаблицаИтоги = Этап1.ОбеспечениеМатериаламиИРаботами.Выгрузить(, "Подразделение, Номенклатура, Характеристика, Количество");
	ТаблицаИтоги.Свернуть("Подразделение, Номенклатура, Характеристика", "Количество");
	Для каждого Строка Из ТаблицаИтоги Цикл
		Строка.Количество = Строка.Количество * Коэффициент;
	КонецЦикла;
	ТаблицаИтоги.Индексы.Добавить("Подразделение, Номенклатура, Характеристика");

	ТаблицаРасход = Этап1.РасходМатериаловИРабот.Выгрузить(, "Подразделение, Номенклатура, Характеристика, Количество");
	ВычестьСтрокиИзТаблицы(ТаблицаИтоги, ТаблицаРасход, Новый Структура("Подразделение, Номенклатура, Характеристика"), Ложь);

	ТаблицаОстатки = Этап1.ОбеспечениеМатериаламиИРаботами.Выгрузить(
	Новый Структура("Отменено", Ложь),
	"Подразделение, Номенклатура, Характеристика, Серия, Упаковка, Количество, СтатьяКалькуляции");
	ТаблицаОстатки.Индексы.Добавить("Подразделение, Номенклатура, Характеристика, Серия");
	ТаблицаОстатки.Индексы.Добавить("Подразделение, Номенклатура, Характеристика");

	ТаблицаРасход = Этап1.РасходМатериаловИРабот.Выгрузить(, "Подразделение, Номенклатура, Характеристика, Серия, Количество");
	ТаблицаЭкономия = Этап1.ЭкономияМатериалов.Выгрузить(, "Подразделение, Номенклатура, Характеристика, Серия, Количество");

	ОбщегоНазначенияУТ.ДобавитьВТаблицуПорядковыйНомерСтрок(ТаблицаОстатки);
	ВычестьСтрокиИзТаблицы(ТаблицаОстатки, ТаблицаРасход, Новый Структура("Подразделение, Номенклатура, Характеристика, Серия"));
	ВычестьСтрокиИзТаблицы(ТаблицаОстатки, ТаблицаЭкономия, Новый Структура("Подразделение, Номенклатура, Характеристика, Серия"));
	ВычестьСтрокиИзТаблицы(ТаблицаОстатки, ТаблицаЭкономия, Новый Структура("Подразделение, Номенклатура, Характеристика"));
	ВычестьСтрокиИзТаблицы(ТаблицаОстатки, ТаблицаРасход, Новый Структура("Подразделение, Номенклатура, Характеристика"));

	Если Этап1.РасходОднойДатой Тогда
		ДатаОперации = Этап1.ДатаРасхода;
	ИначеЕсли Этап1.ФактическоеОкончаниеЭтапа <> '000101010000'
		И Этап1.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.Завершен Тогда
		ДатаОперации = Этап1.ФактическоеОкончаниеЭтапа;
	Иначе
		ДатаОперации = НачалоДня(ТекущаяДатаСеанса());
	КонецЕсли;

	Для Индекс = 0 По ТаблицаОстатки.Количество()-1 Цикл

		СтрокаОстатки = ТаблицаОстатки[Индекс];
		Если СтрокаОстатки.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;

		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаОстатки);
		НайденныеСтроки = ТаблицаИтоги.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.ВГраница() = -1 
			ИЛИ НайденныеСтроки[0].Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		СтрокаИтоги = НайденныеСтроки[0];

		НоваяСтрока = Этап1.РасходМатериаловИРабот.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОстатки,, "Количество");
		НоваяСтрока.Количество = Мин(СтрокаОстатки.Количество, СтрокаИтоги.Количество);
		НоваяСтрока.ДатаРасхода = ДатаОперации;

		ПересчитатьКоличествоУпаковокВСтрокеТЧ1(НоваяСтрока, СтруктураДействий, КэшированныеЗначения);

		СтрокаИтоги.Количество = СтрокаИтоги.Количество - НоваяСтрока.Количество;

	КонецЦикла;

	//ЗаполнитьСтатусыУказанияСерийВТЧ("РасходМатериаловИРабот", ПараметрыУказанияСерий);

	// Выходные и побочные изделия
	Если Этап1.ПроизводствоОднойДатой Тогда
		ДатаОперации = Этап1.ДатаПроизводства;
	ИначеЕсли Этап1.ФактическоеОкончаниеЭтапа <> '000101010000'
		И Этап1.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.Завершен Тогда
		ДатаОперации = Этап1.ФактическоеОкончаниеЭтапа;
	Иначе
		ДатаОперации = НачалоДня(ТекущаяДатаСеанса());
	КонецЕсли;

	Для Сч = 1 По 2 Цикл

		Если Сч = 1 Тогда
			ИмяТЧ      = "ВыходныеИзделия";
			ИмяТЧСерии = "ВыходныеИзделияСерии";
		Иначе
			ИмяТЧ      = "ПобочныеИзделия";
			ИмяТЧСерии = "ПобочныеИзделияСерии";
		КонецЕсли;

		//ТаблицаИтоги = ТабличнаяЧастьПоИмени(ИмяТЧ).Выгрузить(, "Подразделение, Номенклатура, Характеристика, Количество, Произведено");
		ТаблицаИтоги = Этап1[ИмяТЧ].Выгрузить(, "Подразделение, Номенклатура, Характеристика, Количество, Произведено");
		ТаблицаИтоги.Колонки.Добавить("КоличествоПроизведено", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		ТаблицаИтоги.Колонки.Добавить("Распределить", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));

		Для каждого Строка Из ТаблицаИтоги.НайтиСтроки(Новый Структура("Произведено", Истина)) Цикл
			Строка.КоличествоПроизведено = Строка.Количество;
		КонецЦикла;
		ТаблицаИтоги.Свернуть("Подразделение, Номенклатура, Характеристика", "Количество, КоличествоПроизведено, Распределить");
		Для каждого Строка Из ТаблицаИтоги Цикл
			Строка.Распределить = Строка.Количество * Коэффициент - Строка.КоличествоПроизведено;
		КонецЦикла;
		ТаблицаИтоги.Индексы.Добавить("Подразделение, Номенклатура, Характеристика");

		Индекс = 0;
		Пока Индекс < Этап1[ИмяТЧ].Количество() Цикл

			Строка = Этап1[ИмяТЧ][Индекс];
			Индекс = Индекс + 1;
			Если Строка.Произведено
				ИЛИ Строка.Отменено
				ИЛИ Строка.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;

			ЗаполнитьЗначенияСвойств(СтруктураПоиска, Строка);
			СтрокаИтоги = ТаблицаИтоги.НайтиСтроки(СтруктураПоиска)[0];
			Если СтрокаИтоги.Распределить <= 0 Тогда
				Продолжить;
			КонецЕсли;

			СтрокаПроизведено = Строка;
			Если СтрокаИтоги.Распределить < Строка.Количество Тогда

				НоваяСтрока = Этап1[ИмяТЧ].Вставить(Индекс);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка,, "КодСтроки,КлючНоменклатура");
				НоваяСтрока.Количество = СтрокаИтоги.Распределить;

				Строка.Количество = Строка.Количество - СтрокаИтоги.Распределить;

				ПересчитатьКоличествоУпаковокВСтрокеТЧ1(НоваяСтрока, СтруктураДействий, КэшированныеЗначения);
				ПересчитатьКоличествоУпаковокВСтрокеТЧ1(Строка, СтруктураДействий, КэшированныеЗначения);

				Если ИмяТЧ = "ВыходныеИзделия" Тогда
					ПроизводствоКлиентСервер.ПересчитатьДолюСтоимостиПриРазбиенииСтроки(
					НоваяСтрока,
					Строка,
					Этап1.СпособРаспределенияЗатратНаВыходныеИзделия);
				КонецЕсли;

				СтрокаПроизведено = НоваяСтрока;
				Индекс = Индекс + 1;

			КонецЕсли;

			Если СтрокаПроизведено.СтатусУказанияСерий <> 0 Тогда

				Распределить = СтрокаПроизведено.Количество;

				ЗаполнитьЗначенияСвойств(СтруктураПоискаСерии, СтрокаПроизведено);
				Для каждого СтрокаСерии Из Этап1[ИмяТЧСерии].НайтиСтроки(СтруктураПоискаСерии) Цикл

					Если Распределить >= СтрокаСерии.Количество Тогда
						СтрокаСерии.Произведено = Истина;
						СтрокаСерии.ДатаПроизводства = ДатаОперации;
						Распределить = Распределить - СтрокаСерии.Количество;
					Иначе
						НоваяСтрокаСерии = Этап1[ИмяТЧСерии].Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрокаСерии, СтрокаСерии);
						НоваяСтрокаСерии.Количество = Распределить;
						НоваяСтрокаСерии.Произведено = Истина;
						НоваяСтрокаСерии.ДатаПроизводства = ДатаОперации;

						СтрокаСерии.Количество = СтрокаСерии.Количество - Распределить;
						Распределить = 0;
					КонецЕсли;

					Если Распределить = 0 Тогда
						Прервать;
					КонецЕсли;

				КонецЦикла;

			КонецЕсли;

			СтрокаПроизведено.Произведено = Истина;
			СтрокаПроизведено.ДатаПроизводства = ДатаОперации;

			СтрокаИтоги.Распределить = СтрокаИтоги.Распределить - СтрокаПроизведено.Количество;

		КонецЦикла;
		//Если ИмяТЧ = "ПобочныеИзделия" Тогда
		//	Документы.ЭтапПроизводства2_2.ЗаполнитьЦеныПоВидуЦен(ЭтотОбъект);
		//КонецЕсли;
		//ЗаполнитьСтатусыУказанияСерийВТЧ(ИмяТЧ, ПараметрыУказанияСерий);

	КонецЦикла;

	Если ДелитьТрудозатраты ИЛИ Коэффициент = 1 тогда
	// Трудозатраты    
	СтруктураПоиска = Новый Структура("Подразделение, ВидРабот");
	Если Документы.ЭтапПроизводства2_2.ИспользуетсяОтметкаВыполненныхТрудозатрат(Этап1) Тогда

		ТаблицаИтоги = Этап1.Трудозатраты.Выгрузить(, "Подразделение, ВидРабот, Количество, Выполнено");
		ТаблицаИтоги.Колонки.Добавить("КоличествоВыполнено", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		ТаблицаИтоги.Колонки.Добавить("Распределить", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));

		Для каждого Строка Из ТаблицаИтоги.НайтиСтроки(Новый Структура("Выполнено", Истина)) Цикл
			Строка.КоличествоВыполнено = Строка.Количество;
		КонецЦикла;
		ТаблицаИтоги.Свернуть("Подразделение, ВидРабот", "Количество, КоличествоВыполнено, Распределить");
		Для каждого Строка Из ТаблицаИтоги Цикл
			Строка.Распределить = Строка.Количество * Коэффициент - Строка.КоличествоВыполнено;
		КонецЦикла;
		ТаблицаИтоги.Индексы.Добавить("Подразделение, ВидРабот");

		Если Этап1.ВыполнениеРаботОднойДатой Тогда
			ДатаОперации = Этап1.ДатаВыполненияРабот;
		ИначеЕсли Этап1.ФактическоеОкончаниеЭтапа <> '000101010000'
			И Этап1.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.Завершен Тогда
			ДатаОперации = Этап1.ФактическоеОкончаниеЭтапа;
		Иначе
			ДатаОперации = НачалоДня(ТекущаяДатаСеанса());
		КонецЕсли;

		Индекс = 0;
		Пока Индекс < Этап1.Трудозатраты.Количество() Цикл

			Строка = Этап1.Трудозатраты[Индекс];
			Индекс = Индекс + 1;

			Если Строка.Выполнено
				И Не ЗначениеЗаполнено(Строка.Исполнитель)
				И ЗначениеЗаполнено(Исполнитель) Тогда
				Строка.Исполнитель = Исполнитель;
			КонецЕсли;

			Если Строка.Выполнено
				ИЛИ Строка.Отменено
				ИЛИ Строка.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;

			ЗаполнитьЗначенияСвойств(СтруктураПоиска, Строка);
			СтрокаИтоги = ТаблицаИтоги.НайтиСтроки(СтруктураПоиска)[0];
			Если СтрокаИтоги.Распределить <= 0 Тогда
				Продолжить;
			КонецЕсли;

			СтрокаВыполнено = Строка;
			Если СтрокаИтоги.Распределить < Строка.Количество Тогда

				НоваяСтрока = Этап1.Трудозатраты.Вставить(Индекс);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка, , "КодСтроки,КлючВидРабот");
				НоваяСтрока.Количество = СтрокаИтоги.Распределить;

				Строка.Количество = Строка.Количество - СтрокаИтоги.Распределить;

				СтрокаВыполнено = НоваяСтрока;
				Индекс = Индекс + 1;

			КонецЕсли;
			СтрокаВыполнено.Выполнено = Истина;
			СтрокаВыполнено.ДатаВыполнения = ДатаОперации;
			Если ЗначениеЗаполнено(Исполнитель)
				И НЕ ЗначениеЗаполнено(СтрокаВыполнено.Исполнитель) Тогда
				СтрокаВыполнено.Исполнитель = Исполнитель;
			КонецЕсли;

			СтрокаИтоги.Распределить = СтрокаИтоги.Распределить - СтрокаВыполнено.Количество;

		КонецЦикла;

	КонецЕсли;
    КонецЕсли;
КонецПроцедуры

//из ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьКоличествоУпаковокВСтрокеТЧ 2.5.11.56
Процедура ПересчитатьКоличествоУпаковокВСтрокеТЧ1(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт 
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоУпаковок") Тогда
		Если ТекущаяСтрока.Количество = 0 Тогда
			ТекущаяСтрока.КоличествоУпаковок = 0; //Вставка
		ИначеЕсли ТекущаяСтрока.Упаковка = ЭТП_ПовтИсп.ЕдиницаИзмеренияШтуки() тогда  
			ТекущаяСтрока.КоличествоУпаковок = ТекущаяСтрока.Количество; //КонецВставки
		Иначе
			Коэффициент = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентВесОбъемПрочиеРеквизитыУпаковки(ТекущаяСтрока.Упаковка, ТекущаяСтрока.Номенклатура).Коэффициент;
			Если Коэффициент <> 0 Тогда
				ТекущаяСтрока.КоличествоУпаковок = ТекущаяСтрока.Количество / Коэффициент;
			Иначе
				ТекстИсключения = НСтр("ru = 'При попытке пересчета количества в %ЕдИзмерения% превышена допустимая разрядность.';
										|en = 'While trying to recalculate the amount in %ЕдИзмерения%, the allowable capacity exceeded.'");
				ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ЕдИзмерения%", ТекущаяСтрока.Упаковка);
				
				ТекущаяСтрока.Количество = 0;
				ТекущаяСтрока.КоличествоУпаковок = 0;
				ТекущаяСтрока.Упаковка = ПредопределенноеЗначение("Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка");
				
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры  

Функция Выгрузить1(Таблица, СтрокиИлиОтбор = Неопределено, Колонки = Неопределено) Экспорт
	Если ТипЗнч(Таблица) = Тип("ТаблицаЗначений") тогда
		Возврат Таблица.Скопировать(СтрокиИлиОтбор, Колонки);
	Иначе
		Возврат Таблица.Выгрузить(СтрокиИлиОтбор, Колонки);
	КонецЕсли;
КонецФункции

