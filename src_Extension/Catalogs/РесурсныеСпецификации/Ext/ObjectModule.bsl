// И (Статус = Перечисления.СтатусыСпецификаций.ВРазработке ИЛИ Статус = Перечисления.СтатусыСпецификаций.Закрыта)
&После("ПередЗаписью")
Процедура Расш1_ПередЗаписью(Отказ)
	Если Не Отказ  // И ПометкаУдаления
		Тогда 
		ИзменяетсяСтатус = (Ссылка.Статус <> Статус);
		ИзменяетсяВариантПодбора = (Ссылка.ВариантПодбораВДокументы <> ВариантПодбораВДокументы);
		
		Если ИзменяетсяСтатус ИЛИ ИзменяетсяВариантПодбора Тогда
			
			// Проверка наличия спецификации в регистре калькуляций
			ЗапросРегистра = Новый Запрос;
			ЗапросРегистра.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
				|	СпецификацииВКалькуляциях.ПлановаяКалькуляция КАК Калькуляция
				|ИЗ
				|	РегистрСведений.А1_СпецификацииВКалькуляциях КАК СпецификацииВКалькуляциях
				|ГДЕ
				|	СпецификацииВКалькуляциях.РесурснаяСпецификация = &Спецификация
				|	И СпецификацииВКалькуляциях.ПлановаяКалькуляция.Проведен";
			ЗапросРегистра.УстановитьПараметр("Спецификация", Ссылка);
			ВыборкаРегистра = ЗапросРегистра.Выполнить().Выбрать();
			Если ВыборкаРегистра.Следующий() Тогда
				Отказ = Истина;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Нельзя изменить статус и вариант подбора спецификации, она используется в плановой калькуляции: " + ВыборкаРегистра.Калькуляция);
				Возврат;
			КонецЕсли;

				// Проверка на незавершённые этапы производства
			Запрос = Новый Запрос; 
			Запрос.УстановитьПараметр("Спецификация", Ссылка);
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
				|	ЭтапПроизводства2_2.Ссылка КАК Этап
				|ИЗ
				|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
				|ГДЕ
				|	ЭтапПроизводства2_2.Спецификация = &Спецификация
				|	И ЭтапПроизводства2_2.Спецификация.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.Действует) 
				|	И ЭтапПроизводства2_2.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
				|	И ЭтапПроизводства2_2.Проведен";
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Отказ = Истина;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Нельзя изменить спецификацию, по ней существует незавершённый этап: " + Выборка.Этап);
				Возврат;
			КонецЕсли;
		КонецЕсли; 
		
		Если (Ссылка.КонецДействия <> КонецДействия) Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			|	ОбъектыКалькуляции.Ссылка КАК Калькуляция,
			|	ОбъектыКалькуляции.Ссылка.Дата КАК ДатаКалькуляции
			|ИЗ
			|	Документ.ПлановаяКалькуляция2_2.ОбъектыКалькуляции КАК ОбъектыКалькуляции
			|ГДЕ
			|	ОбъектыКалькуляции.Объект = &Спецификация
			|	И ОбъектыКалькуляции.Ссылка.Дата > &КонецДействия
			|	И ОбъектыКалькуляции.Ссылка.Проведен";
			Запрос.УстановитьПараметр("Спецификация", Ссылка);
			Запрос.УстановитьПараметр("КонецДействия", КонецДействия);
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Отказ = Истина;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Нельзя изменить спецификацию, по ней существует плановая калькуляция «" + Выборка.Калькуляция + "» от " + Формат(Выборка.ДатаКалькуляции, "ДЛФ=D") + ", датированная после нового конца действия!");
				Возврат;
			КонецЕсли;
		КонецЕсли;	
		
		Если (Ссылка.Статус = Перечисления.СтатусыСпецификаций.ВРазработке) Тогда  //если переводим из "ВРазработке"   
			
			МинимальнаяПартияЗапуска = Ссылка.МинимальнаяПартияВыпуска;
			ОсновноеИзделиеКоличествоУпаковок = Ссылка.ОсновноеИзделиеКоличествоУпаковок;  
			
			Если МинимальнаяПартияЗапуска <> 0 Тогда 
				КоэффициентНаПартиюЗапуска = ОсновноеИзделиеКоличествоУпаковок / МинимальнаяПартияЗапуска;
			Иначе
				КоэффициентНаПартиюЗапуска = ОсновноеИзделиеКоличествоУпаковок;
			КонецЕсли;
			
			Для Каждого Строка Из МатериалыИУслуги Цикл  
				
				УпаковкаСтроки = Строка.Упаковка;
				
				Если УпаковкаСтроки <> Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка() Тогда   
					
					НоменклатураСтроки = Строка.Номенклатура;				
					КоличествоУпаковокСтроки = Строка.КоличествоУпаковок;   
					
					Запрос = Новый Запрос; 
					Запрос.Текст = "ВЫБРАТЬ
					|	Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
					|ИЗ
					|	Справочник.Номенклатура КАК Номенклатура
					|ГДЕ  
					|	Номенклатура.Ссылка = &НоменклатураСтроки
					|	И Номенклатура.ЕдиницаИзмерения <> &УпаковкаСтроки";	  
					
					Запрос.УстановитьПараметр("НоменклатураСтроки", НоменклатураСтроки);				
					Запрос.УстановитьПараметр("УпаковкаСтроки", УпаковкаСтроки);  
					
					Выборка = Запрос.Выполнить().Выбрать();
					
					Если Выборка.Следующий() Тогда   
						ДанныеУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентВесОбъемПрочиеРеквизитыУпаковки(УпаковкаСтроки, НоменклатураСтроки);
						КоэффициентУпаковки = ДанныеУпаковки.Коэффициент;    				
						Строка.КоличествоУпаковок = КоличествоУпаковокСтроки * КоэффициентУпаковки;				
						Строка.Упаковка = Выборка.ЕдиницаИзмерения; 
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю( "Единица измерения материала «" + НоменклатураСтроки + "» переведена из «" + УпаковкаСтроки + "» в «" + Строка.Упаковка + "»");
					КонецЕсли;
					
					КоличествоМатериалаНаПартиюЗапуска = Строка.КоличествоУпаковок / КоэффициентНаПартиюЗапуска;
					
					Если (КоличествоМатериалаНаПартиюЗапуска < 0.001) И (Строка.АлгоритмРасчетаКоличества = "") Тогда
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю( "Нулевое количество материала «" + НоменклатураСтроки + "» на минимальную партию запуска!");
					КонецЕсли; 
					
				КонецЕсли;
				
			КонецЦикла;			                      
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры


