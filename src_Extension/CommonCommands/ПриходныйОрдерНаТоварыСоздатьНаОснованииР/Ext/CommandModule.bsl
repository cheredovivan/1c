
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	//Вставить содержимое обработчика.
	//ПараметрыФормы = Новый Структура("", );
	//ОткрытьФорму("ОбщаяФорма.", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	Основание =	ЗаполнитьСтруктуру(ПараметрКоманды);
	ОткрытьФорму("Документ.ПриходныйОрдерНаТовары.Форма.ФормаДокумента",
		Новый Структура("Основание",Основание),
		ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
КонецПроцедуры

&НаСервере
Функция ЗаполнитьСтруктуру(ПараметрКоманды)
	Если ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") И
			ЗначениеЗаполнено(ПараметрКоманды.ЗаказПоставщику) тогда
		Возврат ЗаполнитьСтруктуру(ПараметрКоманды.ЗаказПоставщику);
	ИначеЕсли
		ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.ПоступлениеОтПереработчика") И
			ЗначениеЗаполнено(ПараметрКоманды.ЗаказПереработчику) тогда
		Возврат ЗаполнитьСтруктуру(ПараметрКоманды.ЗаказПереработчику);
	КонецЕсли;
	Если ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.ЗаказПереработчику") тогда
		Если ПараметрКоманды.Продукция.Количество()>0 
				И ТипЗнч(ПараметрКоманды.Продукция[0].Получатель) = Тип("СправочникСсылка.Склады") тогда
			Склад = ПараметрКоманды.Продукция[0].Получатель;
		Иначе
			Склад = Справочники.Склады.ПустаяСсылка();
		КонецЕсли;
	Иначе
		Склад = ПараметрКоманды.Склад;
	КонецЕсли;
	Если ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.ЗаказПереработчику") тогда
		ДатаВходящегоДокумента = ПараметрКоманды.ДатаПоДаннымПартнера;
		НомерВходящегоДокумента = ПараметрКоманды.НомерПоДаннымПартнера;
	ИначеЕсли ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.ЗаказПоставщику") тогда
		ДатаВходящегоДокумента = ПараметрКоманды.ДатаПоДаннымПоставщика;
		НомерВходящегоДокумента = ПараметрКоманды.НомерПоДаннымПоставщика;  
	Иначе
		ДатаВходящегоДокумента = ПараметрКоманды.ДатаВходящегоДокумента;
		НомерВходящегоДокумента = ПараметрКоманды.НомерВходящегоДокумента;  
	КонецЕсли;       
	ХозяйственнаяОперация = ПараметрКоманды.ХозяйственнаяОперация;
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПроизводствоУПереработчика тогда
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОтПереработчика;
	КонецЕсли;
	Основание = Новый Структура;
	Основание.Вставить("Распоряжение",               ПараметрКоманды.Ссылка);
	Основание.Вставить("Отправитель",                ПараметрКоманды.Партнер);
	Основание.Вставить("Склад",                      Склад);
	Основание.Вставить("Помещение",                  Справочники.СкладскиеПомещения.ПустаяСсылка());
	Основание.Вставить("ЗонаПриемки",                Справочники.СкладскиеЯчейки.ПустаяСсылка());
	Основание.Вставить("ДатаВходящегоДокумента",     ДатаВходящегоДокумента);
	Основание.Вставить("НомерВходящегоДокумента",    НомерВходящегоДокумента);
	Основание.Вставить("ДатаПоступления",            Дата(1,1,1));
	Основание.Вставить("ХозяйственнаяОперация",      ХозяйственнаяОперация);
	Возврат Основание;	
КонецФункции