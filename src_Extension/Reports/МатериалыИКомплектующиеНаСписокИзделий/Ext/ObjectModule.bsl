&НаСервере
Перем СтатьяКалькуляцииТехотход;  
Перем СтатьяКалькуляцииПокупныеКомплектующие;
Перем СтатьяКалькуляцииПокупныеМатериалы;
Перем СтатьяКалькуляцииЗамена;
//Перем СтатьяКалькуляцииПолуфабрикаты;

Перем ТЗСпецификации;
Перем ТЗМатериалы;
Перем ТЗОстатки; //СвободныеОстатки или ОстаткиНаСкладах?

Функция ПолучитьЗначениеПараметра(ИмяПараметра, ЗначениеПоУмолчанию = Неопределено)
	НайденныйПараметрВарианта = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	Если НайденныйПараметрВарианта = Неопределено Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	// Сначала проверяем пользовательские настройки
	Если ЗначениеЗаполнено(НайденныйПараметрВарианта.ИдентификаторПользовательскойНастройки) Тогда
		НайденныйПараметр = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(НайденныйПараметрВарианта.ИдентификаторПользовательскойНастройки);
		Если НайденныйПараметр <> Неопределено Тогда
			Если НайденныйПараметр.Использование Тогда
				Возврат НайденныйПараметр.Значение;
			Иначе
				Возврат ЗначениеПоУмолчанию; // Параметр отключен пользователем
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Если в пользовательских настройках не найдено - берём из настроек варианта
	Если НайденныйПараметрВарианта.Использование Тогда
		Возврат НайденныйПараметрВарианта.Значение;
	КонецЕсли;
	
	Возврат ЗначениеПоУмолчанию;
КонецФункции

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДиспетчерыВыпуска = ПолучитьЗначениеПараметра("ДиспетчерыВыпуска");
	Если НЕ ЗначениеЗаполнено(ДиспетчерыВыпуска) тогда
		ДиспетчерыВыпуска = Null;
	КонецЕсли;
	
	Период = ПолучитьЗначениеПараметра("Период");
	Если Период <> Неопределено И ЗначениеЗаполнено(Период.ДатаНачала) тогда
		ПолучитьПланПроизводства(Период.ДатаНачала, Период.ДатаОкончания, ДиспетчерыВыпуска);
	КонецЕсли;
	
	Склады = ПолучитьЗначениеПараметра("Склады");
	
	РазузловыватьПолуфабрикатыПроизводимыеНаСтороне = ПолучитьЗначениеПараметра("РазузловыватьПолуфабрикатыПроизводимыеНаСтороне", Ложь);
	
	МенеджерВТ = Неопределено;
	РассчитатьМатериалы(Склады, МенеджерВТ);
	
	Трудозатраты = РасчитатьТрудозатраты(МенеджерВТ);
	ЗаполнитьКоличествоИзделий(Трудозатраты);
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("Материалы", Материалы);
	ВнешниеНаборыДанных.Вставить("Изделия", Изделия);
	ВнешниеНаборыДанных.Вставить("Трудозатраты", Трудозатраты);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, 
					КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровки);
					//КомпоновщикНастроек.Настройки, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных,ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
 	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);	
КонецПроцедуры

&НаСервере

Процедура ПолучитьПланПроизводства(ДатаНачала, ДатаОкончания, ДиспетчерыВыпуска)
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказНаПроизводство2_2Продукция.Номенклатура КАК Номенклатура,
		|	СУММА(ЗаказНаПроизводство2_2Продукция.КоличествоУпаковок) КАК Количество,
		|	ЗаказНаПроизводство2_2Продукция.Спецификация КАК Спецификация
		|ИЗ
		|	Документ.ЗаказНаПроизводство2_2.Продукция КАК ЗаказНаПроизводство2_2Продукция
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			ЭтапПроизводства2_2.Распоряжение КАК Распоряжение
		|		ИЗ
		|			Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
		|		ГДЕ
		|			ЭтапПроизводства2_2.Проведен) КАК Этапы
		|		ПО ЗаказНаПроизводство2_2Продукция.Ссылка = Этапы.Распоряжение
		|ГДЕ
		|	ЗаказНаПроизводство2_2Продукция.Ссылка.Проведен
		|	И ЗаказНаПроизводство2_2Продукция.Ссылка.Дата МЕЖДУ &Дата0 И &Дата1
		|	И (ЗаказНаПроизводство2_2Продукция.Ссылка.Подразделение В (&ДиспетчерыВыпуска)
		|			ИЛИ &ДиспетчерыВыпуска ЕСТЬ NULL)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказНаПроизводство2_2Продукция.Номенклатура,
		|	ЗаказНаПроизводство2_2Продукция.Спецификация
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказНаПроизводство2_2Продукция.Номенклатура.Код";
		//"ВЫБРАТЬ
		//|	ПланПроизводстваПродукция.Номенклатура КАК Номенклатура,
		//|	СУММА(ПланПроизводстваПродукция.Количество) КАК Количество,
		//|	ПланПроизводстваПродукция.Спецификация КАК Спецификация
		//|ИЗ
		//|	Документ.ПланПроизводства.Продукция КАК ПланПроизводстваПродукция
		//|ГДЕ
		//|	ПланПроизводстваПродукция.Ссылка.Проведен
		//|	И ПланПроизводстваПродукция.Ссылка.Дата МЕЖДУ &Дата0 И &Дата1
		//|	И НЕ ПланПроизводстваПродукция.Полуфабрикат
		//|
		//|СГРУППИРОВАТЬ ПО
		//|	ПланПроизводстваПродукция.Номенклатура,
		//|	ПланПроизводстваПродукция.Спецификация
		//|
		//|УПОРЯДОЧИТЬ ПО
		//|	ПланПроизводстваПродукция.Номенклатура.Код";
	Запрос.УстановитьПараметр("Дата0",ДатаНачала);
	Запрос.УстановитьПараметр("Дата1",ДатаОкончания);
	Запрос.УстановитьПараметр("ДиспетчерыВыпуска",ДиспетчерыВыпуска);
	// Если пользователь уже добавил изделия вручную — используем их
	// Если изделий нет — загружаем из заказов на производство за период
	Если Изделия.Количество() = 0 тогда
		Изделия.Загрузить(Запрос.Выполнить().Выгрузить());
	КонецЕсли;
КонецПроцедуры

Процедура РассчитатьМатериалы(Склады, МенеджерВТ)
	Материалы.Очистить();
	Если Изделия.Количество()<=0 тогда
		ВызватьИсключение "Не заполнены изделия";
	КонецЕсли;
	
	МенеджерВТ = ЭТП.РасчитатьМатериалы(Изделия, РазузловыватьПолуфабрикатыПроизводимыеНаСтороне);
	ЗаполнитьТЗ(МенеджерВТ, Склады);
	
	Для каждого СТЗ из Материалы цикл 
		СтрОст = ТЗОстатки.Найти(СТЗ.Номенклатура, "Номенклатура");
		Если СтрОст <> Неопределено тогда
			СТЗ.НаСкладе = СтрОст.Количество;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция РасчитатьТрудозатраты(МенеджерВТ)
	ВидыРаботФиктивные = Справочники.ВидыРаботСотрудников.НайтиПоНаименованию("Фиктивные");
	
	ЭТП.ПолучитьСборки(МенеджерВТ);
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = ТекстЗапросаТрудозатраты();
	Запрос.УстановитьПараметр( "ВидыРаботФиктивные", ВидыРаботФиктивные);
	Запрос.УстановитьПараметр( "Дата", ТекущаяДата());
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Процедура ЗаполнитьКоличествоИзделий(Трудозатраты)
	Трудозатраты.Колонки.Добавить("КоличествоИ", Изделия.ВыгрузитьКолонки().Колонки.Количество.ТипЗначения);
	Для каждого СтрТ из Трудозатраты цикл
		СтрИ = Изделия[СтрТ.Индекс];
		СтрТ.КоличествоИ = СтрИ.Количество;
	КонецЦикла;
	Для каждого СтрМ из Материалы цикл
		СтрИ = Изделия[СтрМ.КоличествоИ];
		СтрМ.КоличествоИ = СтрИ.Количество; 
		СтрМ.СпецификацияИ = СтрИ.Спецификация;
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьТЗ(МенеджерВТ, Склады)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	  
	Запрос.Текст = ТекстЗапросаМатериалы(); 
	Запрос.УстановитьПараметр( "ТекущаяДата", ТекущаяДата());
	
	Материалы.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	СУММА(ТоварыНаСкладахОстатки.ВНаличииОстаток) КАК Количество
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки КАК ТоварыНаСкладахОстатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНоменклатура КАК Детали
		|		ПО ТоварыНаСкладахОстатки.Номенклатура = Детали.Номенклатура
		|ГДЕ
		|	ТоварыНаСкладахОстатки.Склад В(&Склады)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыНаСкладахОстатки.Номенклатура
		|
		|ИМЕЮЩИЕ
		|	СУММА(ТоварыНаСкладахОстатки.ВНаличииОстаток) > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНоменклатура";
	Запрос.УстановитьПараметр( "Склады", Склады );
	ТЗОстатки = Запрос.Выполнить().Выгрузить();	
КонецПроцедуры

Функция ТекстЗапросаМатериалы();
	Возврат
	"ВЫБРАТЬ
	|	ВТМат.Номенклатура КАК Номенклатура,
	|	ВТМат.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ВТМат.Подразделение КАК Подразделение,
	|	СУММА(ВТМат.Количество) КАК Количество,
	|	ВТМат.ГолСпец.ОсновноеИзделиеНоменклатура КАК Изделие,
	|	ВТМат.СпецификацияКуда.ОсновноеИзделиеНоменклатура КАК Сборка,
	|	ВТМат.Индекс КАК Индекс
	|ПОМЕСТИТЬ ВТМат1
	|ИЗ
	|	ВТМат КАК ВТМат
	|ГДЕ
	|	(ВТМат.СпецификацияЧто.Ссылка ЕСТЬ NULL
	|			ИЛИ ВТМат.СтатьяКалькуляции.ИдентификаторДляФормул = ""ПолуфабрикатыПроизводимыеНаСтороне"")
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТМат.Номенклатура,
	|	ВТМат.СтатьяКалькуляции,
	|	ВТМат.Подразделение,
	|	ВТМат.ГолСпец.ОсновноеИзделиеНоменклатура,
	|	ВТМат.СпецификацияКуда.ОсновноеИзделиеНоменклатура,
	|	ВТМат.Индекс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТМат1.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ВТНоменклатура
	|ИЗ
	|	ВТМат1 КАК ВТМат1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РазрешениеНаЗаменуМатериаловМатериалы.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ВТСЗаменой
	|ИЗ
	|	ВТНоменклатура КАК ВТНоменклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РазрешениеНаЗаменуМатериалов.Материалы КАК РазрешениеНаЗаменуМатериаловМатериалы
	|		ПО ВТНоменклатура.Номенклатура = РазрешениеНаЗаменуМатериаловМатериалы.Номенклатура
	|ГДЕ
	|	РазрешениеНаЗаменуМатериаловМатериалы.Ссылка.Проведен
	|	И РазрешениеНаЗаменуМатериаловМатериалы.Ссылка.Статус.Порядок = 1
	|	И РазрешениеНаЗаменуМатериаловМатериалы.Ссылка.ДатаНачалаДействия <= &ТекущаяДата
	|	И (РазрешениеНаЗаменуМатериаловМатериалы.Ссылка.ДатаОкончанияДействия >= &ТекущаяДата
	|			ИЛИ РазрешениеНаЗаменуМатериаловМатериалы.Ссылка.ДатаОкончанияДействия = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РазрешениеНаЗаменуМатериаловМатериалы.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ВТСборкиСЗаменой
	|ИЗ
	|	ВТМат1 КАК ВТМат1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РазрешениеНаЗаменуМатериалов.Материалы КАК РазрешениеНаЗаменуМатериаловМатериалы
	|		ПО ВТМат1.Сборка = РазрешениеНаЗаменуМатериаловМатериалы.Номенклатура
	|ГДЕ
	|	РазрешениеНаЗаменуМатериаловМатериалы.Ссылка.Проведен
	|	И РазрешениеНаЗаменуМатериаловМатериалы.Ссылка.Статус.Порядок = 1
	|	И РазрешениеНаЗаменуМатериаловМатериалы.Ссылка.ДатаНачалаДействия <= &ТекущаяДата
	|	И (РазрешениеНаЗаменуМатериаловМатериалы.Ссылка.ДатаОкончанияДействия >= &ТекущаяДата
	|			ИЛИ РазрешениеНаЗаменуМатериаловМатериалы.Ссылка.ДатаОкончанияДействия = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТМат1.Номенклатура КАК Номенклатура,
	|	ВТМат1.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ВТМат1.Подразделение КАК Подразделение,
	|	ВТМат1.Количество КАК Количество,
	|	ВТМат1.Изделие КАК Изделие,
	|	ВТМат1.Сборка КАК Сборка,
	|	НЕ ВТСЗаменой.Номенклатура ЕСТЬ NULL КАК ДействуетРазрешениеНаЗамену,
	|	НЕ ВТСборкиСЗаменой.Номенклатура ЕСТЬ NULL КАК ДействуетРазрешениеНаЗамену1,
	|	ВТМат1.Индекс КАК КоличествоИ
	|ИЗ
	|	ВТМат1 КАК ВТМат1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСЗаменой КАК ВТСЗаменой
	|		ПО ВТМат1.Номенклатура = ВТСЗаменой.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСборкиСЗаменой КАК ВТСборкиСЗаменой
	|		ПО ВТМат1.Сборка = ВТСборкиСЗаменой.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТМат1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСЗаменой
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСборкиСЗаменой";
КонецФункции

Функция ТекстЗапросаТрудозатраты();
	Возврат
	"ВЫБРАТЬ
	|	ВТСборки.Спецификация.ОсновноеИзделиеНоменклатура КАК Сборка,
	|	РесурсныеСпецификацииТрудозатраты.ВидРабот КАК ВидРабот,
	|	ЕСТЬNULL(РасценкиРаботСотрудниковСрезПоследних.Расценка, 0) КАК Расценка,
	|	РесурсныеСпецификацииТрудозатраты.Количество * ВТСборки.Количество / ВТСборки.ВыходноеКоличество КАК Норма,
	|	ЕСТЬNULL(РесурсныеСпецификацииТрудозатраты.Этап.Подразделение, ВТСборки.ПодразделениеПервогоЭтапа) КАК Подразделение,
	|	ВТСборки.ГолСпец.ОсновноеИзделиеНоменклатура КАК Изделие,
	|	ВТСборки.Количество КАК Количество,
	|	ВТСборки.Индекс КАК Индекс
	|ИЗ
	|	ВТСборки КАК ВТСборки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.Трудозатраты КАК РесурсныеСпецификацииТрудозатраты
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасценкиРаботСотрудников.СрезПоследних(&Дата, ) КАК РасценкиРаботСотрудниковСрезПоследних
	|			ПО РесурсныеСпецификацииТрудозатраты.ВидРабот = РасценкиРаботСотрудниковСрезПоследних.ВидРабот
	|				И РесурсныеСпецификацииТрудозатраты.ВидРабот.КвалификационныйРазряд = РасценкиРаботСотрудниковСрезПоследних.КвалификационныйРазряд
	|		ПО ВТСборки.Спецификация = РесурсныеСпецификацииТрудозатраты.Ссылка
	|ГДЕ
	|	ЕСТЬNULL(РесурсныеСпецификацииТрудозатраты.ВидРабот.Родитель <> &ВидыРаботФиктивные, ИСТИНА)";
	
//	|	РесурсныеСпецификацииТрудозатраты.ВидРабот.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
//	|	РесурсныеСпецификацииТрудозатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
//	|	РесурсныеСпецификацииТрудозатраты.АлгоритмРасчетаКоличества КАК АлгоритмРасчетаКоличества,
//	|	РесурсныеСпецификацииТрудозатраты.ВидРабот.КвалификационныйРазряд КАК КвалификационныйРазряд,
КонецФункции

Функция СведенияОВнешнейОбработке() Экспорт
	Возврат ЭТП.ЗарегистрироватьВнешнийОтчет(ЭтотОбъект);
КонецФункции

