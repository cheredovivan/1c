
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, 
		КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровки);
	//КомпоновщикНастроек.Настройки, ДанныеРасшифровки);
	
	НайденныйПараметр = Неопределено;
    НайденныйПараметрВарианта = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("Период");
    Если НайденныйПараметрВарианта <> Неопределено Тогда
        НайденныйПараметр = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(НайденныйПараметрВарианта.ИдентификаторПользовательскойНастройки);
	КонецЕсли;
	Если НайденныйПараметр = Неопределено тогда
		ВызватьИсключение "Не задан период!";
	КонецЕсли;
	Период = НайденныйПараметр.Значение;
	НайденныйПараметр = Неопределено;
    НайденныйПараметрВарианта = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("СкладыГП");
    Если НайденныйПараметрВарианта <> Неопределено Тогда
        НайденныйПараметр = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(НайденныйПараметрВарианта.ИдентификаторПользовательскойНастройки);
	КонецЕсли;
	Если НайденныйПараметр <> Неопределено тогда
		СкладыГП = НайденныйПараметр.Значение;
	КонецЕсли;
	Изделия = ПолучитьПланПроизводства(Период.ДатаНачала,Период.ДатаОкончания,СкладыГП);
	
	НайденныйПараметр = Неопределено;
    НайденныйПараметрВарианта = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("Склады");
    Если НайденныйПараметрВарианта <> Неопределено Тогда
        НайденныйПараметр = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(НайденныйПараметрВарианта.ИдентификаторПользовательскойНастройки);
	КонецЕсли;
	Если НайденныйПараметр <> Неопределено тогда
		Склады = НайденныйПараметр.Значение;
	КонецЕсли;
	
	РассчитатьМатериалы(Изделия, Склады);
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("Материалы", Материалы);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных,ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
 	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСпецификацию(Изд)
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЗаказНаПроизводство2_2Продукция.Спецификация КАК Спецификация
		|ИЗ
		|	Документ.ЗаказНаПроизводство2_2.Продукция КАК ЗаказНаПроизводство2_2Продукция
		|ГДЕ
		|	ЗаказНаПроизводство2_2Продукция.Ссылка.Проведен
		|	И ЗаказНаПроизводство2_2Продукция.Ссылка.Дата < &Дата
		|	И ЗаказНаПроизводство2_2Продукция.Номенклатура = &Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказНаПроизводство2_2Продукция.Ссылка.Дата УБЫВ";
	Запрос.УстановитьПараметр("Номенклатура",Изд.Номенклатура);
	Запрос.УстановитьПараметр("Дата",Период.ДатаОкончания);
	Выб = Запрос.Выполнить().Выбрать();
	Если НЕ Выб.Следующий() тогда
		ВызватьИсключение "Нет спецификации "+Изд.Номенклатура.Наименование;
	КонецЕсли;
	Изд.Спецификация = Выб.Спецификация;
КонецПроцедуры

&НаСервере
Процедура РассчитатьМатериалы(Изделия, Склады)
	Если Изделия.Количество()<=0 тогда
		ВызватьИсключение "В плане нет изделий!";
	КонецЕсли;
	
	Материалы.Очистить();
	
	Для каждого Изд из Изделия цикл
		Если НЕ ЗначениеЗаполнено(Изд.Спецификация) тогда
			ПолучитьСпецификацию(Изд);
		КонецЕсли;
	КонецЦикла;
	
	МенеджерВТ = ЭТП.РасчитатьМатериалы(Изделия, Истина);
	ЗаполнитьТЗ(МенеджерВТ, Склады);
	МенеджерВТ.Закрыть();
	
	Заказ0 = Неопределено;
	ГОЗ0 = Неопределено;
	НапрДеят0 = Неопределено;
	Для каждого Мат из Материалы цикл
		СтрИзд = Изделия[Мат.Индекс];
		Мат.ЗаказКлиента = СтрИзд.ЗаказКлиента;  
		Мат.ЗаказНаПроизводство = СтрИзд.ЗаказНаПроизводство;
		Мат.Изделие = СтрИзд.Номенклатура;
		Мат.КоличествоИ = СтрИзд.Количество;
		Мат.КодОперации = СтрИзд.КодОперации;   
		Если НЕ ЗначениеЗаполнено(Мат.ЗаказКлиента) тогда
			Заказ0 = Неопределено;
			НапрДеят = Мат.ЗаказНаПроизводство.НаправлениеДеятельности;
			Если НапрДеят <> НапрДеят0 тогда           
				НапрДеят0 = НапрДеят;
				ГОЗ0 = ЭТП.ДогСпецГКДляПечати(,НапрДеят0);
			КонецЕсли;
		Иначе
			НапрДеят0 = Неопределено;
			Если Мат.ЗаказКлиента <> Заказ0 тогда
				Заказ0 = Мат.ЗаказКлиента;
				ГОЗ0 = ЭТП.ДогСпецГКДляПечати(Заказ0);
			КонецЕсли;
		КонецЕсли;
		Мат.ГОЗ =ГОЗ0;
	КонецЦикла;
КонецПроцедуры

Функция ПолучитьПланПроизводства(ДатаНачала, ДатаОкончания, СкладыГП)
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДвижениеПродукцииИМатериаловТовары.Распоряжение.Распоряжение.ДокументОснование КАК Документ.ЗаказКлиента) КАК ЗаказКлиента,
		|	ДвижениеПродукцииИМатериаловТовары.Номенклатура КАК Номенклатура,
		|	ДвижениеПродукцииИМатериаловТовары.Количество КАК Количество,
		|	ДвижениеПродукцииИМатериаловТовары.Распоряжение.Спецификация КАК Спецификация,
		|	КодыОперации.КодОперации КАК КодОперации,
		|	ВЫРАЗИТЬ(ДвижениеПродукцииИМатериаловТовары.Распоряжение.Распоряжение КАК Документ.ЗаказНаПроизводство2_2) КАК ЗаказНаПроизводство
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	Документ.ДвижениеПродукцииИМатериалов.Товары КАК ДвижениеПродукцииИМатериаловТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ДвижениеПродукцииИМатериаловДополнительныеРеквизиты.Ссылка КАК Ссылка,
		|			ВЫРАЗИТЬ(ДвижениеПродукцииИМатериаловДополнительныеРеквизиты.Значение КАК СТРОКА(2)) КАК КодОперации
		|		ИЗ
		|			Документ.ДвижениеПродукцииИМатериалов.ДополнительныеРеквизиты КАК ДвижениеПродукцииИМатериаловДополнительныеРеквизиты
		|		ГДЕ
		|			ДвижениеПродукцииИМатериаловДополнительныеРеквизиты.Свойство = &СвойствоКО) КАК КодыОперации
		|		ПО ДвижениеПродукцииИМатериаловТовары.Ссылка = КодыОперации.Ссылка
		|ГДЕ
		|	ДвижениеПродукцииИМатериаловТовары.Ссылка.Проведен
		|	И ДвижениеПродукцииИМатериаловТовары.Ссылка.Дата МЕЖДУ &Дата0 И &Дата1
		|	И ДвижениеПродукцииИМатериаловТовары.Ссылка.Получатель В(&СкладыГП)
		|	И ДвижениеПродукцииИМатериаловТовары.Ссылка.ХозяйственнаяОперация = &ХозяйственнаяОперация
		|	И ДвижениеПродукцииИМатериаловТовары.Ссылка.Статус.Порядок = 1
		|	И НЕ ДвижениеПродукцииИМатериаловТовары.Распоряжение.Спецификация.Ссылка ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.ЗаказКлиента КАК ЗаказКлиента,
		|	ВТ.Номенклатура КАК Номенклатура,
		|	СУММА(ВТ.Количество) КАК Количество,
		|	ВТ.Спецификация КАК Спецификация,
		|	ВТ.КодОперации КАК КодОперации,
		|	ВТ.ЗаказНаПроизводство КАК ЗаказНаПроизводство
		|ИЗ
		|	ВТ КАК ВТ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ.ЗаказКлиента,
		|	ВТ.Номенклатура,
		|	ВТ.Спецификация,
		|	ВТ.КодОперации,
		|	ВТ.ЗаказНаПроизводство
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказКлиента";
	Запрос.УстановитьПараметр("Дата0",ДатаНачала);
	Запрос.УстановитьПараметр("Дата1",ДатаОкончания);
	Запрос.УстановитьПараметр("СкладыГП",СкладыГП);
	//СкладОтправитель = Справочники.Склады.НайтиПоНаименованию("14 цех кладовая ГП");
	//Запрос.УстановитьПараметр("СкладОтправитель",СкладОтправитель);
	//ЦехОтправитель = Справочники.СтруктураПредприятия.НайтиПоНаименованию("14 цех");
	//Запрос.УстановитьПараметр("Отправитель",ЦехОтправитель);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства);
	СвойствоКО = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ПеремещениеТоваровКодОперации");
	Если ЗначениеЗаполнено(СвойствоКО) тогда
		Запрос.УстановитьПараметр("СвойствоКО",СвойствоКО);  
	КонецЕсли;
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Процедура ЗаполнитьТЗ(МенеджерВТ, Склады)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВТМат.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ВТМат.Номенклатура КАК Материал,
		|	ВТМат.Подразделение КАК Цех,
		|	ВТМат.Индекс КАК Индекс,
		|	СУММА(ВТМат.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТМат1
		|ИЗ
		|	ВТМат КАК ВТМат
		|ГДЕ
		|	(ВТМат.СпецификацияЧто.Ссылка ЕСТЬ NULL
		|			ИЛИ ВТМат.СтатьяКалькуляции.ИдентификаторДляФормул = ""ПолуфабрикатыПроизводимыеНаСтороне"")
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТМат.СтатьяКалькуляции,
		|	ВТМат.Номенклатура,
		|	ВТМат.Подразделение,
		|	ВТМат.Индекс
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТМат1.Материал КАК Номенклатура
		|ПОМЕСТИТЬ ВТНоменклатура
		|ИЗ
		|	ВТМат1 КАК ВТМат1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	СУММА(ТоварыНаСкладахОстатки.ВНаличииОстаток) КАК Количество
		|ПОМЕСТИТЬ ВТОстатки
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки КАК ТоварыНаСкладахОстатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНоменклатура КАК ВТНоменклатура
		|		ПО ТоварыНаСкладахОстатки.Номенклатура = ВТНоменклатура.Номенклатура
		|ГДЕ
		|	ТоварыНаСкладахОстатки.Склад В(&Склады)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыНаСкладахОстатки.Номенклатура
		|
		|ИМЕЮЩИЕ
		|	СУММА(ТоварыНаСкладахОстатки.ВНаличииОстаток) > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНоменклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТМат1.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ВТМат1.Материал КАК Материал,
		|	ВТМат1.Цех КАК Цех,
		|	ВТМат1.Индекс КАК Индекс,
		|	ВТМат1.Количество КАК Количество,
		|	ВТОстатки.Количество КАК НаСкладе
		|ИЗ
		|	ВТМат1 КАК ВТМат1
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстатки КАК ВТОстатки
		|		ПО ВТМат1.Материал = ВТОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	Индекс";
	Запрос.УстановитьПараметр( "Склады", Склады );
	Материалы.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

Функция СведенияОВнешнейОбработке() Экспорт
	Возврат ЭТП.ЗарегистрироватьВнешнийОтчет("МатериалыНаВыпускПоЗКлиентов1",
		"Материальный отчет по выпуску (новый)");
КонецФункции

