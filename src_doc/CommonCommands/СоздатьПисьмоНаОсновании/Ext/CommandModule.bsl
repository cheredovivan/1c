&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ИспользованиеВстроеннойИЛегкойПочтыНастройка = ВстроеннаяПочтаКлиент.ВстроеннаяИлиЛегкаяПочтаИспользуется();    
	ВстроеннаяИлиЛегкаяПочтаДоступна = ВстроеннаяПочтаКлиент.ВстроеннаяИлиЛегкаяПочтаДоступна(ИспользованиеВстроеннойИЛегкойПочтыНастройка);
	
	Если Не ВстроеннаяИлиЛегкаяПочтаДоступна Тогда
		ТекстСообщения = НСтр("ru = 'Для отправки письма включите персональную настройку ""Встроенная"" или ""Легкая почта"".'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если ПараметрКоманды.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не выбрано ни одного объекта для отправки по почте!'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если ИспользованиеВстроеннойИЛегкойПочтыНастройка.ИспользованиеВстроеннойПочты Тогда	
		
		КоличествоПараметров = ПараметрКоманды.Количество();
		ПоследнийПараметр = ПараметрКоманды[КоличествоПараметров-1];
		
		Если ТипЗнч(ПоследнийПараметр) = Тип("СправочникСсылка.Пользователи") Тогда
			ВстроеннаяПочтаКлиент.СоздатьПисьмоНаОснованииПользователей(ПараметрКоманды);
		ИначеЕсли ТипЗнч(ПоследнийПараметр) = Тип("СправочникСсылка.Сотрудники") Тогда
			ВстроеннаяПочтаКлиент.СоздатьПисьмоНаОснованииСотрудников(ПараметрКоманды);
		Иначе 
			ВстроеннаяПочтаКлиент.СоздатьПисьмоНаОсновании(ПоследнийПараметр);
		КонецЕсли;
		
	ИначеЕсли ИспользованиеВстроеннойИЛегкойПочтыНастройка.ИспользованиеЛегкойПочты Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Объекты", ПараметрКоманды);
		ОткрытьФорму(
			"Обработка.ПочтовоеСообщение.Форма.Форма",
			ПараметрыОткрытия,,
			Новый УникальныйИдентификатор);
			
	КонецЕсли;
	
КонецПроцедуры
