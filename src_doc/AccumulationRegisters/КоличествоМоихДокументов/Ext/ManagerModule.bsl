#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает общие итоги "Моих документов".
// 
// Параметры:
//  ОтборПричины - Массив из ПеречислениеСсылка.ПричиныДобавленияВМоиДокументы
// 
// Возвращаемое значение:
//  Структура:
//   * Всего - Число
//   * Просроченных - Число
//   * БезОтвета - Число
//   * Истекающих - Число
//
Функция ОбщиеИтоги(ОтборПричины = Неопределено) Экспорт
	
	ОбщиеИтоги = Новый Структура;
	ОбщиеИтоги.Вставить("Всего", 0);
	ОбщиеИтоги.Вставить("Просроченных", 0);
	ОбщиеИтоги.Вставить("БезОтвета", 0);
	ОбщиеИтоги.Вставить("Истекающих", 0);
	
	УсловиеПоПричинам = УсловиеПоПричинам(ОтборПричины);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	КоличествоМоихДокументовОстатки.БезОтветаОстаток КАК БезОтвета,
		|	КоличествоМоихДокументовОстатки.ВсегоОстаток КАК Всего,
		|	КоличествоМоихДокументовОстатки.ИстекающихОстаток КАК Истекающих,
		|	КоличествоМоихДокументовОстатки.ПросроченныхОстаток КАК Просроченных
		|ИЗ
		|	РегистрНакопления.КоличествоМоихДокументов.Остатки(, Пользователь = &Пользователь
		|	И &УсловиеПоПричинам) КАК КоличествоМоихДокументовОстатки");
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоПричинам", УсловиеПоПричинам);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ОбщиеИтоги, ВыборкаДетальныеЗаписи);
	КонецЕсли;
	
	Возврат ОбщиеИтоги;
	
КонецФункции

// Возвращает итоги "Моих документов" по причинам.
// 
// Параметры:
//  ОтборПричины - Массив из ПеречислениеСсылка.ПричиныДобавленияВМоиДокументы
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
//   * Причина - ПеречислениеСсылка.ПричиныДобавленияВМоиДокументы
//   * Всего - Число
//   * Просрочено - Число
//   * БезОтвета - Число
//   * Истекают - Число
//
Функция ИтогиПоПричинам(ОтборПричины = Неопределено) Экспорт
	
	ИтогиПоПричинам = Новый ТаблицаЗначений;
	ИтогиПоПричинам.Колонки.Добавить("Причина",
		Новый ОписаниеТипов("ПеречислениеСсылка.ПричиныДобавленияВМоиДокументы"));
	ИтогиПоПричинам.Колонки.Добавить("Всего", Новый ОписаниеТипов("Число"));
	ИтогиПоПричинам.Колонки.Добавить("БезОтвета", Новый ОписаниеТипов("Число"));
	ИтогиПоПричинам.Колонки.Добавить("Истекающих", Новый ОписаниеТипов("Число"));
	ИтогиПоПричинам.Колонки.Добавить("Просроченных", Новый ОписаниеТипов("Число"));
	ИтогиПоПричинам.Индексы.Добавить("Причина");
	
	УсловиеПоПричинам = УсловиеПоПричинам(ОтборПричины);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	КоличествоМоихДокументовОстатки.ДобавленВручную КАК ДобавленВручную,
		|	КоличествоМоихДокументовОстатки.НазначенаЗадачаМне КАК НазначенаЗадачаМне,
		|	КоличествоМоихДокументовОстатки.ОтправленаЗадачаОтМеня КАК ОтправленаЗадачаОтМеня,
		|	КоличествоМоихДокументовОстатки.ЯвляюсьАвторомДокумента КАК ЯвляюсьАвторомДокумента,
		|	КоличествоМоихДокументовОстатки.ЯвляюсьАвторомУтвержденияДокумента КАК ЯвляюсьАвторомУтвержденияДокумента,
		|	КоличествоМоихДокументовОстатки.ЯвляюсьОтветственнымЗаДокумент КАК ЯвляюсьОтветственнымЗаДокумент,
		|	КоличествоМоихДокументовОстатки.ЯвляюсьПодписантомДокумента КАК ЯвляюсьПодписантомДокумента,
		|	КоличествоМоихДокументовОстатки.БезОтветаОстаток КАК БезОтвета,
		|	КоличествоМоихДокументовОстатки.ВсегоОстаток КАК Всего,
		|	КоличествоМоихДокументовОстатки.ИстекающихОстаток КАК Истекающих,
		|	КоличествоМоихДокументовОстатки.ПросроченныхОстаток КАК Просроченных
		|	
		|ИЗ
		|	РегистрНакопления.КоличествоМоихДокументов.Остатки(, Пользователь = &Пользователь
		|	И &УсловиеПоПричинам) КАК КоличествоМоихДокументовОстатки");
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоПричинам", УсловиеПоПричинам);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ДобавитьИтогиПоПричине(
			ИтогиПоПричинам,
			ВыборкаДетальныеЗаписи,
			ОтборПричины,
			"ДобавленВручную",
			Перечисления.ПричиныДобавленияВМоиДокументы.ДобавленВручную);
		ДобавитьИтогиПоПричине(
			ИтогиПоПричинам,
			ВыборкаДетальныеЗаписи,
			ОтборПричины,
			"НазначенаЗадачаМне",
			Перечисления.ПричиныДобавленияВМоиДокументы.НазначенаЗадачаМне);
		ДобавитьИтогиПоПричине(
			ИтогиПоПричинам,
			ВыборкаДетальныеЗаписи,
			ОтборПричины,
			"ОтправленаЗадачаОтМеня",
			Перечисления.ПричиныДобавленияВМоиДокументы.ОтправленаЗадачаОтМеня);
		ДобавитьИтогиПоПричине(
			ИтогиПоПричинам,
			ВыборкаДетальныеЗаписи,
			ОтборПричины,
			"ЯвляюсьАвторомДокумента",
			Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомДокумента);
		ДобавитьИтогиПоПричине(
			ИтогиПоПричинам,
			ВыборкаДетальныеЗаписи,
			ОтборПричины,
			"ЯвляюсьАвторомУтвержденияДокумента",
			Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомУтвержденияДокумента);
		ДобавитьИтогиПоПричине(
			ИтогиПоПричинам,
			ВыборкаДетальныеЗаписи,
			ОтборПричины,
			"ЯвляюсьОтветственнымЗаДокумент",
			Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьОтветственнымЗаДокумент);
		ДобавитьИтогиПоПричине(
			ИтогиПоПричинам,
			ВыборкаДетальныеЗаписи,
			ОтборПричины,
			"ЯвляюсьПодписантомДокумента",
			Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьПодписантомДокумента);
	КонецЦикла;
	
	Возврат ИтогиПоПричинам;
	
КонецФункции

// Возвращает итоги "Моих документов" по проектам.
// 
// Параметры:
//  ОтборПричины - Массив из ПеречислениеСсылка.ПричиныДобавленияВМоиДокументы
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
//   * Проект - СправочникСсылка.Проекты
//   * Всего - Число
//   * Просрочено - Число
//   * БезОтвета - Число
//   * Истекают - Число
//
Функция ИтогиПоПроектам(ОтборПричины = Неопределено) Экспорт
	
	УсловиеПоПричинам = УсловиеПоПричинам(ОтборПричины);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	КоличествоМоихДокументовОстатки.Проект КАК Проект,
		|	КоличествоМоихДокументовОстатки.БезОтветаОстаток КАК БезОтвета,
		|	КоличествоМоихДокументовОстатки.ВсегоОстаток КАК Всего,
		|	КоличествоМоихДокументовОстатки.ИстекающихОстаток КАК Истекающих,
		|	КоличествоМоихДокументовОстатки.ПросроченныхОстаток КАК Просроченных
		|ИЗ
		|	РегистрНакопления.КоличествоМоихДокументов.Остатки(, Пользователь = &Пользователь
		|	И &УсловиеПоПричинам) КАК КоличествоМоихДокументовОстатки");
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоПричинам", УсловиеПоПричинам);
	РезультатЗапроса = Запрос.Выполнить();
	ИтогиПоПроектам = РезультатЗапроса.Выгрузить();
	
	Возврат ИтогиПоПроектам;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует отбор в регистре.
// 
// Параметры:
//  ОтборПричины - Массив из ПеречислениеСсылка.ПричиныДобавленияВМоиДокументы
//               - Неопределено
// 
// Возвращаемое значение:
//  Строка
// 
Функция УсловиеПоПричинам(ОтборПричины)
	
	МассивУсловиеПоПричинам = Новый Массив;
	
	Если ОтборПричины <> Неопределено Тогда
		
		ПричинаДобавленВручную = Перечисления.ПричиныДобавленияВМоиДокументы.ДобавленВручную;
		ПричинаНазначенаЗадачаМне = Перечисления.ПричиныДобавленияВМоиДокументы.НазначенаЗадачаМне;
		ПричинаОтправленаЗадачаОтМеня = Перечисления.ПричиныДобавленияВМоиДокументы.ОтправленаЗадачаОтМеня;
		ПричинаЯвляюсьАвторомДокумента = Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомДокумента;
		ПричинаЯвляюсьАвторомУтвержденияДокумента =
			Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомУтвержденияДокумента;
		ПричинаЯвляюсьОтветственнымЗаДокумент =
			Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьОтветственнымЗаДокумент;
		ПричинаЯвляюсьПодписантомДокумента = Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьПодписантомДокумента;
		
		Если ОтборПричины.Найти(ПричинаДобавленВручную) <> Неопределено Тогда
			МассивУсловиеПоПричинам.Добавить("ДобавленВручную");
		КонецЕсли;
		Если ОтборПричины.Найти(ПричинаНазначенаЗадачаМне) <> Неопределено Тогда
			МассивУсловиеПоПричинам.Добавить("НазначенаЗадачаМне");
		КонецЕсли;
		Если ОтборПричины.Найти(ПричинаОтправленаЗадачаОтМеня) <> Неопределено Тогда
			МассивУсловиеПоПричинам.Добавить("ОтправленаЗадачаОтМеня");
		КонецЕсли;
		Если ОтборПричины.Найти(ПричинаЯвляюсьАвторомДокумента) <> Неопределено Тогда
			МассивУсловиеПоПричинам.Добавить("ЯвляюсьАвторомДокумента");
		КонецЕсли;
		Если ОтборПричины.Найти(ПричинаЯвляюсьАвторомУтвержденияДокумента) <> Неопределено Тогда
			МассивУсловиеПоПричинам.Добавить("ЯвляюсьАвторомУтвержденияДокумента");
		КонецЕсли;
		Если ОтборПричины.Найти(ПричинаЯвляюсьОтветственнымЗаДокумент) <> Неопределено Тогда
			МассивУсловиеПоПричинам.Добавить("ЯвляюсьОтветственнымЗаДокумент");
		КонецЕсли;
		Если ОтборПричины.Найти(ПричинаЯвляюсьПодписантомДокумента) <> Неопределено Тогда
			МассивУсловиеПоПричинам.Добавить("ЯвляюсьПодписантомДокумента");
		КонецЕсли;
		
	КонецЕсли;
	
	Если МассивУсловиеПоПричинам.Количество() = 0 Тогда
		УсловиеПоПричинам = "ИСТИНА";
	Иначе
		УсловиеПоПричинам = СтрШаблон("(%1)", СтрСоединить(МассивУсловиеПоПричинам, " ИЛИ "));
	КонецЕсли;
	
	Возврат УсловиеПоПричинам;
	
КонецФункции

// Добалвяет итоги по причине.
// 
// Параметры:
//  ИтогиПоПричинам - см. ИтогиПоПричинам
//  ВыборкаДетальныеЗаписи - ВыборкаИзРезультатаЗапроса
//  ОтборПричины - Массив из ПеречислениеСсылка.ПричиныДобавленияВМоиДокументы
//  ИмяПоляПричины - Строка
//  Причина - ПеречислениеСсылка.ПричиныДобавленияВМоиДокументы
//  
Процедура ДобавитьИтогиПоПричине(ИтогиПоПричинам, ВыборкаДетальныеЗаписи, ОтборПричины, ИмяПоляПричины, Причина)
	
	Если ОтборПричины.Найти(Причина) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыборкаДетальныеЗаписи[ИмяПоляПричины] Тогда
		
		СтрокаПричины = ИтогиПоПричинам.Найти(Причина, "Причина");
		Если СтрокаПричины = Неопределено Тогда
			СтрокаПричины = ИтогиПоПричинам.Добавить();
			СтрокаПричины.Причина = Причина;
		КонецЕсли;
		СтрокаПричины.Всего = СтрокаПричины.Всего + ВыборкаДетальныеЗаписи.Всего;
		СтрокаПричины.БезОтвета = СтрокаПричины.БезОтвета + ВыборкаДетальныеЗаписи.БезОтвета;
		СтрокаПричины.Истекающих = СтрокаПричины.Истекающих + ВыборкаДетальныеЗаписи.Истекающих;
		СтрокаПричины.Просроченных = СтрокаПричины.Просроченных + ВыборкаДетальныеЗаписи.Просроченных;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли