#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс   

#Область ОбновлениеВерсииИБ

#Область ПерейтиНаВерсию_3_0_17_17

// Регистрирует данные к обновлению в плане обмена ОбновлениеИнформационнойБазы
//  см. Стандарты и методики разработки прикладных решений: Параллельный режим отложенного обновления.
//
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию_3_0_17_17(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.Справочники.АдресатыПочтовыхСообщений;
	
	ПолныеИменаОбъектов = Новый Массив;
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	ПолныеИменаОбъектов.Добавить(ПолноеИмяОбъекта);
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	ПараметрыВыборки.ПолныеИменаОбъектов = СтрСоединить(ПолныеИменаОбъектов, ",");
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ОбновляемаяТаблица.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.АдресатыПочтовыхСообщений КАК ОбновляемаяТаблица
		|ГДЕ
		|	ОбновляемаяТаблица.Ссылка > &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка");
	
	СсылкаКОбновлению = ПустаяСсылка();
	ЕстьДанныеКОбработке = Истина;
	Пока ЕстьДанныеКОбработке Цикл
		
		Запрос.УстановитьПараметр("Ссылка", СсылкаКОбновлению);
		
		//@skip-check query-in-loop
		ДанныеКОбработке = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		КоличествоКОбработке = ДанныеКОбработке.Количество();
		ЕстьДанныеКОбработке = КоличествоКОбработке > 0;
		Если ЕстьДанныеКОбработке Тогда
			СсылкаКОбновлению = ДанныеКОбработке[КоличествоКОбработке - 1];
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДанныеКОбработке);
		
	КонецЦикла;
	
КонецПроцедуры

// Обрабатывает данные, зарегистрированные в плане обмена ОбновлениеИнформационнойБазы
//  см. Стандарты и методики разработки прикладных решений: Параллельный режим отложенного обновления.
//
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию_3_0_17_17(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.Справочники.АдресатыПочтовыхСообщений;
	
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	ТаблицаКОбновления = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Для Каждого СтрокаКОбновлению Из ТаблицаКОбновления Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.АдресатыПочтовыхСообщений");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", СтрокаКОбновлению.Ссылка);
			Блокировка.Заблокировать();                         
			
			МассивТекущиеПочтовыеАдреса = Новый Массив;  
			Адрес = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаКОбновлению.Ссылка, "Адрес");
			МассивТекущиеПочтовыеАдреса.Добавить(Адрес);
			
			ОбновитьСведенияОбАдресатах = Ложь;
			ОбработатьДанныеДляПереходаНаНовуюВерсию = Истина;
			ВстроеннаяПочтаСервер.ОбновитьПредставлениеАдресатовДляМассиваАдресов(
				МассивТекущиеПочтовыеАдреса, ОбновитьСведенияОбАдресатах, Неопределено,
				ОбработатьДанныеДляПереходаНаНовуюВерсию);
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				МетаданныеОбъекта,
				СтрокаКОбновлению.Ссылка,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось обработать некоторые объекты (пропущены): %1'"), 
			ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			МетаданныеОбъекта,,
			СтрШаблон(
				НСтр("ru = 'Обработана очередная порция: %1'"),
				ОбъектовОбработано));
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = ОбъектовОбработано;
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
		Параметры.Очередь,
		ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Текст = Параметры.СтрокаПоиска;               
	
	Если Не Параметры.Отбор.Свойство("УчетнаяЗапись") Тогда
		Возврат;
	КонецЕсли;	
	
	СтрокаДанныхПредставление = Параметры.Отбор.СтрокаДанныхПредставление;  
	ЭтоВебКлиент = Параметры.Отбор.ЭтоВебКлиент;
	
	ТекущийПользователь = Параметры.Отбор.ТекущийПользователь;
		
	Если СтрДлина(Текст) <= 1 Тогда   
		СтандартнаяОбработка = Ложь;  
		ДанныеВыбора = Новый СписокЗначений;
		Возврат;
	КонецЕсли;
	
	ТекстДляПолученияДанныхВыбора = Текст;
	ПодстрокиВведенногоТекста = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, ";");
	
	ПодстрокиДанныхОбъекта =
		СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаДанныхПредставление, ";");

	Для Инд = 0 По (ПодстрокиВведенногоТекста.Количество() - 1) Цикл
		Если Инд > (ПодстрокиДанныхОбъекта.Количество() - 1)
			Или ПодстрокиВведенногоТекста[Инд] <> ПодстрокиДанныхОбъекта[инд] Тогда
			ТекстДляПолученияДанныхВыбора = 
				СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(ПодстрокиВведенногоТекста[инд], " ");
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПустаяСтрока(ТекстДляПолученияДанныхВыбора) Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	Иначе
		Текст = ТекстДляПолученияДанныхВыбора;
	КонецЕсли;
	
	ИскатьПоГруппам = Истина;
	
	ДанныеВыбора = ВстроеннаяПочтаСервер.ПолучитьДанныеВыбораДляЭлектронногоПисьма(Текст, 
		ТекущийПользователь, 
		ЭтоВебКлиент,
		ИскатьПоГруппам);
		
	Если ДанныеВыбора.Количество() = 0 Тогда 
		
		СтрокаЗначение = НайтиПоНаименованию(Текст);
		Если ЗначениеЗаполнено(СтрокаЗначение) Тогда 
			ДанныеВыбора.Добавить(СтрокаЗначение, Текст);
		КонецЕсли;
			
	КонецЕсли;		

	ЗаполнитьКартинкиВСпискеВыбора(ДанныеВыбора);	
	
	Если ДанныеВыбора.Количество() <> 0 Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// заполняет картинки адресатов в списке выбора
Процедура ЗаполнитьКартинкиВСпискеВыбора(ДанныеВыбора)
	
	Если ТипЗнч(ДанныеВыбора) <> Тип("СписокЗначений") Тогда
		Возврат;
	КонецЕсли;	
	
	Для Каждого Строка Из ДанныеВыбора Цикл
		
		Если Не (ТипЗнч(Строка.Значение) = Тип("Структура") И Строка.Значение.Свойство("Внешний")) Тогда
			Продолжить;   
		КонецЕсли;	
		
		Если Строка.Значение.Внешний Тогда 
			
			КартинкаВыбора = БиблиотекаКартинок.ВнешнийАдресат16;
			
		Иначе	

			ОписаниеОтсутствия = Неопределено;
			Строка.Значение.Свойство("ОписаниеОтсутствия", ОписаниеОтсутствия);
			Если ОписаниеОтсутствия = Неопределено Тогда
				КартинкаВыбора = БиблиотекаКартинок.ВнутреннийАдресат16;
			Иначе
				КартинкаВыбора = БиблиотекаКартинок.ВнутреннийАдресатОтсутствует16;
			КонецЕсли;
				
		КонецЕсли;	
			
		Если Строка.Значение.Свойство("ЭтоГруппа") И Строка.Значение.ЭтоГруппа = Истина Тогда
			КартинкаВыбора = БиблиотекаКартинок.ВзаимодействиеСотрудников;
		КонецЕсли;	
		
		Строка.Картинка = КартинкаВыбора;
			
	КонецЦикла;	
	
КонецПроцедуры	

#КонецОбласти

#КонецЕсли