#Область ОписаниеПеременных
// Локализация
&НаКлиенте
Перем ПараметрыСообщенияРПД;
//Локализация Конец
#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриСозданииНаСервереРедакцииКонфигурации();
	
	ВестиУчетСканКопийОригиналовДокументов = 
		ПолучитьФункциональнуюОпцию("ВестиУчетСканКопийОригиналовДокументов");
	
	Если Параметры.Свойство("Папка") Тогда 
		Папка = Параметры.Папка;
	КонецЕсли;	
	
	// Локализация
	ДоступноИспользованиеИИДляЗаполнения = РаспознаваниеДокументов.ИспользованиеДоступно() 
		И Пользователи.РолиДоступны("РаспознаваниеДокументов");
	Если ДоступноИспользованиеИИДляЗаполнения И Параметры.Свойство("ИспользоватьИИДляЗаполненияКарточки") Тогда 
		ИспользоватьИИДляЗаполненияКарточки = Параметры.ИспользоватьИИДляЗаполненияКарточки;
	КонецЕсли;
	// Локализация Конец
	
	Если Параметры.Свойство("Файлы") Тогда 
		МассивФайлов = Параметры.Файлы;
		Для Каждого ИмяФайла Из МассивФайлов Цикл
			ФайлПеренесенный = Новый Файл(ИмяФайла);
			
			НовыйЭлемент = Файлы.Добавить();
			НовыйЭлемент.ИмяФайла = ФайлПеренесенный.ИмяБезРасширения;
			НовыйЭлемент.ПутьФайла = ФайлПеренесенный.Путь;
			НовыйЭлемент.ПолноеИмя = ФайлПеренесенный.ПолноеИмя;
			НовыйЭлемент.ИндексКартинки = ФайловыеФункцииКлиентСервер.ПолучитьИндексПиктограммыФайла(ФайлПеренесенный.Расширение);
			
			Если ДелопроизводствоКлиентСервер.ЭтоРасширениеСканКопии(ФайлПеренесенный.Расширение) Тогда 
				НовыйЭлемент.Оригинал = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	КоличествоФайлов = Файлы.Количество();
	Заголовок = СформироватьЗаголовок(КоличествоФайлов);
	
	Подготовил = Сотрудники.ОсновнойСотрудник(); 
	ЭтоДелопроизводитель = РольДоступна("РегистрацияДокументовПредприятия") Или ПользователиСерверПовтИсп.ЭтоПолноправныйПользовательИБ(); 
	
	Если ЭтоДелопроизводитель И Не ИспользоватьИИДляЗаполненияКарточки Тогда 
		РегистрироватьДокументы = Истина;
		ДатаРегистрации = "ТекущаяДата";
		ДатаРегистрацииДокументов = ТекущаяДата();
	Иначе 
		РегистрироватьДокументы = Ложь;
	КонецЕсли;	
	
	// распознавание
	// Локализация
	ИспользоватьРаспознавание = РаботаСФайламиВызовСервера.ПолучитьИспользоватьРаспознаваниеТекста();
	ДоступноРаспознаваниеПоЗапросу = РаботаСФайламиВызовСервера.ДоступноРаспознаваниеПоЗапросу();
	Элементы.ГруппаРаспознавание.Видимость = ИспользоватьРаспознавание И ДоступноРаспознаваниеПоЗапросу;
	Если ИспользоватьРаспознавание = Ложь Тогда
		СтратегияРаспознавания = Перечисления.СтратегииРаспознаванияТекста.НеРаспознавать;
	Иначе
		
		ПрограммаРаспознавания = РаботаСФайламиВызовСервера.ПрограммаРаспознавания();
		
		ЯзыкРаспознавания = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"Распознавание", "ЯзыкРаспознавания");
		
		Если Не ЗначениеЗаполнено(ЯзыкРаспознавания) Тогда
			ЯзыкРаспознавания = РаботаСФайламиВызовСервера.ПолучитьЯзыкРаспознавания();
		КонецЕсли;
		
		ЯзыкиРаспознавания = РаботаСФайламиВызовСервера.ЯзыкиРаспознаванияПрограммы(ПрограммаРаспознавания);
		Если ЯзыкиРаспознавания.Найти(ЯзыкРаспознавания, "Language") = Неопределено Тогда
			ЯзыкРаспознавания = РаботаСФайламиВызовСервера.ЯзыкРаспознаванияПрограммыПоУмолчанию(ПрограммаРаспознавания);
		КонецЕсли;
		
		СтратегияРаспознавания = РаботаСФайламиВызовСервера.СтратегияРаспознаванияФайловВладельца(
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Тип("СправочникСсылка.ДокументыПредприятия")));
		
		ПредставлениеНастроекРаспознавания =
			РаботаСФайламиВызовСервера.ПолучитьПредставлениеНастроекРаспознавания(
			СтратегияРаспознавания, ЯзыкРаспознавания);
		
		Команды.Настройка.Подсказка = ПредставлениеНастроекРаспознавания;
	КонецЕсли;	
	// Локализация Конец
	
	Если Не ЗначениеЗаполнено(ВидДокумента) Тогда 
		ВидДокумента = 
			Делопроизводство.ПолучитьВидДокументаПоУмолчанию(Справочники.ДокументыПредприятия.ПустаяСсылка());
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда 
		Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	Если Константы.ИспользоватьГрифыДоступа.Получить() Тогда
		ГрифДоступа = Константы.ГрифДоступаПоУмолчанию.Получить();
	КонецЕсли;	

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьКатегорииДанных") Тогда
		Элементы.ГруппаКатегории.Видимость = Ложь;
	Иначе
		Если Параметры.Свойство("СписокКатегорий") Тогда
			Для каждого КатегорияВСписке Из Параметры.СписокКатегорий Цикл
				НоваяСтрока = СписокКатегорий.Добавить();
				НоваяСтрока.Значение = КатегорияВСписке;
				НоваяСтрока.ПолноеНаименование = РаботаСКатегориямиДанных.ПолучитьПолныйПутьКатегорииДанных(КатегорияВСписке);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Проект) Тогда
		Проект = РаботаСПроектами.ПолучитьПроектПоУмолчанию();
	КонецЕсли;
	
	// Тематики
	ТематикиИспользуются = РаботаСТематикамиДокументов.ТематикиИспользуются();
	ВестиУчетПоТематикам = ТематикиИспользуются;
	Если ТематикиИспользуются Тогда 
		Элементы.ВидИТематикаТекст.Подсказка = НСтр("ru = 'Вид и тематика документа'");
	Иначе 
		Элементы.ВидИТематикаТекст.Подсказка = НСтр("ru = 'Вид документа'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимость();  
	// Локализация
	УстановитьДополнительныеСвойства();
	// Локализация Конец

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// Локализация
	Если ИспользоватьИИДляЗаполненияКарточки = Ложь Тогда
		ПроверяемыеРеквизиты.Добавить("ВидИТематикаТекст");
	КонецЕсли;
	// Локализация Конец
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям")
	// Локализация
		И ИспользоватьИИДляЗаполненияКарточки = Ложь
	// Локализация Конец
		И ЗначениеЗаполнено(ВидДокумента) 
		И ПолучитьФункциональнуюОпцию("ВестиУчетПоОрганизациям",
			Новый Структура("ВидДокумента", ВидДокумента)) Тогда
		ПроверяемыеРеквизиты.Добавить("Организация");
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа") Тогда 
		ПроверяемыеРеквизиты.Добавить("ГрифДоступа");
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВопросыДеятельности") Тогда 
		ПроверяемыеРеквизиты.Добавить("ВопросДеятельности");
	КонецЕсли;
	
	// Локализация
	Если ИспользоватьИИДляЗаполненияКарточки = Ложь Тогда
	// Локализация Конец
		Если ЗначениеЗаполнено(ВидДокумента) Тогда
			Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидДокумента, "ВестиУчетПоКонтрагентам") Тогда
				ПроверяемыеРеквизиты.Добавить("Контрагент");
			КонецЕсли;	
		КонецЕсли;	
	// Локализация
	КонецЕсли;
	// Локализация Конец
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ПоказыватьСообщениеОбОтправкеФайлаВСервисРаспознавания = ФайловыеФункцииКлиентПовтИсп.
	ПолучитьПерсональныеНастройкиРаботыСФайлами().ПоказыватьСообщениеОбОтправкеФайлаВСервисРаспознавания;

	Если ПоказыватьСообщениеОбОтправкеФайлаВСервисРаспознавания = Истина Тогда
		Если ПараметрыСообщенияРПД <> Неопределено Тогда
			РаспознаваниеДокументовСлужебныйКлиент.
				ОткрытьСообщениеПриОтправкеФайловНаРаспознавание(ПараметрыСообщенияРПД);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УтвердилНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СотрудникиКлиент.ВыбратьСотрудникаИзАдреснойКниги(Элемент, Утвердил);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовилНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СотрудникиКлиент.ВыбратьСотрудникаИзАдреснойКниги(Элемент, Подготовил);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрироватьДокументыПриИзменении(Элемент)
	
	Элементы.ДатаРегистрации.Доступность = РегистрироватьДокументы;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокКатегорийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаРегистрацииОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Возврат;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаРегистрацииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "ПроизвольнаяДата" Тогда 
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПослеВводаДаты",
			ЭтотОбъект,
			Новый Структура("ВыбранноеЗначение", ВыбранноеЗначение));

		ПоказатьВводДаты(ОписаниеОповещения, ДатаРегистрацииДокументов, Нстр("ru = 'Введите дату регистрации'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОригиналПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Расширение = ФайловыеФункцииКлиентСервер.ПолучитьРасширениеИмениФайла(ТекущиеДанные.ПолноеИмя);
	
	Если ТекущиеДанные.Оригинал Тогда 
		Если Не ДелопроизводствоКлиентСервер.ЭтоРасширениеСканКопии(Расширение) Тогда 
			ТекстВопроса = НСтр("ru = 'Выбранный файл, возможно, не является скан-копией. 
			|Вы действительно хотите отметить его как оригинал?'");
			
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ОригиналПриИзмененииПродолжение",
				ЭтотОбъект,
				Новый Структура("ТекущиеДанные", ТекущиеДанные));

			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет, 
				НСтр("ru = 'Отметка оригинала'"));
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОригиналПриИзмененииПродолжение(Результат, Параметры) Экспорт 

	Если Результат <> КодВозвратаДиалога.Да Тогда
		Параметры.ТекущиеДанные.Оригинал = Не Параметры.ТекущиеДанные.Оригинал;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВидИТематикаТекстПриИзменении(Элемент)
	
	ДелопроизводствоКлиент.ВидИТематикаПриИзменении(ЭтаФорма, Истина);
	УстановитьВидимость();  
	// Локализация
	УстановитьДополнительныеСвойства();
	// Локализация Конец
	
КонецПроцедуры

&НаКлиенте
Процедура ВидИТематикаТекстНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДелопроизводствоКлиент.ВидИТематикаНачалоВыбора(ЭтаФорма, Элемент, СтандартнаяОбработка, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидИТематикаТекстОчистка(Элемент, СтандартнаяОбработка)
	
	ДелопроизводствоКлиент.ВидИТематикаОчистка(ЭтаФорма, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидИТематикаТекстОткрытие(Элемент, СтандартнаяОбработка)
	
	ДелопроизводствоКлиент.ВидИТематикаОткрытие(ЭтаФорма, СтандартнаяОбработка, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидИТематикаТекстОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ДелопроизводствоКлиент.ВидИТематикаОбработкаВыбора(ЭтаФорма, ВыбранноеЗначение, СтандартнаяОбработка, Истина);
	УстановитьВидимость(); 
	// Локализация
	УстановитьДополнительныеСвойства();
	// Локализация Конец
	
КонецПроцедуры

&НаКлиенте
Процедура ВидИТематикаТекстАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	ДелопроизводствоКлиент.ВидИТематикаАвтоПодбор(
		ЭтаФорма, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидИТематикаТекстОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	ДелопроизводствоКлиент.ВидИТематикаОкончаниеВводаТекста(
		ЭтаФорма, Текст, ДанныеВыбора, СтандартнаяОбработка, Истина);
	
КонецПроцедуры

// Локализация

&НаКлиенте
Процедура ПродолжениеПослеПредупрежденияОПередачеДанныхВСервисРаспознавания(Результат, Параметры) Экспорт
	
	ЭтоИмпорт = Параметры.Свойство("Импорт");
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		
		Если ЭтоИмпорт = Ложь Тогда
			
			ИспользоватьИИДляЗаполненияКарточки = Ложь;
			
			УстановитьВидимость();
			УстановитьДополнительныеСвойства();
			
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	Если Параметры.Свойство("Импорт") Тогда
		ВыполнитьИмпорт();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредупредитьИВыполнить(Импорт = Ложь)
	
	ПоказыватьПредупреждениеОбОтветственностиПриОтправке = ФайловыеФункцииКлиентПовтИсп.
		ПолучитьПерсональныеНастройкиРаботыСФайлами().ПоказыватьПредупреждениеОбОтветственностиПриОтправке;

	Если ПоказыватьПредупреждениеОбОтветственностиПриОтправке = Истина Тогда
		
		ПараметрыОповещения = Новый Структура;
		
		Если Импорт = Истина Тогда
			ПараметрыОповещения.Вставить("Импорт", Импорт);
		КонецЕсли;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПродолжениеПослеПредупрежденияОПередачеДанныхВСервисРаспознавания", 
			ЭтотОбъект, ПараметрыОповещения);
			
		ОткрытьФорму("Справочник.Файлы.Форма.ПредупреждениеОбОтветственностиПриОтправкеФайлов", , 
			ЭтотОбъект, , , , ОписаниеОповещения);
			
	ИначеЕсли Импорт = Истина Тогда 
		
		ВыполнитьИмпорт();
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьИИДляЗаполненияКарточкиПриИзменении(Элемент)
	
	Если ИспользоватьИИДляЗаполненияКарточки = Истина Тогда
			
		РегистрироватьДокументы = Ложь;
		
		ПредупредитьИВыполнить();
			
	КонецЕсли;
	
	УстановитьВидимость();
	УстановитьДополнительныеСвойства();
	
КонецПроцедуры  

// Локализация Конец  

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыФайлы

&НаКлиенте
Процедура ФайлыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	#Если НЕ ВебКлиент Тогда
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ДиалогОткрытияФайла.ПолноеИмяФайла = "";
		ДиалогОткрытияФайла.Фильтр = НСтр("ru = 'Все файлы(*.*)|*.*'");
		ДиалогОткрытияФайла.МножественныйВыбор = Истина;
		ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите файлы'");
		
		Если ДиалогОткрытияФайла.Выбрать() Тогда
			МассивФайлов = ДиалогОткрытияФайла.ВыбранныеФайлы;
			Для Каждого ИмяФайла Из МассивФайлов Цикл
				ФайлПеренесенный = Новый Файл(ИмяФайла);
				
				НовыйЭлемент = Файлы.Добавить();
				НовыйЭлемент.ИмяФайла = ФайлПеренесенный.ИмяБезРасширения;
				НовыйЭлемент.ПутьФайла = ФайлПеренесенный.Путь;
				НовыйЭлемент.ПолноеИмя = ФайлПеренесенный.ПолноеИмя;
				НовыйЭлемент.ИндексКартинки = 
					ФайловыеФункцииКлиентСервер.ПолучитьИндексПиктограммыФайла(ФайлПеренесенный.Расширение);
				
				Если ДелопроизводствоКлиентСервер.ЭтоРасширениеСканКопии(ФайлПеренесенный.Расширение) Тогда 
					НовыйЭлемент.Оригинал = Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		КоличествоФайлов = Файлы.Количество();
		Заголовок = СформироватьЗаголовок(КоличествоФайлов);
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПослеУдаления(Элемент)
	
	КоличествоФайлов = Файлы.Количество();
	Заголовок = СформироватьЗаголовок(КоличествоФайлов);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Локализация

&НаКлиенте
Процедура НастройкиРаспознавания(Команда)
	
	РаботаСФайламиКлиент.ВыбратьНастройкиРаспознавания(
		Новый Структура("СтратегияРаспознавания, ЯзыкРаспознавания", СтратегияРаспознавания, ЯзыкРаспознавания),
		ЭтаФорма,
		Новый ОписаниеОповещения("НастройкиРаспознаванияПродолжение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиРаспознаванияПродолжение(РезультатОткрытия, Параметры) Экспорт 

	Если ТипЗнч(РезультатОткрытия) = Тип("Структура") Тогда
		СтратегияРаспознавания = РезультатОткрытия.СтратегияРаспознавания;
		ЯзыкРаспознавания = РезультатОткрытия.ЯзыкРаспознавания;
		УстановитьПредставлениеНастроекРаспознавания(СтратегияРаспознавания, ЯзыкРаспознавания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеНастроекРаспознавания(СтратегияРаспознавания, ЯзыкРаспознавания)
	
	ПредставлениеНастроекРаспознавания = РаботаСФайламиВызовСервера.ПолучитьПредставлениеНастроекРаспознавания(
		СтратегияРаспознавания, ЯзыкРаспознавания);
	Команды.Настройка.Подсказка = ПредставлениеНастроекРаспознавания;
	
КонецПроцедуры

// Локализация Конец

&НаКлиенте
Процедура ВыбратьФайлы(Команда)
	
	#Если НЕ ВебКлиент Тогда
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ДиалогОткрытияФайла.ПолноеИмяФайла = "";
		ДиалогОткрытияФайла.Фильтр = НСтр("ru = 'Все файлы(*.*)|*.*'");
		ДиалогОткрытияФайла.МножественныйВыбор = Истина;
		ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите файлы'");
		
		Если ДиалогОткрытияФайла.Выбрать() Тогда
			Файлы.Очистить();
			
			МассивФайлов = ДиалогОткрытияФайла.ВыбранныеФайлы;
			Для Каждого ИмяФайла Из МассивФайлов Цикл
				ФайлПеренесенный = Новый Файл(ИмяФайла);
				
				НовыйЭлемент = Файлы.Добавить();
				НовыйЭлемент.ИмяФайла = ФайлПеренесенный.ИмяБезРасширения;
				НовыйЭлемент.ПутьФайла = ФайлПеренесенный.Путь;
				НовыйЭлемент.ПолноеИмя = ФайлПеренесенный.ПолноеИмя;
				НовыйЭлемент.ИндексКартинки = ФайловыеФункцииКлиентСервер.ПолучитьИндексПиктограммыФайла(ФайлПеренесенный.Расширение);
				
				Если ДелопроизводствоКлиентСервер.ЭтоРасширениеСканКопии(ФайлПеренесенный.Расширение) Тогда 
					НовыйЭлемент.Оригинал = Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		КоличествоФайлов = Файлы.Количество();
		Заголовок = СформироватьЗаголовок(КоличествоФайлов);
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура Импорт(Команда) 
	
	// Локализация
	Если ИспользоватьИИДляЗаполненияКарточки = Истина Тогда
		ДоступностьСервиса = РаспознаваниеДокументовВызовСервера.ИспользованиеДоступно(Истина);
		Если ДоступностьСервиса.ИспользованиеДоступно = Истина Тогда
			ПредупредитьИВыполнить(Истина);
		Иначе
			ТекстСообщения = НСтр("ru = 'Автоматическое заполнение карточек не доступно: %1'", 
				ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
			ТекстСообщения = СтрШаблон(ТекстСообщения, ДоступностьСервиса.Расшифровка);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ИспользоватьИИДляЗаполненияКарточки");
			Возврат;
		КонецЕсли;
	Иначе
	// Локализация Конец
		ВыполнитьИмпорт();
	// Локализация
	КонецЕсли; 
	// Локализация Конец
	
КонецПроцедуры  

&НаКлиенте
Процедура ВыполнитьИмпорт()

	ОчиститьСообщения();
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;	
	
	ВыбранныеФайлы = Новый СписокЗначений;
	Для Каждого Строка Из Файлы Цикл
		ВыбранныеФайлы.Добавить(Строка.ПолноеИмя);
	КонецЦикла;	
	
	Рекурсивно = Ложь;
	КоличествоСуммарное = 0;
	ПсевдоФайловаяСистема = Новый Соответствие;
	РежимЗагрузки = Ложь;
	
	ДобавленныеФайлы = Новый Массив;
	ПараметрыРаспознавания = Новый Структура("СтратегияРаспознавания, ЯзыкРаспознавания", 
		СтратегияРаспознавания, ЯзыкРаспознавания);
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("ОбработчикРезультата", Неопределено);
	ПараметрыВыполнения.Вставить("ПапкаДляДобавления", Неопределено);
	ПараметрыВыполнения.Вставить("ВыбранныеФайлы", ВыбранныеФайлы);
	ПараметрыВыполнения.Вставить("Комментарий", "");
	ПараметрыВыполнения.Вставить("ХранитьВерсии", Истина);
	ПараметрыВыполнения.Вставить("УдалятьФайлыПослеДобавления", УдалятьФайлыПослеДобавления);
	ПараметрыВыполнения.Вставить("Рекурсивно", Рекурсивно);
	ПараметрыВыполнения.Вставить("ИдентификаторФормы", УникальныйИдентификатор);
	ПараметрыВыполнения.Вставить("ПсевдоФайловаяСистема", ПсевдоФайловаяСистема);
	ПараметрыВыполнения.Вставить("ДобавленныеФайлы", ДобавленныеФайлы);
	ПараметрыВыполнения.Вставить("РежимЗагрузки", РежимЗагрузки);
	ПараметрыВыполнения.Вставить("Кодировка", Неопределено);
	ПараметрыВыполнения.Вставить("ПараметрыРаспознавания", ПараметрыРаспознавания);
	ПараметрыВыполнения.Вставить("СписокКатегорий", СписокКатегорий);
	
	Обработчик = Новый ОписаниеОповещения("ИмпортФайловПослеПроверкиРазмеров", ЭтотОбъект, ПараметрыВыполнения);
	РаботаСФайламиКлиент.ПроверитьПредельныйРазмерФайлов(Обработчик, ПараметрыВыполнения);     
	
КонецПроцедуры  

&НаКлиенте
// Продолжение процедуры (см. выше).
Процедура ИмпортФайловПослеПроверкиРазмеров(Результат, ПараметрыВыполнения) Экспорт
	
	Состояние();
	
	Если Результат.Успех = Ложь Тогда
		РаботаСФайламиКлиент.ВернутьРезультат(ПараметрыВыполнения.ОбработчикРезультата, Неопределено);
		Возврат;
	КонецЕсли;
	
	ПараметрыВыполнения.Вставить("КоличествоСуммарное", Результат.КоличествоСуммарное);
	Если ПараметрыВыполнения.КоличествоСуммарное = 0 Тогда
		Если ПараметрыВыполнения.РежимЗагрузки Тогда
			РаботаСФайламиКлиент.ВернутьРезультат(ПараметрыВыполнения.ОбработчикРезультата, Неопределено);
		Иначе
			РаботаСФайламиКлиент.ВернутьРезультатПослеПоказаПредупреждения(ПараметрыВыполнения.ОбработчикРезультата, НСтр("ru = 'Нет файлов для добавления'"), Неопределено);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Счетчик = 0;
	Индикатор = 0;
	Путь = "";
	
	МассивИменФайловСОшибками = Новый Массив;
	МассивСтруктурВсехФайлов = Новый Массив;
	МассивВсехПапок = Новый Массив;
	ДобавленныеФайлы = Новый Массив;
	
	Комментарий = "";
	ХранитьВерсии = Истина;
	ИдентификаторФормы = УникальныйИдентификатор;
	
	МаксРазмерФайла = ФайловыеФункцииКлиентПовтИсп.ПолучитьОбщиеНастройкиРаботыСФайлами().МаксимальныйРазмерФайла;
	ЗапретЗагрузкиФайловПоРасширению = ФайловыеФункцииКлиентПовтИсп.ПолучитьОбщиеНастройкиРаботыСФайлами().ЗапретЗагрузкиФайловПоРасширению;
	СписокЗапрещенныхРасширений = ФайловыеФункцииКлиентПовтИсп.ПолучитьОбщиеНастройкиРаботыСФайлами().СписокЗапрещенныхРасширений;
	
	МассивФайлов = Новый Массив;
	Для Каждого ИмяФайла Из ПараметрыВыполнения.ВыбранныеФайлы Цикл
		Попытка
			ВыбранныйФайл = Новый Файл(ИмяФайла.Значение);
			МассивФайлов.Добавить(ВыбранныйФайл);
		Исключение
			Инфо = ИнформацияОбОшибке();
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					   НСтр("ru = 'Описание=""%1""'"), Инфо.Описание ));
		КонецПопытки;
	КонецЦикла;
	
	// Локализация 
	Отправленные = Новый Массив;
	НеОтправленные = Новый Массив;
	// Конец Локализация
	
	Для Каждого ФайлМассива Из МассивФайлов Цикл
		
		Если Не ФайловыеФункцииКлиентСервер.ФайлМожноЗагружать(
			ФайлМассива, 
			МаксРазмерФайла, 
			ЗапретЗагрузкиФайловПоРасширению, 
			СписокЗапрещенныхРасширений, 
			МассивИменФайловСОшибками) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не РегистрироватьДокументы Или ДатаРегистрации = "ДатаИзмененияФайла" Тогда 
			ДатаРегистрацииДокументов = ФайлМассива.ПолучитьВремяИзменения();	
		ИначеЕсли ДатаРегистрации = "ТекущаяДата" Тогда 
			ДатаРегистрацииДокументов = ТекущаяДата();
		ИначеЕсли ДатаРегистрации = "ДатаСозданияФайла" Тогда 
			ДатаРегистрацииДокументов = ФайлМассива.ПолучитьУниверсальноеВремяИзменения();
		КонецЕсли;
		
		// Локализация 
		Если ИспользоватьИИДляЗаполненияКарточки Тогда
			
			СведенияОФайле = СведенияОФайле(ФайлМассива.ПолноеИмя, ПараметрыВыполнения);
			ОписаниеОшибки = "";
			
			СозданныйФайлСсылка = СоздатьИОтправитьНаРаспознавание(СведенияОФайле, ДатаРегистрацииДокументов, ОписаниеОшибки);
			
			Если Не ПустаяСтрока(ОписаниеОшибки) Тогда
				НеОтправленные.Добавить(ФайлМассива.Имя);
				ПоказатьОповещениеПользователя(СтрШаблон(
						НСтр("ru = 'Не удалось отправить файл на распознавание: %1'", 
							ОбщегоНазначенияКлиент.КодОсновногоЯзыка()), ОписаниеОшибки));
						
				Продолжить;
				
			КонецЕсли;
			
			Отправленные.Добавить(ФайлМассива.Имя);
			
		Иначе
		// Конец Локализация
			
			ВладелецФайла = СоздатьДокументПредприятия(ФайлМассива.ИмяБезРасширения, 
				ДатаРегистрацииДокументов);
			
			СозданныйФайлСсылка = РаботаСФайламиКлиент.СоздатьДокументНаОсновеФайла(
				ФайлМассива.ПолноеИмя,
				ВладелецФайла,
				ЭтаФорма, 
				Истина, //НеОткрыватьКарточкуПослеСозданияИзФайла,
				Неопределено, // ИмяСоздаваемогоФайла
				ПараметрыВыполнения.ПараметрыРаспознавания,
				ПараметрыВыполнения.СписокКатегорий);
				
		// Локализация 
		КонецЕсли;
		// Конец Локализация
		
		Запись = Новый Структура;
		Запись.Вставить("ИмяФайла", ФайлМассива.ПолноеИмя);
		Запись.Вставить("Файл", СозданныйФайлСсылка);
		МассивСтруктурВсехФайлов.Добавить(Запись);
			
		Если ВестиУчетСканКопийОригиналовДокументов Тогда 
			НайденнаяСтрока = Неопределено;
			Для Каждого Строка Из Файлы	Цикл
				Если Строка.ПолноеИмя = ФайлМассива.ПолноеИмя Тогда 
					НайденнаяСтрока = Строка;
					Прервать;
				КонецЕсли;	
			КонецЦикла;	
			Если НайденнаяСтрока <> Неопределено И НайденнаяСтрока.Оригинал Тогда 
				Для Каждого ДобавленныйФайл Из МассивСтруктурВсехФайлов Цикл
					Если ДобавленныйФайл.ИмяФайла = ФайлМассива.ПолноеИмя Тогда 
						Делопроизводство.СохранитьСведенияОбОригиналеФайла(ДобавленныйФайл.Файл, ВладелецФайла);
						Прервать;
					КонецЕсли;	
				КонецЦикла;	
			КонецЕсли;	
		КонецЕсли;		
			
	КонецЦикла;	
	
	КоличествоФайлов = МассивСтруктурВсехФайлов.Количество();
	
	Если КоличествоФайлов > 1 Тогда
		Состояние();

		ТекстСостояния = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Импорт файлов завершен. Импортировано %1 файлов'"), 
			Строка(КоличествоФайлов));
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Импорт файлов:'"),
			,
			ТекстСостояния,
			БиблиотекаКартинок.Информация32);        
		
	Иначе
		Состояние();
	КонецЕсли;
			
	Если УдалятьФайлыПослеДобавления = Истина Тогда
		ПараметрыВыполнения.РежимЗагрузки = Ложь;
		ФайловыеФункцииКлиентСервер.УдалитьФайлыПослеДобавления(
			МассивСтруктурВсехФайлов, МассивВсехПапок, ПараметрыВыполнения.РежимЗагрузки);
	КонецЕсли;
	
	// Вывод сообщений
	// Локализация
	Если ИспользоватьИИДляЗаполненияКарточки Тогда
		Если Отправленные.Количество() > 0 ИЛИ НеОтправленные.Количество() > 0 Тогда
			ПараметрыСообщенияРПД = Новый Структура;
			ПараметрыСообщенияРПД.Вставить("Отправленные", Отправленные);
			ПараметрыСообщенияРПД.Вставить("НеОтправленные", НеОтправленные);
		КонецЕсли;
	Иначе
	// Конец Локализация
		Если МассивИменФайловСОшибками.Количество() <> 0 Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("МассивИменФайловСОшибками", МассивИменФайловСОшибками);
			Если ПараметрыВыполнения.РежимЗагрузки Тогда
				ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Отчет о загрузке файлов'"));
			КонецЕсли;
			
			ОткрытьФорму("Справочник.Файлы.Форма.ФормаОтчета", ПараметрыФормы);
		КонецЕсли;
	// Локализация
	КонецЕсли;
	// Конец Локализация

	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ДанныеДокументовПредприятия"));
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.РеестрДокументовПредприятия"));
	Оповестить("ИмпортФайловВСписокДокументовПредприятияЗавершен");
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКатегории(Команда)
	
	РаботаСКатегориямиДанныхКлиент.ОткрытьФормуПодбораКатегорийДляСпискаКатегорий(СписокКатегорий);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеВводаДаты(Дата, Параметры) Экспорт
	
	Если НЕ Дата = Неопределено Тогда
		ЗначениеСписка = Элементы.ДатаРегистрации.СписокВыбора.НайтиПоЗначению(Параметры.ВыбранноеЗначение);
		ЗначениеСписка.Представление = Формат(Дата, "ДФ='dd.MM.yyyy ЧЧ:мм'");
		ДатаРегистрацииДокументов = Дата;
	Иначе 
		ДатаРегистрации = "ТекущаяДата";
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция СоздатьДокументПредприятия(Знач Заголовок, Знач ДатаРегистрации)
	
	ДокументОбъект = Справочники.ДокументыПредприятия.СоздатьЭлемент();    
	
	ДанныеЗаполнения = Новый Структура;
	
	ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "Заголовок", Заголовок);
	ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "Папка", Папка);
	ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "Заголовок", Заголовок);
	ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "Тематика", Тематика);
	ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "ГрифДоступа", ГрифДоступа);
	ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "ВидДокумента", ВидДокумента);
	ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "Заголовок", Заголовок);
	ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "ВопросДеятельности", ВопросДеятельности);
	ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "Подготовил", Подготовил);
	ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "Утвердил", Утвердил);
	ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "Контрагент", Контрагент);
	ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "КонтактноеЛицо", КонтактноеЛицо);
	ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "ПодписалОтКонтрагента", ПодписалОтКонтрагента);
	ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "ДатаСоздания", ТекущаяДатаСеанса());
	ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "Проект", Проект);
	
	Если ЗначениеЗаполнено(ДокументОбъект.ВидДокумента) Тогда
		
		Если ПолучитьФункциональнуюОпцию("ВестиУчетПоОрганизациям",
			Новый Структура("ВидДокумента", ДокументОбъект.ВидДокумента)) Тогда
				ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "Организация", Организация);
		КонецЕсли;
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьСуммуВоВнутренних",
			Новый Структура("ВидДокумента", ДокументОбъект.ВидДокумента)) Тогда
				ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, "Валюта", Делопроизводство.ПолучитьВалютуПоУмолчанию());
		КонецЕсли;
		
	КонецЕсли;
	
	ДокументОбъект.Заполнить(ДанныеЗаполнения);
	
	// Локализация 
	Если ИспользоватьИИДляЗаполненияКарточки Тогда
		ДокументОбъект.ДополнительныеСвойства.Вставить("ДокументОтправленНаРаспознавание", Истина);
		ДокументОбъект.Записать();
		Делопроизводство.ЗаписатьСостояниеДокумента(
				ДокументОбъект.Ссылка, 
				ДатаРегистрации, 
				Перечисления.СостоянияДокументов.Проект,
				Сотрудники.ОсновнойСотрудник(), , Истина);
	Иначе
	// Локализация Конец
		ДокументОбъект.Записать();
		ВыполнитьДействияРегистрацииДокумента(ДокументОбъект);
	// Локализация	
	КонецЕсли;
	// Конец Локализация 
	
	Возврат ДокументОбъект.Ссылка;
	
КонецФункции

&НаСервере
Процедура ВставитьЗначениеДляЗаполнения(ДанныеЗаполнения, ИмяРеквизита, ЗначениеРеквизита)
	
	Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
		ДанныеЗаполнения.Вставить(ИмяРеквизита, ЗначениеРеквизита);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьДействияРегистрацииДокумента(ДокументОбъект)
	
	Если РегистрироватьДокументы Тогда 
		
		Нумератор = Нумерация.ПолучитьНумераторДокумента(ДокументОбъект); 
		Если ЗначениеЗаполнено(Нумератор) Тогда // автоматическая нумерация
			
			ДокументОбъект.ДатаРегистрации = ДатаРегистрации;
			
			// сформируем текущий номер
			СтруктураПараметров = НумерацияКлиентСервер.ПолучитьПараметрыНумерации(ДокументОбъект);
			Нумерация.СформироватьЧисловойНомерДокумента(СтруктураПараметров, ДокументОбъект.ЧисловойНомер);
			
			ОписанияОшибок = Новый СписокЗначений;
			СтруктураПараметров = НумерацияКлиентСервер.ПолучитьПараметрыНумерации(ДокументОбъект);
			Нумерация.СформироватьСтроковыйНомерДокумента(СтруктураПараметров, ДокументОбъект.РегистрационныйНомер, ОписанияОшибок);
			
			ДокументОбъект.Зарегистрировал = 
				Сотрудники.ОсновнойСотрудник();
			
			ДокументОбъект.Записать();
			
			Делопроизводство.ЗаписатьСостояниеДокумента(
				ДокументОбъект.Ссылка, 
				ДатаРегистрации, 
				Перечисления.СостоянияДокументов.Зарегистрирован, 
				Сотрудники.ОсновнойСотрудник());
		КонецЕсли;	
		
	Иначе	
		Если ЗначениеЗаполнено(Утвердил) Тогда 
			Делопроизводство.ЗаписатьСостояниеДокумента(
				ДокументОбъект.Ссылка, 
				ДатаРегистрации, 
				Перечисления.СостоянияДокументов.Утвержден, 
				Сотрудники.ОсновнойСотрудник());
		Иначе		
			Делопроизводство.ЗаписатьСостояниеДокумента(
				ДокументОбъект.Ссылка, 
				ДатаРегистрации, 
				Перечисления.СостоянияДокументов.Проект, 
				Сотрудники.ОсновнойСотрудник());
		КонецЕсли;		
	КонецЕсли;		

КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимость()
	
	Если ЗначениеЗаполнено(ВидДокумента) Тогда
		Если ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(ВидДокумента, 
			"ВестиУчетПоКонтрагентам") Тогда
			Элементы.Контрагент.Видимость = Истина;
			Элементы.ПодписалОтКонтрагента.Видимость = Истина;
			Элементы.КонтактноеЛицо.Видимость = Истина;
		Иначе
			Элементы.Контрагент.Видимость = Ложь;
			Элементы.ПодписалОтКонтрагента.Видимость = Ложь;
			Элементы.КонтактноеЛицо.Видимость = Ложь;
		КонецЕсли;	
		Если ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(ВидДокумента, 
			"ВестиУчетПоОрганизациям") Тогда
			Элементы.Организация.Видимость = Истина;
		Иначе
			Элементы.Организация.Видимость = Ложь;
		КонецЕсли;	
	Иначе
		Элементы.Организация.Видимость = Ложь;	
		Элементы.Контрагент.Видимость = Ложь;	
		Элементы.ПодписалОтКонтрагента.Видимость = Ложь;
		Элементы.КонтактноеЛицо.Видимость = Ложь;
	КонецЕсли;  
	
	Элементы.ГруппаРегистрация.Видимость = ЭтоДелопроизводитель;     
	
	// Локализация
	
	Элементы.ИспользоватьИИДляЗаполненияКарточки.Видимость = ДоступноИспользованиеИИДляЗаполнения;

	Элементы.Оригинал.Видимость = Не ИспользоватьИИДляЗаполненияКарточки;
	Элементы.ГруппаНастройки.Видимость = Не ИспользоватьИИДляЗаполненияКарточки; 
	Элементы.ВидИТематикаТекст.Видимость = Не ИспользоватьИИДляЗаполненияКарточки; 
	Если ИспользоватьИИДляЗаполненияКарточки Тогда  
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;	
	Иначе  
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
	КонецЕсли;
	
	// Локализация Конец  
	
КонецПроцедуры  

&НаКлиентеНаСервереБезКонтекста
Функция СформироватьЗаголовок(ЧислоФайлов)
	
	ЗаголовокФормы = "";
	
	Если ЧислоФайлов = 0 Тогда
		ЗаголовокФормы = НСтр("ru='Загрузка файлов'");
	Иначе
		ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Загрузка файлов (%1)'"), Строка(ЧислоФайлов));
	КонецЕсли;	
		
	Возврат ЗаголовокФормы;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервереРедакцииКонфигурации()	
	
	Элементы.Организация.Заголовок = РедакцииКонфигурацииКлиентСервер.Организация();
		
КонецПроцедуры

// Локализация

&НаСервере
Функция СоздатьИОтправитьНаРаспознавание(Знач СведенияОФайле, Знач ДатаРегистрации, ОписаниеОшибки = "")
	
	НачатьТранзакцию();
	
	Попытка
		
		ВладелецФайла = СоздатьДокументПредприятия(СведенияОФайле.ИмяБезРасширения, ДатаРегистрации);
		СозданныйФайлСсылка = РаботаСФайламиВызовСервера.СоздатьФайлСВерсией(ВладелецФайла, СведенияОФайле);
		РаспознаваниеДокументовВызовСервера.ОтправитьФайлДокументаНаРаспознавание(СозданныйФайлСсылка);
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ОписаниеОшибки = ОписаниеОшибки();
		ЗаписьЖурналаРегистрации(
			РаспознаваниеДокументов.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка, , ,
			ОписаниеОшибки);
		Возврат Неопределено;
		
	КонецПопытки;
	
	Возврат СозданныйФайлСсылка;
	
КонецФункции

&НаКлиенте
Процедура УстановитьДополнительныеСвойства()

	Элементы.ВидИТематикаТекст.АвтоОтметкаНезаполненного = Не ИспользоватьИИДляЗаполненияКарточки;
	Элементы.ВидИТематикаТекст.ОтметкаНезаполненного = Не ИспользоватьИИДляЗаполненияКарточки;
	
	Элементы.Организация.АвтоОтметкаНезаполненного = Не ИспользоватьИИДляЗаполненияКарточки;
	Элементы.Организация.ОтметкаНезаполненного = Не ИспользоватьИИДляЗаполненияКарточки;
	
	Элементы.Контрагент.АвтоОтметкаНезаполненного = Не ИспользоватьИИДляЗаполненияКарточки;
	Элементы.Контрагент.ОтметкаНезаполненного = Не ИспользоватьИИДляЗаполненияКарточки;
	
КонецПроцедуры

&НаКлиенте
Функция СведенияОФайле(ПолноеИмяФайла, ПараметрыВыполнения)
	
	Если НЕ РаботаСФайламиСлужебныйКлиент.Расширение1СПредприятияПодключено() Тогда
		ФайловыеФункцииСлужебныйКлиент.ПоказатьПредупреждениеОНеобходимостиРасширенияРаботыСФайлами(Неопределено);
		Возврат Неопределено;
	КонецЕсли;
	
	Файл = Новый Файл(ПолноеИмяФайла);
	
	ДляРасчетаМБ = 1048576;
	
	РазмерВМб = Файл.Размер() / ДляРасчетаМБ;

	Если Не ФайловыеФункцииКлиентПовтИсп.ПолучитьОбщиеНастройкиРаботыСФайлами().ИзвлекатьТекстыФайловНаСервере Тогда
		АдресВременногоХранилищаТекста = ФайловыеФункцииСлужебныйКлиентСервер.ИзвлечьТекстВоВременноеХранилище(
			Файл.ПолноеИмя, 
			УникальныйИдентификатор,
			Ложь); // Отказ
	Иначе
		АдресВременногоХранилищаТекста = "";
	КонецЕсли;
	
	ИмяСоздания = Файл.ИмяБезРасширения;
	
	ИмяФайла = ИмяСоздания + Файл.Расширение;
	
	ТекстПояснения = СтрШаблон(
		НСтр("ru = 'Идет сохранение файла ""%1"" (%2 Мб).
			|Пожалуйста, подождите...'"),
		ИмяФайла,
		ФайловыеФункцииСлужебныйКлиентСервер.ПолучитьСтрокуСРазмеромФайла(РазмерВМб));
	
	Состояние(ТекстПояснения);
	
	// Поместим Файл в ВременноеХранилище
	АдресВременногоХранилищаФайла = "";
	
	ПомещаемыеФайлы = Новый Массив;
	Описание = Новый ОписаниеПередаваемогоФайла(Файл.ПолноеИмя, "");
	ПомещаемыеФайлы.Добавить(Описание);
	
	ПомещенныеФайлы = Новый Массив;
	
	Если Не ПоместитьФайлы(ПомещаемыеФайлы, ПомещенныеФайлы, , Ложь, УникальныйИдентификатор) Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Ошибка при помещении файла
			           |""%1""
			           |во временное хранилище.'"),
			Файл.ПолноеИмя);
	КонецЕсли;
	
	Если ПомещенныеФайлы.Количество() = 1 Тогда
		АдресВременногоХранилищаФайла = ПомещенныеФайлы[0].Хранение;
	КонецЕсли;
	
	// Создадим карточку Файла в БД
	СведенияОФайле = РаботаСФайламиКлиентСервер.СведенияОФайле("ФайлСВерсией", Файл);
	СведенияОФайле.АдресВременногоХранилищаФайла = АдресВременногоХранилищаФайла;
	СведенияОФайле.АдресВременногоХранилищаТекста = АдресВременногоХранилищаТекста;
	СведенияОФайле.ПараметрыРаспознавания = ПараметрыВыполнения.ПараметрыРаспознавания;
	СведенияОФайле.ХранитьВерсии = Истина;
	СведенияОФайле.ЗаписатьВИсторию = Истина;
	СведенияОФайле.СписокКатегорий = ПараметрыВыполнения.СписокКатегорий;
	СведенияОФайле.НеобходимоВыполнитьВставкуШКНаКлиенте = Ложь;
	СведенияОФайле.Проект = Проект;
	
	СведенияОФайле.ИмяБезРасширения = ИмяСоздания;

	Возврат СведенияОФайле;
	
КонецФункции

// Конец Локализация

#КонецОбласти
