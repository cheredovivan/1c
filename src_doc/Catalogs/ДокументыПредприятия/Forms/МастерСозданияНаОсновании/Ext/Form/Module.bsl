
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказатьНеДействует = Параметры.ПоказатьНеДействует;
	ПоказатьОбязательныеСвязи = Параметры.ПоказатьОбязательныеСвязи;
	ПоказатьСвязи = Параметры.ПоказатьСвязи;
	
	ВсегоВидимыхЗакладок = 0;
	Если ПоказатьСвязи Тогда
		ВсегоВидимыхЗакладок = ВсегоВидимыхЗакладок + 1;
	КонецЕсли;		
	Если ПоказатьОбязательныеСвязи Тогда
		ВсегоВидимыхЗакладок = ВсегоВидимыхЗакладок + 1;
	КонецЕсли;		
	Если ПоказатьНеДействует Тогда
		ВсегоВидимыхЗакладок = ВсегоВидимыхЗакладок + 1;
	КонецЕсли;	
	
	Элементы.ГруппаСвязи.Видимость = ПоказатьСвязи;
	Элементы.ГруппаСвязиОбязательные.Видимость = ПоказатьОбязательныеСвязи;
	Элементы.ГруппаНеДействует.Видимость = ПоказатьНеДействует;

	Если ВсегоВидимыхЗакладок = 1 Тогда     
		КлючНазначенияИспользования = "Закладка1";
		КлючСохраненияПоложенияОкна = "Закладка1";
	ИначеЕсли ВсегоВидимыхЗакладок = 2 Тогда     
		КлючНазначенияИспользования = "Закладка2";
		КлючСохраненияПоложенияОкна = "Закладка2";
	ИначеЕсли ВсегоВидимыхЗакладок = 3 Тогда     
		КлючНазначенияИспользования = "Закладка4";
		КлючСохраненияПоложенияОкна = "Закладка4";
	КонецЕсли;	
	
	Элементы.ФормаВперед.Доступность = Истина;
	Элементы.ФормаВперед.Заголовок = НСтр("ru = 'Готово'");
	
	ДокументОснование = Параметры.Основание;      
	
	РеквСтарого = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование, "РегистрационныйНомер, НеДействует, ВидДокумента");
	Если ЗначениеЗаполнено(РеквСтарого.РегистрационныйНомер)
		И Не РеквСтарого.НеДействует Тогда
		ДелаетНеДействующим = ДокументОснование;
	КонецЕсли;	

	Контрагент = Параметры.Контрагент;
	Организация = Параметры.Организация;
	Проект = Параметры.Проект;
	
	Для Каждого Строка Из Параметры.МассивТиповСвязей Цикл
		
		ТипСвязи = Строка.Значение;
		
		НовСтр = ТипыСвязей.Добавить();
		НовСтр.ТипСвязи = Строка.Значение;  
		
		Если ЗначениеЗаполнено(ТипСвязи.Комментарий) Тогда 
			НовСтр.Представление = Строка(ТипСвязи) + " (" + ТипСвязи.Комментарий + ")";
		Иначе
			НовСтр.Представление = Строка(ТипСвязи);
		КонецЕсли;	
		
	КонецЦикла;	      
	
	МассивТиповСвязей = Новый Массив;
	Для Каждого Строка Из Параметры.ОбязательныеСвязи Цикл
		МассивТиповСвязей.Добавить(Строка.ТипСвязи);
	КонецЦикла;
	КомментарииТиповСвязи = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивТиповСвязей, "Комментарий");

	ОснованиеПоля = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквСтарого.ВидДокумента, 
		"ЯвляетсяВходящейКорреспонденцией, ЯвляетсяИсходящейКорреспонденцией");
	
	ОснованиеВидКорреспонденции = Неопределено;
	Если ОснованиеПоля.ЯвляетсяВходящейКорреспонденцией Тогда
		ОснованиеВидКорреспонденции = Перечисления.ВидыКорреспонденции.Входящая;
	ИначеЕсли ОснованиеПоля.ЯвляетсяИсходящейКорреспонденцией Тогда
		ОснованиеВидКорреспонденции = Перечисления.ВидыКорреспонденции.Исходящая;	
	КонецЕсли;	
	
	Для Каждого Строка Из Параметры.ОбязательныеСвязи Цикл
		
		СтруктТекущего = Новый Структура("ВидДокумента", Параметры.ВидДокумента);
		СтруктСтарого = Новый Структура("ВидДокумента", РеквСтарого.ВидДокумента);
		НастройкиСвязи = СвязиОбъектов.ПолучитьНастройкуСвязи(СтруктТекущего, СтруктСтарого, Строка.ТипСвязи);
		
		НайденТип = (НастройкиСвязи <> Неопределено);
		
		Если НайденТип Тогда
			
			Если ЗначениеЗаполнено(Строка.ВидКорреспонденцииДокументНа)
				И ЗначениеЗаполнено(ОснованиеВидКорреспонденции) Тогда  
				Если Строка.ВидКорреспонденцииДокументНа <> ОснованиеВидКорреспонденции Тогда
					НайденТип = Ложь;
				КонецЕсли;	
			КонецЕсли;	
			
		КонецЕсли;	
		
		НоваяСтрока = ОбязательныеСвязи.Добавить();
		
		НоваяСтрока.ТипСвязи = Строка.ТипСвязи;
		НоваяСтрока.СсылкаНа = Строка.СсылкаНа;   
		
		НоваяСтрока.ВидКорреспонденцииДокументНа = Строка.ВидКорреспонденцииДокументНа;

		Если НайденТип Тогда
			НоваяСтрока.СвязанныйДокумент = ДокументОснование;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КомментарииТиповСвязи.Получить(Строка.ТипСвязи)) Тогда
			НоваяСтрока.ПредставлениеТипа = СтрШаблон("%1 (%2)", 
				Строка(Строка.ТипСвязи),
				КомментарииТиповСвязи.Получить(Строка.ТипСвязи));
		Иначе
			НоваяСтрока.ПредставлениеТипа = Строка(Строка.ТипСвязи);
		КонецЕсли;
	КонецЦикла;       
	
	Элементы.ГруппаОдинИлиМного.ТекущаяСтраница = Элементы.ГруппаМного;
	Элементы.ОбязательныеСвязи.Видимость = Истина;
	
	// надо получить массив видов документов, которые можно сделать не действующими
	ВидыОтменяемыхДокументовМассив = Делопроизводство.ПолучитьВидыОтменяемыхДокументов(Параметры.ВидДокумента);
	ВидыОтменяемыхДокументов.ЗагрузитьЗначения(ВидыОтменяемыхДокументовМассив);
	
	Элементы.ГруппаГоризонтальноНеДействует.Доступность = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыСвязи

&НаКлиенте
Процедура СвязиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СвязиПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СвязиТипСвязиОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыОбязательныеСвязи

&НаКлиенте
Процедура ОбязательныеСвязиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбязательныеСвязиПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбязательныеСвязиСвязанныйДокументНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Элементы.ОбязательныеСвязи.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ТекущиеДанные = Элементы.ОбязательныеСвязи.ТекущиеДанные;
	
	ОбязательныеСвязиСвязанныйДокументНачалоВыбораРеализация(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбязательныеСвязиСвязанныйДокументНачалоВыбораРеализация(ТекущиеДанные)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"СвязанныйДокументНачалоВыбораПродолжение",
		ЭтотОбъект);
	
	ОбязательныеСвязиПараметр = Новый Массив;
			
	ПараметрыСвязи = Новый Структура("ТипСвязи, СсылкаНа, ВидКорреспонденцииДокументНа",
		ТекущиеДанные.ТипСвязи, ТекущиеДанные.СсылкаНа, ТекущиеДанные.ВидКорреспонденцииДокументНа);
	
	ОбязательныеСвязиПараметр.Добавить(ПараметрыСвязи);
	
	ПараметрыОткрытияФормы = Новый Структура;	
	
	ПараметрыОткрытияФормы.Вставить("Документ", ДокументОснование);
	ПараметрыОткрытияФормы.Вставить("ОбязательныеСвязи", ОбязательныеСвязиПараметр);
	ПараметрыОткрытияФормы.Вставить("Контрагент", Контрагент);
	ПараметрыОткрытияФормы.Вставить("Организация", Организация);
	ПараметрыОткрытияФормы.Вставить("Проект", Проект); 
	
	ИмяФормыСозданияСвязи = "";	
		
	СтрокаПараметров = ТекущиеДанные;
	Если ТипЗнч(СтрокаПараметров.СсылкаНа) = Тип("СправочникСсылка.ДокументыПредприятия")
		Или ТипЗнч(СтрокаПараметров.СсылкаНа) = Тип("СправочникСсылка.ВидыДокументов") Тогда
		ИмяФормыСозданияСвязи = "Справочник.ДокументыПредприятия.Форма.ФормаВыбораДляСозданияСвязи";
	ИначеЕсли ТипЗнч(СтрокаПараметров.СсылкаНа) = Тип("СправочникСсылка.Файлы") Тогда
		ИмяФормыСозданияСвязи = "Справочник.Файлы.Форма.ФормаВыбораДляСозданияСвязи";
	ИначеЕсли ТипЗнч(СтрокаПараметров.СсылкаНа) = Тип("СправочникСсылка.Мероприятия") Тогда
		ИмяФормыСозданияСвязи = "Справочник.Мероприятия.Форма.ФормаВыбораДляСозданияСвязи";
	ИначеЕсли ТипЗнч(СтрокаПараметров.СсылкаНа) = Тип("СправочникСсылка.Проекты") Тогда
		ИмяФормыСозданияСвязи = "Справочник.Проекты.Форма.ФормаВыбораДляСозданияСвязи";
	ИначеЕсли ТипЗнч(СтрокаПараметров.СсылкаНа) = Тип("ДокументСсылка.ВходящееПисьмо") Тогда
		ИмяФормыСозданияСвязи = "Документ.ВходящееПисьмо.Форма.ФормаВыбораДляСозданияСвязи";
	ИначеЕсли ТипЗнч(СтрокаПараметров.СсылкаНа) = Тип("ДокументСсылка.ИсходящееПисьмо") Тогда
		ИмяФормыСозданияСвязи = "Документ.ИсходящееПисьмо.Форма.ФормаВыбораДляСозданияСвязи";
	Иначе	
		ИмяФормыСозданияСвязи = "РегистрСведений.СвязиОбъектов.Форма.ФормаВнешнегоРесурсаДляСозданияСвязи";
	КонецЕсли;	
	
	ОткрытьФорму(ИмяФормыСозданияСвязи, 
		ПараметрыОткрытияФормы, ЭтотОбъект,,,,
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбязательныеСвязиСвязанныйДокументНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовДелаетНеДействующим

&НаКлиенте
Процедура ДелаетНеДействующимНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
		
	Если ВидыОтменяемыхДокументов.Количество() <> 0 Тогда
		ПараметрыФормы.Вставить("Отбор", 
			Новый Структура("ВидДокумента, НеДействует", ВидыОтменяемыхДокументов, Ложь));
	Иначе		
		ПараметрыФормы.Вставить("Отбор", 
			Новый Структура("НеДействует", Ложь));
	КонецЕсли;	
		
	ПараметрыФормы.Вставить("ТолькоЗарегистрированные", Истина);
	ПараметрыФормы.Вставить("ИспользоватьИерархию", Ложь);
	
	ПараметрыФормы.Вставить("ПоказыватьКомандуСоздать", Ложь);
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВводНеДействующихДокументов", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.ДокументыПредприятия.ФормаВыбора", ПараметрыФормы, Элементы.ДелаетНеДействующим,,,,Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура СделатьИсходныйДокументНедействующимПриИзменении(Элемент)
	
	Элементы.ГруппаГоризонтальноНеДействует.Доступность = СделатьИсходныйДокументНедействующим;
	
КонецПроцедуры

#КонецОбласти   

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Вперед(Команда)
	
	ЗакрытьФорму();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СвязанныйДокументНачалоВыбораПродолжение(Результат, Параметры) Экспорт 
	
	Если ЗначениеЗаполнено(Результат) Тогда 
		
		Если Элементы.ОбязательныеСвязи.ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;	
		
		ВыбранныеОбязательныеСвязи = Результат; // массив
		Если ВыбранныеОбязательныеСвязи.Количество() = 1 Тогда
			
			Строка = ВыбранныеОбязательныеСвязи[0];      
			
			ТекущиеДанные = Элементы.ОбязательныеСвязи.ТекущиеДанные;
			
			ТекущиеДанные.СвязанныйДокумент = Строка.СвязанныйОбъект;
			ТекущиеДанные.Комментарий = Строка.Комментарий;
			
		КонецЕсли;	
		
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВводНеДействующихДокументов(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		ДелаетНеДействующим = Результат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму()  
	
	Если ЗначениеЗаполнено(ДелаетНеДействующимДата) И ДелаетНеДействующимДата < ОбщегоНазначенияКлиент.ДатаСеанса() Тогда
		
		ТекстОшибки = НСтр("ru = 'Дата отмены не может быть в прошлом'");
		Отказ = Ложь;
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			ТекстОшибки, , "ДелаетНеДействующимДата", , Отказ);
		Возврат;
	КонецЕсли;	
	
	НомерСтроки = 0;
	Для Каждого Стр Из ОбязательныеСвязи Цикл
		
		Если Не ЗначениеЗаполнено(Стр.СвязанныйДокумент) Тогда  
			
			ТекстОшибки = НСтр("ru = 'Нужно указать все обязательные связи'");
			Отказ = Ложь;            
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ТекстОшибки, ,
				"ОбязательныеСвязи[" + Формат(НомерСтроки, "ЧН=; ЧГ=") + "].СвязанныйДокумент", 
				, Отказ);
				
			Возврат;
		КонецЕсли;	
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;	  
	
	Результат = Новый Структура();
	
	ДелаетНеДействующимВозврат = Неопределено;
	Если СделатьИсходныйДокументНедействующим Тогда
		ДелаетНеДействующимВозврат = ДелаетНеДействующим;
	КонецЕсли;	
	
	Результат.Вставить("ДелаетНеДействующим", 		ДелаетНеДействующимВозврат);
	Результат.Вставить("ДелаетНеДействующимДата", 	ДелаетНеДействующимДата);
	
	ТипыСвязейПриСозданииНаОснованииДокумента = Новый Массив;
	Для Каждого Стр Из ТипыСвязей Цикл
		
		Если Стр.Использовать Тогда
			ТипыСвязейПриСозданииНаОснованииДокумента.Добавить(Стр.ТипСвязи);
		КонецЕсли;	
		
	КонецЦикла;	
	Результат.Вставить("ТипыСвязейПриСозданииНаОснованииДокумента", ТипыСвязейПриСозданииНаОснованииДокумента);
	
	МассивВозвратаОбязательныеСвязи = Новый Массив;
	Для Каждого Строка Из ОбязательныеСвязи Цикл
		
		ПараметрыВозврата = Новый Структура;
		ПараметрыВозврата.Вставить("ТипСвязи", Строка.ТипСвязи);
		ПараметрыВозврата.Вставить("СсылкаНа", Строка.СсылкаНа);
		ПараметрыВозврата.Вставить("СвязанныйОбъект", Строка.СвязанныйДокумент);
		ПараметрыВозврата.Вставить("Комментарий", Строка.Комментарий);
		
		МассивВозвратаОбязательныеСвязи.Добавить(ПараметрыВозврата);
		
	КонецЦикла;	

	Результат.Вставить("МассивВозвратаОбязательныеСвязи", МассивВозвратаОбязательныеСвязи);
	
	Закрыть(Результат); // тут параметры отдать
	
КонецПроцедуры

#КонецОбласти

