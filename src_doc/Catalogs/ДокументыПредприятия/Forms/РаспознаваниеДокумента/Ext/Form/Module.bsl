#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат;
	Конецесли;
	
	Документ = Параметры.ДокументСсылка;

	ЗаполнитьОсновнуюИнформацию();
	
	УстановитьВидимостьПоСостоянию();
		
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПрервать(Команда)
	
	ПрерватьРаспознавание();
	
КонецПроцедуры
	
&НаКлиенте
Процедура Повторить(Команда)
	
	ПовторитьРаспознавание();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


&НаСервере
Процедура ЗаполнитьОсновнуюИнформацию()
	
	ТаблицаИстории = РаспознаваниеДокументов.ИсторияРаспознаванияДокумента(Параметры.ДокументСсылка);
	КоличествоЗаписей = ТаблицаИстории.Количество();
	
	Если КоличествоЗаписей > 0 Тогда
		
		СтатусУстановил = ТаблицаИстории[0].Ответственный;
		ДатаУстановкиСтатуса = ТаблицаИстории[0].Период;
		СтатусРаспознавания = ТаблицаИстории[0].СтатусРаспознавания;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПоСостоянию()
	
	НаРаспознавании = СтатусРаспознавания = Перечисления.СтатусыРаспознаванияДокументов.НаРаспознавании;
	
	Прервано = СтатусРаспознавания = Перечисления.СтатусыРаспознаванияДокументов.Прервано;
	
	ЭтоОбъектЭтогоУзла = ОбщегоНазначенияДокументооборот.ОбъектЭтогоУзла(Параметры.ДокументСсылка);
	
	Элементы.КомандаПрервать.Видимость = НаРаспознавании и ЭтоОбъектЭтогоУзла = Истина;
	
	Элементы.КомандаПовторить.Видимость = Прервано И ЭтоОбъектЭтогоУзла = Истина;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПрерватьРаспознавание()

	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("Документ", Документ);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаОПрерывании", ЭтотОбъект, ПараметрыОбработчика);
	
	ТекстВопроса = НСтр("ru = 'Прервать распознавание документа?'");
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);

КонецПроцедуры

&НаКлиенте 
Процедура ПослеВопросаОПрерывании(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОшибки = "";
	
	ДокументСсылка = ДополнительныеПараметры.Документ;
	
	РаспознаваниеДокументовВызовСервера.ПрерватьРаспознаваниеДокумента(ДокументСсылка, ОписаниеОшибки);
	
	Если Не ПустаяСтрока(ОписаниеОшибки) Тогда
		ВызватьИсключение(ОписаниеОшибки);
	Иначе 
		ПараметрыОповещения = Новый Структура("Документ, СтатусРаспознавания", 
			ДокументСсылка, ПредопределенноеЗначение("Перечисление.СтатусыРаспознаванияДокументов.Прервано"));
		Оповестить("ИзменениеСтатусаРаспознаванияДокумента", ПараметрыОповещения);
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте 	
Процедура ПовторитьРаспознавание()

	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("Документ", Документ);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаОбОтправке", ЭтотОбъект, ПараметрыОбработчика);
	
	ТекстВопроса = НСтр("ru = 'Документ будет повторно отправлен на распознавание. Продолжить?'");
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);

КонецПроцедуры  
	
&НаКлиенте 
Процедура ПослеВопросаОбОтправке(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДокументСсылка = ДополнительныеПараметры.Документ;
	
	МассивФайлов = РаботаСФайламиВызовСервера.ПолучитьВсеПодчиненныеФайлы(ДокументСсылка, Ложь);  
	
	КоличествоФайлов = МассивФайлов.Количество();
	
	ФайлДляРаспознавания = Неопределено;	
	Если МассивФайлов.Количество() > 1 Тогда
		
		СписокФайлов = Новый СписокЗначений;
		СписокФайлов.ЗагрузитьЗначения(МассивФайлов);

			ПараметрыОповещения = Новый Структура;
			ПараметрыОповещения.Вставить("ДокументСсылка",ДокументСсылка);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПредложитьВыбратьФайлЗавершение", ЭтотОбъект, ПараметрыОповещения);
			ТекстЗаголовкаВыбора = НСтр("ru='Выберите файл'");
			СписокФайлов.ПоказатьВыборЭлемента(ОписаниеОповещения, ТекстЗаголовкаВыбора);
		
	ИначеЕсли КоличествоФайлов = 1 Тогда
		
		ФайлДляРаспознавания = МассивФайлов[0]; 
		
	КонецЕсли;
	
	Если ФайлДляРаспознавания <> Неопределено Тогда 
		
		РаспознаваниеДокументовВызовСервера.ОтправитьФайлДокументаНаРаспознавание(ФайлДляРаспознавания);    
		
		ПараметрыОповещения = Новый Структура("Документ, СтатусРаспознавания", 
			ДокументСсылка, ПредопределенноеЗначение("Перечисление.СтатусыРаспознаванияДокументов.НаРаспознавании"));
		
		Оповестить("ИзменениеСтатусаРаспознаванияДокумента", ПараметрыОповещения);
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредложитьВыбратьФайлЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда 
		Возврат;
	Иначе
		
		РаспознаваниеДокументовВызовСервера.ОтправитьФайлДокументаНаРаспознавание(Результат.Значение);
		
		ПараметрыОповещения = Новый Структура("Документ, СтатусРаспознавания", 
			Документ, ПредопределенноеЗначение("Перечисление.СтатусыРаспознаванияДокументов.НаРаспознавании"));
		
		Оповестить("ИзменениеСтатусаРаспознаванияДокумента", ПараметрыОповещения);
		
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
