#Область ОписаниеПеременных

// Кэширование свойств ячеек таблиц
&НаКлиенте
Перем СвойстваЯчеекТаблиц;

// Кэширование свойств элементов формы
&НаКлиенте
Перем ДанныеВыбораРеквизитов;

// Идентификатор удаляемой строки
&НаКлиенте
Перем ИдентификаторУдаленнойСтрокиТЧ;

// Данные для заполнения формы при открытии
&НаСервере
Перем ДанныеЗаполненияПриСоздании;

// Флаг проверки перед закрытием
&НаКлиенте
Перем ПроверкаПередЗакрытиемПройдена;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСервисРаспознаванияДокументовДляЗаполненияКарточек")
		ИЛИ Не ЗначениеЗаполнено(Параметры.ДокументСсылка) Тогда
			Отказ = Истина;
			Возврат;
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	ДоступнаВерификацияПоРоли = Пользователи.РолиДоступны("ПроверкаРаспознанныхДокументов");
	
	Документ = Параметры.ДокументСсылка;
	
	ЭтоОбъектЭтогоУзла = ОбщегоНазначенияДокументооборот.ОбъектЭтогоУзла(Документ);
	
	Если ДоступнаВерификацияПоРоли = Ложь ИЛИ ЭтоОбъектЭтогоУзла = Ложь Тогда
		ОткрытиеВРежимеПросмотраИстории = Истина;
	Иначе
		ОткрытиеВРежимеПросмотраИстории = Параметры.ОткрытиеВРежимеПросмотраИстории;
	КонецЕсли;
	
	ВидДокументаКэш = Параметры.ВидДокументаКэш;
	
	ДанныеЗаполненияПриСоздании = ПолучитьИзВременногоХранилища(Параметры.АдресПараметровЗаполнения);
	
	ЗаполнитьДанныеВерификацииПриСоздании();
	
	// Для реквизитов которые не верифицируются но нужны для сопоставления номенклатуры
	ЗагрузитьВсеДанныеРаспознаннойТаблицы("Товары"); 
	
	ПолеПросмотра = РаспознаваниеДокументовСлужебный.МакетОтображенияКартинкиДокументаHTML();
	
	ИсходноеИзображение = РаспознаваниеДокументовСлужебный.РаспознанныйФайлДокумента(Документ, Истина);
	
	АдресКартинки = ПоместитьВоВременноеХранилище(ИсходноеИзображение, УникальныйИдентификатор);
	
	НастроитьЭлементыПоРеквизитам(); // Обработчики, списки выбора
	
	НастроитьКолонкиТаблицы("Товары");
	НастроитьКолонкиТаблицы("Стороны");
	НастроитьКолонкиТаблицы("Контрагенты");	
	
	Заголовок = ЗаголовокДокумента;
	
	ЗапретРедактированияНоменклатуры = Константы.ЗапретитьРедактированиеТоваровИУслуг.Получить();
	
	ЗаполнитьПоляТекущимиДаннымиДокумента(Параметры.ОткрытиеИзФормыДокумента); // заполняем поля формы
	
	УчитыватьНДС = Делопроизводство.УчитыватьНДС(Организация);
	
	ЗаполнитьТекстВидТематика();
	
	ЗаполнитьТипыБезПравНаСоздание();
	
	ЗаполнитьДанныеУсловногоОформленияТаблиц(); //
	
	ЗаполнитьДанныеЯчеекТаблиц();
	
	ЗаполнитьИзСписковВыбора();
	
	СформироватьЗаголовок();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ЗагрузитьДанныеЯчеекТаблицы", 0.1, Истина);
	
	ПодключитьОбработчикОжидания("ЗагрузитьДанныеРеквизитов", 0.1, Истина);
	
	УстановитьВидимостьЭлементов();
	
	УстановитьСостояниеПроверки();
	
	НастроитьОтображениеНеВерифицированныхРеквизитов();
	
	ПроверкаПередЗакрытиемПройдена = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы = Ложь Тогда
		ДобавитьСозданныеВСпискиВыбора();
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность = Истина И ПроверкаПередЗакрытиемПройдена = Ложь Тогда
		Отказ = Истина;
		ПроверкаПередЗакрытием();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидИТематикаТекстОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьОшибкиСопоставленияНоменклатурыОбработкаНавигационнойСсылки(Элемент, 
												НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВладелецНоменклатуры = ВладелецНоменклатуры();
	
	Если Не ЗначениеЗаполнено(ВладелецНоменклатуры) Тогда
		
		Если ВидДокументаКэш.ВестиУчетПоКонтрагентам Тогда
			
			ТекстСообщения = НСтр("ru = 'Необходимо выбрать контрагента - владельца номенклатуры.'");
			
		ИначеЕсли ВидДокументаКэш.ВестиУчетСторон Тогда
			
			ТекстСообщения = НСтр("ru = '
			|Необходимо выбрать сторону - владельца номенклатуры.'");
			
		Иначе
			
			Возврат;
			
		КонецЕсли;
		
		ПоказатьПредупреждение(, ТекстСообщения);
		
		Возврат;
		
	КонецЕсли;
	
	НоменклатураДляСопоставления = НеСопоставленнаяНоменклатураПоВладельцу(ВладелецНоменклатуры);
	
	Если НоменклатураДляСопоставления.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Сопоставление номенклатуры не требуется.'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("НоменклатураДляСопоставления", НоменклатураДляСопоставления);
	ОбработкаЗавершения = Новый ОписаниеОповещения("ОбработатьСопоставлениеНоменклатуры", ЭтотОбъект, 
																							ДополнительныеПараметры);
	Номенклатура = Новый Массив;
	Для Каждого ДанныеНоменклатуры Из НоменклатураДляСопоставления Цикл
		Номенклатура.Добавить(ДанныеНоменклатуры.НеСопоставленнаяНоменклатура);
	КонецЦикла;
	
	НастройкиОткрытия = Новый Структура;
	НастройкиОткрытия.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	СопоставлениеНоменклатурыКонтрагентовКлиент.
			ОткрытьСопоставлениеНоменклатуры(Номенклатура, НастройкиОткрытия, ОбработкаЗавершения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПросмотраДокументСформирован(Элемент)
	
	Если Элемент.Документ.getElementById("image_setter") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РаспознаваниеДокументовСлужебныйКлиент.ЗагрузитьКартинкуПоАдресу(Элементы.ПолеПросмотра, АдресКартинки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПросмотраПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Если ДанныеСобытия.Element.id = "image_load_button" Тогда
		HTMLДокументСформирован = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаголовокПриИзменении(Элемент)
	
	ПриИзмененииЭлементаФормы(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СодержаниеПриИзменении(Элемент)
	
	ПриИзмененииЭлементаФормы(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	
	ПриИзмененииЭлементаФормы(Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыПриАктивизацииЯчейки(Элемент)
	
	ПриАктивизацииЯчейкиТаблицы(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ПриНачалеРедактированияТЧ(Элемент, НоваяСтрока, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередУдалением(Элемент, Отказ)
	
	ПередУдалениемСтрокиТЧ(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)
	
	ПослеУдаленияСтрокиТЧ(Элемент);
	
КонецПроцедуры


&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	ПриИзмененииЭлементаФормы(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСтороны

&НаКлиенте
Процедура СтороныПриАктивизацииЯчейки(Элемент)
	
	ПриАктивизацииЯчейкиТаблицы(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СтороныПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ПриНачалеРедактированияТЧ(Элемент, НоваяСтрока, Копирование);

КонецПроцедуры

&НаКлиенте
Процедура СтороныПередУдалением(Элемент, Отказ)
	
	ПередУдалениемСтрокиТЧ(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура СтороныПослеУдаления(Элемент)
	
	ПослеУдаленияСтрокиТЧ(Элемент);
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура СтороныПриИзменении(Элемент)
	
	ПриИзмененииЭлементаФормы(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СтороныПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКонтрагенты

&НаКлиенте
Процедура КонтрагентыПриАктивизацииЯчейки(Элемент)
	
	ПриАктивизацииЯчейкиТаблицы(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ПриНачалеРедактированияТЧ(Элемент, НоваяСтрока, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыПередУдалением(Элемент, Отказ)
	
	ПередУдалениемСтрокиТЧ(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыПослеУдаления(Элемент)
	
	ПослеУдаленияСтрокиТЧ(Элемент);
	
КонецПроцедуры


&НаКлиенте
Процедура КонтрагентыПриИзменении(Элемент)
	
	ПриИзмененииЭлементаФормы(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрименитьИЗакрыть(Команда)
	
	ТаблицыВерификации = РаспознаваниеДокументовСлужебныйКлиентСервер.ДанныеВерифицируемыхРеквизитов(ЭтотОбъект);

	ЗначенияРеквизитов = 
		РаспознаваниеДокументовСлужебныйКлиентСервер.ТекущиеЗначенияВерифицируемыхРеквизитовФормы(ЭтотОбъект);
	
	ПараметрыДляПередачи = Новый Структура;
	ПараметрыДляПередачи.Вставить("ТаблицыВерификации", ТаблицыВерификации);
	ПараметрыДляПередачи.Вставить("ЗначенияРеквизитовФормы", ЗначенияРеквизитов);

	АдресДанныхДляПередачи = ПоместитьВоВременноеХранилище(ПараметрыДляПередачи, УникальныйИдентификатор);
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Документ", Документ);
	ПараметрыОповещения.Вставить("АдресДанныхВерификации", АдресДанныхДляПередачи);
	
	Оповестить("ВерификацияРаспознанногоДокумента", ПараметрыОповещения);
	
	ПроверкаПередЗакрытиемПройдена = Истина;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ДанныеРеквизита(ИмяРеквизита = "", ИмяТЧ = Неопределено, ИдентификаторСтроки = Неопределено)
		
	Возврат РаспознаваниеДокументовСлужебныйКлиентСервер.ДанныеРеквизита(
		ЭтотОбъект, ИмяРеквизита, ИмяТЧ, ИдентификаторСтроки);
	
КонецФункции

&НаКлиенте
Функция ДанныеРеквизитаШапкиПоЭлементу(Знач Элемент)
	
	Возврат ДанныеРеквизита(Элемент.Имя);
	
КонецФункции

&НаКлиенте
Функция ДанныеРеквизитаТЧПоЭлементу(Знач Элемент)
	
	Если ТипЗнч(Элемент) <> Тип("ПолеФормы") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(ТекущийЭлемент) <> Тип("ТаблицаФормы") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекущиеДанные = ТекущийЭлемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяРеквизита = СтрЗаменить(Элемент.Имя, ТекущийЭлемент.Имя, "");
	
	Возврат ДанныеРеквизита(ИмяРеквизита, ТекущийЭлемент.Имя, ТекущиеДанные.Идентификатор);
	
КонецФункции

&НаКлиенте
Функция ДанныеСпискаВыбора(ИмяРеквизита)
	
	ДанныеДляВыбора = Новый Массив;
	
	Для Каждого ДанныеВыбора Из ДанныеВыбораРеквизитов Цикл
		
		Если ДанныеВыбора.ИмяРеквизита = ИмяРеквизита Тогда
			ДанныеДляВыбора.Добавить(ДанныеВыбора);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеДляВыбора;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДанныеЗаполненияПоКлючевымРеквизитам(Знач ИмяРаспознанногоРеквизита, Знач ВерифицируемыеРеквизитыШапки)
	
	ЗначенияКлючевыхРеквизитов = Новый Структура;
	ЗначенияКлючевыхРеквизитов.Вставить("ИНН", "");
	
	КлючевыеРеквизиты = РаспознаваниеДокументовСлужебный.КлючевыеРеквизиты(ИмяРаспознанногоРеквизита, 1);
	
	Для Каждого ИмяРеквизита Из КлючевыеРеквизиты Цикл
		
		Отбор = Новый Структура("ИмяРаспознанногоРеквизита", ИмяРеквизита);
		НСтр =  ВерифицируемыеРеквизитыШапки.НайтиСтроки(Отбор);
		
		Если НСтр.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеКлючевогоРеквизита = НСтр[0];
		
		Если СтрНайти(ДанныеКлючевогоРеквизита.ИмяРаспознанногоРеквизита, "ИННКПП") Тогда
			
			ИНН = РаспознаваниеДокументовСлужебный.ИзвлечьИННКППИзСтроки(
														ДанныеКлючевогоРеквизита.РаспознанноеЗначение, "ИНН");
			
		ИначеЕсли СтрНайти(ДанныеКлючевогоРеквизита.ИмяРаспознанногоРеквизита, "ИНН") Тогда
			
			ИНН = ДанныеКлючевогоРеквизита.РаспознанноеЗначение;
			
		Иначе
			
			ИНН = "";
			
		КонецЕсли;
		
		Если Не ПустаяСтрока(ИНН) Тогда
			ЗначенияКлючевыхРеквизитов.Вставить("ИНН", ИНН);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЗначенияКлючевыхРеквизитов;
	
КонецФункции

&НаКлиенте
Процедура ПроверкаПередЗакрытием()
	
	ТекстСобщения = НСтр("ru = 'Данные в форме были изменены. Закрыть форму без сохранения?'", 
		ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
	РежимВопроса = РежимДиалогаВопрос.ДаНет;
	ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемПродолжение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстСобщения, РежимВопроса);

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСозданныеВспискиВыбора()
	
	РеквизитыДляДобавления = Новый Массив;
	
	Для Каждого ДанныеРеквизита Из ВерифицируемыеРеквизитыТЧ Цикл
		
		Если Не (ДанныеРеквизита.СозданВФорме ИЛИ ЗначениеЗаполнено(ДанныеРеквизита.ПодобранноеЗначение)) Тогда
			Продолжить;
		КонецЕсли;
		
		Если РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(ТипЗнч(ДанныеРеквизита.ПодобранноеЗначение)) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыСозданияЭлемента = Новый Структура;
		ПараметрыСозданияЭлемента.Вставить("ИмяРаспознанногоРеквизита");
		ПараметрыСозданияЭлемента.Вставить("УстановленноеЗначение");
		ПараметрыСозданияЭлемента.Вставить("ИмяТаблицы");
		ПараметрыСозданияЭлемента.Вставить("НомерСтрокиТаблицы");
		ЗаполнитьЗначенияСвойств(ПараметрыСозданияЭлемента, ДанныеРеквизита);
		РеквизитыДляДобавления.Добавить(ПараметрыСозданияЭлемента);
		
	КонецЦикла;
	
	Для Каждого ДанныеРеквизита Из ВерифицируемыеРеквизитыШапки Цикл
		
		Если Не (ДанныеРеквизита.СозданВФорме ИЛИ ЗначениеЗаполнено(ДанныеРеквизита.ПодобранноеЗначение)) Тогда
			Продолжить;
		КонецЕсли;
		
		Если РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(ТипЗнч(ДанныеРеквизита.ПодобранноеЗначение)) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыСозданияЭлемента = Новый Структура;
		ПараметрыСозданияЭлемента.Вставить("ИмяРаспознанногоРеквизита");
		ПараметрыСозданияЭлемента.Вставить("УстановленноеЗначение");
		ПараметрыСозданияЭлемента.Вставить("НомерСтрокиТаблицы", 0);
		ЗаполнитьЗначенияСвойств(ПараметрыСозданияЭлемента, ДанныеРеквизита);
		РеквизитыДляДобавления.Добавить(ПараметрыСозданияЭлемента);
		
	КонецЦикла;
	
	Если РеквизитыДляДобавления.Количество() > 0 Тогда
		ДобавитьСозданныеВСпискиВыбораСервер(Документ, РеквизитыДляДобавления);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьСозданныеВСпискиВыбораСервер(Документ, ДанныеРеквизитов)
	
	РаспознаваниеДокументовСлужебный.ДобавитьСозданныеЭлементыВСписокВыбора(Документ, ДанныеРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеЯчеекТаблиц()
	
	ВсеСвойстваЯчеекТаблиц = РаспознаваниеДокументовСлужебный.СвойстваЯчеекТаблицы(ВерифицируемыеРеквизитыТЧ, Документ);
	АдресСвойствЯчеекТаблиц = ПоместитьВоВременноеХранилище(ВсеСвойстваЯчеекТаблиц, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеЯчеекТаблицы() Экспорт
	
	СвойстваЯчеекТаблиц = ПолучитьИзВременногоХранилища(АдресСвойствЯчеекТаблиц);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеРеквизитов() Экспорт
	
	ДанныеВыбораРеквизитов = ПолучитьИзВременногоХранилища(АдресЗначенияВыбораДляШапки);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТипыБезПравНаСоздание() 
	
	ПроверяемыеТипы = Новый Массив;
	ПроверяемыеТипы.Добавить(Тип("СправочникСсылка.Контрагенты"));
	ПроверяемыеТипы.Добавить(Тип("СправочникСсылка.Номенклатура"));
	ПроверяемыеТипы.Добавить(Тип("СправочникСсылка.Пользователи"));
	ПроверяемыеТипы.Добавить(Тип("СправочникСсылка.Сотрудники"));
	ПроверяемыеТипы.Добавить(Тип("СправочникСсылка.КонтактныеЛица"));
		
	ТипыБезПравНаСоздание.Добавить(Тип("СправочникСсылка.Организации"));
	
	Для Каждого Тип Из ПроверяемыеТипы Цикл
		
		ПустаяСсылка = Новый(Тип);
		ИмяСправочника = ПустаяСсылка.Метаданные().Имя;
		
		Если Не ПравоДоступа("Добавление", Метаданные.Справочники[ИмяСправочника]) Тогда
			ТипыБезПравНаСоздание.Добавить(Тип);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет список выбора и подсказку ввода
// 
// Параметры:
//  РеквизитШапки - РеквизитФормы - Реквизит шапки
//  ИзменяемыйЭлемент - ГруппаФормы, ПолеФормы, ДекорацияФормы, ТаблицаФормы, КнопкаФормы - Изменяемый элемент
//  ДанныеДляВыбораРеквизитов - ТаблицаЗначений - Данные для выбора реквизитов
&НаСервере
Процедура ЗаполнитьСписокВыбораИПодсказку(РеквизитШапки, ИзменяемыйЭлемент, ДанныеДляВыбораРеквизитов)
	
	ИзменяемыйЭлемент.Подсказка = Неопределено;
	РаспознанныйТекст = РеквизитШапки.РаспознанныйТекст;
	
	ЭтоПримитивныйТип = РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(
		ТипЗнч(РеквизитШапки.РаспознанноеЗначение));

	Отбор = Новый Структура("ИмяРеквизита", РеквизитШапки.ИмяРаспознанногоРеквизита);
	ПодходящиеЗначения = ДанныеДляВыбораРеквизитов.НайтиСтроки(Отбор);
	
	Если РеквизитШапки.ИмяРеквизита = "ПродавецОрганизация"
		Или РеквизитШапки.ИмяРеквизита = "ПокупательОрганизация"
		Или РеквизитШапки.ИмяРеквизита = "Исполнитель"
		Или ТипыБезПравНаСоздание.НайтиПоЗначению(ТипЗнч(РеквизитШапки.РаспознанноеЗначение)) <> Неопределено
		Или ЭтоПримитивныйТип Тогда
		
		КартинкаСоздание = 2;
	Иначе
		КартинкаСоздание = 1;
	КонецЕсли;
	
	Если Не РаспознаваниеДокументовКлиентСервер.
		РаспознанныйТекстСодержитПустоеЗначениеПоля(РеквизитШапки.ИмяРеквизита, РаспознанныйТекст) Тогда
		СписокДляВыбора = РаспознаваниеДокументовСлужебныйКлиентСервер.ПолучитьСписокДляВыбораПользователем(
			РаспознанныйТекст, ПодходящиеЗначения, КартинкаСоздание);
			
		ИзменяемыйЭлемент.СписокВыбора.Очистить();
		
		Для Каждого ДанныеВыбора Из СписокДляВыбора Цикл
			Если ТипЗнч(ДанныеВыбора.Значение) = Тип("Структура") Тогда
				ИзменяемыйЭлемент.СписокВыбора.Добавить(ДанныеВыбора.Значение.Значение, ДанныеВыбора.Представление, ,
					ДанныеВыбора.Картинка);
			Иначе
				Если ЭтоПримитивныйТип Тогда
					ИзменяемыйЭлемент.СписокВыбора.Добавить(ДанныеВыбора.Значение, ДанныеВыбора.Представление);
				Иначе
					ИзменяемыйЭлемент.СписокВыбора.Добавить(ДанныеВыбора.Значение, ДанныеВыбора.Представление, ,
						ДанныеВыбора.Картинка);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РеквизитШапки.УстановленноеЗначение) Тогда
		
		Если РеквизитШапки.ИмяРеквизита <> "ИтогоСумма" И РеквизитШапки.ИмяРеквизита <> "ИтогоСуммаНДС"
			И РеквизитШапки.ИмяРеквизита <> "ИтогоВсего" И РеквизитШапки.ИмяРеквизита <> "Склад" Тогда
			
			ИзменяемыйЭлемент.ПодсказкаВвода = СтрШаблон(НСтр("ru = 'Не сопоставлен: %1'"), РаспознанныйТекст);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТекстВидТематика()
		
	ВидИТематикаТекст = Строка(ВидДокумента);
	
	Если ВидДокументаКэш.ВестиУчетПоТематикам Тогда
		Элементы.ВидИТематикаТекст.Подсказка = НСтр("ru = 'Вид и тематика документа'");

		Если ЗначениеЗаполнено(Тематика) Тогда
			ВидИТематикаТекст = СтрШаблон("%1 - %2", СокрЛП(ВидДокумента), СокрЛП(Тематика));
		КонецЕсли;
	Иначе
		Элементы.ВидИТематикаТекст.Подсказка = НСтр("ru = 'Вид документа'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоляТекущимиДаннымиДокумента(ОткрытиеИзФормыДокумента)
	
	Если ОткрытиеИзФормыДокумента = Истина Тогда
		ЗначенияРеквизитов = ДанныеЗаполненияПриСоздании.ЗначенияРеквизитовФормы;
				
	Иначе
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, 
							РаспознаваниеДокументовСлужебныйКлиентСервер.ВерифицируемыеРеквизитыДокумента(), Истина);
		
	КонецЕсли;
		
	Для Каждого Реквизит Из ЗначенияРеквизитов Цикл
		
		Если ТипЗнч(ЭтотОбъект[Реквизит.Ключ]) = Тип("ДанныеФормыКоллекция") Тогда
			
			ЭтотОбъект[Реквизит.Ключ].Очистить();
			ДанныеКоллекции = Реквизит.Значение.Выгрузить();

			Для Каждого ЭлементКоллекции Из ДанныеКоллекции Цикл
				
				НовСтр = ЭтотОбъект[Реквизит.Ключ].Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, ЭлементКоллекции);
				НовСтр.Идентификатор = ЭлементКоллекции.Идентификатор;
				НовСтр.ПорядокСтроки = ЭлементКоллекции.НомерСтроки;
				
			КонецЦикла;
			
		Иначе
		
			Если Реквизит.Ключ = "Заголовок" Тогда
				ЗаголовокДокумента = Реквизит.Значение;
			Иначе
				ЭтотОбъект[Реквизит.Ключ] = Реквизит.Значение;
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьЗначенияРеквизитовКорреспонденции();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеУсловногоОформленияТаблиц()
	
	ЗаполнитьПоляУсловногоОформленияТаблицы("Товары");
	
	РаспознаваниеДокументовСлужебныйКлиентСервер.УстановитьУсловноеОформлениеДляТаблицы(ЭтотОбъект, "Товары");
	
	ЗаполнитьПоляУсловногоОформленияТаблицы("Стороны");
	
	РаспознаваниеДокументовСлужебныйКлиентСервер.УстановитьУсловноеОформлениеДляТаблицы(ЭтотОбъект, "Стороны");
	
	ЗаполнитьПоляУсловногоОформленияТаблицы("Контрагенты");
	
	РаспознаваниеДокументовСлужебныйКлиентСервер.УстановитьУсловноеОформлениеДляТаблицы(ЭтотОбъект, "Контрагенты");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоляУсловногоОформленияТаблицы(ИмяТаблицы)
	
	ТаблицаФормы = ЭтотОбъект[ИмяТаблицы];
	
	Если ТаблицаФормы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаФормыЗначение = РеквизитФормыВЗначение(ИмяТаблицы, Тип("ТаблицаЗначений"));
	
	Для Каждого Строка Из ТаблицаФормы Цикл
		
		Для Каждого Колонка Из ТаблицаФормыЗначение.Колонки Цикл
			
			ИмяРеквизита = Колонка.Имя;
			
			ИмяКолонкиТребуетПроверки = ИмяРеквизита + "ТребуетПроверки";
			
			Если Строка.Свойство(ИмяКолонкиТребуетПроверки) = Ложь Тогда
				Продолжить;
			КонецЕсли;
			
			Отбор = Новый Структура;
			Отбор.Вставить("ТабличнаяЧасть", ИмяТаблицы);
			
			Отбор.Вставить("ИмяРеквизита", ИмяРеквизита);
			Отбор.Вставить("Идентификатор", Строка.Идентификатор);
			
			НСтр = ВерифицируемыеРеквизитыТЧ.НайтиСтроки(Отбор);
			
			Если НСтр.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Строка[ИмяКолонкиТребуетПроверки] = НСтр[0].ТребуетПроверки И НСтр[0].Проверен = Ложь;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыПоРеквизитам()
	
	Если ОткрытиеВРежимеПросмотраИстории = Ложь Тогда
		ДанныеДляВыбораРеквизитов = РаспознаваниеДокументовСлужебный.ПолучитьДанныеДляСпискаВыбора(Документ);
		ЗначенияВыбораРеквизитов = ОбщегоНазначения.ТаблицаЗначенийВМассив(ДанныеДляВыбораРеквизитов);
	КонецЕсли;
	
	АдресЗначенияВыбораДляШапки = ПоместитьВоВременноеХранилище(ЗначенияВыбораРеквизитов, УникальныйИдентификатор);
	
	Для Каждого РеквизитШапки Из ВерифицируемыеРеквизитыШапки Цикл
		
		Если ПустаяСтрока(РеквизитШапки.ИмяРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элементы, РеквизитШапки.ИмяРеквизита) 
			Тогда
				Продолжить;
		КонецЕсли;
			
		ИзменяемыйЭлемент = Элементы[РеквизитШапки.ИмяРеквизита];
		
		НастроитьОбработчикиСобытий(ИзменяемыйЭлемент);
		
		Если ОткрытиеВРежимеПросмотраИстории = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьСписокВыбораИПодсказку(РеквизитШапки, ИзменяемыйЭлемент, ДанныеДляВыбораРеквизитов);
		
	КонецЦикла;
	
КонецПроцедуры

// Добавляет обработчики событий для полей формы, устанавливает свойства ИсторияВыбораПриВводе и БыстрыйВыбор
// 
// Параметры:
//  ИзменяемыйЭлемент - ГруппаФормы, ПолеФормы, ДекорацияФормы, ТаблицаФормы, КнопкаФормы - Изменяемый элемент
//  ЗапретитьИсториюВыбора - Булево - Запретить историю выбора
//  РазрешитьБыстрыйВыбор - Булево - Разрешить быстрый выбор
&НаСервере
Процедура НастроитьОбработчикиСобытий(ИзменяемыйЭлемент, ЗапретитьИсториюВыбора = Истина, РазрешитьБыстрыйВыбор = Ложь)
	
	ИзменяемыйЭлемент.БыстрыйВыбор = РазрешитьБыстрыйВыбор;
	ИзменяемыйЭлемент.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	ИзменяемыйЭлемент.КнопкаСоздания = Ложь;
	Если ЗапретитьИсториюВыбора Тогда
		ИзменяемыйЭлемент.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
	КонецЕсли;
	ИзменяемыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_ПриИзмененииПоля");
	ИзменяемыйЭлемент.УстановитьДействие("АвтоПодбор", "Подключаемый_АвтоПодборПоля");
	ИзменяемыйЭлемент.УстановитьДействие("ОбработкаВыбора", "Подключаемый_ОбработкаВыбораПоля");
	
КонецПроцедуры

&НаСервере
Процедура НастроитьКолонкиТаблицы(ИмяТаблицы)
	
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеОбработчики = Новый Массив;
	
	ПараметрыОбработчика_ОбработкаВыбора = Новый Структура;
	ПараметрыОбработчика_ОбработкаВыбора.Вставить("ИмяОбработчика", "ОбработкаВыбора");
	ПараметрыОбработчика_ОбработкаВыбора.Вставить("ИмяПроцедуры", "Подключаемый_ОбработкаВыбораПоля");
	
	ПараметрыОбработчика_ПриИзменении = Новый Структура;
	ПараметрыОбработчика_ПриИзменении.Вставить("ИмяОбработчика", "ПриИзменении");
	ПараметрыОбработчика_ПриИзменении.Вставить("ИмяПроцедуры", "Подключаемый_ПриИзмененииКолонки");
	
	ДобавляемыеОбработчики.Добавить(ПараметрыОбработчика_ОбработкаВыбора);
	ДобавляемыеОбработчики.Добавить(ПараметрыОбработчика_ПриИзменении);
	
	РаспознаваниеДокументовСлужебный.НастроитьСлужебныеКолонкиРекурсивно(
		ДобавляемыеРеквизиты, Элементы[ИмяТаблицы], Элементы[ИмяТаблицы].ПутьКДанным, ДобавляемыеОбработчики);
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	РаспознаваниеДокументовСлужебный.СоздатьЭлементыДобавленныхРеквизитов(
		ЭтотОбъект, ДобавляемыеРеквизиты, Элементы[ИмяТаблицы]);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОтображениеНеВерифицированныхРеквизитов()
	
	Для Каждого РеквизитШапки Из ВерифицируемыеРеквизитыШапки Цикл
		
		Если ПустаяСтрока(РеквизитШапки.ИмяРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элементы, РеквизитШапки.ИмяРеквизита) 
			Тогда
				Продолжить;
		КонецЕсли;
			
		ИзменяемыйЭлемент = Элементы[РеквизитШапки.ИмяРеквизита];
		
		УстановитьФонПоля(ИзменяемыйЭлемент, РеквизитШапки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура НумерацияПорядкаСтрокПоЭлементу(Элемент)
	
	Порядок = 1;
	
	Для Каждого Стр Из ЭтотОбъект[Элемент.Имя] Цикл
		
		Стр.ПорядокСтроки = Порядок;
		Порядок = Порядок + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриблизитьПоКоординатам(Координаты, СтрокВИзображении)
	
	Если HTMLДокументСформирован Тогда
		Если РаспознаваниеДокументовКлиентСервер.ВсеКоординатыНулевые(Координаты) Тогда
			Элементы.ПолеПросмотра.Документ.defaultView.clean_bbox();
		Иначе
			Элементы.ПолеПросмотра.Документ.defaultView.zoom_to_bbox(Координаты[0], Координаты[1], Координаты[2], 
				Координаты[3], СтрокВИзображении);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеИнтерактивногоСозданияКонтрагента(Результат, ПараметрыСоздания) Экспорт
	
	СсылкаНаОбъект = Неопределено;
	
	Если ПараметрыСоздания.Свойство("СсылкаНаОбъект", СсылкаНаОбъект) = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеРеквизита = Неопределено;
	
	Если ПараметрыСоздания.Свойство("ДанныеРеквизита", ДанныеРеквизита) = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеРеквизита.Свойство("ТабличнаяЧасть") Тогда
		
		Отбор = Новый Структура;
		Отбор.Вставить("Идентификатор", ДанныеРеквизита.Идентификатор);
		СтрокиТаблицы = ЭтотОбъект[ДанныеРеквизита.ТабличнаяЧасть].НайтиСтроки(Отбор);
		
		Если СтрокиТаблицы.Количество() > 0 Тогда
			СтрокаТаблицы = СтрокиТаблицы[0];
			СтрокаТаблицы[ДанныеРеквизита.ИмяРеквизита] = СсылкаНаОбъект;
			ОбновитьПризнакВерификацииРеквизитаТаблицы(ДанныеРеквизита, Истина);
		КонецЕсли;
	
	Иначе
		
		ЭтотОбъект[ДанныеРеквизита.ИмяРеквизита] = СсылкаНаОбъект;
		
		Если Контрагенты.Количество() = 1 Тогда
			
			Контрагенты[0].Контрагент = СсылкаНаОбъект;
			
		КонецЕсли;
		
		ОбновитьПризнакВерификацииРеквизита(ДанныеРеквизита, Истина);
		
	КонецЕсли;
	
	ДанныеРеквизита.СозданВФорме = Истина;
	
	УстановитьСостояниеПроверки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииОрганизации()
	
	УчитыватьНДС = Делопроизводство.УчитыватьНДС(Организация);
	Если ЗначениеЗаполнено(Организация) И УчитыватьНДС = Ложь Тогда
		ОбновитьПризнакВерификацииПоля(Элементы["СуммаНДС"]);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриАктивизацииЯчейкиТаблицы(Элемент)
	
	Если ИдентификаторУдаленнойСтрокиТЧ <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ТекущийЭлемент) <> Тип("ТаблицаФормы") Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.Имя = "Стороны" Тогда
		ТекущийВыбранныйЭлемент = Элементы.СтороныСторона;
	Иначе
		ТекущийВыбранныйЭлемент = ТекущийЭлемент.ТекущийЭлемент;
	КонецЕсли;
	
	ДанныеРеквизита = ДанныеРеквизитаТЧПоЭлементу(ТекущийВыбранныйЭлемент);
	
	Если ДанныеРеквизита = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаБезАналога = ТаблицаБезАналогаВРаспознанномДокументе(Элемент.Имя);
	
	ИмяРеквизитаФормы = ТекущийВыбранныйЭлемент.Имя;
	
	ЭлементКолонка = Элементы[ИмяРеквизитаФормы];
	ЭлементКолонка.СписокВыбора.Очистить();
	
	ИмяРеквизитаТЧ = ДанныеРеквизита.ИмяРеквизита;
	
	Если ТаблицаБезАналога = Ложь Тогда
		
		Ключ = РаспознаваниеДокументовСлужебныйКлиентСервер.КлючСвойстваЯчеекТаблицы(
			ИмяРеквизитаТЧ, ДанныеРеквизита.НомерСтрокиТаблицы);
		
		Свойства = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СвойстваЯчеекТаблиц, Ключ);
		Если Свойства = Неопределено Тогда
			
			ДанныеПриближения = Новый Структура("Координаты, СтрокВИзображении", Неопределено, 0);
			ПодключитьОбработчикОжидания("Подключаемый_ПриблизитьПоКоординатам", 0.2, Истина);
			
			Возврат;
		КонецЕсли;
		
		ДанныеПриближения = Новый Структура("Координаты, СтрокВИзображении", Свойства.Координаты, 
			Свойства.СтрокВИзображении);
		
		Если ОткрытиеВРежимеПросмотраИстории = Ложь Тогда
			
			МожноСоздать =  Не РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(Свойства.ТипЗначения) 
				И ТипыБезПравНаСоздание.НайтиПоЗначению(ТипЗнч(ДанныеРеквизита.РаспознанноеЗначение)) = Неопределено;
			
			Если ДанныеРеквизита <> Неопределено Тогда
				
				Если МожноСоздать = Истина Тогда
					КнопкаДобавления = 1;
				Иначе
					КнопкаДобавления = 2;
				КонецЕсли;
				
				СписокДляВыбора = РаспознаваниеДокументовСлужебныйКлиентСервер.
							ПолучитьСписокДляВыбораПользователем(
								ДанныеРеквизита.РаспознанныйТекст, Свойства.ЗначенияВыбора, КнопкаДобавления);
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		КоординатыКартинки = РаспознаваниеДокументовСлужебныйКлиентСервер.ПолучитьНаборКоординат(ДанныеРеквизита);
		
		ДанныеПриближения = Новый Структура(
			"Координаты, СтрокВИзображении", КоординатыКартинки, ДанныеРеквизита.СтрокВИзображении); 
		
		ТипРеквизита = ТипЗнч(ДанныеРеквизита.РаспознанноеЗначение);
		
		Если ОткрытиеВРежимеПросмотраИстории = Ложь Тогда
			
			МожноСоздать = 
				ТипыБезПравНаСоздание.НайтиПоЗначению(ТипРеквизита) = Неопределено;
		
			Если ТипРеквизита = Тип("СправочникСсылка.Номенклатура") Тогда
				МожноСоздать = МожноСоздать И (ЗапретРедактированияНоменклатуры = Ложь);
			КонецЕсли;
			
			ЗначенияВыбора = ДанныеСпискаВыбора(ДанныеРеквизита.ИмяРаспознанногоРеквизита);
			СписокДляВыбора = РаспознаваниеДокументовСлужебныйКлиент.ЗначенияСпискаВыбора(
				ДанныеРеквизита.РаспознанныйТекст, ТипРеквизита, ЗначенияВыбора,
				МожноСоздать);
				
		КонецЕсли;
			
	КонецЕсли;
	
	Если ОткрытиеВРежимеПросмотраИстории = Ложь Тогда
		
		Для Каждого ДанныеВыбора Из СписокДляВыбора Цикл
			
			ЭлементКолонка.СписокВыбора.Добавить(ДанныеВыбора.Значение, ДанныеВыбора.Представление, , 
				ДанныеВыбора.Картинка);
				
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ТекущиеДанные[ИмяРеквизитаТЧ]) Тогда
			ОбновитьПризнакВерификацииЯчейкиТаблицы(ТекущийВыбранныйЭлемент);
		КонецЕсли;
		
		УстановитьСостояниеПроверки();
		
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ПриблизитьПоКоординатам", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеУдаленияСтрокиТЧ(Элемент)
	
	РаспознаваниеДокументовСлужебныйКлиент.ПослеУдаленияСтрокиТЧ(ЭтотОбъект, ИдентификаторУдаленнойСтрокиТЧ, Элемент);
	НумерацияПорядкаСтрокПоЭлементу(Элемент);
	ПриИзмененииЭлементаФормы(Элемент);
	УстановитьСостояниеПроверки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриНачалеРедактированияТЧ(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.ПорядокСтроки = Товары.Количество();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередУдалениемСтрокиТЧ(Элемент, Отказ)
	
	ИдентификаторУдаленнойСтрокиТЧ = Элемент.ТекущиеДанные.Идентификатор;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемПродолжение(ОтветПользователя, Параметры) Экспорт

	Если ОтветПользователя <> КодВозвратаДиалога.Да Тогда
		ПроверкаПередЗакрытиемПройдена = Ложь;
		Возврат;
	КонецЕсли;
	ПроверкаПередЗакрытиемПройдена = Истина;
	Закрыть();

КонецПроцедуры

&НаСервере
Функция ТаблицаБезАналогаВРаспознанномДокументе(Знач ИмяТаблицы)
	
	МассивИмен = Новый Массив;
	МассивИмен.Добавить("Стороны");
	МассивИмен.Добавить("Контрагенты");
	
	Возврат МассивИмен.Найти(ИмяТаблицы) <> Неопределено;
	
КонецФункции

&НаКлиенте
Процедура УстановитьФонПоля(Элемент, ДанныеРеквизита)
	
	ЭлементыСтиля = СтандартныеПодсистемыКлиент.ЭлементыСтиля();

	Если ДанныеРеквизита.ТребуетПроверки = Истина И ДанныеРеквизита.Проверен = Ложь Тогда
		Элемент.ЦветФона = ЭлементыСтиля.ЦветФонаПредупреждения;
	Иначе
		Элемент.ЦветФона = Новый Цвет();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьЭлементов()
	
	НастроитьДоступностьПолей(Элементы.ГруппаЛево.ПодчиненныеЭлементы);
	
	Элементы.ПрименитьИЗакрыть.Видимость = Не ОткрытиеВРежимеПросмотраИстории;
	
	Элементы.ГруппаСумма.Видимость = ВидДокументаКэш.УчитыватьСуммуДокумента;
	
	Элементы.ГруппаСуммаНДС.Видимость = ВидДокументаКэш.УчитыватьСуммуДокумента И УчитыватьНДС;
	
	Элементы.ГруппаИсходящийНомер.Видимость = ВидДокументаКэш.ЯвляетсяВходящейКорреспонденцией;
	
	Элементы.ГруппаКонтрагент.Видимость = ВидДокументаКэш.ВестиУчетПоКонтрагентам;
	
	Элементы.ГруппаОрганизация.Видимость = ВидДокументаКэш.ВестиУчетПоОрганизациям И Не ВидДокументаКэш.ВестиУчетСторон;
	
	Элементы.ГруппаСтороны.Видимость = ВидДокументаКэш.ВестиУчетСторон;
	
	Элементы.СтраницаТовары.Видимость = ВидДокументаКэш.ВестиУчетТоваровИУслуг;
	Элементы.СтраницаТовары.ТолькоПросмотр = ОткрытиеВРежимеПросмотраИстории;
	Элементы.ТоварыСтавкаНДС.Видимость = ВидДокументаКэш.УчитыватьСуммуДокумента И УчитыватьНДС;
	Элементы.ТоварыСуммаНДС.Видимость = ВидДокументаКэш.УчитыватьСуммуДокумента И УчитыватьНДС;
	
	Элементы.СтраницаКонтрагенты.Видимость = ВидДокументаКэш.ВестиУчетПоКонтрагентам И Контрагенты.Количество() > 1;
	Элементы.СтраницаКонтрагенты.ТолькоПросмотр = ОткрытиеВРежимеПросмотраИстории;
	
	Элементы.ГруппаТабличныеЧасти.ОтображениеСтраниц = 
		?(ВидДокументаКэш.ВестиУчетТоваровИУслуг И Элементы.СтраницаКонтрагенты.Видимость, 
			ОтображениеСтраницФормы.ЗакладкиСверху, ОтображениеСтраницФормы.Нет);
			
	Элементы.ГруппаОшибкиСопоставленияНоменклатуры.Видимость = ОткрытиеВРежимеПросмотраИстории = Ложь 
		И НеобходимоСопоставитьНоменклатуру() 
		И (ВидДокументаКэш.ВестиУчетСторон ИЛИ ВидДокументаКэш.ВестиУчетПоКонтрагентам);
		
КонецПроцедуры

&НаКлиенте
Процедура НастроитьДоступностьПолей(ЭлементыФормы)
	
	Если Не ОткрытиеВРежимеПросмотраИстории Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из ЭлементыФормы Цикл
		
		Если ТипЗнч(Элемент) = Тип("ТаблицаФормы") Тогда
			Элемент.ТолькоПросмотр = Истина;
			Продолжить;
		ИначеЕсли ТипЗнч(Элемент) <> Тип("ПолеФормы") И ТипЗнч(Элемент) <> Тип("ГруппаФормы") Тогда
			Продолжить;
		КонецЕсли;
		
		Если Элемент.Вид = ВидГруппыФормы.ОбычнаяГруппа Тогда
			
			НастроитьДоступностьПолей(Элемент.ПодчиненныеЭлементы);
			
		ИначеЕсли Элемент.Вид = ВидПоляФормы.ПолеВвода Тогда
			
			Элемент.РедактированиеТекста = Ложь;
			Элемент.КнопкаВыбора = Ложь;
			
		Иначе
			
			Продолжить;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначенияРеквизитовКорреспонденции()
	
	РеквизитыОтправителя = РаботаСКорреспонденцией.РеквизитыОтправителяДокументовКорреспонденции(Документ).Выгрузить();
	Если РеквизитыОтправителя.Количество() > 0 Тогда
		ИсходящаяДата = РеквизитыОтправителя[0].ДатаКонтрагента;
		ИсходящийНомер = РеквизитыОтправителя[0].НомерКонтрагента;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеПроверки()
	
	КоличествоТребующихПроверки = 
		РаспознаваниеДокументовСлужебныйКлиентСервер.КоличествоРеквизитовДляВерификации(ЭтотОбъект);
			
	Если ОткрытиеВРежимеПросмотраИстории = Истина Тогда
		
		Если КоличествоТребующихПроверки > 0 Тогда
			
			СостояниеПроверки = НСтр("ru = 'Необходима верификация (%1)'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
			СостояниеПроверки = СтрШаблон(СостояниеПроверки, КоличествоТребующихПроверки);
				
			Возврат;
	
		Иначе
			
			ДанныеИстории = ИсторияВерификации();
			
			Если ДанныеИстории = Неопределено Тогда
				СостояниеПроверки = НСтр("ru = 'Верификация не требуется'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
				Возврат;
			КонецЕсли;
			
			ИсторияСтрокой = НСтр("ru = 'Верифицирован %1 (%2)'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
			СостояниеПроверки = 
				СтрШаблон(ИсторияСтрокой, ДанныеИстории.Ответственный, Формат(ДанныеИстории.ДатаПроверки,"ДЛФ=ДВ"));
				
		КонецЕсли;

	Иначе
	
		
		Если КоличествоТребующихПроверки > 0 Тогда
			
			СостояниеПроверки = НСтр("ru = 'Необходима верификация (%1)'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
			СостояниеПроверки = СтрШаблон(СостояниеПроверки, КоличествоТребующихПроверки);
			
		Иначе
			
			СостояниеПроверки = НСтр("ru = 'Верифицирован'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
			
		КонецЕсли;
		
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Функция ИменаКолонокПересчетаСуммТЧ()
	
	ИменаКолонок = Новый Массив;
	ИменаКолонок.Добавить("Количество");
	ИменаКолонок.Добавить("Цена");
	ИменаКолонок.Добавить("Сумма");
	ИменаКолонок.Добавить("СтавкаНДС");
	
	Возврат ИменаКолонок;
	
КонецФункции

&НаКлиенте
Функция ПорогУверенностиПоУмолчанию()
	Возврат РаспознаваниеДокументовКлиентСервер.ПорогУверенностиДляПодстановки();
КонецФункции

&НаСервере
Процедура ЗагрузитьВсеДанныеРаспознаннойТаблицы(ИменаТаблиц)

	// В верифицируемых реквизитах есть только данные привязанные к полям документа
	// Подгружаемые данные нужны для более точного формирования натурального ключа номенклатуры - код единицы, артикул
	ДанныеТаблиц = РегистрыСведений.РаспознанныеДанныеТаблиц.ДанныеТаблицыДокумента(Документ, ИменаТаблиц);
	РаспознанныеДанныеТаблиц.Загрузить(ДанныеТаблиц);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииЭлементаФормы(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

#Область Номенклатура

&НаКлиенте
Функция ВладелецНоменклатуры()
	
	Если ВидДокументаКэш.ВестиУчетПоКонтрагентам = Истина И ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Контрагент;
	КонецЕсли;
	
	ВозможныйВладелец = Неопределено;
	
	Если ВидДокументаКэш.ВестиУчетСторон Тогда
		
		Для Каждого Сторона Из Стороны Цикл
			
			Если ТипЗнч(Сторона.Сторона) = Тип("СправочникСсылка.Контрагенты") Тогда
				
				ВозможныйВладелец = Сторона.Сторона;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;

	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВозможныйВладелец) Тогда
		Возврат ВозможныйВладелец;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Функция НеобходимоСопоставитьНоменклатуру()

	Если ВерифицируемыеРеквизитыТЧ.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ВидДокументаКэш.ВестиУчетТоваровИУслуг = Ложь Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если (ВидДокументаКэш.ВестиУчетПоКонтрагентам ИЛИ ВидДокументаКэш.ВестиУчетСторон) = Ложь Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьНеЗаполненная = Ложь;
	
	Для Каждого Стр Из Товары Цикл
		
		Если ЗначениеЗаполнено(Стр.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		
		ЕстьНеЗаполненная = Истина;
		
		Прервать;
		
	КонецЦикла;
	
	Если ЕстьНеЗаполненная = Ложь Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВладелецНоменклатуры = ВладелецНоменклатуры();
	
	НеСопоставленнаяНоменклатура = НеСопоставленнаяНоменклатураПоВладельцу(ВладелецНоменклатуры);
	
	Если НеСопоставленнаяНоменклатура.Количество() > 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция НеСопоставленнаяНоменклатураПоВладельцу(Знач Владелец)
	
	НеСопоставленнаяНоменклатура = Новый Массив;
	НомерСтроки = 0;
	СчетчикПустыхСтрок = 0;
	МаксКолвоПустыхСтрок = 3;

	ОтборСтрок = Новый Структура("ИмяТаблицы", "Товары");
	
	Пока СчетчикПустыхСтрок < МаксКолвоПустыхСтрок Цикл
		
		НомерСтроки = НомерСтроки + 1;
		
		ОтборСтрок.Вставить("НомерСтрокиТаблицы", НомерСтроки);
		
		// Сначала проверим есть ли эта строка среди верифицируемых реквизитов (м.б. удалена)
		ДанныеСтрокиДляВерификации = ВерифицируемыеРеквизитыТЧ.НайтиСтроки(ОтборСтрок);
		
		Если ДанныеСтрокиДляВерификации.Количество() = 0 Тогда
			СчетчикПустыхСтрок = СчетчикПустыхСтрок + 1;
			Продолжить;
		КонецЕсли;
		
		Если ДанныеСтрокиДляВерификации[0].СтрокаУдалена Тогда
			СчетчикПустыхСтрок = СчетчикПустыхСтрок + 1;
			Продолжить;
		КонецЕсли;
		
		// Данные для сопоставления возьмем из распознанных данных, т.к. при загрузке сопоставление выполняется по ним
		ДанныеСтрокиДляСопоставления = РаспознанныеДанныеТаблиц.НайтиСтроки(ОтборСтрок);
		
		НоваяНоменклатураКонтрагента = РаспознаваниеДокументов
			.НоваяНоменклатураКонтрагентаПоРаспознаннымДанным(ДанныеСтрокиДляСопоставления, Владелец);
			
		Если НоваяНоменклатураКонтрагента = Неопределено Тогда
			Продолжить;
		КонецЕсли;
			
		Отбор = Новый Структура("НоменклатураКонтрагента", НоваяНоменклатураКонтрагента);
			
		СоответствияНоменклатуры = СопоставлениеНоменклатурыКонтрагентов.НайтиСоответствиеНоменклатуры(Отбор, Истина);
		
		Если СоответствияНоменклатуры.Количество() = 0 Тогда
			ДанныеСтрокиСтруктура = Новый Структура;
			ДанныеСтрокиСтруктура.Вставить("ИдентификаторСтроки", ДанныеСтрокиДляВерификации[0].Идентификатор);
			ДанныеСтрокиСтруктура.Вставить("НеСопоставленнаяНоменклатура", НоваяНоменклатураКонтрагента);
			НеСопоставленнаяНоменклатура.Добавить(ДанныеСтрокиСтруктура);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НеСопоставленнаяНоменклатура;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьСопоставлениеНоменклатуры(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТЧТовары = "Товары";
	
	Для Каждого ДанныеНоменклатуры Из ДополнительныеПараметры.НоменклатураДляСопоставления Цикл
		
		ИдентификаторНоменклатуры = ДанныеНоменклатуры.НеСопоставленнаяНоменклатура.Идентификатор;
		
		Если ПустаяСтрока(ИдентификаторНоменклатуры) Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ДанныеСопоставленияНоменклатуры Из Результат Цикл
			
			Если ДанныеСопоставленияНоменклатуры.НоменклатураКонтрагента.Идентификатор <> ИдентификаторНоменклатуры 
				Тогда
					Продолжить;
			КонецЕсли;
			
			НоменклатураИБ = ДанныеСопоставленияНоменклатуры.НоменклатураИБ;
			
			Если Не ЗначениеЗаполнено(НоменклатураИБ.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаТовары = РаспознаваниеДокументовКлиентСервер.СтрокаПоИдентификатору(
				ЭтотОбъект, ИмяТЧТовары, ДанныеНоменклатуры.ИдентификаторСтроки);
			
			СтрокаТовары.Номенклатура = НоменклатураИБ.Номенклатура;
			СтрокаТовары.НоменклатураТребуетПроверки = Ложь;
			
			ДанныеРеквизита = ДанныеРеквизита("Номенклатура", ИмяТЧТовары, ДанныеНоменклатуры.ИдентификаторСтроки);
			ДанныеРеквизита.Проверен = Истина;
			
			ОбновитьПризнакВерификацииРеквизитаТаблицы(ДанныеРеквизита, Истина);
			
			Если Не ЗначениеЗаполнено(СтрокаТовары.ЕдиницаИзмерения) Тогда
				
				СтрокаТовары.ЕдиницаИзмерения = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
					НоменклатураИБ.Номенклатура, "ЕдиницаИзмерения");
				
				СтрокаТовары.ЕдиницаИзмеренияТребуетПроверки = Ложь;
				
				ДанныеРеквизита = 
					ДанныеРеквизита("ЕдиницаИзмерения", ИмяТЧТовары, ДанныеНоменклатуры.ИдентификаторСтроки);
				
				Если ДанныеРеквизита <> Неопределено Тогда
					ДанныеРеквизита.Проверен = Истина;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	УстановитьСостояниеПроверки();
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеИнтерактивногоСозданияНоменклатуры(Результат, ПараметрыСоздания) Экспорт
	
	СсылкаНаОбъект = Неопределено;
	
	Если ПараметрыСоздания.Свойство("СсылкаНаОбъект", СсылкаНаОбъект) = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
		Возврат;
	КонецЕсли;

	ДанныеРеквизита = Неопределено;
	
	Если ПараметрыСоздания.Свойство("ДанныеРеквизита", ДанныеРеквизита) = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеРеквизита.СозданВФорме = Истина;
	
	СтрокаТЧ = РаспознаваниеДокументовКлиентСервер.СтрокаПоИдентификатору(
		ЭтотОбъект, "Товары", ДанныеРеквизита.Идентификатор);
	
	СтрокаТЧ[ДанныеРеквизита.ИмяРеквизита] = СсылкаНаОбъект;
	
	ОбновитьПризнакВерификацииРеквизитаТаблицы(ДанныеРеквизита, Истина);
	УстановитьСостояниеПроверки();
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область Подключаемые

&НаКлиенте
Процедура Подключаемый_ПриИзмененииПоля(Элемент)
	
	Если Элемент.Имя = "Организация" Тогда
		ПриИзмененииОрганизации();
	КонецЕсли;
	
	ОбновитьПризнакВерификацииПоля(Элемент);
	УстановитьСостояниеПроверки();
	УстановитьВидимостьЭлементов();
	ПриИзмененииЭлементаФормы(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АвтоПодборПоля(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, 
			СтандартнаяОбработка)
	
	ИмяРеквизита = Элемент.Имя;
	
	Отбор = Новый Структура("ИмяРеквизита", ИмяРеквизита);
	
	СтрокиРеквизита = ВерифицируемыеРеквизитыШапки.НайтиСтроки(Отбор);
	
	Если СтрокиРеквизита.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Реквизит = СтрокиРеквизита[0];
	
	КоординатыКартинки = РаспознаваниеДокументовСлужебныйКлиентСервер.ПолучитьНаборКоординат(Реквизит);
	
	ПриблизитьПоКоординатам(КоординатыКартинки, Реквизит.СтрокВИзображении);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриАктивизацииПоля(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ИмяРеквизита = Элемент.Имя;
	
	Отбор = Новый Структура("ИмяРеквизита", ИмяРеквизита);
	
	СтрокиРеквизита = ВерифицируемыеРеквизитыШапки.НайтиСтроки(Отбор);
	
	Если СтрокиРеквизита.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Реквизит = СтрокиРеквизита[0];
	
	КоординатыКартинки = РаспознаваниеДокументовСлужебныйКлиентСервер.ПолучитьНаборКоординат(Реквизит);
	
	ПриблизитьПоКоординатам(КоординатыКартинки, Реквизит.СтрокВИзображении);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриблизитьПоКоординатам()
	ПриблизитьПоКоординатам(ДанныеПриближения.Координаты, ДанныеПриближения.СтрокВИзображении);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииКолонки(Элемент)
	
	ТекущиеДанные = ТекущийЭлемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРеквизитаТЧ = СтрЗаменить(Элемент.Имя, ТекущийЭлемент.Имя, "");
	
	Если ЗначениеЗаполнено(ТекущиеДанные[ИмяРеквизитаТЧ]) Тогда
		
		ОбновитьПризнакВерификацииЯчейкиТаблицы(Элемент);
		
	КонецЕсли;
	
	ИменаКолонокПересчетаСумм = ИменаКолонокПересчетаСуммТЧ();
	
	Если ИменаКолонокПересчетаСумм.Найти(ИмяРеквизитаТЧ) <> Неопределено Тогда
		
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
		ДелопроизводствоКлиентСервер.ПересчитатьСуммуВСтрокеТЧ(ТекущиеДанные, ТекущиеДанные.СтавкаНДС);
		
	КонецЕсли;
	
	Если ВидДокументаКэш.ВестиУчетПоОрганизациям И ТекущиеДанные.ПорядокСтроки = 1 Тогда
		
		ТекущиеДанные = Элементы.Стороны.ТекущиеДанные;
		Если ТипЗнч(ТекущиеДанные.Сторона) = Тип("СправочникСсылка.Организации") Тогда
			Организация = ТекущиеДанные.Сторона;
			ПриИзмененииОрганизации();
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьВидимостьЭлементов();
	УстановитьСостояниеПроверки();
	ПриИзмененииЭлементаФормы(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаВыбораПоля(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Строка") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПриИзмененииЭлементаФормы(Элемент);
	ЭтоТаблица = ТипЗнч(ТекущийЭлемент) = Тип("ТаблицаФормы");
	ТаблицаБезАналога = Ложь;
	
	Если ЭтоТаблица Тогда
		
		ДанныеРеквизита = ДанныеРеквизитаТЧПоЭлементу(ТекущийЭлемент.ТекущийЭлемент);
		ТаблицаБезАналога = ТаблицаБезАналогаВРаспознанномДокументе(ДанныеРеквизита.ТабличнаяЧасть);
		
	Иначе
		ДанныеРеквизита = ДанныеРеквизитаШапкиПоЭлементу(ТекущийЭлемент);
	КонецЕсли;
	
	ИмяРаспознанногоРеквизита = ДанныеРеквизита.ИмяРаспознанногоРеквизита;
	ИмяРеквизита = ДанныеРеквизита.ИмяРеквизита;
	
	Если СтрНайти(ИмяРаспознанногоРеквизита, "Организация") > 0 
		ИЛИ ТипыБезПравНаСоздание.НайтиПоЗначению(ТипЗнч(ДанныеРеквизита.РаспознанноеЗначение)) <> Неопределено Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Возврат;
		
	КонецЕсли;
	
	Если ЭтоТаблица И ТаблицаБезАналога = Ложь Тогда
		
		Ключ = РаспознаваниеДокументовСлужебныйКлиентСервер.
			КлючСвойстваЯчеекТаблицы(ИмяРеквизита, ДанныеРеквизита.НомерСтрокиТаблицы);
		
		Свойства = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СвойстваЯчеекТаблиц, Ключ);
		
		Значение = Свойства.Значение;
		
	Иначе
		
		Отбор = Новый Структура("ИмяРаспознанногоРеквизита", ИмяРаспознанногоРеквизита);
		
		ПодходящиеСтроки = ВерифицируемыеРеквизитыШапки.НайтиСтроки(Отбор);
		
		Если ПодходящиеСтроки.Количество() > 0 Тогда
			Значение = ПодходящиеСтроки[0].РаспознанноеЗначение;
		Иначе
			Значение = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	ТипРеквизита = ТипЗнч(Значение);
	
	Если РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(ТипРеквизита) Тогда
		
		Если ТипРеквизита = Тип("Строка") Тогда
			ВыбранноеЗначение = СокрЛП(ВыбранноеЗначение);
		Иначе
			ВыбранноеЗначение = РаспознаваниеДокументовСериализацияСлужебныйКлиентСервер.ПривестиТип(
				ВыбранноеЗначение, ТипРеквизита);
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипРеквизита = Тип("СправочникСсылка.Контрагенты") Тогда
		
		ПараметрыЗаполнения = Новый Структура;
		
		ДанныеЗаполнения = ДанныеЗаполненияПоКлючевымРеквизитам(
			ДанныеРеквизита.ИмяРаспознанногоРеквизита, ВерифицируемыеРеквизитыШапки);
			
		Если Не ПустаяСтрока(ДанныеЗаполнения.ИНН) Тогда
			ПараметрыЗаполнения.Вставить("ТекстЗаполнения", ДанныеЗаполнения.ИНН);
		КонецЕсли;
		
		ЗначенияЗаполнения = Новый Структура("Наименование", ДанныеРеквизита.РаспознанныйТекст);
		ПараметрыЗаполнения.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ДанныеРеквизита", ДанныеРеквизита);
		
		ОписаниеОповещенияОСозданииКонтрагента = 
			Новый ОписаниеОповещения("ПослеИнтерактивногоСозданияКонтрагента", ЭтотОбъект, ПараметрыОповещения);
			
		ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаЭлемента", ПараметрыЗаполнения, Элемент, , , , 
			ОписаниеОповещенияОСозданииКонтрагента, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	ИначеЕсли ТипРеквизита = Тип("СправочникСсылка.Номенклатура") Тогда
		
		ПорогУверенности = ПорогУверенностиПоУмолчанию();
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("Наименование", ДанныеРеквизита.РаспознанныйТекст);
		
		ДанныеАртикул = ДанныеРеквизита("Артикул", ТекущийЭлемент.Имя, ДанныеРеквизита.Идентификатор);
		ДанныеЕдиница = ДанныеРеквизита("ЕдиницаИзмерения", ТекущийЭлемент.Имя, ДанныеРеквизита.Идентификатор);
		ДанныеСтавкаНДС = ДанныеРеквизита("СтавкаНДС", ТекущийЭлемент.Имя, ДанныеРеквизита.Идентификатор);
		ДанныеЦена = ДанныеРеквизита("Цена", ТекущийЭлемент.Имя, ДанныеРеквизита.Идентификатор);
		
		Если ДанныеАртикул <> Неопределено Тогда
			ПараметрыЗаполнения.Вставить("Артикул", ДанныеАртикул.РаспознанноеЗначение);
		КонецЕсли;
		
		Если ДанныеЕдиница <> Неопределено И ДанныеЕдиница.УверенностьРаспознанногоЗначения = ПорогУверенности Тогда
			ПараметрыЗаполнения.Вставить("ЕдиницаИзмерения", ДанныеЕдиница.РаспознанноеЗначение);
		КонецЕсли;
		
		Если ДанныеСтавкаНДС <> Неопределено И ДанныеСтавкаНДС.УверенностьРаспознанногоЗначения = ПорогУверенности Тогда
			ПараметрыЗаполнения.Вставить("СтавкаНДС", ДанныеСтавкаНДС.РаспознанноеЗначение);
		КонецЕсли;
		
		Если ДанныеЦена <> Неопределено Тогда
			ПараметрыЗаполнения.Вставить("Цена", ДанныеЦена.РаспознанноеЗначение);
		КонецЕсли;
		
		ПараметрыОткрытия = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
	
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ДанныеРеквизита", ДанныеРеквизита);
			
		ОписаниеОповещенияОСозданииНоменклатуры = Новый ОписаниеОповещения(
			"ПослеИнтерактивногоСозданияНоменклатуры", ЭтотОбъект, ПараметрыОповещения);
		
		ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта", ПараметрыОткрытия, Элемент, , , , 
			ОписаниеОповещенияОСозданииНоменклатуры, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Верификация

&НаКлиенте
Процедура ОбновитьПризнакВерификацииПоля(Элемент)
	
	Модифицированность = Истина;
	
	ДанныеРеквизита = ДанныеРеквизитаШапкиПоЭлементу(Элемент);
		
	Если ДанныеРеквизита = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьПризнакВерификацииРеквизита(ДанныеРеквизита);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПризнакВерификацииРеквизита(ДанныеРеквизита, СозданИнтерактивно = Ложь)
	
	Если ДанныеРеквизита = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеРеквизита.ТребуетПроверки = Истина Тогда
		ДанныеРеквизита.Проверен = Истина;
	КонецЕсли;
	
	ДанныеРеквизита.УстановленноеЗначение = ЭтотОбъект[ДанныеРеквизита.ИмяРеквизита];
	ДанныеРеквизита.ПодобранноеЗначение = ЭтотОбъект[ДанныеРеквизита.ИмяРеквизита];
	ДанныеРеквизита.СозданВФорме = СозданИнтерактивно;
	
	УстановитьФонПоля(Элементы[ДанныеРеквизита.ИмяРеквизита], ДанныеРеквизита);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПризнакВерификацииЯчейкиТаблицы(Элемент) Экспорт
	
	Модифицированность = Истина;
	
	ДанныеРеквизита = ДанныеРеквизитаТЧПоЭлементу(Элемент);
	
	Если ДанныеРеквизита = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьПризнакВерификацииРеквизитаТаблицы(ДанныеРеквизита);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПризнакВерификацииРеквизитаТаблицы(ДанныеРеквизита, СозданИнтерактивно = Ложь)
	
	ДанныеСтроки = РаспознаваниеДокументовКлиентСервер.СтрокаПоИдентификатору(
		ЭтотОбъект, ДанныеРеквизита.ТабличнаяЧасть, ДанныеРеквизита.Идентификатор);
		
	ИмяКолонкиТребуетВерификации = ДанныеРеквизита.ИмяРеквизита + "ТребуетПроверки";
	
	ДанныеРеквизита.Проверен = Истина;
	ДанныеРеквизита.УстановленноеЗначение = ДанныеСтроки[ДанныеРеквизита.ИмяРеквизита];
	ДанныеРеквизита.ПодобранноеЗначение = ДанныеСтроки[ДанныеРеквизита.ИмяРеквизита];
	ДанныеРеквизита.СозданВФорме = СозданИнтерактивно;
	
	Если ДанныеСтроки.Свойство(ИмяКолонкиТребуетВерификации) Тогда
		ДанныеСтроки[ИмяКолонкиТребуетВерификации] = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеВерификацииПриСоздании()
	
	ДанныеВерификации = Неопределено;
	
	Если ОткрытиеВРежимеПросмотраИстории = Истина Тогда
		РаспознаваниеДокументовСлужебный.ЗаполнитьВерифицируемыеРеквизитыПриОткрытииФормы(ЭтотОбъект, Истина);
	Иначе
		ДанныеВерификации = ДанныеЗаполненияПриСоздании.ДанныеВерификации;
	КонецЕсли;
	
	Если ДанныеВерификации <> Неопределено Тогда
		
		ВерифицируемыеРеквизитыШапки.Загрузить(ДанныеВерификации.ВерифицируемыеРеквизитыШапки.Выгрузить());
		ВерифицируемыеРеквизитыТЧ.Загрузить(ДанныеВерификации.ВерифицируемыеРеквизитыТЧ.Выгрузить());
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ИсторияВерификации()
	
	СтрокаСМаксДатойТЧ = Неопределено;
	СтрокаСМаксДатойШапка = Неопределено;
	МаксДатаТЧ = Дата(1,1,1);
	МаксДатаШапки = Дата(1,1,1);
	
	Если ВерифицируемыеРеквизитыТЧ.Количество() > 0 Тогда
		
		ВерифицируемыеРеквизитыТЧ.Сортировать("ДатаПроверки Убыв");
		СтрокаСМаксДатойТЧ = ВерифицируемыеРеквизитыТЧ[0];
		
	КонецЕсли;
	
	Если ВерифицируемыеРеквизитыШапки.Количество() > 0 Тогда
		
		ВерифицируемыеРеквизитыШапки.Сортировать("ДатаПроверки Убыв");
		СтрокаСМаксДатойШапка = ВерифицируемыеРеквизитыШапки[0];
		
	КонецЕсли;
	
	Если СтрокаСМаксДатойТЧ <> Неопределено И ЗначениеЗаполнено(СтрокаСМаксДатойТЧ.ДатаПроверки) Тогда
		МаксДатаТЧ = СтрокаСМаксДатойТЧ.ДатаПроверки;
	КонецЕсли;
	
	Если СтрокаСМаксДатойШапка <> Неопределено И ЗначениеЗаполнено(СтрокаСМаксДатойШапка.ДатаПроверки) Тогда
		МаксДатаШапки = СтрокаСМаксДатойШапка.ДатаПроверки;
	КонецЕсли;
	
	МаксДата = Макс(МаксДатаТЧ, МаксДатаШапки);
	
	Если ЗначениеЗаполнено(МаксДата) Тогда
		
		Если МаксДата = МаксДатаТЧ Тогда
			СтрокаДанными = СтрокаСМаксДатойТЧ;
		Иначе
			СтрокаДанными = СтрокаСМаксДатойШапка;
		КонецЕсли;
		
		Возврат Новый Структура("ДатаПроверки, Ответственный", СтрокаДанными.ДатаПроверки, СтрокаДанными.Ответственный);
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура СформироватьЗаголовок()
	
	КоличествоТребующихПроверки = 
		РаспознаваниеДокументовСлужебныйКлиентСервер.КоличествоРеквизитовДляВерификации(ЭтотОбъект);
		
	Если КоличествоТребующихПроверки = 0 Тогда
		ТекстЗаголовка = НСтр("ru ='Документ распознан, верификация не требуется (%1)'");
	Иначе
		ТекстЗаголовка = НСтр("ru ='Документ распознан, необходимо верифицировать реквизиты (%1)'");
	КонецЕсли;
	
	Заголовок = СтрШаблон(ТекстЗаголовка, Документ);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИзСписковВыбора()
	
	// При закрытии без сохранения выбранные элементы добавляются в списки выбора на 1 место
	// С порогом уверенности верификации
	СвойстваЯчеекТаблиц = ПолучитьИзВременногоХранилища(АдресСвойствЯчеекТаблиц);
	ДанныеВыбораРеквизитов = ПолучитьИзВременногоХранилища(АдресЗначенияВыбораДляШапки);
	
	Если СвойстваЯчеекТаблиц <> Неопределено Тогда

		Для i = 1 По Товары.Количество() Цикл
			
			Если ЗначениеЗаполнено(Товары[i - 1].Номенклатура) Тогда
				Продолжить;
			КонецЕсли;
			
			Ключ = "Номенклатура_"+Строка(i);
			
			Если Не СвойстваЯчеекТаблиц.Свойство(Ключ) Тогда
				Продолжить;
			КонецЕсли;
			
			СвойстваЯчейки = СвойстваЯчеекТаблиц[Ключ];
			
			Для Каждого ЗначениеВыбора Из СвойстваЯчейки.ЗначенияВыбора Цикл
				
				Если ЗначениеВыбора.СозданВФорме = Истина Тогда
					Товары[i - 1].Номенклатура = ЗначениеВыбора.Значение;
				КонецЕсли;
				
				Прервать;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ДанныеВыбораРеквизитов <> Неопределено Тогда
		
		Для Каждого ДанныеВыбораРеквизита Из ДанныеВыбораРеквизитов Цикл
			
			Если ДанныеВыбораРеквизита.ПорядковыйНомер = 1 Тогда
				
				Если ДанныеВыбораРеквизита.СозданВформе = Истина
					Тогда
						
						Отбор = Новый Структура;
						Отбор.Вставить("ИмяРаспознанногоРеквизита", ДанныеВыбораРеквизита.ИмяРеквизита);
						НСтр = ВерифицируемыеРеквизитыШапки.НайтиСтроки(Отбор);
						
						Если НСтр.Количество() > 0 Тогда
							
							ДанныеРеквизита = НСтр[0];
							
							ИмяРеквизитаФормы = ДанныеРеквизита.ИмяРеквизита;
							
							Если Не ПустаяСтрока(ИмяРеквизитаФормы) Тогда
							
								Если Не ЗначениеЗаполнено(ЭтотОбъект[ИмяРеквизитаФормы]) Тогда
									
									ЭтотОбъект[ИмяРеквизитаФормы] = ДанныеВыбораРеквизита.Значение;
									
									ДанныеРеквизита.УстановленноеЗначение = ДанныеВыбораРеквизита.Значение;
									
								КонецЕсли;
							
							КонецЕсли;
							
						КонецЕсли;
						
						ТЧБезАналогов = Новый Массив;
						ТЧБезАналогов.Добавить("Стороны");
						ТЧБезАналогов.Добавить("Контрагенты");
						
						Для Каждого ИмяТЧ Из ТЧБезАналогов Цикл
						
							Отбор = Новый Структура;
							Отбор.Вставить("ИмяРаспознанногоРеквизита", ДанныеВыбораРеквизита.ИмяРеквизита);
							Отбор.Вставить("ТабличнаяЧасть", ИмяТЧ);
							Отбор.Вставить("ИмяТаблицы", "");
							НСтр = ВерифицируемыеРеквизитыТЧ.НайтиСтроки(Отбор);
							
							Если НСтр.Количество() > 0 Тогда
								Для Каждого ДанныеРеквизита Из НСтр Цикл
									
									ИмяРеквизитаТЧ = ДанныеРеквизита.ИмяРеквизита;
									Идентификатор = ДанныеРеквизита.Идентификатор;
									
									Если Не ЗначениеЗаполнено(Идентификатор) Тогда
										Продолжить;
									КонецЕсли;
									
									ОтборТЧ = Новый Структура("Идентификатор", Идентификатор);
									НСтрТЧ = Стороны.НайтиСтроки(ОтборТЧ);
									Если НСтрТЧ.Количество() > 0 Тогда
										
										СтрокаСДанными = НСтрТЧ[0];
										
										Если ЗначениеЗаполнено(СтрокаСДанными[ИмяРеквизитаТЧ]) Тогда
											Продолжить;
										Иначе
											
											СтрокаСДанными[ИмяРеквизитаТЧ] = ДанныеВыбораРеквизита.Значение;
											ДанныеРеквизита.УстановленноеЗначение = ДанныеВыбораРеквизита.Значение;
											
										КонецЕсли;
										
									КонецЕсли;
									
								КонецЦикла;
								
							КонецЕсли;
							
						КонецЦикла;
						
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти