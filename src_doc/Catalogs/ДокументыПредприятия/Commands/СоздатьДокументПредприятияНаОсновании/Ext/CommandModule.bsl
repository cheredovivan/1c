
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если Не ЗначениеЗаполнено(ПараметрКоманды) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Команда не может быть выполнена для указанного объекта!'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ПараметрыВыполненияКоманды", ПараметрыВыполненияКоманды);
	ПараметрыОповещения.Вставить("ПараметрКоманды", ПараметрКоманды);
	РежимОткрытияФормы = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
	ОповещениеОЗакрытииФормыСозданиеДокументаПоШаблону = Новый ОписаниеОповещения(
		"СозданиеВнутреннегоДокументаНаОсновании", ЭтотОбъект, ПараметрыОповещения);

	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТипШаблонаДокумента",  "ШаблоныДокументов");
	ПараметрыФормы.Вставить("ВозможностьСозданияПустогоДокумента", Ложь);
	
	ОткрытьФорму(
		"ОбщаяФорма.СозданиеДокументаПоШаблону",
		ПараметрыФормы,,,,,
		ОповещениеОЗакрытииФормыСозданиеДокументаПоШаблону,
		РежимОткрытияФормы);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СозданиеВнутреннегоДокументаНаОсновании(Результат, Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Или Результат = "ПрерватьОперацию" Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = ДелопроизводствоКлиент.ПараметрыФормыДляСозданияДокумента(
		Результат, Параметры.ПараметрКоманды);
	
	Открытьформу("Справочник.ДокументыПредприятия.ФормаОбъекта", ПараметрыФормы, Параметры.ПараметрыВыполненияКоманды.Источник);
	
КонецПроцедуры

#Конецобласти
