#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ДатаСоздания = ТекущаяДатаСеанса();
	Ответственный = Сотрудники.ОсновнойСотрудник();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоНовый() Тогда
		
		РеквизитыПапкиПоСсылке = 
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ПометкаУдаления, Родитель, Наименование");
		ПредыдущаяПометкаУдаления = РеквизитыПапкиПоСсылке.ПометкаУдаления;
		
		Если Не ПараметрыСеанса.ЗагрузкаОбработанныхДанныхИзДругойСистемы
				И ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда 
			
			Если НЕ ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(Ссылка).Удаление Тогда
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									 НСтр("ru = 'У вас нет права ""Пометка на удаление"" папки ""%1"".'"),
									 Строка(Ссылка));
			КонецЕсли;
			
			Запрос = Новый Запрос(
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	Мероприятия.Ссылка,
				|	Мероприятия.ПометкаУдаления
				|ИЗ
				|	Справочник.Мероприятия КАК Мероприятия
				|ГДЕ
				|	Мероприятия.Папка = &Ссылка");
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Если ПометкаУдаления <> Выборка.ПометкаУдаления Тогда 
					МероприятиеОбъект = Выборка.Ссылка.ПолучитьОбъект();
					МероприятиеОбъект.Заблокировать();
					МероприятиеОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли