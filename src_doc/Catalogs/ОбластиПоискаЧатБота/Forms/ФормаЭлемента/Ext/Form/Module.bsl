
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Проверить(Команда)

	Если Не ЗначениеЗаполнено(Объект.Скрипт) Тогда
	
		ТекстСообщения = НСтр("ru = 'Не введен исполняемый код'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Скрипт");
		Возврат;
	
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьПродолжение", ЭтаФорма);
	ОткрытьФорму("Справочник.ОбластиПоискаЧатБота.Форма.ВводТекстаСообщения",,,,,,
				ОписаниеОповещения,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	Наименование = "Поиск сотрудника по имени и фамилии";
	ТекстЗапроса = 	"ВЫБРАТЬ ПЕРВЫЕ 5
	|	Сотрудники.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Наименование ПОДОБНО &Наименование";
	
	ТекстНайденСотрудник = НСтр("ru = 'Найден сотрудник:'");
	ТекстНайденыСотрудники = НСтр("ru = 'Найдены сотрудники:'");
	
	ТекстЗаполнения = 
	"//Код для поиска сотрудников по Имени и Фамилии
	|//Подготовим текст сообщения к обработке
	|СтрокаПоиска = ""%"" + Строка(ТекстСообщения);
	|Если СтрНайти(СтрокаПоиска, "" "") > 1 Тогда 
	|	//начало фамилии + начало имени
	|	СтрокаПоиска = СтрЗаменить(СтрокаПоиска, "" "", ""% "");
	|КонецЕсли;
	|
	|//Делаем запрос к области БД
	|Запрос = Новый Запрос();
	|Запрос.Параметры.Вставить(""Наименование"", СтрокаПоиска + ""%"");
	|Запрос.Текст = """ + ТекстЗапроса + """;
	|Выборка = Запрос.Выполнить();
	|
	|//Обрабатываем результат запроса
	|//Помещаем результат поиска в переменную Результат
	|Если Не Выборка.Пустой() Тогда
	|	
	|	Выборка = Выборка.Выгрузить();
	|
	|	Если Выборка.Количество() = 1 Тогда
	|
	|		Результат = """ + ТекстНайденСотрудник + """;
	|
	|	Иначе
	|	
	|		Результат = """ + ТекстНайденыСотрудники + """;
	|	
	|	КонецЕсли; 
	|
	|	Для Каждого Сотрудник Из Выборка Цикл
	|	
	|		Результат = Результат 
	|						+ Символы.ПС 
	|						+ ПолучитьНавигационнуюСсылку(Сотрудник.Ссылка);
	|	
	|	КонецЦикла;
	|
	|КонецЕсли;";
	
	Объект.Наименование = Наименование;
	Объект.Скрипт = ТекстЗаполнения;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверитьПродолжение(ТекстСообщения, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ТекстСообщения) Тогда
	
		Возврат;
	
	КонецЕсли;
	
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(Объект.Скрипт) Тогда
		
		ТекстОшибки = НСтр("ru = 'Скрипт не заполнен'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Скрипт", "Объект.Скрипт");
		Возврат;
	
	КонецЕсли;
	
	Если Модифицированность Тогда
	
		Записать();
	
	КонецЕсли;
	
	ПроверитьНаСервере(ТекстСообщения);
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНаСервере(ТекстСообщения)
	
	Попытка
		
		Сообщить(РезультатПоискаПоОбласти(ТекстСообщения));
		
	Исключение
		
		ПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеПользователю = Нстр("ru = 'При выполнении кода возникли ошибки:'") 
			+ Символы.ПС
			+ ПредставлениеОшибки;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеПользователю);
	
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция РезультатПоискаПоОбласти(Знач ТекстСообщения)

	Результат = Неопределено;
	
	Попытка
		
		Выполнить(Объект.Скрипт);
	
	Исключение
		
		Результат = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	
	КонецПопытки;
	
	Возврат Результат;

КонецФункции

#КонецОбласти
