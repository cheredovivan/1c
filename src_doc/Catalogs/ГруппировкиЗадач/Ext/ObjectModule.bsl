#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Обновляет наименование группировки задач.
// 
// Возвращаемое значение:
//  Булево - Наименование обновлено.
// 
Функция ОбновитьНаименование() Экспорт
	
	НаименованиеОбновлено = Ложь; 
	
	Если Предопределенный Тогда
		Возврат НаименованиеОбновлено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Автор) И (ТипЗнч(Автор) = Тип("СправочникСсылка.Сотрудники") 
		Или ТипЗнч(Автор) = Тип("СправочникСсылка.ПолныеРоли")) Тогда
		НовоеНаименование = РаботаСЗадачамиПовтИсп.ПредставлениеУчастника(Автор, Истина);
		ОбновитьНаименованиеСУчетомМногоязычности(Автор, НовоеНаименование);
		
	ИначеЕсли БезПроекта Тогда
		НовоеНаименование = НСтр("ru = 'Без проекта'");
		ОбновитьНаименованиеСУчетомМногоязычности("ГруппировкаБезПроекта", НовоеНаименование);
		
	ИначеЕсли БезФлага Тогда
		НовоеНаименование = НСтр("ru = 'Без флага'");
		ОбновитьНаименованиеСУчетомМногоязычности("ГруппировкаБезФлага", НовоеНаименование);
		
	ИначеЕсли ЗначениеЗаполнено(ВидДействия) Тогда
		НовоеНаименование = Строка(ВидДействия); 
		ОбновитьНаименованиеСУчетомМногоязычности(ВидДействия, НовоеНаименование);

	ИначеЕсли ЗначениеЗаполнено(ВидЗадачи) Тогда
		НовоеНаименование = Строка(ВидЗадачи);
		ОбновитьНаименованиеСУчетомМногоязычности(ВидЗадачи, НовоеНаименование);
		
	ИначеЕсли ЗначениеЗаполнено(ВидПриложения) Тогда
		НовоеНаименование = Строка(ВидПриложения);
		ОбновитьНаименованиеСУчетомМногоязычности(ВидПриложения, НовоеНаименование);

	ИначеЕсли ЗначениеЗаполнено(Исполнитель) И ТипЗнч(Исполнитель) = Тип("СправочникСсылка.Сотрудники") Тогда
		НовоеНаименование = РаботаСЗадачамиПовтИсп.ПредставлениеУчастника(Исполнитель, Истина);
		ОбновитьНаименованиеСУчетомМногоязычности(Исполнитель, НовоеНаименование);

	ИначеЕсли ЗначениеЗаполнено(Исполнитель) И ТипЗнч(Исполнитель) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
		НовоеНаименование = Строка(Исполнитель); 
		ОбновитьНаименованиеСУчетомМногоязычности(Исполнитель, НовоеНаименование);
	
	ИначеЕсли ЗначениеЗаполнено(Исполнитель) И ТипЗнч(Исполнитель) = Тип("СправочникСсылка.ПолныеРоли") Тогда
		НовоеНаименование = Строка(Исполнитель); 
		ОбновитьНаименованиеСУчетомМногоязычности(
				ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Исполнитель, "Владелец").Владелец, НовоеНаименование);

	ИначеЕсли ЗначениеЗаполнено(Проект) И ТипЗнч(Проект) = Тип("СправочникСсылка.Проекты") Тогда
		НовоеНаименование = Строка(Проект);
		ОбновитьНаименованиеСУчетомМногоязычности(Проект, НовоеНаименование);

	ИначеЕсли ЗначениеЗаполнено(ТипПриложения) Тогда
		НовоеНаименование = Строка(ТипПриложения);
		ОбновитьНаименованиеСУчетомМногоязычности(ТипПриложения, НовоеНаименование);
		
	ИначеЕсли ЗначениеЗаполнено(Флаг) Тогда
		НовоеНаименование = Строка(Флаг);
		ОбновитьНаименованиеСУчетомМногоязычности(Флаг, НовоеНаименование);
		
	Иначе
		НовоеНаименование = НСтр("ru = '<Не указан>'"); 
		ОбновитьНаименованиеСУчетомМногоязычности("НеУказан", НовоеНаименование);

	КонецЕсли;
	
	Если СтрСравнить(Наименование, НовоеНаименование) <> 0 Тогда
		НаименованиеОбновлено = Истина;
		Наименование = НовоеНаименование;
	КонецЕсли;
	
	Если МультиязычностьСервер.ИспользуетсяДополнительныйЯзык(1) Тогда
		СтароеНаименованиеЯзык1 = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "НаименованиеЯзык1");
		Если СтрСравнить(НаименованиеЯзык1, СтароеНаименованиеЯзык1) <> 0 Тогда
			НаименованиеОбновлено = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НаименованиеОбновлено;

КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриЧтенииПредставленийНаСервере() Экспорт
	МультиязычностьСервер.ПриЧтенииПредставленийНаСервере(ЭтотОбъект);
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьНаименование();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует наименование группировки на основном и дополнительных языках конфигурации
// 
// Параметры:
//  Группировка - СправочникОбъект.ГруппировкиЗадач
//  НовоеНаименование - Строка - новое наименование группировки
Процедура ОбновитьНаименованиеСУчетомМногоязычности(Группировка, НовоеНаименование)
	
	ОсновнойЯзыкКонфигурации = МультиязычностьСервер.СведенияОЯзыках().ОсновнойЯзык;

	Если МультиязычностьПовтИсп.КонфигурацияИспользуетТолькоОдинЯзык(Ложь) Тогда
		Возврат;
	КонецЕсли;
		
	Если ТипЗнч(Группировка) = Тип("СправочникСсылка.Сотрудники") И ЗначениеЗаполнено(Группировка) Тогда
		НовоеНаименование = Справочники.Сотрудники.ПредставлениеСотрудникаСУчетомЯзыка(
			Группировка, ОсновнойЯзыкКонфигурации, Истина);
		Для Каждого Язык Из МультиязычностьСервер.СведенияОЯзыках() Цикл
			Если Язык.Значение <> "" И Язык.Значение <> ОсновнойЯзыкКонфигурации 
				И ТипЗнч(Язык.Значение) = Тип("Строка") Тогда
				НаименованиеДопЯзык = Справочники.Сотрудники.ПредставлениеСотрудникаСУчетомЯзыка(
					Группировка, Язык.Значение, Истина);
				ЭтотОбъект["Наименование" + Язык.Ключ] = НаименованиеДопЯзык;
			КонецЕсли;
		КонецЦикла;
	
	ИначеЕсли ТипЗнч(Группировка) = Тип("Строка") Тогда
		НовоеНаименование = НаименованиеГруппировкиИзСтроки(Группировка, ОсновнойЯзыкКонфигурации);
		Для Каждого Язык Из МультиязычностьСервер.СведенияОЯзыках() Цикл
			Если Язык.Значение <> "" И Язык.Значение <> ОсновнойЯзыкКонфигурации 
				И ТипЗнч(Язык.Значение) = Тип("Строка") Тогда
				НаименованиеДопЯзык = НаименованиеГруппировкиИзСтроки(Группировка, Язык.Значение);
				ЭтотОбъект["Наименование" + Язык.Ключ] = НаименованиеДопЯзык;
			КонецЕсли;
		КонецЦикла;	
	
	ИначеЕсли ОбщегоНазначения.ЭтоПеречисление(Группировка.Метаданные()) Тогда
		НовоеНаименование = НаименованиеГруппировкиИзПеречисления(Группировка, ОсновнойЯзыкКонфигурации);
		Для Каждого Язык Из МультиязычностьСервер.СведенияОЯзыках() Цикл
			Если Язык.Значение <> "" И Язык.Значение <> ОсновнойЯзыкКонфигурации 
				И ТипЗнч(Язык.Значение) = Тип("Строка") Тогда
				НаименованиеДопЯзык = НаименованиеГруппировкиИзПеречисления(Группировка, Язык.Значение);
				ЭтотОбъект["Наименование" + Язык.Ключ] = НаименованиеДопЯзык;
			КонецЕсли;
		КонецЦикла;
	
	Иначе
		Для Каждого Язык Из МультиязычностьСервер.СведенияОЯзыках() Цикл
			Если Язык.Значение <> "" И Язык.Значение = ОсновнойЯзыкКонфигурации Тогда
				НовоеНаименование = Группировка.Наименование;
			ИначеЕсли Не ТипЗнч(Язык.Значение) = Тип("Строка") Тогда
				Продолжить;
			Иначе	
				ЭтотОбъект["Наименование" + Язык.Ключ] = Группировка["Наименование" + Язык.Ключ];
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает строковый синоним значения перечисления на указанном языке
// Параметры:
//  Группировка - ПеречислениеСсылка
//  КодЯзыка - Строка - двухбуквенный код языка
// Возвращаемое значение:
//  Строка
Функция НаименованиеГруппировкиИзПеречисления(Группировка, КодЯзыка)
	
	Наименования = Новый Соответствие;
	Наименования[Перечисления.ТипыПриложенийЗадач.Документы] = НСтр("ru = 'Документы'", КодЯзыка);
	Наименования[Перечисления.ТипыПриложенийЗадач.БезПриложений] = НСтр("ru = 'Без приложений'", КодЯзыка);
	Наименования[Перечисления.ТипыПриложенийЗадач.Брони] = НСтр("ru = 'Брони'", КодЯзыка);
	Наименования[Перечисления.ТипыПриложенийЗадач.Контрагенты] = НСтр("ru = 'Контрагенты'", КодЯзыка);
	Наименования[Перечисления.ТипыПриложенийЗадач.Мероприятия] = НСтр("ru = 'Мероприятия'", КодЯзыка);
	Наименования[Перечисления.ТипыПриложенийЗадач.НесколькоПриложений] = НСтр("ru = 'Несколько приложений'", КодЯзыка);
	Наименования[Перечисления.ТипыПриложенийЗадач.Письма] = НСтр("ru = 'Письма'", КодЯзыка);
	Наименования[Перечисления.ТипыПриложенийЗадач.ПроектныеЗадачи] = НСтр("ru = 'Проектные задачи'", КодЯзыка);
	Наименования[Перечисления.ТипыПриложенийЗадач.Проекты] = НСтр("ru = 'Проекты'", КодЯзыка);
	Наименования[Перечисления.ТипыПриложенийЗадач.ПротоколыМероприятий] = НСтр("ru = 'Протоколы мероприятий'", КодЯзыка);
	Наименования[Перечисления.ТипыПриложенийЗадач.СообщенияФорума] = НСтр("ru = 'Сообщения форума'", КодЯзыка);
	Наименования[Перечисления.ТипыПриложенийЗадач.Сотрудники] = НСтр("ru = 'Сотрудники'", КодЯзыка);
	Наименования[Перечисления.ТипыПриложенийЗадач.ТемыФорума] = НСтр("ru = 'Темы форума'", КодЯзыка);
	Наименования[Перечисления.ТипыПриложенийЗадач.Файлы] = НСтр("ru = 'Файлы'", КодЯзыка);
	Наименования[Перечисления.ФлагиОбъектов.Красный] = НСтр("ru = 'Красный флаг'", КодЯзыка);
	Наименования[Перечисления.ФлагиОбъектов.Синий] = НСтр("ru = 'Синий флаг'", КодЯзыка);
	Наименования[Перечисления.ФлагиОбъектов.Желтый] = НСтр("ru = 'Желтый флаг'", КодЯзыка);
	Наименования[Перечисления.ФлагиОбъектов.Зеленый] = НСтр("ru = 'Зеленый флаг'", КодЯзыка);
	Наименования[Перечисления.ФлагиОбъектов.Оранжевый] = НСтр("ru = 'Оранжевый флаг'", КодЯзыка);
	Наименования[Перечисления.ФлагиОбъектов.Лиловый] = НСтр("ru = 'Лиловый флаг'", КодЯзыка);
	Наименования[Перечисления.ОтборыПоУчастникамЗадач.ТекущийПользователь] = НСтр("ru = 'Текущий пользователь'", КодЯзыка);
	Наименования[Перечисления.ОтборыПоУчастникамЗадач.Подчиненные] = НСтр("ru = 'Подчиненные'", КодЯзыка);
	Наименования[Перечисления.ОтборыПоСтатусуЗадач.Все] = НСтр("ru = 'Все'", КодЯзыка);
	Наименования[Перечисления.ОтборыПоСтатусуЗадач.ВСрок] = НСтр("ru = 'В срок'", КодЯзыка);
	Наименования[Перечисления.ОтборыПоСтатусуЗадач.Истекающие] = НСтр("ru = 'Истекающие'", КодЯзыка);
	Наименования[Перечисления.ОтборыПоСтатусуЗадач.НеВыполненные] = НСтр("ru = 'Не выполненные'", КодЯзыка);
	Наименования[Перечисления.ОтборыПоСтатусуЗадач.НеНачатые] = НСтр("ru = 'Не начатые'", КодЯзыка);
	Наименования[Перечисления.ОтборыПоСтатусуЗадач.ПоступилиОбновления] = НСтр("ru = 'Поступили обновления'", КодЯзыка);
	Наименования[Перечисления.ОтборыПоСтатусуЗадач.Просроченные] = НСтр("ru = 'Просроченные'", КодЯзыка);
	Наименования[Перечисления.ТипыОтбораГруппировокЗадач.Задачи] = НСтр("ru = 'Задачи'", КодЯзыка);
	Наименования[Перечисления.ТипыОтбораГруппировокЗадач.ДействияЗадач] = НСтр("ru = 'Действия задач'", КодЯзыка);
	Наименования[Перечисления.ОтборыПоПроектамЗадач.АктивныеПроекты] = НСтр("ru = 'Активные проекты'", КодЯзыка);

	Возврат Наименования[Группировка];

КонецФункции  

// Возвращает строковое наименование группировки на указанном языке
// Параметры:
//  Группировка - Строка
//  КодЯзыка - Строка - двухбуквенный код языка
// Возвращаемое значение:
//  Строка
Функция НаименованиеГруппировкиИзСтроки(Группировка, КодЯзыка)
	
	Если Группировка = "ГруппировкаБезПроекта" Тогда
		НовоеНаименование = НСтр("ru = 'Без проекта'", КодЯзыка);
	ИначеЕсли Группировка = "ГруппировкаБезФлага" Тогда
		НовоеНаименование = НСтр("ru = 'Без флага'", КодЯзыка); 
	Иначе
		НовоеНаименование = НСтр("ru = 'Не указан'", КодЯзыка); 
	КонецЕсли;
	
	Возврат НовоеНаименование;
	
КонецФункции

#КонецОбласти
#КонецЕсли