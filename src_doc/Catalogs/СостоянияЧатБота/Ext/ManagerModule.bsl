#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограмныйИнтерфейс

// Структура для заполнения состояния чат-бота.
// 
// Возвращаемое значение:
//  Структура - Структура состояния чат-бота:
// * Высказывание - Строка - ответное высказывание бота
// * Действие - Строка - ссылка на действие
// * КлючевыеСлова - Строка - слова для поиска
// * Параметры - ТаблицаЗначений - для заполнения параметров состояния:
// ** ИмяПараметра - Строка - 
// ** Значение - ЛюбаяСсылка - 
// * ИмяЗаполняемогоПараметра - Строка - имя параметра
// * ПримерЗаполняемогоПараметра - Неопределено - пример использования
// * Используется - Булево - признак включения
Функция СтруктураСостоянияЧатБота() Экспорт

	ТаблицаПараметры = Новый ТаблицаЗначений();
	ТаблицаПараметры.Колонки.Добавить("ИмяПараметра");
	ТаблицаПараметры.Колонки.Добавить("Значение");
	
	СтруктураСостоянияЧатБота = Новый Структура();
	СтруктураСостоянияЧатБота.Вставить("Высказывание", "");
	СтруктураСостоянияЧатБота.Вставить("Действие", "");
	СтруктураСостоянияЧатБота.Вставить("КлючевыеСлова", "");
	СтруктураСостоянияЧатБота.Вставить("Параметры", ТаблицаПараметры);
	СтруктураСостоянияЧатБота.Вставить("ИмяЗаполняемогоПараметра", "");
	СтруктураСостоянияЧатБота.Вставить("ПримерЗаполняемогоПараметра", Неопределено);
	СтруктураСостоянияЧатБота.Вставить("Используется", Истина);
	
	Возврат СтруктураСостоянияЧатБота;

КонецФункции

// Заполняет состояние чат-бота "функционал"
// 
// Параметры:
//  РежимЗаписи - Булево - Режим записи
// 
Процедура ЗаполнитьСостояниеФункционал(РежимЗаписи = Ложь) Экспорт

	СтруктураЗаполнения = СтруктураСостоянияЧатБота();
	СтруктураЗаполнения.КлючевыеСлова = 
		НСтр("ru = 'Что
			|Умеешь 
			|Можешь 
			|Покажи 
			|Функционал
			|help
			|список команд
			|Возможности
			|Ася, что ты умеешь?'");
	Высказывание = НСтр("ru = 'Вот что я умею'");
	СтруктураЗаполнения.Действие = СтрШаблон(
		"Действия = РаботаСЧатБотомВызовСервера.ПодчиненныеСостояния(
		|ПараметрыПриложения[""СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота""]);
		|РаботаСЧатБотомВызовСервера.ОтправитьСообщение(ПользователиКлиентСервер.ТекущийПользователь(), ""%1"",, Действия);",
		Высказывание);
	
	Состояние = Функционал.ПолучитьОбъект();
	Состояние.Заполнить(СтруктураЗаполнения);
	Если РежимЗаписи = Истина Тогда 
		ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные(Состояние);
	Иначе 
		Состояние.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

