
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СсылкаНаНастройку = Параметры.СсылкаНаНастройку;
	
	Заголовок = СтрШаблон(НСтр("ru = 'Условия запуска (%1)'"), СсылкаНаНастройку);
	
	РеквизитыНастройки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаНастройку, 
		"ВидОбъекта, УсловияЗапуска");
	
	ТипОбъекта = ОбщегоНазначенияДокументооборотКлиентСервер.ТипОбъектаПоТипуВидаОбъекта(
		ТипЗнч(РеквизитыНастройки.ВидОбъекта));
	
	НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипОбъекта", ТипОбъекта);
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(НовыйПараметр);
	НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
	Элементы.УсловияЗапускаУсловие.ПараметрыВыбора = НовыеПараметры;
	
	УсловияЗапуска.Загрузить(РеквизитыНастройки.УсловияЗапуска.Выгрузить());	
	
	Если Параметры.Свойство("ТолькоПросмотр") Тогда
		ТолькоПросмотр = Параметры.ТолькоПросмотр;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если ОбщегоНазначенияДокументооборотКлиент.ПередЗакрытием(
		Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Модифицированность) Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность И Не ВопросЗадан Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПередЗакрытиемПродолжение",
			ЭтотОбъект);
		ПоказатьВопрос(
			ОписаниеОповещения,
			НСтр("ru = 'Данные были изменены. Сохранить изменения?'"), 
			РежимДиалогаВопрос.ДаНетОтмена,, 
			КодВозвратаДиалога.Отмена);
		Отказ = Истина;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемПродолжение(Ответ, Параметры) Экспорт
	
	ВопросЗадан = Истина;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Если НЕ ПроверитьЗаполнениеУсловий() Тогда
			ВопросЗадан = Ложь;
			Возврат;
		КонецЕсли;
		
		ЗаписатьНаСервере();
	ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
		ВопросЗадан = Ложь;
		Возврат;
	КонецЕсли;	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если НЕ ПроверитьЗаполнениеУсловий() Тогда
		Возврат;
	КонецЕсли;
	
	ВопросЗадан = Истина;
	
	ЗаписатьНаСервере();
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СобытияШапкиФормы

&НаКлиенте
Процедура УсловияЗапускаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьНаСервере()
	
	НачатьТранзакцию();
	Попытка
	
		НастройкаОбъект = СсылкаНаНастройку.ПолучитьОбъект();
		
		НастройкаОбъект.УсловияЗапуска.Очистить();
		Для Каждого Стр Из УсловияЗапуска Цикл
			НовСтр = НастройкаОбъект.УсловияЗапуска.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, Стр);
		КонецЦикла;		
		
		НастройкаОбъект.Записать();	
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
			
	КонецПопытки;		
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеУсловий()
	
	ПроверкаПройдена = Истина;
	
	ОчиститьСообщения();
	ОбщегоНазначенияДокументооборотКлиент.УдалитьПустыеСтрокиТаблицы(УсловияЗапуска, "Условие");
	ОбщегоНазначенияДокументооборотКлиент.УдалитьДублиСтрокТаблицы(УсловияЗапуска, "Условие");
	
	Для Каждого СтрУсловие Из УсловияЗапуска Цикл
		Если Не ЗначениеЗаполнено(СтрУсловие.ТекстПредупреждения) Тогда 
			ИндексСтроки = УсловияЗапуска.Индекс(СтрУсловие);
			НомерСтроки = Строка(ИндексСтроки + 1);
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Не заполнен текст предупреждения для условия %1'"), НомерСтроки);
			Поле = СтрШаблон("УсловияЗапуска[%1].ТекстПредупреждения", Формат(ИндексСтроки, "ЧН=; ЧГ="));
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки,, Поле);
			ПроверкаПройдена = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПроверкаПройдена;
	
КонецФункции

#КонецОбласти