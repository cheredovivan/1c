
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
	
	// Вычисление вида замещения, если выполнен переход по команде "Показать в списке".
	Если Не ЗначениеЗаполнено(Параметры.ВидЗамещения)
		И ЗначениеЗаполнено(Параметры.ТекущаяСтрока) Тогда
		Параметры.ВидЗамещения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Параметры.ТекущаяСтрока, "ВидЗамещения");
	КонецЕсли;
	
	// Заголовки.
	Если Параметры.ВидЗамещения = Перечисления.ВидыЗамещения.Замещающие Тогда
		Заголовок = НСтр("ru = 'Замещающие'");
		Элементы.Замещающий.Заголовок = НСтр("ru = 'Замещающий'");
		Элементы.ОтборЗамещающий.ПодсказкаВвода = НСтр("ru = 'Замещающий'");
		Элементы.Основание.Видимость = Истина;
		Элементы.ПустаяКолонка.Видимость = Ложь;
	ИначеЕсли Параметры.ВидЗамещения = Перечисления.ВидыЗамещения.Помощники Тогда
		Заголовок = НСтр("ru = 'Помощники'");
		Элементы.Замещающий.Заголовок = НСтр("ru = 'Помощник'");
		Элементы.ОтборЗамещающий.ПодсказкаВвода = НСтр("ru = 'Помощник'");
		Элементы.Основание.Видимость = Ложь;
		Элементы.ПустаяКолонка.Видимость = Истина;
	Иначе
		Заголовок = НСтр("ru = 'Замещающие и помощники'");
		Элементы.Замещающий.Заголовок = НСтр("ru = 'Замещающий/Помощник'");
		Элементы.ОтборЗамещающий.ПодсказкаВвода = НСтр("ru = 'Замещающий/Помощник'");
		Элементы.Основание.Видимость = Истина;
		Элементы.ПустаяКолонка.Видимость = Истина;
	КонецЕсли;
	
	Элементы.ОтборЗамещающий.Подсказка = Элементы.ОтборЗамещающий.ПодсказкаВвода;
	
	// Быстрый отбор по умолчанию.
	ОтборДата = ТекущаяДатаСеанса();
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, "ОтборДата", ОтборДата, ЗначениеЗаполнено(ОтборДата));
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(
		Элементы.ОтборДата, ОтборДата);
	Если Не Пользователи.ЭтоПолноправныйПользователь(,, Ложь)
		И ПравоДоступа("Изменение", Метаданные.Справочники.ЗамещающиеИПомощники) Тогда
		ОтборСотрудник = Сотрудники.ОсновнойСотрудник();
		ОбщегоНазначенияДокументооборотКлиентСервер.ИзменитьБыстрыйОтборСписка(
			Список, "Сотрудник", ОтборСотрудник, Элементы.ОтборСотрудник);
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	ОбщегоНазначенияДокументооборотКлиентСервер.УстановитьОтборПоказыватьУдаленные(
		Список, ПоказыватьУдаленные, Элементы.ПоказыватьУдаленные);	
	
	Если ЗначениеЗаполнено(Параметры.ВидЗамещения) Тогда
		Список.Параметры.УстановитьЗначениеПараметра("ВидЗамещения", Параметры.ВидЗамещения);
	КонецЕсли;
	Список.Параметры.УстановитьЗначениеПараметра("СтрокаПоВсем", НСтр("ru = 'По всем'"));
	Список.Параметры.УстановитьЗначениеПараметра("СтрокаВыборочно", НСтр("ru = 'Выборочно'"));
	Элементы.Список.Обновить();
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	МК_НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ОбщегоНазначенияДокументооборотКлиентСервер.УстановитьОтборПоказыватьУдаленные(
		Список, ПоказыватьУдаленные, Элементы.ПоказыватьУдаленные);	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборДатаПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, "ОтборДата", ОтборДата, ЗначениеЗаполнено(ОтборДата));
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(
		Элемент, ОтборДата);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСотрудникПриИзменении(Элемент)
	
	ОбщегоНазначенияДокументооборотКлиентСервер.ИзменитьБыстрыйОтборСписка(
		Список, "Сотрудник", ОтборСотрудник, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборЗамещающийПриИзменении(Элемент)
	
	ОбщегоНазначенияДокументооборотКлиентСервер.ИзменитьБыстрыйОтборСписка(
		Список, "Замещающий", ОтборЗамещающий, Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Если Не Копирование Тогда
		Отказ = Истина;
		ЗначенияЗаполнения = Новый Структура("ВидЗамещения", Параметры.ВидЗамещения);
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
		ОткрытьФорму("Справочник.ЗамещающиеИПомощники.ФормаОбъекта", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Для Каждого КлючИЗначение Из Строки Цикл
		ДанныеСтроки = КлючИЗначение.Значение.Данные;
		Если ДанныеСтроки.Свойство("ПериодСтрокой") Тогда
			ДанныеСтроки.ПериодСтрокой = ЗамещающиеИПомощники.ПериодЗамещенияСтрокой(
				ДанныеСтроки.ДатаНачала, ДанныеСтроки.ДатаОкончания);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	ОбщегоНазначенияДокументооборотКлиентСервер.УстановитьОтборПоказыватьУдаленные(
		Список, ПоказыватьУдаленные, Элементы.ПоказыватьУдаленные);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	Список.УсловноеОформление.Элементы.Очистить();
	Элемент = Список.УсловноеОформление.Элементы.Добавить();
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"Шрифт", Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста, , , , , , Истина));
	
КонецПроцедуры

&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	Если Не ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Список.Шапка = Ложь;
	Элементы.Переместить(Элементы.Создать, КоманднаяПанель);
	Элементы.Переместить(Элементы.Скопировать, КоманднаяПанель);
	
	Для Каждого ЭлементКомПанели Из Элементы.КомПанельФормы.ПодчиненныеЭлементы Цикл
		Элементы.Переместить(ЭлементКомПанели, КоманднаяПанель);
	КонецЦикла;
	
	Элементы.ГруппаБыстрыеОтборы.Видимость = Ложь;
	Элементы.МК_ГруппаБыстрыеОтборы.Видимость = Истина;

КонецПроцедуры

#КонецОбласти
