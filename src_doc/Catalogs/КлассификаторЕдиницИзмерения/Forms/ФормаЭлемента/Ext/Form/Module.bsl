#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
	
		МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, Объект);
	
	КонецЕсли;
	
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбщегоНазначенияДокументооборот.ПоказатьНадписьПометкиУдаления(ЭтотОбъект, ТекущийОбъект.ПометкаУдаления);
	
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	МультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Объект.Наименование = СокрЛП(Объект.Наименование);
	Объект.НаименованиеПолное = СокрЛП(Объект.НаименованиеПолное);
	
	Если (ПараметрыЗаписи.Свойство("СразуЗаписать") И ПараметрыЗаписи.СразуЗаписать)
		Или Объект.ПометкаУдаления Тогда
		// Обычная запись.
	Иначе
		// Перехват, чтоб сначала задать вопрос:
		Текст = СообщениеЕслиПустойКод();
		Если Текст = "" Тогда
			Текст = СообщениеЕслиЕстьДубли_Сервер();
		КонецЕсли;
		Если Текст = "" Тогда
			// Сразу запись.
		Иначе
			Отказ = Истина;
			Обработчик = Новый ОписаниеОповещения("ПроверкаПослеВопроса", ЭтотОбъект, Новый Структура());
			ПоказатьВопрос(Обработчик, Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И Не ЗавершениеРаботы И Не Объект.ПометкаУдаления Тогда
		
		Текст = СообщениеЕслиПустойКод();
		Если Текст = "" Тогда
			Текст = СообщениеЕслиЕстьДубли_Сервер();
		КонецЕсли;
		Если Текст = "" Тогда
			// Будет стандартное поведение платформы с вопросом про сохранение.
		Иначе
			// перехват, чтоб сначала задать вопрос про дубли:
			СтандартнаяОбработка = Ложь;
			Отказ = Истина;
			Обработчик = Новый ОписаниеОповещения(
				"ПроверкаПослеВопроса", ЭтотОбъект, Новый Структура("ЭтоЗакрытие", Истина));
			ПоказатьВопрос(Обработчик, Текст, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Нет);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверкаПослеВопроса(Ответ, ДопПараметры) Экспорт

	ЭтоЗакрытие = ДопПараметры.Свойство("ЭтоЗакрытие") И ДопПараметры.ЭтоЗакрытие;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Записать(Новый Структура("СразуЗаписать", Истина)); // уже с признаком, чтоб не спрашивать второй раз.
		Модифицированность = Ложь;
		Если ЭтоЗакрытие Тогда
			Закрыть();
		КонецЕсли;
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда 
		Если ЭтоЗакрытие Тогда
			Модифицированность = Ложь; // Чтоб опять не спросил про сохранение.
			Закрыть();
		КонецЕсли;
		
	// КодВозвратаДиалога.Отмена - Ничего не делать.
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция СообщениеЕслиПустойКод()
	
	Если Не ЗначениеЗаполнено(Объект.Код) Тогда
		Возврат НСтр("ru = 'Код не указан.'") + Текст_ВсеРавноСохранить();
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

&НаСервере
Функция СообщениеЕслиЕстьДубли_Сервер()
	
	Текст = "";
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Т.Наименование,
		|	Т.НаименованиеПолное,
		|	Т.Код
		|ИЗ
		|	Справочник.КлассификаторЕдиницИзмерения КАК Т
		|ГДЕ
		|	Т.Ссылка <> &ЭтотЭлемент
		|	И (Т.Код = &Код ИЛИ Т.Наименование = &Наименование ИЛИ Т.НаименованиеПолное = &НаименованиеПолное)
		|	И НЕ Т.ПометкаУдаления");
	Запрос.УстановитьПараметр("ЭтотЭлемент", Объект.Ссылка);
	Запрос.УстановитьПараметр("Код", Объект.Код);
	Запрос.УстановитьПараметр("Наименование", Объект.Наименование);
	Запрос.УстановитьПараметр("НаименованиеПолное", Объект.НаименованиеПолное);
	Выборка = Запрос.Выполнить().Выбрать();
	КолвоДублей = Выборка.Количество();
	Если КолвоДублей <> 0 Тогда
		Текст = ?(КолвоДублей = 1, НСтр("ru = 'Обнаружен дубль:'"), НСтр("ru = 'Обнаружены дубли:'"));
		Пока Выборка.Следующий() Цикл
			Текст = Текст + Символы.ПС + СтрШаблон(
				НСтр("ru = 'Элемент ""%1"" ""%2"" с кодом ""%3""'"),
				Выборка.Наименование, Выборка.НаименованиеПолное, Выборка.Код);
		КонецЦикла;
		Текст = Текст + Текст_ВсеРавноСохранить();
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция Текст_ВсеРавноСохранить()
	
	Возврат Символы.ПС + НСтр("ru = 'Все равно сохранить изменения?'");
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)

	МультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);

КонецПроцедуры
#КонецОбласти