
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияДокументооборотКлиентСервер.УстановитьОтборПоказыватьУдаленные(
		Список, ПоказыватьУдаленные, Элементы.ПоказыватьУдаленные);
	ОбщегоНазначенияДокументооборот.УстановитьУсловноеОформлениеСписка(Список.УсловноеОформление);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьПослеДобавления" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ОбщегоНазначенияДокументооборотКлиентСервер.УстановитьОтборПоказыватьУдаленные(
		Список, ПоказыватьУдаленные, Элементы.ПоказыватьУдаленные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Не Копирование Тогда
		Отказ = Истина;
		Текст = НСтр("ru = 'Есть возможность подобрать единицу измерения из классификатора.
			|Подобрать?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПодобратьЕдиницуЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	ОбщегоНазначенияДокументооборотКлиентСервер.УстановитьОтборПоказыватьУдаленные(
		Список, ПоказыватьУдаленные, Элементы.ПоказыватьУдаленные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьИзМакета(Команда)
	
	ОткрытьФормуВыбораИзКлассификатора();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВопросПодобратьЕдиницуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОткрытьФормуВыбораИзКлассификатора();
	Иначе
		ОткрытьФорму(
			"Справочник.КлассификаторЕдиницИзмерения.Форма.ФормаЭлемента", ДополнительныеПараметры, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораИзКлассификатора()
	
	СтрокаПоиска = "";
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		СтрокаПоиска = ТекДанные.Код;
	КонецЕсли;
	СтруктураПараметров = Новый Структура("СтрокаПоиска", СтрокаПоиска);
	ОткрытьФорму(
		"Справочник.КлассификаторЕдиницИзмерения.Форма.ФормаВыбораИзКлассификатора", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти
