#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает структуру полей папки файлов
//
// Возвращаемое значение:
//   Структура
//     Родитель
//     Наименование
//     Описание
//     Ответственный
//     ДатаСоздания
//
Функция ПолучитьСтруктуруПапки() Экспорт
	
	ПараметрыПапки = Новый Структура;
	ПараметрыПапки.Вставить("Родитель");
	ПараметрыПапки.Вставить("Наименование");
	ПараметрыПапки.Вставить("Описание");
	ПараметрыПапки.Вставить("Ответственный");
	ПараметрыПапки.Вставить("ДатаСоздания");
	
	Возврат ПараметрыПапки;
	
КонецФункции

// Создает и записывает в БД папку файлов
//
// Параметры:
//   СтруктураПапкиФайлов - Структура - структура полей папки.
//
// Возвращаемое значение - ссылка на созданную папку
//
Функция СоздатьПапку(СтруктураПапкиФайлов) Экспорт
	
	НоваяПапка = СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НоваяПапка, СтруктураПапкиФайлов);
	НоваяПапка.Записать();
	
	Возврат НоваяПапка.Ссылка;
	
КонецФункции

// Проверяет, подходит ли объект к шаблону процесса
Функция ШаблонПодходитДляАвтозапускаБизнесПроцессаПоОбъекту(ШаблонСсылка, ПредметСсылка, Подписчик, ВидСобытия, Условие) Экспорт
	
	Возврат БизнесСобытияВызовСервера.ШаблонПодходитДляАвтозапускаБизнесПроцессаПоПредмету(ШаблонСсылка, 
		ПредметСсылка, Подписчик, ВидСобытия, Условие);
	
КонецФункции

// Возвращает реквизиты объекта, которые разрешается редактировать
// с помощью обработки группового изменения реквизитов.
//
// Возвращаемое значение:
//  Массив - список имен реквизитов объекта.
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	РедактируемыеРеквизиты = Новый Массив;
	РедактируемыеРеквизиты.Добавить("Описание");
	РедактируемыеРеквизиты.Добавить("Ответственный");
	РедактируемыеРеквизиты.Добавить("ДатаСоздания");
	
	Возврат РедактируемыеРеквизиты;
	
КонецФункции

// Начальное заполнение

Процедура ПриНастройкеНачальногоЗаполненияЭлементов(Настройки) Экспорт
	
	Настройки.ПриНачальномЗаполненииЭлемента = Ложь;
	
КонецПроцедуры

// Вызывается при начальном заполнении справочника ПапкиФайлов.
//
// Параметры:
//  КодыЯзыков - Массив - список языков конфигурации. Актуально для мультиязычных конфигураций.
//  Элементы   - ТаблицаЗначений - данные заполнения. Состав колонок соответствует набору реквизитов
//                                 справочника УчетныеЗаписиЭлектроннойПочты.
//  ТабличныеЧасти - Структура - описание табличных частей объекта, где:
//   * Ключ - Строка - имя табличной части;
//   * Значение - ТаблицаЗначений - табличная часть в виде таблицы значений, структуру которой
//                                  необходимо скопировать перед заполнением. Например:
//                                  Элемент.Ключи = ТабличныеЧасти.Ключи.Скопировать();
//                                  ЭлементТЧ = Элемент.Ключи.Добавить();
//                                  ЭлементТЧ.ИмяКлюча = "Первичный";
//
Процедура ПриНачальномЗаполненииЭлементов(КодыЯзыков, Элементы, ТабличныеЧасти) Экспорт
	
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных        = "Шаблоны";
	Элемент.Наименование                     = НСтр("ru = 'Шаблоны файлов'", 
		ОбщегоНазначения.КодОсновногоЯзыка());
	Элемент.ДатаСоздания = ТекущаяДата();
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

#Область ПраваДоступа

// Возвращает строку, содержащую перечисление полей доступа через запятую
// Это перечисление используется в дальнейшем для передачи в метод 
// ОбщегоНазначения.ЗначенияРеквизитовОбъекта()
Функция ПолучитьПоляДоступа() Экспорт
	
	Возврат "Ссылка";
	
КонецФункции

// Заполняет переданный дескриптор доступа 
Процедура ЗаполнитьОсновнойДескриптор(ОбъектДоступа, ДескрипторДоступа) Экспорт
	
	ДокументооборотПраваДоступа.ЗаполнитьНастройкиДескриптора(ДескрипторДоступа, ОбъектДоступа);

КонецПроцедуры	

// Проверяет наличие метода.
// 
// Возвращаемое значение:
//  Булево -
Функция ЕстьМетодПраваПравообладателейПоФайлам() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает права доступа правообладателей, т.е. сотрудников или пользователей, к переданным файлам.
// 
// Параметры:
//	Файлы - Массив Из СправочникСсылка.Файлы -
//	СотрудникиОтбор - Массив Из СправочникСсылка.Сотрудники - Сотрудники для отбора.
//					- Неопределено -
//	ПользователиОтбор - Массив Из СправочникСсылка.Пользователи - Пользователи для отбора.
//					  - Неопределено -
//
// Возвращаемое значение:
//  См. ДокументооборотПраваДоступа.ТаблицаПравСотрудниковПоОбъектам
Функция ПраваПравообладателейПоФайлам(Файлы, СотрудникиОтбор = Неопределено, ПользователиОтбор = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаПрав = ДокументооборотПраваДоступа.ТаблицаПравСотрудниковПоОбъектам();
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Файлы.Ссылка КАК Файл,
		|	ДескрипторыДляОбъектов.Дескриптор КАК ДескрипторПапки
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПапкиФайлов КАК ПапкиФайлов
		|		ПО Файлы.ВладелецФайла = ПапкиФайлов.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|		ПО ПапкиФайлов.Ссылка = ДескрипторыДляОбъектов.Объект
		|ГДЕ
		|	Файлы.Ссылка В(&Файлы)");
	
	Запрос.УстановитьПараметр("Файлы", Файлы);
	
	ТаблицаФайловСДескрипторами = Запрос.Выполнить().Выгрузить();
	ТаблицаФайловСДескрипторами.Индексы.Добавить("ДескрипторПапки");
	
	Дескрипторы = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		Дескрипторы, ТаблицаФайловСДескрипторами.ВыгрузитьКолонку("ДескрипторПапки"));
	
	// Получение прав по дескрипторам папок, но по правилам обработки для файлов.
	Запрос = ЗапросДляРасчетаПрав(
		Дескрипторы,
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.ПапкиФайлов),
		Справочники.ПапкиФайлов);
	
	Если ПользователиОтбор <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"ПО НовыеПрава.Сотрудник = СоставСубъектов.Субъект",
			"ПО НовыеПрава.Сотрудник = СоставСубъектов.Субъект
			|И СоставСубъектов.Пользователь В (&ПользователиОтбор)");
		Запрос.УстановитьПараметр("ПользователиОтбор", ПользователиОтбор);
	ИначеЕсли СотрудникиОтбор <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"ПО НовыеПрава.Сотрудник = СоставСубъектов.Субъект",
			"ПО НовыеПрава.Сотрудник = СоставСубъектов.Субъект
			|И СоставСубъектов.Сотрудник В (&СотрудникиОтбор)");
		Запрос.УстановитьПараметр("СотрудникиОтбор", СотрудникиОтбор);
	КонецЕсли;
	
	ПравилаОбработкиНастроекПапки = Справочники.Файлы.ПолучитьПравилаОбработкиНастроекПравПапки();
	Запрос.УстановитьПараметр("ПравилаОбработкиНастроекПапки", ПравилаОбработкиНастроекПапки);
	
	ВыборкаПоДескрипторам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоДескрипторам.Следующий() Цикл
		
		СтруктураПоиска = Новый Структура("ДескрипторПапки", ВыборкаПоДескрипторам.Дескриптор);
		СтрокиФайлов = ТаблицаФайловСДескрипторами.НайтиСтроки(СтруктураПоиска);
		
		Для Каждого СтрокаФайла Из СтрокиФайлов Цикл
			
			Выборка = ВыборкаПоДескрипторам.Выбрать();
			Пока Выборка.Следующий() Цикл
				Стр = ТаблицаПрав.Добавить();
				ЗаполнитьЗначенияСвойств(Стр, Выборка);
				Стр.ОбъектДоступа = СтрокаФайла.Файл;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ИдентификаторОМ = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.Файлы);
	ДокументооборотПраваДоступа.РасширитьТаблицуПравНеограниченнымиПравами(
		ТаблицаПрав, ИдентификаторОМ, Файлы, СотрудникиОтбор, ПользователиОтбор);
	
	Возврат ТаблицаПрав;
	
КонецФункции

// Возвращает признак того, что менеджер содержит метод ЗапросДляРасчетаПрав()
// 
Функция ЕстьМетодЗапросДляРасчетаПрав() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает запрос для расчета прав доступа по дескрипторам объекта
// 
// Параметры:
//  
//  Дескрипторы - Массив - массив дескрипторов, чьи права нужно рассчитать
//  ИдОбъекта - Ссылка - идентификатор объекта метаданных, назначенный переданным дескрипторам
//  МенеджерОбъектаДоступа - СправочникМенеджер, ДокументМенеджер - менеджер объекта доступа
// 
// Возвращаемое значение - Запрос - запрос, который выберет права доступа для переданного массива дескрипторов
// 
Функция ЗапросДляРасчетаПрав(Дескрипторы, ИдОбъекта, МенеджерОбъектаДоступа) Экспорт
	
	Запрос = Справочники.ДескрипторыДоступаОбъектов.ЗапросДляСтандартногоРасчетаПрав(
		Дескрипторы, ИдОбъекта, МенеджерОбъектаДоступа, Истина, Ложь);
	Запрос.Текст = ДокументооборотПраваДоступаПовтИсп.ТекстЗапросаДляРасчетаПравПапок();
	
	Возврат Запрос;
	
КонецФункции

// Заполняет протокол расчета прав дескрипторов
// 
// Параметры:
//  
//  ПротоколРасчетаПрав - Массив - протокол для заполнения
//  ЗапросПоПравам - Запрос - запрос, который использовался для расчета прав дескрипторов
//  Дескрипторы - Массив - массив дескрипторов, чьи права были рассчитаны
//  
Процедура ЗаполнитьПротоколРасчетаПрав(ПротоколРасчетаПрав, ЗапросПоПравам) Экспорт
	
	Справочники.ДескрипторыДоступаОбъектов.ЗаполнитьПротоколРасчетаПравСтандартно(
		ПротоколРасчетаПрав, ЗапросПоПравам);
	
КонецПроцедуры

Функция ЕстьМетодПолучитьПравилаОбработкиНастроекПравПапки() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает таблицу значений с правилами обработки настроек прав папки,
// которые следует применять для расчета прав объекта
// 
Функция ПолучитьПравилаОбработкиНастроекПравПапки() Экспорт
	
	ТаблицаПравил = ДокументооборотПраваДоступа.ТаблицаПравилОбработкиНастроекПапки();
	
	Стр = ТаблицаПравил.Добавить();
	Стр.Настройка = "ЧтениеПапокИФайлов";
	Стр.Чтение = Истина;
	
	Стр = ТаблицаПравил.Добавить();
	Стр.Настройка = "ИзменениеПапок";
	Стр.Добавление = Истина;
	Стр.Изменение = Истина;
	Стр.Удаление = Истина;
	
	Стр = ТаблицаПравил.Добавить();
	Стр.Настройка = "УправлениеПравами";
	Стр.УправлениеПравами = Истина;
	
	Возврат ТаблицаПравил;
	
КонецФункции

#Область УстаревшиеПроцедурыИФункции

// Устарела. См. ЕстьМетодПраваПравообладателейПоФайлам
// 
// Возвращаемое значение:
//  Булево -
Функция ЕстьМетодПраваСотрудниковПоФайлам() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Устарела. См. ПраваПравообладателейПоФайлам
// 
// Параметры:
//	Файлы - Массив Из СправочникСсылка.Файлы -
//	СотрудникиОтбор - Массив Из СправочникСсылка.Сотрудники -
//					- Неопределено -
//
// Возвращаемое значение:
//  См. ДокументооборотПраваДоступа.ТаблицаПравСотрудниковПоОбъектам
Функция ПраваСотрудниковПоФайлам(Файлы, СотрудникиОтбор = Неопределено) Экспорт
	
	Возврат ПраваПравообладателейПоФайлам(Файлы, СотрудникиОтбор, Неопределено);
	
КонецФункции

#КонецОбласти // УстаревшиеПроцедурыИФункции

#КонецОбласти // ПраваДоступа

#КонецОбласти // ДляВызоваИзДругихПодсистем

#КонецОбласти // ПрограммныйИнтерфейс

#КонецЕсли
