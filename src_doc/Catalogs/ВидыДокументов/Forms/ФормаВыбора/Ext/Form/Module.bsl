#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь(,, Ложь)
		Или Не ПолучитьФункциональнуюОпцию("ДокументооборотИспользоватьОграничениеПравДоступа");
	
	Если Параметры.Отбор.Свойство("ПометкаУдаления") И Не Параметры.Отбор.ПометкаУдаления Тогда 
		Элементы.ПоказыватьУдаленные.Видимость = Ложь;
	Иначе 
		ПоказыватьУдаленные = Ложь;
		ПоказатьУдаленные();
	КонецЕсли;
	
	ТематикиИспользуются = РаботаСТематикамиДокументов.ТематикиИспользуются()
		И (Параметры.Свойство("РежимВыбораСТематикой") И Параметры.РежимВыбораСТематикой);
	Элементы.Тематики.Видимость = ТематикиИспользуются;
	
	// Список видов документов
	ВсеСотрудникиПользователя = Сотрудники.ВсеСотрудникиТекущегоПользователя();
	Список.Параметры.УстановитьЗначениеПараметра("ВсеСотрудникиПользователя", ВсеСотрудникиПользователя);
	Если Параметры.Свойство("ДоступныеВидыДокументов") 
		И ЗначениеЗаполнено(Параметры.ДоступныеВидыДокументов) Тогда 
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Ссылка",
			Параметры.ДоступныеВидыДокументов.ВыгрузитьЗначения(),
			ВидСравненияКомпоновкиДанных.ВСписке,
			НСтр("ru = 'Вид документа'"),
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
	КонецЕсли;
	
	Если Параметры.Свойство("Тематика") И ЗначениеЗаполнено(Параметры.Тематика) Тогда 
		Элементы.Тематики.ТекущаяСтрока = Параметры.Тематика;
	КонецЕсли;
	
	Если ТематикиИспользуются Тогда 
		Тематики.Параметры.УстановитьЗначениеПараметра("ПоказыватьДействующие", Истина);
		Тематики.Параметры.УстановитьЗначениеПараметра("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
	КонецЕсли;
	Тематики.Параметры.УстановитьЗначениеПараметра("ВсеСотрудникиПользователя", ВсеСотрудникиПользователя);
	
	Если Параметры.Свойство("ТипКорреспонденции")
		И ЗначениеЗаполнено(Параметры.ТипКорреспонденции) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, 
			Параметры.ТипКорреспонденции, 
			Истина);
	КонецЕсли;
	
	// Локализация
	Если Параметры.КлючНазначенияИспользования = "ОтборОбращенийГраждан" Тогда
		Заголовок = НСтр("ru = 'Виды документов - обращений граждан/организаций'");
		АвтоЗаголовок = Ложь;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "ЯвляетсяОбращениемОтГраждан", Истина);
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
	КонецЕсли;
	// Локализация Конец
	
	УстановитьУсловноеОформление();
	
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Не Параметры.Отбор.Свойство("ПометкаУдаления") Тогда
		ПоказыватьУдаленные = Настройки["ПоказыватьУдаленные"];
		ПоказатьУдаленные();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Локализация
	Если Параметры.КлючНазначенияИспользования = "ОтборОбращенийГраждан" Тогда
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
	КонецЕсли;
	// Локализация Конец
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
		Или ТекущиеДанные.ЭтоГруппа Тогда 
		Возврат;
	КонецЕсли;
	
	Если Параметры.ПроверятьРазрешенияНаСозданиеДокументов
		И Не ЭтоПолноправныйПользователь
		И Не ТекущиеДанные.ЕстьРазрешениеНаСозданиеДокументов Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьПредупреждение(, СтрШаблон(
			НСтр("ru = '%1 ""%2"".
					|Обратитесь к администратору.'"),
			ДелопроизводствоКлиентСервер.Текст_НетРазрешенияНаСозданиеДокументовВида(),
			ТекущиеДанные.Ссылка));
		Возврат;
	КонецЕсли;
		
	Если ТематикиИспользуются Тогда 
		СтандартнаяОбработка = Ложь;
		
		// Если тематики используются, то проверям права для них.
		Если ТекущиеДанные.ВестиУчетПоТематикам Тогда
			ТекущиеДанные = Элементы.Тематики.ТекущиеДанные;
			Если ТекущиеДанные = Неопределено Тогда 
				Возврат;
			КонецЕсли;
			
			Если Параметры.ПроверятьРазрешенияНаСозданиеДокументов
				И Не ЭтоПолноправныйПользователь
				И Не ТекущиеДанные.ЕстьРазрешениеНаСозданиеДокументов Тогда
				СтандартнаяОбработка = Ложь;
				ПоказатьПредупреждение(, СтрШаблон(
					НСтр("ru = '%1 ""%2"" с тематикой ""%3"".
							|Обратитесь к администратору.'"),
					ДелопроизводствоКлиентСервер.Текст_НетРазрешенияНаСозданиеДокументовВида(),
					Элементы.Список.ТекущиеДанные.Ссылка,
					ТекущиеДанные.Ссылка));
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Оповещение = ОписаниеОповещенияОВыборе();
		
		Если ТекущиеДанные.ПометкаУдаления
			Или Элементы.Тематики.ТекущиеДанные <> Неопределено 
			И Элементы.Тематики.ТекущиеДанные.ПометкаУдаления Тогда
			ПоказатьВопрос(Оповещение, Нстр("ru = 'Выбранные данные помечены на удаление.
					|Выполнить выбор этих данных?'"),
				РежимДиалогаВопрос.ДаНет);
		Иначе
			ВыполнитьОбработкуОповещения(Оповещение, КодВозвратаДиалога.Да);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СписокВыбор(Элемент, Значение, Неопределено, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если ТематикиИспользуются 
		И ТекущийВид <> Элементы.Список.ТекущаяСтрока Тогда 
		
		ТекущийВид = Элементы.Список.ТекущаяСтрока;
		
		ПодключитьОбработчикОжидания("УстановитьПараметрыСписка", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТематики

&НаКлиенте
Процедура ТематикиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Тематики.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Параметры.ПроверятьРазрешенияНаСозданиеДокументов
		И Не ЭтоПолноправныйПользователь
		И Не ТекущиеДанные.ЕстьРазрешениеНаСозданиеДокументов Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьПредупреждение(, СтрШаблон(
			НСтр("ru = '%1 ""%2"" с тематикой ""%3"".
					|Обратитесь к администратору.'"),
			ДелопроизводствоКлиентСервер.Текст_НетРазрешенияНаСозданиеДокументовВида(),
			Элементы.Список.ТекущиеДанные.Ссылка,
			ТекущиеДанные.Ссылка));
		Возврат;
	КонецЕсли;
	
	Оповещение = ОписаниеОповещенияОВыборе();
	Если Элементы.Список.ТекущиеДанные.ПометкаУдаления
		Или ТекущиеДанные.ПометкаУдаления Тогда
		ПоказатьВопрос(Оповещение, Нстр("ru = 'Выбранные данные помечены на удаление.
				|Выполнить выбор этих данных?'"),
			РежимДиалогаВопрос.ДаНет);
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, КодВозвратаДиалога.Да);
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ТематикиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		ТекущиеДанные = Элемент.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		ТекущаяСтрока = Элемент.ТекущаяСтрока;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначениеКопирования", ТекущаяСтрока);
	Иначе
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ВидДокумента", Элементы.Список.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
	Открытьформу("Справочник.ТематикиДокументов.ФормаОбъекта", ПараметрыФормы, Элементы.Тематики);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	
	ПоказатьУдаленные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление();
	
	СпискиДляОформления = Новый Массив;
	СпискиДляОформления.Добавить(Список);
	СпискиДляОформления.Добавить(Тематики);
	
	Для Каждого СписокДляОформления Из СпискиДляОформления Цикл
		
		СписокДляОформления.УсловноеОформление.Элементы.Очистить();
		
		Если Параметры.ПроверятьРазрешенияНаСозданиеДокументов И Не ЭтоПолноправныйПользователь Тогда
			Элемент = СписокДляОформления.УсловноеОформление.Элементы.Добавить();
			ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЕстьРазрешениеНаСозданиеДокументов");
			ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ОтборЭлемента.ПравоеЗначение = Ложь;
			ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			Если СписокДляОформления = Список Тогда
				ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтоГруппа");
				ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
				ОтборЭлемента.ПравоеЗначение = Ложь;
			КонецЕсли;
			Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
		КонецЕсли;
	
		Делопроизводство.СписокДокументовУсловноеОформлениеПомеченныхНаУдаление(СписокДляОформления);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьУдаленные()
	
	Если ПоказыватьУдаленные Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "ПометкаУдаления");
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПометкаУдаления", Ложь);
	КонецЕсли;
	
	Если ПоказыватьУдаленные Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Тематики, "ПометкаУдаления");
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Тематики, "ПометкаУдаления", Ложь);
	КонецЕсли;
	
	Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыСписка()
	
	Тематики.Параметры.УстановитьЗначениеПараметра("ВидДокумента", 
		ТекущийВид);
	
	Если ЗначениеЗаполнено(ТекущийВид) И Элементы.Список.ТекущиеДанные <> Неопределено
		И Элементы.Список.ТекущиеДанные.ВестиУчетПоТематикам Тогда 
		Элементы.Тематики.Доступность = Истина;
	Иначе 
		Элементы.Тематики.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжениеПослеПодтвержденияВыбораПомеченногоНаУдаление(Результат, ВыбранныеДанные) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		ОповеститьОВыборе(ВыбранныеДанные);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция ОписаниеОповещенияОВыборе()

	Результат = Новый Структура("ВидДокумента, Тематика",
			Элементы.Список.ТекущаяСтрока, 
			Элементы.Тематики.ТекущаяСтрока);
	
	Оповещение = Новый ОписаниеОповещения(
				"ПродолжениеПослеПодтвержденияВыбораПомеченногоНаУдаление",
				ЭтотОбъект, 
				Результат);

	Возврат Оповещение;

КонецФункции

#КонецОбласти
