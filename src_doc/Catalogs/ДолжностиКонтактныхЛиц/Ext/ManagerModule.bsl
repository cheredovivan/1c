#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Ищет должность КЛ, если не находит, то создает.
// 
// Параметры:
//  Наименование - Строка - Должность в виде строки.
//  ТолькоНайти - Булево - Только найти, не создавать. Если не найдено, вернуть пустую ссылку.
//  НаименованиеЯзык1 - Строка - Может быть не указано.
//  НаименованиеЯзык2 - Строка - Может быть не указано.
// 
// Возвращаемое значение:
//  СправочникСсылка.ДолжностиКонтактныхЛиц - Найденная или вновь созданная должность, пустая ссылка, если не найдено
//											  и не создано.
Функция НайтиСоздатьДолжность(
	Знач Наименование, ТолькоНайти = Ложь, НаименованиеЯзык1 = "", НаименованиеЯзык2 = "") Экспорт
	
	// Уберем пробелы в начале и конце, двойные/тройные пробелы:
	Наименование = СокрЛП(Наименование);
	Наименование = СтрЗаменить(Наименование, "  ", "");
	Наименование = СтрЗаменить(Наименование, "   ", "");
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Ссылка КАК ДолжностьКЛ
		|ИЗ
		|	Справочник.ДолжностиКонтактныхЛиц
		|ГДЕ
		|	Наименование = &ДолжностьСтрокой
		|	И НЕ ПометкаУдаления
		|УПОРЯДОЧИТЬ ПО
		|	Наименование УБЫВ");
	Запрос.УстановитьПараметр("ДолжностьСтрокой", Наименование);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ДолжностьКЛ;
	КонецЕсли;
	
	// Не нашли.
	
	Если ТолькоНайти Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	ДолжностьОбъект = СоздатьЭлемент();
	ДолжностьОбъект.Наименование = Наименование;
	Если Не МультиязычностьПовтИсп.КонфигурацияИспользуетТолькоОдинЯзык(Ложь) Тогда
		Если МультиязычностьСервер.ИспользуетсяДополнительныйЯзык(1) Тогда
			ДолжностьОбъект.НаименованиеЯзык1 = НаименованиеЯзык1;
		КонецЕсли;
		Если МультиязычностьСервер.ИспользуетсяДополнительныйЯзык(2) Тогда
			ДолжностьОбъект.НаименованиеЯзык2 = НаименованиеЯзык2;
		КонецЕсли;
	КонецЕсли;
	ДолжностьОбъект.Записать();
	
	Возврат ДолжностьОбъект.Ссылка;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

#Область ОбработчикиОбновления

// Регистрирует данные к обновлению в плане обмена ОбновлениеИнформационнойБазы.
// Для копирования необходимых должности из справочника "Должности" в "Должности контактных лиц"
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаВерсию_3_0_17_3(Параметры) Экспорт
	
	Параметры.ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	Параметры.ПараметрыВыборки.ПолныеИменаОбъектов = Метаданные.Справочники.Должности.ПолноеИмя();
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	Ссылка КАК ДолжностьСотрудника
		|ИЗ
		|	Справочник.Должности
		|ГДЕ
		|	Ссылка > &ДолжностьСотрудника
		|	И НЕ ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка");
	СсылкаКОбновлению = Справочники.Должности.ПустаяСсылка();
	ЕстьДанныеКОбработке = Истина;
	Пока ЕстьДанныеКОбработке Цикл
		Запрос.УстановитьПараметр("ДолжностьСотрудника", СсылкаКОбновлению);
		
		//@skip-check query-in-loop - обработка порциями.
		ДанныеКОбработке = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ДолжностьСотрудника"); // Массив Из СправочникСсылка.Должности
		КоличествоКОбработке = ДанныеКОбработке.Количество();
		ЕстьДанныеКОбработке = КоличествоКОбработке > 0;
		Если ЕстьДанныеКОбработке Тогда
			СсылкаКОбновлению = ДанныеКОбработке[КоличествоКОбработке - 1];
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДанныеКОбработке);
	КонецЦикла;
	
КонецПроцедуры

// Обрабатывает данные, зарегистрированные в плане обмена ОбновлениеИнформационнойБазы.
// Копирует необходимые должности из справочника ""Должности"" в ""Должности контактных лиц"".
// Разделение справочников. Справочник "Должности" должен остаться только у сотрудников, а у контактных лиц новый
// справочник "Должности контактных лиц".
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ОбработатьДанныеДляПереходаНаВерсию_3_0_17_3(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.Справочники.Должности;
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	ТаблицаКОбновления = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Для Каждого СтрокаКОбновлению Из ТаблицаКОбновления Цикл
		
		НачатьТранзакцию();
		Попытка
			ДолжностьСотрудника = СтрокаКОбновлению.Ссылка; // СправочникСсылка.Должности
			
			Блокировка = Новый БлокировкаДанных();
			ЭлементБлокировки = Блокировка.Добавить("Справочник.Должности");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ДолжностьСотрудника);
			Блокировка.Заблокировать();
			
			//@skip-check query-in-loop - поиск контактных лиц,
			ЗаменитьНаНовуюДолжностьУКонтактныхЛиц_ПереходНаВерсию3_0_17_3(ДолжностьСотрудника);
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ДолжностьСотрудника);
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				МетаданныеОбъекта,
				СтрокаКОбновлению.Ссылка,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Не удалось обработать некоторые объекты (пропущены): %1'"), 
			ПроблемныхОбъектов);
	Иначе
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			МетаданныеОбъекта,,
			СтрШаблон(
				НСтр("ru = 'Обработана очередная порция: %1'"),
				ОбъектовОбработано));
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = ОбъектовОбработано;
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
		Параметры.Очередь,
		ПолноеИмяОбъекта);
	
КонецПроцедуры

// Регистрирует данные к обновлению в плане обмена ОбновлениеИнформационнойБазы.
// Для пометки на удаление должностей из справочника "Должности", которых нет у сотрудников, но есть у контактных лиц.
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаВерсию_3_0_17_4(Параметры) Экспорт
	
	Параметры.ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	Параметры.ПараметрыВыборки.ПолныеИменаОбъектов = Метаданные.Справочники.Должности.ПолноеИмя();
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	Ссылка КАК ДолжностьСотрудника
		|ИЗ
		|	Справочник.Должности
		|ГДЕ
		|	Ссылка > &ДолжностьСотрудника
		|	И НЕ ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка");
	СсылкаКОбновлению = Справочники.Должности.ПустаяСсылка();
	ЕстьДанныеКОбработке = Истина;
	Пока ЕстьДанныеКОбработке Цикл
		Запрос.УстановитьПараметр("ДолжностьСотрудника", СсылкаКОбновлению);
		
		//@skip-check query-in-loop - обработка порциями.
		ДанныеКОбработке = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ДолжностьСотрудника"); // Массив Из СправочникСсылка.Должности
		КоличествоКОбработке = ДанныеКОбработке.Количество();
		ЕстьДанныеКОбработке = КоличествоКОбработке > 0;
		Если ЕстьДанныеКОбработке Тогда
			СсылкаКОбновлению = ДанныеКОбработке[КоличествоКОбработке - 1];
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДанныеКОбработке);
	КонецЦикла;
	
КонецПроцедуры

// Обрабатывает данные, зарегистрированные в плане обмена ОбновлениеИнформационнойБазы.
// Помечает на удаление элементы справочника "Должности", если должностей нет у сотрудников, но есть у контактных лиц.
// Разделение справочников. Если должность есть только у контактных лиц, но нет у сотрудников, то такие помечаем на
// удаление. И сразу очищаем УдалитьДолжность у этих контактных лиц.
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ОбработатьДанныеДляПереходаНаВерсию_3_0_17_4(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.Справочники.Должности;
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	ТаблицаКОбновления = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Для Каждого СтрокаКОбновлению Из ТаблицаКОбновления Цикл
		
		НачатьТранзакцию();
		Попытка
			ДолжностьСотрудника = СтрокаКОбновлению.Ссылка; // СправочникСсылка.Должности
			
			Блокировка = Новый БлокировкаДанных();
			ЭлементБлокировки = Блокировка.Добавить("Справочник.Должности");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ДолжностьСотрудника);
			Блокировка.Заблокировать();
			
			//@skip-check query-in-loop - поиск контактных лиц,
			ПометитьНаУдалениеДолжность_ПереходНаВерсию3_0_17_4(ДолжностьСотрудника);
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ДолжностьСотрудника);
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				МетаданныеОбъекта,
				СтрокаКОбновлению.Ссылка,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Не удалось обработать некоторые объекты (пропущены): %1'"), 
			ПроблемныхОбъектов);
	Иначе
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			МетаданныеОбъекта,,
			СтрШаблон(
				НСтр("ru = 'Обработана очередная порция: %1'"),
				ОбъектовОбработано));
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = ОбъектовОбработано;
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
		Параметры.Очередь,
		ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
// См. ЗапретРедактированияРеквизитовОбъектовПереопределяемый.ПриОпределенииОбъектовСЗаблокированнымиРеквизитами.
// 
// Возвращаемое значение:
//  Массив из Строка - Блокируемые реквизиты
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив();
	Результат.Добавить("Наименование; Наименование, Склонения");
	
	Возврат Результат;
	
КонецФункции
// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
	// Служебный метод БСП.
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти // Конец ДляВызоваИзДругихПодсистем

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Мультиязычность
	МультиязычностьКлиентСервер.ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка);
	// Конец СтандартныеПодсистемы.Мультиязычность
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Мультиязычность
	МультиязычностьКлиентСервер.ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка);
	// Конец СтандартныеПодсистемы.Мультиязычность
	
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Мультиязычность
	МультиязычностьСервер.ОбработкаПолученияДанныхВыбора(
		ДанныеВыбора, Параметры, СтандартнаяОбработка, Метаданные.Справочники.ДолжностиКонтактныхЛиц);
	// Конец СтандартныеПодсистемы.Мультиязычность
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Ищет контактные лица со "старой" должностью, и ставит в них новую. Если не найдена по наименованию, то создает и
// ставит.
// 
// Параметры:
//  СтараяДолжность - СправочникСсылка.Должности - "Старая должность" контактного лица.
Процедура ЗаменитьНаНовуюДолжностьУКонтактныхЛиц_ПереходНаВерсию3_0_17_3(СтараяДолжность)
	
	Если Не ЗначениеЗаполнено(СтараяДолжность) Тогда
		Возврат; // перестраховка.
	КонецЕсли;
	
	РеквизитыСтарой = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		СтараяДолжность, "Наименование, НаименованиеЯзык1, НаименованиеЯзык2");
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Ссылка КАК КонтактноеЛицо
		|ИЗ
		|	Справочник.КонтактныеЛица
		|ГДЕ
		|	УдалитьДолжность = &Должность
		|	И Должность = ЗНАЧЕНИЕ(Справочник.ДолжностиКонтактныхЛиц.ПустаяСсылка)");
	Запрос.УстановитьПараметр("Должность", СтараяДолжность);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НачатьТранзакцию();
		Попытка
			КЛОбъект = Выборка.КонтактноеЛицо.ПолучитьОбъект();
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.КонтактныеЛица");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Ссылка", КЛОбъект.Ссылка);
			Блокировка.Заблокировать();
			
			//@skip-check query-in-loop
			КЛОбъект.Должность = НайтиСоздатьДолжность(
				РеквизитыСтарой.Наименование,
				Истина,
				РеквизитыСтарой.НаименованиеЯзык1,
				РеквизитыСтарой.НаименованиеЯзык2); // Пытаемся найти по наименованию.
			
			Если Не ЗначениеЗаполнено(КЛОбъект.Должность) Тогда
				// Должности КЛ с таким же наименованием, как у старой должности нет, создаем с таким же GUID,
				// как у СтараяДолжность. Если это Холдинг, то во всех узлах создастся одинаково:
				ДолжностьКЛ = СоздатьЭлемент();
				ЗаполнитьЗначенияСвойств(ДолжностьКЛ, РеквизитыСтарой);
				ДолжностьКЛ.УстановитьСсылкуНового(ПолучитьСсылку(СтараяДолжность.УникальныйИдентификатор()));
				ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные(ДолжностьКЛ);
				
				КЛОбъект.Должность = ДолжностьКЛ.Ссылка;
			КонецЕсли;
			
			// КЛОбъект.УдалитьДолжность - остается старой, для дальнейшей проставки пометки на удаление у
			// справочника "Должности".
			ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные(КЛОбъект); // Каждый узел делает сам, без обмена.
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

// Если должность есть только у контактных лиц, но нет у сотрудников, то такие помечаем на
// удаление. И сразу очищаем УдалитьДолжность у этих контактных лиц.
// 
// Параметры:
//  ДолжностьСотрудника - СправочникСсылка.Должности - Должность, которая теперь стала только у сотрудников
Процедура ПометитьНаУдалениеДолжность_ПереходНаВерсию3_0_17_4(ДолжностьСотрудника)
	
	Если Не ЗначениеЗаполнено(ДолжностьСотрудника) Тогда
		Возврат; // перестраховка.
	КонецЕсли;
	
	// Есть ли должность хоть у одного сотрудника (помеченных на удаление тоже учтем):
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Ссылка
		|ИЗ
		|	Справочник.Сотрудники
		|ГДЕ
		|	Должность = &ДолжностьСотрудника");
	Запрос.УстановитьПараметр("ДолжностьСотрудника", ДолжностьСотрудника);
	ЕстьДолжностьУСотрудников = Не Запрос.Выполнить().Пустой();
	
	// Если должность сотрудника есть у контактных лиц, очищаем ее из реквизита УдалитьДолжность.
	// Если она у кого-то есть.
	// Чтобы потом потенциально могло нормально пройти удаление помеченных объектов:
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Ссылка КАК КонтактноеЛицо
		|ИЗ
		|	Справочник.КонтактныеЛица
		|ГДЕ
		|	НЕ ПометкаУдаления
		|	И УдалитьДолжность = &ДолжностьСотрудника";
	Выборка = Запрос.Выполнить().Выбрать();
	ЕстьДолжностьУКонтактныхЛиц = Ложь;
	Пока Выборка.Следующий() Цикл
		КЛОбъект = Выборка.КонтактноеЛицо.ПолучитьОбъект();
		КЛОбъект.УдалитьДолжность = Справочники.Должности.ПустаяСсылка();
		ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные(КЛОбъект);  // Каждый узел делает сам, без обмена.
		ЕстьДолжностьУКонтактныхЛиц = Истина;
	КонецЦикла;
	
	Если ЕстьДолжностьУКонтактныхЛиц И Не ЕстьДолжностьУСотрудников Тогда
		// Ни у кого из сотрудников нет, но есть у контактных лиц, помечаем должность на удаление:
		ДолжностьОбъект = ДолжностьСотрудника.ПолучитьОбъект();
		ДолжностьОбъект.ПометкаУдаления = Истина;
		ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные(ДолжностьОбъект); // Каждый узел делает сам, без обмена.
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

