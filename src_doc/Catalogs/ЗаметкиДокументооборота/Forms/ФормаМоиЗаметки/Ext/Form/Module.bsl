
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяЭтойФормы = "Справочник.ЗаметкиДокументооборота.Форма.ФормаМоиЗаметки";
	
	ПоказыватьУдаленные = Ложь;
	
	ОбновитьПоказУдаленных();
		
	Список.Параметры.УстановитьЗначениеПараметра("Тема", Неопределено);
	
	ОбновитьЧислоЗаметокСервер();
	
	ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
	Если ЭтоМобильныйКлиент Тогда
		ПредставлениеТекущейТемы = НСтр("ru = 'Все темы'");
		Список.Параметры.УстановитьЗначениеПараметра("ЭтоМобильныйКлиент", ЭтоМобильныйКлиент);
		МК_НастроитьЭлементыФормы();
	КонецЕсли;
	
	ОбновитьЧислоЗаметокСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаметкаИзменена" Тогда
		Если Элементы.Список.ТекущаяСтрока = Источник Тогда
			ОбновитьСодержаниеВыбраннойЗаметки();
		КонецЕсли;
		
		ОбновитьЧислоЗаметок();
		Элементы.ДеревоТем.Обновить();
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредставлениеТекущейТемыНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Элементы.МК_Страницы.ТекущаяСтраница = Элементы.МК_СтраницаТемы;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если ЗначениеЗаполнено(Элементы.Список.ТекущаяСтрока) Тогда
		ПодключитьОбработчикОжидания("ОтложенныйВыборЗаметки", 0.2, Истина);	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Не Копирование Тогда
		Отказ = Истина;
		СоздатьНовуюЗаметку();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаЗапросаОбновления(Элемент)
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоТем

&НаКлиенте
Процедура ДеревоТемПриАктивизацииСтроки(Элемент)
	
	Если ЗначениеЗаполнено(Элементы.ДеревоТем.ТекущаяСтрока) Тогда
		ПодключитьОбработчикОжидания("ВыборТемы", 0.2, Истина);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьЗаметку(Команда)
	
	СоздатьНовуюЗаметку();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = НЕ ПоказыватьУдаленные;
	ОбновитьПоказУдаленных();
	
КонецПроцедуры

&НаКлиенте
Процедура Поиск(Команда)
	
	Открытьформу("Справочник.ЗаметкиДокументооборота.Форма.ФормаПоиска", , ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура МК_ВыбратьТему(Команда)
	
	Элементы.МК_Страницы.ТекущаяСтраница = Элементы.МК_СтраницаТемы;
	
КонецПроцедуры

&НаКлиенте
Процедура МК_ВернутьсяКЗаметкам(Команда)
	
	Элементы.МК_Страницы.ТекущаяСтраница = Элементы.МК_СтраницаЗаметки;
	
КонецПроцедуры

&НаКлиенте
Процедура МК_СброситьТему(Команда)
	
	ПредставлениеТекущейТемы = НСтр("ru = 'Все темы'");
	Список.Параметры.УстановитьЗначениеПараметра("Тема", Неопределено);
	Элементы.МК_СброситьТему.Видимость = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыборТемы()
	
	ТекущаяТема = Элементы.ДеревоТем.ТекущаяСтрока;
	Список.Параметры.УстановитьЗначениеПараметра("Тема", Элементы.ДеревоТем.ТекущаяСтрока);
	
	Если ЭтоМобильныйКлиент Тогда
		
		МК_СохранитьНастройки();
		
		ПредставлениеТекущейТемы = Элементы.ДеревоТем.ДанныеСтроки(ТекущаяТема).Наименование;
		Элементы.МК_Страницы.ТекущаяСтраница = Элементы.МК_СтраницаЗаметки;
		Элементы.МК_СброситьТему.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПоказУдаленных()
	
	// Отображение пометки у команды
	Элементы.ФормаПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	
	// Установки видимости колонок с числом заметок
	Элементы.ДеревоТемЧислоЗаметок.Видимость = Не ПоказыватьУдаленные;
	Элементы.ДеревоТемЧислоЗаметокВсего.Видимость = ПоказыватьУдаленные;
	
	Если Не ПоказыватьУдаленные Тогда
		
		// Установка отбора
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
			"ПометкаУдаления",
			Ложь,
			ВидСравненияКомпоновкиДанных.Равно);	
			
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ДеревоТем.Отбор,
			"ПометкаУдаления",
			Ложь,
			ВидСравненияКомпоновкиДанных.Равно);	
			
	Иначе		
		
		// Удаление отбора
		
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор, "ПометкаУдаления");
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ДеревоТем.Отбор, "ПометкаУдаления");
			
	КонецЕсли;
	
	ОбновитьЧислоЗаметокСервер()
	
КонецПроцедуры

&НаКлиенте
Процедура ОтложенныйВыборЗаметки()
	
	ОбновитьСодержаниеВыбраннойЗаметки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСодержаниеВыбраннойЗаметки()
	
	Если Элементы.Список.ТекущаяСтрока <> Неопределено Тогда
		СодержаниеЗаметки = ПолучитьСодержаниеВыбраннойЗаметки(Элементы.Список.ТекущаяСтрока);
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСодержаниеВыбраннойЗаметки(Заметка)
	
	Возврат Заметка.Содержание.Получить();
	
КонецФункции

&НаКлиенте
Процедура СоздатьНовуюЗаметку()
	
	ПараметрыФормы = Новый Структура("Тема", Элементы.ДеревоТем.ТекущаяСтрока);
	Открытьформу("Справочник.ЗаметкиДокументооборота.Форма.ФормаЭлемента", ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьЧислоЗаметок()
	
	ОбновитьЧислоЗаметокСервер();
	
КонецПроцедуры	

&НаСервере
Процедура ОбновитьЧислоЗаметокСервер()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ КОЛИЧЕСТВО(*) КАК ЧислоЗаметок
		|ИЗ
		|	Справочник.ЗаметкиДокументооборота КАК ЗаметкиДокументооборота
		|ГДЕ
		|	ЗаметкиДокументооборота.ПометкаУдаления = Ложь";

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Заголовок = НСтр("ru = 'Заметки'") + " (" + Строка(ВыборкаДетальныеЗаписи.ЧислоЗаметок) + ")";
	КонецЕсли;

КонецПроцедуры	

#Область СлужебныеПроцедурыИФункции_МобильныйКлиент

&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	МК_ЗагрузитьНастройки();
	
	МК.УстановитьОбщиеСвойстваФормы(ЭтотОбъект);
	МК_ЭлементыСтиля = МК.ЭлементыСтиля();
	
	Элементы.Список.Шапка = Ложь;
	Элементы.Список.ПоложениеСтрокиПоиска = ПоложениеСтрокиПоиска.ПотянутьСверху;
	Элементы.СодержаниеЗаметки.Видимость = Ложь;
	Элементы.Приоритет.Видимость = Ложь;
	
	Элементы.ДеревоТем.Шапка = Ложь;
	Элементы.ДеревоТем.ИспользованиеТекущейСтроки = ИспользованиеТекущейСтрокиТаблицы.ОтображениеВыделенияИВыбор;
	
	Элементы.Изменена.ФиксацияВТаблице = ФиксацияВТаблице.Право;

	Элементы.МК_Страницы.Видимость = Истина;
	МК.ОформитьАкцентнуюКнопку(Элементы.МК_ВернутьсяКЗаметкам);
	
	Элементы.Переместить(Элементы.ДеревоТем, Элементы.МК_СтраницаТемы, Элементы.МК_ВернутьсяКЗаметкам);
	Элементы.Переместить(Элементы.Список, Элементы.МК_СтраницаЗаметки);
	
	Элементы.МК_Страницы.ТекущаяСтраница = Элементы.МК_СтраницаЗаметки;
	
	МК.ПреобразоватьКнопкуВАкцентную(ЭтотОбъект, МК_ЭлементыСтиля, Элементы.ФормаСоздатьЗаметку, Элементы.МК_СтраницаЗаметки);
	Элементы.ФормаСоздатьЗаметку.Заголовок = НСтр("ru = 'Создать'");

	Если ТекущаяТема = Неопределено Тогда
		Элементы.МК_СброситьТему.Видимость = Ложь;
	Иначе
		Элементы.МК_СброситьТему.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура МК_ЗагрузитьНастройки()
	
	ТекущаяТемаПриСоздании = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			ИмяЭтойФормы, "ТекущаяТема", Неопределено);

	Если ЗначениеЗаполнено(ТекущаяТемаПриСоздании) Тогда
		ТекущаяТема = ТекущаяТемаПриСоздании;
	КонецЕсли;

	Если Не ТекущаяТема = Неопределено Тогда
		ПредставлениеТекущейТемы = ТекущаяТема.Наименование;
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("Тема", ТекущаяТема);

КонецПроцедуры

&НаСервере
Процедура МК_СохранитьНастройки()

	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяЭтойФормы, "ТекущаяТема", ТекущаяТема);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
