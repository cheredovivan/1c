#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщиеКартинки = РаботаСВиджетами.ОбщиеКартинки();
	Для Каждого ОбщаяКартинка Из ОбщиеКартинки Цикл
		
		Элементы.Картинка.СписокВыбора.Добавить(ОбщаяКартинка.Имя, ОбщаяКартинка.Представление);
		
	КонецЦикла;
	Элементы.Картинка.СписокВыбора.СортироватьПоЗначению();
	
	ВсеИсточникиСтрокой = РаботаСВиджетами.ВсеИсточникиСтрокой();
	Для Каждого ИсточникСтрокой Из ВсеИсточникиСтрокой Цикл
		
		Элементы.Источник.СписокВыбора.Добавить(ИсточникСтрокой);
		
	КонецЦикла;
	Элементы.Источник.СписокВыбора.СортироватьПоЗначению();
	
	ОбновитьДоступностьДействия();
	ОбновитьДоступностьЗначения();
	ОбновитьСвойстваГрупповогоПоказателя();
	
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ТолькоПросмотр = Объект.Предопределенный;
	
	ОбщегоНазначенияДокументооборот.ПоказатьНадписьПометкиУдаления(
		ЭтотОбъект,
		ТекущийОбъект.ПометкаУдаления,
		Элементы.ГруппаПометкаУдаления.Имя);
	
	Элементы.ГруппаПредопределенный.Видимость = Объект.Предопределенный;
	
	КлючСохраненияПоложенияОкна =
		Строка(Элементы.ГруппаПредопределенный.Видимость)
		+ Строка(Элементы.ГруппаПометкаУдаления.Видимость);
	
	ОбновитьДоступностьДействия();
	ОбновитьДоступностьЗначения();
	ОбновитьСвойстваГрупповогоПоказателя();
	
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	МультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДействиеПриИзменении(Элемент)
	
	Объект.ПараметрДействия1 = "";
	Объект.ПараметрДействия2 = "";
	Объект.Источник = "";
	
	ОбновитьДоступностьДействия();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипПриИзменении(Элемент)
	
	Объект.Выражение = "";
	Объект.ГрупповойПоказатель = Неопределено;
	Объект.СвойствоГрупповогоПоказателя = "";
	
	ОбновитьДоступностьЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрупповойПоказательПриИзменении(Элемент)
	
	ОбновитьСвойстваГрупповогоПоказателя();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)
    МультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДоступностьДействия()
	
	ОписаниеДействия = Перечисления.ДействияВиджетов.ОписаниеДействия(Объект.Действие);
	
	Элементы.ПараметрДействия1.Доступность = ОписаниеДействия.КоличествоПараметров >= 1;
	Элементы.ПараметрДействия1.ПодсказкаВвода = ОписаниеДействия.ПодсказкаПараметра1;
	
	Элементы.ПараметрДействия2.Доступность = ОписаниеДействия.КоличествоПараметров >= 2;
	Элементы.ПараметрДействия2.ПодсказкаВвода = ОписаниеДействия.ПодсказкаПараметра2;
	
	Элементы.Источник.Доступность = ОписаниеДействия.ТребуетсяИсточник;
	Элементы.Источник.ПодсказкаВвода = ОписаниеДействия.ПодсказкаИсточника;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДоступностьЗначения()
	
	Элементы.Выражение.Доступность =
		Объект.Тип = Перечисления.ТипыПоказателейВиджетов.Обычный;
	Если Элементы.Выражение.Доступность Тогда
		Элементы.Выражение.ПодсказкаВвода = 
			"Параметры.Результат = РаботаСЗадачами.ЗадачиОтМеняИтоги(Пользователи.ТекущийПользователь()).ОжидающихВыполнения";
	Иначе
		Элементы.Выражение.ПодсказкаВвода = "";
	КонецЕсли;
	
	Элементы.ГрупповойПоказатель.Доступность =
		Объект.Тип = Перечисления.ТипыПоказателейВиджетов.Групповой;
	
	Элементы.СвойствоГрупповогоПоказателя.Доступность =
		Объект.Тип = Перечисления.ТипыПоказателейВиджетов.Групповой;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСвойстваГрупповогоПоказателя()
	
	Элементы.СвойствоГрупповогоПоказателя.СписокВыбора.Очистить();
	
	Если Не ЗначениеЗаполнено(Объект.ГрупповойПоказатель) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыПоказателя =
		ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ГрупповойПоказатель, "Свойства");
	
	Элементы.СвойствоГрупповогоПоказателя.СписокВыбора.ЗагрузитьЗначения(
		РеквизитыПоказателя.Свойства.Выгрузить().ВыгрузитьКолонку("ИмяСвойства"));
	
КонецПроцедуры

#КонецОбласти