#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Протоколирование работы пользователей
	ПротоколированиеРаботыСотрудников.ЗаписатьОткрытие(Объект.Ссылка);
	
	ТолькоПросмотр = Объект.Предопределенный;
	Элементы.ПояснениеПредопределенного.Видимость = Объект.Предопределенный;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Объект.Ссылка.Пустая() И Не ЗначениеЗаполнено(Объект.ТипОбъекта)
		И Параметры.Свойство("ПараметрыВыбора")
		И Параметры.ПараметрыВыбора.Свойство("ТипОбъекта") Тогда
		Объект.ТипОбъекта = Параметры.ПараметрыВыбора.ТипОбъекта;
	КонецЕсли;
	
	Если Объект.ТипОбъекта = Перечисления.ТипыОбъектов.ЗадачиПроцессов 
		Или Объект.ТипОбъекта = Перечисления.ТипыОбъектов.НаименованияОписанияЗадач Тогда
		Элементы.ТипОбъекта.СписокВыбора.Очистить();
		Элементы.ТипОбъекта.СписокВыбора.Добавить(Объект.ТипОбъекта);
		Элементы.ТипОбъекта.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Контекст = Новый Соответствие;
	Контекст.Вставить("Объект");
	Контекст.Вставить("Результат");
	РедакторВстроенногоЯзыка.ПриСозданииНаСервере(ЭтотОбъект, "Редактор", Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	РедакторВстроенногоЯзыкаКлиент.ПриОткрытии(
		Элементы.РедакторВстроенногоЯзыка,
		Элементы.Выражение,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Объект.Выражение = РедакторВстроенногоЯзыкаКлиент.ПолучитьТекст(
		Элементы.РедакторВстроенногоЯзыка, Объект.Выражение);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// Протоколирование работы пользователей
	ПротоколированиеРаботыСотрудников.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыСотрудников.ЗаписатьИзменение(Объект.Ссылка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РедакторВстроенногоЯзыкаДокументСформирован(Элемент)
	
	Если РедакторВстроенногоЯзыкаКлиент.ИспользоватьРедактор(ЭтотОбъект) И ПустаяСтрока(Объект.Выражение) Тогда
		Объект.Выражение =
			НСтр("ru = '// Объект - СправочникОбъект.ДокументПредприятия, СправочникОбъект.Мероприятия;
				|// Результат - возвращаемый результат.'");
	КонецЕсли;
	Если РедакторВстроенногоЯзыкаКлиент.ДокументСформирован(
			Элемент, ЭтотОбъект, Объект.Выражение, Объект.Предопределенный) Тогда
		ПодключитьОбработчикОжидания("ПослеЗагрузкиРедактора", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедакторВстроенногоЯзыкаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	НажатаКнопкаEsc = Ложь;
	РедакторВстроенногоЯзыкаКлиент.ПриНажатии(
		Элемент,
		ДанныеСобытия,
		СтандартнаяОбработка,
		ЭтотОбъект,
		НажатаКнопкаEsc);
	Если НажатаКнопкаEsc Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеЗагрузкиРедактора()
	
	РедакторВстроенногоЯзыкаКлиент.ПослеЗагрузкиРедактора(Элементы.РедакторВстроенногоЯзыка, ЭтотОбъект);
	
КонецПроцедуры


#КонецОбласти