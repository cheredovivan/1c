
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ОбработчикиОбновления

// Процедура обновления на 3.0.15
// 
// Параметры:
//  Параметры - Структура - Параметры обработчика:
//   * ПрогрессВыполнения - Структура:
//      ** ОбработаноОбъектов - Число.
//      ** ВсегоОбъектов - Число.
//   * ОбработкаЗавершена - Булево
//
Процедура ОбновитьРодительОбъектаГруппКонтактов(Параметры) Экспорт
	
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(АдреснаяКнига.Ссылка) КАК ВсегоОбъектов
		|ИЗ
		|	Справочник.АдреснаяКнига КАК АдреснаяКнига
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.АдреснаяКнига КАК АдреснаяКнигаРодитель
		|		ПО АдреснаяКнига.Родитель = АдреснаяКнигаРодитель.Ссылка
		|ГДЕ
		|	АдреснаяКнига.ТипДанныхОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыДанныхАдреснойКниги.ГруппаКонтактовПользователей)
		|	И АдреснаяКнига.РодительОбъекта = НЕОПРЕДЕЛЕНО
		|	И ТИПЗНАЧЕНИЯ(АдреснаяКнига.ОбъектДоступа) = ТИП(Справочник.Сотрудники)");
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Параметры.ПрогрессВыполнения.ВсегоОбъектов = Выборка.ВсегоОбъектов;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбработаноОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 10000
	|	АдреснаяКнига.Ссылка КАК Ссылка,
	|	АдреснаяКнигаРодитель.Объект КАК ОбъектРодителя,
	|	АдреснаяКнига.ОбъектДоступа КАК Сотрудник
	|ИЗ
	|	Справочник.АдреснаяКнига КАК АдреснаяКнига
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.АдреснаяКнига КАК АдреснаяКнигаРодитель
	|		ПО АдреснаяКнига.Родитель = АдреснаяКнигаРодитель.Ссылка
	|ГДЕ
	|	АдреснаяКнига.ТипДанныхОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыДанныхАдреснойКниги.ГруппаКонтактовПользователей)
	|	И АдреснаяКнига.РодительОбъекта = НЕОПРЕДЕЛЕНО
	|	И ТИПЗНАЧЕНИЯ(АдреснаяКнига.ОбъектДоступа) = ТИП(Справочник.Сотрудники)");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Ссылка = Выборка.Ссылка;
			Объект = Ссылка.ПолучитьОбъект(); 
			НовыйОбъектРодителя = Выборка.ОбъектРодителя;
			Если НовыйОбъектРодителя = "Избранное" Тогда
				НовыйОбъектРодителя = Справочники.ГруппыКонтактовПользователей.ПолучитьКорневуюГруппу(Выборка.Сотрудник);
			КонецЕсли;
			Объект.РодительОбъекта = НовыйОбъектРодителя;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(Объект);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось установить родителя группы контактов пользователей %1 по причине:
				|%2'"),
				Выборка.Ссылка,
				ПодробноеПредставлениеОшибки); 
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				Выборка.Ссылка.Метаданные(),
				Выборка.Ссылка,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбработаноОбъектов = 0 И ПроблемныхОбъектов > 0 Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Процедуре ПерейтиНаВерсию_3_0_15_30 не удалось установить родителя для нескольких групп контактов: %1'"),
			ПроблемныхОбъектов);
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = 
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ОбработаноОбъектов;
	Параметры.ОбработкаЗавершена = (ОбработаноОбъектов = 0);
	
КонецПроцедуры

#Область ПерейтиНаВерсию_3_0_17_24

// Регистрирует данные к обновлению в плане обмена ОбновлениеИнформационнойБазы
//  см. Стандарты и методики разработки прикладных решений: Параллельный режим отложенного обновления.
//
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию_3_0_17_24(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.Справочники.АдреснаяКнига;
	
	ПолныеИменаОбъектов = Новый Массив;
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	ПолныеИменаОбъектов.Добавить(ПолноеИмяОбъекта);
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	ПараметрыВыборки.ПолныеИменаОбъектов = СтрСоединить(ПолныеИменаОбъектов, ",");
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ОбновляемаяТаблица.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.АдреснаяКнига КАК ОбновляемаяТаблица
		|ГДЕ
		|	ОбновляемаяТаблица.Ссылка > &Ссылка
		|	И (ОбновляемаяТаблица.ПометкаУдаления = ЛОЖЬ
		|		ИЛИ ОбновляемаяТаблица.ОтображатьВАдреснойКниге = ИСТИНА)
		|	И (
		|	// Удаляем участников мероприятия из адресной книги.
		|	ОбновляемаяТаблица.РодительОбъекта ССЫЛКА Справочник.Мероприятия
		|	// Удаляем мероприятия из адресной книги.
		|	ИЛИ ОбновляемаяТаблица.Объект ССЫЛКА Справочник.Мероприятия
		|	// Удаляем папки мероприятий из адресной книги.
		|	ИЛИ ОбновляемаяТаблица.Объект ССЫЛКА Справочник.ПапкиМероприятий
		|	// Удаляем удаленные разделы, ожидаем ""По мероприятим"".
		|	ИЛИ (ОбновляемаяТаблица.ТипДанныхОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыДанныхАдреснойКниги.Раздел)
		|	И ОбновляемаяТаблица.ИмяПредопределенныхДанных = """"))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка");
	
	СсылкаКОбновлению = ПустаяСсылка();
	ЕстьДанныеКОбработке = Истина;
	Пока ЕстьДанныеКОбработке Цикл
		
		Запрос.УстановитьПараметр("Ссылка", СсылкаКОбновлению);
		
		//@skip-check query-in-loop
		ДанныеКОбработке = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		КоличествоКОбработке = ДанныеКОбработке.Количество();
		ЕстьДанныеКОбработке = КоличествоКОбработке > 0;
		Если ЕстьДанныеКОбработке Тогда
			СсылкаКОбновлению = ДанныеКОбработке[КоличествоКОбработке - 1];
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДанныеКОбработке);
		
	КонецЦикла;
	
КонецПроцедуры

// Обрабатывает данные, зарегистрированные в плане обмена ОбновлениеИнформационнойБазы
//  см. Стандарты и методики разработки прикладных решений: Параллельный режим отложенного обновления.
//
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию_3_0_17_24(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.Справочники.АдреснаяКнига;
	
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	ТаблицаКОбновления = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	РазделыЭлементов = РазделыАдреснойКнигиЭлементов(ТаблицаКОбновления);
	Для Каждого СтрокаКОбновлению Из ТаблицаКОбновления Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.АдреснаяКнига");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", СтрокаКОбновлению.Ссылка);
			Блокировка.Заблокировать();
			
			АдреснаяКнигаОбъект = СтрокаКОбновлению.Ссылка.ПолучитьОбъект();
			Если АдреснаяКнигаОбъект <> Неопределено Тогда
				Если Не АдреснаяКнигаОбъект.ПометкаУдаления Или АдреснаяКнигаОбъект.ОтображатьВАдреснойКниге Тогда
					АдреснаяКнигаОбъект.ОтображатьВАдреснойКниге = Ложь;
					АдреснаяКнигаОбъект.ПометкаУдаления = Истина;
				КонецЕсли;
				
				Если Не АдреснаяКнигаОбъект.Предопределенный И Не ЗначениеЗаполнено(АдреснаяКнигаОбъект.Раздел) Тогда
					АдреснаяКнигаОбъект.Раздел = РазделыЭлементов[СтрокаКОбновлению.Ссылка];
				КонецЕсли;
				
				Если АдреснаяКнигаОбъект.Модифицированность() Тогда
					ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьОбъект(АдреснаяКнигаОбъект);
				Иначе
					ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(АдреснаяКнигаОбъект.Ссылка);
				КонецЕсли;
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(СтрокаКОбновлению.Ссылка);
			КонецЕсли;
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			Если ТранзакцияАктивна() Тогда
				ВызватьИсключение;
			КонецЕсли;
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				МетаданныеОбъекта,
				СтрокаКОбновлению.Ссылка,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось обработать некоторые объекты (пропущены): %1'"), 
			ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			МетаданныеОбъекта,,
			СтрШаблон(
				НСтр("ru = 'Обработана очередная порция: %1'"),
				ОбъектовОбработано));
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = ОбъектовОбработано;
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
		Параметры.Очередь,
		ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#Область ПерейтиНаВерсию_3_0_18_11

// Регистрирует данные к обновлению в плане обмена ОбновлениеИнформационнойБазы
// см. Стандарты и методики разработки прикладных решений: Параллельный режим отложенного обновления.
//
// Параметры:
//	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию_3_0_18_11(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.Справочники.АдреснаяКнига;
	
	ПолныеИменаОбъектов = Новый Массив;
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	ПолныеИменаОбъектов.Добавить(ПолноеИмяОбъекта);
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	ПараметрыВыборки.ПолныеИменаОбъектов = СтрСоединить(ПолныеИменаОбъектов, ",");
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ОбновляемаяТаблица.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.АдреснаяКнига КАК ОбновляемаяТаблица
		|ГДЕ
		|	ОбновляемаяТаблица.Ссылка > &Ссылка
		|	И НЕ ОбновляемаяТаблица.Предопределенный
		|	И ОбновляемаяТаблица.Раздел = &ПустаяСсылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка");
	
	СсылкаКОбновлению = ПустаяСсылка();
	ЕстьДанныеКОбработке = Истина;
	Пока ЕстьДанныеКОбработке Цикл
		
		Запрос.УстановитьПараметр("Ссылка", СсылкаКОбновлению);
		Запрос.УстановитьПараметр("ПустаяСсылка", ПустаяСсылка());
		
		//@skip-check query-in-loop
		ДанныеКОбработке = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		КоличествоКОбработке = ДанныеКОбработке.Количество();
		ЕстьДанныеКОбработке = КоличествоКОбработке > 0;
		Если ЕстьДанныеКОбработке Тогда
			СсылкаКОбновлению = ДанныеКОбработке[КоличествоКОбработке - 1];
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДанныеКОбработке);
		
	КонецЦикла;
	
КонецПроцедуры

// Обрабатывает данные, зарегистрированные в плане обмена ОбновлениеИнформационнойБазы
// см. Стандарты и методики разработки прикладных решений: Параллельный режим отложенного обновления.
//
// Параметры:
//	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию_3_0_18_11(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.Справочники.АдреснаяКнига;
	
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	ТаблицаКОбновления = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	РазделыЭлементов = РазделыАдреснойКнигиЭлементов(ТаблицаКОбновления);
	Для Каждого СтрокаКОбновлению Из ТаблицаКОбновления Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.АдреснаяКнига");
			ЭлементБлокировки.ИсточникДанных = ТаблицаКОбновления;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
			Блокировка.Заблокировать();
			
			АдреснаяКнигаОбъект = СтрокаКОбновлению.Ссылка.ПолучитьОбъект();
			Если АдреснаяКнигаОбъект <> Неопределено Тогда
				Если Не ЗначениеЗаполнено(АдреснаяКнигаОбъект.Раздел) Тогда
					АдреснаяКнигаОбъект.Раздел = РазделыЭлементов[СтрокаКОбновлению.Ссылка];
				КонецЕсли;
				
				Если (Не АдреснаяКнигаОбъект.ПометкаУдаления Или АдреснаяКнигаОбъект.ОтображатьВАдреснойКниге)
						И (ТипЗнч(АдреснаяКнигаОбъект.РодительОбъекта) = Тип("СправочникСсылка.Мероприятия")
							Или ТипЗнч(АдреснаяКнигаОбъект.Объект) = Тип("СправочникСсылка.Мероприятия")
							Или ТипЗнч(АдреснаяКнигаОбъект.Объект) = Тип("СправочникСсылка.ПапкиМероприятий")
							Или (АдреснаяКнигаОбъект.ТипДанныхОбъекта = Перечисления.ТипыДанныхАдреснойКниги.Раздел)
									И Не ЗначениеЗаполнено(АдреснаяКнигаОбъект.ИмяПредопределенныхДанных)) Тогда
					
					АдреснаяКнигаОбъект.ОтображатьВАдреснойКниге = Ложь;
					АдреснаяКнигаОбъект.ПометкаУдаления = Истина;
				КонецЕсли;
				
				Если АдреснаяКнигаОбъект.Модифицированность() Тогда
					ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьОбъект(АдреснаяКнигаОбъект);
				Иначе
					ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(АдреснаяКнигаОбъект.Ссылка);
				КонецЕсли;
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(СтрокаКОбновлению.Ссылка);
			КонецЕсли;
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			Если ТранзакцияАктивна() Тогда
				ВызватьИсключение;
			КонецЕсли;
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				МетаданныеОбъекта,
				СтрокаКОбновлению.Ссылка,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось обработать некоторые объекты (пропущены): %1'"), 
			ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			МетаданныеОбъекта,,
			СтрШаблон(
				НСтр("ru = 'Обработана очередная порция: %1'"),
				ОбъектовОбработано));
	КонецЕсли;
	
	ОбработкаЗавершена =
		ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
	Если ОбработкаЗавершена Тогда
		Константы.ПараметрыОбновленияАдреснойКниги.УстановитьИспользованиеОбновленнойАрхитектурыАдреснойКниги(Истина);
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = ОбъектовОбработано;
	Параметры.ОбработкаЗавершена = ОбработкаЗавершена;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

// Обновляет данные адресной книги по объекту.
//
// Параметры:
//	Объект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект-источник данных адресной книги.
//	Родитель - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Родитель объекта-источника данных адресной книги.
//	Раздел - СправочникСсылка.АдреснаяКнига - Раздел адресной книги, в котором необходимо обновить данные.
//	ОбъектДоступа - ОпределяемыйТип.ОбъектДоступаАдреснойКниги - Объект, определяющий доступ к данным адресной книги.
//
Процедура ОбновитьДанныеОбъекта(Объект, Родитель, Раздел, ОбъектДоступа = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ОбщегоНазначения.ЗначениеСсылочногоТипа(Объект)
			И Не ОбщегоНазначения.СсылкаСуществует(Объект) Тогда
		Возврат;
	КонецЕсли;
	
	ДопустимыеТипы = Метаданные.Справочники.АдреснаяКнига.Реквизиты.Объект.Тип;
	ОбъектДопустимогоТипа = ДопустимыеТипы.ПривестиЗначение(Объект);
	
	ИменаТиповОбновляемыхОбъектов = Новый Массив;
	ИменаТиповОбновляемыхОбъектов.Добавить(ИмяТипаОбъектаАдреснойКниги(ОбъектДопустимогоТипа));

	ТекстЗапроса = ТекстЗапросаОбновлениеОбъектаАдреснойКниги(ИменаТиповОбновляемыхОбъектов);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Объект", ОбъектДопустимогоТипа);
	Запрос.УстановитьПараметр("Родитель", Родитель);
	Запрос.УстановитьПараметр("РодительУказан", ЗначениеЗаполнено(Родитель));
	Запрос.УстановитьПараметр("Раздел", Раздел);
	Запрос.УстановитьПараметр("РазделУказан", ЗначениеЗаполнено(Раздел));
	Запрос.УстановитьПараметр("ОбъектДоступа", ОбъектДоступа);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Ссылка) Тогда
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
			ОбъектВАдреснойКниге = Выборка.Ссылка.ПолучитьОбъект();
			ОбъектВАдреснойКниге.Родитель = Выборка.Родитель;
			ОбъектВАдреснойКниге.Объект = Выборка.Объект;
			ОбъектВАдреснойКниге.ОбъектДоступа = Выборка.ОбъектДоступа;
			ОбъектВАдреснойКниге.Раздел = Выборка.Раздел;
			ОбъектВАдреснойКниге.ОтображатьВАдреснойКниге = Выборка.ОтображатьВАдреснойКниге;
			ОбъектВАдреснойКниге.ПредставлениеОбъекта = ПредставлениеОбъектаАдреснойКниги(
				Выборка.ПредставлениеОбъекта, Выборка.ДополнительноеОписание, Выборка.ПредставлениеОбъектаОсобое);
			УстановитьПорядокОбъектаВСписке(ОбъектВАдреснойКниге, Выборка.Объект, Выборка.Родитель);
			ОбъектВАдреснойКниге.Записать();
			РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Иначе
			ОбъектВАдреснойКниге = СоздатьЭлемент();
			ОбъектВАдреснойКниге.Родитель = Выборка.Родитель;
			ОбъектВАдреснойКниге.Объект = Выборка.Объект;
			ОбъектВАдреснойКниге.ОбъектДоступа = Выборка.ОбъектДоступа;
			ОбъектВАдреснойКниге.Раздел = Выборка.Раздел;
			ОбъектВАдреснойКниге.ОтображатьВАдреснойКниге = Выборка.ОтображатьВАдреснойКниге;
			ОбъектВАдреснойКниге.ПредставлениеОбъекта = ПредставлениеОбъектаАдреснойКниги(
				Выборка.ПредставлениеОбъекта, Выборка.ДополнительноеОписание, Выборка.ПредставлениеОбъектаОсобое);
			УстановитьПорядокОбъектаВСписке(ОбъектВАдреснойКниге, Выборка.Объект, Выборка.Родитель);
			ОбъектВАдреснойКниге.Записать();
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

// Обновляет данные адресной книги по списку подчиненных объектов.
//
// Параметры:
//	Объект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект-источник данных адресной книги.
//	Родитель - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Родитель объекта-источника данных адресной книги.
//	СписокПодчиненных - Массив Из Произвольный - Список подчиненных объектов.
//	Раздел - СправочникСсылка.АдреснаяКнига - Раздел адресной книги, в котором необходимо обновить данные.
//	ОбъектДоступа - ОпределяемыйТип.ОбъектДоступаАдреснойКниги - Объект, определяющий доступ к данным адресной книги.
//	ОбновитьТолькоСоставПодчиненных - Булево - Признак необходимости обновления только состава подчиненных объектов.
//
Процедура ОбновитьСписокПодчиненныхОбъектов(
		Объект, Родитель, Знач СписокПодчиненных, Раздел,
		ОбъектДоступа = Неопределено, ОбновитьТолькоСоставПодчиненных = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ОбщегоНазначения.ЗначениеСсылочногоТипа(Объект)
			И Не ОбщегоНазначения.СсылкаСуществует(Объект) Тогда
		Возврат;
	КонецЕсли;
	
	ДопустимыеТипы = Метаданные.Справочники.АдреснаяКнига.Реквизиты.Объект.Тип;
	ОбъектДопустимогоТипа = ДопустимыеТипы.ПривестиЗначение(Объект);
	
	ИменаТиповОбновляемыхОбъектов = Новый Массив;
	ИменаТиповОбновляемыхОбъектов.Добавить(ИмяТипаОбъектаАдреснойКниги(ОбъектДопустимогоТипа));
	
	ПодчиненныеЭлементы = НоваяТаблицаЭлементовАдреснойКниги();
	Для Каждого Подчиненный Из СписокПодчиненных Цикл
		ПодчиненныйДопустимогоТипа = ДопустимыеТипы.ПривестиЗначение(Подчиненный);
		
		СтрокаТаблицы = ПодчиненныеЭлементы.Добавить();
		СтрокаТаблицы.Объект = ПодчиненныйДопустимогоТипа;
		СтрокаТаблицы.ОбъектДоступа = ОбъектДоступа;
		СтрокаТаблицы.Раздел = Раздел;
		
		ИменаТиповОбновляемыхОбъектов.Добавить(ИмяТипаОбъектаАдреснойКниги(ПодчиненныйДопустимогоТипа));
	КонецЦикла;
	ИменаТиповОбновляемыхОбъектов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ИменаТиповОбновляемыхОбъектов);
	
	ТекстЗапроса = ТекстЗапросаОбновлениеСпискаПодчиненныхОбъектов(ИменаТиповОбновляемыхОбъектов);
	
	НачатьТранзакцию();
	Попытка
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("ВнешнийИсточник", ПодчиненныеЭлементы);
		Запрос.УстановитьПараметр("Объект", ОбъектДопустимогоТипа);
		Запрос.УстановитьПараметр("Родитель", Родитель);
		Запрос.УстановитьПараметр("РодительУказан", ЗначениеЗаполнено(Родитель));
		Запрос.УстановитьПараметр("Раздел", Раздел);
		Запрос.УстановитьПараметр("РазделУказан", ЗначениеЗаполнено(Раздел));
		Запрос.УстановитьПараметр("ОбъектДоступа", ОбъектДоступа);
		Запрос.УстановитьПараметр("ОбновитьТолькоСоставПодчиненных", ОбновитьТолькоСоставПодчиненных);
		
		ПакетРезультатов = Запрос.ВыполнитьПакет();
		
		// Удаление старых
		Выборка = ПакетРезультатов[ПакетРезультатов.ВГраница() - 2].Выбрать();
		Пока Выборка.Следующий() Цикл
			ОбъектАдреснойКниги = Выборка.Ссылка.ПолучитьОбъект();
			ОбъектАдреснойКниги.ОбменДанными.Загрузка = Истина;
			ОбъектАдреснойКниги.Удалить();
		КонецЦикла;
		
		// Добавление новых
		Выборка = ПакетРезультатов[ПакетРезультатов.ВГраница() - 1].Выбрать();
		Пока Выборка.Следующий() Цикл
			НовыйПодчиненный = СоздатьЭлемент();
			НовыйПодчиненный.Родитель = Выборка.Родитель;
			НовыйПодчиненный.Объект = Выборка.Объект;
			НовыйПодчиненный.РодительОбъекта = Выборка.РодительОбъекта;
			НовыйПодчиненный.ОбъектДоступа = Выборка.ОбъектДоступа;
			НовыйПодчиненный.Раздел = Выборка.Раздел;
			НовыйПодчиненный.ОтображатьВАдреснойКниге = Выборка.ОтображатьВАдреснойКниге;
			НовыйПодчиненный.ПредставлениеОбъекта = ПредставлениеОбъектаАдреснойКниги(
				Выборка.ПредставлениеОбъекта, Выборка.ДополнительноеОписание, Выборка.ПредставлениеОбъектаОсобое);
			УстановитьПорядокОбъектаВСписке(НовыйПодчиненный, Выборка.Объект, Выборка.Родитель);
			НовыйПодчиненный.Записать();
		КонецЦикла;
		
		// Обновление строк старых подчиненных
		Выборка = ПакетРезультатов[ПакетРезультатов.ВГраница()].Выбрать();
		Пока Выборка.Следующий() Цикл
			ПодчиненныйОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ПодчиненныйОбъект.Раздел = Выборка.Раздел;
			ПодчиненныйОбъект.ОбъектДоступа = Выборка.ОбъектДоступа;
			ПодчиненныйОбъект.ОтображатьВАдреснойКниге = Выборка.ОтображатьВАдреснойКниге;
			ПодчиненныйОбъект.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Обнвляет данные отображения объектов адресной книги.
//
// Параметры:
//	Объект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект-источник данных адресной книги.
//
Процедура ОбновитьДанныеОтображенияПодчиненногоОбъекта(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ОбщегоНазначения.ЗначениеСсылочногоТипа(Объект)
			И Не ОбщегоНазначения.СсылкаСуществует(Объект) Тогда
		Возврат;
	КонецЕсли;
	
	ДопустимыеТипы = Метаданные.Справочники.АдреснаяКнига.Реквизиты.Объект.Тип;
	ОбъектДопустимогоТипа = ДопустимыеТипы.ПривестиЗначение(Объект);	
	
	Разделы = Новый Массив;
	Разделы.Добавить(Избранное);
	
	ТипОбъекта = ТипЗнч(ОбъектДопустимогоТипа);
	Если ТипОбъекта = Тип("СправочникСсылка.Сотрудники")
			Или ТипОбъекта = Тип("СправочникСсылка.Пользователи")
			Или ТипОбъекта = Тип("СправочникСсылка.РолиИсполнителей") Тогда
		
		Разделы.Добавить(Сотрудники);
		Разделы.Добавить(ПоСтруктуреПредприятия);
		Разделы.Добавить(ПоРабочимГруппам);
		Разделы.Добавить(ПоПроектам);
		Разделы.Добавить(ВсеПользователи);
	КонецЕсли;
	
	ИменаТиповОбновляемыхОбъектов = Новый Массив;
	ИменаТиповОбновляемыхОбъектов.Добавить(ИмяТипаОбъектаАдреснойКниги(ОбъектДопустимогоТипа));
	
	ТекстЗапроса = ТекстЗапросаОбновлениеДанныхОтображенияПодчиненныхОбъектов(ИменаТиповОбновляемыхОбъектов);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Объект", ОбъектДопустимогоТипа);
	Запрос.УстановитьПараметр("РодительУказан", Ложь);
	Запрос.УстановитьПараметр("Раздел", Разделы);
	Запрос.УстановитьПараметр("РазделУказан", ЗначениеЗаполнено(Разделы));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		ОбъектВАдреснойКниге = Выборка.Ссылка.ПолучитьОбъект();
		ОбъектВАдреснойКниге.ОтображатьВАдреснойКниге = Выборка.ОтображатьВАдреснойКниге;
		ОбъектВАдреснойКниге.ПредставлениеОбъекта = ПредставлениеОбъектаАдреснойКниги(
			Выборка.ПредставлениеОбъекта, Выборка.ДополнительноеОписание, Выборка.ПредставлениеОбъектаОсобое);
		ОбъектВАдреснойКниге.Записать();
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

// Добавляет в список сотрудников роли, исполнителями которых являются сотрудники из этого же списка.
//
// Параметры:
//	СписокСотрудников - Массив Из СправочникСсылка.Сотрудники,
//						Массив Из СправочникСсылка.Пользователи - Список сотрудников.
//	ВключатьТолькоРолиСотрудниковИзСписка - Булево - Включать в список только роли сотрудников из списка.
//
Процедура РасширитьСписокСотрудниковРолями(СписокСотрудников, ВключатьТолькоРолиСотрудниковИзСписка = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Если ВключатьТолькоРолиСотрудниковИзСписка Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ИсполнителиРолей.РольИсполнителя.Владелец КАК Роль
			|ИЗ
			|	РегистрСведений.ИсполнителиРолей КАК ИсполнителиРолей
			|ГДЕ
			|	ИсполнителиРолей.Исполнитель В (&СписокПользователей)";
		Запрос.УстановитьПараметр("СписокПользователей", СписокСотрудников);
	Иначе
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	РолиИсполнителей.Ссылка КАК Роль
			|ИЗ
			|	Справочник.РолиИсполнителей КАК РолиИсполнителей";
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СписокСотрудников.Добавить(Выборка.Роль);
	КонецЦикла;
	
КонецПроцедуры

// Удаляет объекты адресной книги по объекту-источнику.
//
// Параметры:
//	Объект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект-источник данных адресной книги.
//	Родитель - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Родитель объекта-источника данных адресной книги.
//	СсылкаНаРаздел - СправочникСсылка.АдреснаяКнига - Раздел адресной книги, в котором необходимо удалить данные.
//
Процедура УдалитьОбъектыАдреснойКниги(Объект, Родитель = Неопределено, СсылкаНаРаздел = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбъектыАдреснойКниги = СсылкиНаОбъектыАдреснойКниги(Объект, СсылкаНаРаздел);
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.АдреснаяКнига");
		Для Каждого СсылкаНаОбъект Из ОбъектыАдреснойКниги Цикл
			ЭлементБлокировки.УстановитьЗначение("Ссылка", СсылкаНаОбъект);
		КонецЦикла;
		Блокировка.Заблокировать();
		
		Для Каждого СсылкаНаОбъект Из ОбъектыАдреснойКниги Цикл
			ЗаблокироватьДанныеДляРедактирования(СсылкаНаОбъект);
			
			ОбъектВАдреснойКниге = СсылкаНаОбъект.ПолучитьОбъект();
			Если ОбъектВАдреснойКниге = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ОбъектВАдреснойКниге.ОбменДанными.Загрузка = Истина;
			ОбъектВАдреснойКниге.Удалить();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Выполняет полное обновление адресной книги.
//
Процедура Заполнить() Экспорт
	
	УстановитьПризнакИспользованияОбновленнойАрхитектуры();
	ОчиститьАдреснуюКнигу();
	ЗаполнитьАдреснуюКнигу();
	РегистрыСведений.ОбъектыПоискаВАдреснойКниге.ЗаполнитьСловаПоиска();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает ссылки на объекты адресной книги.
//
// Параметры:
//	Объект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект-источник данных адресной книги.
//	СсылкаНаРаздел - СправочникСсылка.АдреснаяКнига - Раздел адресной книги, из которого необходимо получить объекты.
//
// Возвращаемое значение:
//	Массив Из СправочникСсылка.АдреснаяКнига - Ссылки на объекты.
//
Функция СсылкиНаОбъектыАдреснойКниги(Объект, СсылкаНаРаздел = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаСсылкиНаОбъектыАдреснойКниги(ВариантыВыборкиДанныхОбъекта().СсылкиНаОбъекты);
	Запрос.УстановитьПараметр("Объект", Объект);
	Запрос.УстановитьПараметр("РодительУказан", Ложь);
	Запрос.УстановитьПараметр("Раздел", СсылкаНаРаздел);
	Запрос.УстановитьПараметр("РазделУказан", ЗначениеЗаполнено(СсылкаНаРаздел));
	
	СсылкиНаОбъекты = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат СсылкиНаОбъекты;
	
КонецФункции

// Возвращает ссылку на объект адресной книги.
//
// Параметры:
//	Объект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект-источник данных адресной книги.
//	СсылкаНаРаздел - СправочникСсылка.АдреснаяКнига - Раздел адресной книги, из которого необходимо получить объект.
//
// Возвращаемое значение:
//	СправочникСсылка.АдреснаяКнига - Ссылка на объект.
//
Функция СсылкаНаОбъектВАдреснойКниге(Объект, СсылкаНаРаздел = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СсылкаНаОбъект = ПустаяСсылка();
	
	СсылкиНаОбъекты = СсылкиНаОбъектыАдреснойКниги(Объект, СсылкаНаРаздел);
	Если ЗначениеЗаполнено(СсылкиНаОбъекты) Тогда
		СсылкаНаОбъект = СсылкиНаОбъекты[0];
	КонецЕсли;
	
	Возврат СсылкаНаОбъект;
	
КонецФункции

// Заполняет предопределенные разделы адресной книги.
//
Процедура ЗаполнитьПредопределенныеРазделы() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		
		// Начальное заполнение разделов
		РазделАдреснойКниги = Избранное.ПолучитьОбъект();
		РазделАдреснойКниги.Объект = НСтр("ru = 'Избранное'");
		РазделАдреснойКниги.ОтображатьВАдреснойКниге = Истина;
		РазделАдреснойКниги.ПредставлениеОбъекта = РазделАдреснойКниги.Объект;
		РазделАдреснойКниги.ПорядокОбъектаВСписке = -13;
		РазделАдреснойКниги.Записать();
		
		РазделАдреснойКниги = Организации.ПолучитьОбъект();
		РазделАдреснойКниги.Объект = РедакцииКонфигурацииКлиентСервер.Организации();
		РазделАдреснойКниги.ОтображатьВАдреснойКниге = Истина;
		РазделАдреснойКниги.ПредставлениеОбъекта = РазделАдреснойКниги.Объект;
		РазделАдреснойКниги.ПорядокОбъектаВСписке = -12;
		РазделАдреснойКниги.Записать();
		
		РазделАдреснойКниги = Сотрудники.ПолучитьОбъект();
		РазделАдреснойКниги.Объект = НСтр("ru = 'Сотрудники'");
		РазделАдреснойКниги.ОтображатьВАдреснойКниге = Истина;
		РазделАдреснойКниги.ПредставлениеОбъекта = РазделАдреснойКниги.Объект;
		РазделАдреснойКниги.ПорядокОбъектаВСписке = -11;
		РазделАдреснойКниги.Записать();
		
		РазделАдреснойКниги = ПоСтруктуреПредприятия.ПолучитьОбъект();
		РазделАдреснойКниги.Объект = НСтр("ru = 'По подразделениям'");
		РазделАдреснойКниги.ОтображатьВАдреснойКниге = Истина;
		РазделАдреснойКниги.ПредставлениеОбъекта = РазделАдреснойКниги.Объект;
		РазделАдреснойКниги.ПорядокОбъектаВСписке = -10;
		РазделАдреснойКниги.Записать();
		
		РазделАдреснойКниги = ПоРабочимГруппам.ПолучитьОбъект();
		РазделАдреснойКниги.Объект = НСтр("ru = 'По рабочим группам'");
		РазделАдреснойКниги.ОтображатьВАдреснойКниге = Истина;
		РазделАдреснойКниги.ПредставлениеОбъекта = РазделАдреснойКниги.Объект;
		РазделАдреснойКниги.ПорядокОбъектаВСписке = -9;
		РазделАдреснойКниги.Записать();
		
		РазделАдреснойКниги = ПоПроектам.ПолучитьОбъект();
		РазделАдреснойКниги.Объект = НСтр("ru = 'По проектам'");
		РазделАдреснойКниги.ОтображатьВАдреснойКниге = Истина;
		РазделАдреснойКниги.ПредставлениеОбъекта = РазделАдреснойКниги.Объект;
		РазделАдреснойКниги.ПорядокОбъектаВСписке = -7;
		РазделАдреснойКниги.Записать();
		
		РазделАдреснойКниги = АвтоподстановкиДляПроцессов.ПолучитьОбъект();
		РазделАдреснойКниги.Объект = НСтр("ru = 'Автоподстановки'");
		РазделАдреснойКниги.ОтображатьВАдреснойКниге = Истина;
		РазделАдреснойКниги.ПредставлениеОбъекта = РазделАдреснойКниги.Объект;
		РазделАдреснойКниги.ПорядокОбъектаВСписке = -6;
		РазделАдреснойКниги.Записать();
		
		РазделАдреснойКниги = АвтоподстановкиДляДокументов.ПолучитьОбъект();
		РазделАдреснойКниги.Объект = НСтр("ru = 'Автоподстановки'");
		РазделАдреснойКниги.ОтображатьВАдреснойКниге = Истина;
		РазделАдреснойКниги.ПредставлениеОбъекта = РазделАдреснойКниги.Объект;
		РазделАдреснойКниги.ПорядокОбъектаВСписке = -5;
		РазделАдреснойКниги.Записать();
		
		РазделАдреснойКниги = АвтоподстановкиДляЗадач.ПолучитьОбъект();
		РазделАдреснойКниги.Объект = НСтр("ru = 'Автоподстановки'");
		РазделАдреснойКниги.ОтображатьВАдреснойКниге = Истина;
		РазделАдреснойКниги.ПредставлениеОбъекта = РазделАдреснойКниги.Объект;
		РазделАдреснойКниги.ПорядокОбъектаВСписке = -5;
		РазделАдреснойКниги.Записать();
		
		РазделАдреснойКниги = АвтоподстановкиДляМероприятий.ПолучитьОбъект();
		РазделАдреснойКниги.Объект = НСтр("ru = 'Автоподстановки'");
		РазделАдреснойКниги.ОтображатьВАдреснойКниге = Истина;
		РазделАдреснойКниги.ПредставлениеОбъекта = РазделАдреснойКниги.Объект;
		РазделАдреснойКниги.ПорядокОбъектаВСписке = -5;
		РазделАдреснойКниги.Записать();
		
		РазделАдреснойКниги = АвтоподстановкиДляЗадачПроцессов.ПолучитьОбъект();
		РазделАдреснойКниги.Объект = НСтр("ru = 'Автоподстановки'");
		РазделАдреснойКниги.ОтображатьВАдреснойКниге = Истина;
		РазделАдреснойКниги.ПредставлениеОбъекта = РазделАдреснойКниги.Объект;
		РазделАдреснойКниги.ПорядокОбъектаВСписке = -5;
		РазделАдреснойКниги.Записать();
		
		РазделАдреснойКниги = ВсеАвтоподстановки.ПолучитьОбъект();
		РазделАдреснойКниги.Объект = НСтр("ru = 'Автоподстановки'");
		РазделАдреснойКниги.ОтображатьВАдреснойКниге = Истина;
		РазделАдреснойКниги.ПредставлениеОбъекта = РазделАдреснойКниги.Объект;
		РазделАдреснойКниги.ПорядокОбъектаВСписке = -5;
		РазделАдреснойКниги.Записать();
		
		РазделАдреснойКниги = Роли.ПолучитьОбъект();
		РазделАдреснойКниги.Объект = НСтр("ru = 'Роли'");
		РазделАдреснойКниги.ОтображатьВАдреснойКниге = Истина;
		РазделАдреснойКниги.ПредставлениеОбъекта = РазделАдреснойКниги.Объект;
		РазделАдреснойКниги.ПорядокОбъектаВСписке = -4;
		РазделАдреснойКниги.Записать();
		
		РазделАдреснойКниги = Контрагенты.ПолучитьОбъект();
		РазделАдреснойКниги.Объект = НСтр("ru = 'Контрагенты'");
		РазделАдреснойКниги.ОтображатьВАдреснойКниге = Истина;
		РазделАдреснойКниги.ПредставлениеОбъекта = РазделАдреснойКниги.Объект;
		РазделАдреснойКниги.ПорядокОбъектаВСписке = -3;
		РазделАдреснойКниги.Записать();
		
		РазделАдреснойКниги = ЛичныеАдресаты.ПолучитьОбъект();
		РазделАдреснойКниги.Объект = НСтр("ru = 'Личные адресаты'");
		РазделАдреснойКниги.ОтображатьВАдреснойКниге = Истина;
		РазделАдреснойКниги.ПредставлениеОбъекта = РазделАдреснойКниги.Объект;
		РазделАдреснойКниги.ПорядокОбъектаВСписке = -2;
		РазделАдреснойКниги.Записать();
		
		РазделАдреснойКниги = ВсеПользователи.ПолучитьОбъект();
		РазделАдреснойКниги.Объект = Справочники.РабочиеГруппы.ВсеПользователи;
		РазделАдреснойКниги.ОтображатьВАдреснойКниге = Истина;
		РазделАдреснойКниги.ПредставлениеОбъекта = РазделАдреснойКниги.Объект;
		РазделАдреснойКниги.ПорядокОбъектаВСписке = 0;
		РазделАдреснойКниги.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Заполняет адресную книгу.
//
Процедура ЗаполнитьАдреснуюКнигу() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаполнитьПредопределенныеРазделы();
	
	// Избранное
	Выборка = Справочники.ГруппыКонтактовПользователей.ВыбратьИерархически();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ОбщийСписокРассылки Тогда
			ОбъектДоступа = Выборка.Ссылка;
		Иначе
			ОбъектДоступа = Выборка.Автор;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.Родитель) Тогда
			РаботаСАдреснойКнигой.ОбновитьДанныеОбъекта(
				Выборка.Ссылка, Выборка.Родитель, Избранное, ОбъектДоступа);
		КонецЕсли;
		
		КонтактыГруппы = Новый Массив;
		Для Каждого СтрКонтакт Из Выборка.Контакты Цикл
			Если ЗначениеЗаполнено(СтрКонтакт.Контакт) Тогда
				КонтактыГруппы.Добавить(СтрКонтакт.Контакт);
			Иначе
				КонтактыГруппы.Добавить(СтрКонтакт.КонтактнаяИнформация);
			КонецЕсли;
		КонецЦикла;
		
		РаботаСАдреснойКнигой.ОбновитьСписокПодчиненныхОбъектов(
			Выборка.Ссылка, Выборка.Родитель, Избранное, КонтактыГруппы, ОбъектДоступа);
		
	КонецЦикла;
	Выборка = Неопределено;
	
	// По структуре предприятия
	Выборка = Справочники.СтруктураПредприятия.ВыбратьИерархически();
	Пока Выборка.Следующий() Цикл
		
		РаботаСАдреснойКнигой.ОбновитьДанныеОбъекта(
			Выборка.Ссылка, Выборка.Родитель, ПоСтруктуреПредприятия);
		
		МодульСотрудники = ОбщегоНазначения.ОбщийМодуль("Сотрудники");
		СотрудникиПодразделения = МодульСотрудники.СотрудникиПодразделения(Выборка.Ссылка, Ложь);
		
		//@skip-check query-in-loop
		РасширитьСписокСотрудниковРолями(СотрудникиПодразделения);
		
		РаботаСАдреснойКнигой.ОбновитьСписокПодчиненныхОбъектов(
			Выборка.Ссылка, Выборка.Родитель, ПоСтруктуреПредприятия, СотрудникиПодразделения);
		
	КонецЦикла;
	Выборка = Неопределено;
	
	// По рабочим группам
	Выборка = Справочники.РабочиеГруппы.ВыбратьИерархически();
	Пока Выборка.Следующий() Цикл
		
		РаботаСАдреснойКнигой.ОбновитьДанныеОбъекта(
			Выборка.Ссылка, Выборка.Родитель, ПоРабочимГруппам);
		
		Если Выборка.Ссылка = Справочники.РабочиеГруппы.ВсеПользователи Тогда
			ЗапросПодчиненных = Новый Запрос(
				"ВЫБРАТЬ
				|	Сотрудники.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.Сотрудники КАК Сотрудники");
			Состав = ЗапросПодчиненных.Выполнить().Выгрузить().ВыгрузитьКолонку(0); //@skip-check query-in-loop
			ВключатьТолькоРолиСотрудниковИзСписка = Ложь;
		Иначе
			Состав = Выборка.Состав.ВыгрузитьКолонку("Участник");
			ВключатьТолькоРолиСотрудниковИзСписка = Истина;
		КонецЕсли;
		
		//@skip-check query-in-loop
		РасширитьСписокСотрудниковРолями(Состав, ВключатьТолькоРолиСотрудниковИзСписка);
		
		РаботаСАдреснойКнигой.ОбновитьСписокПодчиненныхОбъектов(
			Выборка.Ссылка, Выборка.Родитель, ПоРабочимГруппам, Состав);
		
	КонецЦикла;
	Выборка = Неопределено;
	
	// По проектам
	Выборка = Справочники.ПапкиПроектов.ВыбратьИерархически();
	Пока Выборка.Следующий() Цикл
		
		РаботаСАдреснойКнигой.ОбновитьДанныеОбъекта(
			Выборка.Ссылка, Выборка.Родитель, ПоПроектам, Выборка.Ссылка);
		
	КонецЦикла;
	Выборка = Неопределено;
	
	Выборка = Справочники.Проекты.ВыбратьИерархически();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		
		РаботаСАдреснойКнигой.ОбновитьДанныеОбъекта(
			Выборка.Ссылка, Выборка.Папка, ПоПроектам, Выборка.Ссылка);
		
		ПроектнаяКоманда = Выборка.ПроектнаяКоманда.ВыгрузитьКолонку("Исполнитель");
		
		ИндексУчастникаПроекта = ПроектнаяКоманда.Количество() - 1;
		Пока ИндексУчастникаПроекта >= 0 Цикл
			УчастникПроекта = ПроектнаяКоманда[ИндексУчастникаПроекта];
			ТипУчастника = ТипЗнч(УчастникПроекта);
			Если ТипУчастника <> Тип("СправочникСсылка.Сотрудники")
					И ТипУчастника <> Тип("СправочникСсылка.Пользователи")
					И ТипУчастника <> Тип("СправочникСсылка.ПолныеРоли") Тогда
				
				ПроектнаяКоманда.Удалить(ИндексУчастникаПроекта);
			КонецЕсли;
			ИндексУчастникаПроекта = ИндексУчастникаПроекта - 1;
		КонецЦикла;
		
		РаботаСАдреснойКнигой.ОбновитьСписокПодчиненныхОбъектов(
			Выборка.Ссылка, Выборка.Родитель, ПоПроектам, ПроектнаяКоманда, Выборка.Ссылка);
		
	КонецЦикла;
	Выборка = Неопределено;
	
	// Роли
	Выборка = Справочники.РолиИсполнителей.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		РаботаСАдреснойКнигой.ОбновитьДанныеОбъекта(
			Выборка.Ссылка, Неопределено, Роли);
		
	КонецЦикла;
	Выборка = Неопределено;
	
	// Контрагенты
	Выборка = Справочники.Контрагенты.ВыбратьИерархически();
	Пока Выборка.Следующий() Цикл
		
		РаботаСАдреснойКнигой.ОбновитьДанныеОбъекта(
			Выборка.Ссылка, Выборка.Родитель, Контрагенты, Выборка.Ссылка);
		
	КонецЦикла;
	Выборка = Неопределено;
	
	Выборка = Справочники.КонтактныеЛица.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		РаботаСАдреснойКнигой.ОбновитьДанныеОбъекта(
			Выборка.Ссылка, Выборка.Владелец, Контрагенты, Выборка.Владелец);
		
	КонецЦикла;
	Выборка = Неопределено;
	
	// Личные адресаты
	Выборка = Справочники.ГруппыЛичныхАдресатов.ВыбратьИерархически();
	Пока Выборка.Следующий() Цикл
		
		РаботаСАдреснойКнигой.ОбновитьДанныеОбъекта(
			Выборка.Ссылка, Выборка.Родитель, ЛичныеАдресаты, Выборка.Сотрудник);
		
	КонецЦикла;
	Выборка = Неопределено;
	
	Выборка = Справочники.ЛичныеАдресаты.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		РаботаСАдреснойКнигой.ОбновитьДанныеОбъекта(
			Выборка.Ссылка, Выборка.Группа, ЛичныеАдресаты, Выборка.Сотрудник);
		
	КонецЦикла;
	Выборка = Неопределено;
	
	// Организации
	Выборка = Справочники.Организации.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		РаботаСАдреснойКнигой.ОбновитьДанныеОбъекта(
			Выборка.Ссылка, Неопределено, Организации);
		
	КонецЦикла;
	Выборка = Неопределено;
	
	// Автоподстановки для объектов
	Выборка = Справочники.АвтоподстановкиДляОбъектов.ВыбратьИерархически();
	Пока Выборка.Следующий() Цикл
		
		РазделыАдреснойКниги =
			Справочники.АвтоподстановкиДляОбъектов.РазделыАдреснойКниги(Выборка.ТипОбъекта);
		
		Для Каждого РазделАдреснойКниги Из РазделыАдреснойКниги Цикл
			РаботаСАдреснойКнигой.ОбновитьДанныеОбъекта(
				Выборка.Ссылка, Выборка.Родитель, РазделАдреснойКниги);
		КонецЦикла;
		
	КонецЦикла;
	Выборка = Неопределено;
	
	// Автоподстановки для процессов
	Выборка = Справочники.АвтоподстановкиДляПроцессов.ВыбратьИерархически();
	Пока Выборка.Следующий() Цикл
		
		РаботаСАдреснойКнигой.ОбновитьДанныеОбъекта(
			Выборка.Ссылка, Выборка.Родитель, АвтоподстановкиДляПроцессов);
		
	КонецЦикла;
	Выборка = Неопределено;
	
КонецПроцедуры

// Выполняет очистку адресной книги.
//
Процедура ОчиститьАдреснуюКнигу() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Предопределенный Тогда
			Продолжить;
		КонецЕсли;
		
		Если РаботаСАдреснойКнигойПовтИсп.ИспользоватьОтложенноеОбновление() Тогда
			РаботаСАдреснойКнигой.УдалитьОбъектыАдреснойКниги(Выборка.Объект);
		Иначе
			ОбъектАдреснойКниги = Выборка.Ссылка.ПолучитьОбъект();
			ОбъектАдреснойКниги.ОбменДанными.Загрузка = Истина;
			ОбъектАдреснойКниги.Удалить();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает соответствие полных имен объектов-источников адресной книги их типам.
//
// Возвращаемое значение:
//	Соответствие Из КлючИЗначение:
//		Ключ - Тип.
//		Значение - Строка.
//
Функция ИменаТиповОбъектовАдреснойКниги() Экспорт
	
	ИменаОбъектов = Новый Соответствие;
	
	Для Каждого Тип Из Метаданные.Справочники.АдреснаяКнига.Реквизиты.Объект.Тип.Типы() Цикл
		Если Тип = Тип("Строка") Тогда
			ИменаОбъектов.Вставить(Тип, "Строка");
		Иначе
			ИменаОбъектов.Вставить(Тип, Метаданные.НайтиПоТипу(Тип).ПолноеИмя());
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИменаОбъектов;
	
КонецФункции

// Возвращает список полных имен объектов-источников адресной книги.
//
// Возвращаемое значение:
//	Массив Из Строка - Массив имен.
//
Функция МассивИменОбъектовАдреснойКниги() Экспорт
	
	МассивИменОбъектов = Новый Массив;
	
	Для Каждого Тип Из Метаданные.Справочники.АдреснаяКнига.Реквизиты.Объект.Тип.Типы() Цикл
		Если Тип = Тип("Строка") Тогда
			МассивИменОбъектов.Добавить("Строка");
		Иначе
			МассивИменОбъектов.Добавить(Метаданные.НайтиПоТипу(Тип).ПолноеИмя());
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивИменОбъектов;
	
КонецФункции

// Проверяет, поддерживает ли объект обновление данных адресной книги.
//
// Параметры:
//	Объект - СправочникОбъект, РегистрСведенийНаборЗаписей - Объект, который необходимо проверить.
//	
// Возвращаемое значение:
//	Булево - Истина, если объект поддерживает обновление адресной книги.
//
Функция ОбъектПоддерживаетОбновлениеДанныхАдреснойКниги(Объект) Экспорт
	
	ТипыОбъектовПоддерживающиеОбновлениеДанных =
		РаботаСАдреснойКнигойПовтИсп.ТипыОбъектовПоддерживающихОбновлениеДанныхАдреснойКниги();
	
	Возврат ТипыОбъектовПоддерживающиеОбновлениеДанных.СодержитТип(ТипЗнч(Объект));
	
КонецФункции

// Возвращает список типов, которые поддерживают обновление данных адресной книги.
//
// Возвращаемое значение:
//	ОписаниеТипов - Список типов, которые поддерживают обновление адресной книги.
//
Функция ТипыОбъектовПоддерживающихОбновлениеДанныхАдреснойКниги() Экспорт
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникОбъект.АвтоподстановкиДляОбъектов"));
	МассивТипов.Добавить(Тип("СправочникОбъект.АвтоподстановкиДляПроцессов"));
	МассивТипов.Добавить(Тип("СправочникОбъект.ГруппыКонтактовПользователей"));
	МассивТипов.Добавить(Тип("СправочникОбъект.ГруппыЛичныхАдресатов"));
	МассивТипов.Добавить(Тип("СправочникОбъект.Должности"));
	МассивТипов.Добавить(Тип("СправочникОбъект.КонтактныеЛица"));
	МассивТипов.Добавить(Тип("СправочникОбъект.Контрагенты"));
	МассивТипов.Добавить(Тип("СправочникОбъект.ЛичныеАдресаты"));
	МассивТипов.Добавить(Тип("СправочникОбъект.Организации"));
	МассивТипов.Добавить(Тип("СправочникОбъект.ПапкиПроектов"));
	МассивТипов.Добавить(Тип("СправочникОбъект.Проекты"));
	МассивТипов.Добавить(Тип("СправочникОбъект.РабочиеГруппы"));
	МассивТипов.Добавить(Тип("СправочникОбъект.РолиИсполнителей"));
	МассивТипов.Добавить(Тип("СправочникОбъект.Сотрудники"));
	МассивТипов.Добавить(Тип("СправочникОбъект.СтруктураПредприятия"));
	МассивТипов.Добавить(Тип("СправочникОбъект.ФизическиеЛица"));
	МассивТипов.Добавить(Тип("РегистрСведенийНаборЗаписей.ИсполнителиРолей"));
	
	Возврат Новый ОписаниеТипов(МассивТипов);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиОбновления

// Возвращает список соответствий элементов разделам адресной книги.
//
// Параметры:
//	ТаблицаЭлементов - ТаблицаЗначений:
//		* Ссылка - СправочникСсылка.АдреснаяКнига - Элемент адресной книги, для которого нужно определить раздел.
//
// Возвращаемое значение:
//	Соответствие Из КлючИЗначение:
//		Ключ - СправочникСсылка.АдреснаяКнига - Ссылка на элемент адресной книги.
//		Значение - СправочникСсылка.АдреснаяКнига - Раздел адресной книги.
//
Функция РазделыАдреснойКнигиЭлементов(ТаблицаЭлементов)
	
	РазделыЭлементов = Новый Соответствие;
	
	Если ТипЗнч(ТаблицаЭлементов) <> Тип("ТаблицаЗначений")
			Или Не ЗначениеЗаполнено(ТаблицаЭлементов)
			Или ТаблицаЭлементов.Колонки.Найти("Ссылка") = Неопределено Тогда
			
		Возврат РазделыЭлементов;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Таблица.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ТаблицаСсылок
		|ИЗ
		|	&ВнешнийИсточник КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АдреснаяКнига.Ссылка КАК Ссылка,
		|	&ТекстВыборкаРеквизитРаздел КАК Раздел
		|ИЗ
		|	Справочник.АдреснаяКнига КАК АдреснаяКнига
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаСсылок КАК ТаблицаСсылок
		|		ПО АдреснаяКнига.Ссылка = ТаблицаСсылок.Ссылка");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстВыборкаРеквизитРаздел", ТекстРеквизитРазделИерархия());
	Запрос.УстановитьПараметр("ВнешнийИсточник", ТаблицаЭлементов.Скопировать(, "Ссылка"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РазделыЭлементов.Вставить(Выборка.Ссылка, Выборка.Раздел);
	КонецЦикла;
	
	Возврат РазделыЭлементов;
	
КонецФункции

#КонецОбласти

// Проверяет, используется ли обновленная архитектура адресной книги, если нет - устанавливает признак использования.
//
Процедура УстановитьПризнакИспользованияОбновленнойАрхитектуры()
	
	Если Константы.ПараметрыОбновленияАдреснойКниги.ИспользоватьОбновленнуюАрхитектуруАдреснойКниги() = Ложь Тогда
		Константы.ПараметрыОбновленияАдреснойКниги.УстановитьИспользованиеОбновленнойАрхитектурыАдреснойКниги(Истина);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает имя типа объекта адресной книги.
//
// Параметры:
//	Объект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект-источник данных адресной книги.
//
// Возвращаемое значение:
//	Строка - Имя типа.
//
Функция ИмяТипаОбъектаАдреснойКниги(Объект)
	
	Возврат РаботаСАдреснойКнигойПовтИсп.ИменаТиповОбъектовАдреснойКниги()[ТипЗнч(Объект)];
	
КонецФункции

// Формирует представление объекта адресной книги.
//
// Параметры:
//	Представление - Строка - Стандартное представление объекта.
//	ДополнительноеОписание - Строка - Дополнительное описание объекта.
//	ПредставлениеОсобое - Строка - Особое представление объекта.
//
// Возвращаемое значение:
//	Строка.
//
Функция ПредставлениеОбъектаАдреснойКниги(Представление, ДополнительноеОписание, ПредставлениеОсобое = "")

	ПредставлениеОбъекта = 
		СтрШаблон("%1%2",
			?(ЗначениеЗаполнено(ПредставлениеОсобое), ПредставлениеОсобое, Представление),
			ДополнительноеОписание);
			
	Возврат ПредставлениеОбъекта;
	
КонецФункции

// Устанавливает порядок объекта в адресной книге.
//
// Параметры:
//	ЭлементАдреснойКниги - СправочникОбъект.АдреснаяКнига - Объект адресной книги.
//	Объект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект-источник данных адресной книги.
//	РодительОбъекта - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Родитель объекта-источника данных адресной книги.
//
Процедура УстановитьПорядокОбъектаВСписке(ЭлементАдреснойКниги, Объект, РодительОбъекта)
	
	Если ЭлементАдреснойКниги.Предопределенный Тогда
		Возврат;
	КонецЕсли;
	
	ПорядокОбъектаВСписке = 99;
	
	ТипОбъекта = ТипЗнч(Объект);
	
	Если ТипОбъекта = Тип("СправочникСсылка.СтруктураПредприятия")
			Или ТипОбъекта = Тип("СправочникСсылка.РабочиеГруппы")
			Или ТипОбъекта = Тип("СправочникСсылка.ПапкиПроектов")
			Или ТипОбъекта = Тип("СправочникСсылка.ГруппыКонтактовПользователей")
			Или ТипОбъекта = Тип("СправочникСсылка.ГруппыЛичныхАдресатов") Тогда
		
		ПорядокОбъектаВСписке = 1;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.Проекты") Тогда
		ПорядокОбъектаВСписке = 3;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.РолиИсполнителей") Тогда
		ПорядокОбъектаВСписке = 4;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.Сотрудники") Тогда
		ПорядокОбъектаВСписке = 5;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.Пользователи") Тогда
		ПорядокОбъектаВСписке = 5;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.Контрагенты") Тогда
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект, "ЭтоГруппа") Тогда //@skip-check bsl-legacy-check-string-literal
			ПорядокОбъектаВСписке = 1;
		Иначе
			ПорядокОбъектаВСписке = 2;
		КонецЕсли;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ЛичныеАдресаты")
			Или ТипОбъекта = Тип("СправочникСсылка.КонтактныеЛица") Тогда
		
		ПорядокОбъектаВСписке = 6;
	КонецЕсли;

	Если ЗначениеЗаполнено(РодительОбъекта) Тогда
		ТипРодителя = ТипЗнч(РодительОбъекта);
		Если ТипРодителя = Тип("СправочникСсылка.ГруппыКонтактовПользователей") Тогда
			Если ТипОбъекта = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
				ПорядокОбъектаВСписке = 3;
			ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.РабочиеГруппы") Тогда
				ПорядокОбъектаВСписке = 7;
			ИначеЕсли ТипОбъекта = Тип("Строка") Тогда
				СписокФункций = РаботаСАдреснойКнигой.ПолучитьСписокДоступныхФункций();
				ЭтоАвтоподстановка = Ложь;
				Для Инд = 0 По СписокФункций.Количество() - 1 Цикл
					Если СписокФункций[Инд].Представление = Объект Тогда
						ЭтоАвтоподстановка = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если ЭтоАвтоподстановка Тогда
					ПорядокОбъектаВСписке = 7;
				Иначе
					ПорядокОбъектаВСписке = 3;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ЭлементАдреснойКниги.ПорядокОбъектаВСписке = ПорядокОбъектаВСписке;
	
КонецПроцедуры

// Возвращает варианты выборки данных.
//
// Возвращаемое значение:
//	Структура:
//		* СсылкаНаОбъект - Строка.
//		* СсылкаНаОбъектИРеквизиты - Строка.
//		* ДанныеДляСозданияОбъекта - Строка.
//
Функция ВариантыВыборкиДанныхОбъекта()
	
	Варианты = Новый Структура;
	Варианты.Вставить("СсылкаНаОбъект", "СсылкаНаОбъект");
	Варианты.Вставить("СсылкаНаОбъектИРеквизиты", "СсылкаНаОбъектИРеквизиты");
	Варианты.Вставить("СсылкиНаОбъекты", "СсылкиНаОбъекты");
	Варианты.Вставить("СсылкиНаОбъектыИРеквизиты", "СсылкиНаОбъектыИРеквизиты");
	Варианты.Вставить("ДанныеДляСозданияОбъекта", "ДанныеДляСозданияОбъекта");
	
	Возврат Варианты;
	
КонецФункции

// Возвращает новую таблицу описания элементов адресной книги.
//
// Возвращаемое значение:
//	ТаблицаЗначений:
//		* Объект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект-источник данных адресной книги.
//		* ОбъектДоступа - ОпределяемыйТип.ОбъектДоступаАдреснойКниги - Объект доступа.
//		* Раздел - СправочникСсылка.АдреснаяКнига - Раздел адресной книги.
//
Функция НоваяТаблицаЭлементовАдреснойКниги()
	
	МетаданныеАдреснойКниги = Метаданные.Справочники.АдреснаяКнига;
	
	ТаблицаЭлементов = Новый ТаблицаЗначений;
	
	ТаблицаЭлементов.Колонки.Добавить("Объект", МетаданныеАдреснойКниги.Реквизиты.Объект.Тип);
	ТаблицаЭлементов.Колонки.Добавить("ОбъектДоступа", МетаданныеАдреснойКниги.Реквизиты.ОбъектДоступа.Тип);
	ТаблицаЭлементов.Колонки.Добавить("Раздел", МетаданныеАдреснойКниги.Реквизиты.Раздел.Тип);
	
	Возврат ТаблицаЭлементов;
	
КонецФункции

#Область ТекстыЗапросовОбновленияДанныхАдреснойКниги

// Возвращает текст запроса выборки данных для обновления объекта адресной книги.
//
// Параметры:
//	ИменаТиповОбъектов - Массив Из Строка - Имена типов объектов адресной книги.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаОбновлениеОбъектаАдреснойКниги(ИменаТиповОбъектов = Неопределено)
	
	СхемаЗапроса = Новый СхемаЗапроса;
	
	ЭлементСхемы = СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество() - 1];
	ЭлементСхемы.УстановитьТекстЗапроса(
		ТекстЗапросаСсылкиНаОбъектыАдреснойКниги(ВариантыВыборкиДанныхОбъекта().ДанныеДляСозданияОбъекта));
	ЭлементСхемы.ТаблицаДляПомещения = "ДанныеОбъекта";
	
	ЭлементСхемы = СхемаЗапроса.ПакетЗапросов.Добавить();
	ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВложенныйЗапрос.*
		|ИЗ
		|	(ВЫБРАТЬ
		|		1 КАК Приоритет,
		|		АдреснаяКнига.Ссылка КАК Ссылка,
		|		АдреснаяКнига.ОтображатьВАдреснойКниге КАК ОтображатьВАдреснойКниге,
		|		АдреснаяКнига.ТипДанныхОбъекта КАК ТипДанныхОбъекта
		|	ИЗ
		|		Справочник.АдреснаяКнига КАК АдреснаяКнига
		|	ГДЕ
		|		АдреснаяКнига.Объект = &Родитель
		|		И АдреснаяКнига.Раздел = &Раздел
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		2,
		|		АдреснаяКнига.Ссылка,
		|		АдреснаяКнига.ОтображатьВАдреснойКниге,
		|		АдреснаяКнига.ТипДанныхОбъекта
		|	ИЗ
		|		Справочник.АдреснаяКнига КАК АдреснаяКнига
		|	ГДЕ
		|		АдреснаяКнига.Ссылка = &Раздел) КАК ВложенныйЗапрос
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВложенныйЗапрос.Приоритет";
	Если Не РаботаСАдреснойКнигойПовтИсп.ИспользоватьОбновленнуюАрхитектуру() Тогда
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса, "АдреснаяКнига.Раздел = &Раздел", "АдреснаяКнига.Ссылка В ИЕРАРХИИ(&Раздел)");
	КонецЕсли;
	ЭлементСхемы.УстановитьТекстЗапроса(ТекстЗапроса);
	ЭлементСхемы.ТаблицаДляПомещения = "ДанныеРодителяОбъекта";
	
	ЭлементСхемы = СхемаЗапроса.ПакетЗапросов.Добавить();
	ЭлементСхемы.УстановитьТекстЗапроса(
		"ВЫБРАТЬ
		|	ДанныеОбъекта.Ссылка КАК Ссылка,
		|	ВЫБОР
		|		КОГДА ДанныеОбъекта.Ссылка = ДанныеРодителяОбъекта.Ссылка
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ПустаяСсылка)
		|		ИНАЧЕ ЕСТЬNULL(ДанныеРодителяОбъекта.Ссылка, ДанныеОбъекта.Родитель)
		|	КОНЕЦ КАК Родитель,
		|	ДанныеОбъекта.Объект КАК Объект,
		|	ДанныеОбъекта.РодительОбъекта КАК РодительОбъекта,
		|	ДанныеОбъекта.ОбъектДоступа КАК ОбъектДоступа,
		|	ДанныеОбъекта.Раздел КАК Раздел,
		|	ДанныеОбъекта.ОтображатьВАдреснойКниге КАК ОтображатьВАдреснойКниге,
		|	ВЫБОР
		|		КОГДА ДанныеОбъекта.Ссылка = ДанныеРодителяОбъекта.Ссылка
		|			ТОГДА ДанныеОбъекта.ОтображатьВАдреснойКниге
		|		ИНАЧЕ ЕСТЬNULL(ДанныеРодителяОбъекта.ОтображатьВАдреснойКниге, ДанныеОбъекта.ОтображатьВАдреснойКниге)
		|	КОНЕЦ КАК ОтображатьРодителяВАдреснойКниге,
		|	ВЫБОР
		|		КОГДА ДанныеОбъекта.Ссылка = ДанныеРодителяОбъекта.Ссылка
		|			ТОГДА ДанныеОбъекта.ТипДанныхРодителя
		|		ИНАЧЕ ЕСТЬNULL(ДанныеРодителяОбъекта.ТипДанныхОбъекта, ДанныеОбъекта.ТипДанныхРодителя)
		|	КОНЕЦ КАК ТипДанныхРодителя
		|ИЗ
		|	ДанныеОбъекта КАК ДанныеОбъекта
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеРодителяОбъекта КАК ДанныеРодителяОбъекта
		|		ПО (ИСТИНА)");
	ЭлементСхемы.ТаблицаДляПомещения = "ЗаписиАдреснойКниги";
	
	ТекстЗапроса = Новый Массив;
	ТекстЗапроса.Добавить(СхемаЗапроса.ПолучитьТекстЗапроса());
	ТекстЗапроса.Добавить(
		ТекстЗапросаВыборкиДополнительныхСведений(
			"ЗаписиАдреснойКниги", "ЗаписиАдреснойКнигиСДополнительнымиСведениями", ИменаТиповОбъектов));
	ТекстЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ЗаписиАдреснойКниги.Ссылка КАК Ссылка,
		|	ЗаписиАдреснойКниги.Родитель КАК Родитель,
		|	ЗаписиАдреснойКниги.Объект КАК Объект,
		|	ЗаписиАдреснойКниги.РодительОбъекта КАК РодительОбъекта,
		|	ЗаписиАдреснойКниги.ОбъектДоступа КАК ОбъектДоступа,
		|	ЗаписиАдреснойКниги.Раздел КАК Раздел,
		|	ЗаписиАдреснойКниги.ОтображатьВАдреснойКнигеНовый КАК ОтображатьВАдреснойКниге,
		|	ЗаписиАдреснойКниги.ОтображатьРодителяВАдреснойКниге КАК ОтображатьРодителяВАдреснойКниге,
		|	ПРЕДСТАВЛЕНИЕ(ЗаписиАдреснойКниги.Объект) КАК ПредставлениеОбъекта,
		|	ЗаписиАдреснойКниги.ПредставлениеОбъектаОсобое КАК ПредставлениеОбъектаОсобое,
		|	ЗаписиАдреснойКниги.ДополнительноеОписание КАК ДополнительноеОписание
		|ИЗ
		|	ЗаписиАдреснойКнигиСДополнительнымиСведениями КАК ЗаписиАдреснойКниги");
	
	ТекстЗапроса = СтрСоединить(ТекстЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса выборки данных для обновления списка подчиненных объектов.
//
// Параметры:
//	ИменаТиповОбъектов - Массив Из Строка - Имена типов объектов адресной книги.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаОбновлениеСпискаПодчиненныхОбъектов(ИменаТиповОбъектов = Неопределено)
	
	СхемаЗапроса = Новый СхемаЗапроса;
	
	ЭлементСхемы = СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество() - 1];
	ЭлементСхемы.УстановитьТекстЗапроса(
		"ВЫБРАТЬ
		|	ВнешнийИсточник.Объект КАК Объект,
		|	ВнешнийИсточник.ОбъектДоступа КАК ОбъектДоступа,
		|	ВнешнийИсточник.Раздел КАК Раздел
		|ИЗ
		|	&ВнешнийИсточник КАК ВнешнийИсточник");
	ЭлементСхемы.ТаблицаДляПомещения = "ВнешнийИсточник";
	
	ЭлементСхемы = СхемаЗапроса.ПакетЗапросов.Добавить();
	ЭлементСхемы.УстановитьТекстЗапроса(
		ТекстЗапросаСсылкиНаОбъектыАдреснойКниги(ВариантыВыборкиДанныхОбъекта().СсылкаНаОбъектИРеквизиты));
	ЭлементСхемы.ТаблицаДляПомещения = "ДанныеОбъекта";
	
	ЭлементСхемы = СхемаЗапроса.ПакетЗапросов.Добавить();
	ЭлементСхемы.УстановитьТекстЗапроса(
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ПустаяСсылка) КАК Ссылка,
		|	ДанныеОбъекта.Ссылка КАК Родитель,
		|	ВнешнийИсточник.Объект КАК Объект,
		|	ДанныеОбъекта.Объект КАК РодительОбъекта,
		|	ВнешнийИсточник.ОбъектДоступа КАК ОбъектДоступа,
		|	ВнешнийИсточник.Раздел КАК Раздел,
		|	ЛОЖЬ КАК ОтображатьВАдреснойКниге,
		|	ДанныеОбъекта.ОтображатьВАдреснойКниге КАК ОтображатьРодителяВАдреснойКниге,
		|	ДанныеОбъекта.ТипДанныхОбъекта КАК ТипДанныхРодителя
		|ИЗ
		|	ВнешнийИсточник КАК ВнешнийИсточник
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОбъекта КАК ДанныеОбъекта
		|		ПО (ИСТИНА)");
	ЭлементСхемы.ТаблицаДляПомещения = "ПодчиненныеЭлементы";
	
	ЭлементСхемы = СхемаЗапроса.ПакетЗапросов.Добавить();
	ЭлементСхемы.УстановитьТекстЗапроса(ТекстЗапросаДанныеАдреснойКнигиОбновлениеСпискаПодчиненныхОбъектов());
	ЭлементСхемы.ТаблицаДляПомещения = "ДанныеАдреснойКниги";
	
	ЭлементСхемы = СхемаЗапроса.ПакетЗапросов.Добавить();
	ЭлементСхемы.УстановитьТекстЗапроса(
		"ВЫБРАТЬ
		|	МАКСИМУМ(ВложенныйЗапрос.Ссылка) КАК Ссылка,
		|	ВЫБОР
		|		КОГДА СУММА(ВложенныйЗапрос.Действие) > 0
		|			ТОГДА МАКСИМУМ(ВложенныйЗапрос.РодительНовый)
		|		ИНАЧЕ МАКСИМУМ(ВложенныйЗапрос.РодительТекущий)
		|	КОНЕЦ КАК Родитель,
		|	ВложенныйЗапрос.Объект КАК Объект,
		|	ВложенныйЗапрос.РодительОбъекта КАК РодительОбъекта,
		|	ВЫБОР
		|		КОГДА СУММА(ВложенныйЗапрос.Действие) > 0
		|			ТОГДА МАКСИМУМ(ВложенныйЗапрос.ОбъектДоступаНовый)
		|		ИНАЧЕ МАКСИМУМ(ВложенныйЗапрос.ОбъектДоступаТекущий)
		|	КОНЕЦ КАК ОбъектДоступа,
		|	ВЫБОР
		|		КОГДА СУММА(ВложенныйЗапрос.Действие) > 0
		|			ТОГДА МАКСИМУМ(ВложенныйЗапрос.РазделНовый)
		|		ИНАЧЕ МАКСИМУМ(ВложенныйЗапрос.РазделТекущий)
		|	КОНЕЦ КАК Раздел,
		|	ВЫБОР
		|		КОГДА СУММА(ВложенныйЗапрос.Действие) > 0
		|			ТОГДА МАКСИМУМ(ВложенныйЗапрос.ОтображатьВАдреснойКнигеНовый)
		|		ИНАЧЕ МАКСИМУМ(ВложенныйЗапрос.ОтображатьВАдреснойКнигеТекущий)
		|	КОНЕЦ КАК ОтображатьВАдреснойКниге,
		|	ВЫБОР
		|		КОГДА СУММА(ВложенныйЗапрос.Действие) > 0
		|			ТОГДА МАКСИМУМ(ВложенныйЗапрос.ОтображатьРодителяВАдреснойКнигеНовый)
		|		ИНАЧЕ МАКСИМУМ(ВложенныйЗапрос.ОтображатьРодителяВАдреснойКнигеТекущий)
		|	КОНЕЦ КАК ОтображатьРодителяВАдреснойКниге,
		|	ВЫБОР
		|		КОГДА СУММА(ВложенныйЗапрос.Действие) > 0
		|			ТОГДА МАКСИМУМ(ВложенныйЗапрос.ТипДанныхРодителяНовый)
		|		ИНАЧЕ МАКСИМУМ(ВложенныйЗапрос.ТипДанныхРодителяТекущий)
		|	КОНЕЦ КАК ТипДанныхРодителя,
		|	ЕСТЬNULL(МАКСИМУМ(ВложенныйЗапрос.ОбъектДоступаНовый), НЕОПРЕДЕЛЕНО) КАК ОбъектДоступаНовый,
		|	ЕСТЬNULL(МАКСИМУМ(ВложенныйЗапрос.ОтображатьВАдреснойКнигеНовый), ЛОЖЬ) КАК ОтображатьВАдреснойКнигеНовый,
		|	ЕСТЬNULL(МАКСИМУМ(ВложенныйЗапрос.ОтображатьРодителяВАдреснойКнигеНовый), ЛОЖЬ) КАК ОтображатьРодителяВАдреснойКнигеНовый,
		|	СУММА(ВложенныйЗапрос.Действие) КАК Действие
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТекущиеДанные.Ссылка КАК Ссылка,
		|		ТекущиеДанные.Родитель КАК РодительТекущий,
		|		NULL КАК РодительНовый,
		|		ТекущиеДанные.Объект КАК Объект,
		|		ТекущиеДанные.РодительОбъекта КАК РодительОбъекта,
		|		ТекущиеДанные.ОбъектДоступа КАК ОбъектДоступаТекущий,
		|		NULL КАК ОбъектДоступаНовый,
		|		ТекущиеДанные.Раздел КАК РазделТекущий,
		|		NULL КАК РазделНовый,
		|		ТекущиеДанные.ОтображатьВАдреснойКниге КАК ОтображатьВАдреснойКнигеТекущий,
		|		NULL КАК ОтображатьВАдреснойКнигеНовый,
		|		ТекущиеДанные.ОтображатьРодителяВАдреснойКниге КАК ОтображатьРодителяВАдреснойКнигеТекущий,
		|		NULL КАК ОтображатьРодителяВАдреснойКнигеНовый,
		|		ТекущиеДанные.ТипДанныхРодителя КАК ТипДанныхРодителяТекущий,
		|		NULL КАК ТипДанныхРодителяНовый,
		|		-1 КАК Действие
		|	ИЗ
		|		ДанныеАдреснойКниги КАК ТекущиеДанные
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НовыеДанные.Ссылка,
		|		NULL,
		|		НовыеДанные.Родитель,
		|		НовыеДанные.Объект,
		|		НовыеДанные.РодительОбъекта,
		|		NULL,
		|		НовыеДанные.ОбъектДоступа,
		|		NULL,
		|		НовыеДанные.Раздел,
		|		NULL,
		|		НовыеДанные.ОтображатьВАдреснойКниге,
		|		NULL,
		|		НовыеДанные.ОтображатьРодителяВАдреснойКниге,
		|		NULL,
		|		НовыеДанные.ТипДанныхРодителя,
		|		1
		|	ИЗ
		|		ПодчиненныеЭлементы КАК НовыеДанные) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Объект,
		|	ВложенныйЗапрос.РодительОбъекта");
	ЭлементСхемы.ТаблицаДляПомещения = "ЗаписиАдреснойКниги";
	
	ТекстЗапроса = Новый Массив;
	ТекстЗапроса.Добавить(СхемаЗапроса.ПолучитьТекстЗапроса());
	ТекстЗапроса.Добавить(
		ТекстЗапросаВыборкиДополнительныхСведений(
			"ЗаписиАдреснойКниги", "ЗаписиАдреснойКнигиСДополнительнымиСведениями", ИменаТиповОбъектов));
	
	ТекстЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ЗаписиАдреснойКниги.Ссылка КАК Ссылка
		|ИЗ
		|	ЗаписиАдреснойКнигиСДополнительнымиСведениями КАК ЗаписиАдреснойКниги
		|ГДЕ
		|	ЗаписиАдреснойКниги.Действие = -1");
		
	ТекстЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ЗаписиАдреснойКниги.Родитель КАК Родитель,
		|	ЗаписиАдреснойКниги.Объект КАК Объект,
		|	ЗаписиАдреснойКниги.РодительОбъекта КАК РодительОбъекта,
		|	ЗаписиАдреснойКниги.ОбъектДоступа КАК ОбъектДоступа,
		|	ЗаписиАдреснойКниги.Раздел КАК Раздел,
		|	ЗаписиАдреснойКниги.ОтображатьВАдреснойКнигеНовый КАК ОтображатьВАдреснойКниге,
		|	ЗаписиАдреснойКниги.ОтображатьРодителяВАдреснойКниге КАК ОтображатьРодителяВАдреснойКниге,
		|	ПРЕДСТАВЛЕНИЕ(ЗаписиАдреснойКниги.Объект) КАК ПредставлениеОбъекта,
		|	ЗаписиАдреснойКниги.ПредставлениеОбъектаОсобое КАК ПредставлениеОбъектаОсобое,
		|	ЗаписиАдреснойКниги.ДополнительноеОписание КАК ДополнительноеОписание
		|ИЗ
		|	ЗаписиАдреснойКнигиСДополнительнымиСведениями КАК ЗаписиАдреснойКниги
		|ГДЕ
		|	ЗаписиАдреснойКниги.Действие = 1");

	ТекстЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ЗаписиАдреснойКниги.Ссылка КАК Ссылка,
		|	ЗаписиАдреснойКниги.Раздел КАК Раздел,
		|	ЗаписиАдреснойКниги.ОбъектДоступаНовый КАК ОбъектДоступа,
		|	ЗаписиАдреснойКниги.ОтображатьВАдреснойКнигеНовый КАК ОтображатьВАдреснойКниге,
		|	ПРЕДСТАВЛЕНИЕ(ЗаписиАдреснойКниги.Объект) КАК ПредставлениеОбъекта,
		|	ЗаписиАдреснойКниги.ПредставлениеОбъектаОсобое КАК ПредставлениеОбъектаОсобое,
		|	ЗаписиАдреснойКниги.ДополнительноеОписание КАК ДополнительноеОписание
		|ИЗ
		|	ЗаписиАдреснойКнигиСДополнительнымиСведениями КАК ЗаписиАдреснойКниги
		|ГДЕ
		|	НЕ &ОбновитьТолькоСоставПодчиненных
		|	И ЗаписиАдреснойКниги.Действие = 0
		|	И (ЗаписиАдреснойКниги.ОбъектДоступа <> ЗаписиАдреснойКниги.ОбъектДоступаНовый
		|			ИЛИ ЗаписиАдреснойКниги.ОтображатьВАдреснойКниге <> ЗаписиАдреснойКниги.ОтображатьВАдреснойКнигеНовый)");
	
	ТекстЗапроса = СтрСоединить(ТекстЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса выборки данных для обновления данных подчиненных объектов.
//
// Параметры:
//	ИменаТиповОбъектов - Массив Из Строка - Имена типов объектов адресной книги.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаОбновлениеДанныхОтображенияПодчиненныхОбъектов(ИменаТиповОбъектов = Неопределено)
	
	СхемаЗапроса = Новый СхемаЗапроса;
	
	ЭлементСхемы = СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество() - 1];
	ЭлементСхемы.УстановитьТекстЗапроса(
		ТекстЗапросаСсылкиНаОбъектыАдреснойКниги(ВариантыВыборкиДанныхОбъекта().СсылкиНаОбъектыИРеквизиты));
	ЭлементСхемы.ТаблицаДляПомещения = "ДанныеОбъектов";

	ЭлементСхемы = СхемаЗапроса.ПакетЗапросов.Добавить();
	ЭлементСхемы.УстановитьТекстЗапроса(
		"ВЫБРАТЬ
		|	ДанныеОбъектов.Ссылка КАК Ссылка,
		|	ДанныеОбъектов.Родитель КАК Родитель,
		|	ДанныеОбъектов.Объект КАК Объект,
		|	ДанныеОбъектов.РодительОбъекта КАК РодительОбъекта,
		|	ДанныеОбъектов.Раздел КАК Раздел,
		|	ДанныеОбъектов.ОбъектДоступа КАК ОбъектДоступа,
		|	ДанныеОбъектов.ОтображатьВАдреснойКниге КАК ОтображатьВАдреснойКниге,
		|	ДанныеОбъектов.ТипДанныхОбъекта КАК ТипДанныхОбъекта,
		|	ДанныеОбъектов.ОтображатьРодителяВАдреснойКниге КАК ОтображатьРодителяВАдреснойКниге,
		|	ДанныеОбъектов.ТипДанныхРодителя КАК ТипДанныхРодителя
		|ИЗ
		|	ДанныеОбъектов КАК ДанныеОбъектов
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	АдреснаяКнига.Ссылка КАК Ссылка,
		|	АдреснаяКнига.Родитель КАК Родитель,
		|	АдреснаяКнига.Объект КАК Объект,
		|	АдреснаяКнига.РодительОбъекта КАК РодительОбъекта,
		|	АдреснаяКнига.Раздел КАК Раздел,
		|	АдреснаяКнига.ОбъектДоступа КАК ОбъектДоступа,
		|	АдреснаяКнига.ОтображатьВАдреснойКниге КАК ОтображатьВАдреснойКниге,
		|	АдреснаяКнига.ТипДанныхОбъекта КАК ТипДанныхОбъекта,
		|	ЕСТЬNULL(АдреснаяКнига.Родитель.ОтображатьВАдреснойКниге, ЛОЖЬ) КАК ОтображатьРодителяВАдреснойКниге,
		|	ЕСТЬNULL(АдреснаяКнига.Родитель.ТипДанныхОбъекта, ЗНАЧЕНИЕ(Перечисление.ТипыДанныхАдреснойКниги.ПустаяСсылка)) КАК ТипДанныхРодителя
		|ИЗ
		|	Справочник.АдреснаяКнига КАК АдреснаяКнига
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОбъектов КАК ДанныеОбъектов
		|		ПО АдреснаяКнига.Ссылка = ДанныеОбъектов.Родитель
		|		И ДанныеОбъектов.ТипДанныхРодителя = ЗНАЧЕНИЕ(Перечисление.ТипыДанныхАдреснойКниги.ГруппаКонтактовПользователей)");
	ЭлементСхемы.ТаблицаДляПомещения = "ЗаписиАдреснойКниги";
	
	ТекстЗапроса = Новый Массив;
	ТекстЗапроса.Добавить(СхемаЗапроса.ПолучитьТекстЗапроса());
	ТекстЗапроса.Добавить(
		ТекстЗапросаВыборкиДополнительныхСведений(
			"ЗаписиАдреснойКниги", "ЗаписиАдреснойКнигиСДополнительнымиСведениями", ИменаТиповОбъектов));
	ТекстЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ЗаписиАдреснойКниги.Ссылка КАК Ссылка,
		|	ЗаписиАдреснойКниги.Родитель КАК Родитель,
		|	ЗаписиАдреснойКниги.Объект КАК Объект,
		|	ЗаписиАдреснойКниги.РодительОбъекта КАК РодительОбъекта,
		|	ЗаписиАдреснойКниги.ОбъектДоступа КАК ОбъектДоступа,
		|	ЗаписиАдреснойКниги.Раздел КАК Раздел,
		|	ЗаписиАдреснойКниги.ОтображатьВАдреснойКнигеНовый КАК ОтображатьВАдреснойКниге,
		|	ЗаписиАдреснойКниги.ОтображатьРодителяВАдреснойКниге КАК ОтображатьРодителяВАдреснойКниге,
		|	ПРЕДСТАВЛЕНИЕ(ЗаписиАдреснойКниги.Объект) КАК ПредставлениеОбъекта,
		|	ЗаписиАдреснойКниги.ПредставлениеОбъектаОсобое КАК ПредставлениеОбъектаОсобое,
		|	ЗаписиАдреснойКниги.ДополнительноеОписание КАК ДополнительноеОписание
		|ИЗ
		|	ЗаписиАдреснойКнигиСДополнительнымиСведениями КАК ЗаписиАдреснойКниги");
	
	ТекстЗапроса = СтрСоединить(ТекстЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса выборки ссылки на объект адресной книги.
//
// Параметры:
//	ВариантВыборкиДанных - см. ВариантыВыборкиДанныхОбъекта.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаСсылкиНаОбъектыАдреснойКниги(ВариантВыборкиДанных = "СсылкаНаОбъект")
	
	ВариантыВыборкиДанныхОбъекта = ВариантыВыборкиДанныхОбъекта();
	
	ТекстЗапроса = ТекстЗапросаСсылкиНаОбъектыАдреснойКнигиШаблон();
	
	ТекстРазмерВыборки = "ВЫБРАТЬ ПЕРВЫЕ 1";
	ТекстВыборкаПрочиеРеквизиты = "ИСТИНА";
	ТекстВыборкаСозданиеПрочиеРеквизиты = "ИСТИНА";
	ТекстВыборкаРеквизитРаздел = "АдреснаяКнига.Раздел";
	ТекстВыборкаСозданиеРеквизитРаздел = "ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ПустаяСсылка)";
	ТекстУсловиеВыборПредопределенных = "ИСТИНА";
	ТекстУсловиеРеквизитРаздел = "АдреснаяКнига.Раздел В(&Раздел)";
	ТекстУсловиеСозданиеОбъекта = "ЛОЖЬ";
	
	Если ВариантВыборкиДанных <> ВариантыВыборкиДанныхОбъекта.СсылкаНаОбъект
			И ВариантВыборкиДанных <> ВариантыВыборкиДанныхОбъекта.СсылкиНаОбъекты Тогда
		
		ТекстВыборкаПрочиеРеквизиты =
			"	АдреснаяКнига.Родитель КАК Родитель,
			|	АдреснаяКнига.Объект КАК Объект,
			|	АдреснаяКнига.РодительОбъекта КАК РодительОбъекта,
			|	АдреснаяКнига.ОбъектДоступа КАК ОбъектДоступа,
			|	АдреснаяКнига.ОтображатьВАдреснойКниге КАК ОтображатьВАдреснойКниге,
			|	АдреснаяКнига.ТипДанныхОбъекта КАК ТипДанныхОбъекта,
			|	ЕСТЬNULL(АдреснаяКнига.Родитель.ОтображатьВАдреснойКниге, ЛОЖЬ) КАК ОтображатьРодителяВАдреснойКниге,
			|	ЕСТЬNULL(АдреснаяКнига.Родитель.ТипДанныхОбъекта, ЗНАЧЕНИЕ(Перечисление.ТипыДанныхАдреснойКниги.ПустаяСсылка)) КАК ТипДанныхРодителя";
		ТекстВыборкаСозданиеПрочиеРеквизиты =
			"	ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ПустаяСсылка) КАК Родитель,
			|	НЕОПРЕДЕЛЕНО КАК Объект,
			|	НЕОПРЕДЕЛЕНО КАК РодительОбъекта,
			|	НЕОПРЕДЕЛЕНО КАК ОбъектДоступа,
			|	ЛОЖЬ КАК ОтображатьВАдреснойКниге,
			|	ЗНАЧЕНИЕ(Перечисление.ТипыДанныхАдреснойКниги.ПустаяСсылка) КАК ТипДанныхОбъекта,
			|	ЛОЖЬ КАК ОтображатьРодителяВАдреснойКниге,
			|	ЗНАЧЕНИЕ(Перечисление.ТипыДанныхАдреснойКниги.ПустаяСсылка) КАК ТипДанныхРодителя";
	КонецЕсли;
	
	Если ВариантВыборкиДанных = ВариантыВыборкиДанныхОбъекта.СсылкиНаОбъекты
			Или ВариантВыборкиДанных = ВариантыВыборкиДанныхОбъекта.СсылкиНаОбъектыИРеквизиты Тогда
		
		ТекстРазмерВыборки = "ВЫБРАТЬ";
		ТекстУсловиеВыборПредопределенных = "ЛОЖЬ";
	КонецЕсли;
	
	Если ВариантВыборкиДанных = ВариантыВыборкиДанныхОбъекта.ДанныеДляСозданияОбъекта Тогда
		ТекстВыборкаСозданиеРеквизитРаздел = "&Раздел";
		ТекстВыборкаСозданиеПрочиеРеквизиты =
			"	&Раздел КАК Родитель,
			|	&Объект КАК Объект,
			|	&Родитель КАК РодительОбъекта,
			|	&ОбъектДоступа КАК ОбъектДоступа,
			|	ЛОЖЬ КАК ОтображатьВАдреснойКниге,
			|	ЗНАЧЕНИЕ(Перечисление.ТипыДанныхАдреснойКниги.ПустаяСсылка) КАК ТипДанныхОбъекта,
			|	ЛОЖЬ КАК ОтображатьРодителяВАдреснойКниге,
			|	ЗНАЧЕНИЕ(Перечисление.ТипыДанныхАдреснойКниги.ПустаяСсылка) КАК ТипДанныхРодителя";
		ТекстУсловиеСозданиеОбъекта = "ИСТИНА";
	КонецЕсли;
	
	Если Не РаботаСАдреснойКнигойПовтИсп.ИспользоватьОбновленнуюАрхитектуру() Тогда
		ТекстВыборкаРеквизитРаздел = ТекстРеквизитРазделИерархия();
		ТекстУсловиеРеквизитРаздел = "АдреснаяКнига.Ссылка В ИЕРАРХИИ(&Раздел)"; 
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстВыборкаРеквизитРаздел", ТекстВыборкаРеквизитРаздел);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстВыборкаСозданиеРеквизитРаздел", ТекстВыборкаСозданиеРеквизитРаздел);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстВыборкаПрочиеРеквизиты", ТекстВыборкаПрочиеРеквизиты);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстВыборкаСозданиеПрочиеРеквизиты", ТекстВыборкаСозданиеПрочиеРеквизиты);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстУсловиеСозданиеОбъекта", ТекстУсловиеСозданиеОбъекта);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстУсловиеВыборПредопределенных", ТекстУсловиеВыборПредопределенных);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстУсловиеРеквизитРаздел", ТекстУсловиеРеквизитРаздел);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ ПЕРВЫЕ 1", ТекстРазмерВыборки);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает шаблон текста запроса выборки ссылки на объект адресной книги.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаСсылкиНаОбъектыАдреснойКнигиШаблон()
	
	Возврат
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВложенныйЗапрос.*
		|ИЗ
		|	(ВЫБРАТЬ
		|		1 КАК Приоритет,
		|		АдреснаяКнига.Ссылка КАК Ссылка,
		|		ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ПоРабочимГруппам) КАК Раздел,
		|		&ТекстВыборкаПрочиеРеквизиты
		|	ИЗ
		|		Справочник.АдреснаяКнига КАК АдреснаяКнига
		|	ГДЕ
		|		&Объект = ЗНАЧЕНИЕ(Справочник.РабочиеГруппы.ВсеПользователи)
		|		И АдреснаяКнига.Ссылка = ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ВсеПользователи)
		|		И &ТекстУсловиеВыборПредопределенных
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		2 КАК Приоритет,
		|		АдреснаяКнига.Ссылка КАК Ссылка,
		|		ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.Избранное) КАК Раздел,
		|		&ТекстВыборкаПрочиеРеквизиты
		|	ИЗ
		|		Справочник.АдреснаяКнига КАК АдреснаяКнига
		|	ГДЕ
		|		ТИПЗНАЧЕНИЯ(&Объект) = ТИП(Справочник.ГруппыКонтактовПользователей)
		|		И НЕ &РодительУказан
		|		И АдреснаяКнига.Ссылка = ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.Избранное)
		|		И &ТекстУсловиеВыборПредопределенных
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		3 КАК Приоритет,
		|		АдреснаяКнига.Ссылка КАК Ссылка,
		|		&ТекстВыборкаРеквизитРаздел КАК Раздел,
		|		&ТекстВыборкаПрочиеРеквизиты
		|	ИЗ
		|		Справочник.АдреснаяКнига КАК АдреснаяКнига
		|	ГДЕ
		|		АдреснаяКнига.Объект = &Объект
		|		И ВЫБОР
		|				КОГДА &РазделУказан
		|					ТОГДА &ТекстУсловиеРеквизитРаздел
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		4 КАК Приоритет,
		|		ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ПустаяСсылка) КАК Ссылка,
		|		&ТекстВыборкаСозданиеРеквизитРаздел КАК Раздел,
		|		&ТекстВыборкаСозданиеПрочиеРеквизиты
		|	ГДЕ
		|		&ТекстУсловиеСозданиеОбъекта) КАК ВложенныйЗапрос
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВложенныйЗапрос.Приоритет";
	
КонецФункции

// Возвращает текст запроса выборки данных адресной книги для обновления списка подчиненных объектов.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаДанныеАдреснойКнигиОбновлениеСпискаПодчиненныхОбъектов()
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	АдреснаяКнига.Ссылка КАК Ссылка,
		|	АдреснаяКнига.Родитель КАК Родитель,
		|	АдреснаяКнига.Объект КАК Объект,
		|	АдреснаяКнига.РодительОбъекта КАК РодительОбъекта,
		|	АдреснаяКнига.ОбъектДоступа КАК ОбъектДоступа,
		|	&ТекстВыборкаРеквизитРаздел КАК Раздел,
		|	АдреснаяКнига.ОтображатьВАдреснойКниге КАК ОтображатьВАдреснойКниге,
		|	АдреснаяКнига.Родитель.ОтображатьВАдреснойКниге КАК ОтображатьРодителяВАдреснойКниге,
		|	АдреснаяКнига.Родитель.ТипДанныхОбъекта КАК ТипДанныхРодителя
		|ИЗ
		|	Справочник.АдреснаяКнига КАК АдреснаяКнига
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОбъекта КАК ДанныеОбъекта
		|		ПО АдреснаяКнига.РодительОбъекта = ДанныеОбъекта.Объект
		|		И &ТекстУсловиеОтбораРаздел";
	
	ТекстВыборкаРеквизитРаздел = "АдреснаяКнига.Раздел";
	ТекстУсловиеОтбораРаздел = "АдреснаяКнига.Раздел = ДанныеОбъекта.Раздел";
	
	Если Не РаботаСАдреснойКнигойПовтИсп.ИспользоватьОбновленнуюАрхитектуру() Тогда
		ТекстВыборкаРеквизитРаздел = ТекстРеквизитРазделИерархия();
		ТекстУсловиеОтбораРаздел = СтрШаблон("%1 = ДанныеОбъекта.Раздел", ТекстРеквизитРазделИерархия());
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстВыборкаРеквизитРаздел", ТекстВыборкаРеквизитРаздел);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстУсловиеОтбораРаздел", ТекстУсловиеОтбораРаздел);
	
	Возврат ТекстЗапроса;

КонецФункции

// Возвращает текст описания реквизита Раздел, выраженного через иерархию.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстРеквизитРазделИерархия()
	
	Возврат
		"ВЫБОР
		|	КОГДА АдреснаяКнига.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.Избранное))
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.Избранное)
		|	КОГДА АдреснаяКнига.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ПоСтруктуреПредприятия))
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ПоСтруктуреПредприятия)
		|	КОГДА АдреснаяКнига.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ВсеПользователи))
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ПоРабочимГруппам)
		|	КОГДА АдреснаяКнига.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ПоРабочимГруппам))
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ПоРабочимГруппам)
		|	КОГДА АдреснаяКнига.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.АвтоподстановкиДляДокументов))
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.АвтоподстановкиДляДокументов)
		|	КОГДА АдреснаяКнига.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ПоПроектам))
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ПоПроектам)
		|	КОГДА АдреснаяКнига.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.АвтоподстановкиДляПроцессов))
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.АвтоподстановкиДляПроцессов)
		|	КОГДА АдреснаяКнига.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.АвтоподстановкиДляМероприятий))
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.АвтоподстановкиДляМероприятий)
		|	КОГДА АдреснаяКнига.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.АвтоподстановкиДляЗадачПроцессов))
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.АвтоподстановкиДляЗадачПроцессов)
		|	КОГДА АдреснаяКнига.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ВсеАвтоподстановки))
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ВсеАвтоподстановки)
		|	КОГДА АдреснаяКнига.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.АвтоподстановкиДляЗадач))
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.АвтоподстановкиДляЗадач)
		|	КОГДА АдреснаяКнига.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.Контрагенты))
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.Контрагенты)
		|	КОГДА АдреснаяКнига.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ЛичныеАдресаты))
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ЛичныеАдресаты)
		|	КОГДА АдреснаяКнига.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.Роли))
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.Роли)
		|	КОГДА АдреснаяКнига.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.Организации))
		|		ТОГДА ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.Организации)
		|	ИНАЧЕ ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.Сотрудники)
		|КОНЕЦ";
	
КонецФункции

#Область ТекстыЗапросовВыборкиДополнительныхСведений

// Возвращает текст запроса выборки дополнительных сведений об объектах адресной книги.
//
// Параметры:
//	ИмяОсновнойТаблицы - Строка - Имя таблицы, содержащей сведения об объектах адресной книги.
//	ИмяТаблицыРезультата - Строка - Имя таблицы, в которую следует поместить данные об объектах и сведениях о них.
//	ИменаТиповОбъектов - Массив Из Строка - Имена типов объектов адресной книги.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаВыборкиДополнительныхСведений(
		ИмяОсновнойТаблицы, ИмяТаблицыРезультата, ИменаТиповОбъектов = Неопределено)
	
	ТекстыЗапросовОтличныеОтШаблонного =
		ТекстыЗапросовВыборкиДополнительныхСведенийОтличныеОтСтандартного(ИмяОсновнойТаблицы);
	
	ТекстЗапроса = Новый Массив;
	
	ИменаТипов = ИменаТиповОбъектов;
	Если Не ЗначениеЗаполнено(ИменаТипов) Тогда
		ИменаТипов = РаботаСАдреснойКнигойПовтИсп.МассивИменОбъектовАдреснойКниги();
	КонецЕсли;
	
	Для Каждого ИмяТипа Из ИменаТипов Цикл
		ТекстЗапросаТипа = ТекстыЗапросовОтличныеОтШаблонного[ИмяТипа];
		Если ТекстЗапросаТипа = Неопределено Тогда
			ТекстЗапросаТипа =
				ТекстЗапросаВыборкиДополнительныхСведенийСтандартный(ИмяОсновнойТаблицы, ИмяТипа);
		КонецЕсли;
		
		ТекстЗапроса.Добавить(ТекстЗапросаТипа);
	КонецЦикла;
	
	СхемаЗапроса = Новый СхемаЗапроса;
	ЭлементСхемы = СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество() - 1];
	ЭлементСхемы.УстановитьТекстЗапроса(
		СтрСоединить(ТекстЗапроса, ОбщегоНазначения.ТекстОбъединитьВсе()));
	ЭлементСхемы.ТаблицаДляПомещения = "ДополнительныеСведенияОЗаписяхАдреснойКниги";

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ОсновнаяТаблица.*,
		|	ЕСТЬNULL(ДополнительныеСведения.ОтображатьВАдреснойКниге, ЛОЖЬ) КАК ОтображатьВАдреснойКнигеНовый,
		|	ЕСТЬNULL(ДополнительныеСведения.ПредставлениеОбъектаОсобое, """") КАК ПредставлениеОбъектаОсобое,
		|	ЕСТЬNULL(ДополнительныеСведения.ДополнительноеОписание, """") КАК ДополнительноеОписание
		|ПОМЕСТИТЬ #ИмяТаблицыРезультата 
		|ИЗ
		|	#ИмяОсновнойТаблицы КАК ОсновнаяТаблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДополнительныеСведенияОЗаписяхАдреснойКниги КАК ДополнительныеСведения
		|		ПО ОсновнаяТаблица.Объект = ДополнительныеСведения.Объект
		|			И ОсновнаяТаблица.Родитель = ДополнительныеСведения.Родитель
		|			И ОсновнаяТаблица.РодительОбъекта = ДополнительныеСведения.РодительОбъекта
		|			И ОсновнаяТаблица.ОбъектДоступа = ДополнительныеСведения.ОбъектДоступа";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяОсновнойТаблицы", ИмяОсновнойТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяТаблицыРезультата", ИмяТаблицыРезультата);
	
	ИтоговыйТекстЗапроса = Новый Массив;
	ИтоговыйТекстЗапроса.Добавить(СхемаЗапроса.ПолучитьТекстЗапроса());
	ИтоговыйТекстЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(ИтоговыйТекстЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает список текстов запросов выборки дополнительных сведений, отличных от стандартного.
//
// Параметры:
//	ИмяОсновнойТаблицы - Строка - Имя таблицы, содержащей сведения об объектах адресной книги.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстыЗапросовВыборкиДополнительныхСведенийОтличныеОтСтандартного(ИмяОсновнойТаблицы)
	
	ТекстыЗапросовОтличныеОтШаблонного = Новый Соответствие;
	ТекстыЗапросовОтличныеОтШаблонного.Вставить(
		"Справочник.СтруктураПредприятия",
		ТекстЗапросаВыборкиДополнительныхСведенийСтруктураПредприятия(ИмяОсновнойТаблицы));
	ТекстыЗапросовОтличныеОтШаблонного.Вставить(
		"Справочник.ГруппыКонтактовПользователей",
		ТекстЗапросаВыборкиДополнительныхСведенийГруппыКонтактовПользователей(ИмяОсновнойТаблицы));
	ТекстыЗапросовОтличныеОтШаблонного.Вставить(
		"Справочник.Проекты",
		ТекстЗапросаВыборкиДополнительныхСведенийПроекты(ИмяОсновнойТаблицы));
	ТекстыЗапросовОтличныеОтШаблонного.Вставить(
		"Справочник.Сотрудники",
		ТекстЗапросаВыборкиДополнительныхСведенийСотрудники(ИмяОсновнойТаблицы));
	ТекстыЗапросовОтличныеОтШаблонного.Вставить(
		"Справочник.Пользователи",
		ТекстЗапросаВыборкиДополнительныхСведенийПользователи(ИмяОсновнойТаблицы));
	ТекстыЗапросовОтличныеОтШаблонного.Вставить(
		"Справочник.РабочиеГруппы",
		ТекстЗапросаВыборкиДополнительныхСведенийРабочиеГруппы(ИмяОсновнойТаблицы));
	ТекстыЗапросовОтличныеОтШаблонного.Вставить(
		"Справочник.КонтактныеЛица",
		ТекстЗапросаВыборкиДополнительныхСведенийКонтактныеЛица(ИмяОсновнойТаблицы));
	ТекстыЗапросовОтличныеОтШаблонного.Вставить(
		"Строка",
		ТекстЗапросаВыборкиДополнительныхСведенийСтрока(ИмяОсновнойТаблицы));
	
	Возврат ТекстыЗапросовОтличныеОтШаблонного;
	
КонецФункции

// Возвращает текст запроса выборки дополнительных сведений о структуре предприятия.
//
// Параметры:
//	ИмяОсновнойТаблицы - Строка - Имя таблицы, содержащей сведения об объектах адресной книги.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаВыборкиДополнительныхСведенийСтруктураПредприятия(ИмяОсновнойТаблицы)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.Объект КАК Объект,
		|	Таблица.Родитель КАК Родитель,
		|	Таблица.РодительОбъекта КАК РодительОбъекта,
		|	Таблица.ОбъектДоступа КАК ОбъектДоступа,
		|	ВЫБОР
		|		КОГДА Таблица.Родитель <> ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ПустаяСсылка)
		|				И Таблица.ОтображатьРодителяВАдреснойКниге = ЛОЖЬ
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ НЕ СтруктураПредприятия.ПометкаУдаления
		|	КОНЕЦ КАК ОтображатьВАдреснойКниге,
		|	"""" КАК ПредставлениеОбъектаОсобое,
		|	ВЫБОР
		|		КОГДА Таблица.Родитель = ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.Избранное)
		|				ИЛИ Таблица.ТипДанныхРодителя = ЗНАЧЕНИЕ(Перечисление.ТипыДанныхАдреснойКниги.ГруппаКонтактовПользователей)
		|			ТОГДА "" (Подразделение)""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ДополнительноеОписание
		|ИЗ
		|	#ИмяОсновнойТаблицы КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|		ПО Таблица.Объект = СтруктураПредприятия.Ссылка";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяОсновнойТаблицы", ИмяОсновнойТаблицы);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса выборки дополнительных сведений о группах контактов пользователей.
//
// Параметры:
//	ИмяОсновнойТаблицы - Строка - Имя таблицы, содержащей сведения об объектах адресной книги.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаВыборкиДополнительныхСведенийГруппыКонтактовПользователей(ИмяОсновнойТаблицы)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.Объект КАК Объект,
		|	Таблица.Родитель КАК Родитель,
		|	Таблица.РодительОбъекта КАК РодительОбъекта,
		|	Таблица.ОбъектДоступа КАК ОбъектДоступа,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА Таблица.Родитель <> ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ПустаяСсылка)
		|					И Таблица.ОтображатьРодителяВАдреснойКниге = ЛОЖЬ
		|				ТОГДА ЛОЖЬ
		|			ИНАЧЕ НЕ ГруппыКонтактовПользователей.ПометкаУдаления
		|		КОНЕЦ) КАК ОтображатьВАдреснойКниге,
		|	"""" КАК ПредставлениеОбъектаОсобое,
		|	"" ("" + СТРОКА(КОЛИЧЕСТВО(ГруппыКонтактовПользователейКонтакты.Контакт)) + "")"" КАК ДополнительноеОписание
		|ИЗ
		|	#ИмяОсновнойТаблицы КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыКонтактовПользователей КАК ГруппыКонтактовПользователей
		|		ПО Таблица.Объект = ГруппыКонтактовПользователей.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыКонтактовПользователей.Контакты КАК ГруппыКонтактовПользователейКонтакты
		|		ПО (Таблица.Объект = ГруппыКонтактовПользователей.Ссылка)
		|			И (ВЫБОР
		|				КОГДА ТИПЗНАЧЕНИЯ(ГруппыКонтактовПользователейКонтакты.Контакт) = ТИП(Справочник.Пользователи)
		|					ТОГДА ЕСТЬNULL(ВЫРАЗИТЬ(ГруппыКонтактовПользователейКонтакты.Контакт КАК Справочник.Пользователи).ПометкаУдаления = ЛОЖЬ, ИСТИНА)
		|							И ЕСТЬNULL(ВЫРАЗИТЬ(ГруппыКонтактовПользователейКонтакты.Контакт КАК Справочник.Пользователи).Служебный = ЛОЖЬ, ИСТИНА)
		|							И ЕСТЬNULL(ВЫРАЗИТЬ(ГруппыКонтактовПользователейКонтакты.Контакт КАК Справочник.Пользователи).Недействителен = ЛОЖЬ, ИСТИНА)
		|				КОГДА ТИПЗНАЧЕНИЯ(ГруппыКонтактовПользователейКонтакты.Контакт) = ТИП(СТРОКА)
		|						ИЛИ ГруппыКонтактовПользователейКонтакты.Контакт = НЕОПРЕДЕЛЕНО
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ ЕСТЬNULL(ГруппыКонтактовПользователейКонтакты.Контакт.ПометкаУдаления = ЛОЖЬ, ИСТИНА)
		|			КОНЕЦ)
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Объект,
		|	Таблица.Родитель,
		|	Таблица.РодительОбъекта,
		|	Таблица.ОбъектДоступа,
		|	ГруппыКонтактовПользователей.Ссылка";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяОсновнойТаблицы", ИмяОсновнойТаблицы);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса выборки дополнительных сведений о проектах.
//
// Параметры:
//	ИмяОсновнойТаблицы - Строка - Имя таблицы, содержащей сведения об объектах адресной книги.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаВыборкиДополнительныхСведенийПроекты(ИмяОсновнойТаблицы)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.Объект КАК Объект,
		|	Таблица.Родитель КАК Родитель,
		|	Таблица.РодительОбъекта КАК РодительОбъекта,
		|	Таблица.ОбъектДоступа КАК ОбъектДоступа,
		|	ВЫБОР
		|		КОГДА Таблица.Родитель <> ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.ПустаяСсылка)
		|				И Таблица.ОтображатьРодителяВАдреснойКниге = ЛОЖЬ
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ НЕ Проекты.ПометкаУдаления
		|	КОНЕЦ КАК ОтображатьВАдреснойКниге,
		|	"""" КАК ПредставлениеОбъектаОсобое,
		|	"""" КАК ДополнительноеОписание
		|ИЗ
		|	#ИмяОсновнойТаблицы КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Проекты КАК Проекты
		|		ПО Таблица.Объект = Проекты.Ссылка";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяОсновнойТаблицы", ИмяОсновнойТаблицы);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса выборки дополнительных сведений о сотрудниках.
//
// Параметры:
//	ИмяОсновнойТаблицы - Строка - Имя таблицы, содержащей сведения об объектах адресной книги.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаВыборкиДополнительныхСведенийСотрудники(ИмяОсновнойТаблицы)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.Объект КАК Объект,
		|	Таблица.Родитель КАК Родитель,
		|	Таблица.РодительОбъекта КАК РодительОбъекта,
		|	Таблица.ОбъектДоступа КАК ОбъектДоступа,
		|	Сотрудники.ПометкаУдаления = ЛОЖЬ
		|		И Сотрудники.Действует = ИСТИНА КАК ОтображатьВАдреснойКниге,
		|	Сотрудники.ПредставлениеВПереписке КАК ПредставлениеОбъектаОсобое,
		|	"""" КАК ДополнительноеОписание
		|ИЗ
		|	#ИмяОсновнойТаблицы КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО Таблица.Объект = Сотрудники.Ссылка";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяОсновнойТаблицы", ИмяОсновнойТаблицы);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса выборки дополнительных сведений о пользователях.
//
// Параметры:
//	ИмяОсновнойТаблицы - Строка - Имя таблицы, содержащей сведения об объектах адресной книги.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаВыборкиДополнительныхСведенийПользователи(ИмяОсновнойТаблицы)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.Объект КАК Объект,
		|	Таблица.Родитель КАК Родитель,
		|	Таблица.РодительОбъекта КАК РодительОбъекта,
		|	Таблица.ОбъектДоступа КАК ОбъектДоступа,
		|	НЕ Пользователи.ПометкаУдаления
		|		И НЕ Пользователи.Недействителен
		|		И НЕ Пользователи.Служебный КАК ОтображатьВАдреснойКниге,
		|	Пользователи.ПредставлениеВПерепискеСРангом КАК ПредставлениеОбъектаОсобое,
		|	"""" КАК ДополнительноеОписание
		|ИЗ
		|	#ИмяОсновнойТаблицы КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО Таблица.Объект = Пользователи.Ссылка";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяОсновнойТаблицы", ИмяОсновнойТаблицы);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса выборки дополнительных сведений о рабочих группах.
//
// Параметры:
//	ИмяОсновнойТаблицы - Строка - Имя таблицы, содержащей сведения об объектах адресной книги.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаВыборкиДополнительныхСведенийРабочиеГруппы(ИмяОсновнойТаблицы)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.Объект КАК Объект,
		|	Таблица.Родитель КАК Родитель,
		|	Таблица.РодительОбъекта КАК РодительОбъекта,
		|	Таблица.ОбъектДоступа КАК ОбъектДоступа,
		|	РабочиеГруппы.ПометкаУдаления <> ИСТИНА
		|		И РабочиеГруппы.Недействительна <> ИСТИНА КАК ОтображатьВАдреснойКниге,
		|	"""" КАК ПредставлениеОбъектаОсобое,
		|	ВЫБОР
		|		КОГДА Таблица.Родитель = ЗНАЧЕНИЕ(Справочник.АдреснаяКнига.Избранное)
		|				ИЛИ Таблица.ТипДанныхРодителя = ЗНАЧЕНИЕ(Перечисление.ТипыДанныхАдреснойКниги.ГруппаКонтактовПользователей)
		|			ТОГДА "" (Рабочая группа)""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ДополнительноеОписание
		|ИЗ
		|	#ИмяОсновнойТаблицы КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РабочиеГруппы КАК РабочиеГруппы
		|		ПО Таблица.Объект = РабочиеГруппы.Ссылка";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяОсновнойТаблицы", ИмяОсновнойТаблицы);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса выборки дополнительных сведений о контактных лицах.
//
// Параметры:
//	ИмяОсновнойТаблицы - Строка - Имя таблицы, содержащей сведения об объектах адресной книги.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаВыборкиДополнительныхСведенийКонтактныеЛица(ИмяОсновнойТаблицы)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.Объект КАК Объект,
		|	Таблица.Родитель КАК Родитель,
		|	Таблица.РодительОбъекта КАК РодительОбъекта,
		|	Таблица.ОбъектДоступа  КАК ОбъектДоступа,
		|	КонтактныеЛица.ПометкаУдаления <> ИСТИНА
		|		И КонтактныеЛица.НеДействует <> ИСТИНА КАК ОтображатьВАдреснойКниге,
		|	"""" КАК ПредставлениеОбъектаОсобое,
		|	"""" КАК ДополнительноеОписание
		|ИЗ
		|	#ИмяОсновнойТаблицы КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица КАК КонтактныеЛица
		|		ПО Таблица.Объект = КонтактныеЛица.Ссылка";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяОсновнойТаблицы", ИмяОсновнойТаблицы);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса выборки дополнительных сведений об объектах типа Строка.
//
// Параметры:
//	ИмяОсновнойТаблицы - Строка - Имя таблицы, содержащей сведения об объектах адресной книги.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаВыборкиДополнительныхСведенийСтрока(ИмяОсновнойТаблицы)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.Объект КАК Объект,
		|	Таблица.Родитель КАК Родитель,
		|	Таблица.РодительОбъекта КАК РодительОбъекта,
		|	Таблица.ОбъектДоступа КАК ОбъектДоступа,
		|	ИСТИНА КАК ОтображатьВАдреснойКниге,
		|	"""" КАК ПредставлениеОбъектаОсобое,
		|	"""" КАК ДополнительноеОписание
		|ИЗ
		|	#ИмяОсновнойТаблицы КАК Таблица
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(Таблица.Объект) = ТИП(СТРОКА)";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяОсновнойТаблицы", ИмяОсновнойТаблицы);
	
	Возврат ТекстЗапроса;	
	
КонецФункции

// Возвращает стандартный текст запроса выборки дополнительных сведений.
//
// Параметры:
//	ИмяОсновнойТаблицы - Строка - Имя таблицы, содержащей сведения об объектах адресной книги.
//	ИмяТаблицыОбъекта - Строка - Имя основной таблицы объекта.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаВыборкиДополнительныхСведенийСтандартный(ИмяОсновнойТаблицы, ИмяТаблицыОбъекта)

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.Объект КАК Объект,
		|	Таблица.Родитель КАК Родитель,
		|	Таблица.РодительОбъекта КАК РодительОбъекта,
		|	Таблица.ОбъектДоступа КАК ОбъектДоступа,
		|	НЕ ТаблицаОбъекта.ПометкаУдаления КАК ОтображатьВАдреснойКниге,
		|	"""" КАК ПредставлениеОбъектаОсобое,
		|	"""" КАК ДополнительноеОписание
		|ИЗ
		|	#ИмяОсновнойТаблицы КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ИмяТаблицыОбъекта КАК ТаблицаОбъекта
		|		ПО Таблица.Объект = ТаблицаОбъекта.Ссылка";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяОсновнойТаблицы", ИмяОсновнойТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяТаблицыОбъекта", ИмяТаблицыОбъекта);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли
