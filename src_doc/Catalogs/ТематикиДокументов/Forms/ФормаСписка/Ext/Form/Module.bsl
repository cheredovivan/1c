
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСТематикамиДокументов.УстановитьУсловноеОформлениеТематик(Список);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ПараметрыОтбора = Новый Соответствие;
	Параметрыотбора.Вставить("ПоказыватьУдаленные", ПоказыватьУдаленные);
	Параметрыотбора.Вставить("ПоказыватьНеДействующие", ПоказыватьНеДействующие);
	УстановитьОтборСписка(Список, Параметрыотбора);
	
	НастроитьФормуДляМобильногоПриНеобходимости();
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьОтборСписка(Список, Настройки);
	
	Если ПараметрыСеанса.ЭтоМобильныйКлиент Тогда
		Элементы.МК_ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
		Элементы.МК_ПоказыватьНеДействующие.Пометка = ПоказыватьНеДействующие;
	Иначе
		Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
		Элементы.ПоказыватьНеДействующие.Пометка = ПоказыватьНеДействующие;
	КонецЕсли;
	
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(
		Элементы.ОтборВидДокумента, ОтборВидДокумента);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтборВидДокументаПриИзменении(Элемент)
	
	ПараметрыОтбора = Новый Соответствие;
	Параметрыотбора.Вставить("ОтборВидДокумента", ОтборВидДокумента);
	
	УстановитьОтборСписка(Список, Параметрыотбора);
	
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(Элемент, ОтборВидДокумента);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		ТекущиеДанные = Элемент.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		ТекущаяСтрока = Элемент.ТекущаяСтрока;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначениеКопирования", ТекущаяСтрока);
	ИначеЕсли ЗначениеЗаполнено(ОтборВидДокумента) Тогда 
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ВидДокумента", ОтборВидДокумента);
	КонецЕсли;
	
	Открытьформу("Справочник.ТематикиДокументов.ФормаОбъекта", ПараметрыФормы, Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	ПараметрыОтбора = Новый Соответствие;
	Параметрыотбора.Вставить("ПоказыватьУдаленные", ПоказыватьУдаленные);
	
	УстановитьОтборСписка(Список, Параметрыотбора);
	
	#Если МобильныйКлиент Тогда
		Элементы.МК_ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	#Иначе
		Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;	
	#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьНеДействующие(Команда)
	
	ПоказыватьНеДействующие = Не ПоказыватьНеДействующие;
	ПараметрыОтбора = Новый Соответствие;
	Параметрыотбора.Вставить("ПоказыватьНеДействующие", ПоказыватьНеДействующие);
	
	УстановитьОтборСписка(Список, Параметрыотбора);
	
	#Если МобильныйКлиент Тогда
		Элементы.МК_ПоказыватьНеДействующие.Пометка = ПоказыватьНеДействующие;
	#Иначе
		Элементы.ПоказыватьНеДействующие.Пометка = ПоказыватьНеДействующие;	
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборСписка(Список, ПараметрыОтбора)
	
	// вид документа 
	ВидДокумента = ПараметрыОтбора.Получить("ОтборВидДокумента");
	Если ВидДокумента <> Неопределено Тогда 
		Если Не ЗначениеЗаполнено(ВидДокумента) Тогда
			ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор,
				"ВидДокумента");
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
				"ВидДокумента",
				ВидДокумента,
				ВидСравненияКомпоновкиДанных.ВИерархии);
		КонецЕсли;
	КонецЕсли;
	
	ПоказыватьУдаленные = ПараметрыОтбора.Получить("ПоказыватьУдаленные");
	Если ПоказыватьУдаленные <> Неопределено Тогда 
		Если ПоказыватьУдаленные Тогда
			ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "ПометкаУдаления");
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПометкаУдаления", Ложь);
		КонецЕсли;
	КонецЕсли;
	
	ПоказыватьНеДействующие = ПараметрыОтбора.Получить("ПоказыватьНеДействующие");
	Если ПоказыватьНеДействующие <> Неопределено Тогда 
		Если ПоказыватьНеДействующие Тогда
			Параметр = Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПоказыватьДействующие"));
			Параметр.Использование = Ложь;
			Параметр = Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ТекущаяДата"));
			Параметр.Использование = Ложь;
		Иначе
			Список.Параметры.УстановитьЗначениеПараметра("ПоказыватьДействующие", Истина);
			Список.Параметры.УстановитьЗначениеПараметра("ТекущаяДата", НачалоДня(ТекущаяДата()));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуДляМобильногоПриНеобходимости()
	
	Если Не ПараметрыСеанса.ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;

	Элементы.ГруппаБыстрыеОтборы.Видимость = Ложь;
	Элементы.МК_ФормаСоздать.Видимость = Истина;
	Элементы.МК_ОтборВидДокумента.Видимость = Истина;
	Элементы.МК_ПоказыватьУдаленные.Видимость = Истина;
	Элементы.МК_ПоказыватьНеДействующие.Видимость = Истина;
	
КонецПроцедуры

#КонецОбласти