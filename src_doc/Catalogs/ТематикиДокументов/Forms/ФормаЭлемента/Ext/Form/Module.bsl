#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПротоколированиеРаботыСотрудников.ЗаписатьОткрытие(Объект.Ссылка);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Элементы.ДекорацияДатаСоздания.Заголовок = СтрШаблон(НСтр("ru = 'Создана: %1, %2'; en = 'Created: %1, %2'"),
		Формат(Объект.ДатаСоздания, "ДЛФ=D"), Строка(Объект.Создал));
		
	Если Параметры.Свойство("ВидДокумента") И ЗначениеЗаполнено(Параметры.ВидДокумента) Тогда 
		Объект.ВидДокумента = Параметры.ВидДокумента;
		Элементы.ВидДокумента.Видимость = Ложь;
		
	ИначеЕсли Объект.Ссылка.Пустая() И ЗначениеЗаполнено(Объект.ВидДокумента) Тогда 
		Элементы.ВидДокумента.Видимость = Ложь;
		
	Иначе
		Элементы.ВидДокумента.ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
	Элементы.НеДействуетДата.Доступность = Объект.НеДействует;
	
	Если Объект.ПометкаУдаления Тогда 
		КлючНазначенияИспользования = "ТематикаУдалена";
	КонецЕсли;
	
	Нумерация.ПоказатьИндексНумерации(ЭтотОбъект);
	КоличествоНастроекПравилОбработки = НастройкиДействий.КоличествоНастроекПравилОбработки(Объект.Ссылка);
	Если КоличествоНастроекПравилОбработки > 0 Тогда 
		ПравилаОбработки = СтрШаблон(НСтр("ru = 'Правила обработки (%1)'"),
			КоличествоНастроекПравилОбработки);
	Иначе 
		ПравилаОбработки = НСтр("ru = 'Правила обработки (не заданы)'");
	КонецЕсли;
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбщегоНазначенияДокументооборот.ПоказатьНадписьПометкиУдаления(
		ЭтотОбъект, ТекущийОбъект.ПометкаУдаления);
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НастройкиДействий" Или ИмяСобытия = "Запись_ПравилаОбработки" Тогда
		КоличествоНастроекПравилОбработки = НастройкиДействийВызовСервера.КоличествоНастроекПравилОбработки(
			Объект.Ссылка);
		Если КоличествоНастроекПравилОбработки > 0 Тогда 
			ПравилаОбработки = СтрШаблон(НСтр("ru = 'Правила обработки (%1)'"),
				КоличествоНастроекПравилОбработки);
		Иначе 
			ПравилаОбработки = НСтр("ru = 'Правила обработки (не заданы)'");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", НЕ ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
	Если ЗначениеЗаполнено(ТекущийОбъект.Ссылка) Тогда 
		ПараметрыЗаписи.Вставить("ПредыдущееНеДействует", 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийОбъект.Ссылка, "НеДействует"));
	КонецЕсли;
	МультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПротоколированиеРаботыСотрудников.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыСотрудников.ЗаписатьИзменение(Объект.Ссылка);
	
	ИндексНумерации = СокрЛП(ИндексНумерации);
	Если ИндексНумерации <> ИндексНумерацииПриОткрытии Тогда 
		Если ЗначениеЗаполнено(ИндексНумерации) Тогда 
			РегистрыСведений.ИндексыНумерации.ЗаписатьИндексНумерации(Объект.Ссылка, ИндексНумерации);
		Иначе 
			РегистрыСведений.ИндексыНумерации.УдалитьИндексНумерации(Объект.Ссылка);
		КонецЕсли;
		
		ИндексНумерацииПриОткрытии = ИндексНумерации;
	КонецЕсли;
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.ЭтоНовыйОбъект Тогда 
		СтруктураОповещения = Новый Структура("ВидДокумента, Тематика",
			Объект.ВидДокумента, Объект.Ссылка);
		Оповестить("ТематикаДокумента_Создание", СтруктураОповещения); 
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("ПредыдущееНеДействует")
		И ПараметрыЗаписи.ПредыдущееНеДействует <> Объект.НеДействует Тогда 
		СтруктураОповещения = Новый Структура("ВидДокумента, Тематика",
			Объект.ВидДокумента, Объект.Ссылка);
		Оповестить("ТематикаДокумента_Изменилась", СтруктураОповещения); 
	КонецЕсли;
	
	Оповестить("Тематика_Запись", Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НеДействуетПриИзменении(Элемент)
	
	Если Объект.НеДействует Тогда
		Если Объект.НеДействуетДата = '00010101' Тогда
			Объект.НеДействуетДата = ТекущаяДата();
		КонецЕсли;
	Иначе
		Объект.НеДействуетДата = '00010101';
	КонецЕсли;
	
	Элементы.НеДействуетДата.Доступность = Объект.НеДействует;
	
КонецПроцедуры

&НаКлиенте
Процедура ПравилаОбработкиНажатие(Элемент, СтандартнаяОбработка)
	
	НастройкиДействийКлиент.ПравилаОбработкиНажатие(ЭтаФорма, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)
	
	МультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры
#КонецОбласти