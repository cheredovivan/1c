#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Проверка дублей исполнителей
	КоличествоИсполнителей = Исполнители.Количество();
	Для Инд1 = 0 По КоличествоИсполнителей-2 Цикл
		Строка1 = Исполнители[Инд1];
		Если Не ЗначениеЗаполнено(Строка1.Исполнитель) Тогда 
			Продолжить;
		КонецЕсли;
		
		Для Инд2 = Инд1+1 По КоличествоИсполнителей-1 Цикл
			Строка2 = Исполнители[Инд2];
			
			Если Строка1.Исполнитель = Строка2.Исполнитель Тогда 
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Исполнитель ""%1"" указан дважды в списке исполнителей!'"), 
					Строка(Строка2.Исполнитель));
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения, ЭтотОбъект,
					"Исполнители[" + Формат(Строка2.НомерСтроки-1, "ЧГ=0") + "].Исполнитель",, 
					Отказ);
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	// Проверка дублей предшественников
	КоличествоПредшественников = Предшественники.Количество();
	Для Инд1 = 0 По КоличествоПредшественников-2 Цикл
		Строка1 = Предшественники[Инд1];
		Если Не ЗначениеЗаполнено(Строка1.Предшественник) Тогда 
			Продолжить;
		КонецЕсли;
		
		Для Инд2 = Инд1+1 По КоличествоПредшественников-1 Цикл
			Строка2 = Предшественники[Инд2];
			
			Если Строка1.Предшественник = Строка2.Предшественник Тогда 
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Предшественник ""%1"" указан дважды в списке предшественников!'"), 
					Строка(Строка2.Предшественник));
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения, ЭтотОбъект,
					"Предшественники[" + Формат(Строка2.НомерСтроки-1, "ЧГ=0") + "].Предшественник",, 
					Отказ);
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.НачалоНеПозднее 
	 Или ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.НачалоНеРанее
	 Или ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.ОкончаниеНеПозднее
	 Или ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.ОкончаниеНеРанее 
	 Или ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.ФиксированноеНачало
	 Или ТипОграничения = Перечисления.ТипыОграниченийПроектныхЗадач.ФиксированноеОкончание Тогда 
		ПроверяемыеРеквизиты.Добавить("ДатаОграничения");
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		ДополнительныеСвойства.Вставить("ЭтоНовый", Истина);
	КонецЕсли;
	
	РеквизитыСсылки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка,
		"ПометкаУдаления,
		|Родитель,
		|Предмет,
		|Исполнители,
		|ИсполнителямРазрешеноИзменятьПредмет");
	
	ПредыдущаяПометкаУдаления = Ложь;
	Если Не Ссылка.Пустая() Тогда
		ПредыдущаяПометкаУдаления = РеквизитыСсылки.ПометкаУдаления;
		
		Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда 
			Если ПометкаУдаления Тогда
				ДополнительныеСвойства.Вставить("НужноПометитьНаУдалениеБизнесСобытия", Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ТекущийРодитель = РеквизитыСсылки.Родитель;
	ДополнительныеСвойства.Вставить("ТекущийРодитель", ТекущийРодитель);
	ДополнительныеСвойства.Вставить(
		"ПометкаУдаления", 
		?(Ссылка.Пустая(), Ложь, РеквизитыСсылки.ПометкаУдаления));
	
	// Расширение РГ предмета.
	Если ДелопроизводствоКлиентСервер.ЭтоДокумент(Предмет) Тогда
		ДобавляемыеУчастникиРГ = Новый Соответствие;
		ТипКонтейнерСотрудников = Метаданные.ОпределяемыеТипы.КонтейнерыСотрудников.Тип;
		Если РеквизитыСсылки.Предмет <> Предмет
			Или ИсполнителямРазрешеноИзменятьПредмет = Истина
				И РеквизитыСсылки.ИсполнителямРазрешеноИзменятьПредмет <> Истина Тогда
			
			// Добавление всех исполнителей.
			Для Каждого Стр Из Исполнители Цикл
				Если ТипКонтейнерСотрудников.СодержитТип(ТипЗнч(Стр.Исполнитель)) Тогда
					ДобавляемыеУчастникиРГ[Стр.Исполнитель] = ИсполнителямРазрешеноИзменятьПредмет;
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			НовыеИсполнители = Исполнители.ВыгрузитьКолонку("Исполнитель");
			Для Каждого Стр Из РеквизитыСсылки.Исполнители.Выгрузить() Цикл
				ИндексНайденногоЭлемента = НовыеИсполнители.Найти(Стр.Исполнитель);
				Если ИндексНайденногоЭлемента <> Неопределено Тогда
					НовыеИсполнители.Удалить(ИндексНайденногоЭлемента);
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого Исполнитель Из НовыеИсполнители Цикл
				Если ТипКонтейнерСотрудников.СодержитТип(ТипЗнч(Исполнитель)) Тогда
					ДобавляемыеУчастникиРГ[Исполнитель] = ИсполнителямРазрешеноИзменятьПредмет;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		ДополнительныеСвойства.Вставить("ДобавляемыеУчастникиРГ", ДобавляемыеУчастникиРГ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ДополнительныеСвойства.Свойство("ПроверитьПредшественников") И ДополнительныеСвойства.ПроверитьПредшественников Тогда
		РаботаСПроектами.ПроверитьПредшественников(Ссылка);
	КонецЕсли;	   
	
	// Возможно, выполнена явная регистрация событий при загрузке объекта.
	Если Не ДополнительныеСвойства.Свойство("НеРегистрироватьБизнесСобытия") Тогда
		Если ДополнительныеСвойства.Свойство("ЭтоНовый") И ДополнительныеСвойства.ЭтоНовый Тогда
			БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(Ссылка, Справочники.ВидыБизнесСобытий.СозданиеПроектнойЗадачи);	
		Иначе
			БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(Ссылка, Справочники.ВидыБизнесСобытий.ИзменениеПроектнойЗадачи);
		КонецЕсли;	
	КонецЕсли;	
	
	Если ДополнительныеСвойства.Свойство("НужноПометитьНаУдалениеБизнесСобытия") Тогда
		БизнесСобытияВызовСервера.ПометитьНаУдалениеСобытияПоИсточнику(Ссылка);
	КонецЕсли;	

	// установка признака суммарной задачи у родителя
	ТекущийРодитель = ДополнительныеСвойства.ТекущийРодитель;
	Если Родитель <> ТекущийРодитель Тогда 
		
		Если ЗначениеЗаполнено(ТекущийРодитель) Тогда 
			ЭтоСуммарнаяЗадача = РаботаСПроектами.ЭтоСуммарнаяЗадача(ТекущийРодитель);
			Если ЭтоСуммарнаяЗадача <> ТекущийРодитель.СуммарнаяЗадача Тогда 
				ЗаблокироватьДанныеДляРедактирования(ТекущийРодитель.Ссылка);
				РодительОбъект = ТекущийРодитель.ПолучитьОбъект();
				РодительОбъект.СуммарнаяЗадача = ЭтоСуммарнаяЗадача;
				РодительОбъект.Записать();
			КонецЕсли;
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(Родитель) Тогда 
			ЭтоСуммарнаяЗадача = РаботаСПроектами.ЭтоСуммарнаяЗадача(Родитель);
			Если ЭтоСуммарнаяЗадача <> Родитель.СуммарнаяЗадача Тогда 
				ЗаблокироватьДанныеДляРедактирования(Родитель.Ссылка);
				РодительОбъект = Родитель.ПолучитьОбъект();
				РодительОбъект.СуммарнаяЗадача = ЭтоСуммарнаяЗадача;
				РодительОбъект.Записать();
			КонецЕсли;
		КонецЕсли;	
		
	КонецЕсли;	
	
	ИдентификаторФормы = Неопределено;
	ДополнительныеСвойства.Свойство("ИдентификаторФормы", ИдентификаторФормы);
	
	Если ПометкаУдаления <> ДополнительныеСвойства.ПометкаУдаления Тогда
		РаботаСПроектами.ЗаполнитьКодыСДРПроектныхЗадач(Владелец.Ссылка, ИдентификаторФормы);
	КонецЕсли;
		
	// Расширение РГ предмета.
	Если РаботаСРабочимиГруппами.ПоОбъектуВедетсяАвтоматическоеЗаполнениеРабочейГруппы(Предмет) Тогда
		УстановитьПривилегированныйРежим(Истина);
		ДобавляемыеУчастникиРГ = Неопределено;
		Если ДополнительныеСвойства.Свойство("ДобавляемыеУчастникиРГ", ДобавляемыеУчастникиРГ)
			И ДобавляемыеУчастникиРГ.Количество() > 0 Тогда
			ТаблицаНабора = РаботаСРабочимиГруппами.ПолучитьРабочуюГруппуДокумента(Предмет);
			КолСтрокДоДобавления = ТаблицаНабора.Количество();
			Для Каждого КлючИЗначение Из ДобавляемыеУчастникиРГ Цикл
				РаботаСРабочимиГруппами.ДобавитьУчастникаВТаблицуНабора(
					ТаблицаНабора, КлючИЗначение.Ключ, КлючИЗначение.Значение);
			КонецЦикла;
			Если ТаблицаНабора.Количество() > КолСтрокДоДобавления Тогда
				РаботаСРабочимиГруппами.ПерезаписатьРабочуюГруппуОбъекта(
					Предмет, ТаблицаНабора, Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ВстроеннаяПочтаКлиентСервер.ЭтоПисьмо(ДанныеЗаполнения) Тогда
		Предмет = ДанныеЗаполнения;
		Наименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "Тема");
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	СуммарнаяЗадача = Ложь;
	КодСДР = "";
	НомерЗадачиВУровне = 0;
	НачалоФакт = '00010101';
	ОкончаниеФакт = '00010101';
	ДлительностьФакт = 0;

КонецПроцедуры

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
