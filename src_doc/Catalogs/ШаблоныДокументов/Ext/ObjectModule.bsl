#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Или ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ПредыдущиеЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка,
			"ПометкаУдаления");
	Иначе
		ПредыдущиеЗначенияРеквизитов = Новый Структура(
			"ПометкаУдаления", Ложь);
	КонецЕсли;
	
	РеквизитыВидаДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидДокумента,
		"ВестиУчетПоОрганизациям, ВестиУчетСторон, ЯвляетсяИсходящейКорреспонденцией");
	
	// Сброс организации при смене вида.
	Если ЗначениеЗаполнено(Организация)
		И ЗначениеЗаполнено(ВидДокумента)
		И ЗначениеЗаполнено(Ссылка) Тогда
		
		ВестиУчетПоОрганизациям = РеквизитыВидаДокумента.ВестиУчетПоОрганизациям;
		Если ВестиУчетПоОрганизациям = Ложь Тогда
			Организация = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	// Обработка рабочей группы
	СсылкаОбъекта = Ссылка;
	// Установка ссылки нового
	Если Не ЗначениеЗаполнено(СсылкаОбъекта) Тогда
		СсылкаОбъекта = ПолучитьСсылкуНового();
		Если Не ЗначениеЗаполнено(СсылкаОбъекта) Тогда
			СсылкаНового = Справочники.ШаблоныДокументов.ПолучитьСсылку();
			УстановитьСсылкуНового(СсылкаНового);
			СсылкаОбъекта = СсылкаНового;
		КонецЕсли;
	КонецЕсли;
	
	// Подготовка рабочей группы
	РабочаяГруппа = РегистрыСведений.РабочиеГруппы.ПолучитьУчастниковПоОбъекту(СсылкаОбъекта);
		
	// Добавление участников, переданных "снаружи", например из формы объекта
	Если ДополнительныеСвойства.Свойство("РабочаяГруппаДобавить") Тогда
		
		Для Каждого Эл Из ДополнительныеСвойства.РабочаяГруппаДобавить Цикл
			
			// Добавление участника в итоговую рабочую группу
			Строка = РабочаяГруппа.Добавить();
			Строка.Участник = Эл.Участник;
			Строка.Изменение = Эл.Изменение;
			
		КонецЦикла;	
			
	КонецЕсли;		
	
	// Удаление участников, переданных "снаружи", например из формы объекта
	Если ДополнительныеСвойства.Свойство("РабочаяГруппаУдалить") Тогда
		
		Для Каждого Эл Из ДополнительныеСвойства.РабочаяГруппаУдалить Цикл
			
			// Поиск удаляемого участника в итоговой рабочей группе
			Для Каждого Эл2 Из РабочаяГруппа Цикл
				
				Если Эл2.Участник = Эл.Участник 
					И Эл2.Изменение = Эл.Изменение Тогда
					
					// Удаление участника из итоговой рабочей группы
					РабочаяГруппа.Удалить(Эл2);
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;	
				
		КонецЦикла;	
			
	КонецЕсли;			
	
	// Запись итоговой рабочей группы
	РаботаСРабочимиГруппами.ПерезаписатьРабочуюГруппуОбъекта(
		СсылкаОбъекта,
		РабочаяГруппа,
		Ложь); // ОбновитьПрава
	
	// Установка необходимости обновления прав доступа
	ДополнительныеСвойства.Вставить("ДополнительныеПравообразующиеЗначенияИзменены");
	
	// Пометка на удаление приложенных файлов.
	Если ПометкаУдаления <> ПредыдущиеЗначенияРеквизитов.ПометкаУдаления Тогда 
		
		Если ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(Ссылка).Удаление Тогда 
			РаботаСФайламиВызовСервера.ПометитьНаУдалениеПриложенныеФайлы(Ссылка, ПометкаУдаления);
			ШаблоныДокументов.ПометитьНаУдалениеПравилаЗаполнения(Ссылка, ПометкаУдаления);
		Иначе
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У вас нет права ""Пометка на удаление"" шаблона ""%1"".'"),
				Строка(Ссылка));
		КонецЕсли;	
		
	КонецЕсли;
	
	КоличествоКонтрагентов = Контрагенты.Количество(); 
	Для ОбратныйИндекс = 1 По КоличествоКонтрагентов Цикл 
		
		Строка = Контрагенты[КоличествоКонтрагентов - ОбратныйИндекс]; 
		Если Не ЗначениеЗаполнено(Строка.Контрагент) Тогда 
			Контрагенты.Удалить(Строка); 
		КонецЕсли; 
	КонецЦикла;
	
	Если Контрагенты.Количество() = 0 Тогда
		Контрагент = Неопределено;
		КонтактноеЛицо = Неопределено;
	Иначе
		Строка = Контрагенты[0];
		Контрагент = Строка.Контрагент;
		КонтактноеЛицо = Строка.КонтактноеЛицо;
		Если РеквизитыВидаДокумента.ЯвляетсяИсходящейКорреспонденцией = Истина Тогда
			Способ = Строка.Способ;
		КонецЕсли;
	КонецЕсли;
	
	// Работа со сторонами
	КоличествоСторон = Стороны.Количество(); 
	Для ОбратныйИндекс = 1 По КоличествоСторон Цикл 
		
		Строка = Стороны[КоличествоСторон - ОбратныйИндекс];
		Если Не ЗначениеЗаполнено(Строка.Сторона)
			И Не ЗначениеЗаполнено(Строка.Наименование)
			И Не ЗначениеЗаполнено(Строка.КонтактноеЛицо) Тогда 
				Стороны.Удалить(Строка);
		КонецЕсли; 
		
	КонецЦикла;
	
	Если (ДополнительныеСвойства.Свойство("ВестиУчетСторон") И ДополнительныеСвойства.ВестиУчетСторон)
		ИЛИ (ЗначениеЗаполнено(ВидДокумента) И РеквизитыВидаДокумента.ВестиУчетСторон = Истина) Тогда
			РаботаСПодписямиДокументов.ПеренестиКонтрагентовИзСторон(Контрагенты, Стороны);
			РаботаСПодписямиДокументов.ПеренестиОрганизациюИзСторон(Организация, Стороны); 
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоГруппа Тогда 
		Возврат;
	КонецЕсли;
	
	Автор = Сотрудники.ОсновнойСотрудник();
	
	Если Не ЗначениеЗаполнено(Организация) Тогда 
		Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидДокумента) Тогда 
		РеквизитыВида = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидДокумента,
			"ВестиУчетПоНоменклатуреДел, ВестиУчетСторон, ЯвляетсяЗаявкойНаОплату, ВариантПодписания");
		
		Если РеквизитыВида.ВестиУчетПоНоменклатуреДел Тогда
			Делопроизводство.ПроверитьСоответствиеНоменклатурыДел(ЭтотОбъект, Отказ);
		КонецЕсли;
		
		Если РеквизитыВида.ВестиУчетСторон Тогда
			РаботаСПодписямиДокументов.ПроверитьЗаполнениеСторон(
				ЭтотОбъект, Отказ, РеквизитыВида.ЯвляетсяЗаявкойНаОплату, РеквизитыВида.ВариантПодписания);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если НЕ ЭтоГруппа Тогда
		Автор = Сотрудники.ОсновнойСотрудник();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли