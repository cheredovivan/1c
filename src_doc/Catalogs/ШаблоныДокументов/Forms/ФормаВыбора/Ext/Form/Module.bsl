#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыОтбора = Новый Соответствие;
	Параметрыотбора.Вставить("ПоказыватьУдаленные", ПоказыватьУдаленные);
	УстановитьОтборСписка(Список, Параметрыотбора);
	
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьОтборСписка(Список, Настройки);
	
	Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписанШаблонДокумента" Тогда
		Элементы.Список.Обновить();
		Элементы.Список.ТекущаяСтрока = Параметр.Шаблон;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура КомандаСоздать(Знач Команда)
	
	ТекстВопроса = НСтр(
		"ru = 'Новый шаблон можно добавить только в карточке вида документа на закладке «Создание документов».
			|Открыть список видов документов?'");
	Ответ = Ждать ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНетАсинх(ТекстВопроса);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОткрытьФорму("ОбщаяФорма.ВидыДокументов",, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	ПараметрыОтбора = Новый Соответствие;
	Параметрыотбора.Вставить("ПоказыватьУдаленные", ПоказыватьУдаленные);
	
	УстановитьОтборСписка(Список, Параметрыотбора);
	
	Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборСписка(Список, ПараметрыОтбора)
	
	ПоказыватьУдаленные = ПараметрыОтбора.Получить("ПоказыватьУдаленные");
	Если ПоказыватьУдаленные <> Неопределено Тогда 
		Если ПоказыватьУдаленные Тогда
			ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "ПометкаУдаления");
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПометкаУдаления", Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти