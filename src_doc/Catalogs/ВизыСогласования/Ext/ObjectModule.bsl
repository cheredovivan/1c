#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Если ЭтоНовый() Тогда
		ДатаСоздания = ТекущаяДатаСеанса();
	КонецЕсли;	

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДатаСоздания = ТекущаяДатаСеанса();

КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка прав на добавление или изменение. Права на добавление имеет автор, 
	// на изменение - автор и исполнитель, а также их делегаты и руководители.
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Если ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа()
		И Не ПривилегированныйРежим()
		И Не Пользователи.ЭтоПолноправныйПользователь(ТекущийПользователь) Тогда
		
		// Права на добавление/изменение документа
		ПраваНаДокумент = ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(Документ);
		ОперацияРазрешена = ?(ЭтоНовый(), 
			ПраваНаДокумент.Добавление Или ПраваНаДокумент.Изменение,
			ПраваНаДокумент.Изменение);
		
		// Для Визы по действию, достаточно права на документ
		Если ОперацияРазрешена И Не ДействияКлиентСервер.ЭтоДействиеСогласования(Источник) Тогда
			
			// Права на добавление/изменение визы
			Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	ЕСТЬNULL(МАКСИМУМ(ВложенныйЗапрос.ПравоДобавления), ЛОЖЬ) КАК ПравоДобавления,
				|	ЕСТЬNULL(МАКСИМУМ(ВложенныйЗапрос.ПравоИзменения), ЛОЖЬ) КАК ПравоИзменения
				|ИЗ
				|(
				|	ВЫБРАТЬ
				|		ИСТИНА КАК ПравоДобавления,
				|		ИСТИНА КАК ПравоИзменения
				|	ИЗ
				|		РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
				|	ГДЕ
				|		СоставСубъектов.Пользователь = &ТекущийПользователь
				|		И СоставСубъектов.Субъект = &Автор
				|	
				|	ОБЪЕДИНИТЬ ВСЕ
				|	
				|	ВЫБРАТЬ
				|		ЛОЖЬ,
				|		ИСТИНА
				|	ИЗ
				|		РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
				|	ГДЕ
				|		СоставСубъектов.Пользователь = &ТекущийПользователь
				|		И СоставСубъектов.Субъект = &Исполнитель
				|) КАК ВложенныйЗапрос");
			
			Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
			Запрос.УстановитьПараметр("Автор", Автор);
			Запрос.УстановитьПараметр("Исполнитель", ?(ЗначениеЗаполнено(Исполнитель), Исполнитель, РольИсполнителя));
			
			Выборка = Запрос.Выполнить().Выбрать();
			Выборка.Следующий();
			
			ОперацияРазрешена = ?(ЭтоНовый(), Выборка.ПравоДобавления, Выборка.ПравоИзменения);
			
		КонецЕсли;
		
		Если Не ОперацияРазрешена Тогда
			ВызватьИсключение НСтр("ru = 'Недостаточно прав для выполнения операции. 
			|Обратитесь к администратору.'");
		КонецЕсли;
		
	КонецЕсли;
	
	// Расширение рабочей группы.
	Если Не ПометкаУдаления
		И РаботаСРабочимиГруппами.ПоОбъектуВедетсяАвтоматическоеЗаполнениеРабочейГруппы(Документ)
		И Не ДействияКлиентСервер.ЭтоДействиеСогласования(Источник) Тогда
		ТаблицаУчастников = РаботаСРабочимиГруппами.ПолучитьПустуюТаблицуУчастников();
		РаботаСРабочимиГруппами.ДобавитьУчастникаВТаблицуНабора(ТаблицаУчастников, Исполнитель);
		РаботаСРабочимиГруппами.ДобавитьУчастникаВТаблицуНабора(ТаблицаУчастников, Автор);
		Попытка
			РаботаСРабочимиГруппами.ДобавитьУчастниковВРабочуюГруппуОбъекта(
				Документ, ТаблицаУчастников, Истина);
		Исключение
			Если ЗначениеЗаполнено(Источник) Тогда
				// Если виза создана процессом или задачей, то
				// ошибку перезаписи обрабатываем особым образом.
				ПредставлениеПредмета = Строка(Документ);
				ТипПредмета = ТипЗнч(Документ);
				РаботаСРабочимиГруппами.ОбработатьИсключениеПерезаписиРабочейГруппыПредметаПроцесса(
					ПредставлениеПредмета,
					ТипПредмета,
					ИнформацияОбОшибке());
			Иначе
				ВызватьИсключение;
			КонецЕсли;
		КонецПопытки;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		ДополнительныеСвойства.Вставить("ПредыдущийРезультатСогласования", Неопределено);
	Иначе
		ДополнительныеСвойства.Вставить("ПредыдущийРезультатСогласования", 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "РезультатСогласования"));
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// При установке результата зафиксируем версии файла документа
	Если ЗначениеЗаполнено(РезультатСогласования) 
		И Не ЗначениеЗаполнено(ДополнительныеСвойства.ПредыдущийРезультатСогласования) Тогда
		РегистрыСведений.ВерсииСогласованныхФайлов.ДобавитьВерсииФайловПоВизе(Ссылка, Документ);
	
	ИначеЕсли Не ЗначениеЗаполнено(РезультатСогласования) 
		И ЗначениеЗаполнено(ДополнительныеСвойства.ПредыдущийРезультатСогласования) Тогда
		РегистрыСведений.ВерсииСогласованныхФайлов.УдалитьВерсииФайловПоВизе(Ссылка);
		
	КонецЕсли;
			
КонецПроцедуры

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
