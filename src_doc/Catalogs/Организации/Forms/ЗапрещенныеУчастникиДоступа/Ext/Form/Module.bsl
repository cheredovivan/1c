#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Организация = Параметры.Организация;
	КонецЕсли;
	УстановитьОтборПоОрганизации();
	
	СписокВыбораТипов = Элементы.ТипОбъектов.СписокВыбора;
	Для Каждого ЭлементМетаданных Из МетаданныеОбъектовДляОбработки() Цикл
		СписокВыбораТипов.Добавить(ЭлементМетаданных.Синоним);
	КонецЦикла;
	ТипОбъектов = СписокВыбораТипов[0].Значение;
	ПрименитьТипОбъектов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	УстановитьОтборПоОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектовПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ТипОбъектов) Тогда
		ТипОбъектов = Элементы.ТипОбъектов.СписокВыбора[0].Значение;
	КонецЕсли;
	ПрименитьТипОбъектов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРасхождения

&НаКлиенте
Процедура РасхожденияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущийЭлемент = Элементы.РасхожденияОрганизация Тогда
		ПоказатьЗначение(, ВыбраннаяСтрока.Организация);
	Иначе
		ПоказатьЗначение(, ВыбраннаяСтрока.Объект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура УдалитьАвтоматически(Команда)
	
	Ответ = Ждать ВопросАсинх(
		РаботаСРабочимиГруппамиКлиент.ТекстВопроса_УвереныУдалитьЗапрещенныхУчастников(),
		РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ОбработаноУспешно = 0;
	ОбработаноСОшибкой = 0;
	
	ВсегоОбъектов = ОбъектыКОбработке(Истина);
	ШаблонТекстаСостояния =
		НСтр("ru = 'Найдено объектов: %1
					|Обработано: %2
					|Не удалось обработать: %3%4'");
	Состояние(СтрШаблон(ШаблонТекстаСостояния, ВсегоОбъектов, ОбработаноУспешно, ОбработаноСОшибкой,
		?(ОбработаноСОшибкой = 0, "", ", " + НСтр("ru = 'подробности см. в журнале регистрации'"))));
	
	НомерИтерации = 1;
	ЛимитКоличестваИтераций = 100000;

	//Первая итерация
	РезультатИтерации = ВыполнитьИтерацию();
	
	Пока РезультатИтерации.ЕстьОбъектыКОбработке
		И НомерИтерации < ЛимитКоличестваИтераций
		И ОбработаноУспешно + ОбработаноСОшибкой <= ВсегоОбъектов Цикл
		ОбработаноУспешно = ОбработаноУспешно + РезультатИтерации.ОбработаноУспешно;
		ОбработаноСОшибкой = ОбработаноСОшибкой + РезультатИтерации.ОбработаноСОшибкой;
		Состояние(СтрШаблон(ШаблонТекстаСостояния, ВсегоОбъектов, ОбработаноУспешно, ОбработаноСОшибкой,
			?(ОбработаноСОшибкой = 0, "", ", " + НСтр("ru = 'подробности см. в журнале регистрации'"))));
		РезультатИтерации = ВыполнитьИтерацию(); //@skip-check query-in-loop - порционная обработка.
		НомерИтерации = НомерИтерации + 1;
		ОбработкаПрерыванияПользователя();
	КонецЦикла;
	
	Элементы.Расхождения.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборПоОрганизации()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Расхождения, "Организация", Организация,,,
		ЗначениеЗаполнено(Организация),
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
	Элементы.Расхождения.Обновить();
		
КонецПроцедуры

&НаСервере
Функция ВыполнитьИтерацию()
	
	ЗаписьЖурналаРегистрации(
		СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,,,
		НСтр("ru = 'Начало итерации удаления запрещенных участников доступа'"));
				
	ОбработаноУспешно = 0;
	ОбработаноСОшибкой = 0;
	
	ВыбранныеОбъекты = ОбъектыКОбработке();
	Для Каждого ВыбранныйОбъект Из ВыбранныеОбъекты Цикл
		НачатьТранзакцию();
		Попытка
			ОбработатьОбъект(ВыбранныйОбъект); //@skip-check query-in-loop
			ОбработаноУспешно = ОбработаноУспешно + 1;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ОбработаноСОшибкой = ОбработаноСОшибкой + 1;
			ЗаписьЖурналаРегистрации(
				СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,
				ВыбранныйОбъект.Метаданные(), ВыбранныйОбъект,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации(
		СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,,,
		НСтр("ru = 'Завершение итерации удаления запрещенных участников доступа'"));
				
	Возврат Новый Структура("ЕстьОбъектыКОбработке, ОбработаноУспешно, ОбработаноСОшибкой",
		ВыбранныеОбъекты.Количество() > 0, ОбработаноУспешно, ОбработаноСОшибкой);
	
КонецФункции

&НаСервере
Функция ОбъектыКОбработке(ТолькоКоличество = Ложь)
	
	Возврат Справочники.Организации.ОбъектыСЗапрещеннымиУчастникамиДоступа(
		МетаданныеПоТипуОбъектов(ТипОбъектов), ТолькоКоличество, Организация);
	
КонецФункции

&НаСервере
Процедура ОбработатьОбъект(ВыбранныйОбъект)
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РабочиеГруппы");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Объект", ВыбранныйОбъект);
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	СотрудникиВКонтейнерах.Сотрудник КАК Сотрудник,
			|	РабочиеГруппы.Участник КАК Участник
			|ПОМЕСТИТЬ СотрудникиРГ
			|ИЗ
			|	Справочник.ДокументыПредприятия КАК ОбъектыДоступа
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РабочиеГруппы КАК РабочиеГруппы
			|		ПО (ОбъектыДоступа.Ссылка = РабочиеГруппы.Объект)
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
			|		ПО (РабочиеГруппы.Участник = СотрудникиВКонтейнерах.Контейнер)
			|ГДЕ
			|	ОбъектыДоступа.Ссылка = &ВыбранныйОбъект
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Организации.Ссылка КАК Ссылка,
			|	ДескрипторыДляОбъектов.Дескриптор КАК Дескриптор
			|ПОМЕСТИТЬ ОрганизацииИДескрипторы
			|ИЗ
			|	Справочник.Организации КАК Организации
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДокументыПредприятия КАК ОбъектыДоступа
			|		ПО (ОбъектыДоступа.Организация = Организации.Ссылка)
			|			И (ОбъектыДоступа.Ссылка = &ВыбранныйОбъект)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|		ПО Организации.Ссылка = ДескрипторыДляОбъектов.Объект
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник КАК Сотрудник
			|ПОМЕСТИТЬ ИмеющиеПрава
			|ИЗ
			|	ОрганизацииИДескрипторы КАК ОрганизацииИДескрипторы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|		ПО ОрганизацииИДескрипторы.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СотрудникиРГ.Участник КАК Участник
			|ИЗ
			|	СотрудникиРГ КАК СотрудникиРГ
			|		ЛЕВОЕ СОЕДИНЕНИЕ ИмеющиеПрава КАК ИмеющиеПрава
			|		ПО (СотрудникиРГ.Сотрудник = ИмеющиеПрава.Сотрудник)
			|ГДЕ
			|	ИмеющиеПрава.Сотрудник ЕСТЬ NULL");
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДокументыПредприятия",
			МетаданныеПоТипуОбъектов(ТипОбъектов).Имя);
		Запрос.УстановитьПараметр("ВыбранныйОбъект", ВыбранныйОбъект);
		Запрос.УстановитьПараметр("ГруппаДоступа", Организация);
			
		ТаблицаУчастников = РаботаСРабочимиГруппами.ПолучитьРабочуюГруппуДокумента(ВыбранныйОбъект);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НайденнаяСтрока = ТаблицаУчастников.Найти(Выборка.Участник, "Участник");
			Если НайденнаяСтрока <> Неопределено Тогда
				ТаблицаУчастников.Удалить(НайденнаяСтрока);
			КонецЕсли;
		КонецЦикла;
		РаботаСРабочимиГруппами.ПерезаписатьРабочуюГруппуОбъекта(
			ВыбранныйОбъект, ТаблицаУчастников, Истина);
	
		ЗафиксироватьТранзакцию();
	
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция СобытиеЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Удаление запрещенных участников доступа'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

&НаСервереБезКонтекста
Функция МетаданныеОбъектовДляОбработки()
	
	Возврат ДокументооборотПраваДоступа.МетаданныеОбъектовДляПоискаЗапрещенныхУчастников();
	
КонецФункции

&НаСервереБезКонтекста
Функция МетаданныеПоТипуОбъектов(ТипОбъектов)

	МетаданныеОбъектов = Неопределено;
	Для Каждого ЭлементМетаданных Из МетаданныеОбъектовДляОбработки() Цикл
		Если ЭлементМетаданных.Синоним = ТипОбъектов Тогда
			МетаданныеОбъектов = ЭлементМетаданных;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если МетаданныеОбъектов = Неопределено Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Не найден элемент метаданных ""%1""'"), ТипОбъектов);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Возврат МетаданныеОбъектов;
	
КонецФункции

&НаСервере
Процедура ПрименитьТипОбъектов()
	
	МетаданныеОбъектов = МетаданныеПоТипуОбъектов(ТипОбъектов);
	Элементы.РасхожденияОбъект.Заголовок = 
		?(ЗначениеЗаполнено(МетаданныеОбъектов.ПредставлениеОбъекта),
			МетаданныеОбъектов.ПредставлениеОбъекта, МетаданныеОбъектов.Имя);
		
	ШаблонТекстаЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РабочиеГруппы.Участник КАК Участник,
		|	СотрудникиВКонтейнерах.Сотрудник КАК Сотрудник,
		|	ОбъектыДоступа.Ссылка КАК Ссылка,
		|	ОбъектыДоступа.Организация КАК Организация
		|ПОМЕСТИТЬ СотрудникиРГ
		|ИЗ
		|	Справочник.ДокументыПредприятия КАК ОбъектыДоступа
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РабочиеГруппы КАК РабочиеГруппы
		|		ПО (ОбъектыДоступа.Ссылка = РабочиеГруппы.Объект)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
		|		ПО (РабочиеГруппы.Участник = СотрудникиВКонтейнерах.Контейнер)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО (СотрудникиВКонтейнерах.Сотрудник = Сотрудники.Ссылка)
		|			И (НЕ Сотрудники.ПометкаУдаления)
		|ГДЕ
		|	ОбъектыДоступа.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Организации.Ссылка КАК Ссылка,
		|	ДескрипторыДляОбъектов.Дескриптор КАК Дескриптор
		|ПОМЕСТИТЬ ОрганизацииИДескрипторы
		|ИЗ
		|	Справочник.Организации КАК Организации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СотрудникиРГ КАК СотрудникиРГ
		|		ПО (СотрудникиРГ.Организация = Организации.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|		ПО Организации.Ссылка = ДескрипторыДляОбъектов.Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОрганизацииИДескрипторы.Ссылка КАК Организация,
		|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ИмеющиеПрава
		|ИЗ
		|	ОрганизацииИДескрипторы КАК ОрганизацииИДескрипторы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
		|		ПО ОрганизацииИДескрипторы.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиРГ.Ссылка КАК Объект,
		|	СотрудникиРГ.Организация КАК Организация,
		|	СотрудникиРГ.Участник КАК Участник,
		|	МАКСИМУМ(СотрудникиРГ.Сотрудник) КАК КтоЗапрещен
		|ИЗ
		|	СотрудникиРГ КАК СотрудникиРГ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ИмеющиеПрава КАК ИмеющиеПрава
		|		ПО СотрудникиРГ.Сотрудник = ИмеющиеПрава.Сотрудник
		|			И СотрудникиРГ.Организация = ИмеющиеПрава.Организация
		|ГДЕ
		|	ИмеющиеПрава.Сотрудник ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудникиРГ.Ссылка,
		|	СотрудникиРГ.Организация,
		|	СотрудникиРГ.Участник";
	
	Расхождения.ТекстЗапроса = СтрЗаменить(ШаблонТекстаЗапроса,
		"ДокументыПредприятия", МетаданныеПоТипуОбъектов(ТипОбъектов).Имя);
	
КонецПроцедуры

#КонецОбласти
