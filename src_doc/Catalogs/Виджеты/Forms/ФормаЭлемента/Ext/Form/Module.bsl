#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщиеКартинки = РаботаСВиджетами.ОбщиеКартинки();
	Для Каждого ОбщаяКартинка Из ОбщиеКартинки Цикл
		
		Элементы.Картинка.СписокВыбора.Добавить(ОбщаяКартинка.Имя, ОбщаяКартинка.Представление);
		Элементы.МК_Картинка.СписокВыбора.Добавить(ОбщаяКартинка.Имя, ОбщаяКартинка.Представление);
	
	КонецЦикла;
	Элементы.Картинка.СписокВыбора.СортироватьПоЗначению();
	
	Элементы.МК_Картинка.СписокВыбора.СортироватьПоЗначению();
	
	ОбновитьВидВиджета();
	
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ТолькоПросмотр = Объект.Предопределенный;
	
	ОбщегоНазначенияДокументооборот.ПоказатьНадписьПометкиУдаления(
		ЭтотОбъект,
		ТекущийОбъект.ПометкаУдаления,
		Элементы.ГруппаПометкаУдаления.Имя);
	
	Элементы.ГруппаПредопределенный.Видимость = Объект.Предопределенный;
	
	КлючСохраненияПоложенияОкна =
		Строка(Элементы.ГруппаПредопределенный.Видимость)
		+ Строка(Элементы.ГруппаПометкаУдаления.Видимость);
	
	
	ОбновитьВидВиджета();
	
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	МультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидПриИзменении(Элемент)
	
	ОбновитьВидВиджета();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)
    МультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьВидВиджета()
	
	Элементы.ГруппаБлокиНавигации.Видимость =
		Объект.Вид = Перечисления.ВидыВиджетов.БлокНавигации;
	
	Элементы.ГруппаВложенныеВиджеты.Видимость =
		Объект.Вид = Перечисления.ВидыВиджетов.Альтернативный;
	
	Элементы.ГруппаДиаграмма.Видимость =
		Объект.Вид = Перечисления.ВидыВиджетов.Диаграмма
		Или Объект.Вид = Перечисления.ВидыВиджетов.ДиаграммаСЛегендой;
	
	Элементы.ГруппаДополнительныеКоманды.Видимость = 
		Объект.Вид = Перечисления.ВидыВиджетов.Заголовок
		Или Объект.Вид = Перечисления.ВидыВиджетов.Показатели
		Или Объект.Вид = Перечисления.ВидыВиджетов.Список;
	
	Элементы.ГруппаДополнительныеПоказатели.Видимость =
		Объект.Вид = Перечисления.ВидыВиджетов.Показатели
		Или Объект.Вид = Перечисления.ВидыВиджетов.ДиаграммаСЛегендой;
	
	Элементы.ГруппаКоманды.Видимость =
		Объект.Вид = Перечисления.ВидыВиджетов.Диаграмма
		Или Объект.Вид = Перечисления.ВидыВиджетов.ДиаграммаСЛегендой
		Или Объект.Вид = Перечисления.ВидыВиджетов.Заголовок
		Или Объект.Вид = Перечисления.ВидыВиджетов.Команда;
	
	Элементы.ГруппаКомпоновка.Видимость =
		Объект.Вид = Перечисления.ВидыВиджетов.Комплексный;
	
	Элементы.ГруппаОсновныеПоказатели.Видимость =
		Объект.Вид = Перечисления.ВидыВиджетов.Диаграмма
		Или Объект.Вид = Перечисления.ВидыВиджетов.ДиаграммаСЛегендой
		Или Объект.Вид = Перечисления.ВидыВиджетов.Заголовок
		Или Объект.Вид = Перечисления.ВидыВиджетов.Показатели
		Или Объект.Вид = Перечисления.ВидыВиджетов.Список;
	
	Элементы.ГруппаПоказателиЗаголовка.Видимость =
		Объект.Вид = Перечисления.ВидыВиджетов.Диаграмма
		Или Объект.Вид = Перечисления.ВидыВиджетов.Заголовок
		Или Объект.Вид = Перечисления.ВидыВиджетов.Показатели
		Или Объект.Вид = Перечисления.ВидыВиджетов.Список;
	
	Элементы.ПредставлениеПустого.Видимость =
		Объект.Вид = Перечисления.ВидыВиджетов.Список;
	
	Элементы.ПоложениеЗаголовка.Видимость =
		Объект.Вид = Перечисления.ВидыВиджетов.Команда
		Или Объект.Вид = Перечисления.ВидыВиджетов.Показатели
		Или Объект.Вид = Перечисления.ВидыВиджетов.Заголовок;
	
	Элементы.Картинка.Видимость = 
		Объект.Вид = Перечисления.ВидыВиджетов.Список;
	
КонецПроцедуры

#КонецОбласти