#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидУсловия = Перечисления.ВидыУсловийПравилАвтоматическойКатегоризации.КлючевыеСлова;
	Если ЗначениеЗаполнено(Параметры.ВидУсловия) И ЗначениеЗаполнено(Параметры.Выражение) Тогда
		ВидУсловия = Параметры.ВидУсловия;
		ТекстУсловия = Параметры.Выражение;
	КонецЕсли;
	
	Если ПараметрыСеанса.ЭтоМобильныйКлиент Тогда
		
		ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Верх;
		Элементы.ФормаСохранить.Отображение = ОтображениеКнопки.Картинка;
		Элементы.ФормаСохранить.Картинка = БиблиотекаКартинок.ЗаписатьИЗакрыть;
		Элементы.ВидУсловияМК.Видимость = Истина;
		Элементы.ВидУсловия.Видимость = Ложь;
		
	КонецЕсли;
	
	Элементы.ДекорацияПримерКода.Видимость = (ВидУсловия = Перечисления.ВидыУсловийПравилАвтоматическойКатегоризации.ВычисляемоеВыражение);
	
	КлючСохраненияПоложенияОкна = Строка(ВидУсловия);
	
	Контекст = Новый Соответствие;
	Контекст.Вставить("Результат");
	Контекст.Вставить("Источник");
	РедакторВстроенногоЯзыка.ПриСозданииНаСервере(ЭтотОбъект, "Редактор", Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВидУсловия = ПредопределенноеЗначение("Перечисление.ВидыУсловийПравилАвтоматическойКатегоризации.ВычисляемоеВыражение") Тогда
		
		РедакторВстроенногоЯзыкаКлиент.ПриОткрытии(
			Элементы.РедакторВстроенногоЯзыка,
			Элементы.ТекстУсловия,
			ЭтотОбъект);
		
	Иначе
		
		Элементы.РедакторСтраницы.Видимость = Ложь;
		Элементы.РедакторВстроенногоЯзыка.Видимость = Ложь;
		Элементы.ТекстУсловия.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидУсловияПриИзменении(Элемент)
	
	ТекстУсловия = РедакторВстроенногоЯзыкаКлиент.ПолучитьТекст(Элементы.РедакторВстроенногоЯзыка, ТекстУсловия);
	
	Элементы.ДекорацияПримерКода.Видимость =
		(ВидУсловия = ПредопределенноеЗначение("Перечисление.ВидыУсловийПравилАвтоматическойКатегоризации.ВычисляемоеВыражение"));
	
	Если ВидУсловия = ПредопределенноеЗначение("Перечисление.ВидыУсловийПравилАвтоматическойКатегоризации.ВычисляемоеВыражение") Тогда
		
		РедакторВстроенногоЯзыкаКлиент.ПриОткрытии(
			Элементы.РедакторВстроенногоЯзыка,
			Элементы.ТекстУсловия,
			ЭтотОбъект);
		
	Иначе
		
		Элементы.РедакторСтраницы.Видимость = Ложь;
		Элементы.РедакторВстроенногоЯзыка.Видимость = Ложь;
		Элементы.ТекстУсловия.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедакторВстроенногоЯзыкаДокументСформирован(Элемент)
	
	Если РедакторВстроенногоЯзыкаКлиент.ДокументСформирован(Элемент, ЭтотОбъект, ТекстУсловия) Тогда
		ПодключитьОбработчикОжидания("ПослеЗагрузкиРедактора", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедакторВстроенногоЯзыкаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	НажатаКнопкаEsc = Ложь;
	
	РедакторВстроенногоЯзыкаКлиент.ПриНажатии(
		Элемент,
		ДанныеСобытия,
		СтандартнаяОбработка,
		ЭтотОбъект,
		НажатаКнопкаEsc);
	
	Если НажатаКнопкаEsc Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	ТекстУсловия = РедакторВстроенногоЯзыкаКлиент.ПолучитьТекст(Элементы.РедакторВстроенногоЯзыка, ТекстУсловия);
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеУсловия = Новый Структура("ВидУсловия, ЗначениеУсловия", ВидУсловия, ТекстУсловия);
	Закрыть(ДанныеУсловия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеЗагрузкиРедактора()
	
	РедакторВстроенногоЯзыкаКлиент.ПослеЗагрузкиРедактора(Элементы.РедакторВстроенногоЯзыка, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти
