#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого ПодчиненныйЭлементы Из Элементы.СтраницыСписков.ПодчиненныеЭлементы Цикл
		ПодчиненныйЭлементы.Видимость = Ложь;	
	КонецЦикла;
	
	Для Каждого ДопустимыйТип Из Параметры.ПравилоАвтокатегоризации.ТипыОбъектов Цикл
		ИндексЗначенияПеречисления = Перечисления.ТипыОбъектов.Индекс(ДопустимыйТип.ТипДанных);
		ИмяЗначенияПеречисления = Метаданные.Перечисления.ТипыОбъектов.ЗначенияПеречисления[ИндексЗначенияПеречисления].Имя;
		Элементы["Страница" + ИмяЗначенияПеречисления].Видимость = Истина;
	КонецЦикла;
	
	СписокДокументыПредприятия.Параметры.УстановитьЗначениеПараметра("Правило", Параметры.ПравилоАвтокатегоризации);
	СписокФайлы.Параметры.УстановитьЗначениеПараметра("Правило", Параметры.ПравилоАвтокатегоризации);
	СписокФайлы.Параметры.УстановитьЗначениеПараметра("ПользовательИЕгоСотрудники", 
		СотрудникиВызовСервера.ПользовательИЕгоСотрудники(Пользователи.ТекущийПользователь()));	
		
	СписокМероприятия.Параметры.УстановитьЗначениеПараметра("Правило", Параметры.ПравилоАвтокатегоризации);
	СписокПроектныеЗадачи.Параметры.УстановитьЗначениеПараметра("Правило", Параметры.ПравилоАвтокатегоризации);
	СписокПроекты.Параметры.УстановитьЗначениеПараметра("Правило", Параметры.ПравилоАвтокатегоризации);
		
	РаботаСФайламиВызовСервера.ЗаполнитьУсловноеОформлениеСпискаФайлов(СписокФайлы);
	Делопроизводство.СписокДокументовУсловноеОформлениеПомеченныхНаУдаление(СписокДокументыПредприятия);
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНоменклатуруДел") Тогда 
		Элементы.ИндексСостоянияДела.Видимость = Ложь;
	КонецЕсли;
	
	//подсчет количества объектов в списках
	Запрос = Новый Запрос;
	Для Каждого Страница Из Элементы.СтраницыСписков.ПодчиненныеЭлементы Цикл
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Объекты.Ссылка
		|ИЗ
	 	|	Справочник." + СтрЗаменить(Страница.Имя, "Страница", "") + " КАК Объекты
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КатегорииОбъектов КАК КатегорииОбъектов
		|		ПО КатегорииОбъектов.ОбъектДанных = Объекты.Ссылка
		|		И КатегорииОбъектов.Автор = &Правило
		|	<СоединениеДляФайлов>";
		
		Если Найти(Страница.Имя, "Файлы") > 0 Тогда
			СоединениеДляФайлов = 
				"ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПапкиФайлов КАК СправочникПапкиФайлов
				|ПО Объекты.ВладелецФайла = СправочникПапкиФайлов.Ссылка";
		Иначе
			СоединениеДляФайлов = "";
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "<СоединениеДляФайлов>", СоединениеДляФайлов); 	
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Правило", Параметры.ПравилоАвтокатегоризации); 
		Количество = Запрос.Выполнить().Выбрать().Количество();
		
		Страница.Заголовок = Страница.Заголовок + ?(Количество > 0, " (" + Строка(Количество) + ")", "(0)")
	КонецЦикла;
	
	НастроитьФормуДляМобильногоПриНеобходимости();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокДокументыПредприятия

&НаКлиенте
Процедура СписокДокументыПредприятияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСоСпискамиДокументовКлиент.ОбработкаВыбора(
		ЭтотОбъект, Элементы.СписокДокументыПредприятия, Поле, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДокументыПредприятияОбработкаЗапросаОбновления()
	
	Элементы.СписокДокументыПредприятия.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокФайлы

&НаКлиенте
Процедура СписокФайлыОбработкаЗапросаОбновления()
	
	Элементы.СписокФайлы.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_МобильныйКлиент

&НаСервере
Процедура НастроитьФормуДляМобильногоПриНеобходимости()
	
	Если Не ПараметрыСеанса.ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	// Страница "Документы".
	Элементы.СписокДокументыПредприятияЕстьКатегорииДанных.Видимость = Ложь;
	Элементы.СписокДокументыПредприятияПапка.Видимость = Ложь;
	Элементы.СписокДокументыПредприятияГруппаРегистрационныйНомерИДата.Видимость = Ложь;
	Элементы.СписокДокументыПредприятияВнутреннийНомер.Видимость = Ложь;
	Элементы.СписокДокументыПредприятияВидДокумента.Видимость = Ложь;
	Элементы.СписокДокументыПредприятияОрганизация.Видимость = Ложь;
	Элементы.СписокДокументыПредприятияПодразделение.Видимость = Ложь;
	Элементы.СписокДокументыПредприятияНоменклатураДел.Видимость = Ложь;
	Элементы.СписокДокументыПредприятияУтвердил.Видимость = Ложь;
	Элементы.СписокДокументыПредприятияДатаСоздания.Видимость = Ложь;
	
	Элементы.СписокДокументыПредприятияГруппаКолонкиМК.Видимость = Истина;
	Элементы.Переместить(
		Элементы.СписокДокументыПредприятияГруппаФайлыЗадачи, Элементы.СписокДокументыПредприятияГруппаКартинкиМК);
	Элементы.Переместить(
		Элементы.СписокДокументыПредприятияГруппаЭПКатегории, Элементы.СписокДокументыПредприятияГруппаКартинкиМК);
	Элементы.Переместить(
		Элементы.СписокДокументыПредприятияЗаголовок, Элементы.СписокДокументыПредприятияГруппаЗаголовокПодготовилМК);
	Элементы.Переместить(
		Элементы.СписокДокументыПредприятияПодготовил, Элементы.СписокДокументыПредприятияГруппаЗаголовокПодготовилМК);
	Элементы.Переместить(
		Элементы.СписокДокументыПредприятияРегистрационныйНомерИДата, Элементы.СписокДокументыПредприятияГруппаРегНомерДатаМК);
		
	Элементы.СписокДокументыПредприятияПодготовил.ЦветТекста = WebЦвета.ТемноСерый;
	Элементы.СписокДокументыПредприятия.Шапка = Ложь;
	
	Элементы.СписокДокументыПредприятияЗаголовок.Высота = 3;
	Элементы.СписокДокументыПредприятияРегистрационныйНомерИДата.Высота = 2;
	Элементы.СписокДокументыПредприятияРегистрационныйНомерИДата.Ширина = 7;
	
	// Страница "Файлы".
	Элементы.СписокФайлыПодписанЭП.Видимость = Ложь;
	Элементы.СписокФайлыЕстьКатегорииДанных.Видимость = Ложь;
	Элементы.СписокФайлыОписание.Видимость = Ложь;
	Элементы.СписокФайлыРедактирует.Видимость = Ложь;
	Элементы.СписокФайлыТекущаяВерсияНомерВерсии.Видимость = Ложь;
	Элементы.СписокФайлыТекущаяВерсияДатаСоздания.Видимость = Ложь;
	Элементы.СписокФайлыТекущаяВерсияАвтор.Видимость = Ложь;
	Элементы.СписокФайлыТекущаяВерсияРазмер.Видимость = Ложь;
	Элементы.СписокФайлыВладелецФайла.Видимость = Ложь;
	Элементы.СписокФайлыХранитьВерсии.Видимость = Ложь;
	Элементы.СписокФайлыРасширение.Видимость = Ложь;
	
	Элементы.СписокФайлыГруппаКолонкиМК.Видимость = Истина;
	Элементы.Переместить(
		Элементы.СписокФайлыНаименование, Элементы.СписокФайлыГруппаНаименованиеАвторМК);
	Элементы.Переместить(
		Элементы.СписокФайлыИндексКартинки, Элементы.СписокФайлыГруппаКартинкаНаименованияМК);
	Элементы.Переместить(
		Элементы.СписокФайлыАвтор, Элементы.СписокФайлыГруппаНаименованиеАвторМК);

	Элементы.СписокФайлыНаименованиеИКартинка.Группировка = ГруппировкаКолонок.Горизонтальная;
	Элементы.СписокФайлыАвтор.ЦветТекста = WebЦвета.ТемноСерый;
	Элементы.СписокФайлы.Шапка = Ложь;
	
КонецПроцедуры
	
#КонецОбласти
