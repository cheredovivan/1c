#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СсылкаНаКатегорию") Тогда
		Если НЕ Параметры.СсылкаНаКатегорию.Пустая() Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
				"Категория",
				Параметры.СсылкаНаКатегорию,
				ВидСравненияКомпоновкиДанных.Равно);
			Категория = Параметры.СсылкаНаКатегорию;
			Элементы.Список.ПодчиненныеЭлементы.Категория.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	НастроитьФормуДляМобильногоПриНеобходимости();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если НЕ Копирование Тогда
		ПараметрыФормы = Новый Структура("Категория", Категория);
		ОткрытьФорму("Справочник.ПравилаАвтоматическойКатегоризацииДанных.ФормаОбъекта", ПараметрыФормы, Элемент); 
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры                                       

&НаКлиенте
Процедура СписокОбработкаЗапросаОбновления()
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьПравило(Команда)
	
	Если НЕ Элементы.Список.ТекущиеДанные = Неопределено Тогда
		ПараметрыФормы = Новый Структура("ПравилоАК", Элементы.Список.ТекущиеДанные.Ссылка);
	 	ОткрытьФорму("Справочник.ПравилаАвтоматическойКатегоризацииДанных.Форма.ФормаПроверкаПравила", ПараметрыФормы,,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_МобильныйКлиент

&НаСервере
Процедура НастроитьФормуДляМобильногоПриНеобходимости()
	
	Если Не ПараметрыСеанса.ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Список.Шапка = Ложь;
	Элементы.Список.ПоложениеСтрокиПоиска = ПоложениеСтрокиПоиска.ЗаголовокФормы;
	
	Элементы.ГруппаКолонокМК.Видимость = Истина;
	Элементы.Переместить(Элементы.Используется, Элементы.ГруппаИспользуетсяМК);
	Элементы.Переместить(Элементы.Ссылка, Элементы.ГруппаНаименованиеАвторМК);
	Элементы.Переместить(Элементы.Автор, Элементы.ГруппаНаименованиеАвторМК);
	Элементы.Переместить(Элементы.ДатаСоздания, Элементы.ГруппаДатаМК);
	
	Элементы.СписокДопустимыхТипов.Видимость = Ложь;
	Элементы.Категория.Видимость = Ложь;
	
	Элементы.Автор.ЦветТекста = WebЦвета.ТемноСерый;
	Элементы.ДатаСоздания.Ширина = 7;
	
	Элементы.Переместить(Элементы.ФормаПроверитьПравило, Элементы.Список.КонтекстноеМеню);

КонецПроцедуры

#КонецОбласти
