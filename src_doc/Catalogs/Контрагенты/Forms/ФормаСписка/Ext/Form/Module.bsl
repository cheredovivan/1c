#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Параметры.УстановитьЗначениеПараметра("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
	Список.Параметры.УстановитьЗначениеПараметра("ЮрЛицо", НСтр("ru = 'Юр. лицо'"));
	Список.Параметры.УстановитьЗначениеПараметра("ФизЛицо", НСтр("ru = 'Физ. лицо'"));
	Список.Параметры.УстановитьЗначениеПараметра("ИП", НСтр("ru = 'ИП'"));
	Список.Параметры.УстановитьЗначениеПараметра("НеРезидент", НСтр("ru = 'Нерезидент'"));
	
	// Контроль
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьКонтрольОбъектов") Тогда 
		Элементы.СостояниеКонтроля.Видимость = Ложь;
	КонецЕсли;
	
	// Кэшируемые значения
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	ЕстьДоступныеУчетныеЗаписиДляОтправки = 
		РаботаСПочтовымиСообщениямиВызовСервера.ЕстьДоступныеУчетныеЗаписиДляОтправки();
	
	Делопроизводство.СписокДокументовУсловноеОформлениеПомеченныхНаУдаление(Список);
	НастройкиФормы = ОбщегоНазначения.ХранилищеСистемныхНастроекЗагрузить(ИмяФормы + "/ТекущиеДанные", "");
	Если НастройкиФормы = Неопределено Или НастройкиФормы.Получить("ПоказыватьУдаленные") = Неопределено Тогда
		Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
		РаботаСоСпискамиКонтрагентов.УстановитьОтборПомеченныхНаУдаление(ЭтотОбъект);
		Если ЗначениеЗаполнено(ТекущийКонтрагент) Тогда 
			ОбновитьОбзорКонтрагента();
		КонецЕсли;
	КонецЕсли;
	
	// Учет трудозатрат
	УчетВремени.ПроинициализироватьПараметрыУчетаВремени(
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ОпцияИспользоватьУчетВремени,
		Неопределено,
		ВидыРабот,
		СпособУказанияВремени,
		Команды.ПереключитьХронометраж,
		Элементы.ПереключитьХронометраж,
		Элементы.УказатьТрудозатраты);
	
	РаботаСоСпискамиКонтрагентов.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаКоманднаяПанель;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	НастроитьЭлементыФормыДляМобильногоУстройства();
	
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект);
	
	КонтрагентыЛокализация.ПриСозданииНаСервереФормаСписка(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

#Если МобильныйКлиент Тогда
	// Активизируем строку сразу
	СписокПриАктивизацииСтроки(Неопределено);
#КонецЕсли
	
	КонтрагентыКлиентЛокализация.ПриОткрытии_ФормаСписка(ЭтотОбъект, Отказ);
	
	РаботаСоСпискамиКонтрагентовКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ГруппаКонтрагентов" Тогда
		РаботаСоСпискамиКонтрагентовКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
		РаботаСоСпискамиКонтрагентовКлиент.ЗаполнитьГруппы(ЭтотОбъект);
	КонецЕсли;
	
	Если ИмяСобытия = "ЗаписьКонтроля" Тогда
		Если ЗначениеЗаполнено(Параметр.Предмет)
			И ТипЗнч(Параметр.Предмет) = Тип("СправочникСсылка.Контрагенты") Тогда 
			ОповеститьОбИзменении(Параметр.Предмет);
		КонецЕсли;	
	КонецЕсли;
	
	Если (ИмяСобытия = "СозданоНовоеКонтактноеЛицо" Или ИмяСобытия = "ИзменилосьКонтактноеЛицо"
		Или ИмяСобытия = "ИзменилсяКонтрагент") 
		И Параметр.Владелец = ТекущийКонтрагент Тогда
		ОбновитьОбзорКонтрагента();
	КонецЕсли;
	
	КонтрагентыКлиентЛокализация.ОбработкаОповещения_ФормаСписка(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	РаботаСоСпискамиКонтрагентов.ПриЗагрузкеДанныхИзНастроекНаСервере(ЭтотОбъект, Настройки);
	
	Если ЗначениеЗаполнено(ТекущийКонтрагент) Тогда
		ОбновитьОбзорКонтрагента();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()
	
	НастроитьЭлементыФормыДляМобильногоУстройства();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда 
		Возврат;
	КонецЕсли;	
	
	Если РаботаСИнтегрированнымиСистемамиКлиент.ЭтоНавигационнаяСсылка(ДанныеСобытия.Href) Тогда
		РаботаСИнтегрированнымиСистемамиКлиент.ОткрытьНавигационнуюСсылку(ДанныеСобытия.Href);
		Возврат;
	КонецЕсли;
	
	Если Лев(ДанныеСобытия.Href, 6) <> "v8doc:" Тогда 
		Возврат;
	КонецЕсли;	
	НавигационнаяСсылкаПоля = Сред(ДанныеСобытия.Href, 7);
	
	ПараметрыНажатия = Новый Структура;
	ПараметрыНажатия.Вставить("ТекущийПользователь", ТекущийПользователь);
	ПараметрыНажатия.Вставить("Элемент", Элемент);
	ПараметрыНажатия.Вставить("ЭтаФорма", ЭтаФорма);
	ПараметрыНажатия.Вставить("Объект", ТекущийКонтрагент);
	ПараметрыНажатия.Вставить("ЭтоСписок", Истина);
	
	ДелопроизводствоКлиент.ОбработатьНажатиеНаПолеОбзор(ТекущийКонтрагент,
		НавигационнаяСсылкаПоля, ПараметрыНажатия);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Отказ = Истина;
	КлючеваяОперация = РаботаСКонтрагентамиДокументооборотКлиентСервер.КлючеваяОперацияСозданиеКонтрагента();
	ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
	
	ПараметрыОткрытия = Новый Структура;
	Если ЗначениеЗаполнено(ТекущаяГруппа) И ТипЗнч(ТекущаяГруппа) = Тип("СправочникСсылка.Контрагенты") Тогда
		ПараметрыОткрытия.Вставить("Группа", ТекущаяГруппа);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыОткрытия, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КлючеваяОперация = РаботаСКонтрагентамиДокументооборотКлиентСервер.КлючеваяОперацияОткрытиеКонтрагента();
	ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Ключ", ТекущиеДанные.Ссылка);
	
	ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыОткрытия, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("АктивизацияСтрокиСписка", 0.2, Истина);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// Локализация
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиКлиент.ПриАктивизацииСтроки_ФормаСписка(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	// Локализация Конец
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	// Локализация
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентами.ПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	// Локализация Конец
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	КлючеваяОперация = РаботаСКонтрагентамиДокументооборотКлиентСервер.КлючеваяОперацияОткрытиеКонтрагента();
	ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
	
	ПараметрыОткрытия = Новый Структура("Ключ", ВыбраннаяСтрока);
	ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыОткрытия);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыГруппы

&НаКлиенте
Процедура ГруппыПриАктивизацииСтроки(Элемент)
	
	РаботаСоСпискамиКонтрагентовКлиент.ГруппыПриАктивизацииСтроки(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	РаботаСоСпискамиКонтрагентовКлиент.ГруппыНачалоПеретаскивания(ЭтотОбъект, Элемент, ПараметрыПеретаскивания,
		Выполнение);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	РаботаСоСпискамиКонтрагентовКлиент.ГруппыПроверкаПеретаскивания(ЭтотОбъект, Элемент, ПараметрыПеретаскивания,
		СтандартнаяОбработка, Строка, Поле);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	РаботаСоСпискамиКонтрагентовКлиент.ГруппыПеретаскивание(ЭтотОбъект, Элемент, ПараметрыПеретаскивания,
		СтандартнаяОбработка, Строка, Поле);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	РаботаСоСпискамиКонтрагентовКлиент.ГруппыПередНачаломДобавления(ЭтотОбъект, Элемент, Отказ, Копирование, Родитель,
		ЭтоГруппа, Параметр);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПередУдалением(Элемент, Отказ)
	
	РаботаСоСпискамиКонтрагентовКлиент.ГруппыПередУдалением(ЭтотОбъект, Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПередНачаломИзменения(Элемент, Отказ)
	
	РаботаСоСпискамиКонтрагентовКлиент.ГруппыПередНачаломИзменения(ЭтотОбъект, Элемент, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	РаботаСоСпискамиКонтрагентовКлиент.ПоказыватьУдаленные(ЭтотОбъект, Команда);
	ПоказыватьУдаленныеСервер();
	
КонецПроцедуры

// Учет времени

&НаКлиенте
Процедура ПереключитьХронометраж(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;

	ПараметрыОповещения = Неопределено;
	НуженДиалог = УчетВремениКлиент.НуженДиалогДляХронометража(ВключенХронометраж, 
		ДатаНачалаХронометража, ВидыРабот);
	
	Если НуженДиалог = Ложь Тогда
		
		ПереключитьХронометражСервер(ПараметрыОповещения);
		УчетВремениКлиент.ПоказатьОповещение(ПараметрыОповещения, ВключенХронометраж, ТекущиеДанные.Ссылка);
	
	Иначе
		ДлительностьРаботы = УчетВремениКлиент.ПолучитьДлительностьРаботы(ДатаНачалаХронометража);
		
		ОписаниеРаботы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Работа с контрагентом ""%1""'"),
			ТекущиеДанные.Наименование);
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ДатаОтчета", ТекущаяДата());
		ПараметрыФормы.Вставить("ВидыРабот", ВидыРабот);
		ПараметрыФормы.Вставить("ОписаниеРаботы", ОписаниеРаботы);
		ПараметрыФормы.Вставить("ДлительностьРаботы", ДлительностьРаботы);
		ПараметрыФормы.Вставить("НачалоРаботы", ДатаНачалаХронометража);
		ПараметрыФормы.Вставить("Объект", ТекущиеДанные.Ссылка);
		ПараметрыФормы.Вставить("СпособУказанияВремени", СпособУказанияВремени);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПереключитьХронометражПродолжение",
			ЭтотОбъект,
			Новый Структура("Объект", ТекущиеДанные.Ссылка));
		
		ОткрытьФорму("РегистрСведений.ФактическиеТрудозатраты.Форма.ФормаДобавленияРаботы", ПараметрыФормы,,,,,
			ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьХронометражПродолжение(Результат, Параметры) Экспорт 
	
	Если Результат <> Неопределено Тогда
		ДобавитьВОтчетИОбновитьФорму(Результат, ПараметрыОповещения);
		УчетВремениКлиент.ПоказатьОповещение(ПараметрыОповещения, ВключенХронометраж, Параметры.Объект);
	Иначе
		ОтключитьХронометражСервер();
	КонецЕсли;  

КонецПроцедуры

&НаКлиенте
Процедура УказатьТрудозатраты(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ДатаОтчета = ТекущаяДата();
	
	УчетВремениКлиент.ДобавитьВОтчетКлиент(
		ДатаОтчета,
		ВключенХронометраж, 
		ДатаНачалаХронометража, 
		ДатаКонцаХронометража, 
		ВидыРабот, 
		ТекущиеДанные.Ссылка,
		СпособУказанияВремени,
		ЭтаФорма.Элементы.ПереключитьХронометраж,
		Ложь,
		ЭтаФорма); // Выполнена
	
КонецПроцедуры

&НаКлиенте
Процедура Трудозатраты(Команда)
	
	ПараметрыФормы = Новый Структура("Источник", ТекущийКонтрагент);
	ОткрытьФорму("РегистрСведений.ФактическиеТрудозатраты.Форма.ФормаСпискаИсточника", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчеты(Команда)
		
	Раздел = ПредопределенноеЗначение("Перечисление.РазделыОтчетов.КонтрагентыСписок");
	
	ЗаголовокФормы = НСтр("ru = 'Отчеты по контрагентам'");
	
	ПараметрыФормы = Новый Структура("Раздел, ЗаголовокФормы, НеОтображатьИерархию, ", 
										Раздел, ЗаголовокФормы, Истина, );
	
	ОткрытьФорму(
		"Обработка.ВсеОтчеты.Форма.ФормаПоКатегориям",
		ПараметрыФормы,
		ЭтаФорма, 
		"КонтрагентыСписок");

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЭлемент(Команда)

	ПерейтиПоНавигационнойСсылке(ПолучитьНавигационнуюСсылку(ТекущийКонтрагент));

КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьПодчиненные(Команда)
	
	РаботаСоСпискамиКонтрагентовКлиент.ПоказыватьПодчиненные(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура РежимПросмотраСписком(Команда)
	
	РаботаСоСпискамиКонтрагентовКлиент.РежимПросмотраСписком(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура РежимПросмотраПоГруппам(Команда)
	
	РаботаСоСпискамиКонтрагентовКлиент.РежимПросмотраПоГруппам(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьГруппы(Команда)
	
	РаботаСоСпискамиКонтрагентовКлиент.СвернутьГруппы(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьГруппы(Команда)
	
	РаботаСоСпискамиКонтрагентовКлиент.РазвернутьГруппы(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьГруппы(Команда)
	
	РаботаСоСпискамиКонтрагентовКлиент.ОбновитьГруппы(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура АктивизацияСтрокиСписка()
	
	Если ТекущийКонтрагент <> Элементы.Список.ТекущаяСтрока Тогда
		ТекущийКонтрагент = Элементы.Список.ТекущаяСтрока;
		ОбновитьПараметрыУчетаВремениВФорме();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОбзорКонтрагента()
	
	ПредставлениеHTML = ОбзорКонтрагента.ПолучитьОбзорКонтрагента(
		ТекущийКонтрагент, ПоказыватьУдаленные);
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		МК_КлиентСервер.АдаптироватьHtmlПодЭкранМобильногоПриНеобходимости(ПредставлениеHTML);
	КонецЕсли;
	
КонецПроцедуры

// Локализация

// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКомандыЭДО()
	ОбменСКонтрагентамиКлиент.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

// Локализация Конец

&НаСервере
Процедура ПоказыватьУдаленныеСервер()
	
	РаботаСоСпискамиКонтрагентов.УстановитьОтборПомеченныхНаУдаление(ЭтотОбъект);
	Если ЗначениеЗаполнено(ТекущийКонтрагент) Тогда
		ОбновитьОбзорКонтрагента();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_Хронометраж

&НаСервере
Процедура ПереключитьХронометражСервер(ПараметрыОповещения) Экспорт
	
	Если ТекущийКонтрагент = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	УчетВремени.ПереключитьХронометражСервер(
		ПараметрыОповещения,
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ТекущийКонтрагент,
		ВидыРабот,
		ЭтаФорма.Команды.ПереключитьХронометраж,
		ЭтаФорма.Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВОтчетИОбновитьФорму(ПараметрыОтчета, ПараметрыОповещения) Экспорт
	
	УчетВремени.ДобавитьВОтчетИОбновитьФорму(
	    ПараметрыОтчета, 
		ПараметрыОповещения,
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ЭтаФорма.Команды.ПереключитьХронометраж,
		ЭтаФорма.Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьХронометражСервер() Экспорт
	
	Если ТекущийКонтрагент = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	УчетВремени.ОтключитьХронометражСервер(
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ТекущийКонтрагент,
		ЭтаФорма.Команды.ПереключитьХронометраж,
		ЭтаФорма.Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПараметрыУчетаВремениВФорме()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено 
		Или ТипЗнч(Элементы.Список.ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Элементы.ПереключитьХронометраж.Доступность = Ложь;
		Элементы.УказатьТрудозатраты.Доступность = Ложь;
		ПредставлениеHTML = "<html><body></body></html>";
		Возврат;
	КонецЕсли;
	
	ТекущийКонтрагент = ТекущиеДанные.Ссылка;
	
	ПараметрыУчетаВремени = ПолучитьПараметрыУчетаВремени(ТекущиеДанные.Ссылка);
	
	ДатаНачалаХронометража = ПараметрыУчетаВремени.ДатаНачалаХронометража;
	ДатаКонцаХронометража = ПараметрыУчетаВремени.ДатаКонцаХронометража;
	ВключенХронометраж = ПараметрыУчетаВремени.ВключенХронометраж;
	ОпцияИспользоватьУчетВремени = ПараметрыУчетаВремени.ОпцияИспользоватьУчетВремени;
	ВидыРабот = ПараметрыУчетаВремени.ВидыРабот;
	СпособУказанияВремени = ПараметрыУчетаВремени.СпособУказанияВремени;
	
	Для Каждого СвойствоЭлемента Из ПараметрыУчетаВремени.ПереключитьХронометраж Цикл
		Элементы.ПереключитьХронометраж[СвойствоЭлемента.Ключ] = СвойствоЭлемента.Значение;
	КонецЦикла;
	
	Для Каждого СвойствоЭлемента Из ПараметрыУчетаВремени.УказатьТрудозатраты Цикл
		Элементы.УказатьТрудозатраты[СвойствоЭлемента.Ключ] = СвойствоЭлемента.Значение;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыУчетаВремени(Документ)
	
	Результат = Новый Структура;
	
	ДатаНачалаХронометража = Неопределено;
	ДатаКонцаХронометража = Неопределено;
	ВключенХронометраж = Неопределено;
	ОпцияИспользоватьУчетВремени = Неопределено;
	ВидыРабот = Неопределено;
	СпособУказанияВремени = Неопределено;
	
	ПереключитьХронометражНеМеняяПодсказку = Новый Структура("Имя, Подсказка");
	
	ПереключитьХронометраж = Новый Структура("Доступность, Пометка, Видимость");
	ПереключитьХронометраж.Доступность = Истина;
	
	УказатьТрудозатраты = Новый Структура("Доступность");
	УказатьТрудозатраты.Доступность = Истина;
	
	УчетВремени.ПроинициализироватьПараметрыУчетаВремени(
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ОпцияИспользоватьУчетВремени,
		Документ,
		ВидыРабот,
		СпособУказанияВремени,
		ПереключитьХронометражНеМеняяПодсказку,
		ПереключитьХронометраж,
		УказатьТрудозатраты);
		
	Результат.Вставить("ПереключитьХронометраж", Новый Соответствие);
	Результат.Вставить("УказатьТрудозатраты", Новый Соответствие);
	
	Результат.ПереключитьХронометраж.Вставить(
		"Доступность",
		ПереключитьХронометраж.Доступность);
	Результат.ПереключитьХронометраж.Вставить(
		"Пометка",
		ПереключитьХронометраж.Пометка);
	Результат.УказатьТрудозатраты.Вставить(
		"Доступность",
		УказатьТрудозатраты.Доступность);
	
	Результат.Вставить("ДатаНачалаХронометража", ДатаНачалаХронометража);
	Результат.Вставить("ДатаКонцаХронометража", ДатаКонцаХронометража);
	Результат.Вставить("ВключенХронометраж", ВключенХронометраж);
	Результат.Вставить("ОпцияИспользоватьУчетВремени", ОпцияИспользоватьУчетВремени);
	Результат.Вставить("ВидыРабот", ВидыРабот);
	Результат.Вставить("СпособУказанияВремени", СпособУказанияВремени);
	
	ОбновитьОбзорКонтрагента();
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура НастроитьЭлементыФормыДляМобильногоУстройства()
	
	ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
	
	Если Не ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоМобильныйСБольшимЭкраном = МК_КлиентСервер.ЭтоМобильныйСБольшимЭкраном();
	
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	
	Если ЭтоМобильныйСБольшимЭкраном Тогда
		//Настраиваем саму форму
		Элементы.ГруппаСписокИПревью.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	КонецЕсли;
	
	//Отображение команд на форме
	Элементы.ГруппаКоманды.Видимость = Ложь;
	Элементы.МК_ГруппаСоздатьНаОсновании.Видимость = Истина;
	Элементы.МК_ОткрытьОтчеты.Видимость = Истина;
	Элементы.МК_ОтправитьОбъекты.Видимость = Истина;
	
	//Настраиваем список
	Элементы.Список.Шапка = Ложь;
	Элементы.Список.ВертикальныеЛинии = Ложь;
	
	//Настраиваем поля списка
	Элементы.РегистрационныйНомер.Видимость = Ложь;
	Элементы.СостояниеКонтроля.Видимость = Ложь;
	Элементы.Код.Видимость = Ложь;
	Элементы.Ссылка.Видимость = Ложь;
	Элементы.КПП.Видимость = Ложь;
	Элементы.МК_СписокКолонка.Группировка = ГруппировкаКолонок.Вертикальная;	
	
	Элементы.Переместить(Элементы.СписокГруппаЮрФизЛицоКомментарий, Элементы.МК_СписокКолонкаВтораяСтрока);
	Элементы.Переместить(Элементы.ИНН, Элементы.МК_СписокКолонкаВтораяСтрока);
	Элементы.Переместить(Элементы.Комментарий, Элементы.МК_СписокКолонкаТретьяСтрока);
	
	Элементы.СписокКонтекстноеМенюОткрытьЭлемент.Видимость = ЭтоМобильныйСБольшимЭкраном;
	Элементы.Превью.Видимость = ЭтоМобильныйСБольшимЭкраном;
	
	Элементы.Список.ИспользованиеТекущейСтроки = ?(ЭтоМобильныйСБольшимЭкраном,
		ИспользованиеТекущейСтрокиТаблицы.ОтображениеВыделения,
		ИспользованиеТекущейСтрокиТаблицы.Выбор);
	
	КонтрагентыЛокализация.НастроитьЭлементыФормыДляМобильногоУстройстваФормаСписка(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти
