
#Область ОписаниеПеременных

// СтандартныеПодсистемы.РаботаСКонтрагентами
&НаКлиенте
Перем ОтключитьАвтоЗаполнениеРеквизитов;

&НаКлиенте
Перем ФормаДлительнойОперации Экспорт;
// Конец СтандартныеПодсистемы.РаботаСКонтрагентами

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПротоколированиеРаботыСотрудников.ЗаписатьОткрытие(Объект.Ссылка);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Объект);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОбработкаНавигационнойСсылки", Истина);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаКонтактнаяИнформация");
	ДополнительныеПараметры.Вставить("ПоложениеЗаголовкаКИ", ПоложениеЗаголовкаЭлементаФормы.Лево);
	Если Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		Если ЗначениеЗаполнено(Объект.ФизЛицо) Тогда 
			Объект.КонтактнаяИнформация.Загрузить(Объект.ФизЛицо.КонтактнаяИнформация.Выгрузить());
		КонецЕсли;
		УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(ЭтотОбъект, Объект.ФизЛицо, ДополнительныеПараметры);
	Иначе
		УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(ЭтотОбъект, Объект, ДополнительныеПараметры);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация	
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ФормаСтандартныеКоманды;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// Локализация
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ЭтоНовыйЭлемент = Параметры.Ключ.Пустая();
	Элементы.ГруппаЗаполнениеПоДаннымЕдиныхГосРеестров.Видимость = ЭтоНовыйЭлемент;
	Элементы.ЗаполнитьПоИНН.Видимость = НЕ ЭтоНовыйЭлемент;
	Элементы.ЗаполнитьПоИНН1.Видимость = НЕ ЭтоНовыйЭлемент;
	Если ЭтоНовыйЭлемент Тогда
		ЗаполнитьРеквизитыПоВходящимПараметрам(Параметры);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
	// Локализация Конец
	
	ФизическоеЛицо = Перечисления.ЮрФизЛицо.ФизЛицо;
	СтароеЮрФизЛицо = Объект.ЮрФизЛицо;
	
	Если Параметры.Свойство("Группа") Тогда
		Объект.Родитель = Параметры.Группа;
	КонецЕсли;
	
	Если Параметры.Свойство("Представление") Тогда
		Объект.Наименование = Параметры.Представление;
	КонецЕсли;	
	
	Если Параметры.Свойство("Адрес") Тогда
		
		ДобавленАдрес = Истина;
		
		// EmailАдресата
		Если ЗначениеЗаполнено(Параметры.Адрес) И Найти(Параметры.Адрес, "@") <> 0 Тогда
			НовСтр = Объект.КонтактнаяИнформация.Добавить();
			НовСтр.ЗначенияПолей = Параметры.Адрес;
			НовСтр.АдресЭП = Параметры.Адрес;
			НовСтр.Представление = Параметры.Адрес;
			НовСтр.Вид = Справочники.ВидыКонтактнойИнформации.EmailКонтрагента;
			НовСтр.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
		КонецЕсли;
		
	КонецЕсли;	
	
	Если Параметры.Свойство("ЛичныйАдресат") Тогда
		
		ЛичныйАдресат = Параметры.ЛичныйАдресат;
		
		Объект.Наименование = ЛичныйАдресат.Наименование;
		Объект.Ответственный = ЛичныйАдресат.Сотрудник;
		Объект.Комментарий = ЛичныйАдресат.Комментарий;
		
		НеиспользованныеПоля = "";
		
		Для Каждого КонтактИнфоАдресата Из ЛичныйАдресат.КонтактнаяИнформация Цикл
			
			ЕстьПолеТакогоВида = Ложь;
			Для Каждого КонтактИнфоКонтрагента Из Объект.КонтактнаяИнформация Цикл
				
				Если Строка(КонтактИнфоКонтрагента.Вид) = Строка(КонтактИнфоАдресата.Вид) Тогда
					ЕстьПолеТакогоВида = Истина;
					Прервать;
				КонецЕсли;	
				
			КонецЦикла;	
			
			Если ЕстьПолеТакогоВида = Ложь Тогда
				
				Если КонтактИнфоАдресата.Вид = Справочники.ВидыКонтактнойИнформации.EmailАдресата Тогда
					
					НовСтр = Объект.КонтактнаяИнформация.Добавить();
					НовСтр.ЗначенияПолей = КонтактИнфоАдресата.ЗначенияПолей;
					НовСтр.АдресЭП = КонтактИнфоАдресата.АдресЭП;
					НовСтр.Представление = КонтактИнфоАдресата.Представление;
					НовСтр.Вид = Справочники.ВидыКонтактнойИнформации.EmailКонтрагента;
					НовСтр.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
					
				КонецЕсли;	
				
				Если КонтактИнфоАдресата.Вид = Справочники.ВидыКонтактнойИнформации.РабочийТелефонАдресата Тогда
					
					НовСтр = Объект.КонтактнаяИнформация.Добавить();
					НовСтр.ЗначенияПолей = КонтактИнфоАдресата.ЗначенияПолей;
					НовСтр.Представление = КонтактИнфоАдресата.Представление;
					НовСтр.НомерТелефона = КонтактИнфоАдресата.НомерТелефона;
					НовСтр.НомерТелефонаБезКодов = КонтактИнфоАдресата.НомерТелефонаБезКодов;
					НовСтр.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;
					НовСтр.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
					
				КонецЕсли;	
				
				Если КонтактИнфоАдресата.Вид = Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресАдресата Тогда
					
					НовСтр = Объект.КонтактнаяИнформация.Добавить();
					НовСтр.ЗначенияПолей = КонтактИнфоАдресата.ЗначенияПолей;
					НовСтр.Представление = КонтактИнфоАдресата.Представление;
					НовСтр.Страна = КонтактИнфоАдресата.Страна;
					НовСтр.Регион = КонтактИнфоАдресата.Регион;
					НовСтр.Город = КонтактИнфоАдресата.Город;
					НовСтр.Вид = Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресКонтрагента;
					НовСтр.Тип = Перечисления.ТипыКонтактнойИнформации.Адрес;
					
				КонецЕсли;	
				
			Иначе	
				
				НеиспользованныеПоля = НеиспользованныеПоля + " "
					+ Строка(КонтактИнфоАдресата.Вид) + ": " + КонтактИнфоАдресата.ЗначенияПолей + 
					" " + КонтактИнфоАдресата.Представление;
				
			КонецЕсли;	
			
		КонецЦикла;	
		
		Объект.Комментарий = Объект.Комментарий + " " + НеиспользованныеПоля;
		
	КонецЕсли;	
	
	Если Не Объект.Ссылка.Пустая() И Объект.ЮрФизЛицо = ФизическоеЛицо Тогда 
		Элементы.ФизЛицо1.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если Параметры.Свойство("ОткрытьНаЗакладкеКонтактнойИнформации") Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаКонтактнаяИнформация;
	КонецЕсли;	
	
	РаботаСПоследнимиОбъектами.ЗаписатьОбращениеКОбъекту(Объект.Ссылка);
	
	НастройкиФормы = ОбщегоНазначения.ХранилищеСистемныхНастроекЗагрузить(ИмяФормы + "/ТекущиеДанные", "");
	Если НастройкиФормы = Неопределено 
		Или НастройкиФормы.Получить("ПоказыватьНеДействительныеКонтактныеЛица") = Неопределено Тогда
		ЗаполнитьСписокКонтактныхЛиц();
	КонецЕсли;
	
	Если Справочники.БанковскиеСчета.ИмеетБанковскийСчет(Объект.Ссылка) Тогда 
		Элементы.БанковскийСчетЗадан.Видимость = Истина;
		Элементы.БанковскийСчетНеЗадан.Видимость = Ложь;
	Иначе 
		Элементы.БанковскийСчетЗадан.Видимость = Ложь;
		Элементы.БанковскийСчетНеЗадан.Видимость = Истина;
	КонецЕсли;
	
	Нумерация.ПоказатьИндексНумерации(ЭтотОбъект);
	
	Если Параметры.Свойство("ОткрытьЗакладкуКЛ") Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаКонтактныеЛица;
	КонецЕсли;
	
	// Учет трудозатрат
	УчетВремени.ПроинициализироватьПараметрыУчетаВремени(
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ОпцияИспользоватьУчетВремени,
		Объект.Ссылка,
		ВидыРабот,
		СпособУказанияВремени,
		Команды.ПереключитьХронометраж,
		Элементы.ПереключитьХронометраж,
		Элементы.УказатьТрудозатраты);
		
	Если Параметры.Ключ.Пустая() Тогда
		ФормироватьНаименованиеПолноеАвтоматически = ПустаяСтрока(Объект.НаименованиеПолное)
			Или (Объект.НаименованиеПолное = Объект.Наименование);
	КонецЕсли;
		
// Локализация
	Если РольДоступна("ПросмотрОтчетаДосьеКонтрагента") Или РольДоступна("ПолныеПрава") Тогда 
		Элементы.ДосьеКонтрагента.Видимость = Истина;
	Иначе 
		Элементы.ДосьеКонтрагента.Видимость = Ложь;
	КонецЕсли;
// Локализация Конец
	
	ОбновитьЗадачиПоПриложению();
	
	НастроитьЭлементыФормыПодМобильныйКлиентПриНеобходимости();
	
	Если Не ПравоДоступа("Изменение", Метаданные.Справочники.КонтактныеЛица) Тогда
		Элементы.КонтактныеЛица.ИзменятьПорядокСтрок = Ложь;
		Элементы.КонтактныеЛица.ИзменятьСоставСтрок = Ложь;
	КонецЕсли;
	
	Юрлица.УстановитьПодсказкиПроИсторию(ЭтотОбъект);
	
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	
	Если Не МультиязычностьДокументооборот.ИспользуетсяТолькоОдинЯзык() Тогда 
		МассивРеквизитов = Юрлица.ДобавитьМультиязычныеРеквизитыНаФорму("ИсторияНаименований", 
			"Наименование",  "НаименованиеПолное");
		
		ИзменитьРеквизиты(МассивРеквизитов);
	КонецЕсли;	
	
	КонтрагентыЛокализация.ПриСозданииНаСервереФормаЭлемента(ЭтотОбъект, Отказ, СтандартнаяОбработка, Параметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимость();
	
	Оповестить("ОбновитьСписокПоследних");
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	КонтрагентыКлиентЛокализация.ПриОткрытии_ФормаЭлемента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	 
	Если (ИмяСобытия = "ИзмененоФизическоеЛицо") 
	   И (Параметр = Объект.ФизЛицо) 
	   И (Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо")) Тогда 
	   
		ЗагрузитьКонтактнуюИнформацию();
	КонецЕсли;
	
	Если ИмяСобытия = "ЗаписатьОсновнойБанковскийСчет"
		И ТипЗнч(Параметр) = Тип("Структура")
		И Параметр.Владелец = Объект.Ссылка
	Тогда 
		Объект.ОсновнойБанковскийСчет = Параметр.ОсновнойБанковскийСчет;
		Элементы.БанковскийСчетЗадан.Видимость = Истина;
		Элементы.БанковскийСчетНеЗадан.Видимость = Ложь;
		Записать();
	КонецЕсли;
	
	Если ИмяСобытия = "СозданоНовоеКонтактноеЛицо" 
		И Параметр.Владелец = Объект.Ссылка Тогда
		ЗаполнитьСписокКонтактныхЛиц();
	ИначеЕсли ИмяСобытия = "ИзменилосьКонтактноеЛицо"
		И Параметр.Владелец = Объект.Ссылка Тогда
		Для Каждого Строка Из КонтактныеЛица Цикл
			Если Строка.Ссылка = Параметр.КонтактноеЛицо Тогда 
				СтруктураДанных = ПолучитьДанныеДляСписка(Параметр.КонтактноеЛицо);
				ЗаполнитьЗначенияСвойств(Строка, СтруктураДанных);
				Прервать;
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;
	
	
	Если ИмяСобытия = "ОтредактированаИсторияКПП" Тогда
		
		ЮрлицаКлиент.ОбновитьИсториюКППВФорме(ЭтотОбъект, Параметр.ИсторияКПП);
		
	ИначеЕсли ИмяСобытия = "ОтредактированаИсторияНаименований" Тогда
		
		Если Параметр.Юрлицо = Объект.Ссылка Тогда
			ЮрлицаКлиент.ОбновитьИсториюНаименованийВФорме(ЭтотОбъект, Параметр.ИсторияНаименований);
		КонецЕсли;

	ИначеЕсли ИмяСобытия = "ПослеВводаСтрокНаРазныхЯзыках" Тогда
		
		ЮрлицаКлиент.УстановитьАктуальноеЗначениеИсторииНаименований(ЭтотОбъект);
		
	КонецЕсли;
	
	КонтрагентыКлиентЛокализация.ОбработкаОповещения_ФормаЭлемента(ЭтотОбъект, Объект, ИмяСобытия, Параметр, Источник);

	ОбработатьСобытиеЗаписиЗадачи(ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОтображатьФотографииПерсональнаяНастройка =
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиПрограммы",
		"ОтображатьФотографииПерсональнаяНастройка",
		Истина);
		
	ОтображатьФотографииОбщаяНастройка = ПолучитьФункциональнуюОпцию("ОтображатьФотографииОбщаяНастройка");
	
	ПолучатьФотографии = Истина;
	
	Если Не ОтображатьФотографииОбщаяНастройка 
		Или Не ОтображатьФотографииПерсональнаяНастройка
		Или ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая Тогда
		ПолучатьФотографии = Ложь;
		Элементы.Фотография.Видимость = Ложь;
	КонецЕсли;	
	
	Если ПолучатьФотографии Тогда
		Фотография = РаботаСФотографиями.ПолучитьАдресФото(Объект.Ссылка, 
			УникальныйИдентификатор, ЕстьКартинка);
	КонецЕсли;	
	
	Если ЕстьКартинка Тогда
		
		Элементы.Фотография.Гиперссылка = Не ЕстьКартинка;
		Элементы.АдресКартинкиКонтекстноеМенюДобавитьИзображение.Доступность = Не ЕстьКартинка;
		Элементы.АдресКартинкиКонтекстноеМенюОчиститьИзображение.Доступность = ЕстьКартинка;
	КонецЕсли;	
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	Если Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект.ФизЛицо);
	Иначе
		УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ФормироватьНаименованиеПолноеАвтоматически = ПустаяСтрока(Объект.НаименованиеПолное)
		Или (Объект.НаименованиеПолное = Объект.Наименование);
	
	Юрлица.ПрочитатьИсториюВФорме(ЭтотОбъект);
	
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	КлючеваяОперация = РаботаСКонтрагентамиДокументооборотКлиентСервер.КлючеваяОперацияЗаписьКонтрагента();
	УИДЗамера = ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация,, Ложь);
	ПараметрыЗаписи.Вставить("УИДЗамера", УИДЗамера);
	
	ТекстСообщения = "";
	
	// Проверка корректности заполнения реквизитов контрагента
	КонтрагентыКлиентЛокализация.ПередЗаписью_ФормаЭлемента(Объект, ТекстСообщения, Отказ, ПараметрыЗаписи);
	
	// Если есть дубли, покажем их:
	Если НЕ Отказ И ЗначениеЗаполнено(Объект.Наименование) Тогда
		
		Если Не ПараметрыЗаписи.Свойство("ЗаписьДублирующегоКонтрагента") Тогда
			
			НайденныеДубли = НайденныеДублиКонтрагента_Сервер();
			Если НайденныеДубли.Количество() > 0 Тогда
				ПараметрыФормы = Новый Структура;
				ПараметрыФормы.Вставить("НайденныеОбъекты", НайденныеДубли);
				ПараметрыФормы.Вставить("ИмяОбъектаМетаданных", "Контрагенты");
				ОписаниеОповещения = Новый ОписаниеОповещения(
					"ПередЗаписьюЕстьДубли_Продолжение",
					ЭтотОбъект,
					ПараметрыЗаписи);
				ОткрытьФорму(
					"Справочник.Контрагенты.Форма.СписокДублейЮрлиц", 
					ПараметрыФормы, 
					ЭтотОбъект,,,,
					ОписаниеОповещения,
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
				Отказ = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТекущийОбъект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда 
		
		Если ТекущийОбъект.Ссылка.Пустая() И ТекущийОбъект.ФизЛицо.Пустая() Тогда 
			ОбъектФизЛицо = Справочники.ФизическиеЛица.СоздатьЭлемент();
			ОбъектФизЛицо.Наименование = ТекущийОбъект.Наименование;
			// Здесь нужна дополнительная операция, чтобы перенести контактные данные.
			// СтандартныеПодсистемы.КонтактнаяИнформация
			УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(ЭтотОбъект, ОбъектФизЛицо);
			// Конец СтандартныеПодсистемы.КонтактнаяИнформация
			ОбъектФизЛицо.Записать();
			ТекущийОбъект.ФизЛицо = ОбъектФизЛицо.Ссылка;
		ИначеЕсли Не ТекущийОбъект.ФизЛицо.Пустая() Тогда 
			ЗаблокироватьДанныеДляРедактирования(ТекущийОбъект.ФизЛицо);
			ОбъектФизЛицо = ТекущийОбъект.ФизЛицо.ПолучитьОбъект();
			// Здесь нужна дополнительная операция, чтобы перенести контактные данные.
			// СтандартныеПодсистемы.КонтактнаяИнформация
			УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(ЭтотОбъект, ОбъектФизЛицо);
			// Конец СтандартныеПодсистемы.КонтактнаяИнформация
			ОбъектФизЛицо.Записать();
		Иначе 
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Укажите ""Физическое лицо""'"),,
				"Объект.ФизЛицо",, 
				Отказ); 
			ТекущийЭлемент = Элементы.ФизЛицо1;
			Возврат;
		КонецЕсли;
		
		Элементы.ФизЛицо1.ТолькоПросмотр = Истина;
		
	ИначеЕсли ТекущийОбъект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо
		Или ТекущийОбъект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда 
		
		Если Не ТекущийОбъект.ФизЛицо.Пустая() Тогда 
			ЗаблокироватьДанныеДляРедактирования(ТекущийОбъект.ФизЛицо);
			ОбъектФизЛицо = ТекущийОбъект.ФизЛицо.ПолучитьОбъект();
			ОбъектФизЛицо.УстановитьПометкуУдаления(Истина);
			ТекущийОбъект.ФизЛицо = Неопределено;
		КонецЕсли;
		
	КонецЕсли;	
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
	// Для уже созданного контрагента лучше записать историю как можно раньше, чтобы корректно обновился 
	// реквизит "КонтрагентыДляСписков":
	Если Не ПараметрыЗаписи.ЭтоНовыйОбъект Тогда
		Если ИсторияКПП_Модифицированность Или ИсторияНаименований_Модифицированность Тогда
			ТекущийОбъект.ДополнительныеСвойства.Вставить("ИзмениласьИсторияРеквизитов", Истина);
		КонецЕсли;
		
		Юрлица.ЗаписатьИсториюВФорме(ЭтотОбъект, ТекущийОбъект.Ссылка, Отказ);
	КонецЕсли;
	
	Если ДанныеКонтактныхЛиц.Количество() <> 0 Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ДанныеКонтактныхЛиц", ДанныеКонтактныхЛиц);
	КонецЕсли;
	
	МультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);
	
	КонтрагентыЛокализация.ПередЗаписьюНаСервереФормаЭлемента(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.ЭтоНовыйОбъект Тогда
		Юрлица.ЗаписатьИсториюВФорме(ЭтотОбъект, ТекущийОбъект.Ссылка, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПротоколированиеРаботыСотрудников.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыСотрудников.ЗаписатьИзменение(Объект.Ссылка);
	
	Если ПараметрыЗаписи.Свойство("ЭтоНовыйОбъект") И ПараметрыЗаписи.ЭтоНовыйОбъект = Истина Тогда
		РаботаСПоследнимиОбъектами.ЗаписатьОбращениеКОбъекту(Объект.Ссылка);
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("НетНастройкиНумерации", Нумерация.НетНастройкиНумерации(ТекущийОбъект.Ссылка));
	ПараметрыЗаписи.Вставить("Модифицированность", Модифицированность);
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	ИндексНумерации = СокрЛП(ИндексНумерации);
	Если ИндексНумерации <> ИндексНумерацииПриОткрытии Тогда 
		Если ЗначениеЗаполнено(ИндексНумерации) Тогда 
			РегистрыСведений.ИндексыНумерации.ЗаписатьИндексНумерации(Объект.Ссылка, ИндексНумерации);
		Иначе 
			РегистрыСведений.ИндексыНумерации.УдалитьИндексНумерации(Объект.Ссылка);
		КонецЕсли;
		
		ИндексНумерацииПриОткрытии = ИндексНумерации;
	КонецЕсли;
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ДанныеКонтактныхЛиц")
		И Не ТекущийОбъект.Модифицированность() Тогда
		
		ДанныеКонтактныхЛиц.Очистить();
		ЗаполнитьСписокКонтактныхЛиц();
		
	КонецЕсли;

	КонтрагентыЛокализация.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПослеЗаписиКлиент(ПараметрыЗаписи);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(ПараметрыЗаписи.УИДЗамера);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Объект.Ссылка)
		И Не ЗначениеЗаполнено(Объект.ФизЛицо)
		И Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Поле  ""Физическое лицо"" не заполнено'"),, "Объект.ФизЛицо",, Отказ);
	   
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Объект, Отказ);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПередЗакрытием(
		Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Модифицированность) Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		Отказ = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияДокументооборотКлиент.ВставитьВОписаниеОповещенияОЗакрытииСсылкуНаОбъект(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПриЗакрытии(ЗавершениеРаботы) Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияДокументооборотКлиент.ВставитьВОписаниеОповещенияОЗакрытииСсылкуНаОбъект(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Настройки["ПоказыватьНеДействительныеКонтактныеЛица"] <> Неопределено Тогда
		ПоказыватьНеДействительныеКонтактныеЛица = Настройки["ПоказыватьНеДействительныеКонтактныеЛица"];
		Элементы.ПоказыватьНеДействительныеКонтактныеЛица.Пометка = ПоказыватьНеДействительныеКонтактныеЛица;
		ЗаполнитьСписокКонтактныхЛиц();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)
    МультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	
	КонтрагентыКлиентЛокализация.ИННПриИзменении(ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура КПППриИзменении(Элемент)

	КонтрагентыКлиентЛокализация.КПППриИзменении(ЭтотОбъект);
	
	ЮрлицаКлиент.УстановитьАктуальноеЗначениеИсторииКПП(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИсторияКППНажатие(Элемент)
	
	ЮрлицаКлиент.ОткрытьФормуИсторииКПП(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЮрФизЛицоПриИзменении(Элемент)
	
	Если Объект.ЮрФизЛицо = СтароеЮрФизЛицо Тогда 
		Возврат;
	КонецЕсли;	
		
	ИзменитьВидКонтрагента();
	УстановитьВидимость();
		
	ЭтоЮридическоеЛицо = (Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо"));
	Если НЕ ЭтоЮридическоеЛицо Тогда
		Объект.КПП = "";
	КонецЕсли;
	
	ЭтоИностранныйКонтрагент = (Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент"));
	РеквизитыПроверкиКонтрагентов.ЭтоИностранныйКонтрагент = ЭтоИностранныйКонтрагент;
		
	СтароеЮрФизЛицо = Объект.ЮрФизЛицо;

	КонтрагентыКлиентЛокализация.ЮрФизЛицоПриИзменении(ЭтотОбъект, Объект, ЭтоЮридическоеЛицо);	
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если ФормироватьНаименованиеПолноеАвтоматически 
		Или Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо") Тогда 
		Объект.НаименованиеПолное = Объект.Наименование;
	КонецЕсли;
	
	ЮрлицаКлиент.УстановитьАктуальноеЗначениеИсторииНаименований(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеПриИзменении(Элемент)

	ФормироватьНаименованиеПолноеАвтоматически = ПустаяСтрока(Объект.НаименованиеПолное)
		ИЛИ (Объект.НаименованиеПолное = Объект.Наименование);
	
	ЮрлицаКлиент.УстановитьАктуальноеЗначениеИсторииНаименований(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Список = Новый СписокЗначений;
	Если ЗначениеЗаполнено(Объект.НаименованиеПолное) Тогда
		Список.Добавить(Объект.НаименованиеПолное);
	КонецЕсли;
	Для каждого НаименованиеАвтозаполнения Из НаименованияАвтозаполнения Цикл
		ТекНаименование = НаименованиеАвтозаполнения.Значение;
		Если ЗначениеЗаполнено(ТекНаименование)	
			И Список.НайтиПоЗначению(ТекНаименование) = Неопределено Тогда
			Список.Добавить(ТекНаименование);
		КонецЕсли;
	КонецЦикла;
	Если ЗначениеЗаполнено(Объект.Наименование)
		И Список.НайтиПоЗначению(Объект.Наименование) = Неопределено Тогда
		Список.Добавить(Объект.Наименование);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("НаименованиеПолноеНачалоВыбораЗавершение", ЭтотОбъект);
	ПоказатьВыборИзСписка(Оповещение, Список, Элементы.НаименованиеПолное);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Объект.НаименованиеПолное = Результат.Значение;
		Модифицированность = Истина;
		ФормироватьНаименованиеПолноеАвтоматически = ПустаяСтрока(Объект.НаименованиеПолное)
			ИЛИ (Объект.НаименованиеПолное = Объект.Наименование);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИсторияНаименованийНажатие(Элемент)
	
	ЮрлицаКлиент.ОткрытьФормуИсторииНаименований(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновнойБанковскийСчетНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда 
		СтандартнаяОбработка = Ложь;
		ПоказатьПредупреждение(, НСтр("ru = 'Для выбора основного счета необходимо записать контрагента'"));
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ФизЛицо1ПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ФизЛицо) Тогда 
		Объект.Наименование = СокрЛП(Объект.ФизЛицо);
		Объект.НаименованиеПолное = Объект.Наименование;
	КонецЕсли;
	ЗагрузитьКонтактнуюИнформацию();
	
КонецПроцедуры

&НаКлиенте
Процедура ФизЛицо2ПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ФизЛицо) Тогда 
		Объект.Наименование = СокрЛП(Объект.ФизЛицо);
		Объект.НаименованиеПолное = Объект.Наименование;
	КонецЕсли;
	ЗагрузитьКонтактнуюИнформацию();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресКартинкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗаблокироватьДанныеФормыДляРедактирования();
	ДобавитьИзображениеНаКлиенте();
	
КонецПроцедуры

// Локализация
// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура ПолеПоискаИНННаименованиеПриИзменении(Элемент)
	
	ЗаполнитьПоДаннымЕдиныхГосРеестровНаКлиенте(ПолеПоискаИНННаименование);
	
КонецПроцедуры

&НаКлиенте
Процедура РодительАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = СформироватьДанныеВыбораРодителя(Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РодительОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = СформироватьДанныеВыбораРодителя(Текст);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКонтактныеЛица

&НаКлиенте
Процедура КонтактныеЛицаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если Объект.Ссылка.Пустая() Тогда 
		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Создание:'"), 
			ПолучитьНавигационнуюСсылку(Объект.Ссылка),
			Строка(Объект.Ссылка),
			БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	
	Если Копирование Тогда 
		ПараметрыФормы.Вставить("ЗначениеКопирования", Элементы.КонтактныеЛица.ТекущиеДанные.Ссылка);
	Иначе 
		ПараметрыФормы.Вставить("Владелец", Объект.Ссылка);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.КонтактныеЛица.Форма.ФормаЭлемента", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактныеЛицаПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	Если Объект.Ссылка.Пустая() Тогда 
		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Создание:'"), 
			ПолучитьНавигационнуюСсылку(Объект.Ссылка),
			Строка(Объект.Ссылка),
			БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
	ТекущиеДанные = Элементы.КонтактныеЛица.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ТекущиеДанные.Ссылка);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеИзмененияКонтактногоЛица", ЭтотОбъект, ТекущиеДанные);
	ОткрытьФорму("Справочник.КонтактныеЛица.Форма.ФормаЭлемента", ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеИзмененияКонтактногоЛица(Результат, ДополнительныеПараметры) Экспорт

	ИзменитьСоставКонтактныхЛиц(ДополнительныеПараметры);

КонецПроцедуры

&НаКлиенте
Процедура КонтактныеЛицаПриАктивизацииСтроки(Элемент)
	
	УстановитьЭлементыФормыОсновногоКонтактногоЛица(Элементы.КонтактныеЛица.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактныеЛицаПередУдалением(Элемент, Отказ)
	
	ВыделенныеСтроки = Элементы.КонтактныеЛица.ВыделенныеСтроки;
	КоличествоСтрок = ВыделенныеСтроки.Количество();
	
	// Если карточка еще не записана, то удаляем просто элементы списка
	Если Объект.Ссылка.Пустая() Тогда 
		КоличествоКонтактныхЛиц = ?(КоличествоКонтактныхЛиц > КоличествоСтрок, 
			КоличествоКонтактныхЛиц - КоличествоСтрок, 0);
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	СписокКонтактныхЛиц = Новый СписокЗначений;
	
	Если КоличествоСтрок = 0 Тогда 
		Возврат;
	ИначеЕсли КоличествоСтрок = 1 Тогда 
		ТекущиеДанные = Элементы.КонтактныеЛица.ТекущиеДанные;
		СписокКонтактныхЛиц.Добавить(ТекущиеДанные.Ссылка);
		ТекущаяПометкаУдаления = ТекущиеДанные.ПометкаУдаления;
		
		Если ТекущаяПометкаУдаления Тогда
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Снять с ""%1"" пометку на удаление?'"),
					Строка(ТекущиеДанные.Ссылка));
		Иначе
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Пометить ""%1"" на удаление?'"),
					Строка(ТекущиеДанные.Ссылка));
		КонецЕсли;
	Иначе 
		
		ТекущаяПометкаУдаления = Ложь;
		Для Каждого ВыбраннаяСтрока Из ВыделенныеСтроки Цикл
			ДанныеСтроки = Элементы.КонтактныеЛица.ДанныеСтроки(ВыбраннаяСтрока);
			СписокКонтактныхЛиц.Добавить(ДанныеСтроки.Ссылка);
			
			Если ДанныеСтроки.ПометкаУдаления Тогда 
				ТекущаяПометкаУдаления = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если ТекущаяПометкаУдаления Тогда
			ТекстВопроса = НСтр("ru = 'Снять с выделенных элементов пометку на удаление?'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Пометить выделенные элементы на удаление?'");
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("КонтактныеЛица",  СписокКонтактныхЛиц);
	ПараметрыОбработчика.Вставить("ВыделенныеСтроки",  ВыделенныеСтроки);
	ПараметрыОбработчика.Вставить("ТекущаяПометкаУдаления", ТекущаяПометкаУдаления);
	
	ОписаниеОповещенияОтветаНаВопрос = Новый ОписаниеОповещения(
		"КонтактныеЛицаПередУдалениемПродолжение",
		ЭтотОбъект,
		ПараметрыОбработчика);
	ПоказатьВопрос(ОписаниеОповещенияОтветаНаВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактныеЛицаПередУдалениемПродолжение(Результат, Параметры) Экспорт 
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяПометкаУдаления = Параметры.ТекущаяПометкаУдаления;
	ТекстОшибки = "";
	
	Если УстановитьПометкуНаУдаление(Параметры.КонтактныеЛица, Не ТекущаяПометкаУдаления, ТекстОшибки) Тогда
		Если ПоказыватьНеДействительныеКонтактныеЛица Тогда
			Для Каждого ВыбраннаяСтрока Из Параметры.ВыделенныеСтроки Цикл
				ДанныеСтроки = Элементы.КонтактныеЛица.ДанныеСтроки(ВыбраннаяСтрока);
				ДанныеСтроки.ПометкаУдаления = Не ТекущаяПометкаУдаления;
				Если ДанныеСтроки.ОсновноеКонтактноеЛицо = Истина И ДанныеСтроки.ПометкаУдаления = Истина Тогда
					УстановитьЭлементыФормыОсновногоКонтактногоЛица(ДанныеСтроки);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		Если ТекущаяПометкаУдаления Тогда
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось снять пометку на удаление.
					|%1'"),
				ТекстОшибки);
		Иначе
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось установить пометку на удаление.
					|%1'"),
				ТекстОшибки);
		КонецЕсли;
		
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	СотрудникиКлиент.СотрудникОбработкаВыбора(Объект, "Ответственный", ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НеДействует(Команда)
	
	ТекущиеДанные = Элементы.КонтактныеЛица.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НеДействуетНаСервере(ТекущиеДанные.Ссылка);
	
	Если ТекущиеДанные.НеДействует = Истина И ТекущиеДанные.Ссылка = ОсновноеКонтактноеЛицо Тогда
		ОсновноеКонтактноеЛицо = ПредопределенноеЗначение("Справочник.КонтактныеЛица.ПустаяСсылка");
	КонецЕсли;
	
	ИзменитьСоставКонтактныхЛиц(ТекущиеДанные);
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено,
	СтандартнаяОбработка = Неопределено)
	
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.РаботаСКонтрагентами
#Область РаботаСКонтрагентамиБИП_Команды

// Локализация
&НаКлиенте
Процедура ЗаполнитьПоНаименованию(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Наименование) Тогда
		ПоказатьПредупреждение(, НСтр("ru='Поле ""Наименование"" не заполнено'"));
		ТекущийЭлемент = Элементы.Наименование;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПоДаннымЕдиныхГосРеестровНаКлиенте(Объект.Наименование, Ложь);
	
КонецПроцедуры
// Локализация Конец

// Локализация
&НаКлиенте
Процедура ЗаполнитьПоДаннымЕдиныхГосРеестров(Команда)
	
	Если Не ЗначениеЗаполнено(ПолеПоискаИНННаименование) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru='Поле ""ИНН или наименование"" не заполнено.'"),
			,
			"ПолеПоискаИНННаименование");
		ТекущийЭлемент = Элементы.ПолеПоискаИНННаименование;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПоДаннымЕдиныхГосРеестровНаКлиенте(ПолеПоискаИНННаименование);
	
КонецПроцедуры
// Локализация Конец

// Локализация
&НаКлиенте
Процедура ЗаполнитьПоИНН(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.ИНН) Тогда
		ПоказатьПредупреждение(, НСтр("ru='Поле ""ИНН"" не заполнено'"));
		
		Если Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель") Тогда 
			ТекущийЭлемент = Элементы.ИНН2;
		Иначе 
			ТекущийЭлемент = Элементы.ИНН;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если Объект.ЮрФизЛицо <> ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель")
		И Объект.ЮрФизЛицо <> ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо") Тогда 
		ПоказатьПредупреждение(,
			НСтр("ru = 'Заполнение по ИНН возможно только для юридических лиц и индивидуальных предпринимателей'"));
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПоДаннымЕдиныхГосРеестровНаКлиенте(Объект.ИНН, Истина);
	
КонецПроцедуры
// Локализация Конец

// Локализация
&НаКлиенте
Процедура ПроверитьКонтрагента(Команда)
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПроверитьКонтрагентаПоКнопке(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
	
КонецПроцедуры
// Локализация Конец

#КонецОбласти
// Конец СтандартныеПодсистемы.РаботаСКонтрагентами

// Локализация
// Команды1СПАРКРиски
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду1СПАРКРиски(Команда)

	СПАРКРискиКлиент.ВыполнитьПодключаемуюКоманду(Команда, ЭтотОбъект, Объект);

КонецПроцедуры
// Конец Команды1СПАРКРиски
// Локализация Конец

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)
	
	ЗаписатьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьНеДействительныеКонтактныеЛица(Команда)
	
	ПоказыватьНеДействительныеКонтактныеЛица = Не ПоказыватьНеДействительныеКонтактныеЛица;
	Элементы.ПоказыватьНеДействительныеКонтактныеЛица.Пометка = ПоказыватьНеДействительныеКонтактныеЛица;
	
	ЗаполнитьСписокКонтактныхЛиц();
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновноеКонтактноеЛицо(Команда)
	
	Если Элементы.КонтактныеЛица.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОписаниеОповещенияДляЗаписиОбъекта = Новый ОписаниеОповещения("ОсновноеКонтактноеЛицоЗаписатьОбъект",
			ЭтотОбъект);
		ТекстВопроса = НСтр("ru='Для выбора основного контактного лица необходимо записать объект. Записать?'");
		ПоказатьВопрос(ОписаниеОповещенияДляЗаписиОбъекта, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОсновноеКонтактноеЛицоПродолжение", ЭтотОбъект);
	
	Если Элементы.КонтактныеЛица.ТекущиеДанные.НеДействует = Истина
		И Элементы.КонтактныеЛица.ТекущиеДанные.ОсновноеКонтактноеЛицо = Ложь Тогда
		ТекстВопроса = НСтр("ru='Контактное лицо является недействующим. Продолжить?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	ИначеЕсли Элементы.КонтактныеЛица.ТекущиеДанные.ПометкаУдаления = Истина
		И Элементы.КонтактныеЛица.ТекущиеДанные.ОсновноеКонтактноеЛицо = Ложь Тогда
		ТекстВопроса = НСтр("ru='Контактное лицо помечено на удаление. Продолжить?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновноеКонтактноеЛицоЗаписатьОбъект(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновноеКонтактноеЛицоПродолжение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Если ОсновноеКонтактноеЛицо = Элементы.КонтактныеЛица.ТекущиеДанные.Ссылка Тогда
		ОсновноеКонтактноеЛицо = ПредопределенноеЗначение("Справочник.КонтактныеЛица.ПустаяСсылка");
		Элементы.КонтактныеЛица.ТекущиеДанные.ОсновноеКонтактноеЛицо = Ложь;
	Иначе
		ПредыдущееОсновноеКонтактноеЛицо = КонтактныеЛица.НайтиСтроки(Новый Структура("ОсновноеКонтактноеЛицо", Истина));
		Если ПредыдущееОсновноеКонтактноеЛицо.Количество() Тогда
			ПредыдущееОсновноеКонтактноеЛицо[0].ОсновноеКонтактноеЛицо = Ложь;
		КонецЕсли;
		ОсновноеКонтактноеЛицо = Элементы.КонтактныеЛица.ТекущиеДанные.Ссылка;
	КонецЕсли;
	
	СнятьУстановитьОтметкуОсновногоКонтактногоЛицаКонтрагента(Объект.Ссылка, ОсновноеКонтактноеЛицо);
	
	УстановитьЭлементыФормыОсновногоКонтактногоЛица(Элементы.КонтактныеЛица.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьБанковскийСчет(Команда)
	
	ОписаниеОповещенияОтветаНаВопрос = Новый ОписаниеОповещения(
		"СоздатьБанковскийСчетПродолжение",
		ЭтотОбъект);
	
	Если Объект.Ссылка.Пустая() Тогда
		ПоказатьВопрос(ОписаниеОповещенияОтветаНаВопрос,
			НСтр("ru = 'Данные еще не записаны.
                 |Создание ""Банковского счета"" возможно только после записи данных. Записать?'"),
			РежимДиалогаВопрос.ДаНет);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияОтветаНаВопрос, КодВозвратаДиалога.Ок);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьБанковскийСчетПродолжение(Результат, Параметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Да И Не Записать() Тогда 
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("ЗначенияЗаполнения", Новый Структура("Владелец", Объект.Ссылка));
	ОткрытьФорму(
		"Справочник.БанковскиеСчета.ФормаОбъекта", СтруктураПараметров, ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура Трудозатраты(Команда)
	
	ПараметрыФормы = Новый Структура("Источник", Объект.Ссылка);
	ОткрытьФорму("РегистрСведений.ФактическиеТрудозатраты.Форма.ФормаСпискаИсточника", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваДоступа(Команда)
	
	ДокументооборотПраваДоступаКлиент.ОткрытьФормуПравДоступа(ЭтотОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Учет времени

&НаКлиенте
Процедура ПереключитьХронометраж(Команда)
	ПараметрыОповещения = Неопределено;
	НуженДиалог = УчетВремениКлиент.НуженДиалогДляХронометража(ВключенХронометраж, 
		ДатаНачалаХронометража, ВидыРабот);
	
	Если НуженДиалог = Ложь Тогда
		
		ПереключитьХронометражСервер(ПараметрыОповещения);
		УчетВремениКлиент.ПоказатьОповещение(ПараметрыОповещения, ВключенХронометраж, Объект.Ссылка);
	
	Иначе
		ДлительностьРаботы = УчетВремениКлиент.ПолучитьДлительностьРаботы(ДатаНачалаХронометража);
		
		ОписаниеРаботы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Работа с контрагентом""%1""'"),
			Объект.Наименование);
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ДатаОтчета", ТекущаяДата());
		ПараметрыФормы.Вставить("ВидыРабот", ВидыРабот);
		ПараметрыФормы.Вставить("ОписаниеРаботы", ОписаниеРаботы);
		ПараметрыФормы.Вставить("ДлительностьРаботы", ДлительностьРаботы);
		ПараметрыФормы.Вставить("НачалоРаботы", ДатаНачалаХронометража);
		ПараметрыФормы.Вставить("Объект", Объект.Ссылка);
		ПараметрыФормы.Вставить("СпособУказанияВремени", СпособУказанияВремени);
			
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПереключитьХронометражПродолжение",
			ЭтотОбъект);
		
		ОткрытьФорму("РегистрСведений.ФактическиеТрудозатраты.Форма.ФормаДобавленияРаботы", ПараметрыФормы,,,,,
			ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьХронометражПродолжение(Результат, Параметры) Экспорт 
	
	Если Результат <> Неопределено Тогда
		ДобавитьВОтчетИОбновитьФорму(Результат, ПараметрыОповещения);
		УчетВремениКлиент.ПоказатьОповещение(ПараметрыОповещения, ВключенХронометраж, Объект.Ссылка);
	Иначе
		ОтключитьХронометражСервер();
	КонецЕсли;  

КонецПроцедуры

&НаКлиенте
Процедура УказатьТрудозатраты(Команда)
	
	ДатаОтчета = ТекущаяДата();
	
	УчетВремениКлиент.ДобавитьВОтчетКлиент(
		ДатаОтчета,
		ВключенХронометраж, 
		ДатаНачалаХронометража, 
		ДатаКонцаХронометража, 
		ВидыРабот, 
		Объект.Ссылка,
		СпособУказанияВремени,
		Элементы.ПереключитьХронометраж,
		Ложь,
		ЭтотОбъект); // Выполнена
		
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИзображение(Команда)
	
	РаботаСФотографиями.ОчиститьИзображение(Объект.Ссылка, УникальныйИдентификатор);
	
	ЕстьКартинка = Ложь;
	Элементы.Фотография.Гиперссылка = Не ЕстьКартинка;
	Элементы.АдресКартинкиКонтекстноеМенюДобавитьИзображение.Доступность = Не ЕстьКартинка;
	Элементы.АдресКартинкиКонтекстноеМенюОчиститьИзображение.Доступность = ЕстьКартинка;
	Прочитать();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображение(Команда)
	
	ДобавитьИзображениеНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ВсеПоКонтрагенту(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Контрагент еще не записан.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Контрагент", Объект.Ссылка);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", "ВсеПоКонтрагенту");
	ОткрытьФорму("Отчет.СводкаПоКонтрагенту.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛентаКонтрагента(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Контрагент еще не записан.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Контрагент", Объект.Ссылка);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", "Лента");
	ОткрытьФорму("Отчет.СводкаПоКонтрагенту.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДосьеКонтрагента(Команда)
// Локализация
	Если Объект.Ссылка.Пустая() Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Контрагент еще не записан.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Контрагент", Объект.Ссылка);
	
	ОткрытьФорму("Отчет.ДосьеКонтрагента.Форма", ПараметрыФормы, ЭтотОбъект);
// Локализация Конец
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура НеДействуетНаСервере(КонтактноеЛицоСсылка)
	
	КонтактноеЛицоОбъект = КонтактноеЛицоСсылка.ПолучитьОбъект();
	КонтактноеЛицоОбъект.НеДействует = Не КонтактноеЛицоОбъект.НеДействует;
	КонтактноеЛицоОбъект.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСоставКонтактныхЛиц(ИзмененныйЭлемент)
	
	Если Не ПоказыватьНеДействительныеКонтактныеЛица Тогда
		
		ЗаполнитьСписокКонтактныхЛиц();
		
	Иначе
		
		ИзмененныйЭлементНеДействует = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			ИзмененныйЭлемент.Ссылка, "НеДействует");
		ИзмененныйЭлемент.НеДействует = ИзмененныйЭлементНеДействует;
		Если ИзмененныйЭлементНеДействует = Истина Тогда
			ОсновноеКонтактноеЛицо = ПредопределенноеЗначение("Справочник.КонтактныеЛица.ПустаяСсылка");
			УстановитьЭлементыФормыОсновногоКонтактногоЛица(ИзмененныйЭлемент);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Учет времени

&НаСервере
Процедура ПереключитьХронометражСервер(ПараметрыОповещения)
	
	УчетВремени.ПереключитьХронометражСервер(
		ПараметрыОповещения,
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		Объект.Ссылка,
		ВидыРабот,
		Команды.ПереключитьХронометраж,
		Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьХронометражСервер()
	
	УчетВремени.ОтключитьХронометражСервер(
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		Объект.Ссылка,
		Команды.ПереключитьХронометраж,
		Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВОтчетИОбновитьФорму(ПараметрыОтчета, ПараметрыОповещения)
	
	УчетВремени.ДобавитьВОтчетИОбновитьФорму(
		ПараметрыОтчета, 
		ПараметрыОповещения,
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		Команды.ПереключитьХронометраж,
		Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьВидКонтрагента()
	
	ИзменитьКонтактнуюИнформацию();
	
	// Обработчик механизма "Свойства".
	ОбновитьЭлементыДополнительныхРеквизитов();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимость()
	
	Элементы.ГруппаКакЮрЛицо.Видимость = Ложь;
	Элементы.ГруппаКакФизЛицо.Видимость = Ложь;
	Элементы.ГруппаКакИП.Видимость = Ложь;
	Элементы.ГруппаКакНерезидент.Видимость = Ложь;
	// Локализация
	Элементы.ГруппаРегламентированныйУчет.Видимость = Ложь;
	// Локализация Конец
	
	Если Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо") Тогда
		Элементы.ГруппаКакФизЛицо.Видимость = Истина;
		Элементы.Фотография.Видимость = Истина;
		
		Если Объект.ФизЛицо.Пустая() Тогда
			Элементы.ФизЛицо1.ТолькоПросмотр = Ложь;
		КонецЕсли;
		
	ИначеЕсли Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель") Тогда
		Элементы.ГруппаКакИП.Видимость = Истина;
		Элементы.Фотография.Видимость = Ложь;
		
	ИначеЕсли Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент") Тогда	
		Элементы.ГруппаКакНерезидент.Видимость = Истина;
		Элементы.Фотография.Видимость = Ложь;
		
	Иначе
		Элементы.ГруппаКакЮрЛицо.Видимость = Истина;
		Элементы.Фотография.Видимость = Ложь;
		// Локализация
		Элементы.ГруппаРегламентированныйУчет.Видимость = Истина;
		// Локализация Конец
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьКонтактнуюИнформацию()
	
	// Обработчик подсистемы "Контактная информация".
	Если Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтотОбъект, Объект.ФизЛицо);
	Иначе
		УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтотОбъект, Объект);
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура ЗагрузитьКонтактнуюИнформацию()
	
	Объект.КонтактнаяИнформация.Загрузить(Объект.ФизЛицо.КонтактнаяИнформация.Выгрузить());
	ИзменитьКонтактнуюИнформацию();
	
КонецПроцедуры	

&НаСервере
Функция НайденныеДублиКонтрагента_Сервер()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда // проверим уникальность только по измененным
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Ссылка,
			"Наименование, НаименованиеПолное, ИНН, РегистрационныйНомер");
	Иначе
		Реквизиты = Новый Структура();
		Реквизиты.Вставить("Наименование");
		Реквизиты.Вставить("НаименованиеПолное");
		Реквизиты.Вставить("ИНН");
		Реквизиты.Вставить("РегистрационныйНомер");
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("Код", Неопределено); // не сравниваем, но получаем
	
	Для Каждого Реквизит Из Реквизиты Цикл
		Если ЗначениеЗаполнено(Объект[Реквизит.Ключ])
			И Объект[Реквизит.Ключ] <> Реквизит.Значение Тогда
			СтруктураПоиска.Вставить(Реквизит.Ключ, "=");
		КонецЕсли;
	КонецЦикла;
	
	Если СтруктураПоиска.Количество() > 1 Тогда
		Возврат РаботаСДублямиЗначений.НайтиДубли(Объект, СтруктураПоиска);
	КонецЕсли;
	Возврат Новый Массив();
	
КонецФункции

&НаКлиенте
Процедура ЗаписатьИЗакрыть()
	
	ПараметрыЗаписи = Новый Структура();
	ПараметрыЗаписи.Вставить("Закрыть", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюЕстьДубли_Продолжение(Результат, ПараметрыЗаписи) Экспорт
	
	Если Результат = Истина Тогда
		// Чтобы второй раз не открывать форму про дубли:
		ПараметрыЗаписи.Вставить("ЗаписьДублирующегоКонтрагента", Истина);
		Записать(ПараметрыЗаписи);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаписатьИЗакрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиКлиент(ПараметрыЗаписи)
	
	Если Не ПараметрыЗаписи.Свойство("ПоказаноПредупреждение") 
		И ПараметрыЗаписи.Свойство("НетНастройкиНумерации") И ПараметрыЗаписи.НетНастройкиНумерации = Истина Тогда
		ОписаниеПредупреждения = Новый ОписаниеОповещения(
			"ПослеЗаписиПродолжениеПослеПредупреждения",
			ЭтотОбъект,
			ПараметрыЗаписи);
		ПоказатьПредупреждение(ОписаниеПредупреждения,
			НСтр("ru = 'Документы с данным контрагентом нельзя будет зарегистрировать, так как отсутствует подходящая настройка нумерации.'"));
		Возврат;	
	КонецЕсли;	
	
	Если ПараметрыЗаписи.Свойство("Модифицированность") И ПараметрыЗаписи.Модифицированность Тогда
		ПараметрОповещения = Новый Структура;
		ПараметрОповещения.Вставить("Владелец", Объект.Ссылка);
		Оповестить("ИзменилсяКонтрагент", ПараметрОповещения);
	КонецЕсли;
	
	Оповестить("Запись_Контрагент", , Объект.Ссылка);
	
	Если ПараметрыЗаписи.Свойство("Закрыть") Тогда
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Изменение:'"),
			ПолучитьНавигационнуюСсылку(Объект.Ссылка),
			Строка(Объект.Ссылка),
			БиблиотекаКартинок.Информация32);
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиПродолжениеПослеПредупреждения(ПараметрыЗаписи) Экспорт
	
	ПараметрыЗаписи.Вставить("ПоказаноПредупреждение", Истина);
	ПослеЗаписиКлиент(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеНаКлиенте()
	
	ПараметрыОписания = Новый Структура(
		"АдресВременногоХранилищаФайла", 
		"");
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ДобавитьИзображениеНаКлиентеПродолжение",
		ЭтотОбъект,
		ПараметрыОписания);	
		
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ТекстВопроса = НСтр("ru='Для выбора изображения необходимо записать объект. Записать?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе 
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеНаКлиентеПродолжение(Результат, Параметры)Экспорт 
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать() Тогда 
		Возврат;
	КонецЕсли;	

	Если Не ЕстьКартинка И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ВыборКартинкиПродолжение",
			ЭтотОбъект,
			Параметры);
		
		ФайловыеФункцииКлиент.ВыбратьКартинкуИПоместитьВХранилище(
			ОписаниеОповещения, УникальныйИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКартинкиПродолжение(Результат, Параметры) Экспорт

	Если Результат <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСФотографиями.ЗаписатьИзображение(Параметры.АдресВременногоХранилищаФайла, 
		УникальныйИдентификатор, 
		Объект.Ссылка, Объект.Наименование);

	ЕстьКартинка = Истина;
	Элементы.Фотография.Гиперссылка = Не ЕстьКартинка;
	Элементы.АдресКартинкиКонтекстноеМенюДобавитьИзображение.Доступность = Не ЕстьКартинка;
	Элементы.АдресКартинкиКонтекстноеМенюОчиститьИзображение.Доступность = ЕстьКартинка;
	Прочитать();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКонтактныеЛица(Отбор, ПоказыватьУдаленные = Истина)
	
	Если Не ЗначениеЗаполнено(Отбор) Тогда 
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КонтактныеЛица.Ссылка КАК КонтактноеЛицо,
		|	КонтактныеЛица.ПометкаУдаления КАК ПометкаУдаления,
		|	КонтактныеЛица.Наименование КАК Наименование,
		|	КонтактныеЛица.Должность КАК Должность,
		|	КонтактныеЛица.НеДействует КАК НеДействует,
		|	ЕСТЬNULL(КонтактнаяИнформацияEmail.Представление, """") КАК Email,
		|	ЕСТЬNULL(КонтактнаяИнформацияМобильный.Представление, """") КАК Мобильный,
		|	ЕСТЬNULL(КонтактнаяИнформацияТелефон.Представление, """") КАК Телефон
		|ИЗ
		|	Справочник.КонтактныеЛица КАК КонтактныеЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица.КонтактнаяИнформация КАК КонтактнаяИнформацияEmail
		|		ПО КонтактныеЛица.Ссылка = КонтактнаяИнформацияEmail.Ссылка
		|		И (КонтактнаяИнформацияEmail.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailКонтактногоЛица))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица.КонтактнаяИнформация КАК КонтактнаяИнформацияМобильный
		|		ПО КонтактныеЛица.Ссылка = КонтактнаяИнформацияМобильный.Ссылка
		|		И
		|			(КонтактнаяИнформацияМобильный.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.МобильныйТелефонКонтактногоЛица))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица.КонтактнаяИнформация КАК КонтактнаяИнформацияТелефон
		|		ПО КонтактныеЛица.Ссылка = КонтактнаяИнформацияТелефон.Ссылка
		|		И (КонтактнаяИнформацияТелефон.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонКонтактногоЛица))
		| ГДЕ
		| 	%1
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование
		|ИТОГИ
		|ПО
		|	КонтактноеЛицо";
		
	ТекстОтбора = "";
	Если ТипЗнч(Отбор) = Тип("СправочникСсылка.Контрагенты") Тогда 
		ТекстОтбора = ТекстОтбора + "КонтактныеЛица.Владелец = &Параметр";
	Иначе 
		ТекстОтбора = ТекстОтбора + "КонтактныеЛица.Ссылка = &Параметр";
	КонецЕсли;
	
	Если Не ПоказыватьУдаленные Тогда 
		ТекстОтбора = ТекстОтбора + "
		|	И НЕ КонтактныеЛица.ПометкаУдаления
		|	И НЕ КонтактныеЛица.НеДействует";
	КонецЕсли;
		
	Запрос.Текст = СтрШаблон(Запрос.Текст, ТекстОтбора);
	
	Запрос.Параметры.Вставить("Параметр", Отбор);
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Ссылка");
	Таблица.Колонки.Добавить("Наименование");
	Таблица.Колонки.Добавить("Должность");
	Таблица.Колонки.Добавить("АдресаТелефоны");
	Таблица.Колонки.Добавить("ПометкаУдаления");
	Таблица.Колонки.Добавить("НеДействует");
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		Строка = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, Выборка);
		Строка.Ссылка = Выборка.КонтактноеЛицо;
		Строка.АдресаТелефоны = "";
		
		ВыборкаОбъектов = Выборка.Выбрать();
		Пока ВыборкаОбъектов.Следующий() Цикл
			
			Если ЗначениеЗаполнено(ВыборкаОбъектов.Телефон) Тогда 
				СтрокаТелефон = СокрЛП(ВыборкаОбъектов.Телефон) + ", ";
				
				Если СтрНайти(Строка.АдресаТелефоны, СтрокаТелефон) = 0 Тогда 
					Строка.АдресаТелефоны = Строка.АдресаТелефоны + СтрокаТелефон;
				КонецЕсли;
			КонецЕсли;
				
			Если ЗначениеЗаполнено(ВыборкаОбъектов.Мобильный) Тогда 
				СтрокаМобильный = НСтр("ru = 'моб.'") + " " + СокрЛП(ВыборкаОбъектов.Мобильный) + ", ";
				
				Если СтрНайти(Строка.АдресаТелефоны, СтрокаМобильный) = 0 Тогда 
					Строка.АдресаТелефоны = Строка.АдресаТелефоны + СтрокаМобильный;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ВыборкаОбъектов.Email) Тогда 
				СтрокаEmail = СокрЛП(ВыборкаОбъектов.Email) + ", ";
				
				Если СтрНайти(Строка.АдресаТелефоны, СтрокаEmail) = 0 Тогда 
					Строка.АдресаТелефоны = Строка.АдресаТелефоны + СтрокаEmail;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Строка.АдресаТелефоны = Лев(Строка.АдресаТелефоны, СтрДлина(Строка.АдресаТелефоны) - 2);
	КонецЦикла;
	
	Возврат Таблица;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокКонтактныхЛиц()
	
	ТаблицаКонтактныеЛица = ПолучитьКонтактныеЛица(
		Объект.Ссылка, ПоказыватьНеДействительныеКонтактныеЛица);
	КонтактныеЛица.Очистить();
	
	ОсновноеКонтактноеЛицо = РегистрыСведений.ОсновныеКонтактныеЛицаКонтрагентов.ОсновноеКонтактноеЛицоКонтрагента(
		Объект.Ссылка);
	
	Для Каждого КонтактноеЛицо Из ТаблицаКонтактныеЛица Цикл
		НоваяСтрока = КонтактныеЛица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, КонтактноеЛицо);
		Если НоваяСтрока.Ссылка = ОсновноеКонтактноеЛицо Тогда
			НоваяСтрока.ОсновноеКонтактноеЛицо = Истина;
		КонецЕсли;
	КонецЦикла;
	
	ДоступностьКоманд = (КонтактныеЛица.Количество() > 0);
	Элементы.КонтактныеЛицаНеДействует.Доступность = ДоступностьКоманд;
	Элементы.КонтактныеЛицаКонтекстноеМенюНеДействует.Доступность = ДоступностьКоманд;
	КоличествоКонтактныхЛиц = ТаблицаКонтактныеЛица.Количество();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеДляСписка(КонтактноеЛицо)
	
	Таблица = ПолучитьКонтактныеЛица(КонтактноеЛицо);
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Ссылка");
	СтруктураДанных.Вставить("ПометкаУдаления");
	СтруктураДанных.Вставить("Наименование");
	СтруктураДанных.Вставить("Должность");
	СтруктураДанных.Вставить("АдресаТелефоны");
	ЗаполнитьЗначенияСвойств(СтруктураДанных, Таблица[0]);
	
	Возврат СтруктураДанных;
	
КонецФункции

&НаСервере
Функция УстановитьПометкуНаУдаление(СписокКонтактныхЛиц, ЗначениеПометкиУдаления, ТекстОшибки) Экспорт

	Для Каждого КонтактноеЛицо Из СписокКонтактныхЛиц Цикл 
		Попытка
			ОбъектКонтактногоЛица = КонтактноеЛицо.Значение.Ссылка.ПолучитьОбъект();
			ОбъектКонтактногоЛица.Заблокировать();
			ОбъектКонтактногоЛица.УстановитьПометкуУдаления(ЗначениеПометкиУдаления);
			ОбъектКонтактногоЛица.Разблокировать();
			
			Если КонтактноеЛицо.Значение.Ссылка = ОсновноеКонтактноеЛицо И ЗначениеПометкиУдаления = Истина Тогда
				ОсновноеКонтактноеЛицо = Справочники.КонтактныеЛица.ПустаяСсылка();
			КонецЕсли;
		Исключение
			ТекстОшибки = ОписаниеОшибки();
			Возврат Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Если Не ПоказыватьНеДействительныеКонтактныеЛица Тогда 
		ЗаполнитьСписокКонтактныхЛиц();
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Процедура СнятьУстановитьОтметкуОсновногоКонтактногоЛицаКонтрагента(Контрагент, КонтактноеЛицо)
	
	Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
		РегистрыСведений.ОсновныеКонтактныеЛицаКонтрагентов.УстановитьОсновноеКонтактноеЛицоКонтрагента(
			Контрагент, КонтактноеЛицо);
	Иначе
		РегистрыСведений.ОсновныеКонтактныеЛицаКонтрагентов.СнятьОтметкуОсновногоКонтактногоЛицаКонтрагента(Контрагент);
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.КонтактнаяИнформация
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент)
	УправлениеКонтактнойИнформациейКлиент.НачатьИзменение(ЭтотОбъект, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.НачатьВыбор(ЭтотОбъект, Элемент, , СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриНажатии(Элемент, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.НачатьВыбор(ЭтотОбъект, Элемент, , СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОчистка(Элемент, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.НачатьОчистку(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияВыполнитьКоманду(Команда)
	УправлениеКонтактнойИнформациейКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.АвтоПодборАдреса(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.ОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, Элемент.Имя, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.НачатьОбработкуНавигационнойСсылки(ЭтотОбъект, Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьОбновлениеКонтактнойИнформации(Результат, ДополнительныеПараметры) Экспорт
	ОбновитьКонтактнуюИнформацию(Результат);
КонецПроцедуры

&НаСервере
Процедура ОбновитьКонтактнуюИнформацию(Результат)
	УправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию(ЭтотОбъект, Объект, Результат);
КонецПроцедуры
// Конец СтандартныеПодсистемы.КонтактнаяИнформация

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// Локализация

// СтандартныеПодсистемы.РаботаСКонтрагентами
#Область РаботаСКонтрагентамиБИП_СлужебныеПроцедурыИФункции

// Локализация
&НаКлиенте
Процедура Подключаемый_ПоказатьПредложениеИспользоватьПроверкуКонтрагентов()
	ПроверкаКонтрагентовКлиент.ПредложитьВключитьПроверкуКонтрагентов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьРезультатПроверкиКонтрагентов()
	ПроверкаКонтрагентовКлиент.ОбработатьРезультатПроверкиКонтрагентовВСправочнике(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыПоВходящимПараметрам(Параметры)
	
	Если Параметры.Свойство("ТекстЗаполнения") Тогда
		ЗаполнитьРеквизитыПоТекстуЗаполнения(Параметры.ТекстЗаполнения);
	КонецЕсли;
	
	Если Параметры.Свойство("ЗначенияЗаполнения") И Не ЗначениеЗаполнено(Объект.Наименование) Тогда
		// если по ИНН не удалось
		ЗаполнитьЗначенияСвойств(Объект, Параметры.ЗначенияЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыПоТекстуЗаполнения(ТекстЗаполнения)

	Если ЮрлицаКлиентСервер.ЭтоИНН(ТекстЗаполнения) Тогда 
	
		Объект.Наименование = "";
		Объект.ИНН = ТекстЗаполнения;
		Объект.ЮрФизЛицо = ?(СтрДлина(ТекстЗаполнения) = 10,
			Перечисления.ЮрФизЛицо.ЮрЛицо,
			Перечисления.ЮрФизЛицо.ФизЛицо);
			
		ЗаполнитьРеквизитыПоИНННаСервере(ТекстЗаполнения);
		ТекстЗаполнения = Неопределено;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДаннымЕдиныхГосРеестровНаКлиенте(СтрокаПоиска, Знач ЗаполнениеПоИНН = Неопределено)

	Если ПустаяСтрока(СтрокаПоиска) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтключитьАвтоЗаполнениеРеквизитов <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОтключитьАвтоЗаполнениеРеквизитов = Истина;
	ПодключитьОбработчикОжидания("Подключаемый_ВключитьАвтоЗаполнениеРеквизитов", 0.1, Истина);
	
	СтрокаПоиска = СокрЛП(СтрокаПоиска);
	ПроверятьИНН = ЗаполнениеПоИНН <> Истина;
	
	Если ЗаполнениеПоИНН = Неопределено Тогда
		ЗаполнениеПоИНН = ЮрлицаКлиентСервер.ЭтоИНН(СтрокаПоиска);
	КонецЕсли;
	
	Если (ПроверятьИНН И ЗначениеЗаполнено(Объект.ИНН))
		Или ЗначениеЗаполнено(Объект.КПП)
		Или ЗначениеЗаполнено(Объект.Наименование) 
		Или ЗначениеЗаполнено(Объект.НаименованиеПолное) Тогда
		
		ДопПараметры = Новый Структура("ЗаполнениеПоИНН, СтрокаПоиска", ЗаполнениеПоИНН, СтрокаПоиска);
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьПоДаннымЕдиныхГосРеестровЗавершение", 
			ЭтотОбъект, ДопПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ЮрлицаКлиент.ТекстВопроса_Перезаполнить(), РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		Если ЗаполнениеПоИНН Тогда
			ЗаполнитьРеквизитыПоИНННаКлиенте(СтрокаПоиска);
			ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентаВСправочнике(ЭтотОбъект);
		Иначе 
			ЗаполнитьРеквизитыПоНаименованиюНаКлиенте(СтрокаПоиска);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДаннымЕдиныхГосРеестровЗавершение(Ответ, ДопПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		КонтактныеЛица.Очистить();
		ДанныеКонтактныхЛиц.Очистить();
		Если ДопПараметры.ЗаполнениеПоИНН Тогда
			ЗаполнитьРеквизитыПоИНННаКлиенте(ДопПараметры.СтрокаПоиска);
		Иначе 
			ЗаполнитьРеквизитыПоНаименованиюНаКлиенте(ДопПараметры.СтрокаПоиска);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоИНННаКлиенте(СтрокаИНН)
	
	ОписаниеОшибки = "";
	ЗаполнитьРеквизитыПоИНННаСервере(СтрокаИНН, ОписаниеОшибки);
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		РаботаСКонтрагентамиКлиент.ОбработатьОшибку(ОписаниеОшибки);
	ИначеЕсли Объект.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо") Тогда
		// Проверка юридического лица по данным сервиса ИФНС после заполнения реквизитов - мог измениться КПП.
		ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентаВСправочнике(ЭтотОбъект);
		
		// ИнтернетПоддержкаПользователей.СПАРКРиски
		ИндексыСПАРКРиски = Неопределено; // Сбросить полученные значения.
		ОбновитьОтображениеИндексыСПАРК();
		// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
		
		ПоказатьОповещениеПользователя(
			ЮрлицаКлиент.ТекстОповещение_ЗаполненоПоИНН(), , , БиблиотекаКартинок.Информация32);
	Иначе
		ПоказатьОповещениеПользователя(
			ЮрлицаКлиент.ТекстОповещение_ЗаполненоПоИНН(), , , БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоНаименованиюНаКлиенте(СтрокаНаименование)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СтрокаПоиска", СтрокаНаименование);
	ДопПараметры = Новый Структура;
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьРеквизитыПоНаименованиюЗавершение", ЭтотОбъект, ДопПараметры);
	ОткрытьФорму("ОбщаяФорма.ЗаполнениеРеквизитовКонтрагента", 
		ПараметрыФормы, ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоНаименованиюЗавершение(Результат, ДопПараметры) Экспорт

	Если НЕ ЮрлицаКлиентСервер.ЭтоИНН(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРеквизитыПоИНННаКлиенте(Результат);
	ТекущийЭлемент = Элементы.Наименование;

КонецПроцедуры 

&НаКлиенте
Процедура Подключаемый_ВключитьАвтоЗаполнениеРеквизитов()

	ОтключитьАвтоЗаполнениеРеквизитов = Неопределено;	

КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьРеквизитыПоИНННаСервере(СтрокаИНН, ОписаниеОшибки = "")
	
	ЭтоЮридическоеЛицо = (Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо);
	Если ЭтоЮридическоеЛицо И СтрДлина(СтрокаИНН) = 12 Тогда
		ОписаниеОшибки = НСтр(
			"ru='Длина ИНН не соответствует виду контрагента ""Юридическое лицо"". Измените вид контрагента или длину ИНН.'");
		Возврат;
	КонецЕсли;
	
	РеквизитыКонтрагента = Неопределено;
	Если ЭтоЮридическоеЛицо Тогда
		СведенияОЮрлице = РаботаСКонтрагентами.СведенияОЮридическомЛицеПоИНН(СтрокаИНН);
		РеквизитыКонтрагента = СведенияОЮрлице.ЕГРЮЛ;
	Иначе
		РеквизитыКонтрагента = РаботаСКонтрагентами.РеквизитыПредпринимателяПоИНН(СтрокаИНН);
	КонецЕсли;
	
	Если РеквизитыКонтрагента = Неопределено Тогда
		Если ЗначениеЗаполнено(СведенияОЮрлице.ОписаниеОшибки) Тогда
			ОписаниеОшибки = СведенияОЮрлице.ОписаниеОшибки;
		Иначе
			ОписаниеОшибки = НСтр("ru='Ошибка получения данных контрагента. Возможно, указан ошибочный ИНН.'");
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыКонтрагента.ОписаниеОшибки) Тогда
		ОписаниеОшибки = РеквизитыКонтрагента.ОписаниеОшибки;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект, РеквизитыКонтрагента);
	Объект.НаименованиеПолное = РеквизитыКонтрагента.НаименованиеСокращенное;
	
	Юрлица.ЗаполнитьИсториюВФормеИзЕГРЮЛ(ЭтотОбъект, РеквизитыКонтрагента);
	
	НаименованияАвтозаполнения.Очистить();
	НаименованияАвтозаполнения.Добавить(РеквизитыКонтрагента.НаименованиеПолное);
	НаименованияАвтозаполнения.Добавить(РеквизитыКонтрагента.НаименованиеСокращенное);
	
	Если ЭтоЮридическоеЛицо Тогда
		ЗаполнитьЭлементКонтактнойИнформации(
			Справочники.ВидыКонтактнойИнформации.ЮридическийАдресКонтрагента, РеквизитыКонтрагента.ЮридическийАдрес);
		
		Если РеквизитыКонтрагента.ДанныеРуководителей.Руководители.Количество() <> 0 Тогда
			
			Если Не Объект.Ссылка.Пустая() Тогда 
				ЗаполнитьСписокКонтактныхЛиц();
				ТаблицаКонтактныеЛица = ПолучитьКонтактныеЛица(
					Объект.Ссылка, ПоказыватьНеДействительныеКонтактныеЛица);
				КонтактныеЛица.Очистить();
				
				Для Каждого КонтактноеЛицо Из ТаблицаКонтактныеЛица Цикл
					НоваяСтрока = КонтактныеЛица.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, КонтактноеЛицо);
				КонецЦикла;
			КонецЕсли;
			
			Для Каждого СтрокаРуководитель Из РеквизитыКонтрагента.ДанныеРуководителей.Руководители Цикл
				
				Отбор = Новый Структура("Наименование", СтрокаРуководитель.Представление);
				Строки = КонтактныеЛица.НайтиСтроки(Отбор);
				Если Строки.Количество() = 0 Тогда 
					УстановитьПривилегированныйРежим(Истина);
					Должность = Справочники.ДолжностиКонтактныхЛиц.НайтиСоздатьДолжность(СтрокаРуководитель.Должность);
					УстановитьПривилегированныйРежим(Ложь);
					
					НоваяСтрока = КонтактныеЛица.Добавить();
					НоваяСтрока.Наименование = СтрокаРуководитель.Представление;
					НоваяСтрока.Должность = Должность;
					
					ДанныеКонтактногоЛица = Новый Структура("Наименование, Должность");
					ЗаполнитьЗначенияСвойств(ДанныеКонтактногоЛица, НоваяСтрока);
					ДанныеКонтактныхЛиц.Добавить(ДанныеКонтактногоЛица);
					
				КонецЕсли;
				
			КонецЦикла;
			
			КоличествоКонтактныхЛиц = КонтактныеЛица.Количество();
		КонецЕсли;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЭлементКонтактнойИнформации(ВидКонтактнойИнформации, СтруктураДанных)
	
	Если СтруктураДанных = Неопределено 
		ИЛИ НЕ ЗначениеЗаполнено(СтруктураДанных.Представление) Тогда
		Возврат;
	КонецЕсли;
	
	Отбор  = Новый Структура("Вид", ВидКонтактнойИнформации);
	Строки = ЭтотОбъект.КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
	ДанныеСтроки = ?(Строки.Количество() = 0, Неопределено, Строки[0]);
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ДанныеСтроки.Представление = СтруктураДанных.Представление;
	ДанныеСтроки.Значение = СтруктураДанных.КонтактнаяИнформация;
	ЭтотОбъект[ДанныеСтроки.ИмяРеквизита] = СтруктураДанных.Представление;
	
КонецПроцедуры
// Локализация Конец

#КонецОбласти
// Конец СтандартныеПодсистемы.РаботаСКонтрагентами

// Локализация
// Начало СтандартныеПодсистемы.ИндексыСПАРКРиски
&НаКлиенте
Процедура ДекорацияИндексыСПАРКРискиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	СПАРКРискиКлиент.ОбработкаНавигационнойСсылки(ЭтотОбъект, Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);

КонецПроцедуры

// Обновляет отображение индексов СПАРК Риски.
//
// Параметры:
//  Нет.
//
&НаКлиенте
Процедура Подключаемый_ОбновитьОтображениеИндексыСПАРК()

	ОбновитьОтображениеИндексыСПАРК();

КонецПроцедуры

// Обновляет отображение индексов СПАРК Риски.
&НаКлиенте
Процедура ОбновитьОтображениеИндексыСПАРК()

	ПараметрыОтображения = Новый Структура("ВариантОтображения", "Многострочный");
	СПАРКРискиКлиент.ОтобразитьИндексыСПАРК(
		ИндексыСПАРКРиски,
		Объект,
		Объект.ИНН, // Искать по ИНН
		ОбщегоНазначенияДокументооборотКлиентСервер.ВидКонтрагентаСпаркПоВидуКонтрагента(Объект.ЮрФизЛицо),
		ЭтотОбъект,
		ПараметрыОтображения);

КонецПроцедуры

// Конец СтандартныеПодсистемы.ИндексыСПАРКРиски
// Локализация Конец

&НаКлиенте
Процедура ОбработатьСобытиеЗаписиЗадачи(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия <> "Запись_Задача" Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьЗадачиПоПриложению();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗадачиПоПриложению()
	
	ЗаголовокКомандыЗадачиПоПриложению = РаботаСЗадачами.ЗаголовокКомандыЗадачиПоПриложению(Объект.Ссылка);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЗадачиПоПриложению",
		"Заголовок",
		ЗаголовокКомандыЗадачиПоПриложению);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЗадачиПоПриложению",
		"Видимость",
		ЗначениеЗаполнено(ЗаголовокКомандыЗадачиПоПриложению));
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормыПодМобильныйКлиентПриНеобходимости()
	
	Если Не ПараметрыСеанса.ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	//Настрока самой формы
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Половинный;
	Элементы.ГруппаРеквизитыИФото.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно;
	Элементы.ИННЮрЛицо.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
		
	//Сначала отображаем фото, если оно есть - иначе оно уходит вниз и на мобильном можно не увидеть.
	Элементы.Переместить(Элементы.ГруппаФотоИСпарк, Элементы.ГруппаРеквизитыИФото);
	Элементы.Переместить(Элементы.ГруппаРеквизиты, Элементы.ГруппаРеквизитыИФото);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЭлементыФормыОсновногоКонтактногоЛица(СтрокаКонтактногоЛица)
	
	Если СтрокаКонтактногоЛица = Неопределено Или Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаКонтактногоЛица.ОсновноеКонтактноеЛицо = (ОсновноеКонтактноеЛицо = СтрокаКонтактногоЛица.Ссылка);
	Элементы.ОсновноеКонтактноеЛицо.Пометка = (СтрокаКонтактногоЛица.ОсновноеКонтактноеЛицо = Истина);
	
КонецПроцедуры

// Локализация
// ЭлектронноеВзаимодействие.ОбменСКонтрагентами

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКомандыЭДО()
	
	ОбменСКонтрагентамиКлиент.ОбновитьКоманды(ЭтотОбъект, Объект);
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
// Локализация Конец

&НаСервереБезКонтекста
Функция СформироватьДанныеВыбораРодителя(Текст)
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Если Не ЗначениеЗаполнено(Текст) Тогда
		Возврат ДанныеВыбора;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 10
		|	Контрагенты.Ссылка,
		|	Контрагенты.Представление
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ПометкаУдаления = ЛОЖЬ
		|	И Контрагенты.Наименование ПОДОБНО &Текст
		|	И Контрагенты.ЭтоГруппа = ИСТИНА";
	
	Запрос.УстановитьПараметр("Текст", "%" + Текст + "%");
	ОбъектМетаданных = Метаданные.Справочники.Контрагенты;
	Если Не МультиязычностьПовтИсп.КонфигурацияИспользуетТолькоОдинЯзык(
		ОбъектМетаданных.ТабличныеЧасти.Найти("Представления") = Неопределено) Тогда
		
		Если МультиязычностьСервер.ИспользуетсяДополнительныйЯзык(1) Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст,
				"Контрагенты.Наименование ПОДОБНО &Текст",
				"(Контрагенты.Наименование ПОДОБНО &Текст ИЛИ Контрагенты.НаименованиеЯзык1 ПОДОБНО &Текст)");
		КонецЕсли;
		
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка, Выборка.Представление);
	КонецЦикла;
	
	Возврат ДанныеВыбора;
	
КонецФункции

#КонецОбласти
