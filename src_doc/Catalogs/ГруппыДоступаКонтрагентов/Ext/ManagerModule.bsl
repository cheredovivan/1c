#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ПраваДоступа

// Возвращает наименования реквизитов, необходимых для определения прав доступа.
//
Функция ПолучитьПоляДоступа() Экспорт
	
	Возврат "Ссылка, Ответственный, Доступ";
	
КонецФункции

// Заполняет переданный дескриптор доступа
Процедура ЗаполнитьОсновнойДескриптор(ОбъектДоступа, ДескрипторДоступа) Экспорт
	
	Строка = ДескрипторДоступа.Контрагенты.Добавить();
	Строка.ГруппаДоступа = ОбъектДоступа.Ссылка;
	
КонецПроцедуры

// Проверяет наличие метода.
// 
Функция ЕстьМетодЗаполнитьДескрипторыОбъекта() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Заполняет переданную таблицу дескрипторов объекта.
// 
Процедура ЗаполнитьДескрипторыОбъекта(ОбъектДоступа, ТаблицаДескрипторов, ПротоколРасчетаПрав = Неопределено) Экспорт
	
	// Если по каким-то предметам даны права по разрезу ГДК, на нее должны быть права.  
	ДокументооборотПраваДоступа.ЗаполнитьДескрипторОбъектаОсновной(ОбъектДоступа, ТаблицаДескрипторов);
	
	ДокументооборотПраваДоступа.ДобавитьИндивидуальныйДескриптор(
		ОбъектДоступа, ТаблицаДескрипторов, ОбъектДоступа.Ответственный, Истина);
	
	ТаблицаДоступа = ОбъектДоступа.Доступ.Выгрузить();
	Для Каждого СтрДоступа Из ТаблицаДоступа Цикл
		ДокументооборотПраваДоступа.ДобавитьИндивидуальныйДескриптор(
			ОбъектДоступа, ТаблицаДескрипторов, СтрДоступа.Участник, Истина);
	КонецЦикла;
	
	Если ПротоколРасчетаПрав <> Неопределено Тогда
		ЗаписьПротокола = Новый Структура("Элемент, Описание",
			ОбъектДоступа.Ответственный, НСтр("ru = 'Ответственный'"));
		ПротоколРасчетаПрав.Добавить(ЗаписьПротокола);
		ЗаписьПротокола = Новый Структура("Элемент, Описание",
			ОбъектДоступа.Ссылка, НСтр("ru = 'Сотрудники на закладке ""Доступ""'"));
		ПротоколРасчетаПрав.Добавить(ЗаписьПротокола);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает признак того, что менеджер содержит метод ЗапросДляРасчетаПрав()
// 
Функция ЕстьМетодЗапросДляРасчетаПрав() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает запрос для расчета прав доступа по дескрипторам объекта
// 
// Параметры:
//  
//  Дескрипторы - Массив - массив дескрипторов, чьи права нужно рассчитать
//  ИдОбъекта - Ссылка - идентификатор объекта метаданных, назначенный переданным дескрипторам
//  МенеджерОбъектаДоступа - СправочникМенеджер, ДокументМенеджер - менеджер объекта доступа
// 
// Возвращаемое значение - Запрос - запрос, который выберет права доступа для переданного массива дескрипторов
// 
Функция ЗапросДляРасчетаПрав(Дескрипторы, ИдОбъекта, МенеджерОбъектаДоступа) Экспорт
	
	Запрос = Справочники.ДескрипторыДоступаОбъектов.ЗапросДляСтандартногоРасчетаПрав(
		Дескрипторы, ИдОбъекта, МенеджерОбъектаДоступа, Ложь, Истина);
	Запрос.Текст = ДокументооборотПраваДоступаПовтИсп.ТекстЗапросаДляРасчетаПравРазрезаДоступа();
	
	Возврат Запрос;
	
КонецФункции

// Заполняет протокол расчета прав дескрипторов
// 
// Параметры:
//  
//  ПротоколРасчетаПрав - Массив - протокол для заполнения
//  ЗапросПоПравам - Запрос - запрос, который использовался для расчета прав дескрипторов
//  Дескрипторы - Массив - массив дескрипторов, чьи права были рассчитаны
//  
Процедура ЗаполнитьПротоколРасчетаПрав(ПротоколРасчетаПрав, ЗапросПоПравам) Экспорт
	
	Справочники.ДескрипторыДоступаОбъектов.ЗаполнитьПротоколРасчетаПравСтандартно(
		ПротоколРасчетаПрав, ЗапросПоПравам);
	
КонецПроцедуры

#КонецОбласти // ПраваДоступа

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает порцию объектов с запрещенными участниками доступа, либо количество таких участников.
//
// Параметры:
//  ТолькоКоличество - Булево - если Истина, то возвращается количество объектов.
//  ГруппаОтбор - СправочникСсылка.ГруппыДоступаКонтрагентов, Неопределено - группа для отбора.
//
// Возвращаемое значение:
//  Число - Количество объектов с запрещенными участниками доступа.
//  Массив Из СправочникСсылка.Контрагенты - Порция таких объектов.
//
Функция ОбъектыСЗапрещеннымиУчастникамиДоступа(ТолькоКоличество = Ложь, ГруппаОтбор = Неопределено) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000 РАЗРЕШЕННЫЕ
		|	КонтрагентыДокумента.Ссылка КАК Объект
		|ИЗ
		|	Справочник.ДокументыПредприятия.Контрагенты КАК КонтрагентыДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК КонтрагентыСпр
		|		ПО КонтрагентыДокумента.Контрагент = КонтрагентыСпр.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РабочиеГруппы КАК РабочиеГруппы
		|		ПО КонтрагентыДокумента.Ссылка = РабочиеГруппы.Объект
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиУчастников
		|		ПО (РабочиеГруппы.Участник = СотрудникиУчастников.Контейнер)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторам
		|			ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторам.Дескриптор
		|		ПО (ДескрипторыДляОбъектов.Объект = КонтрагентыСпр.ГруппаДоступа)
		|		И (ПраваПоДескрипторам.Сотрудник = СотрудникиУчастников.Сотрудник)
		|ГДЕ
		|	ДескрипторыДляОбъектов.Объект ЕСТЬ NULL
		|	И НЕ СотрудникиУчастников.Сотрудник.ПометкаУдаления
		|	И КонтрагентыСпр.ГруппаДоступа = &ГруппаДоступа");
	
	Если ТолькоКоличество Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РАЗЛИЧНЫЕ ПЕРВЫЕ 1000", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"КонтрагентыДокумента.Ссылка КАК Объект",
			"КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КонтрагентыДокумента.Ссылка) КАК КолОбъектов");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ГруппаОтбор) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"КонтрагентыСпр.ГруппаДоступа = &ГруппаДоступа",
			"КонтрагентыСпр.ГруппаДоступа <> ЗНАЧЕНИЕ(Справочник.ГруппыДоступаКонтрагентов.ПустаяСсылка)");
	КонецЕсли;
	Запрос.УстановитьПараметр("ГруппаДоступа", ГруппаОтбор);
	
	Если ТолькоКоличество Тогда
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		Возврат Выборка.КолОбъектов;
	Иначе
		Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Объект");
	КонецЕсли;
	
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#КонецЕсли
