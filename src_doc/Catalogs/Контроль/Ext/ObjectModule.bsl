#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
//Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда 
		
		Автор = Сотрудники.ОсновнойСотрудник();
		
		ПоставленНаКонтроль = Истина;
		ДатаПостановкиНаКонтроль = ТекущаяДатаСеанса();
		
		Если Не ЗначениеЗаполнено(Контролер) Тогда 
			Контролер = Автор;
		КонецЕсли;
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Файлы") Тогда 
			
			Предмет = ДанныеЗаполнения;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения));
			НовыйИсполнитель = Исполнители.Добавить();
			НовыйИсполнитель.Исполнитель = Сотрудники.ОсновнойСотрудникПользователя(
				ДанныеЗаполнения.Автор);
		
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда 
			
			Предмет = ДанныеЗаполнения;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения));
			
			ДанныеПредмета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, 
				"Подготовил, Ответственный, СрокИсполнения, ДатаОкончанияДействия, Адресат");
			Если ЗначениеЗаполнено(ДанныеПредмета.Ответственный) Тогда
				
				Ответственный = Сотрудники.ОсновнойСотрудникПользователя(
					ДанныеПредмета.Ответственный);
				Если ЗначениеЗаполнено(Ответственный) Тогда
					НовыйИсполнитель = Исполнители.Добавить();
					НовыйИсполнитель.Исполнитель = Ответственный;
				КонецЕсли;
				
			ИначеЕсли ЗначениеЗаполнено(ДанныеПредмета.Подготовил) Тогда
				Подготовил = Сотрудники.ОсновнойСотрудникПользователя(
					ДанныеПредмета.Подготовил);
				Если ЗначениеЗаполнено(Подготовил) Тогда
					НовыйИсполнитель = Исполнители.Добавить();
					НовыйИсполнитель.Исполнитель = Подготовил;
				КонецЕсли;
			КонецЕсли;	
			Если ЗначениеЗаполнено(ДанныеПредмета.СрокИсполнения) Тогда 
				СрокИсполнения = ДанныеПредмета.СрокИсполнения;
			ИначеЕсли ЗначениеЗаполнено(ДанныеПредмета.ДатаОкончанияДействия) Тогда 
				СрокИсполнения = ДанныеПредмета.ДатаОкончанияДействия;
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(ДанныеПредмета.Адресат) Тогда
				
				Адресат = Сотрудники.ОсновнойСотрудникПользователя(
					ДанныеПредмета.Адресат);
				Если ЗначениеЗаполнено(Адресат) Тогда
					НовыйИсполнитель = Исполнители.Добавить();
					НовыйИсполнитель.Исполнитель = Адресат;
				КонецЕсли;
			КонецЕсли;
		
		ИначеЕсли ДействияКлиентСервер.ЭтоДействие(ДанныеЗаполнения) Тогда
			
			Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ДействияОзнакомления") Тогда
				ДействияСервер.ЗаполнитьКонтрольнуюКарточкуПоДействиюОзнакомления(ЭтотОбъект, ДанныеЗаполнения);
			Иначе
				ДействияСервер.ЗаполнитьКонтрольнуюКарточкуПоДействию(ЭтотОбъект, ДанныеЗаполнения);
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.Исполнение") Тогда 
			
			Предмет = ДанныеЗаполнения;
			ДанныеПредмета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, 
				"Наименование, Описание, СрокИсполненияПроцесса, НомерИтерации,
				|Проверяющий, Контролер");
			
			Описание = СформироватьОписание(ДанныеЗаполнения, ДанныеПредмета.Наименование, ДанныеПредмета.Описание);
			СрокИсполнения = ДанныеПредмета.СрокИсполненияПроцесса;
			
			Для Каждого Исполнитель Из ДанныеЗаполнения.Исполнители Цикл 
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = Исполнитель.Исполнитель;
				НовыйИсполнитель.Ответственный = Исполнитель.Ответственный;
				НовыйИсполнитель.Источник = Исполнитель.ЗадачаИсполнителя;
			КонецЦикла;
			
			Если ЗначениеЗаполнено(ДанныеПредмета.Проверяющий) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Проверяющий;
				
				ПараметрыОтбора = Новый Структура("НомерИтерации", ДанныеПредмета.НомерИтерации);
				НайденныеСтроки = ДанныеЗаполнения.РезультатыПроверки.НайтиСтроки(ПараметрыОтбора);
				Если НайденныеСтроки.Количество() > 0 Тогда 
					НовыйИсполнитель.Источник = НайденныеСтроки[0].ЗадачаПроверяющего;
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.Ознакомление") Тогда 
			
			Предмет = ДанныеЗаполнения;
			
			ДанныеПредмета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, 
				"Наименование, Описание, СрокИсполненияПроцесса");
			
			Описание = СформироватьОписание(ДанныеЗаполнения, ДанныеПредмета.Наименование, ДанныеПредмета.Описание);
			СрокИсполнения = ДанныеПредмета.СрокИсполненияПроцесса;
			
			Для Каждого Исполнитель Из ДанныеЗаполнения.Исполнители Цикл 
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = Исполнитель.Исполнитель;
				НовыйИсполнитель.Источник = Исполнитель.ЗадачаИсполнителя;
			КонецЦикла;
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.Утверждение")
			Или  ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.Рассмотрение")
			Или ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.Регистрация") Тогда 
			
			Предмет = ДанныеЗаполнения;
			ДанныеПредмета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, 
				"Наименование, Описание, СрокИсполненияПроцесса, СрокИсполнения, Автор,
				|Исполнитель");
			
			Описание = СформироватьОписание(ДанныеЗаполнения, ДанныеПредмета.Наименование, ДанныеПредмета.Описание);
			Если ЗначениеЗаполнено(ДанныеПредмета.СрокИсполнения) Тогда
				СрокИсполнения = ДанныеПредмета.СрокИсполнения;
			Иначе
				СрокИсполнения = ДанныеПредмета.СрокИсполненияПроцесса;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеПредмета.Исполнитель) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Исполнитель;
				
				Если ЗначениеЗаполнено(ДанныеПредмета.Автор) Тогда 
					НовыйИсполнитель.Ответственный = Истина;
				КонецЕсли;
			КонецЕсли;

			Если ЗначениеЗаполнено(ДанныеПредмета.Автор) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Автор;
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.Приглашение") Тогда 
			
			Предмет = ДанныеЗаполнения;
			ДанныеПредмета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, 
				"Наименование, Описание, СрокИсполненияПроцесса, Автор");
			
			Описание = СформироватьОписание(ДанныеЗаполнения, ДанныеПредмета.Наименование, ДанныеПредмета.Описание);
			СрокИсполнения = ДанныеПредмета.СрокИсполненияПроцесса;
			ИсполнителиПредмета = ДанныеЗаполнения.Исполнители;
			
			Для Каждого Исполнитель Из ИсполнителиПредмета Цикл 
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = Исполнитель.Исполнитель;
				НовыйИсполнитель.Источник = Исполнитель.ЗадачаИсполнителя;
			КонецЦикла;

			Если ЗначениеЗаполнено(ДанныеПредмета.Автор) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Автор;
			КонецЕсли;	
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.Согласование") Тогда 
			
			Предмет = ДанныеЗаполнения;
			ДанныеПредмета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, 
				"Наименование, Описание, СрокИсполненияПроцесса, Автор");
			
			Описание = СформироватьОписание(ДанныеЗаполнения, ДанныеПредмета.Наименование, ДанныеПредмета.Описание);
			СрокИсполнения = ДанныеПредмета.СрокИсполненияПроцесса;
			ИсполнителиПредмета = ДанныеЗаполнения.Исполнители;
			
			Для Каждого Исполнитель Из ИсполнителиПредмета Цикл 
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = 
					Сотрудники.ОсновнойСотрудникПользователя(Исполнитель.Исполнитель);
				НовыйИсполнитель.Источник = Исполнитель.ЗадачаИсполнителя;
			КонецЦикла;

			Если ЗначениеЗаполнено(ДанныеПредмета.Автор) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Автор;
			КонецЕсли;	
		
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.Подписание") Тогда
			
			Предмет = ДанныеЗаполнения;
			ДанныеПредмета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, 
				"Наименование, Описание, СрокИсполненияПроцесса, Автор, НомерИтерации");
			
			Описание = СформироватьОписание(ДанныеЗаполнения, ДанныеПредмета.Наименование, ДанныеПредмета.Описание);
			СрокИсполнения = ДанныеПредмета.СрокИсполненияПроцесса;
			ИсполнителиПредмета = ДанныеЗаполнения.Участники;
			
			Для Каждого Исполнитель Из ИсполнителиПредмета Цикл
				
				ТекУчастник = Исполнитель.Подписывающий;
				Если Не ЗначениеЗаполнено(ТекУчастник) Тогда
					ТекУчастник = Исполнитель.Участник;
				КонецЕсли;
				
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = 
					Сотрудники.ОсновнойСотрудникПользователя(
						ТекУчастник);
				
				Если Исполнитель.ТочкаМаршрута = БизнесПроцессы.Подписание.ТочкиМаршрута.ОбработатьРезультат Тогда
					
					СтруктураОтбора = Новый Структура("НомерИтерации",	
						ДанныеПредмета.НомерИтерации);
							
					СтрокиНайдены = ДанныеЗаполнения.РезультатыОбработок.НайтиСтроки(СтруктураОтбора);
				Иначе
					
					СтруктураОтбора = Новый Структура("Идентификатор, НомерИтерации",	
						Исполнитель.Идентификатор, ДанныеПредмета.НомерИтерации);
					
					СтрокиНайдены = ДанныеЗаполнения.РезультатыПодписания.НайтиСтроки(СтруктураОтбора);
				КонецЕсли;		
				
				Если СтрокиНайдены.Количество() = 1 Тогда	
					НовыйИсполнитель.Источник = СтрокиНайдены[0].ЗадачаПроцесса;
				КонецЕсли;
				
			КонецЦикла;

			Если ЗначениеЗаполнено(ДанныеПредмета.Автор) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Автор;
			КонецЕсли;	
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.КомплексныйПроцесс") Тогда 
			
			Предмет = ДанныеЗаполнения;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения));
			СрокИсполнения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "СрокИсполненияПроцесса");
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	ДочерниеБизнесПроцессы.ДочернийПроцесс
				|ПОМЕСТИТЬ ДочерниеПроцессы
				|ИЗ
				|	РегистрСведений.ДочерниеБизнесПроцессы КАК ДочерниеБизнесПроцессы
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеБизнесПроцессов КАК ДанныеБизнесПроцессов
				|		ПО ДочерниеБизнесПроцессы.ДочернийПроцесс = ДанныеБизнесПроцессов.БизнесПроцесс
				|			И ДочерниеБизнесПроцессы.СвязующаяЗадача = ДанныеБизнесПроцессов.ВедущаяЗадача
				|ГДЕ
				|	ДочерниеБизнесПроцессы.РодительскийПроцесс = &БизнесПроцесс
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ЗадачаИсполнителя.Ссылка,
				|	ЗадачаИсполнителя.ТочкаМаршрута,
				|	ЗадачаИсполнителя.БизнесПроцесс,
				|	ЗадачаИсполнителя.Исполнитель,
				|	ЗадачаИсполнителя.РольИсполнителя,
				|	ЗадачаИсполнителя.ДатаИсполнения,
				|	ЗадачаИсполнителя.Выполнена
				|ИЗ
				|	ДочерниеПроцессы КАК ДочерниеПроцессы
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
				|		ПО ДочерниеПроцессы.ДочернийПроцесс = ЗадачаИсполнителя.БизнесПроцесс
				|ГДЕ
				|	ЗадачаИсполнителя.ПометкаУдаления = ЛОЖЬ";
			
			Запрос.УстановитьПараметр("БизнесПроцесс", Предмет);
			ЗадачиПроцесса = Запрос.Выполнить().Выгрузить();
			
			Для Каждого СтрокаЗадача Из ЗадачиПроцесса Цикл
				БизнесПроцесс = СтрокаЗадача.БизнесПроцесс;
				
				Если СтрокаЗадача.ТочкаМаршрута = БизнесПроцессы.Согласование.ТочкиМаршрута.Согласовать Тогда 
					
					Отбор = Новый Структура("НомерИтерации, ЗадачаИсполнителя", БизнесПроцесс.НомерИтерации, СтрокаЗадача.Ссылка);
					Если БизнесПроцесс.РезультатыСогласования.НайтиСтроки(Отбор).Количество() = 0 Тогда 
						Продолжить;
					КонецЕсли;
					
				ИначеЕсли СтрокаЗадача.ТочкаМаршрута = БизнесПроцессы.Исполнение.ТочкиМаршрута.Исполнить
					  Или СтрокаЗадача.ТочкаМаршрута = БизнесПроцессы.Исполнение.ТочкиМаршрута.ОтветственноеИсполнение Тогда 
					
					Отбор = Новый Структура("НомерИтерации, ЗадачаИсполнителя", БизнесПроцесс.НомерИтерации, СтрокаЗадача.Ссылка);
					Если БизнесПроцесс.РезультатыИсполнения.НайтиСтроки(Отбор).Количество() = 0 Тогда 
						Продолжить;
					КонецЕсли;
					
				КонецЕсли;	
				
				НоваяСтрока = Исполнители.Добавить();
				НоваяСтрока.Источник = СтрокаЗадача.Ссылка;
				НоваяСтрока.Исполнено = СтрокаЗадача.Выполнена;
				НоваяСтрока.ДатаИсполнения = СтрокаЗадача.ДатаИсполнения;
				Если ЗначениеЗаполнено(СтрокаЗадача.Исполнитель) Тогда 
					НоваяСтрока.Исполнитель = СтрокаЗадача.Исполнитель;
				Иначе
					НоваяСтрока.Исполнитель = СтрокаЗадача.РольИсполнителя;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ВеткиПереписки") Тогда 
			
			Предмет = ДанныеЗаполнения;
			РеквизитыПисьма = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения,
			 	"КорневоеПисьмо.Тема, КорневоеПисьмо");
			 
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(РеквизитыПисьма.КорневоеПисьмоТема));
				
			КорневоеПисьмо = РеквизитыПисьма.КорневоеПисьмо;
			Если ТипЗнч(КорневоеПисьмо) = Тип("ДокументСсылка.ИсходящееПисьмо") Тогда
				Для Каждого Строка Из КорневоеПисьмо.ПолучателиПисьма Цикл 
					НовыйИсполнитель = Исполнители.Добавить();
					НовыйИсполнитель.Исполнитель = 
						Сотрудники.ОсновнойСотрудникПользователя(Строка.Адресат);
					НовыйИсполнитель.Источник = КорневоеПисьмо;
				КонецЦикла;
			Иначе 
				КорневоеПисьмоОтправительАдресат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения,
					"КорневоеПисьмо.ОтправительАдресат");
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = 
					Сотрудники.ОсновнойСотрудникПользователя(КорневоеПисьмоОтправительАдресат);
				НовыйИсполнитель.Источник = КорневоеПисьмо;
			КонецЕсли; 
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВходящееПисьмо") Тогда 
			
			ВеткаПереписки = РегистрыСведений.ПисьмаВеток.ПолучитьВетку(ДанныеЗаполнения);
			Предмет = ВеткаПереписки;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения.Тема));
				
			НовыйИсполнитель = Исполнители.Добавить();
			НовыйИсполнитель.Исполнитель = 
				Сотрудники.ОсновнойСотрудникПользователя(ДанныеЗаполнения.ОтправительАдресат);	
			НовыйИсполнитель.Источник = ДанныеЗаполнения.Ссылка;
				
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ИсходящееПисьмо") Тогда 	
			
			ВеткаПереписки = РегистрыСведений.ПисьмаВеток.ПолучитьВетку(ДанныеЗаполнения);
			Предмет = ВеткаПереписки;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения.Тема));
				
			Для Каждого Строка Из ДанныеЗаполнения.ПолучателиПисьма Цикл 
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = 
					Сотрудники.ОсновнойСотрудникПользователя(Строка.Адресат);
				НовыйИсполнитель.Источник = ДанныеЗаполнения.Ссылка;
			КонецЦикла;	
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда 
			
			Предмет = ДанныеЗаполнения;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения));
				
			ДанныеПредмета = РаботаСПроектами.ПолучитьСрокиПроектнойЗадачи(ДанныеЗаполнения);
			СрокИсполнения = ДанныеПредмета.ТекущийПланОкончание;
			
			ИсполнителиПредмета = ДанныеЗаполнения.Исполнители;
			Для Каждого Исполнитель Из ИсполнителиПредмета Цикл 
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = Сотрудники.ОсновнойСотрудникПользователя(
					Исполнитель.Исполнитель);
			КонецЦикла;

		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Проекты") Тогда 
			
			Предмет = ДанныеЗаполнения;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения));
				
			ДанныеПредмета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, 
				"ТекущийПланОкончание, Руководитель");

			СрокИсполнения = ДанныеПредмета.ТекущийПланОкончание;
			
			Если ЗначениеЗаполнено(ДанныеПредмета.Руководитель) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = Сотрудники.ОсновнойСотрудникПользователя(
					ДанныеПредмета.Руководитель);
			КонецЕсли;	
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Мероприятия") Тогда 
			
			Предмет = ДанныеЗаполнения;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения));
				
			ДанныеПредмета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, 
				"ДатаОкончания, Организатор, Куратор,
				|Председатель,
				|Секретарь");

			СрокИсполнения = ДанныеПредмета.ДатаОкончания;
			
			Если ЗначениеЗаполнено(ДанныеПредмета.Председатель)
				И (ТипЗнч(ДанныеПредмета.Председатель) = Тип("СправочникСсылка.Пользователи")
					Или ТипЗнч(ДанныеПредмета.Председатель) = Тип("СправочникСсылка.Сотрудники")
					Или ТипЗнч(ДанныеПредмета.Председатель) = Тип("СправочникСсылка.ПолныеРоли"))Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = 
					Сотрудники.ОсновнойСотрудникПользователя(ДанныеПредмета.Председатель);
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(ДанныеПредмета.Секретарь)
				И (ТипЗнч(ДанныеПредмета.Секретарь) = Тип("СправочникСсылка.Пользователи")
					Или ТипЗнч(ДанныеПредмета.Секретарь) = Тип("СправочникСсылка.Сотрудники")
					Или ТипЗнч(ДанныеПредмета.Секретарь) = Тип("СправочникСсылка.ПолныеРоли"))Тогда
				
				СекретарьПользователь = 
					Сотрудники.ОсновнойСотрудникПользователя(
					ДанныеПредмета.Секретарь);
				
				НайденныеИсполнители = Исполнители.НайтиСтроки(
					Новый Структура("Исполнитель", СекретарьПользователь));
				
				Если НайденныеИсполнители.Количество() = 0 Тогда 
					НовыйИсполнитель = Исполнители.Добавить();
					НовыйИсполнитель.Исполнитель = СекретарьПользователь;
				КонецЕсли;
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(ДанныеПредмета.Организатор)
				И (ТипЗнч(ДанныеПредмета.Организатор) = Тип("СправочникСсылка.Пользователи")
					Или ТипЗнч(ДанныеПредмета.Организатор) = Тип("СправочникСсылка.Сотрудники")) Тогда
				
				Организатор = 
					Сотрудники.ОсновнойСотрудникПользователя(
					ДанныеПредмета.Организатор);
					
					НайденныеИсполнители = Исполнители.НайтиСтроки(
					Новый Структура("Исполнитель", Организатор));
				
				Если НайденныеИсполнители.Количество() = 0 Тогда 
					НовыйИсполнитель = Исполнители.Добавить();
					НовыйИсполнитель.Исполнитель = Организатор;
				КонецЕсли;
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(ДанныеПредмета.Куратор) Тогда
				
				Куратор = 
					Сотрудники.ОсновнойСотрудникПользователя(
					ДанныеПредмета.Куратор);
				НайденныеИсполнители = Исполнители.НайтиСтроки(
					Новый Структура("Исполнитель", Куратор));
				
				Если НайденныеИсполнители.Количество() = 0 Тогда 
					НовыйИсполнитель = Исполнители.Добавить();
					НовыйИсполнитель.Исполнитель = Куратор;
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") И ЗначениеЗаполнено(ДанныеЗаполнения) Тогда 
			
			Предмет = ДанныеЗаполнения;
			ДанныеПредмета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, 
				"Наименование, Описание, СрокИсполнения,
				|Исполнитель, РольИсполнителя");
			
			Описание = СформироватьОписание(ДанныеЗаполнения, ДанныеПредмета.Наименование, ДанныеПредмета.Описание);
			СрокИсполнения = ДанныеПредмета.СрокИсполнения;
			
			НовыйИсполнитель = Исполнители.Добавить();
			НовыйИсполнитель.Исполнитель = ?(ЗначениеЗаполнено(ДанныеПредмета.Исполнитель), 
				ДанныеПредмета.Исполнитель,
				ДанныеПредмета.РольИсполнителя);
			НовыйИсполнитель.Источник = Предмет;
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Задача") Тогда 
			
			Предмет = ДанныеЗаполнения;
			
			ДанныеПредмета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				ДанныеЗаполнения, 
				"Описание, Срок, Участники");
			
			УчастникиПредмета = ДанныеПредмета.Участники.Выгрузить();
			УчастникиПредмета.Сортировать("НомерСтроки Возр");
			
			Описание = СтрШаблон(
				"%1 ""%2""
				|%3",
				НСтр("ru = 'Контролировать'"),
				Строка(ДанныеЗаполнения),
				ДанныеПредмета.Описание);
			
			СрокИсполнения = ДанныеПредмета.Срок;
			
			Для Каждого СтрокаУчастника Из УчастникиПредмета Цикл 
				
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = СтрокаУчастника.Участник;
				НовыйИсполнитель.Ответственный =
					РаботаСЗадачамиПовтИсп.ЭтоОтветственный(СтрокаУчастника.ВидУчастника);
				НовыйИсполнитель.Источник = СтрокаУчастника.ДействиеУчастника;
				
			КонецЦикла;
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ДействиеЗадачи") Тогда 
			
			Предмет = ДанныеЗаполнения;
			
			ДанныеПредмета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				ДанныеЗаполнения, 
				"Задача, ЕстьОсобыйСрок, ОсобыйСрок, ОсобоеОписание, Исполнитель");
			
			ДанныеЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				ДанныеПредмета.Задача, 
				"Срок, Описание, Источник");       
				
			ПредметКонтроляПриЗаписи = Ложь;
			ДополнительныеСвойства.Свойство("ПредметКонтроляПриЗаписи", ПредметКонтроляПриЗаписи);				
			Если ТипЗнч(ДанныеЗадачи.Источник) = Тип("СправочникСсылка.ДействияОзнакомления") И ПредметКонтроляПриЗаписи = Истина Тогда
				ДействияСервер.ЗаполнитьКонтрольнуюКарточкуПоДействиюОзнакомления(ЭтотОбъект, ДанныеЗадачи.Источник);
				Возврат;
			КонецЕсли;	
			
			Описание = СтрШаблон(
				"%1 ""%2""
				|%3",
				НСтр("ru = 'Контролировать'"),
				Строка(ДанныеЗаполнения),
				?(ЗначениеЗаполнено(ДанныеПредмета.ОсобоеОписание),
					ДанныеПредмета.ОсобоеОписание,
					ДанныеЗадачи.Описание));
				
			СрокИсполнения = Документы.ДействиеЗадачи.Срок(
				ДанныеПредмета.ЕстьОсобыйСрок,
				ДанныеПредмета.ОсобыйСрок,
				ДанныеЗадачи.Срок);
			
			НовыйИсполнитель = Исполнители.Добавить();
			НовыйИсполнитель.Исполнитель = ДанныеПредмета.Исполнитель;
			НовыйИсполнитель.Источник = ДанныеЗаполнения;
		
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
			И Не ДанныеЗаполнения.Свойство("ИдентификаторВышестоящего")
			И ДанныеЗаполнения.Свойство("Действие")
			И ДействияКлиентСервер.ЭтоДействие(ДанныеЗаполнения.Действие) Тогда
			
			ДействияСервер.ЗаполнитьКонтрольнуюКарточкуПоПунктуИсполнения(ЭтотОбъект, ДанныеЗаполнения);
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
			И ДанныеЗаполнения.Свойство("ИдентификаторВышестоящего")
			И ДанныеЗаполнения.Свойство("Действие")
			И ДействияКлиентСервер.ЭтоДействие(ДанныеЗаполнения.Действие) Тогда
			
			ДействияСервер.ЗаполнитьКонтрольнуюКарточкуПоПодчиненномуИсполнению(ЭтотОбъект, ДанныеЗаполнения);
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") И ЗначениеЗаполнено(ДанныеЗаполнения) Тогда 
			
			Предмет = ДанныеЗаполнения;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения));
			
		КонецЕсли;
		
		Если Исполнители.Количество() > 1 И ЗначениеЗаполнено(Контролер) Тогда 
			КоличествоИсполнителей = Исполнители.Количество();
			Для Инд = 1 По КоличествоИсполнителей Цикл
				Строка = Исполнители[КоличествоИсполнителей - Инд];
				Если Строка.Исполнитель = Контролер Тогда 
					Исполнители.Удалить(Строка);
				КонецЕсли;	
			КонецЦикла;
		КонецЕсли;	
		
		Если Исполнители.Количество() > 1 Тогда 
			Если Не ЗначениеЗаполнено(Предмет) 
				Или (Не ОбщегоНазначения.ЭтоБизнесПроцесс(Предмет.Метаданные())
					И Не ДействияКлиентСервер.ЭтоДействие(Предмет)) Тогда 
				Если Исполнители.Найти(Истина, "Ответственный") = Неопределено Тогда 
					Исполнители[0].Ответственный = Истина;
				КонецЕсли;	
			КонецЕсли;
		Иначе
			Если Исполнители.Количество() = 1 И Исполнители[0].Ответственный Тогда
				Исполнители[0].Ответственный = Ложь;
			КонецЕсли;	
		КонецЕсли;	
		
		УстановитьПривилегированныйРежим(Истина);
		
		// постановка на контроль уже стартованного процесса
		Если ЗначениеЗаполнено(Предмет)
			И ОбщегоНазначения.ЭтоБизнесПроцесс(Предмет.Метаданные())
			И Предмет.Стартован Тогда 
			
			Если ТипЗнч(Предмет) <> Тип("БизнесПроцессСсылка.КомплексныйПроцесс") Тогда 
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ЗадачаИсполнителя.Ссылка,
				|	ЗадачаИсполнителя.Исполнитель,
				|	ЗадачаИсполнителя.РольИсполнителя,
				|	ЗадачаИсполнителя.Выполнена
				|ИЗ
				|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
				|ГДЕ
				|	ЗадачаИсполнителя.БизнесПроцесс = &БизнесПроцесс
				|	И НЕ ЗадачаИсполнителя.ПометкаУдаления
				|
				|УПОРЯДОЧИТЬ ПО
				|	ЗадачаИсполнителя.Дата УБЫВ";
				
				Запрос.УстановитьПараметр("БизнесПроцесс", Предмет);
				ЗадачиПроцесса = Запрос.Выполнить().Выгрузить();
				
				КоличествоИсполнителей = Исполнители.Количество();
				Для Инд = 1 По КоличествоИсполнителей Цикл
					Строка = Исполнители[КоличествоИсполнителей - Инд];
					Если ЗначениеЗаполнено(Строка.Источник) Тогда 
						Продолжить;
					КонецЕсли;	
					
					НайденнаяСтрока = Неопределено;
					Для Каждого СтрокаЗадача Из ЗадачиПроцесса Цикл
						Если ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.ПолныеРоли")
							И Строка.Исполнитель = СтрокаЗадача.РольИсполнителя Тогда 
							НайденнаяСтрока = СтрокаЗадача;
							Прервать;
						ИначеЕсли Строка.Исполнитель = СтрокаЗадача.Исполнитель Тогда 
							НайденнаяСтрока = СтрокаЗадача;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					
					Если НайденнаяСтрока <> Неопределено Тогда 
						Строка.Источник = НайденнаяСтрока.Ссылка;
						Строка.Исполнено = НайденнаяСтрока.Выполнена;
						ЗадачиПроцесса.Удалить(НайденнаяСтрока);
					КонецЕсли;	
				КонецЦикла;	
				
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Предмет) Тогда 
		
		// Проверка прав доступа контролера на предмет
		ВнешняяТранзакция = ТранзакцияАктивна();
		
		Если Не ВнешняяТранзакция Тогда
			НачатьТранзакцию();
		КонецЕсли;
		
		ПерезаписатьРабочуюГруппуПредмета(Предмет);
		
		// Проверка прав на предмет
		ПроверитьПраваКонтролераНаПредмет(Отказ);
		
		// Отмена транзакции, в рамках которой была создана рабочая группа для 
		// проверки прав
		Если Не ВнешняяТранзакция Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияДокументооборот.УстановитьДополнительноеСвойствоЭтоНовый(ЭтотОбъект);
	ОбщегоНазначенияДокументооборот.УстановитьДополнительноеСвойствоПредыдущиеЗначенияРеквизитов(ЭтотОбъект);
	
	// Данные формируем, если не грузятся из сторонней системы.
	Если Не ПараметрыСеанса.ЗагрузкаОбработанныхДанныхИзДругойСистемы Тогда
		ИсполнителиСтрокой = ПолучитьИсполнителейСтрокой();
	КонецЕсли;
	
	Наименование = СвернутьТекстВСтроку(Описание, 150);
	
	// проверка возможности пометки удаления 
	СтараяПометкаУдаления = Ложь;
	Если ЗначениеЗаполнено(Ссылка) Тогда 
		СтараяПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления");
	КонецЕсли;
	ДополнительныеСвойства.Вставить("СтараяПометкаУдаления", СтараяПометкаУдаления);
	
	Если ПометкаУдаления И Не СтараяПометкаУдаления Тогда 
		Если Контроль.ЭтоПроцессСКонтролером(Предмет) Тогда 
			РеквизитыПредмета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Предмет,
				"Стартован, ПометкаУдаления, Контролер");
			Если Контролер = Сотрудники.ОсновнойСотрудникПользователя(РеквизитыПредмета.Контролер)
				И РеквизитыПредмета.Стартован 
				И Не РеквизитыПредмета.ПометкаУдаления Тогда 
					ТекстСообщения = НСтр("ru = 'Нельзя пометить на удаление контрольную карточку, автоматически созданную процессом.'");
					ВызватьИсключение ТекстСообщения;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	НачальныйСнятСКонтроля = Ложь;
	Если ЗначениеЗаполнено(Ссылка) Тогда 
		НачальныйСнятСКонтроля = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "СнятСКонтроля");
	КонецЕсли;	
	Если Не НачальныйСнятСКонтроля И Не СнятСКонтроля Тогда 
		Если Исполнители.Количество() = 0 Тогда 
			ВсеПроконтролированы = Ложь;
		Иначе	
			ВсеПроконтролированы = Истина;
			Для Каждого Строка Из Исполнители Цикл
				Если Не Строка.Проконтролировано Тогда 
					ВсеПроконтролированы = Ложь;
				КонецЕсли;	
			КонецЦикла;	
		КонецЕсли;	
		Если ВсеПроконтролированы Тогда 
			СнятСКонтроля = Истина;
			ДатаСнятияСКонтроля = ТекущаяДатаСеанса();
		КонецЕсли;	
	КонецЕсли;
	
	// обновление рабочей группы предмета
	Если ЗначениеЗаполнено(Предмет) Тогда 
		ПерезаписатьРабочуюГруппуПредмета(Предмет);
	КонецЕсли;
	
	СтарыйКонтролер = Справочники.Сотрудники.ПустаяСсылка();
	Если ЗначениеЗаполнено(Ссылка) Тогда 
		СтарыйКонтролер = Сотрудники.ОсновнойСотрудникПользователя(
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Контролер"));
	КонецЕсли;
	ДополнительныеСвойства.Вставить("СтарыйКонтролер", СтарыйКонтролер);
	
	ОбновитьДатуУдаления();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// установка контролера в процессе
	СтарыйКонтролер = ДополнительныеСвойства.СтарыйКонтролер;
	СтараяПометкаУдаления = ДополнительныеСвойства.СтараяПометкаУдаления;
	УстановкаПометки = Не СтараяПометкаУдаления И ПометкаУдаления;
	
	Если Не УстановкаПометки 
		И Контроль.ЭтоПроцессСКонтролером(Предмет) Тогда
			
		Если Не ЗначениеЗаполнено(Предмет.Контролер) Или 
			(СтарыйКонтролер = Сотрудники.ОсновнойСотрудникПользователя(Предмет.Контролер) 
			И СтарыйКонтролер <> Контролер) Тогда 
			ПраваПоОбъекту = ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(Предмет); 
			Если ПраваПоОбъекту.Изменение Тогда 
				ПредметОбъект = Предмет.ПолучитьОбъект();
				ПредметОбъект.Контролер = Контролер;
				ПредметОбъект.Записать();
			КонецЕсли;
		КонецЕсли;
			
	ИначеЕсли ТипЗнч(Предмет) = Тип("СправочникСсылка.ДействияИсполнения")
		И ДополнительныеСвойства.Свойство("НеОбновлятьПредмет") = Ложь Тогда 
		
		РеквизитыДействия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Предмет, 
			"Контролер, Пункты");
		Если ЗначениеЗаполнено(ИдентификаторПункта) Тогда
			ДанныеПункта = Справочники.ДействияИсполнения.ДанныеПунктаДействия(
				Предмет, ИдентификаторПункта, РеквизитыДействия.Пункты.Выгрузить());
				
			Если Не ЗначениеЗаполнено(ДанныеПункта.Контролер) 
				Или (СтарыйКонтролер = ДанныеПункта.Контролер 
					И СтарыйКонтролер <> Контролер)  
				И ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(Предмет).Изменение Тогда 
				ПредметОбъект = Предмет.ПолучитьОбъект();
				Для Каждого СтрПункт Из ПредметОбъект.Пункты Цикл
					Если СтрПункт.Идентификатор <> ИдентификаторПункта Тогда
						Продолжить;
					КонецЕсли;
					
					СтрПункт.Контролер = Контролер;
					Прервать;
				КонецЦикла;
				ПредметОбъект.Записать();
			КонецЕсли;
		Иначе
			
			// Если контроль на подчиненном исполнении, то не записываем контролера в действие.
			НужноЗаписатьКонтролераВДействие =
				Не ЗначениеЗаполнено(ИдентификаторВышестоящегоЭтапа)
				И (Не ЗначениеЗаполнено(РеквизитыДействия.Контролер)
					Или (СтарыйКонтролер = РеквизитыДействия.Контролер
						И СтарыйКонтролер <> Контролер)
					И ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(Предмет).Изменение);
			
			Если НужноЗаписатьКонтролераВДействие Тогда
				ПредметОбъект = Предмет.ПолучитьОбъект();
				ПредметОбъект.Контролер = Контролер;
				ПредметОбъект.Записать();
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	// установка состояния контроля для списков
	УстановитьСостояниеКонтроляВКеше(Предмет);
	
	// Обновление прав на предмет контроля.
	ОбновитьПраваПредметаКонтроля(ЭтотОбъект, Предмет);
	
	// Отразим в виджетах.
	Документы.ДанныеКонтроля.ОтразитьКонтроль(Ссылка);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Для Каждого Исполнитель Из Исполнители Цикл 
		Исполнитель.Исполнено = Ложь;
	КонецЦикла;	
	
	ПоставленНаКонтроль = Истина;
	ДатаПостановкиНаКонтроль = ТекущаяДатаСеанса();
	
	СнятСКонтроля = Ложь;
	ДатаСнятияСКонтроля = '00010101';
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьОписание(Предмет, Наименование, Описание)
	
	КонтрольноеСлово = "";
		
	Если ТипЗнч(Предмет) = Тип("БизнесПроцессСсылка.Исполнение") Тогда 
		КонтрольноеСлово = НСтр("ru = 'Исполнить'");
		СловоЗамены = НСтр("ru = 'Контролировать исполнение'");
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("БизнесПроцессСсылка.Ознакомление") Тогда
		КонтрольноеСлово = НСтр("ru = 'Ознакомиться'");
		СловоЗамены = НСтр("ru = 'Контролировать ознакомление'");
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("БизнесПроцессСсылка.Утверждение") Тогда
		КонтрольноеСлово = НСтр("ru = 'Утвердить'");
		СловоЗамены = НСтр("ru = 'Контролировать утверждение'");
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("БизнесПроцессСсылка.Подписание") Тогда
		КонтрольноеСлово = НСтр("ru = 'Подписать'");
		СловоЗамены = НСтр("ru = 'Контролировать подписание'");
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("БизнесПроцессСсылка.Рассмотрение") Тогда
		КонтрольноеСлово = НСтр("ru = 'Рассмотреть'");
		СловоЗамены = НСтр("ru = 'Контролировать рассмотрение'");
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("БизнесПроцессСсылка.Регистрация") Тогда
		КонтрольноеСлово = НСтр("ru = 'Зарегистрировать'");
		СловоЗамены = НСтр("ru = 'Контролировать регистрацию'");
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("БизнесПроцессСсылка.Приглашение") Тогда
		КонтрольноеСлово = НСтр("ru = 'Принять участие в'");
		СловоЗамены = НСтр("ru = 'Контролировать приглашение'");
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("БизнесПроцессСсылка.Согласование") Тогда
		КонтрольноеСлово = НСтр("ru = 'Согласовать'");
		СловоЗамены = НСтр("ru = 'Контролировать согласование'");
		
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(КонтрольноеСлово) Тогда 
		
		ТекстОписания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Контролировать: %1'"), 
			СокрЛП(Наименование));
		
	Иначе	
		Если Лев(СокрЛП(Наименование), СтрДлина(КонтрольноеСлово)) = КонтрольноеСлово Тогда
			
			ТекстОписания = СловоЗамены + " " + СокрЛП(Сред(СокрЛП(Наименование), СтрДлина(КонтрольноеСлово)+1));
			
		Иначе	
			
			ТекстОписания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать: %1'"), 
				СокрЛП(Наименование));
			
		КонецЕсли;
	КонецЕсли;	
		
	Если ЗначениеЗаполнено(Описание) Тогда 
		ТекстОписания = ТекстОписания + Символы.ПС + Символы.ПС + Описание;
	КонецЕсли;
			
	Возврат ТекстОписания;
	
КонецФункции

Функция СвернутьТекстВСтроку(Текст, ДлинаСтроки)
	
	СтрокаВозврата = Текст;
	СтрокаВозврата = СтрЗаменить(СтрокаВозврата, Символы.ВК, " ");
	СтрокаВозврата = СтрЗаменить(СтрокаВозврата, Символы.ПС, " ");
	СтрокаВозврата = СтрЗаменить(СтрокаВозврата, Символы.Таб, " ");
	СтрокаВозврата = СтрЗаменить(СтрокаВозврата, "  ", " ");
	СтрокаВозврата = Лев(СтрокаВозврата, ДлинаСтроки);
	
	Возврат СтрокаВозврата;
	
КонецФункции

Процедура УстановитьСостояниеКонтроляВКеше(Предмет) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Предмет) Тогда 
		Возврат;
	КонецЕсли;	
	
	Предметы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Предмет);
	
	Если ТипЗнч(Предмет) = Тип("ДокументСсылка.Задача") Тогда
		
		ИсточникЗадачи = РаботаСЗадачами.ИсточникЗадачи(Предмет);
		Если ЗначениеЗаполнено(ИсточникЗадачи) Тогда
			Предметы.Добавить(ИсточникЗадачи);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("ДокументСсылка.ДействиеЗадачи") Тогда
		
		ИсточникДействияЗадачи = РаботаСЗадачами.ИсточникДействияЗадачи(Предмет);
		Если ЗначениеЗаполнено(ИсточникДействияЗадачи) Тогда
			Предметы.Добавить(ИсточникДействияЗадачи);
		КонецЕсли;
		
	ИначеЕсли РаботаСЗадачами.ЭтоИсточникЗадач(Предмет) Тогда
		
		ЗадачаПоИсточнику = Документы.Задача.НайтиПоИсточнику(Предмет);
		Если ЗначениеЗаполнено(ЗадачаПоИсточнику) Тогда
			Предметы.Добавить(ЗадачаПоИсточнику);
		КонецЕсли;
		
	ИначеЕсли РаботаСЗадачами.ЭтоИсточникДействийЗадач(Предмет) Тогда
		
		ДействиеЗадачиПоИсточнику = Документы.ДействиеЗадачи.НайтиПоИсточнику(Предмет);
		Если ЗначениеЗаполнено(ДействиеЗадачиПоИсточнику) Тогда
			Предметы.Добавить(ДействиеЗадачиПоИсточнику);
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстЗапросаПоПроцессам = "";
	Для Каждого БизнесПроцесс Из Метаданные.БизнесПроцессы Цикл
		Если Не БизнесПроцесс.ТабличныеЧасти.Предметы.Реквизиты.Предмет.Тип.СодержитТип(ТипЗнч(Предмет)) Тогда 
			Продолжить;
		КонецЕсли;
		
		ТекстЗапросаПоПроцессам = ТекстЗапросаПоПроцессам +
			"ВЫБРАТЬ
			|	Ссылка
			|		" + ?(ТекстЗапросаПоПроцессам = "", " ПОМЕСТИТЬ ПодчиненныеПроцессы " , "") + "
			|ИЗ
			|	БизнесПроцесс." + БизнесПроцесс.Имя + ".Предметы
			|ГДЕ 
			|	Предмет В (&Предметы) И РольПредмета = ЗНАЧЕНИЕ(Перечисление.РолиПредметов.Основной)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|";
	КонецЦикла;	
	
	Если ТекстЗапросаПоПроцессам = "" Тогда 
		
		ТекстЗапроса = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Контроль.Ссылка,
			|	Контроль.СнятСКонтроля,
			|	Контроль.ПоставленНаКонтроль,
			|	Контроль.СрокИсполнения
			|ИЗ
			|	Справочник.Контроль КАК Контроль
			|ГДЕ
			|	Контроль.Предмет В (&Предметы)
			|	И НЕ Контроль.ПометкаУдаления";
		
	Иначе
		
		ДлинаТекстаОбъединения = СтрДлина(Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС);
		ТекстЗапросаПоПроцессам = Лев(ТекстЗапросаПоПроцессам, СтрДлина(ТекстЗапросаПоПроцессам) - ДлинаТекстаОбъединения);
		ТекстЗапросаПоПроцессам = ТекстЗапросаПоПроцессам + ";";
		
		ТекстЗапроса = ТекстЗапросаПоПроцессам + 
			"ВЫБРАТЬ
			|	Контроль.Ссылка,
			|	Контроль.СнятСКонтроля,
			|	Контроль.ПоставленНаКонтроль,
			|	Контроль.СрокИсполнения
			|ИЗ
			|	Справочник.Контроль КАК Контроль
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПодчиненныеПроцессы КАК ПодчиненныеПроцессы
			|		ПО Контроль.Предмет = ПодчиненныеПроцессы.Ссылка
			|ГДЕ
			|	НЕ Контроль.ПометкаУдаления
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Контроль.Ссылка,
			|	Контроль.СнятСКонтроля,
			|	Контроль.ПоставленНаКонтроль,
			|	Контроль.СрокИсполнения
			|ИЗ
			|	Справочник.Контроль КАК Контроль
			|ГДЕ
			|	Контроль.Предмет В (&Предметы)
			|	И НЕ Контроль.ПометкаУдаления";
			
	КонецЕсли;
	
	ТекстЗапросаПоДействиям = ДействияСервер.ТекстЗапросаДействияПредмета(Предмет, Истина);
	Если ЗначениеЗаполнено(ТекстЗапросаПоДействиям) Тогда
		ТекстЗапроса = ТекстЗапросаПоДействиям + ТекстЗапроса;
		ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Контроль.Ссылка,
			|	Контроль.СнятСКонтроля,
			|	Контроль.ПоставленНаКонтроль,
			|	Контроль.СрокИсполнения
			|ИЗ
			|	Справочник.Контроль КАК Контроль
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеДействия КАК ВсеДействия
			|		ПО Контроль.Предмет = ВсеДействия.Ссылка
			|ГДЕ
			|	НЕ Контроль.ПометкаУдаления";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Предметы", Предметы);
	
	Результат = Запрос.Выполнить().Выгрузить();
	Если Результат.Количество() = 0 Тогда 
		
		СрокИсполненияКеш = '00010101';
		СрокИсполненияОбщийКеш = '00010101';
		СостояниеКонтроля = Перечисления.СостоянияКонтроля.ПустаяСсылка();
		
	ИначеЕсли Результат.Количество() = 1 Тогда 
		
		СрокИсполненияКеш = Результат[0].СрокИсполнения;
		СрокИсполненияОбщийКеш = Результат[0].СрокИсполнения;
		Если Результат[0].СнятСКонтроля Тогда 
			СостояниеКонтроля = Перечисления.СостоянияКонтроля.СнятСКонтроля;
		Иначе
			СостояниеКонтроля = Перечисления.СостоянияКонтроля.НаКонтроле;
		КонецЕсли;	
		
	Иначе
		
		Если Результат.Найти(Истина, "СнятСКонтроля") <> Неопределено
		   И Результат.Найти(Ложь, "СнятСКонтроля") <> Неопределено Тогда 
			СостояниеКонтроля = Перечисления.СостоянияКонтроля.СмешанноНесколько;
		ИначеЕсли Результат.Найти(Истина, "СнятСКонтроля") <> Неопределено Тогда 
			СостояниеКонтроля = Перечисления.СостоянияКонтроля.СнятСКонтроляНесколько;
		Иначе	
			СостояниеКонтроля = Перечисления.СостоянияКонтроля.НаКонтролеНесколько;
		КонецЕсли;	
		
		СрокИсполненияКеш = '39990101';
		СрокИсполненияОбщийКеш = '00010101';
		ЕстьПустойСрок = Ложь;
		Для Каждого Строка Из Результат Цикл 
			Если Строка.СнятСКонтроля Тогда 
				Продолжить;
			КонецЕсли;	
			Если ЗначениеЗаполнено(Строка.СрокИсполнения) И Строка.СрокИсполнения < СрокИсполненияКеш Тогда 
				СрокИсполненияКеш = Строка.СрокИсполнения;
			КонецЕсли;
			Если Строка.СрокИсполнения > СрокИсполненияОбщийКеш Тогда 
				СрокИсполненияОбщийКеш = Строка.СрокИсполнения;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Строка.СрокИсполнения) Тогда 
				ЕстьПустойСрок = Истина;
			КонецЕсли;	
		КонецЦикла;	
		Если СрокИсполненияКеш = '39990101' Тогда 
			СрокИсполненияКеш = '00010101';
		КонецЕсли;	
		Если ЕстьПустойСрок Тогда 
			СрокИсполненияОбщийКеш = '00010101';
		КонецЕсли;	
		
	КонецЕсли;	
	
	Если ТипЗнч(Предмет) = Тип("СправочникСсылка.ВеткиПереписки") Тогда 
		
		ПредметКонтроля = Предмет.КорневоеПисьмо;
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("ДокументСсылка.Задача") Тогда
		
		ИсточникЗадачи = РаботаСЗадачами.ИсточникЗадачи(Предмет);
		Если ЗначениеЗаполнено(ИсточникЗадачи) Тогда
			ПредметКонтроля = ИсточникЗадачи;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("ДокументСсылка.ДействиеЗадачи") Тогда
		
		ИсточникДействияЗадачи = РаботаСЗадачами.ИсточникДействияЗадачи(Предмет);
		Если ЗначениеЗаполнено(ИсточникДействияЗадачи) Тогда
			ПредметКонтроля = ИсточникДействияЗадачи;
		КонецЕсли;
		
	Иначе
		
		ПредметКонтроля = Предмет;
		
	КонецЕсли;	
	
	РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(ПредметКонтроля,
		"СостояниеКонтроля",
		СостояниеКонтроля);	
	
	РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(ПредметКонтроля,
		"СрокИсполнения",
		СрокИсполненияКеш);
	
	РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(ПредметКонтроля,
		"СрокИсполненияОбщий",
		СрокИсполненияОбщийКеш);
	
	Если ОбщегоНазначения.ЭтоБизнесПроцесс(Предмет.Метаданные()) Тогда 
		ОсновныеПредметы = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(Предмет, , Истина);
		Для Каждого ОсновнойПредмет Из ОсновныеПредметы Цикл
			УстановитьСостояниеКонтроляВКеше(ОсновнойПредмет);
		КонецЦикла;	
	КонецЕсли;
			
	Если ДействияКлиентСервер.ЭтоДействие(Предмет) Тогда 
		ОсновнойПредмет = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Предмет, "Предмет");
		УстановитьСостояниеКонтроляВКеше(ОсновнойПредмет);	
	КонецЕсли;	
	
	// письма веток
	Если ТипЗнч(Предмет) = Тип("СправочникСсылка.ВеткиПереписки") Тогда
		ВеткаПереписки = Предмет;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПисьмаВеток.Письмо КАК Письмо
		|ПОМЕСТИТЬ ПисьмаВеток
		|ИЗ
		|	РегистрСведений.ПисьмаВеток КАК ПисьмаВеток
		|ГДЕ
		|	ПисьмаВеток.ВеткаПереписки = &ВеткаПереписки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПисьмаВеток.Письмо
		|ИЗ
		|	ПисьмаВеток КАК ПисьмаВеток
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ИсходящееПисьмо.Ссылка
		|ИЗ
		|	Документ.ИсходящееПисьмо КАК ИсходящееПисьмо
		|ГДЕ
		|	ИсходящееПисьмо.ДатаОтправки = ДАТАВРЕМЯ(1, 1, 1)
		|	И ИсходящееПисьмо.ПисьмоОснование В
		|			(ВЫБРАТЬ
		|				ПисьмаВеток.Письмо
		|			ИЗ
		|				ПисьмаВеток)
		|	И (ИсходящееПисьмо.ТипОтвета = ЗНАЧЕНИЕ(Перечисление.ТипыОтвета.ОтветНаПисьмо)
		|			ИЛИ ИсходящееПисьмо.ТипОтвета = ЗНАЧЕНИЕ(Перечисление.ТипыОтвета.ПересылкаПисьма))";
		
		Запрос.УстановитьПараметр("ВеткаПереписки", ВеткаПереписки);
		
		ПисьмаВетки = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Письмо");
		Для Каждого ПисьмоВетки Из ПисьмаВетки Цикл
			
			РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(ПисьмоВетки,
				"СостояниеКонтроля",
				СостояниеКонтроля);
			
			РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(ПисьмоВетки,
				"СрокИсполнения",
				СрокИсполненияКеш);
	
			РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(ПисьмоВетки,
				"СрокИсполненияОбщий",
				СрокИсполненияОбщийКеш);
				
		КонецЦикла;
	КонецЕсли;
	
	Справочники.Контроль.УстановитьСостояниеКонтроляВКеше(Предмет, СрокИсполнения);
	
КонецПроцедуры	

Процедура ПерезаписатьРабочуюГруппуПредмета(Предмет)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Предмет) Тогда 
		Возврат;
	КонецЕсли;	
	
	Если СнятСКонтроля Тогда 
		Возврат;
	КонецЕсли;	
	
	// Добавление контролера в рабочую группу предмета
	Если ТипЗнч(Предмет) = Тип("СправочникСсылка.ДокументыПредприятия") 
		Или ТипЗнч(Предмет) = Тип("СправочникСсылка.Мероприятия")
		Или ТипЗнч(Предмет) = Тип("СправочникСсылка.Проекты") Тогда 
		
	 	Если РаботаСРабочимиГруппами.ПоОбъектуВедетсяАвтоматическоеЗаполнениеРабочейГруппы(Предмет) Тогда
			
			ТаблицаУчастников = РаботаСРабочимиГруппами.ПолучитьПустуюТаблицуУчастников();
			РаботаСРабочимиГруппами.ДобавитьУчастникаВТаблицуНабора(ТаблицаУчастников, Контролер);
			РаботаСРабочимиГруппами.ДобавитьУчастниковВРабочуюГруппуОбъекта(
				Предмет, ТаблицаУчастников, Истина);
				
		КонецЕсли;
		
	// Добавление контролера процесса в рабочую группу предмета	
	ИначеЕсли ОбщегоНазначения.ЭтоБизнесПроцесс(Предмет.Метаданные()) Тогда 
		
		Если Предмет.Стартован Тогда 
			ПредметыПроцесса = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(Предмет);
			Для Каждого ПредметПроцесса Из ПредметыПроцесса Цикл
				Попытка
					ПерезаписатьРабочуюГруппуПредмета(ПредметПроцесса);
				Исключение
					ПредставлениеПредмета = Строка(ПредметПроцесса);
					ТипПредмета = ТипЗнч(ПредметПроцесса);
					РаботаСРабочимиГруппами.ОбработатьИсключениеПерезаписиРабочейГруппыПредметаПроцесса(
						ПредставлениеПредмета,
						ТипПредмета,
						ИнформацияОбОшибке());
				КонецПопытки;
			КонецЦикла;	
		КонецЕсли;
			
	КонецЕсли;	
	
	// добавление контролера процесса\задачи в участники процесса
	Если ОбщегоНазначения.ЭтоБизнесПроцесс(Предмет.Метаданные()) Тогда 
		БизнесПроцесс = Предмет;
	ИначеЕсли ТипЗнч(Предмет) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда 	
		БизнесПроцесс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Предмет, "БизнесПроцесс");
	Иначе	
		БизнесПроцесс = Неопределено;
	КонецЕсли;	
	
	Если БизнесПроцесс <> Неопределено Тогда 
		СтруктураУчастник = Новый Структура(
			"Участник",
			Контролер);
		НаборУчастниковПроцесса = РегистрыСведений.УчастникиПроцессов.ПолучитьУчастников(БизнесПроцесс);
		ЗаполнитьЗначенияСвойств(НаборУчастниковПроцесса.Добавить(), СтруктураУчастник);
		НаборУчастниковПроцесса.Свернуть(
			"Участник");
		РегистрыСведений.УчастникиПроцессов.ЗаписатьНаборПоПроцессу(БизнесПроцесс, НаборУчастниковПроцесса);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьПраваКонтролераНаПредмет(Отказ)
	
	Если ТипЗнч(Предмет) = Тип("ДокументСсылка.Задача")
		Или ТипЗнч(Предмет) = Тип("ДокументСсылка.ДействиеЗадачи")
		Или ДействияКлиентСервер.ЭтоДействие(Предмет) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Контролер) = Тип("СправочникСсылка.Пользователи") 
		Или ТипЗнч(Контролер) = Тип("СправочникСсылка.Сотрудники") Тогда

		Если ТипЗнч(Контролер) = Тип("СправочникСсылка.Сотрудники") Тогда
			Если ТипЗнч(Предмет) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда
				Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Предмет, "Владелец");
				ПраваПоОбъекту = ДокументооборотПраваДоступа.ПраваСотрудникаПоОбъекту(Проект, Контролер);
			ИначеЕсли ТипЗнч(Предмет) = Тип("СправочникСсылка.ВеткиПереписки") Тогда
				КорневоеПисьмо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Предмет, "КорневоеПисьмо");
				ПраваПоОбъекту = ДокументооборотПраваДоступа.ПраваСотрудникаПоОбъекту(КорневоеПисьмо, Контролер);
			Иначе
				ПраваПоОбъекту = ДокументооборотПраваДоступа.ПраваСотрудникаПоОбъекту(Предмет, Контролер);
			КонецЕсли;
		Иначе
			Если ТипЗнч(Предмет) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда
				Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Предмет, "Владелец");
				ПраваПоОбъекту = ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(Проект, Контролер);
			ИначеЕсли ТипЗнч(Предмет) = Тип("СправочникСсылка.ВеткиПереписки") Тогда
				КорневоеПисьмо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Предмет, "КорневоеПисьмо");
				ПраваПоОбъекту = ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(КорневоеПисьмо, Контролер);
			Иначе
				ПраваПоОбъекту = ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(Предмет, Контролер);
			КонецЕсли;
		КонецЕсли;
		
		Если Не ПраваПоОбъекту.Чтение Тогда 
			ПредметСтрокой = Контроль.СформироватьПредставлениеПредмета(Предмет);
		
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У контролера ""%1"" нет прав на предмет ""%2"".'"),
				Контролер, 
				ПредметСтрокой);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю( 
				ТекстСообщения,
				ЭтотОбъект, 
				"Контролер", 
				, 
				Отказ);	
		КонецЕсли;
	
	ИначеЕсли ТипЗнч(Контролер) = Тип("СправочникСсылка.ПолныеРоли") Тогда
		
		МассивПользователей = РегистрыСведений.ИсполнителиРолей.ИсполнителиРоли(Контролер, Истина);
		
		МассивПользователей = 
			Сотрудники.ЛюбыеПользователиСотрудников(
			МассивПользователей);
		
		КодВозврата = Истина;
		Для каждого Пользователь Из МассивПользователей Цикл
			Если ТипЗнч(Предмет) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда
				Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Предмет, "Владелец");
				ПраваПоОбъекту = ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(Проект, Пользователь);
			ИначеЕсли ТипЗнч(Предмет) = Тип("СправочникСсылка.ВеткиПереписки") Тогда
				КорневоеПисьмо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Предмет, "КорневоеПисьмо");
				ПраваПоОбъекту = ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(КорневоеПисьмо, Пользователь);
			Иначе
				ПраваПоОбъекту = ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(Предмет, Пользователь);
			КонецЕсли;
			Если Не ПраваПоОбъекту.Чтение Тогда 
				КодВозврата = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если КодВозврата = Ложь Тогда
			ПредметСтрокой = Контроль.СформироватьПредставлениеПредмета(Предмет);
		
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не все пользователи  роли ""%1"" имеют права на предмет ""%2"".'"),
				Контролер, 
				ПредметСтрокой);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю( 
				ТекстСообщения,
				ЭтотОбъект, 
				"Контролер", 
				, 
				Отказ);	
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьИсполнителейСтрокой()
	
	Перем ИсполнителиСтрокой;
	
	ИсполнителиСтрокой = "";
	Для Каждого Строка Из Исполнители Цикл
		ИсполнителиСтрокой = ИсполнителиСтрокой + Строка(Строка.Исполнитель) + "; ";
	КонецЦикла;	
	Если ИсполнителиСтрокой	<> "" Тогда 
		ИсполнителиСтрокой = Лев(ИсполнителиСтрокой, СтрДлина(ИсполнителиСтрокой) - 2);
	КонецЕсли;	
	
	Возврат ИсполнителиСтрокой;
	
КонецФункции	

Процедура ОбновитьПраваПредметаКонтроля(ЭтотОбъект, Предмет)
	
	Если (ТипЗнч(Предмет) = Тип("ДокументСсылка.Задача")
		Или ТипЗнч(Предмет) = Тип("ДокументСсылка.ДействиеЗадачи"))
		И (ДополнительныеСвойства.ЭтоНовый
			Или ОбщегоНазначенияДокументооборот.ИзменилосьЗначениеРеквизитов(ЭтотОбъект, "Контролер")) Тогда
		ДокументооборотПраваДоступа.ОпределитьДескрипторыОбъекта(Предмет);
		
	ИначеЕсли ДействияКлиентСервер.ЭтоДействие(Предмет)
		И (ДополнительныеСвойства.ЭтоНовый
			Или ОбщегоНазначенияДокументооборот.ИзменилосьЗначениеРеквизитов(ЭтотОбъект, "Контролер")) Тогда
		// Права на предмет
		ПредметДействия = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Предмет, "Предмет");
		ТаблицаУчастников = РаботаСРабочимиГруппами.ПолучитьПустуюТаблицуУчастников();
		НоваяСтрока = ТаблицаУчастников.Добавить();
		НоваяСтрока.Участник = Контролер;
		Попытка
			РаботаСРабочимиГруппами.ДобавитьУчастниковВРабочуюГруппуОбъекта(
				ПредметДействия, ТаблицаУчастников);
		Исключение
				
			ТекстИсключения = НСтр("ru = 'Не удалось поставить на контроль ""%1"" по следующей причине:
								   |
								   |%2'");
		
			ТекстИсключения = СтрШаблон(ТекстИсключения,
				Строка(ПредметДействия), 
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Если РаботаСБизнесПроцессами.ЭтоФоновоеВыполнениеПроцесса() Тогда
		
				// Если исключение возникает в фоновом задании, то в журнал регистрации пишется ошибка
				// и работа кода не прерывается.
				ЗаписьЖурналаРегистрации(
					НСтр("ru = 'Перезапись рабочей группы предмета действия'"),
					УровеньЖурналаРегистрации.Ошибка,,,
					ТекстИсключения);
			Иначе
				// Иначе вызывается исключение.
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

// Обновляет дату удаления.
// 
Процедура ОбновитьДатуУдаления()
	
	Если ПометкаУдаления И Не ЗначениеЗаполнено(ДатаУдаления) Тогда
		ДатаУдаления = ТекущаяДатаСеанса();
	ИначеЕсли Не ПометкаУдаления И ЗначениеЗаполнено(ДатаУдаления) Тогда
		ДатаУдаления = Дата(1, 1, 1);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
