#Область ОписаниеПеременных

&НаКлиенте
Перем ТекущаяСтрока; // Текущая строка списка

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьЗаголовокФормы();
	ОбновитьОтображениеЭлементов();
	УстановитьПараметрыСписка();
	УстановитьУсловноеОформление();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.КоманднаяПанельСписка;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// Мобильный клиент
	ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
	ЭтоМобильныйСБольшимЭкраном = МК_КлиентСервер.ЭтоМобильныйСБольшимЭкраном();
	МК_НастроитьЭлементыФормы();
	// Конец Мобильный клиент
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЭтоМобильныйСБольшимЭкраном Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписьКонтроля" Или ИмяСобытия = "Запись_Контроль" Тогда 
		ПредыдущаяСтрокаСписка = Неопределено;
		ОбновитьСведенияОКонтроле();
		Элементы.Список.Обновить();
		Если ИмяСобытия = "ЗаписьКонтроля" Тогда
			УстановитьТекущуюСтроку(Параметр.Ссылка);
		ИначеЕсли ИмяСобытия = "Запись_Контроль" Тогда
			УстановитьТекущуюСтроку(Параметр);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ОбновитьОтображениеЭлементов();
	УстановитьПараметрыСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()

	Если ЭтоМобильныйКлиент Тогда
		ЭтоМобильныйСБольшимЭкраном = МК_КлиентСервер.ЭтоМобильныйСБольшимЭкраном();
		МК_НастроитьЭлементыФормы();
		Если ЭтоМобильныйСБольшимЭкраном Тогда
			Если ТекущаяСтрокаСписка <> Неопределено Тогда
				Элементы.Список.ТекущаяСтрока = ТекущаяСтрока;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредметНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Предмет) Тогда 
		ПоказатьЗначение(, Предмет);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)

	ПодключитьОбработчикОжидания("ОбновитьСведенияОКонтроле", 0.2, Истина);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если Параметры.ОтображатьТолькоПросроченные Тогда
		Возврат;
	КонецЕсли;
	
	Если Копирование Тогда
		
		Если ТипЗнч(Элементы.Список.ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
			Возврат;
		КонецЕсли;
		
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначениеКопирования", ТекущиеДанные.Ссылка);
		Открытьформу("Справочник.Контроль.Форма.ФормаНового", ПараметрыФормы, ЭтотОбъект);
		
	Иначе
		
		ОткрытьФорму("Справочник.Контроль.Форма.ФормаНового",, ЭтотОбъект, Новый УникальныйИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ОткрытьКарточкуКонтроля();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура СписокПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если Параметры.ОтображатьТолькоПросроченные Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") 
		И ПараметрыПеретаскивания.Значение.Количество() > 0 Тогда
		Если ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("ДанныеФормыЭлементДерева") Тогда 
			Основание = ПараметрыПеретаскивания.Значение[0].Ссылка;
		Иначе 
			Основание = ПараметрыПеретаскивания.Значение[0];
		КонецЕсли;
		
		Если ПодходящийТипПредмета(Основание) Тогда 
			ПараметрыФормыОткрытия = Новый Структура("Основание", Основание);
			ОткрытьФорму("Справочник.Контроль.Форма.ФормаНового", ПараметрыФормыОткрытия);
		Иначе 
			ПоказатьПредупреждение(, НСтр("ru = 'Объект нельзя поставить на контроль'"));
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаЗапросаОбновления(Элемент)
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаПолученияНавигационнойСсылки(Элемент, Ключ, Представление, ПолученнаяНавигационнаяСсылка,
	СтандартнаяОбработка)
	
	Если ТипЗнч(Элементы.Список.ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПолученнаяНавигационнаяСсылка = ПолучитьНавигационнуюСсылку(ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаПолученияСпискаНавигационныхСсылок(Элемент, СписокНавигационыхСсылок, КлючПоУмолчанию)
	
	Если ТипЗнч(Элементы.Список.ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КлючПоУмолчанию = "Контроль";
	ПредставлениеФормы = Строка(ТекущиеДанные.Ссылка);
	СписокНавигационыхСсылок.Добавить(КлючПоУмолчанию, ПредставлениеФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	ОбщегоНазначенияДокументооборотКлиент.УстановитьПометкуУдаленияВыделенныхСтрок(Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИсполнители

//@skip-check module-structure-form-event-regions
&НаКлиенте
Процедура ИсполнителиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ИсполнителиСостояние 
	 Или Поле = Элементы.ИсполнителиСостояниеССостояниемОдин Тогда 
	 
	 	СтандартнаяОбработка = Ложь;
		Строка = Исполнители.НайтиПоИдентификатору(ВыбраннаяСтрока);
		Если ЗначениеЗаполнено(Строка.Источник) Тогда 
			ПоказатьЗначение(, Строка.Источник);
			
		ИначеЕсли ЗначениеЗаполнено(Предмет)
			И ДействияКлиентСервер.ЭтоДействие(Предмет) Тогда 
			ПоказатьЗначение(, Предмет);
		КонецЕсли;
		
	ИначеЕсли Поле = Элементы.ИсполнителиИсполнитель
		  Или Поле = Элементы.ИсполнителиИсполнительССостояниемОдин
		  Или Поле = Элементы.ИсполнителиИсполнительБезСостоянияНесколько
		  Или Поле = Элементы.ИсполнителиИсполнительБезСостоянияОдин Тогда 
		
		СтандартнаяОбработка = Ложь;
		Строка = Исполнители.НайтиПоИдентификатору(ВыбраннаяСтрока);
		
		Если ЗначениеЗаполнено(Строка.Исполнитель) Тогда 
			Если ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.АдресатыПочтовыхСообщений") Тогда 
				СтруктураАдресата = ВстроеннаяПочтаСервер.ПолучитьПредставлениеИКонтактАдресата(Строка.Исполнитель);
				Если ЗначениеЗаполнено(СтруктураАдресата.Контакт) Тогда 
					ПоказатьЗначение(, СтруктураАдресата.Контакт);
				КонецЕсли;
			Иначе 
				ПоказатьЗначение(, Строка.Исполнитель);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура СнятьСКонтроля(Команда)
	
	Если ТипЗнч(Элементы.Список.ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ТекущиеДанные.СнятСКонтроля Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Запись уже снята с контроля.'"));
		Возврат;
	КонецЕсли;	
	
	КонтрольКлиент.СнятьСКонтроля(ТекущиеДанные.Ссылка);
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Снят с контроля'"),
		ПолучитьНавигационнуюСсылку(ТекущиеДанные.Ссылка),
		Строка(ТекущиеДанные.Ссылка));
	
	РеквизитыКонтроля = ОбщегоНазначенияДокументооборотВызовСервера.ЗначенияРеквизитовОбъекта(
		ТекущаяСтрокаСписка, "СрокИсполнения");
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"СнятьСКонтроляПродолжение",
		ЭтотОбъект);
	
	УчетВремениКлиент.ДобавитьВОтчетПослеВыполненияЗадачи(ОпцияИспользоватьУчетВремени,
		РеквизитыКонтроля.СрокИсполнения, ТекущаяСтрокаСписка, ВключенХронометраж, 
		ДатаНачалаХронометража, ДатаКонцаХронометража,
		ВидыРабот, СпособУказанияВремени, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьСКонтроляПродолжение(Результат, Параметры) Экспорт
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиСрок(Команда)
	
	Если ТипЗнч(Элементы.Список.ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.СнятСКонтроля Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Нельзя перенести срок, так как карточка уже снята с контроля.'"));
		Возврат;
	КонецЕсли;
		
	ОписаниеОповещения = Новый ОписаниеОповещения("ПеренестиСрокПродолжение", ЭтотОбъект,
		Новый Структура("ТекущиеДанные", ТекущиеДанные));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СрокИсполнения", ТекущиеДанные.СрокИсполнения);
	ПараметрыФормы.Вставить("Контроль", ТекущиеДанные.Ссылка);
	
	ОткрытьФорму("Справочник.Контроль.Форма.ПереносСрока", ПараметрыФормы,,,,,
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуКонтроляКоманда(Команда)
	
	ОткрытьКарточкуКонтроля();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПоказатьСнятыеСКонтроля(Команда)
	
	КомандаПоказатьСнятыеСКонтроляНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ПредыдущаяСтрокаСписка = Неопределено;
	ОбновитьСведенияОКонтроле();
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура Автообновление(Команда)
	
	АвтообновлениеКлиент.УстановитьПараметрыАвтообновленияСписка(ЭтотОбъект, "Список");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Учет времени

&НаКлиенте
Процедура ПереключитьХронометраж(Команда)
	
	ПараметрыОповещения = Неопределено;
	НуженДиалог = УчетВремениКлиент.НуженДиалогДляХронометража(ВключенХронометраж, 
		ДатаНачалаХронометража, ВидыРабот);
	
	Если НуженДиалог = Ложь Тогда
		
		ПереключитьХронометражСервер(ПараметрыОповещения);
		УчетВремениКлиент.ПоказатьОповещение(ПараметрыОповещения, ВключенХронометраж, ТекущаяСтрокаСписка);
	
	Иначе
		ДлительностьРаботы = УчетВремениКлиент.ПолучитьДлительностьРаботы(ДатаНачалаХронометража);
		
		ОписаниеРаботы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Работа с контролем ""%1""'"),
			ТекущаяСтрокаСписка);
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ДатаОтчета", ТекущаяДата());
		ПараметрыФормы.Вставить("ВидыРабот", ВидыРабот);
		ПараметрыФормы.Вставить("ОписаниеРаботы", ОписаниеРаботы);
		ПараметрыФормы.Вставить("ДлительностьРаботы", ДлительностьРаботы);
		ПараметрыФормы.Вставить("НачалоРаботы", ДатаНачалаХронометража);
		ПараметрыФормы.Вставить("Объект", ТекущаяСтрокаСписка);
		ПараметрыФормы.Вставить("СпособУказанияВремени", СпособУказанияВремени);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПереключитьХронометражПродолжение", ЭтотОбъект,
			Новый Структура("ПараметрыОповещения", ПараметрыОповещения));
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФорму("РегистрСведений.ФактическиеТрудозатраты.Форма.ФормаДобавленияРаботы",
			ПараметрыФормы, , , , , ОписаниеОповещения, РежимОткрытияОкна);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьХронометражПродолжение(ПараметрыОтчета, Параметры) Экспорт 
	
	Если ПараметрыОтчета <> Неопределено Тогда
		ДобавитьВОтчетИОбновитьФорму(ПараметрыОтчета, Параметры.ПараметрыОповещения);
		УчетВремениКлиент.ПоказатьОповещение(Параметры.ПараметрыОповещения,
			ВключенХронометраж, ТекущаяСтрокаСписка);
	Иначе
		ОтключитьХронометражСервер();
	КонецЕсли;  

КонецПроцедуры

&НаКлиенте
Процедура УказатьТрудозатраты(Команда)
	
	ДатаОтчета = ТекущаяДата();
	
	УчетВремениКлиент.ДобавитьВОтчетКлиент(
		ДатаОтчета,
		ВключенХронометраж, 
		ДатаНачалаХронометража, 
		ДатаКонцаХронометража, 
		ВидыРабот, 
		ТекущаяСтрокаСписка,
		СпособУказанияВремени,
		Элементы.ПереключитьХронометраж,
		Ложь,
		ЭтотОбъект); // Выполнена
		
КонецПроцедуры

&НаКлиенте
Процедура ОтчетНаКонтроле(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", "НаКонтроле");
	ПараметрыФормы.Вставить("Контролер", ПользователиКлиент.ТекущийПользователь());
	
	ОткрытьФорму("Отчет.Контроль.Форма", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетИстекающиеСроки(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", "КонтрольСИстекающимСроком");
	ПараметрыФормы.Вставить("Контролер", ПользователиКлиент.ТекущийПользователь());
	
	ОткрытьФорму("Отчет.Контроль.Форма", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетСводкаОКонтроле(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", "СводкаОКонтроле");
	ПараметрыФормы.Вставить("Контролер", ПользователиКлиент.ТекущийПользователь());
	
	ОткрытьФорму("Отчет.Контроль.Форма", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчеты(Команда)
	
	Раздел = ПредопределенноеЗначение("Перечисление.РазделыОтчетов.КонтрольСписок");
	
	ЗаголовокФормы = НСтр("ru = 'Отчеты по контролю'");
	
	РазделГипперссылка = НастройкиВариантовОтчетовДокументооборот.ПолучитьРазделОтчетаПоИмени("СовместнаяРабота");
	
	ПараметрыФормы = Новый Структура("Раздел, ЗаголовокФормы, НеОтображатьИерархию, РазделГипперссылка", 
										Раздел, ЗаголовокФормы, Истина, РазделГипперссылка);
	
	ОткрытьФорму(
		"Обработка.ВсеОтчеты.Форма.ФормаПоКатегориям",
		ПараметрыФормы,
		ЭтотОбъект, 
		"КонтрольСписок");

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьУдаленные(Команда)
	
	КлючУдаленныеОбъекты = ОбщегоНазначенияДокументооборотКлиентСервер.КлючУдаленныеОбъекты();
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", КлючУдаленныеОбъекты);
	
	ОткрытьФорму("Справочник.Контроль.Форма.ФормаСписка", ПараметрыФормы,, КлючУдаленныеОбъекты);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПеренестиСрокПродолжение(Результат, Параметры) Экспорт 
	
	ТекущиеДанные = Параметры.ТекущиеДанные;
	
	Если Результат <> Неопределено 
		И ТипЗнч(Результат) = Тип("Структура") 
		И ТекущиеДанные.СрокИсполнения <> Результат.НовыйСрок Тогда
		КонтрольКлиент.ПеренестиСрок(ТекущиеДанные.Ссылка, Результат.НовыйСрок);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуКонтроля()
	
	Если ТипЗнч(Элементы.Список.ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ТекущиеДанные.Ссылка);
	
	ОткрытьФорму("Справочник.Контроль.Форма.ФормаЭлемента", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСведенияОКонтроле()
	
	ТекущиеДанные = РаботаСоСпискамиДокументовКлиент.ПолучитьДанныеТекущейСтрокиСписка(
		Элементы.Список,
		Элементы.Список.ТекущаяСтрока);
	
	Если ТекущиеДанные = Неопределено Тогда
		Исполнители.Очистить();
		Описание = "";
		ПредыдущаяСтрокаСписка = Неопределено;
		ПредметСтрокой = "";
		Предмет = Неопределено;
		
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Ссылка = ПредыдущаяСтрокаСписка Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	ТекущаяСтрокаСписка = ТекущиеДанные.Ссылка;
	
	ДанныеКонтроля = ПолучитьДанныеКонтроля(ТекущиеДанные.Ссылка);
	
	Исполнители.Очистить();
	Для Каждого Строка Из ДанныеКонтроля.МассивИсполнителей Цикл
		НоваяСтрока = Исполнители.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	Описание = ДанныеКонтроля.Описание;
	
	Предмет = ДанныеКонтроля.Предмет;
	Если ЗначениеЗаполнено(Предмет) Тогда
		ПредметСтрокой = ДанныеКонтроля.ПредметСтрокой;
		Элементы.ПредметСтрокой.Гиперссылка = Истина;
	Иначе
		ПредметСтрокой = НСтр("ru = 'Нет'");
		Элементы.ПредметСтрокой.Гиперссылка = Ложь;
	КонецЕсли;
	
	Если ДанныеКонтроля.ВыводитьСостояние Тогда 
		Если Исполнители.Количество() > 1 Тогда 
			Элементы.ГруппаИсполнителиСтраницы.ТекущаяСтраница = Элементы.ГруппаИсполнителиССостояниемНесколько;
		Иначе
			Элементы.ГруппаИсполнителиСтраницы.ТекущаяСтраница = Элементы.ГруппаИсполнителиССостояниемОдин;
		КонецЕсли;
	Иначе
		Если Исполнители.Количество() > 1 Тогда 
			Элементы.ГруппаИсполнителиСтраницы.ТекущаяСтраница = Элементы.ГруппаИсполнителиБезСостоянияНесколько;
		Иначе
			Элементы.ГруппаИсполнителиСтраницы.ТекущаяСтраница = Элементы.ГруппаИсполнителиБезСостоянияОдин;
		КонецЕсли;	
	КонецЕсли;	
	
	// Установить значения реквизитов
	Если ТипЗнч(ДанныеКонтроля.ДополнительныеРеквизиты) = Тип("Структура") Тогда
		Для каждого Реквизит Из ДанныеКонтроля.ДополнительныеРеквизиты Цикл
			ЭтотОбъект[Реквизит.Ключ] = Реквизит.Значение;
		КонецЦикла;
	КонецЕсли;
	
	// Установить свойства элементов
	Если ТипЗнч(ДанныеКонтроля.СвойстваЭлементов) = Тип("Структура") Тогда
		Для каждого Элемент Из ДанныеКонтроля.СвойстваЭлементов Цикл
			НайденныйЭлемент = Элементы[Элемент.Ключ];
			Для каждого СвойствоЭлементаИЗначение Из Элемент.Значение Цикл
				ИмяСвойства = СвойствоЭлементаИЗначение.Ключ;
				ЗначениеСвойства = СвойствоЭлементаИЗначение.Значение;
							
				НайденныйЭлемент[ИмяСвойства] = ЗначениеСвойства;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;

	ПредыдущаяСтрокаСписка = ТекущиеДанные.Ссылка;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеКонтроля(КарточкаКонтроля)
	
	МассивИсполнителей = Новый Массив;
	
	Для Каждого Строка Из КарточкаКонтроля.Исполнители Цикл
		СтруктураИсполнителя = Новый Структура("Исполнитель, Состояние, Ответственный, Источник, Проконтролировано");
		СтруктураИсполнителя.Исполнитель = Строка.Исполнитель;
		СтруктураИсполнителя.Ответственный = Строка.Ответственный;
		СтруктураИсполнителя.Источник = Строка.Источник;
		СтруктураИсполнителя.Проконтролировано = Строка.Проконтролировано;
		
		Состояние = "";
		Если ЗначениеЗаполнено(КарточкаКонтроля.Предмет) Тогда 
			Если ТипЗнч(КарточкаКонтроля.Предмет) = Тип("СправочникСсылка.ВеткиПереписки") Тогда 
				Если ТипЗнч(Строка.Источник) = Тип("ДокументСсылка.ВходящееПисьмо") Тогда 
					Состояние = НСтр("ru = 'Ответ получен'");
				ИначеЕсли ТипЗнч(Строка.Источник) = Тип("ДокументСсылка.ИсходящееПисьмо") Тогда 
					Состояние = НСтр("ru = 'Ответ не получен'");
				КонецЕсли;	
			ИначеЕсли ОбщегоНазначения.ЭтоБизнесПроцесс(КарточкаКонтроля.Предмет.Метаданные())
				Или ТипЗнч(КарточкаКонтроля.Предмет) = Тип("ЗадачаСсылка.ЗадачаИсполнителя")
				Или ТипЗнч(КарточкаКонтроля.Предмет) = Тип("ДокументСсылка.ДействиеЗадачи")
				Или ТипЗнч(КарточкаКонтроля.Предмет) = Тип("ДокументСсылка.Задача")
				Или ДействияКлиентСервер.ЭтоДействие(КарточкаКонтроля.Предмет) Тогда
				Если Строка.Исполнено Тогда 
					Состояние = НСтр("ru = 'Исполнено'");
				КонецЕсли;	
		    КонецЕсли;
		КонецЕсли;
		СтруктураИсполнителя.Состояние = Состояние;
		
		МассивИсполнителей.Добавить(СтруктураИсполнителя);
	КонецЦикла;
	
	Результат = новый Структура;
	Результат.Вставить("МассивИсполнителей", МассивИсполнителей);
	Результат.Вставить("Описание", КарточкаКонтроля.Описание);
	
	Если ТипЗнч(КарточкаКонтроля.Предмет) = Тип("СправочникСсылка.ВеткиПереписки") Тогда 
		Результат.Вставить("Предмет", КарточкаКонтроля.Предмет.КорневоеПисьмо);
	Иначе
		Результат.Вставить("Предмет", КарточкаКонтроля.Предмет);	
	КонецЕсли;	
	Результат.Вставить("ПредметСтрокой", Контроль.СформироватьПредставлениеПредмета(Результат.Предмет));
	
	ВыводитьСостояние = Ложь;
	Если ЗначениеЗаполнено(КарточкаКонтроля.Предмет) Тогда 
		Если ТипЗнч(КарточкаКонтроля.Предмет) = Тип("СправочникСсылка.ВеткиПереписки")
		 Или ОбщегоНазначения.ЭтоБизнесПроцесс(КарточкаКонтроля.Предмет.Метаданные()) 
		 Или ТипЗнч(КарточкаКонтроля.Предмет) = Тип("ЗадачаСсылка.ЗадачаИсполнителя")
		 Или ТипЗнч(КарточкаКонтроля.Предмет) = Тип("ДокументСсылка.Задача")
		 Или ТипЗнч(КарточкаКонтроля.Предмет) = Тип("ДокументСсылка.ДействиеЗадачи")
		 Или ДействияКлиентСервер.ЭтоДействие(КарточкаКонтроля.Предмет) Тогда
			ВыводитьСостояние = Истина; 
		КонецЕсли; 
	КонецЕсли;
	Результат.Вставить("ВыводитьСостояние", ВыводитьСостояние);
	
	// Хронометраж
	ДополнительныеРеквизиты = Новый Структура;
	СвойстваЭлементов = Новый Структура;
	
	ДатаНачалаХронометража = Неопределено;
	ДатаКонцаХронометража = Неопределено;
	ВключенХронометраж = Неопределено;
	ОпцияИспользоватьУчетВремени = Неопределено;
	ВидыРабот = Неопределено;
	СпособУказанияВремени = Неопределено;
	
	ПереключитьХронометражНеМеняяПодсказку = Новый Структура("Имя, Подсказка");
	
	ПереключитьХронометраж = Новый Структура("Доступность, Пометка");
	ПереключитьХронометраж.Доступность = Истина;
	
	УказатьТрудозатраты = Новый Структура("Доступность");
	УказатьТрудозатраты.Доступность = Истина;
	
	УчетВремени.ПроинициализироватьПараметрыУчетаВремени(
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ОпцияИспользоватьУчетВремени,
		КарточкаКонтроля,
		ВидыРабот,
		СпособУказанияВремени,
		ПереключитьХронометражНеМеняяПодсказку,
		ПереключитьХронометраж,
		УказатьТрудозатраты);
		
	СвойстваЭлементов.Вставить("ПереключитьХронометраж", Новый Соответствие);
	СвойстваЭлементов.Вставить("УказатьТрудозатраты", Новый Соответствие);
	
	СвойстваЭлементов.ПереключитьХронометраж.Вставить(
		"Доступность",
		ПереключитьХронометраж.Доступность);
	СвойстваЭлементов.ПереключитьХронометраж.Вставить(
		"Пометка",
		ПереключитьХронометраж.Пометка);
	СвойстваЭлементов.УказатьТрудозатраты.Вставить(
		"Доступность",
		УказатьТрудозатраты.Доступность);
	
	ДополнительныеРеквизиты.Вставить("ДатаНачалаХронометража", ДатаНачалаХронометража);
	ДополнительныеРеквизиты.Вставить("ДатаКонцаХронометража", ДатаКонцаХронометража);
	ДополнительныеРеквизиты.Вставить("ВключенХронометраж", ВключенХронометраж);
	ДополнительныеРеквизиты.Вставить("ОпцияИспользоватьУчетВремени", ОпцияИспользоватьУчетВремени);
	ДополнительныеРеквизиты.Вставить("ВидыРабот", ВидыРабот);
	ДополнительныеРеквизиты.Вставить("СпособУказанияВремени", СпособУказанияВремени);
	
	Результат.Вставить("ДополнительныеРеквизиты", ДополнительныеРеквизиты);
	Результат.Вставить("СвойстваЭлементов", СвойстваЭлементов);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПодходящийТипПредмета(ОбъектПеретаскивания)
	
	МассивТиповДанных = Метаданные.Справочники.Контроль.Реквизиты.Предмет.Тип.Типы();
	
	Если МассивТиповДанных.Найти(ТипЗнч(ОбъектПеретаскивания)) = Неопределено Тогда 
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции	

&НаСервере
Процедура ОтключитьХронометражСервер()
	
	УчетВремени.ОтключитьХронометражСервер(
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ТекущаяСтрокаСписка,
		Команды.ПереключитьХронометраж,
		Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаСервере
Процедура ПереключитьХронометражСервер(ПараметрыОповещения)
	
	УчетВремени.ПереключитьХронометражСервер(
		ПараметрыОповещения,
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ТекущаяСтрокаСписка,
		ВидыРабот,
		Команды.ПереключитьХронометраж,
		Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВОтчетИОбновитьФорму(ПараметрыОтчета, ПараметрыОповещения)
	
	УчетВремени.ДобавитьВОтчетИОбновитьФорму(
		ПараметрыОтчета, 
		ПараметрыОповещения,
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		Команды.ПереключитьХронометраж,
		Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	Если Не ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементыСтиля = МК.ЭлементыСтиля();
	
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	
	Элементы.ПраваяКолонка.Видимость = ЭтоМобильныйСБольшимЭкраном;
	Если ЭтоМобильныйСБольшимЭкраном Тогда
		Элементы.ГруппаСписки.Группировка =
			ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		Элементы.ПраваяКолонка.ЦветФона = ЭлементыСтиля.ЦветФонаГруппы;
		Элементы.Список.ИспользованиеТекущейСтроки =
			ИспользованиеТекущейСтрокиТаблицы.ОтображениеВыделения;
	Иначе
		Элементы.Список.ИспользованиеТекущейСтроки =
			ИспользованиеТекущейСтрокиТаблицы.Выбор;
	КонецЕсли;
	
	ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Половинный;
	
	Элементы.Список.Шапка = Ложь;
	
	Элементы.Список.ВертикальныеЛинии = Ложь;
	Элементы.Описание.РастягиватьПоВертикали = Истина;
	Элементы.Описание.АвтоМаксимальнаяВысота = Ложь;
	
	// Трансформация кнопки "Создать" в акцентную 
	МК.ПреобразоватьКнопкуВАкцентную(ЭтотОбъект,
		ЭлементыСтиля,
		Элементы.Создать,
		Элементы.МК_ГруппаНижнийБлокВСписке);

	Элементы.Изменить.Видимость = Ложь;
	Элементы.УстановитьПометкуУдаления.Видимость = Ложь;
	Элементы.Обновить.Видимость = Ложь;
	Элементы.ГруппаОтчеты.Видимость = Ложь;
	Элементы.СохранитьНастройкиДинамическогоСписка.Видимость = Ложь;
	Элементы.ЗагрузитьНастройкиДинамическогоСписка.Видимость = Ложь;
	Элементы.ВывестиСписок.Видимость = Ложь;
	Элементы.ОткрытьОтчеты.Видимость = Ложь;
	
	// Контекстное меню списка.
	//  Написать письмо
	Элементы.Переместить(Элементы.СоздатьПисьмоНаОсновании,
		Элементы.Список.КонтекстноеМеню, Элементы.СписокКонтекстноеМенюСкопировать);
	Элементы.СоздатьПисьмоНаОсновании.Заголовок = НСтр("ru='Написать письмо'");
	//  Записать в календарь
	Элементы.Переместить(Элементы.СправочникЗаписиРабочегоКалендаряСоздатьЗаписьКалендаряНаОсновании,
		Элементы.Список.КонтекстноеМеню, Элементы.СписокКонтекстноеМенюСкопировать);
	Элементы.СправочникЗаписиРабочегоКалендаряСоздатьЗаписьКалендаряНаОсновании.Заголовок =
		НСтр("ru='Записать в календарь'");
	Если Не ЭтоМобильныйСБольшимЭкраном Тогда
		// Хронометраж
		Элементы.Переместить(Элементы.ПереключитьХронометраж, Элементы.Список.КонтекстноеМеню);
		//  Снять с контроля
		Элементы.Переместить(Элементы.СнятьСКонтроля, Элементы.Список.КонтекстноеМеню);
		//  Указать трудозатраты
		Элементы.Переместить(Элементы.УказатьТрудозатраты, Элементы.Список.КонтекстноеМеню);
		//  Перенести срок
		Элементы.Переместить(Элементы.ПеренестиСрок, Элементы.Список.КонтекстноеМеню);
		//  Пометить на удаление
		Элементы.Переместить(Элементы.СписокКонтекстноеМенюУстановитьПометкуУдаления,
			Элементы.Список.КонтекстноеМеню);
	КонецЕсли;

	//  Скопировать
	Элементы.СписокКонтекстноеМенюСкопировать.Видимость = Истина;
	
	// Отключение прочих команд
	Элементы.СписокКонтекстноеМенюСоздать.Видимость = Ложь;
	Элементы.СписокКонтекстноеМенюИзменить.Видимость = Ложь;
	Элементы.СписокКонтекстноеМенюНайти.Видимость = Ложь;
	Элементы.СписокКонтекстноеМенюОтменитьПоиск.Видимость = Ложь;
	Элементы.СписокКонтекстноеМенюКопироватьВБуферОбмена.Видимость = Ложь;
	Элементы.СписокКонтекстноеМенюАвтообновление.Видимость = Ложь;
	
	// Настройка списка.
	Элементы.Ссылка.Видимость = Ложь;
	
	// 2 строки:
	//  Что  | Срок
	//  Кого | Дата создания
	Элементы.Переместить(Элементы.СписокИсполнителиСтрокой, Элементы.ГруппаСтрока, Элементы.Ссылка);
	Элементы.СписокИсполнителиСтрокой.Ширина = 20;
	
	ГруппаСтрокаДаты = Элементы.Найти("МК_ГруппаСтрокаДаты");
	Если ГруппаСтрокаДаты = Неопределено Тогда
		ГруппаСтрокаДаты = Элементы.Добавить("МК_ГруппаСтрокаДаты",
			Тип("ГруппаФормы"),
			Элементы.Список);
	КонецЕсли;
	
	Элементы.Переместить(Элементы.СписокСрокИсполнения, ГруппаСтрокаДаты);
	Элементы.Переместить(Элементы.СписокОсталосьДней, ГруппаСтрокаДаты);
	Элементы.Переместить(Элементы.СписокДатаПостановки, ГруппаСтрокаДаты);
	Элементы.СписокОсталосьДней.ОтображатьВШапке = Ложь;
	Элементы.СписокСрокИсполнения.Ширина = 6;
	Элементы.СписокОсталосьДней.Ширина = 6;
	Элементы.СписокДатаПостановки.Ширина = 6;
	
	Элементы.СписокНаименование.Ширина = 15;
	Элементы.СписокИсполнителиСтрокой.Ширина = 15;
	
	// Вторая строка
	Элементы.СписокИсполнителиСтрокой.Шрифт = ШрифтыСтиля.МелкийШрифтТекста;
	Элементы.СписокИсполнителиСтрокой.ЦветТекста = WebЦвета.Серый;
	Элементы.СписокДатаПостановки.Шрифт = ШрифтыСтиля.МелкийШрифтТекста;
	Элементы.СписокДатаПостановки.ЦветТекста = WebЦвета.Серый;
	
	// Настройка кнопок
	Элементы.ДекорацияОтступПередКнопками.Видимость = Ложь;
	Элементы.МК_ДекорацияОтступПередКнопками.Видимость = Истина;
	Элементы.ГруппаКнопки.Видимость = Ложь;
	Элементы.МК_ГруппаКнопки.Видимость = Истина;

КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыСписка()
	
	ИмяПараметраПросроченные = "ОтображатьТолькоПросроченные";
	ИмяПараметраНаКонтроле = "ОтображатьТолькоНаКонтроле";
	
	Если Параметры.ОтображатьТолькоПросроченные Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Список,
			ИмяПараметраПросроченные,
			Истина,
			Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Список,
			ИмяПараметраНаКонтроле,
			Ложь,
			Ложь);
		
	ИначеЕсли ПоказатьСнятыеСКонтроля Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Список,
			ИмяПараметраПросроченные,
			Ложь,
			Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Список,
			ИмяПараметраНаКонтроле,
			Ложь,
			Ложь);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Список,
			ИмяПараметраПросроченные,
			Ложь,
			Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Список,
			ИмяПараметраНаКонтроле,
			Истина,
			Истина);
		
	КонецЕсли;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Список.Параметры.УстановитьЗначениеПараметра("ТекущийПользователь", ТекущийПользователь);
	
	Список.Параметры.УстановитьЗначениеПараметра("СотрудникиПользователя", ПараметрыСеанса.СотрудникиПользователя);
	
	РолиТекущегоПользователя = Сотрудники.РолиТекущегоПользователя();
	Список.Параметры.УстановитьЗначениеПараметра("РолиТекущегоПользователя", РолиТекущегоПользователя);
	
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	Список.Параметры.УстановитьЗначениеПараметра("ТекущаяДата", ТекущаяДатаСеанса);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеЭлементов()
	
	Элементы.КомандаПоказатьСнятыеСКонтроля.Пометка = ПоказатьСнятыеСКонтроля;
	
	Элементы.КомандаПоказатьСнятыеСКонтроля.Видимость = Не Параметры.ОтображатьТолькоПросроченные;
	Элементы.ПоказатьУдаленные.Видимость = Не Параметры.ОтображатьТолькоПросроченные;
	Элементы.Создать.Видимость = Не Параметры.ОтображатьТолькоПросроченные;
	Элементы.СписокКонтекстноеМенюСоздать.Видимость = Не Параметры.ОтображатьТолькоПросроченные;
	Элементы.СписокКонтекстноеМенюСкопировать.Видимость = Не Параметры.ОтображатьТолькоПросроченные;
	
КонецПроцедуры

&НаСервере
Процедура КомандаПоказатьСнятыеСКонтроляНаСервере()
	
	ПоказатьСнятыеСКонтроля = Не ПоказатьСнятыеСКонтроля;
	
	ОбновитьОтображениеЭлементов();
	УстановитьПараметрыСписка();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	Если Параметры.ОтображатьТолькоПросроченные Тогда
		Заголовок = НСтр("ru = 'На контроле (просрочено)'");
	Иначе
		Заголовок = НСтр("ru = 'На контроле'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Контроль.УстановитьУсловноеОформлениеСнятСКонтроля(Список.УсловноеОформление);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущуюСтроку(КонтрольСсылка)
	
	АктуальныйКлюч = АктуальныйКлюч(КонтрольСсылка, Параметры.ОтображатьТолькоПросроченные, ПоказатьСнятыеСКонтроля);
	Если АктуальныйКлюч = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Список.ТекущаяСтрока = АктуальныйКлюч;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция АктуальныйКлюч(Знач КонтрольСсылка, Знач ОтображатьТолькоПросроченные, Знач ПоказатьСнятыеСКонтроля)
	
	АктуальныйКлюч = Неопределено;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	ДанныеКонтроляВРеестре =
		РегистрыСведений.РеестрКонтролей.ДанныеКонтроляВРеестре(ТекущийПользователь, КонтрольСсылка);
	Если ДанныеКонтроляВРеестре = Неопределено Тогда
		Возврат АктуальныйКлюч;
	КонецЕсли;
	
	СтруктураКлюча = Новый Структура;
	СтруктураКлюча.Вставить("Пользователь", ДанныеКонтроляВРеестре.Пользователь);
	СтруктураКлюча.Вставить("ДатаСортировки", ДанныеКонтроляВРеестре.ДатаСортировки);
	СтруктураКлюча.Вставить("Основание", ДанныеКонтроляВРеестре.Основание);
	АктуальныйКлюч = РегистрыСведений.РеестрКонтролей.СоздатьКлючЗаписи(СтруктураКлюча);
	
	Возврат АктуальныйКлюч;
	
КонецФункции

#КонецОбласти
