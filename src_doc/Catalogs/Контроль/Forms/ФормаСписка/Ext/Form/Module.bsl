#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработатьКлючНазначенияИспользования();
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработатьКлючНазначенияИспользования()
	
	Если КлючНазначенияИспользования = ОбщегоНазначенияДокументооборотКлиентСервер.КлючУдаленныеОбъекты() Тогда
		
		АвтоЗаголовок = Ложь;
		Заголовок = НСтр("ru = 'Удаленные контроли'");
		
		Элементы.ДатаУдаления.Видимость = Истина;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"ПометкаУдаления",
			Истина);
		
		Если Не ПользователиСерверПовтИсп.ЭтоПолноправныйПользовательИБ() Тогда
			ТекущиеКонтролеры = Контроль.ТекущиеКонтролеры();
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список,
				"Контролер",
				ТекущиеКонтролеры);
		КонецЕсли;
		
		// Порядок.
		Порядок = Список.КомпоновщикНастроек.Настройки.Порядок;
		Порядок.ИдентификаторПользовательскойНастройки = "ОсновнойПорядок";
		
		Порядок.Элементы.Очистить();
		
		ЭлементПорядка = Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных("ДатаУдаления");
		ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Убыв;
		ЭлементПорядка.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		ЭлементПорядка.Использование = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Контроль.УстановитьУсловноеОформлениеСнятСКонтроля(Список.УсловноеОформление);
	
КонецПроцедуры

#КонецОбласти