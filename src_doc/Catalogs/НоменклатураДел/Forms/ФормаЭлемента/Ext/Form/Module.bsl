#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПротоколированиеРаботыСотрудников.ЗаписатьОткрытие(Объект.Ссылка);
	
	Если Объект.Ссылка.Пустая() Тогда
		Если Не ЗначениеЗаполнено(Объект.Год) Тогда
			Объект.Год = Год(ТекущаяДатаСеанса());
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.СрокХранения) Тогда 
			Объект.СрокХранения = 0;
		КонецЕсли;	
		
		Если Не ЗначениеЗаполнено(Объект.Организация) Тогда 
			Если ЗначениеЗаполнено(Объект.Раздел) Тогда 
				Объект.Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Раздел, "Организация");
			Иначе 	
				Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
	
	СрокХраненияЗаполнитьСписокВыбора(Элементы.СрокХранения.СписокВыбора, Объект.КатегорияДела);
	
	ФормаДокументовПриОткрытии = Объект.ФормаДокументов;
	
	// Инструкции
	ПоказыватьИнструкции = ПолучитьФункциональнуюОпцию("ИспользоватьИнструкции");
	ПолучитьИнструкции();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьПодписьЛет();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПроверятьТомаГибридногоДела = Не Объект.Ссылка.Пустая()
		И Объект.ФормаДокументов = ПредопределенноеЗначение("Перечисление.ВариантыФормДокументов.БумажнаяИлиЭлектронная")
		И Объект.ФормаДокументов <> ФормаДокументовПриОткрытии;
		
	Если ПроверятьТомаГибридногоДела
		И Не ПараметрыЗаписи.Свойство("НеПоказыватьПредупреждениеПоГибриднымДелам") Тогда
		
		РеквизитыНоменклатурыДел = Новый Структура;
		РеквизитыНоменклатурыДел.Вставить("НоменклатураДел", Объект.Ссылка);
		РеквизитыНоменклатурыДел.Вставить("ФормаДокументов", Объект.ФормаДокументов);
		
		ПараметрыПроверкиГибридногоДела = Новый Структура;
		ПараметрыПроверкиГибридногоДела.Вставить("ПоказыватьПредупреждение", Ложь);
		ПараметрыПроверкиГибридногоДела.Вставить("ПараметрыФормыВопроса", Неопределено);
		ПараметрыПроверкиГибридногоДела.Вставить("РеквизитыНоменклатурыДел", РеквизитыНоменклатурыДел);
		
		Делопроизводство.ПроверитьТомаГибридногоДела(ПараметрыПроверкиГибридногоДела);
		Если ПараметрыПроверкиГибридногоДела.ПоказыватьПредупреждение Тогда
			
			Отказ = Истина;
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПредупрежденияПриПроверкеГибридногоДела",
				ЭтотОбъект, ПараметрыЗаписи);
			ОткрытьФорму("ОбщаяФорма.РасширенныйВопрос", ПараметрыПроверкиГибридногоДела.ПараметрыФормыВопроса,,,,,
				ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
			
	КонецЕсли;
	
	ОбщегоНазначенияДокументооборотКлиент.УдалитьПустыеСтрокиТаблицы(
		Объект.ВидыДокументов, "ВидДокумента");
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПротоколированиеРаботыСотрудников.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыСотрудников.ЗаписатьИзменение(Объект.Ссылка);
	
	Если Не ПараметрыЗаписи.ЭтоНовыйОбъект Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Дела.Ссылка
		|ИЗ
		|	Справочник.ДелаХраненияДокументов КАК Дела
		|ГДЕ
		|	НЕ Дела.ПометкаУдаления
		|	И НЕ Дела.ДелоЗакрыто
		|	И Дела.НоменклатураДел.Индекс = &Индекс
		|	И Дела.Организация = &Организация
		|	И (Дела.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ Дела.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)
		|				И ГОД(Дела.ДатаОкончания) >= &Год)";
	
	Запрос.УстановитьПараметр("Индекс", ТекущийОбъект.Индекс);
	Запрос.УстановитьПараметр("Организация", ТекущийОбъект.Организация);
	Запрос.УстановитьПараметр("Год", ТекущийОбъект.Год);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		ДелоОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		НоваяСтрока = ДелоОбъект.ОтноситсяКНоменклатуреДел.Добавить();
		НоваяСтрока.НоменклатураДел = ТекущийОбъект.Ссылка;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДелоОбъект);
		РазблокироватьДанныеДляРедактирования(Выборка.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзмененаНоменклатураДел", Объект.Ссылка, ЭтаФорма);
	
	Если ПараметрыЗаписи.Свойство("Закрыть") Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)

	Если Настройки["ПоказыватьИнструкции"] <> Неопределено
		И ПолучитьФункциональнуюОпцию("ИспользоватьИнструкции") Тогда
		ПолучитьИнструкции();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбщегоНазначенияДокументооборот.ПоказатьНадписьПометкиУдаления(
		ЭтотОбъект, ТекущийОбъект.ПометкаУдаления);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СрокХраненияПриИзменении(Элемент)
	
	Если ТипЗнч(Объект.СрокХранения) = Тип("Строка")
			И ОбщегоНазначенияКлиентСервер.ЭтоЧисло(Объект.СрокХранения) Тогда
		Объект.СрокХранения = Число(Объект.СрокХранения);
	КонецЕсли;
	
	ЗаполнитьПодписьЛет();
	
КонецПроцедуры

&НаКлиенте
Процедура СрокХраненияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = НСтр("ru = '<Произвольный>'") Тогда 
		
		СтандартнаяОбработка = Ложь;
		Объект.СрокХранения = "";
		
	ИначеЕсли ВыбранноеЗначение = НСтр("ru = '1 год'") 
		  Или ВыбранноеЗначение = НСтр("ru = '3 года'") 
		  Или ВыбранноеЗначение = НСтр("ru = '5 лет'") 
		  Или ВыбранноеЗначение = НСтр("ru = '10 лет'") 
		  Или ВыбранноеЗначение = НСтр("ru = '15 лет'") 
		  Или ВыбранноеЗначение = НСтр("ru = '50 лет'") 
		  Или ВыбранноеЗначение = НСтр("ru = '75 лет'") Тогда 
		
		СтандартнаяОбработка = Ложь;
		Объект.СрокХранения = Число(Лев(ВыбранноеЗначение, 2));
		ЗаполнитьПодписьЛет();
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СрокОперативногоХраненияПриИзменении(Элемент)
	
	ЗаполнитьПодписьЛет();
	
КонецПроцедуры

&НаКлиенте
Процедура КатегорияДелаПриИзменении(Элемент)
	
	Если Объект.КатегорияДела = ПредопределенноеЗначение("Перечисление.КатегорииДел.ВременноеДо10") Тогда
		
		Объект.СрокХранения = "";
		
	ИначеЕсли Объект.КатегорияДела = ПредопределенноеЗначение("Перечисление.КатегорииДел.ВременноеСвыше10") Тогда
		
		Объект.СрокХранения = "";	
		
	ИначеЕсли Объект.КатегорияДела = ПредопределенноеЗначение("Перечисление.КатегорииДел.Постоянное") Тогда
		
		Объект.СрокХранения = НСтр("ru = 'Постоянно'");
		
	КонецЕсли;	
	
	СрокХраненияЗаполнитьСписокВыбора(Элементы.СрокХранения.СписокВыбора, Объект.КатегорияДела);
	
КонецПроцедуры

&НаКлиенте
Процедура РазделНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПоказыватьДелаЗа", Объект.Год);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("ТекущаяСтрока", Объект.Раздел);
	
	ОткрытьФорму("Справочник.РазделыНоменклатурыДел.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнструкцияПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСИнструкциямиКлиент.ОткрытьСсылку(ДанныеСобытия.Href, ДанныеСобытия.Element, Элемент.Документ);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура("ТекущаяСтрока", Элементы.ВидыДокументов.ТекущиеДанные.ВидДокумента);
	ОткрытьФорму("Справочник.ВидыДокументов.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыДокументовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
 		Элементы.ВидыДокументов.ТекущиеДанные.ВидДокумента = 
			ПредопределенноеЗначение("Справочник.ВидыДокументов.ПустаяСсылка");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = СформироватьДанныеВыбораВидаДокументов(Текст);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = СформироватьДанныеВыбораВидаДокументов(Текст);
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ФормаДокументовПриИзменении(Элемент)
	
	ПримечаниеДляЭД = НСтр("ru = '(ЭД)'");
	ПримечаниеДляГД = НСтр("ru = '(ГД)'");
	Если Объект.ФормаДокументов =
		ПредопределенноеЗначение("Перечисление.ВариантыФормДокументов.Электронная") Тогда 
		Если СтрНайти(Объект.Примечание, ПримечаниеДляЭД) = 0 Тогда 
			Объект.Примечание = Объект.Примечание + ПримечаниеДляЭД;
		КонецЕсли;
	ИначеЕсли СтрНайти(Объект.Примечание, ПримечаниеДляЭД) > 0 Тогда 
		Объект.Примечание = СтрЗаменить(Объект.Примечание, ПримечаниеДляЭД, "");
	КонецЕсли;
	
	Если Объект.ФормаДокументов =
		ПредопределенноеЗначение("Перечисление.ВариантыФормДокументов.БумажнаяИлиЭлектронная") Тогда 
		Если СтрНайти(Объект.Примечание, ПримечаниеДляГД) = 0 Тогда 
			Объект.Примечание = Объект.Примечание + ПримечаниеДляГД;
		КонецЕсли;
		ПоказатьИнформациюПоГибриднымДелам();
	ИначеЕсли СтрНайти(Объект.Примечание, ПримечаниеДляГД) > 0 Тогда 
		Объект.Примечание = СтрЗаменить(Объект.Примечание, ПримечаниеДляГД, "");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)
	
	ЗаписатьИЗакрытьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьИнструкции(Команда)
	
	ПоказыватьИнструкции = Не ПоказыватьИнструкции;
	ПолучитьИнструкции();
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаписатьИЗакрытьНаКлиенте()

	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("Закрыть", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПредупрежденияПриПроверкеГибридногоДела(Ответ, ПараметрыЗаписи) Экспорт
	
	Если Ответ <> "Да" Тогда
		Возврат;
	КонецЕсли;
	ПараметрыЗаписи.Вставить("НеПоказыватьПредупреждениеПоГибриднымДелам");
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СрокХраненияЗаполнитьСписокВыбора(СписокВыбора, КатегорияДела)
	
	СписокВыбора.Очистить();
	
	Если КатегорияДела = ПредопределенноеЗначение("Перечисление.КатегорииДел.ВременноеДо10") Тогда
		
		СписокВыбора.Добавить(НСтр("ru = '1 год'"));
		СписокВыбора.Добавить(НСтр("ru = '3 года'"));
		СписокВыбора.Добавить(НСтр("ru = '5 лет'"));
		СписокВыбора.Добавить(НСтр("ru = '10 лет'"));
		СписокВыбора.Добавить(НСтр("ru = 'До минования надобности'"));
		СписокВыбора.Добавить(НСтр("ru = 'До замены новыми'"));
		СписокВыбора.Добавить(НСтр("ru = '<Произвольный>'"));
		
	ИначеЕсли КатегорияДела = ПредопределенноеЗначение("Перечисление.КатегорииДел.ВременноеСвыше10") Тогда
		
		СписокВыбора.Добавить(НСтр("ru = '15 лет'"));
		СписокВыбора.Добавить(НСтр("ru = '50 лет'"));
		СписокВыбора.Добавить(НСтр("ru = '75 лет'"));
		СписокВыбора.Добавить(НСтр("ru = 'До замены новыми'"));
		СписокВыбора.Добавить(НСтр("ru = '<Произвольный>'"));
		
	ИначеЕсли КатегорияДела = ПредопределенноеЗначение("Перечисление.КатегорииДел.Постоянное") Тогда
		
		СписокВыбора.Добавить(НСтр("ru = 'Постоянно'"));
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПодписьЛет()
	
	Если ТипЗнч(Объект.СрокХранения) = Тип("Число") Тогда 
		Элементы.ДекорацияЛет.Заголовок = "(" + ДелопроизводствоКлиентСервер.ПодписьЛет(Объект.СрокХранения) + ")";
	Иначе
		Элементы.ДекорацияЛет.Заголовок = "";
	КонецЕсли;	
	
	Элементы.ДекорацияЛетОперативного.Заголовок = 
		"(" + ДелопроизводствоКлиентСервер.ПодписьЛет(Объект.СрокОперативногоХранения) + ")";
	
КонецПроцедуры	

&НаСервере
Процедура ПолучитьИнструкции()
	
	РаботаСИнструкциями.ПолучитьИнструкции(ЭтотОбъект, 55, 85);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьДанныеВыбораВидаДокументов(Текст)
	
	ДанныеВыбора = Новый СписокЗначений;
	Запрос = Новый Запрос;
	Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ВидыДокументов.Ссылка
			|ИЗ
			|	Справочник.ВидыДокументов КАК ВидыДокументов
			|ГДЕ
			|	ВидыДокументов.Наименование ПОДОБНО &Текст"; 
				   
	Запрос.УстановитьПараметр("Текст", Текст + "%");

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 (%2)'"), Выборка.Ссылка, ТипЗнч(Выборка.Ссылка)));
	КонецЦикла;
		
	Возврат ДанныеВыбора;

КонецФункции

&НаКлиенте
Процедура ПоказатьИнформациюПоГибриднымДелам()
	
	НастройкиИнформированияПриВыбореГибридныхДел =
		Делопроизводство.ПолучитьНастройкиИнформированияПриРаботеСГибриднымиДелами();
		
	ПоказыватьПредупреждение =
		НастройкиИнформированияПриВыбореГибридныхДел.ПоказыватьПредупреждениеПриВыбореГибридногоДела;
	Если Не ЗначениеЗаполнено(ПоказыватьПредупреждение) Тогда
		ПоказыватьПредупреждение = Истина;
	КонецЕсли;
	
	Если ПоказыватьПредупреждение Тогда
			
		ВариантыОтвета = Новый СписокЗначений;
		ВариантыОтвета.Добавить("Да", НСтр("ru = 'Понятно'"));
			
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Гибридные дела'"));
		ПараметрыФормы.Вставить("ТекстВопроса", НСтр("ru = 'В соответствии с рекомендациями Росархива,
		|электронные документы составляют первый (отдельный) том дела, документы на бумажном носителе формируются во второй и последующие тома.'"));
		ПараметрыФормы.Вставить("СписокДоступныхВариантов", ВариантыОтвета);
		ПараметрыФормы.Вставить("ВариантОтветаПоУмолчанию", "Да");
		ПараметрыФормы.Вставить("КлючПерсональнойНастройки", НСтр("ru = 'НастройкиИнформированияПриВыбореГибридныхДел'"));
		ПараметрыФормы.Вставить("ИмяПерсональнойНастройки", НСтр("ru = 'ПоказыватьПредупреждениеПриВыбореГибридногоДела'"));
		
		ОткрытьФорму("ОбщаяФорма.РасширенныйВопрос", ПараметрыФормы,,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
