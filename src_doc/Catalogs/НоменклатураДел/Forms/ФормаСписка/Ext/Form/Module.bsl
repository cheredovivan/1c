#Область ОписаниеПеременных

&НаКлиенте
Перем ПерваяАктивизация;

&НаКлиенте
Перем ПодменюПечатьДоступностьБСП;

&НаКлиенте
Перем ПодменюПечатьЗаглушкаВидимостьБСП;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриСозданииНаСервереРедакцииКонфигурации();	
	
	ПапкаПриОткрытии = Справочники.РазделыНоменклатурыДел.ПустаяСсылка();
	Список.Параметры.УстановитьЗначениеПараметра("Раздел", ПапкаПриОткрытии);
	
	ПоказыватьДелаЗа = Год(ТекущаяДатаСеанса());
	Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	УстановитьОтбор();
	
	ИспользоватьУчетПоОрганизациям = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям");
	
	// Отображение помеченных на удаление.
	ОбщегоНазначенияДокументооборотКлиентСервер.УстановитьОтборПоказыватьУдаленные(
		Список, ПоказыватьУдаленные, Элементы.ПоказыватьУдаленные);	
	ОбщегоНазначенияДокументооборотКлиентСервер.УстановитьОтборПоказыватьУдаленные(
		Разделы, ПоказыватьУдаленные, Элементы.ПоказыватьУдаленные);	
	
	ОбщегоНазначенияДокументооборот.УстановитьУсловноеОформлениеСписка(Список.УсловноеОформление);
	ОбщегоНазначенияДокументооборот.УстановитьУсловноеОформлениеСписка(Разделы.УсловноеОформление);

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	МК_НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьОтборСписка(Настройки);
	
	ОбщегоНазначенияДокументооборотКлиентСервер.УстановитьОтборПоказыватьУдаленные(
		Список, ПоказыватьУдаленные, Элементы.ПоказыватьУдаленные);
	ОбщегоНазначенияДокументооборотКлиентСервер.УстановитьОтборПоказыватьУдаленные(
		Разделы, ПоказыватьУдаленные, Элементы.ПоказыватьУдаленные);
	
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(Элементы.ОрганизацияОтбор, Организация);
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(Элементы.ПодразделениеОтбор, Подразделение);
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(Элементы.ГрифДоступаОтбор, ГрифДоступа);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьРазделыНоменклатурыДел" И (Параметр = ПоказыватьДелаЗа ИЛИ ПоказыватьДелаЗа = 0)  Тогда 
		Элементы.Разделы.Обновить();
	КонецЕсли;	
	
	Если ИмяСобытия = "СкопированаНоменклатураДел" И (Параметр = ПоказыватьДелаЗа ИЛИ ПоказыватьДелаЗа = 0)  Тогда 
		Элементы.Разделы.Обновить();
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()
	
	#Если МобильныйКлиент Тогда
		МК_НастроитьЭлементыФормы();
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказыватьДелаЗаПриИзменении(Элемент)
	
	УстановитьОтбор();
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаДокументовОтборПриИзменении(Элемент)
	
	УстановитьОтбор();
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(Элемент, ФормаДокументов);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОтборПриИзменении(Элемент)
	
	УстановитьОтбор();
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(Элемент, Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОтборПриИзменении(Элемент)
	
	УстановитьОтбор();
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(Элемент, Подразделение);
	
КонецПроцедуры

&НаКлиенте
Процедура ГрифДоступаОтборПриИзменении(Элемент)
	
	УстановитьОтбор();
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(Элемент, ГрифДоступа);
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыПриАктивизацииСтроки(Элемент)
	
	Если ПерваяАктивизация = Истина Или ПерваяАктивизация = Неопределено Тогда
		ПерваяАктивизация = Ложь;
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ОбработкаОжидания", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	#Если МобильныйКлиент Тогда
		СтандартнаяОбработка = Ложь;
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ПараметрыФормы = Новый Структура;
	
	Если Копирование Тогда 
		ПараметрыФормы.Вставить("ЗначениеКопирования", Элементы.Разделы.ТекущаяСтрока);
	Иначе 
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Родитель", Элементы.Разделы.ТекущаяСтрока);
		ЗначенияЗаполнения.Вставить("Год", ПоказыватьДелаЗа);
		
		Если ИспользоватьУчетПоОрганизациям Тогда 
			ЗначенияЗаполнения.Вставить("Организация", Организация);
		КонецЕсли;
		
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.РазделыНоменклатурыДел.ФормаОбъекта", ПараметрыФормы, Элементы.Разделы);
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)

	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") 
		И ПараметрыПеретаскивания.Значение.Количество() > 0 
		И ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("СправочникСсылка.НоменклатураДел") Тогда
		СтандартнаяОбработка = Ложь;
			
		Если ИзменитьРаздел(ПараметрыПеретаскивания.Значение, Строка) Тогда 
			Элементы.Список.Обновить();
			
			Если ПараметрыПеретаскивания.Значение.Количество() = 1 Тогда
				ПолноеОписание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Номенклатура дел ""%1"" перенесена в раздел ""%2""'"),
					ПараметрыПеретаскивания.Значение[0],
					Строка);
				
				ПоказатьОповещениеПользователя(
					НСтр("ru = 'Номенклатура дел перенесена.'"),
					ПолучитьНавигационнуюСсылку(ПараметрыПеретаскивания.Значение[0]),
					ПолноеОписание,
					БиблиотекаКартинок.Информация32);
			Иначе
				ПолноеОписание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Элементы номенклатуры дел (%1 шт.) перенесены в раздел ""%2""'"),
					ПараметрыПеретаскивания.Значение.Количество(),
					Строка);
				
				ПоказатьОповещениеПользователя(
					НСтр("ru = 'Элементы номенклатуры дел перенесены.'"),
					,
					ПолноеОписание,
					БиблиотекаКартинок.Информация32);
			КонецЕсли;

		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	ТекущийРаздел    = Элементы.Разделы.ТекущаяСтрока;
	ТекущийЭлементНД = Элементы.Список.ТекущаяСтрока;
	
	Отказ = Истина;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Раздел", ТекущийРаздел);
	ЗначенияЗаполнения.Вставить("Год", ПоказыватьДелаЗа);
	Если ИспользоватьУчетПоОрганизациям Тогда 
		ЗначенияЗаполнения.Вставить("Организация", Организация);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	Если Копирование Тогда 
		ПараметрыФормы.Вставить("ЗначениеКопирования", ТекущийЭлементНД);
	КонецЕсли;	
	
	Открытьформу("Справочник.НоменклатураДел.ФормаОбъекта", ПараметрыФормы, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	ОбщегоНазначенияДокументооборотКлиентСервер.УстановитьОтборПоказыватьУдаленные(
		Список, ПоказыватьУдаленные, Элементы.ПоказыватьУдаленные);	
	ОбщегоНазначенияДокументооборотКлиентСервер.УстановитьОтборПоказыватьУдаленные(
		Разделы, ПоказыватьУдаленные, Элементы.ПоказыватьУдаленные);	
	
КонецПроцедуры

&НаКлиенте
Процедура Скопировать(Команда)
	
	ПараметрыФормы = Новый Структура("Год, Организация", ПоказыватьДелаЗа, Организация);
	ОткрытьФорму("Справочник.НоменклатураДел.Форма.ФормаКопирования", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭлемент(Команда)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Раздел", Элементы.Разделы.ТекущаяСтрока);
	ЗначенияЗаполнения.Вставить("Год", ПоказыватьДелаЗа);
	Если ИспользоватьУчетПоОрганизациям Тогда 
		ЗначенияЗаполнения.Вставить("Организация", Организация);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	Открытьформу("Справочник.НоменклатураДел.ФормаОбъекта", ПараметрыФормы, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРаздел(Команда)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Родитель", Элементы.Разделы.ТекущаяСтрока);
	ЗначенияЗаполнения.Вставить("Год", ПоказыватьДелаЗа);
	Если ИспользоватьУчетПоОрганизациям Тогда 
		ЗначенияЗаполнения.Вставить("Организация", Организация);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОткрытьФорму("Справочник.РазделыНоменклатурыДел.ФормаОбъекта", ПараметрыФормы, Элементы.Разделы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРазделы(Команда)
	
	ПараметрыФормы = Новый Структура("Год, Организация", ПоказыватьДелаЗа, Организация);
	ОткрытьФорму("Справочник.НоменклатураДел.Форма.ФормаЗаполненияРазделов", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьНоменклатурыДел(Команда)
	
	УправлениеПечатьюКлиентДокументооборот.ПечатьНоменклатурыДел(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьИтоговойЗаписи(Команда)
	
	УправлениеПечатьюКлиентДокументооборот.ПечатьИтоговойЗаписиНоменклатурыДел(ЭтотОбъект);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	// Документооборот
	ПодменюПечатьБСП = Элементы.Найти("ПодменюПечать");
	ПодменюПечатьЗаглушкаБСП = Элементы.Найти("ПодменюПечатьЗаглушка");
	Если ПодменюПечатьБСП <> Неопределено
		И ПодменюПечатьДоступностьБСП <> Неопределено Тогда
		ПодменюПечатьБСП.Доступность = ПодменюПечатьДоступностьБСП;
		ПодменюПечатьДоступностьБСП = Неопределено;
	КонецЕсли;
	Если ПодменюПечатьЗаглушкаБСП <> Неопределено
		И ПодменюПечатьЗаглушкаВидимостьБСП <> Неопределено Тогда
		ПодменюПечатьЗаглушкаБСП.Видимость = ПодменюПечатьЗаглушкаВидимостьБСП;
		ПодменюПечатьЗаглушкаВидимостьБСП = Неопределено;
	КонецЕсли;
	// Конец Документооборот
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
	// Документооборот
	Элементы.ПечатьИтоговойЗаписи.Видимость = Истина;
	Элементы.ПечатьНоменклатурыДел.Видимость = Истина;
	Если ПодменюПечатьБСП <> Неопределено Тогда
		ПодменюПечатьДоступностьБСП = ПодменюПечатьБСП.Доступность;
		ПодменюПечатьБСП.Доступность = Истина;
	КонецЕсли;
	Если ПодменюПечатьЗаглушкаБСП <> Неопределено Тогда
		ПодменюПечатьЗаглушкаВидимостьБСП = ПодменюПечатьЗаглушкаБСП.Видимость;
		ПодменюПечатьЗаглушкаБСП.Видимость = Ложь;
	КонецЕсли;
	// Конец Документооборот
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаОжидания()
	
	Если Элементы.Разделы.ТекущаяСтрока <> Неопределено Тогда 
		Список.Параметры.УстановитьЗначениеПараметра("Раздел", Элементы.Разделы.ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборСписка(ПараметрыОтбора)

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
		ПараметрыОтбора["Организация"] = Неопределено;
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("Год", ПараметрыОтбора["ПоказыватьДелаЗа"]);
	Разделы.Параметры.УстановитьЗначениеПараметра("Год", ПараметрыОтбора["ПоказыватьДелаЗа"]);
	
	Если ЗначениеЗаполнено(ПараметрыОтбора["ФормаДокументов"]) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
			"ФормаДокументов",
			ПараметрыОтбора["ФормаДокументов"]);
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор, "ФормаДокументов");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыОтбора["Организация"]) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
			"Организация",
			ПараметрыОтбора["Организация"]);
			
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Разделы.Отбор,
			"Организация",
			ПараметрыОтбора["Организация"]);
			
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор, "Организация");
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Разделы.Отбор, "Организация");	
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ПараметрыОтбора["Подразделение"]) Тогда 
		
		МассивПодразделений = Делопроизводство.ПолучитьПодразделениеИРодителей(ПараметрыОтбора["Подразделение"]);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Разделы.Отбор,
			"Подразделение",
			МассивПодразделений,
			ВидСравненияКомпоновкиДанных.ВСписке);
			
		МассивПодразделений = Делопроизводство.ПолучитьПодразделениеИПустуюСсылку(ПараметрыОтбора["Подразделение"]);	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
			"Раздел.Подразделение",
			МассивПодразделений,
			ВидСравненияКомпоновкиДанных.ВСписке);

	Иначе 
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор, "Раздел.Подразделение");	
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Разделы.Отбор, "Подразделение");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыОтбора["ГрифДоступа"]) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
			"ГрифДоступа",
			ПараметрыОтбора["ГрифДоступа"]);
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор, "ГрифДоступа");
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьОтбор()
	
	ПараметрыОтбора = Новый Соответствие;
	ПараметрыОтбора.Вставить("ПоказыватьДелаЗа", ПоказыватьДелаЗа);
	ПараметрыОтбора.Вставить("ФормаДокументов",  ФормаДокументов);
	ПараметрыОтбора.Вставить("Организация",      Организация);
	ПараметрыОтбора.Вставить("Подразделение",    Подразделение);
	ПараметрыОтбора.Вставить("ГрифДоступа",      ГрифДоступа);
	УстановитьОтборСписка(ПараметрыОтбора);
	
КонецПроцедуры	

&НаСервереБезКонтекста 
Функция ИзменитьРаздел(МассивНоменклатурДел, НовыйРаздел)
	
	Если МассивНоменклатурДел.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИспользоватьУчетПоОрганизациям = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям");
	Для Каждого НоменклатураДелСсылка Из МассивНоменклатурДел Цикл
		Если ЗначениеЗаполнено(НовыйРаздел) И (НоменклатураДелСсылка.Год <> НовыйРаздел.Год) Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(	
				НСтр("ru = 'Год номенклатуры дел ""%1"" не совпадает с годом раздела'"),
				Строка(НоменклатураДелСсылка));
		КонецЕсли;
		
		Если ИспользоватьУчетПоОрганизациям Тогда 
			Если ЗначениеЗаполнено(НовыйРаздел) И ЗначениеЗаполнено(НоменклатураДелСсылка.Организация) 
				И НоменклатураДелСсылка.Организация <> НовыйРаздел.Организация Тогда 
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(	
					РедакцииКонфигурацииКлиентСервер.ОшибкаПроверкиЗаполненияНоменклатураОрганизации(),
					Строка(НоменклатураДелСсылка));
			КонецЕсли;
		КонецЕсли;	

	КонецЦикла;	
	
	РазделыИзменились = Ложь; 
	НачатьТранзакцию();
	Попытка
		Для Каждого НоменклатураДелСсылка Из МассивНоменклатурДел Цикл
			// Если тот же раздел, то ничего делать не надо
			Если НоменклатураДелСсылка.Раздел = НовыйРаздел Тогда
				Продолжить;
			КонецЕсли;
			
			ЗаблокироватьДанныеДляРедактирования(НоменклатураДелСсылка);
			НоменклатураДелОбъект = НоменклатураДелСсылка.ПолучитьОбъект();
			НоменклатураДелОбъект.Раздел = НовыйРаздел;
			НоменклатураДелОбъект.Записать();                   

			РазделыИзменились = Истина;
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат РазделыИзменились;
	
КонецФункции	

&НаСервере
Процедура ПриСозданииНаСервереРедакцииКонфигурации()
	
	Элементы.ОрганизацияОтбор.Подсказка = РедакцииКонфигурацииКлиентСервер.Организация();
	Элементы.ОрганизацияОтбор.ПодсказкаВвода = РедакцииКонфигурацииКлиентСервер.Организация();
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункцииМобильныйКлиент

&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	Если Не ПараметрыСеанса.ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	Элементы.ПодразделениеОтбор.РастягиватьПоГоризонтали = Истина;
	Элементы.Разделы.ВариантУправленияВысотой = ВариантУправленияВысотойТаблицы.ПоСодержимому;
	Если Не МК_КлиентСервер.ЭтоМобильныйСБольшимЭкраном() Тогда
		Элементы.Разделы.НачальноеОтображениеДерева = НачальноеОтображениеДерева.НеРаскрывать;
	КонецЕсли;
	Элементы.Список.Шапка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

