#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьСтруктуруНоменклатурыДел() Экспорт 
	
	ПараметрыНоменклатурыДел = Новый Структура;
	ПараметрыНоменклатурыДел.Вставить("Год");
	ПараметрыНоменклатурыДел.Вставить("Раздел");
	ПараметрыНоменклатурыДел.Вставить("Организация");
	ПараметрыНоменклатурыДел.Вставить("Индекс");
	ПараметрыНоменклатурыДел.Вставить("ПолноеНаименование");
	ПараметрыНоменклатурыДел.Вставить("СрокХранения");
	ПараметрыНоменклатурыДел.Вставить("КатегорияДела");
	ПараметрыНоменклатурыДел.Вставить("НомераСтатей");
	ПараметрыНоменклатурыДел.Вставить("ОтметкаЭПК");
	ПараметрыНоменклатурыДел.Вставить("Примечание");
	
	Возврат ПараметрыНоменклатурыДел;
	
КонецФункции

Функция СоздатьНоменклатуруДел(СтруктураНоменклатурыДел) Экспорт
	
	НовыйЭлементНД = Справочники.НоменклатураДел.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НовыйЭлементНД, СтруктураНоменклатурыДел);
	НовыйЭлементНД.Записать();
	
	НовыйЭлементДело = Справочники.ДелаХраненияДокументов.СоздатьЭлемент();
	НовыйЭлементДело.НоменклатураДел = НовыйЭлементНД.Ссылка;
	НовыйЭлементДело.ДатаНачала = Дата(НовыйЭлементНД.Год, 1, 1);
	НовыйЭлементДело.НомерТома = 1;
	НовыйЭлементДело.Записать();
	
	Возврат НовыйЭлементНД.Ссылка;
	
КонецФункции

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Наименование");
	Поля.Добавить("Индекс")
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = СокрЛП(Данные.Индекс) + " " + СокрЛП(Данные.Наименование);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Печать.

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "НоменклатураДел") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"НоменклатураДел",
			"Номенклатура дел",
			ПечатьНоменклатурыДел(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Справочник.НоменклатураДел.ПФ_MXL_НоменклатураДел");
			
		КонецЕсли;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИтоговаяЗапись") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ИтоговаяЗапись",
			"Итоговая запись",
			ПечатьИтоговойЗаписи(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Справочник.НоменклатураДел.ПФ_MXL_ИтоговаяЗапись");
			
	КонецЕсли;

	
КонецПроцедуры

Функция ПечатьНоменклатурыДел(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	// Создаем табличный документ и устанавливаем имя параметров печати
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "НоменклатураДел";
	
	Если ПараметрыПечати.Свойство("Подразделение") И ЗначениеЗаполнено(ПараметрыПечати.Подразделение) Тогда
		Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.НоменклатураДел.ПФ_MXL_НоменклатураДелПодразделения");
		Подразделение = ПараметрыПечати.Подразделение;
	Иначе
		Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.НоменклатураДел.ПФ_MXL_НоменклатураДел");
		Подразделение = Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СправочникНоменклатураДел.Индекс КАК Индекс,
			|	СправочникНоменклатураДел.ПолноеНаименование КАК Наименование,
			|	СправочникНоменклатураДел.Раздел КАК Раздел,
			|	СправочникНоменклатураДел.ОтметкаЭПК КАК ОтметкаЭПК,
			|	СправочникНоменклатураДел.НомераСтатей КАК НомераСтатей,
			|	СправочникНоменклатураДел.СрокХранения КАК СрокХранения,
			|	СправочникНоменклатураДел.Примечание КАК Примечание,
			|	Дела.КоличествоТомов
			|ИЗ
			|	Справочник.НоменклатураДел КАК СправочникНоменклатураДел
			|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
			|			КОЛИЧЕСТВО(ДелаХраненияДокументов.Ссылка) КАК КоличествоТомов,
			|			ДелаХраненияДокументов.НоменклатураДел КАК НоменклатураДел
			|		ИЗ
			|			Справочник.ДелаХраненияДокументов КАК ДелаХраненияДокументов
			|		ГДЕ
			|			НЕ ДелаХраненияДокументов.ПометкаУдаления
			|		
			|		СГРУППИРОВАТЬ ПО
			|			ДелаХраненияДокументов.НоменклатураДел) КАК Дела
			|		ПО СправочникНоменклатураДел.Ссылка = Дела.НоменклатураДел
			|ГДЕ
			|	НЕ СправочникНоменклатураДел.ПометкаУдаления
			|	И СправочникНоменклатураДел.Год = &Год
			|	И (СправочникНоменклатураДел.Организация = &Организация
			|			ИЛИ &ИспользоватьУчетПоОрганизациям = ЛОЖЬ
			|			ИЛИ &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
			|	И (СправочникНоменклатураДел.Раздел.Подразделение = &Подразделение
			|			ИЛИ &Подразделение = НЕОПРЕДЕЛЕНО)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Индекс";
	
	Если ЗначениеЗаполнено(Подразделение) Тогда
		Запрос.УстановитьПараметр("Подразделение", ПараметрыПечати.Подразделение);
	Иначе
		Запрос.УстановитьПараметр("Подразделение", Неопределено);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Год", ПараметрыПечати.Год);
	Запрос.УстановитьПараметр("Организация", ПараметрыПечати.Организация);
	Запрос.УстановитьПараметр("ИспользоватьУчетПоОрганизациям", ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям"));
	ВыборкаСтроки = Запрос.Выполнить().Выбрать();
	
	ВыборкаРазделы = Справочники.РазделыНоменклатурыДел.ВыбратьИерархически(,, Новый Структура("Год", ПараметрыПечати.Год), "Индекс Возр");

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка 	 = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока 	 = Макет.ПолучитьОбласть("Строка");
	ОбластьРаздел 	 = Макет.ПолучитьОбласть("Раздел");
	ОбластьПодвал    = Макет.ПолучитьОбласть("Подвал");
	ТабДок.Очистить();

	// заголовок
	ДатаПечати = Дата(ПараметрыПечати.Год, 1, 1);
	
	ОбластьЗаголовок.Параметры.Год = Формат(ПараметрыПечати.Год, "ЧГ=");
	ОбластьЗаголовок.Параметры.НаименованиеПредприятия = 
		Справочники.Организации.ПредставлениеОрганизацииНаДату(ПараметрыПечати.Организация, НачалоГода(ДатаПечати));
	
	МассивДляПолученияФИО = Новый Массив;
	Если Не ЗначениеЗаполнено(Подразделение) Тогда
		
		Руководитель = Справочники.Организации.ОтветственноеЛицо(
			"Руководитель", ПараметрыПечати.Организация, ДатаПечати);
		МассивДляПолученияФИО.Добавить(Руководитель);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Подразделение) Тогда
		РуководительДОУ = СтруктураПредприятия.РуководительПодразделения(ПараметрыПечати.Подразделение);
	Иначе
		РуководительДОУ = Справочники.Организации.ОтветственноеЛицо(
			"РуководительСлужбыДОУ", ПараметрыПечати.Организация, ДатаПечати);
	КонецЕсли;
	
	МассивДляПолученияФИО.Добавить(РуководительДОУ);
	
	ФИОСотрудников = Сотрудники.ПредставленияВДокументахСотрудниковНаДату(МассивДляПолученияФИО, ДатаПечати);
	
	Если ЗначениеЗаполнено(Подразделение) Тогда
		ОбластьЗаголовок.Параметры.НаименованиеПодразделения = Подразделение.Наименование;
	Иначе
		ОбластьЗаголовок.Параметры.Руководитель = ФИОСотрудников[Руководитель];
		ОбластьЗаголовок.Параметры.Дата = ДатаПечати;
		ОбластьЗаголовок.Параметры.ДолжностьРуководителя = Сотрудники.ДолжностьСотрудника(Руководитель);
	КонецЕсли;
	
	ТабДок.Вывести(ОбластьЗаголовок);
	
	// шапка
	ТабДок.Вывести(ОбластьШапка);
	
	// разделы
	Пока ВыборкаРазделы.Следующий() Цикл
		Если ВыборкаРазделы.ПометкаУдаления Тогда 
			Продолжить;
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") = Истина 
		   И ЗначениеЗаполнено(ПараметрыПечати.Организация) 
		   И ВыборкаРазделы.Организация <> ПараметрыПечати.Организация Тогда 
			Продолжить;
		КонецЕсли;
		
		Если ПараметрыПечати.Свойство("Подразделение") 
			И ЗначениеЗаполнено(ПараметрыПечати.Подразделение)
			И ВыборкаРазделы.Подразделение <> ПараметрыПечати.Подразделение Тогда
			Продолжить;
		КонецЕсли;	
		
		ОбластьРаздел.Параметры.Индекс = ВыборкаРазделы.Индекс;
		ОбластьРаздел.Параметры.Наименование = ВыборкаРазделы.Наименование;
		ТабДок.Вывести(ОбластьРаздел);
		
		// строки
		ВыборкаСтроки.Сбросить();
		Пока ВыборкаСтроки.НайтиСледующий(ВыборкаРазделы.Ссылка, "Раздел") Цикл
			ОбластьСтрока.Параметры.Заполнить(ВыборкаСтроки);
			
			НомераСтатей = ВыборкаСтроки.НомераСтатей;
			СрокХранения = ВыборкаСтроки.СрокХранения;
			ОтметкаЭПК 	 = ВыборкаСтроки.ОтметкаЭПК;
			
			Если ТипЗнч(СрокХранения) = Тип("Число") Тогда 
				ОбластьСтрока.Параметры.СрокХраненияНомераСтатей = Строка(СрокХранения) + " "
					+ ДелопроизводствоКлиентСервер.ПодписьЛет(СрокХранения) 
					+ ?(ОтметкаЭПК, " ЭПК", "") 
					+ ?(ЗначениеЗаполнено(НомераСтатей), ", " + НомераСтатей, "");
			Иначе
				ОбластьСтрока.Параметры.СрокХраненияНомераСтатей = СрокХранения 
					+ ?(ОтметкаЭПК, " ЭПК", "")
					+ ?(ЗначениеЗаполнено(НомераСтатей), ", " + НомераСтатей, "");
			КонецЕсли;
			
			ТабДок.Вывести(ОбластьСтрока);
		КонецЦикла;	
	КонецЦикла;
	
	// Подвал	
	Если ЗначениеЗаполнено(Подразделение) Тогда
		РуководительДОУ = СтруктураПредприятия.РуководительПодразделения(ПараметрыПечати.Подразделение);
	Иначе
		РуководительДОУ = Справочники.Организации.ОтветственноеЛицо(
			"РуководительСлужбыДОУ", ПараметрыПечати.Организация, ДатаПечати);
	КонецЕсли;
	
	ОбластьПодвал.Параметры.РуководительДОУ = ФИОСотрудников[РуководительДОУ];
	ДолжностьРуководителяДОУ = Сотрудники.ДолжностьСотрудника(РуководительДОУ);
	
	Если Не ЗначениеЗаполнено(ДолжностьРуководителяДОУ) И ЗначениеЗаполнено(Подразделение) Тогда 
		ДолжностьРуководителяДОУ = НСтр("ru = 'Руководитель структурного подразделения'");
	ИначеЕсли Не ЗначениеЗаполнено(ДолжностьРуководителяДОУ) Тогда
		ДолжностьРуководителяДОУ = НСтр("ru = 'Руководитель службы ДОУ'");
	КонецЕсли;
	
	ОбластьПодвал.Параметры.ДолжностьРуководителяДОУ = ДолжностьРуководителяДОУ;
	ОбластьПодвал.Параметры.Дата = Формат(ТекущаяДатаСеанса(), "ДЛФ=D");
	
	ТабДок.Вывести(ОбластьПодвал);
	
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_НоменклатураДел";
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьИтоговойЗаписи(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	// Создаем табличный документ и устанавливаем имя параметров печати
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ИтоговаяЗапись";
	
	ФормыДокументов = Новый Соответствие;
	ФормыДокументов.Вставить(НСтр("ru = 'Электронных'"), Перечисления.ВариантыФормДокументов.Электронная);
	ФормыДокументов.Вставить(НСтр("ru = 'На бумажном носителе'"), Перечисления.ВариантыФормДокументов.Бумажная);
	
	КатегорииДел = Новый Соответствие;
	КатегорииДел.Вставить(НСтр("ru = 'Временного (до 10 лет включительно)'"), Перечисления.КатегорииДел.ВременноеДо10);
	КатегорииДел.Вставить(НСтр("ru = 'Временного (свыше 10 лет)'"), Перечисления.КатегорииДел.ВременноеСвыше10);
	КатегорииДел.Вставить(НСтр("ru = 'Постоянного'"), Перечисления.КатегорииДел.Постоянное);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.НоменклатураДел.ПФ_MXL_ИтоговаяЗапись");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НоменклатураДелСправочник.КатегорияДела КАК КатегорияДела,
	|	СУММА(1) КАК Всего,
	|	СУММА(ВЫБОР
	|		КОГДА НоменклатураДелСправочник.ОтметкаЭПК
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СОтметкойЭПК,
	|	СУММА(ВЫБОР
	|		КОГДА Дела.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)
	|		И ГОД(Дела.ДатаОкончания) <> &Год
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК Переходящих,
	|	Дела.ФормаДокументов КАК ФормаДокументов
	|ИЗ
	|	Справочник.ДелаХраненияДокументов КАК Дела
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураДел как НоменклатураДелСправочник
	|		По (НоменклатураДелСправочник.Ссылка = Дела.НоменклатураДел)
	|ГДЕ
	|	НоменклатураДелСправочник.Год = &Год
	|	И (НоменклатураДелСправочник.Организация = &Организация
	|	ИЛИ &ИспользоватьУчетПоОрганизациям = ЛОЖЬ)
	|СГРУППИРОВАТЬ ПО
	|	Дела.ФормаДокументов,
	|	НоменклатураДелСправочник.КатегорияДела";
	Запрос.УстановитьПараметр("Год", ПараметрыПечати.Год);
	Запрос.УстановитьПараметр("Организация", ПараметрыПечати.Организация);
	Запрос.УстановитьПараметр("ИспользоватьУчетПоОрганизациям", ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям"));
	
	Выборка = Запрос.Выполнить().Выгрузить();
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка 	 = Макет.ПолучитьОбласть("Шапка");
	ОбластьФормаДокументов 	 = Макет.ПолучитьОбласть("ФормаДокументов");
	ОбластьСтрока 	 = Макет.ПолучитьОбласть("Строка");
	ОбластьИтого	 = Макет.ПолучитьОбласть("Итого");
	ОбластьПодвал 	 = Макет.ПолучитьОбласть("Подвал");
	ТабДок.Очистить();

	// заголовок
	ОбластьЗаголовок.Параметры.Год = Формат(ПараметрыПечати.Год, "ЧГ=");
	ТабДок.Вывести(ОбластьЗаголовок);
	
	// шапка
	ТабДок.Вывести(ОбластьШапка);
	
	// строки
	Для Каждого ФормаДокументов Из ФормыДокументов Цикл
	
		ОбластьФормаДокументов.Параметры.ФормаДокументов = ФормаДокументов.Ключ;
		ТабДок.Вывести(ОбластьФормаДокументов);
		
		Для Каждого КатегорияДел Из КатегорииДел Цикл
			ОтборДел = Новый Структура("ФормаДокументов, КатегорияДела", ФормаДокументов.Значение, КатегорияДел.Значение);
			Дела = Выборка.НайтиСтроки(ОтборДел);
			Если Дела.Количество() Тогда 
				ОбластьСтрока.Параметры.Заполнить(Дела[0]);
			Иначе
				ОбластьСтрока.Параметры.Всего = "";
				ОбластьСтрока.Параметры.Переходящих = "";
				ОбластьСтрока.Параметры.СОтметкойЭПК = "";
			КонецЕсли;
			ОбластьСтрока.Параметры.КатегорияДелаСтр = КатегорияДел.Ключ;
			ТабДок.Вывести(ОбластьСтрока);
		КонецЦикла;
	
	КонецЦикла;
	
	ОбластьИтого.Параметры.Итого = Выборка.Итог("Всего");
	ОбластьИтого.Параметры.ИтогоПереходящих = Выборка.Итог("Переходящих");
	ОбластьИтого.Параметры.ИтогоСОтметкойЭПК = Выборка.Итог("СОтметкойЭПК");
	ТабДок.Вывести(ОбластьИтого);
	
	// подвал
	ДатаПодписи = ТекущаяДатаСеанса();
	РуководительСлужбыДОУ = Справочники.Организации.ОтветственноеЛицо(
		"РуководительСлужбыДОУ", ПараметрыПечати.Организация, ДатаПодписи);
	
	Если ЗначениеЗаполнено(РуководительСлужбыДОУ) Тогда
		ОбластьПодвал.Параметры.РуководительДОУ = ОбщегоНазначенияДокументооборот.ЗначениеРеквизитаОбъектаВПривилегированномРежиме(
			РуководительСлужбыДОУ, "ПредставлениеВДокументах");
	Иначе
		ОбластьПодвал.Параметры.РуководительДОУ = "__________________";
	КонецЕсли;
	
	ДолжностьРуководителяДОУ = Сотрудники.ДолжностьСотрудника(РуководительСлужбыДОУ);
	Если Не ЗначениеЗаполнено(ДолжностьРуководителяДОУ) Тогда
		ДолжностьРуководителяДОУ = НСтр("ru = 'Руководитель службы ДОУ'");
	КонецЕсли;
	
	ОбластьПодвал.Параметры.ДолжностьРуководителяДОУ = ДолжностьРуководителяДОУ;
	
	ОбластьПодвал.Параметры.РаботникАрхива = "__________________";
	ОбластьПодвал.Параметры.ДолжностьРаботникаАрхива = "_____________________";
	
	ОбластьПодвал.Параметры.ДатаПодписи = ДатаПодписи;
	ТабДок.Вывести(ОбластьПодвал);
	
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ИтоговаяЗапись";
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Возврат ТабДок;
	
КонецФункции

// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - ТаблицаЗначений - Таблица с командами заполнения. Для изменения.
//       См. описание 1 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
КонецПроцедуры

#КонецОбласти

#КонецЕсли

