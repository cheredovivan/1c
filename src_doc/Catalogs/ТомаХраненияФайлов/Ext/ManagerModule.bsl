#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Находит максимальный порядок среди томов
Функция НайтиМаксимальныйПорядок(Родитель) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	МАКСИМУМ(Тома.ПорядокЗаполнения) КАК МаксимальныйНомер
	               |ИЗ
	               |	Справочник.ТомаХраненияФайлов КАК Тома
	               |ГДЕ
	               |	Тома.Родитель = &Родитель";
	Запрос.Параметры.Вставить("Родитель", Родитель); // только в своей группе проверяем уникальность
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если Выборка.МаксимальныйНомер = Null Тогда
			Возврат 0;
		Иначе
			Возврат Число(Выборка.МаксимальныйНомер);
		КонецЕсли;		
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции // НайтиМаксимальныйПорядок

// Возвращает реквизиты объекта, которые разрешается редактировать
// с помощью обработки группового изменения реквизитов.
//
// Возвращаемое значение:
//  Массив - список имен реквизитов объекта.
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	РедактируемыеРеквизиты = Новый Массив;
	РедактируемыеРеквизиты.Добавить("Комментарий");
	
	Возврат РедактируемыеРеквизиты;
	
КонецФункции

// Выполняет поиск и создание группы томов по умолчанию.
//
Процедура ДобавитьГруппуТомовПоУмолчанию() Экспорт
	
	ИмяГруппыПоУмолчанию = ИмяГруппыПоУмолчанию();
	
	Группа = НайтиПоНаименованию(ИмяГруппыПоУмолчанию, Истина);
	Если Не ЗначениеЗаполнено(Группа) Тогда
		Группа = СоздатьГруппу();
		Группа.Наименование = ИмяГруппыПоУмолчанию;

		ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные(Группа);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяГруппыПоУмолчанию()
	
	ИмяГруппыПоУмолчанию = 
		НСтр("ru='Группа по умолчанию'", МультиязычностьСервер.СведенияОЯзыках().ОсновнойЯзык);

	Возврат ИмяГруппыПоУмолчанию;
	
КонецФункции

#КонецОбласти

#КонецЕсли