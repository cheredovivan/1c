// @strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбщегоНазначенияБЭД.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	СписокКомандДоверитель.Очистить();
	СписокКомандПредставитель.Очистить();
	ШаблонПредставленияКоманды = НСтр("ru='Выбрать %1';");
	Для Каждого Тип Из Метаданные.ОпределяемыеТипы.Организация.Тип.Типы() Цикл
		МетаданныеОбъекта = Метаданные.НайтиПоТипу(Тип);
		Если МетаданныеОбъекта = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СвойстваМетаданных = Новый Структура("ПредставлениеОбъекта, Синоним", "", "");
		ЗаполнитьЗначенияСвойств(СвойстваМетаданных, МетаданныеОбъекта);
		ВыбранноеИмя = ?(ЗначениеЗаполнено(СвойстваМетаданных.ПредставлениеОбъекта),
			СвойстваМетаданных.ПредставлениеОбъекта, СвойстваМетаданных.Синоним);
		Представление = СтрШаблон(ШаблонПредставленияКоманды, ПолучитьСклоненияСтроки(НРег(ВыбранноеИмя), ,
			"ПД=Винительный;")[0]);
		СписокКомандДоверитель.Добавить(Тип, Представление);
		СписокКомандПредставитель.Добавить(Тип, Представление);
	КонецЦикла;
	Для Каждого Тип Из Метаданные.ОпределяемыеТипы.КонтрагентБЭД.Тип.Типы() Цикл
		МетаданныеОбъекта = Метаданные.НайтиПоТипу(Тип);
		Если МетаданныеОбъекта = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СвойстваМетаданных = Новый Структура("ПредставлениеОбъекта, Синоним", "", "");
		ЗаполнитьЗначенияСвойств(СвойстваМетаданных, МетаданныеОбъекта);
		ВыбранноеИмя = ?(ЗначениеЗаполнено(СвойстваМетаданных.ПредставлениеОбъекта),
			СвойстваМетаданных.ПредставлениеОбъекта, СвойстваМетаданных.Синоним);
		Представление = СтрШаблон(ШаблонПредставленияКоманды, ПолучитьСклоненияСтроки(НРег(ВыбранноеИмя), ,
			"ПД=Винительный;")[0]);
		СписокКомандДоверитель.Добавить(Тип, Представление);
		СписокКомандПредставитель.Добавить(Тип, Представление);
	КонецЦикла;
	Для Каждого Тип Из Метаданные.ОпределяемыеТипы.ФизическоеЛицо.Тип.Типы() Цикл
		МетаданныеОбъекта = Метаданные.НайтиПоТипу(Тип);
		Если МетаданныеОбъекта = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СвойстваМетаданных = Новый Структура("ПредставлениеОбъекта, Синоним", "", "");
		ЗаполнитьЗначенияСвойств(СвойстваМетаданных, МетаданныеОбъекта);
		ВыбранноеИмя = ?(ЗначениеЗаполнено(СвойстваМетаданных.ПредставлениеОбъекта),
			СвойстваМетаданных.ПредставлениеОбъекта, СвойстваМетаданных.Синоним);
		Представление = СтрШаблон(ШаблонПредставленияКоманды, ПолучитьСклоненияСтроки(НРег(ВыбранноеИмя), ,
			"ПД=Винительный;")[0]);
		СписокКомандПредставитель.Добавить(Тип, Представление);
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИННДоверителяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	СтороныДоверенности = НовыеСтороныДоверенности();
	СтороныДоверенности.ЭтоДоверитель = Истина;
	
	ОбработчикКоманды = Новый ОписаниеОповещения("ВыполнитьКомандуВыбора", ЭтотОбъект, СтороныДоверенности);
	ПоказатьВыборИзМеню(ОбработчикКоманды, СписокКомандДоверитель);
КонецПроцедуры

&НаКлиенте
Процедура ИННДоверителяАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	Если Ожидание > 0 И ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = МашиночитаемыеДоверенностиВызовСервера.ДанныеВыбораСубъекта(Текст, СписокКомандДоверитель);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИННДоверителяОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ВыбранноеЗначение = ПолучитьИНН(ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ИННПредставителяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	СтороныДоверенности = НовыеСтороныДоверенности();
	СтороныДоверенности.ЭтоПредставитель = Истина;
	
	ОбработчикКоманды = Новый ОписаниеОповещения("ВыполнитьКомандуВыбора", ЭтотОбъект, СтороныДоверенности);
	ПоказатьВыборИзМеню(ОбработчикКоманды, СписокКомандПредставитель);
КонецПроцедуры

&НаКлиенте
Процедура ИННПредставителяАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	Если Ожидание > 0 И ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = МашиночитаемыеДоверенностиВызовСервера.ДанныеВыбораСубъекта(Текст, СписокКомандПредставитель);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИННПредставителяОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ВыбранноеЗначение = ПолучитьИНН(ВыбранноеЗначение);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	Если ПроверитьЗаполнение() Тогда
		ЗагрузитьИЗакрытьФормуНаКлиенте();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИЗакрытьФормуНаКлиенте() 
	
	ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, 
									Новый Структура("ИННДоверителя, ИННПредставителя, НомерДоверенности",
													  ИННДоверителя, ИННПредставителя, НомерДоверенности));
	
	Результат = ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры.Результат;
	
	Если Результат.СсылкаНаДоверенность <> Неопределено Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьИНН(Знач Ссылка)
	ИмяРеквизитаИНН = "";
	Если ИнтеграцияЭДО.ЭтоОрганизация(Ссылка) Тогда
		ИмяРеквизитаИНН = ЭлектронноеВзаимодействие.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
	ИначеЕсли ИнтеграцияЭДО.ЭтоКонтрагент(Ссылка) Тогда
		ИмяРеквизитаИНН = ЭлектронноеВзаимодействие.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННКонтрагента");
	КонецЕсли;
	Если ЗначениеЗаполнено(ИмяРеквизитаИНН) Тогда
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизитаИНН);
	КонецЕсли;
	
	ТипЗначения = ТипЗнч(Ссылка);
	ЭтоФизЛицо = Метаданные.ОпределяемыеТипы.ФизическоеЛицо.Тип.СодержитТип(ТипЗначения);
	Если ЭтоФизЛицо Тогда
		ДанныеФизЛица = МашиночитаемыеДоверенности.ДанныеФизЛица(Ссылка);
		Возврат ДанныеФизЛица.ИНН;
	КонецЕсли;
		
	Возврат "";
КонецФункции

// Параметры:
//  ВыбранноеЗначение - ЭлементСпискаЗначений:
//  * Значение - Тип
//  ДополнительныеПараметры - Неопределено
//  						- см. МашиночитаемыеДоверенностиКлиент.НовыеСтороныДоверенности
//
&НаКлиенте
Процедура ВыполнитьКомандуВыбора(ВыбранноеЗначение, ДополнительныеПараметры = Неопределено) Экспорт
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ЭлементСпискаЗначений") Тогда
		ОбработчикЗавершенияВыбора = 
			Новый ОписаниеОповещения("ВыбратьУчастникаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВводЗначения(ОбработчикЗавершенияВыбора, Неопределено, "", ВыбранноеЗначение.Значение);
	КонецЕсли;
КонецПроцедуры

// Параметры:
//  ВыбранноеЗначение - ОпределяемыйТип.Организация
//                    - ОпределяемыйТип.КонтрагентБЭД
//  ДополнительныеПараметры - Неопределено,
//  						- см. МашиночитаемыеДоверенностиКлиент.НовыеСтороныДоверенности
//
&НаКлиенте
Процедура ВыбратьУчастникаЗавершение(ВыбранноеЗначение, ДополнительныеПараметры = Неопределено) Экспорт
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Если ДополнительныеПараметры.Свойство("ЭтоДоверитель") И ДополнительныеПараметры.ЭтоДоверитель Тогда
			ИННДоверителя = ПолучитьИНН(ВыбранноеЗначение);
		ИначеЕсли ДополнительныеПараметры.Свойство("ЭтоПредставитель") И ДополнительныеПараметры.ЭтоПредставитель Тогда
			ИННПредставителя = ПолучитьИНН(ВыбранноеЗначение);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


// Возвращает структуру, для определения стороны доверенности.
// 
// Возвращаемое значение:
// - Структура:
//    * ЭтоДоверитель - Булево
//    * ЭтоПредставитель - Булево
//
&НаКлиенте
Функция НовыеСтороныДоверенности() 
	
	Результат = Новый Структура;
	Результат.Вставить("ЭтоДоверитель", Ложь);
	Результат.Вставить("ЭтоПредставитель", Ложь);
	Возврат Результат;
	
КонецФункции

#КонецОбласти
