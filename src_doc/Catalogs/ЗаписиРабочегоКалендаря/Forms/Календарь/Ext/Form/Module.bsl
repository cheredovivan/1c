#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ОтображаемаяДата = НачалоДня(ТекущаяДата());
	ПериодОтображения = РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку(
		"ПериодОтображенияРабочегоКалендаря");
	ОтображатьЛегенду = РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку(
		"ОтображатьЛегенду");
	
	УстановитьАвтообновление();
	УстановитьВариантРаботыФормы();
	
	ОбновитьОтображениеСервер();
	
	//МобильныйКлиент
		ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
		МК_НастроитьЭлементыФормы();
	//Конец МобильныйКлиент
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ДатаСегодня = НачалоДня(ТекущаяДата());
	
	УстановитьДоступностьЭлементов();
	УстановитьОтображаемуюДату(ОтображаемаяДата);
	
#Если Не ВебКлиент И Не МобильныйКлиент Тогда
	УстановитьАвтообновлениеФормы();
#КонецЕсли
	
	ПланировщикПриАктивизации(Элементы.Планировщик, Истина);
	
#Если МобильныйКлиент Тогда
	ОбновитьНадписьЗаголовок();
#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ЗаписьКалендаря" Тогда
		
		Если Источник = УникальныйИдентификатор Тогда
			Возврат;
		КонецЕсли;
		
		Если ТипЗнч(Параметр) = Тип("Массив") Тогда
			Если Параметр.Количество() <> 0 Тогда
				ТекущаяЗаписьКалендаря = Параметр[0];
				ТекущаяДатаНачала = Неопределено;
			КонецЕсли;
		КонецЕсли;
		
		ОбновитьОтображениеКлиент(Параметр);
		
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_ДоступноеВремяФизЛица"
		Или ИмяСобытия = "Запись_ЗанятостьФизЛица"
		Или ИмяСобытия = "Запись_Мероприятие"
		Или ИмяСобытия = "Запись_Отсутствие"
		Или ИмяСобытия = "Закрытие_НастройкиСинхронизацииКалендаря" Тогда
		
		ОбновитьОтображениеКлиент();
		
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_НастройкиКалендаря" Тогда
		ОбновитьОтображениеКлиент(,, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодОтображенияПриИзменении(Элемент)
	
	УстановитьОтображаемуюДату(ОтображаемаяДата);
	
	ОбновитьДанныеКалендаря("ПериодОтображения");
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображаемаяДатаПриИзменении(Элемент)
	
	ВыделенныеДаты.ЗагрузитьЗначения(Элемент.ВыделенныеДаты);
	ОбновитьНастройкиОтображения();
	
	ОбновитьДанныеКалендаря();
	
#Если МобильныйКлиент Тогда
	ИзменитьВидимостьВыбораДаты();
#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображаемаяДатаПриАктивизацииДаты(Элемент)
	
	Если Элемент.ВыделенныеДаты.Количество() = ВыделенныеДаты.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ВыделенныеДаты.ЗагрузитьЗначения(Элемент.ВыделенныеДаты);
	Если ВыделенныеДаты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Если ВыделенныеДаты.НайтиПоЗначению(ОтображаемаяДата) = Неопределено Тогда
		ОтображаемаяДата = ВыделенныеДаты[ВыделенныеДаты.Количество() - 1].Значение;
	КонецЕсли;
	ОбновитьНастройкиОтображения();
	
	ОбновитьДанныеКалендаря("ПериодОтображения");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображаемаяДатаПриВыводеПериода(Элемент, ОформлениеПериода)
	
	ОбщегоНазначенияДокументооборотКлиент.ПриВыводеПериода(Элемент, ОформлениеПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображаемаяДатаПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания,
	СтандартнаяОбработка, ВыбраннаяДата)
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыПеретаскивания.Значение.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("ЭлементПланировщика") Тогда
		СтандартнаяОбработка = Ложь;
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("Структура") Тогда
		СтандартнаяОбработка =
			Не РаботаСРабочимКалендаремКлиентСервер.ЭтоЭлементЗаписиКалендаря(
				ПараметрыПеретаскивания.Значение[0]);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображаемаяДатаПеретаскивание(Элемент, ПараметрыПеретаскивания,
	СтандартнаяОбработка, ВыбраннаяДата)
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыПеретаскивания.Значение.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("ЭлементПланировщика") Тогда
		СтандартнаяОбработка = Ложь;
		РаботаСРабочимКалендаремКлиент.ПеренестиЭлементыПланировщикаНаДату(
			Элементы.Планировщик, Планировщик, НастройкиОтображения,
			ПараметрыПеретаскивания.Значение, ВыбраннаяДата, УникальныйИдентификатор);
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("Структура") Тогда
		
		Если Не РаботаСРабочимКалендаремКлиентСервер.ЭтоЭлементЗаписиКалендаря(
				ПараметрыПеретаскивания.Значение[0]) Тогда
			Возврат;
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		РаботаСРабочимКалендаремКлиент.ПеренестиЭлементыЗаписейКалендаряНаДату(
			ПараметрыПеретаскивания.Значение, ВыбраннаяДата);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФизЛицаКалендаряНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьКалендарьСотрудника();
	
КонецПроцедуры

&НаКлиенте
Процедура ФизЛицаКалендаряПриИзменении(Элемент)
	
	ОбновитьНастройкиОтображения();
	ЗавершениеВыбратьКалендарьСотрудникаСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПустоНажатие(Элемент)
	
	ВыбратьКалендарьСотрудника();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКалендаря

&НаКлиенте
Процедура ПланировщикПриАктивизации(Элемент, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПриАктивизации(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, Значения, Текст, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПередСозданием(
		Элемент,
		Начало,
		Конец,
		Значения,
		Текст,
		СтандартнаяОбработка,
		ИспользоватьБыстроеРедактирование,
		ФизЛицоКалендаря,
		НастройкиОтображения,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикВыбор(Элемент, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаВыбораЭлемента(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриОкончанииРедактирования(Элемент, НовыйЭлемент, ОтменаРедактирования)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаОкончанияРедактированияЭлемента(
		Элемент, НовыйЭлемент, ОтменаРедактирования,
		Планировщик, НастройкиОтображения, УникальныйИдентификатор, ФизЛицоКалендаря);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломРедактирования(Элемент, НовыйЭлемент, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПередНачаломРедактированиемЭлемента(Элемент,
		НовыйЭлемент, СтандартнаяОбработка, ФизЛицоКалендаря);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередУдалением(Элемент, Отказ)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПередУдалениемЭлемента(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриСменеТекущегоПериодаОтображения(Элемент, ТекущиеПериодыОтображения,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.ПланировщикПередНачаломБыстрогоРедактирования(Элемент,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПланДня

&НаКлиенте
Процедура ПланДняПриАктивизацииСтроки(Элемент)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаАктивизацииОбластиПланДня(
		Элемент, ТекущаяЗаписьКалендаря, ТекущаяДатаНачала);
	
	Если ЭтоМобильныйКлиент Тогда
		ПодключитьОбработчикОжидания("МК_ОбработатьОткрытиеЗаписиИзПланаДня", 0.3, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПланДняВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаВыбораПланДня(
		Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланДняПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПередНачаломДобавленияПланДня(
		Элемент, Отказ, Копирование, Родитель, Группа, НастройкиОтображения, ФизЛицоКалендаря);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланДняПередНачаломИзменения(Элемент, Отказ)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПередНачаломИзмененияПланДня(Элементы.ПланДня, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланДняПередУдалением(Элемент, Отказ)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПередУдалениемПланДня(Элементы.ПланДня, ПланДня, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланДняНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаНачалаПеретаскиванияПланДня(
		Элемент, ПараметрыПеретаскивания, Выполнение, НастройкиОтображения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланДняПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка,
	Строка, Поле)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПроверкиПеретаскиванияПланДня(
		Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле, НастройкиОтображения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланДняПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	РаботаСРабочимКалендаремКлиент.ОбработкаПеретаскиванияПланДня(
		Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле, НастройкиОтображения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отработать(Команда)
	
	РаботаСРабочимКалендаремКлиент.ОтработатьВыделенныеЗаписиКалендаря(Элементы.Планировщик);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтработатьПланДня(Команда)
	
	РаботаСРабочимКалендаремКлиент.ОтработатьВыделенныеЗаписиКалендаряПланДня(Элементы.ПланДня, ПланДня);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКалендарь(Команда)
	
	ОбновитьОтображениеКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиКалендаря(Команда)
	
	ОткрытьФорму("Справочник.ЗаписиРабочегоКалендаря.Форма.НастройкиКалендаря");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьПредыдущийПериод(Команда)
	
	УстановитьОтображаемуюДату(
		РаботаСРабочимКалендаремКлиентСервер.ПолучитьДатуНачалаПредыдущегоПериода(
			ПериодОтображения, ОтображаемаяДата));
	
	ОбновитьДанныеКалендаря();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьСегодня(Команда)
	
	УстановитьОтображаемуюДату(ТекущаяДата());
	
	ОбновитьДанныеКалендаря();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьСледующийПериод(Команда)
	
	УстановитьОтображаемуюДату(
		РаботаСРабочимКалендаремКлиентСервер.ПолучитьДатуНачалаСледующегоПериода(
			ПериодОтображения, ОтображаемаяДата));
	
	ОбновитьДанныеКалендаря();
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	Если ПериодОтображения <>
			ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда
		ВыделенныеЭлементы =
			РаботаСРабочимКалендаремКлиент.ВыделенныеЭлементы(Элементы.Планировщик);
	Иначе
		ВыделенныеЭлементы =
			РаботаСРабочимКалендаремКлиент.ВыделенныеЭлементыПланДня(
				Элементы.ПланДня, ПланДня, Истина);
	КонецЕсли;
	РаботаСРабочимКалендаремКлиент.Печать(ВыделенныеЭлементы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьЛегенду(Команда)
	
	СкрытьЛегендуСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗанятость(Команда)
	
	Если ОтображаемаяДата = НачалоДня(ТекущаяДата()) Тогда
		РаботаСРабочимКалендаремКлиент.СоздатьЗанятость();
		Возврат;
	КонецЕсли;
	
	ДатаНачалаНовойЗаписи = ОтображаемаяДата + НастройкиОтображения.ОтображатьВремяС * 3600;
	ДатаОкончанияНовойЗаписи = ДатаНачалаНовойЗаписи + 3600;
	РаботаСРабочимКалендаремКлиент.СоздатьЗанятость(ДатаНачалаНовойЗаписи,
		ДатаОкончанияНовойЗаписи, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаписьКалендаря(Команда)
	
	Если ПериодОтображения =
			ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда
		РаботаСРабочимКалендаремКлиент.СоздатьЗаписьКалендаряПланДня(
			Элементы.ПланДня, НастройкиОтображения, ФизЛицоКалендаря);
	Иначе
		РаботаСРабочимКалендаремКлиент.СоздатьЗаписьКалендаряПланировщик(
			НастройкиОтображения, ФизЛицоКалендаря);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветЖелтый(Команда)
	
	УстановитьЦветСобытияРабочегоКалендаря(
		ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Желтый"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветЗеленый(Команда)
	
	УстановитьЦветСобытияРабочегоКалендаря(
		ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Зеленый"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветКрасный(Команда)
	
	УстановитьЦветСобытияРабочегоКалендаря(
		ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Красный"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветНет(Команда)
	
	УстановитьЦветСобытияРабочегоКалендаря(
		ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Нет"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветОранжевый(Команда)
	
	УстановитьЦветСобытияРабочегоКалендаря(
		ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Оранжевый"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветСиний(Команда)
	
	УстановитьЦветСобытияРабочегоКалендаря(
		ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Синий"));
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьЗаписьКалендаря(Команда)
	
	Если Элементы.Планировщик.ВыделенныеЭлементы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСРабочимКалендаремКлиент.СкопироватьЗаписьКалендаря(
		Элементы.Планировщик.ВыделенныеЭлементы[0].Значение.Ссылка,
		Элементы.Планировщик.ВыделенныеЭлементы[0].Значение.ДатаНачала,
		Элементы.Планировщик.ВыделенныеЭлементы[0].Значение.ДатаОкончания);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПериодНажатие(Элемент)
	
	ИзменитьВидимостьВыбораДаты();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗавершениеВыбратьКалендарьСотрудникаСервер()
	
	РаботаСРабочимКалендаремСервер.УстановитьПерсональнуюНастройку(
		"ТекущиеКалендариВсеКалендари", ФизЛицаКалендаря.ВыгрузитьЗначения());
	ОбновитьОтображениеСервер();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементов()
	
	Элементы.ГруппаЛегенда.Видимость = ОтображатьЛегенду;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементов()
	
	Если ПериодОтображения =
		ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда
		
		Элементы.ГруппаОписаниеЛегенды.Доступность = Ложь;
		
	Иначе
		
		Элементы.ГруппаОписаниеЛегенды.Доступность = 
			РаботаСРабочимКалендаремКлиентСервер.ПолучитьОтобразитьЗанятостьВДне(НастройкиОтображения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СкрытьЛегендуСервер()
	
	ОтображатьЛегенду = Ложь;
	РаботаСРабочимКалендаремСервер.УстановитьПерсональнуюНастройку("ОтображатьЛегенду", Ложь);
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьАвтообновление()
	
	НастройкиАвтообновления = Автообновление.ПолучитьНастройкиАвтообновленияФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьАвтообновлениеФормы()
	
	Если ТипЗнч(НастройкиАвтообновления) = Тип("Структура")
		И НастройкиАвтообновления.Автообновление Тогда
		ПодключитьОбработчикОжидания(
			"ОбработкаАвтообновления", 
			НастройкиАвтообновления.ПериодАвтоОбновления,
			Ложь);
	Иначе
		ОтключитьОбработчикОжидания("ОбработкаАвтообновления");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаАвтообновления()
	
	Если ТипЗнч(НастройкиАвтообновления) <> Тип("Структура")
		Или (ТипЗнч(НастройкиАвтообновления) = Тип("Структура")
		И Не НастройкиАвтообновления.Автообновление) Тогда
		ОтключитьОбработчикОжидания("ОбработкаАвтообновления");
	Иначе
		
		Если ПериодОтображения =
				ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда
			
			ОбновитьОтображениеКлиент();
			
		Иначе
			
			ОбновитьДанныеКалендаря();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтображаемуюДату(НоваяОтображаемаяДата)
	
	Элементы.ОтображаемаяДата.ВыделенныеДаты.Очистить();
	ОтображаемаяДата = НоваяОтображаемаяДата;
	Элементы.ОтображаемаяДата.ВыделенныеДаты.Добавить(ОтображаемаяДата);
	ВыделенныеДаты.ЗагрузитьЗначения(Элементы.ОтображаемаяДата.ВыделенныеДаты);
	
	ОбновитьНастройкиОтображения();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветСобытияРабочегоКалендаря(Цвет)
	
	Если ПериодОтображения =
			ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда
		РаботаСРабочимКалендаремКлиент.ОбработкаИзмененияЦветаПланДня(Элементы.ПланДня, ПланДня, Цвет);
	Иначе
		РаботаСРабочимКалендаремКлиент.ОбработкаИзмененияЦвета(Элементы.Планировщик, Цвет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеКлиент(Параметр = Неопределено, ИзмененнаяНастройка = Неопределено, ОбновитьНастройки = Ложь)
	
	ДатаСегодня = НачалоДня(ТекущаяДата());
	
	Если Параметр <> Неопределено
		И ПериодОтображения <>
			ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда
		
		РаботаСРабочимКалендаремКлиент.ОбновитьЗаписиКалендаряВПланировщике(
			Планировщик, НастройкиОтображения, Параметр);
		Возврат;
		
	КонецЕсли;
	
	СвернутыеЭлементы = РаботаСРабочимКалендаремКлиент.ПолучитьСвернутыеЭлементыПланаДня(
		Элементы.ПланДня, ПланДня);
	ОбновитьОтображениеСервер(Параметр, ИзмененнаяНастройка, ОбновитьНастройки);
	РаботаСРабочимКалендаремКлиент.ВосстановитьСостояниеПланаДня(
		Элементы.ПланДня, ПланДня, СвернутыеЭлементы, ТекущаяЗаписьКалендаря, ТекущаяДатаНачала);
	
	#Если Не ВебКлиент Тогда
		Если ОбновитьНастройки Тогда
			УстановитьАвтообновлениеФормы();
		КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеКалендаря(ИзмененнаяНастройка = Неопределено)
	
	КлючеваяОперация = РаботаСРабочимКалендаремКлиентСервер.КлючеваяОперацияОбновлениеКалендаря();
	ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
	
	Если ПериодОтображения =
			ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда
		
		ОбновитьОтображениеКлиент(, ИзмененнаяНастройка);
		
	Иначе
		
		РаботаСРабочимКалендаремКлиент.ОбновитьОтображениеПланировщика(
			Планировщик, НастройкиОтображения, ИзмененнаяНастройка);
		
	КонецЕсли;
	
#Если МобильныйКлиент Тогда
	ОбновитьНадписьЗаголовок();
#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеСервер(Параметр = Неопределено, ИзмененнаяНастройка = Неопределено, ОбновитьНастройки = Ложь)
	
	РаботаСРабочимКалендаремСервер.СохранитьНастройку(НастройкиОтображения, ИзмененнаяНастройка);
	
	НастройкиОтображения = РаботаСРабочимКалендаремСервер.ПолучитьНастройкиОтображения();
	НастройкиОтображения.ФизЛица.Очистить();
	Для Каждого ОчередноеФизЛицо Из ФизЛицаКалендаря Цикл
		НастройкиОтображения.ФизЛица.Добавить(ОчередноеФизЛицо.Значение);
	КонецЦикла;
	Если ФизЛицаКалендаря.Количество() = 1 Тогда
		ФизЛицоКалендаря = ФизЛицаКалендаря[0].Значение;
	Иначе
		ФизЛицоКалендаря = Неопределено;
	КонецЕсли;
	НастройкиОтображения.ОтображаемаяДата = ОтображаемаяДата;
	НастройкиОтображения.ВыделенныеДаты = ВыделенныеДаты.ВыгрузитьЗначения();
	НастройкиОтображения.ПериодОтображения = ПериодОтображения;
	
	УстановитьТекущуюСтраницу(Элементы, ФизЛицаКалендаря, ПериодОтображения);
	
	Если ПериодОтображения = Перечисления.ПериодОтображенияРабочегоКалендаря.ПланДня Тогда
		
		ПланДняЗначение = РеквизитФормыВЗначение("ПланДня");
		РаботаСРабочимКалендаремСервер.ОтобразитьПланДня(ПланДняЗначение, НастройкиОтображения);
		ЗначениеВРеквизитФормы(ПланДняЗначение, "ПланДня");
		
	Иначе
		
		РаботаСРабочимКалендаремСервер.ОтобразитьКалендарь(Планировщик, НастройкиОтображения);
		
		Если ЭтоМобильныйКлиент Тогда
			ИспользоватьБыстроеРедактирование = Ложь;
		Иначе
			ИспользоватьБыстроеРедактирование =
				РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку(
					"ИспользоватьБыстроеРедактирование");
		КонецЕсли;
		
		УстановитьВидимостьЭлементов();
		
	КонецЕсли;
	
	Если ОбновитьНастройки Тогда
		УстановитьАвтообновление();
		ОтображатьЛегенду = РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображатьЛегенду");
		УстановитьВидимостьЭлементов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВариантРаботыФормы()
	
	Если Не ЗначениеЗаполнено(Параметры.ВариантРаботыФормы) Тогда
		Параметры.ВариантРаботыФормы = "МойКалендарь";
	КонецЕсли;
	
	Если Параметры.ВариантРаботыФормы = "МойКалендарь" Тогда
		КлючНазначенияИспользования = "МойКалендарь";
		Заголовок = НСтр("ru = 'Мой календарь'");
		ФизЛицоКалендаря = ПользователиДокументооборот.ФизЛицоПользователя();
		ФизЛицаКалендаря.Добавить(ФизЛицоКалендаря);
		Элементы.ФизЛицаКалендаря.Видимость = Ложь;
		Элементы.ФормаСоздатьЗанятость.Видимость = Истина;
	ИначеЕсли Параметры.ВариантРаботыФормы = "ВсеКалендари" Тогда
		КлючНазначенияИспользования = "ВсеКалендари";
		Заголовок = НСтр("ru = 'Все календари'");
		ФизЛицаКалендаря.ЗагрузитьЗначения(РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку(
			"ТекущиеКалендариВсеКалендари"));
		Элементы.ФизЛицаКалендаря.Видимость = Истина;
		Элементы.ФормаСоздатьЗанятость.Видимость = Ложь;
	ИначеЕсли Параметры.ВариантРаботыФормы = "КалендарьСотрудника" Тогда
		КлючНазначенияИспользования = "КалендарьСотрудника";
		Заголовок = СтрШаблон(НСтр("ru = 'Календарь сотрудника %1'"), Параметры.ФизЛицо);
		ФизЛицоКалендаря = Параметры.ФизЛицо;
		ФизЛицаКалендаря.Добавить(ФизЛицоКалендаря);
		Элементы.ФизЛицаКалендаря.Видимость = Ложь;
		Элементы.ФормаСоздатьЗанятость.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНастройкиОтображения()
	
	НастройкиОтображения.ФизЛица.Очистить();
	Для Каждого ОчередноеФизЛицо Из ФизЛицаКалендаря Цикл
		НастройкиОтображения.ФизЛица.Добавить(ОчередноеФизЛицо.Значение);
	КонецЦикла;
	Если ФизЛицаКалендаря.Количество() = 1 Тогда
		ФизЛицоКалендаря = ФизЛицаКалендаря[0].Значение;
	Иначе
		ФизЛицоКалендаря = Неопределено;
	КонецЕсли;
	НастройкиОтображения.ОтображаемаяДата = ОтображаемаяДата;
	НастройкиОтображения.ВыделенныеДаты = ВыделенныеДаты.ВыгрузитьЗначения();
	НастройкиОтображения.ПериодОтображения = ПериодОтображения;
	
	УстановитьТекущуюСтраницу(Элементы, ФизЛицаКалендаря, ПериодОтображения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКалендарьСотрудника()
	
	ВыбранныеСотрудники = Новый Массив;
	Для Каждого СотрудникФизЛица Из СотрудникиФизЛицКалендаря(ФизЛицаКалендаря) Цикл
		СтруктураВыбранногоАдресата = РаботаСАдреснойКнигойКлиент.СтруктураВыбранногоАдресата();
		СтруктураВыбранногоАдресата.Контакт = СотрудникФизЛица;
		ВыбранныеСотрудники.Добавить(СтруктураВыбранногоАдресата);
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимРаботыФормы", 2);
	ПараметрыФормы.Вставить("ОтображатьСотрудников", Истина);
	ПараметрыФормы.Вставить("ЗаголовокФормы", НСтр("ru = 'Выбор сотрудников календаря'"));
	ПараметрыФормы.Вставить("ВыбранныеАдресаты", ВыбранныеСотрудники);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВыбратьКалендарьСотрудника", ЭтотОбъект);
	
	РаботаСАдреснойКнигойКлиент.ВыбратьАдресатов(ПараметрыФормы, ЭтаФорма, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыбратьКалендарьСотрудника(ВыбранныеСотрудники, ДопПараметры) Экспорт
	
	Если ВыбранныеСотрудники = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныеСотрудникиМассив = Новый Массив;
	Для Каждого ВыбранныйСотрудник Из ВыбранныеСотрудники Цикл
		ВыбранныеСотрудникиМассив.Добавить(ВыбранныйСотрудник.Контакт);
	КонецЦикла;
	
	ФизЛицаКалендаря.ЗагрузитьЗначения(
		СотрудникиВызовСервера.ФизЛицаСотрудников(ВыбранныеСотрудникиМассив));
	
	ОбновитьНастройкиОтображения();
	ЗавершениеВыбратьКалендарьСотрудникаСервер();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СотрудникиФизЛицКалендаря(ФизЛица)
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Сотрудники.Владелец КАК ФизЛицо,
		|	ВЫБОР
		|		КОГДА НЕ ОсновныеСотрудники.Сотрудник ЕСТЬ NULL
		|			ТОГДА 1
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК Приоритет
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСотрудники КАК ОсновныеСотрудники
		|		ПО Сотрудники.Ссылка = ОсновныеСотрудники.Сотрудник
		|ГДЕ
		|	Сотрудники.Владелец В (&ФизЛица)
		|	И Сотрудники.Действует = ИСТИНА
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
	
	Запрос.УстановитьПараметр("ФизЛица", ФизЛица.ВыгрузитьЗначения());
	
	ПройденныеФизЛица = Новый Соответствие();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ПройденныеФизЛица[Выборка.ФизЛицо] = Истина Тогда
			Продолжить;
		КонецЕсли;
		ПройденныеФизЛица[Выборка.ФизЛицо] = Истина;
		
		Результат.Добавить(Выборка.Сотрудник);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекущуюСтраницу(Элементы, ФизЛицаКалендаря, ПериодОтображения)
	
	УказаныФизЛица = ФизЛицаКалендаря.Количество() <> 0;
	
	Если УказаныФизЛица
		И ПериодОтображения =
			ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда
		Элементы.СтраницыПланировщика.ТекущаяСтраница = Элементы.СтраницаПланДня;
	ИначеЕсли УказаныФизЛица Тогда
		Элементы.СтраницыПланировщика.ТекущаяСтраница = Элементы.СтраницаПланировщик;
	Иначе
		Элементы.СтраницыПланировщика.ТекущаяСтраница = Элементы.СтраницаПусто;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	РаботаСРабочимКалендаремСервер.УстановитьУсловноеОформлениеПланДня(УсловноеОформление);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьВыбораДаты()
	
	Элементы.ГруппаДата.Видимость = Не Элементы.ГруппаДата.Видимость;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНадписьЗаголовок()
	
	ПредставленияПериода = "";
	Если ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.Месяц") Тогда
		ПредставленияПериода = Формат(ОтображаемаяДата, "ДФ='MMMM yy'");
		
	ИначеЕсли ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.Неделя")
		Или ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ТриДня")
		Или ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда
		
		НачалоПериода = РаботаСРабочимКалендаремКлиентСервер.ПолучитьДатуНачалаОтображаемогоПериода(
						ПериодОтображения, ОтображаемаяДата);
		КонецПериода = РаботаСРабочимКалендаремКлиентСервер.ПолучитьДатуОкончанияОтображаемогоПериода(
						ПериодОтображения, ОтображаемаяДата);
		ПредставленияПериода = СтрШаблон("%1 - %2", Формат(НачалоПериода, "ДФ=dd.MM"), Формат(КонецПериода, "ДФ=dd.MM"));
		
	ИначеЕсли ПериодОтображения = ПредопределенноеЗначение("Перечисление.ПериодОтображенияРабочегоКалендаря.День")Тогда
		ПредставленияПериода = Формат(ОтображаемаяДата, "ДФ='dd MMM yyyy'");
		
	КонецЕсли;
	
	Элементы.НадписьПериод.Заголовок = ПредставленияПериода;
	
КонецПроцедуры

&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	Если Не ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	МК_ЭлементыСтиля = МК.ЭлементыСтиля();
	
	// Транформация кнопки "Записать и Закрыть" в акцентную
	МК.ПреобразоватьКнопкуВАкцентную(ЭтотОбъект,
		МК_ЭлементыСтиля,
		Элементы.ФормаСоздатьЗаписьКалендаря,
		Элементы.МК_НижнийБлок);

	// Настройка периода для управления отображением календаря
	Элементы.ГруппаВыделеннаяПанель.Видимость = Ложь;
	Элементы.РазделительШапки.Видимость = Ложь;
	Элементы.ПериодОтображения.Видимость = Ложь;
	Элементы.ГруппаНавигацияМК.Видимость = Истина;

	// Период отображения - свой для МК
	ПериодОтображенияПолеВвода = Элементы.Добавить("ПериодОтображенияПолеВвода", Тип("ПолеФормы"));
	ПериодОтображенияПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПериодОтображенияПолеВвода.ПутьКДанным = "ПериодОтображения";
	ПериодОтображенияПолеВвода.УстановитьДействие("ПриИзменении", "ПериодОтображенияПриИзменении");
	ПериодОтображенияПолеВвода.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПериодОтображенияПолеВвода.РастягиватьПоГоризонтали = Ложь;
	ПериодОтображенияПолеВвода.АвтоМаксимальнаяШирина = Ложь;
	ПериодОтображенияПолеВвода.МаксимальнаяШирина = 3;
	ПериодОтображенияПолеВвода.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
	
	СписокВыбора = ПериодОтображенияПолеВвода.СписокВыбора;
	СписокВыбора.Очистить();
	СписокВыбора.Добавить(Перечисления.ПериодОтображенияРабочегоКалендаря.День,
		НСтр("ru = 'День'"));
	СписокВыбора.Добавить(Перечисления.ПериодОтображенияРабочегоКалендаря.ТриДня,
		НСтр("ru = '3 дня'"));
	СписокВыбора.Добавить(Перечисления.ПериодОтображенияРабочегоКалендаря.Неделя,
		НСтр("ru = 'Неделя'"));
	СписокВыбора.Добавить(Перечисления.ПериодОтображенияРабочегоКалендаря.Месяц,
		НСтр("ru = 'Месяц'"));
	СписокВыбора.Добавить(Перечисления.ПериодОтображенияРабочегоКалендаря.ПланДня,
		НСтр("ru = 'План дня'"));
	
	// Отображение текущего периода (текущий день, текущий месяц, текущая неделя) картинкой
	Элементы.ФормаОтобразитьСегодня.Картинка = БиблиотекаКартинок.Сегодня;
	Элементы.ФормаОтобразитьСегодня.Отображение = ОтображениеКнопки.Картинка;
	Элементы.ФормаОтобразитьСегодня.ГоризонтальноеПоложениеВГруппе =
		ГоризонтальноеПоложениеЭлемента.Право;
	
	// Расстановка элементов по порядку.
	Элементы.Переместить(ПериодОтображенияПолеВвода,
		Элементы.ГруппаПланировщикКоманднаяПанель,
		Элементы.ГруппаНавигацияМК);
	
	Элементы.Переместить(Элементы.ФормаОтобразитьПредыдущийПериод,
		Элементы.ГруппаНавигацияМК,
		Элементы.НадписьПериод);
	
	Элементы.Переместить(Элементы.ФормаОтобразитьСледующийПериод, Элементы.ГруппаНавигацияМК);
	
	Элементы.Переместить(Элементы.ФормаОтобразитьСегодня, Элементы.ГруппаНавигацияМК);
	
	Элементы.ГруппаПланировщикКоманднаяПанель.ВертикальноеПоложениеВГруппе =
		ВертикальноеПоложениеЭлемента.Центр;
	
	Элементы.ФормаОтобразитьСледующийПериод.ВертикальноеПоложениеВГруппе =
		ВертикальноеПоложениеЭлемента.Центр;
	
	Элементы.ФормаОтобразитьПредыдущийПериод.ВертикальноеПоложениеВГруппе =
		ВертикальноеПоложениеЭлемента.Центр;
	
	Элементы.ФормаОтобразитьСегодня.ВертикальноеПоложениеВГруппе =
		ВертикальноеПоложениеЭлемента.Центр;
	
	// Цвет картинок управления периодом - черный - доп. кнопки
	МК_КлиентСервер.ОформитьКнопкуФильтра(МК_ЭлементыСтиля,
		Элементы.ФормаОтобразитьПредыдущийПериод);
	
	МК_КлиентСервер.ОформитьКнопкуФильтра(МК_ЭлементыСтиля,
		Элементы.ФормаОтобразитьСледующийПериод);
	
	МК_КлиентСервер.ОформитьКнопкуФильтра(МК_ЭлементыСтиля, Элементы.ФормаОтобразитьСегодня);
	
	// Планировщик (дата, легенда)
	Элементы.Планировщик.Ширина = 1;
	Элементы.Планировщик.Высота = 1;
	
	Элементы.ГруппаПланировщикИДата.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаДата.Видимость = Ложь;
	
	Элементы.ГруппаДата.РастягиватьПоГоризонтали = Истина;
	Элементы.ГруппаДата.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.ОтображаемаяДата.РежимВыделения = РежимВыделенияДаты.Одиночный;
	Элементы.ОтображаемаяДата.ОтображатьТекущуюДату = Ложь;
	
	Элементы.ГруппаЛегенда.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	
	// Физ. лиц календаря.
	Элементы.ГруппаФизЛицаКалендаряМК.Видимость = Истина;
	Элементы.Переместить(
		Элементы.ФизЛицаКалендаря, Элементы.ГруппаФизЛицаКалендаряМК);
	
	// План дня
	Элементы.ПланДня.ИспользованиеТекущейСтроки =
		ИспользованиеТекущейСтрокиТаблицы.ОтображениеВыделения;
	Элементы.ПланДняКонтекстноеМенюСоздатьСобытиеКалендаря.Видимость = Ложь;
	Элементы.ПланДняКонтекстноеМенюИзменить.Видимость = Ложь;
	Элементы.ПланДняКонтекстноеМенюПечать.Видимость = Ложь;
	Элементы.ФормаОбновить.Видимость = Ложь;
	
	// Отображение нижнего блока с акцентной кнопкой
	Элементы.МК_НижнийБлок.Видимость = Истина;

КонецПроцедуры

&НаКлиенте
Процедура МК_ОбработатьОткрытиеЗаписиИзПланаДня()
			
	ТекущиеДанные = Элементы.ПланДня.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСРабочимКалендаремКлиент.ОбработкаВыбораПланДня(
		Элементы.ПланДня, Элементы.ПланДня.ТекущаяСтрока, Неопределено, Ложь);
	
КонецПроцедуры

#КонецОбласти