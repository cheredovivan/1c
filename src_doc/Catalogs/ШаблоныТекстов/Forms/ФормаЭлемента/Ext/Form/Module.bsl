
&НаКлиенте
Перем ПолноеИмяПеретаскиваемогоФайла;

&НаКлиенте
Перем ТекущееПоложениеНачало; // позиция начала выделения в блоке текста в HTML письме

&НаКлиенте
Перем ТекущееПоложениеКонец;  // позиция конца выделения в блоке текста в HTML письме

&НаКлиенте
Перем ТекущийНомерУзла; // номер текущего узла в HTML письме

&НаКлиенте
Перем ГоризонтальнаяПрокруткаHTML; // положение гор прокрутки в HTML письме

&НаКлиенте
Перем ВертикальнаяПрокруткаHTML; // положение вертикальной прокрутки в HTML письме

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст;
	КонецЕсли;	
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Если Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Объект.Автор = СотрудникиВызовСервера.ОсновнойСотрудникПользователя(
			Пользователи.ТекущийПользователь());
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Параметры.Свойство("ОбластьПрименения") И ЗначениеЗаполнено(Параметры.ОбластьПрименения)
			И Не ЗначениеЗаполнено(Объект.ОбластьПрименения) Тогда 
			Объект.ОбластьПрименения = Параметры.ОбластьПрименения;
		
		ИначеЕсли Не ЗначениеЗаполнено(Объект.ОбластьПрименения) Тогда
			Объект.ОбластьПрименения = Перечисления.ОбластиПримененияШаблоновТекстов.Почта;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Объект.ОбластьПрименения = Перечисления.ОбластиПримененияШаблоновТекстов.ЗадачаОписание Тогда
			Объект.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.РазмеченныйТекст;
			Если Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
				РеквизитыЗначенияКопирования =
					ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
						Параметры.ЗначениеКопирования,
						"ОписаниеФорматированное");
				ОписаниеФорматированное =
					РеквизитыЗначенияКопирования.ОписаниеФорматированное.Получить();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ТекстПисьма = Объект.Шаблон;
		// также картинки тут корректировать
		
		Если Объект.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
			
			Если Не ЗначениеЗаполнено(ТекстПисьма) Тогда
				ТекстПисьма = "<html><body contentEditable=true></body></html>";
			КонецЕсли;	
		
			ВключитьРежимРедактированияHTML();
		КонецЕсли;	
	КонецЕсли;	
	
	ПриложениеЯвляетсяВебКлиентом = ОбщегоНазначенияДокументооборот.ПриложениеЯвляетсяВебКлиентом();
	
	Если Параметры.Свойство("ЗаголовокФормыСоздания") И ЗначениеЗаполнено(Параметры.ЗаголовокФормыСоздания) Тогда 
		ЗаголовокФормыСоздания = Параметры.ЗаголовокФормыСоздания;
		Заголовок = Параметры.ЗаголовокФормыСоздания + НСтр("ru=' (создание)'");
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	
	// Отображение шрифта
	НастройкаШрифтОтображения = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПерсональнуюНастройку("ШрифтОтображения");
	НастройкаОтображатьДругиеШрифтыВПисьмах = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПерсональнуюНастройку("ОтображатьДругиеШрифтыВПисьмах");
	ШрифтОтображения = НастройкаШрифтОтображения;
	
	Если Не Объект.ОбластьПрименения = Перечисления.ОбластиПримененияШаблоновТекстов.Резолюции
		И Не Объект.ОбластьПрименения = Перечисления.ОбластиПримененияШаблоновТекстов.Почта Тогда 
		
		ПростойШаблон = Истина;  // упрощенная форма
		
		Элементы.ГруппаПолейПодписи.Видимость = Ложь;
		Элементы.Доступ.Видимость = Ложь;
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Элементы.ОбщийШаблонЗакладкаШаблон.Видимость = Истина;
		Элементы.Шаблон.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
		Элементы.НастроитьДоступ.Видимость = Объект.ОбщийШаблон;
		Если Объект.Пользователи.Количество() <> 0 Тогда
			Элементы.НастроитьДоступ.Заголовок 
				= СтрШаблон(НСтр("ru = 'Настроить доступ (%1)'"), Объект.Пользователи.Количество());
		Иначе	
			Элементы.НастроитьДоступ.Заголовок = НСтр("ru = 'Настроить доступ'");
		КонецЕсли;	
		
		КлючСохраненияПоложенияОкна = "ШаблоныТекстов.ПростойШаблон";
		
	Иначе	
		
		Элементы.ОбщийШаблонЗакладкаШаблон.Видимость = Ложь;
		Элементы.НастроитьДоступ.Видимость = Ложь;
		
		КлючСохраненияПоложенияОкна = "ШаблоныТекстов.РасширенныйШаблон";
		
	КонецЕсли;	
	
	Если Объект.ОбластьПрименения = Перечисления.ОбластиПримененияШаблоновТекстов.Резолюции Тогда 
		Элементы.ВставитьШаблонВремени.Видимость = Ложь;
		Элементы.ВставитьСрокИсполнения.Видимость = Истина;
	КонецЕсли;
		
	Если Не Объект.ОбщийШаблон Тогда 
		Элементы.ГруппаДоступа.Доступность = Ложь;
	КонецЕсли;
	
	Если Не Объект.Ссылка.Пустая() Тогда 
		ПраваПоОбъекту = ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(Объект.Ссылка);
		Если Не ПраваПоОбъекту.Изменение Тогда
			ТолькоПросмотр = Истина;
			Элементы.ПользователиПодобратьПользователей.Доступность = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.Наименование)
		И Не ЗначениеЗаполнено(Объект.Шаблон)
		И Объект.ОбластьПрименения = Перечисления.ОбластиПримененияШаблоновТекстов.Почта Тогда
		
		Объект.Шаблон = Объект.Наименование;
		
	КонецЕсли;	
	
	Элементы.ПодменюСпособРедактирования.Видимость 
		= (Объект.ОбластьПрименения = Перечисления.ОбластиПримененияШаблоновТекстов.Почта);
	
	Если Объект.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		Элементы.ТекстПисьмаHTML.Видимость = Истина;
		Элементы.ГруппаСтраницыТекстаПисьма.ТекущаяСтраница = Элементы.ГруппаСтраницаHTML;
		Элементы.ГруппаСтраницаПолеВвода.Видимость = Ложь;
		Элементы.СтраницаОписаниеФорматированние.Видимость = Ложь;
	ИначеЕсли Объект.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.РазмеченныйТекст Тогда
		Элементы.ТекстПисьмаHTML.Видимость = Ложь;
		Элементы.ГруппаСтраницаПолеВвода.Видимость = Ложь;
		Элементы.СтраницаОписаниеФорматированние.Видимость = Истина;
		Элементы.ГруппаСтраницыТекстаПисьма.ТекущаяСтраница = Элементы.СтраницаОписаниеФорматированние;
		Элементы.ФормаПроверитьОрфографию.Видимость = Ложь;
	Иначе
		Элементы.ТекстПисьмаHTML.Видимость = Ложь;
		Элементы.ГруппаСтраницаПолеВвода.Видимость = Истина;
		Элементы.ГруппаСтраницыТекстаПисьма.ТекущаяСтраница = Элементы.ГруппаСтраницаПолеВвода;
		Элементы.СтраницаОписаниеФорматированние.Видимость = Ложь;
	КонецЕсли;
	
	Если ПриложениеЯвляетсяВебКлиентом Тогда
		Элементы.ПодменюСпособРедактирования.Видимость = Ложь;
	КонецЕсли;	
	
	Если Объект.ОбластьПрименения <> Перечисления.ОбластиПримененияШаблоновТекстов.Почта Тогда
		Элементы.ВставитьШаблонВремени.Видимость = Ложь;
	КонецЕсли;	
	
	НастроитьРезультатДействия();
	
	Если Объект.ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.HTML") Тогда
		Элементы.ФормаФорматHTML.Пометка = Истина;
		Элементы.ФормаАвтопереносСтрок.Пометка = Ложь;
	Иначе
		Элементы.ФормаФорматHTML.Пометка = Ложь;
		Элементы.ФормаАвтопереносСтрок.Пометка = Истина;
	КонецЕсли;	
	
	ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
	Если ЭтоМобильныйКлиент Тогда
		МК_НастроитьЭлементыФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если Не ЗначениеЗаполнено(Объект.ТипТекста) Тогда
		Объект.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст;
	КонецЕсли;	
	
	ТекстПисьма = Объект.Шаблон;
	
	Вложения.Очистить();
	ОбновитьФайлыШаблона();
	
	Если Объект.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		
		ВключитьРежимРедактированияHTML();
		
		ВстроеннаяПочтаСервер.ВставитьКартинкиВТекстHTML(
			ТекстПисьма,
			Объект.Ссылка,
			УникальныйИдентификатор,
			ИдентификаторыКартинокПисьма);
			
	КонецЕсли;
	
	ОбновитьСразуВыполнитьЗадачу();
	
	Если Объект.ОбластьПрименения = Перечисления.ОбластиПримененияШаблоновТекстов.ЗадачаОписание Тогда
		ОписаниеФорматированное = ТекущийОбъект.ОписаниеФорматированное.Получить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Объект.ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.HTML") Тогда
		ВыполнитьЗаписьHTML();
	КонецЕсли;
	
	ВыполняетсяЗапись = Истина;
	
	Если Не ПоместитьФайлыСДискаВоВременноеХранилище() Тогда
		ВыполняетсяЗапись = Ложь;
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовый", Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
	ТекстПисьмаДляЗаписи = "";
	Если ТекущийОбъект.ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.HTML") Тогда
		ТекстПисьмаДляЗаписи = ТекстПисьмаHTMLПриЗаписи;
	Иначе	
		ТекстПисьмаДляЗаписи = ТекстПисьма;
	КонецЕсли;
	
	Если ИдентификаторыКартинокПисьма.Количество() <> 0 Тогда
		
		Для Каждого Строка Из ИдентификаторыКартинокПисьма Цикл
			
			ОписаниеИсточника = Строка.Значение;
			
			Если Найти(ТекстПисьмаДляЗаписи, ОписаниеИсточника.НавигационнаяСсылка) <> 0 Тогда
				
				// при записи возвращаем GUID - а не навигационную ссылку на временное хранилище
				ТекстПисьмаДляЗаписи = СтрЗаменить(ТекстПисьмаДляЗаписи, 
					ОписаниеИсточника.НавигационнаяСсылка,
					"cid:" + ОписаниеИсточника.ИсточникФайла);
				
			Иначе
				
				// картинку из HTML удалили - удалим и из ИдентификаторыПочтовыхВложений, и пометим файл на удаление 
				УдаляемыеКартинки.Добавить(ОписаниеИсточника.ИсточникФайла);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;                     
	
	// файла картинок добавим
	Для Каждого Строка Из СписокДобавленныхФайлов Цикл
		
		ОписаниеФайла = Строка.Значение;
		
		Если Не ЗначениеЗаполнено(ОписаниеФайла.Ссылка) И Не ПустаяСтрока(ОписаниеФайла.УникальныйИдентификаторФайла) Тогда

			ТекстПисьмаДляЗаписи = СтрЗаменить(ТекстПисьмаДляЗаписи, ОписаниеФайла.Адрес, ОписаниеФайла.УникальныйИдентификаторФайла);	
			
		КонецЕсли;
		
	КонецЦикла;	
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ТекстПисьма", ТекстПисьмаДляЗаписи);
	
	Если Объект.ОбластьПрименения = Перечисления.ОбластиПримененияШаблоновТекстов.ЗадачаОписание Тогда
		ТекущийОбъект.ОписаниеФорматированное = Новый ХранилищеЗначения(ОписаниеФорматированное);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьВложения(ТекущийОбъект.Ссылка);
	
	// файла картинок добавим
	Для Каждого Строка Из СписокДобавленныхФайлов Цикл
		
		ОписаниеФайла = Строка.Значение;
		
		ВложениеПисьмаСсылка = Неопределено;
		
		Если ЗначениеЗаполнено(ОписаниеФайла.Ссылка) Тогда
			
			ВложениеПисьмаСсылка = ОписаниеФайла.Ссылка;
			
		Иначе
			
			ИмяФайлаИнфо = РаботаСоСтроками.РазложитьИмяФайла(ОписаниеФайла.ИмяФайла);
			ВремяИзмененияУниверсальное = РаботаСФайламиКлиентСервер.ПолучитьУниверсальноеВремя(ОписаниеФайла.ВремяИзменения);
			АдресВременногоХранилищаТекста = "";
			
			// Создадим карточку Файла в БД
			СведенияОФайле = РаботаСФайламиКлиентСервер.СведенияОФайле("ФайлСВерсией");
			СведенияОФайле.АдресВременногоХранилищаФайла = ОписаниеФайла.Адрес;
			СведенияОФайле.АдресВременногоХранилищаТекста = АдресВременногоХранилищаТекста;
			СведенияОФайле.ИмяБезРасширения = ИмяФайлаИнфо.Имя;
			СведенияОФайле.РасширениеБезТочки = ИмяФайлаИнфо.Расширение;
			СведенияОФайле.Размер = ОписаниеФайла.Размер;
			СведенияОФайле.ВремяИзменения = ОписаниеФайла.ВремяИзменения;
			СведенияОФайле.ВремяИзмененияУниверсальное = ВремяИзмененияУниверсальное;
			СведенияОФайле.ХранитьВерсии = Ложь;
				
			ВложениеПисьмаСсылка = РаботаСФайламиВызовСервера.СоздатьФайлСВерсией(ТекущийОбъект.Ссылка, СведенияОФайле);
				
		КонецЕсли;
		
		Если Не ПустаяСтрока(ОписаниеФайла.УникальныйИдентификаторФайла) Тогда
			РегистрыСведений.ИдентификаторыПочтовыхВложений.УстановитьИдентификаторПочтовогоВложения(ВложениеПисьмаСсылка, 
				ОписаниеФайла.УникальныйИдентификаторФайла);
		КонецЕсли;
		
	КонецЦикла;	
	
	Если ТекущийОбъект.ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.HTML") Тогда
		ТекстПисьма = ТекстПисьмаHTMLПриЗаписи;
	КонецЕсли;	
	
	ВыполняетсяЗапись = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(ЗаголовокФормыСоздания) Тогда
		Заголовок = Объект.Наименование + " (" + ЗаголовокФормыСоздания + ")";
	КонецЕсли;	

	ТекстПисьмаПриОткрытии = "";
	
	Вложения.Очистить();
	
	Если СписокДобавленныхФайлов.Количество() <> 0 Тогда
		ВстроеннаяПочтаСервер.ВставитьКартинкиВТекстHTML(ТекстПисьма, Объект.Ссылка, УникальныйИдентификатор,
			ИдентификаторыКартинокПисьма);
	КонецЕсли;	
	
	СписокДобавленныхФайлов.Очистить();
	
	Для Каждого Строка Из УдаляемыеКартинки Цикл
		
		Идентификатор = Строка.Значение;
		
		Файл = НайтиИдентификаторПочтовыхВложений(Идентификатор, ТекущийОбъект.Ссылка);
		Если ЗначениеЗаполнено(Файл) Тогда
			
			Попытка
				ЗаблокироватьДанныеДляРедактирования(Файл);
				ФайлОбъект = Файл.ПолучитьОбъект();
				ФайлОбъект.УстановитьПометкуУдаления(Истина);
				РазблокироватьДанныеДляРедактирования(Файл);
			Исключение
				СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			КонецПопытки;	
			
		КонецЕсли;	
		
	КонецЦикла;	
	
	УдаляемыеКартинки.Очистить();
	
	ОбновитьФайлыШаблона();
	ВключитьРежимРедактированияHTML();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПользователиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.ПользовательИлиГруппа = 
			ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПользовательИлиГруппаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьПользователей(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПользовательИлиГруппаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СотрудникиКлиент.ОбработкаВыбораКонтейнера(
		Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПользовательИлиГруппаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		
		ДополнениеТипа = Новый ОписаниеТипов("СправочникСсылка.Сотрудники, 
			|СправочникСсылка.ПолныеРоли, СправочникСсылка.РабочиеГруппы,
			|СправочникСсылка.СтруктураПредприятия,");
		
		ДанныеВыбора = СотрудникиВызовСервера.СформироватьДанныеВыбора(Текст, ДополнениеТипа);
		СтандартнаяОбработка = Ложь;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщийШаблонПриИзменении(Элемент)
	
	Элементы.ГруппаДоступа.Доступность = Объект.ОбщийШаблон;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщийШаблонЗакладкаШаблонПриИзменении(Элемент)
	
	Элементы.НастроитьДоступ.Видимость = Объект.ОбщийШаблон;
	
КонецПроцедуры

&НаКлиенте
Процедура СразуВыполнитьЗадачуПриИзменении(Элемент)
	
	Если СразуВыполнитьЗадачу Тогда
		Объект.РезультатДействия = Элементы.РезультатДействия.СписокВыбора[0].Значение;
	Иначе
		Объект.РезультатДействия =
			ПредопределенноеЗначение("Справочник.РезультатыДействийЗадач.ПустаяСсылка");
	КонецЕсли;
	Элементы.РезультатДействия.ТолькоПросмотр = Не СразуВыполнитьЗадачу;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатДействияПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.РезультатДействия) Тогда
		СразуВыполнитьЗадачу = Ложь;
	КонецЕсли;
	Элементы.РезультатДействия.ТолькоПросмотр = Не СразуВыполнитьЗадачу;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ТекстПисьма) И ЗначениеЗаполнено(Объект.Наименование) Тогда
		ТекстПисьма = Объект.Наименование;
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ШаблонПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.Наименование) И ЗначениеЗаполнено(ТекстПисьма) Тогда
		Объект.Наименование = ТекстПисьма;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура АвторОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	СотрудникиКлиент.СотрудникОбработкаВыбора(Объект, "Автор", ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВставитьДатаИВремя(Команда)
	
	ШаблонТекста = "[День].[Месяц].[Год] [Час]:[Минута]";
	ВставитьШаблонТекста(ШаблонТекста);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьДень(Команда)
	
	ШаблонТекста = "[День]";
	ВставитьШаблонТекста(ШаблонТекста);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьШаблонТекста(ШаблонТекста)
	
	Если Объект.ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.HTML") Тогда
		
		HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ;
		
		range = HTMLДокумент.getSelection().getRangeAt(0);
		Если range = Неопределено Тогда
			// Фокус не на поле с письмом.
			HTMLДокумент.body.focus();
			range = HTMLДокумент.getSelection().getRangeAt(0);
		КонецЕсли;
		range.deleteContents();
		
		новыйНод = HTMLДокумент.createElement("div");
		новыйНод.innerHTML = ШаблонТекста;
		
		range.insertNode(новыйНод);
		
	Иначе	
		
		Элементы.Шаблон.ВыделенныйТекст = ШаблонТекста;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьМесяц(Команда)
	
	ШаблонТекста = "[Месяц]";
	ВставитьШаблонТекста(ШаблонТекста);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьГод(Команда)
	
	ШаблонТекста = "[Год]";
	ВставитьШаблонТекста(ШаблонТекста);	
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьЧас(Команда)
	
	ШаблонТекста = "[Час]";
	ВставитьШаблонТекста(ШаблонТекста);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьМинута(Команда)
	
	ШаблонТекста = "[Минута]";
	ВставитьШаблонТекста(ШаблонТекста);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьПользователей(Команда)
	
	ВыбратьПользователей(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПользователей(МножественныйВыбор)
	
	РабочаяГруппа = Новый Массив;
	
	Если МножественныйВыбор Тогда
		Для Каждого ТаблицаСтрока Из Объект.Пользователи Цикл
			Участник = Новый Структура("Контакт", ТаблицаСтрока.ПользовательИлиГруппа);
			РабочаяГруппа.Добавить(Участник);
		КонецЦикла;
		
		РежимРаботыФормы = 2;
		ЗаголовокФормы = НСтр("ru = 'Подбор сотрудников общего шаблона текста'");
		ЗаголовокСпискаВыбранных = НСтр("ru = 'Выбранные сотрудники/группы:'");
		ЗаголовокСпискаАдреснойКниги = НСтр("ru = 'Все сотрудники/группы:'");
	Иначе
		РежимРаботыФормы = 1;
		ЗаголовокФормы = НСтр("ru = 'Выбор участника группы доступа'");
		ЗаголовокСпискаВыбранных = "";
		ЗаголовокСпискаАдреснойКниги = "";
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗаголовокФормы", ЗаголовокФормы);
	ПараметрыФормы.Вставить("ЗаголовокСпискаВыбранных", ЗаголовокСпискаВыбранных);
	ПараметрыФормы.Вставить("ЗаголовокСпискаАдреснойКниги", ЗаголовокСпискаАдреснойКниги);
	ПараметрыФормы.Вставить("РежимРаботыФормы", РежимРаботыФормы);
	ПараметрыФормы.Вставить("ВыбранныеАдресаты", РабочаяГруппа);
	ПараметрыФормы.Вставить("ВыбиратьКонтейнерыПользователей", Истина);
	ПараметрыФормы.Вставить("ОтображатьСотрудников", Истина);
	ПараметрыФормы.Вставить("ПодменятьПользователейСотрудниками", Истина);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗавершениеПодбораПользователей", ЭтотОбъект, МножественныйВыбор);
	
	РаботаСАдреснойКнигойКлиент.ВыбратьАдресатов(ПараметрыФормы, ЭтаФорма, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеПодбораПользователей(ВыбранныеПользователи, МножественныйВыбор) Экспорт
	
	Если ВыбранныеПользователи = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если МножественныйВыбор Тогда
		Объект.Пользователи.Очистить();
		Для Каждого ГруппаСтрока Из ВыбранныеПользователи Цикл
			Строка = Объект.Пользователи.Добавить();
			Строка.ПользовательИлиГруппа = ГруппаСтрока.Контакт;
		КонецЦикла;
	Иначе
		ТекущаяСтрока = Элементы.Пользователи.ТекущаяСтрока;
		Если ТекущаяСтрока <> Неопределено Тогда
			ТекущиеДанные = Объект.Пользователи.НайтиПоИдентификатору(ТекущаяСтрока);
			ТекущиеДанные.ПользовательИлиГруппа = ВыбранныеПользователи[0].Контакт;
		КонецЕсли;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьОрфографию(Команда)
	
	#Если Не ВебКлиент Тогда
		
		Если Объект.ТипТекста <> ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.HTML") Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеПроверитьОрфографиюТекст", ЭтотОбъект);
			ОрфографияКлиент.ПроверитьОрфографиюТекст(ОписаниеОповещения, Объект.Шаблон);
		КонецЕсли;
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеПроверитьОрфографиюТекст(Результат, Параметры) Экспорт
	
	Если Результат.ТекстИзменен Тогда
		Объект.Шаблон = Результат.ПроверяемыйТекст;
		ТекстПисьма = Объект.Шаблон;
		Модифицированность = Истина;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваДоступа(Команда)
	
	ДокументооборотПраваДоступаКлиент.ОткрытьФормуПравДоступа(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьДоступ(Команда)
	
	Если Модифицированность Тогда
		Если НЕ Записать() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Объект.ОбщийШаблон = Истина;
	
	ПараметрПользователи = Новый Массив;
	Для Каждого Строка Из Объект.Пользователи Цикл
		ПараметрПользователи.Добавить(Строка.ПользовательИлиГруппа);
	КонецЦикла;	
	
	ПараметрыФормы = Новый Структура("Пользователи", 
		ПараметрПользователи);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"НастроитьДоступПродолжение",
		ЭтотОбъект);
		
	ОткрытьФорму("Справочник.ШаблоныТекстов.Форма.НастройкаДоступа", 
		ПараметрыФормы, ЭтаФорма,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура НастроитьДоступПродолжение(Результат, Параметры) Экспорт 
	
	Если ТипЗнч(Результат) = Тип("Массив")  Тогда 
		
		Объект.Пользователи.Очистить();
		Для Каждого Пользователь Из Результат Цикл
			Строка = Объект.Пользователи.Добавить();
			Строка.ПользовательИлиГруппа = Пользователь;
		КонецЦикла;	
		
		Если Объект.Пользователи.Количество() <> 0 Тогда
			Элементы.НастроитьДоступ.Заголовок 
				= СтрШаблон(НСтр("ru = 'Настроить доступ (%1)'"), Объект.Пользователи.Количество());
		Иначе	
			Элементы.НастроитьДоступ.Заголовок = НСтр("ru = 'Настроить доступ'");
		КонецЕсли;	
		
		Модифицированность = Истина;
		
	КонецЕсли;	
		
КонецПроцедуры	

&НаКлиенте
Процедура АвтопереносСтрок(Команда)
	
	Если Объект.ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.HTML") Тогда
		
		ТекстВопроса = НСтр("ru = 'Преобразование письма в текст удалит форматирование, вставленные изображения, таблицы, гиперссылки и другие элементы оформления.
			|Продолжить?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеАвтопереносаСтрок", ЭтотОбъект);	
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена, 0);
		
		Возврат;
		
	КонецЕсли;
	
	ПродолжитьАвтопереносСтрок();
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматHTML(Команда)
	
	Если Объект.ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.HTML") Тогда
		Возврат;
	Иначе
		// преобразуем текст в HTML
		ВыполнитьПереключениеВHTML();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовHTMLПисьма

&НаКлиенте
Процедура ВыполнитьHTMLКоманду(Команда)
	
	HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
	HTMLДокумент.execCommand(Команда);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Полужирный(Команда)
	
	ВыполнитьHTMLКоманду("Bold");
	
КонецПроцедуры

&НаКлиенте
Процедура Наклонный(Команда)
	
	ВыполнитьHTMLКоманду("italic");
	
КонецПроцедуры

&НаКлиенте
Процедура Подчеркнутый(Команда)
	
	ВыполнитьHTMLКоманду("underline");
	
КонецПроцедуры

&НаКлиенте
Процедура МаркированныйСписок(Команда)
	
	ВыполнитьHTMLКоманду("insertUnorderedList");
	
КонецПроцедуры

&НаКлиенте
Процедура НумерованныйСписок(Команда)
	
	ВыполнитьHTMLКоманду("insertOrderedList");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтступВлево(Команда)
	
	ВыполнитьHTMLКоманду("outdent");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтступВправо(Команда)
	ВыполнитьHTMLКоманду("indent");
КонецПроцедуры

&НаКлиенте
Процедура ВыравниваниеВлево(Команда)
	ВыполнитьHTMLКоманду("justifyLeft");
КонецПроцедуры

&НаКлиенте
Процедура ВыравниваниеПоЦентру(Команда)
	ВыполнитьHTMLКоманду("justifyCenter");
КонецПроцедуры

&НаКлиенте
Процедура ВыравниваниеВправо(Команда)
	ВыполнитьHTMLКоманду("justifyRight");
КонецПроцедуры

&НаКлиенте
Процедура ЦветТекста(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеЦветТекста", ЭтотОбъект);
	ОткрытьФорму("Документ.ИсходящееПисьмо.Форма.ФормаВыбораЦвета",, ЭтаФорма,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеЦветТекста(Цвет, Параметры) Экспорт

	Если Цвет <> Неопределено Тогда
		HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
		HTMLДокумент.execCommand("foreColor", Ложь, Цвет );
		Модифицированность = Истина;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЦветФона(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеЦветФона", ЭтотОбъект);
	ОткрытьФорму("Документ.ИсходящееПисьмо.Форма.ФормаВыбораЦветаФона",, ЭтаФорма,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеЦветФона(Цвет, Параметры) Экспорт
	
	Если Цвет <> Неопределено Тогда
		HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
		HTMLДокумент.execCommand("backColor", Ложь, Цвет );
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФормат(Команда)
	
	HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
	HTMLДокумент.execCommand("removeFormat", Ложь, "");
	
	Модифицированность = Истина;
	ЭтаФорма.ТекущийЭлемент = Элементы.ТекстПисьмаHTML;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьШрифт(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьШрифтПродолжение", ЭтотОбъект);
	ОткрытьФорму("Документ.ИсходящееПисьмо.Форма.ФормаВыбораШрифта",, ЭтаФорма,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьШрифтПродолжение(Результат, Параметры) Экспорт 
	
	Если Результат = Неопределено Тогда 
		Возврат;
		
	ИначеЕсли Результат = "Шрифт по умолчанию" Тогда 
		HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
		HTMLДокумент.execCommand("fontName", Ложь, НастройкаШрифтОтображения.Имя);
		Модифицированность = Истина;
		
	ИначеЕсли Результат = "Другой.." Тогда 
		ДиалогВыбораШрифта = Новый ДиалогВыбораШрифта;
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеИзменитьШрифт", ЭтотОбъект);
		ДиалогВыбораШрифта.Показать(ОписаниеОповещения);
		
	Иначе 
		HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
		HTMLДокумент.execCommand("fontName", Ложь, Результат);
		Модифицированность = Истина;
		
	КонецЕсли;
	
	ЭтаФорма.ТекущийЭлемент = Элементы.ТекстПисьмаHTML;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеИзменитьШрифт(Шрифт, Параметры) Экспорт

	Если Шрифт = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПустаяСтрока(Шрифт.Имя) Тогда
		
		HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
		HTMLДокумент.execCommand("fontName", Ложь, Шрифт.Имя);
		Модифицированность = Истина;
		
	КонецЕсли;
	
	РазмерШрифта = 2;
	
	Если Шрифт.Размер <> -1 Тогда
		Если Шрифт.Размер < 8 Тогда
			РазмерШрифта = 1;
		ИначеЕсли Шрифт.Размер <= 10 Тогда
			РазмерШрифта = 2;	
		ИначеЕсли Шрифт.Размер <= 12 Тогда
			РазмерШрифта = 3;	
		ИначеЕсли Шрифт.Размер <= 14 Тогда
			РазмерШрифта = 4;	
		ИначеЕсли Шрифт.Размер <= 16 Тогда
			РазмерШрифта = 5;	
		ИначеЕсли Шрифт.Размер <= 18 Тогда
			РазмерШрифта = 6;	
		Иначе
			РазмерШрифта = 7;	
		КонецЕсли;	
	КонецЕсли;
	
	HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
	HTMLДокумент.execCommand("fontSize", Ложь, РазмерШрифта);
	Модифицированность = Истина;
	
	Если Шрифт.Зачеркивание = Истина Тогда
		
		HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
		ВыполнитьHTMLКоманду("strikeThrough");
		Модифицированность = Истина;
		
	КонецЕсли;	
	
	Если Шрифт.Жирный = Истина Тогда
		
		HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
		ВыполнитьHTMLКоманду("Bold");
		Модифицированность = Истина;
		
	КонецЕсли;	
	
	Если Шрифт.Наклонный = Истина Тогда
		
		HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
		ВыполнитьHTMLКоманду("italic");
		Модифицированность = Истина;
		
	КонецЕсли;	
	
	Если Шрифт.Подчеркивание = Истина Тогда
		
		HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
		ВыполнитьHTMLКоманду("underline");
		Модифицированность = Истина;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура УвеличитьШрифт(Команда)
	
	HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
	Размер = HTMLДокумент.queryCommandValue("fontSize");
	
	Если Не ЗначениеЗаполнено(Размер) Тогда 
		Размер = 2;
	КонецЕсли;
	HTMLДокумент.execCommand("fontSize", Ложь, Размер + 1);
	
	Модифицированность = Истина;
	ЭтаФорма.ТекущийЭлемент = Элементы.ТекстПисьмаHTML;
	
КонецПроцедуры

&НаКлиенте
Процедура УменьшитьШрифт(Команда)
	
	HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
	Размер = HTMLДокумент.queryCommandValue("fontSize");
	Если Не ЗначениеЗаполнено(Размер) Тогда 
		Размер = 3;
	КонецЕсли;
	HTMLДокумент.execCommand("fontSize", Ложь, Размер - 1);
	
	Модифицированность = Истина;
	ЭтаФорма.ТекущийЭлемент = Элементы.ТекстПисьмаHTML;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВставитьКартинкуИзБуфера(Команда)
	
	Если Объект.ТипТекста <> ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.HTML") Тогда
		Возврат;
	КонецЕсли;	
	
	КартинкаИзБуфераОбмена = Ждать СредстваБуфераОбмена.ПолучитьДанныеАсинх(СтандартныйФорматДанныхБуфераОбмена.Картинка);
	
	Если КартинкаИзБуфераОбмена = Неопределено Тогда
		ПоказатьПредупреждение(
			, НСтр(
				"ru='Буфер обмена не содержит картинки'"
				, "ru"
			)
		);
		Возврат;
	КонецЕсли;
	
	ПутьКФайлу = "";
	
#Если Не ВебКлиент Тогда 
	
	ПутьКФайлу = ПолучитьИмяВременногоФайла("png");
	КартинкаИзБуфераОбмена.Записать(ПутьКФайлу);

#КонецЕсли

	ДобавитьКартинкуВЛинукс(ПутьКФайлу);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьКартинку(Команда)
	
	Обработчик = Новый ОписаниеОповещения("ВставитьКартинкуПослеУстановкиРасширения", ЭтотОбъект);
	ФайловыеФункцииСлужебныйКлиент.ПоказатьВопросОбУстановкеРасширенияРаботыСФайлами(Обработчик);
	
КонецПроцедуры	
	
&НаКлиенте
Процедура ВставитьКартинкуПослеУстановкиРасширения(Результат, ПараметрыВыполнения) Экспорт
	
	Если НЕ ФайловыеФункцииСлужебныйКлиент.Расширение1СПредприятияПодключено() Тогда
		Возврат;	
	КонецЕсли;
		
	Фильтр = НСтр("ru = 'Все картинки (*.bmp;*.gif;*.png;*.jpeg;*.dib;*.rle;*.tif;*.jpg;*.ico;*.wmf;*.emf)|*.bmp;*.gif;*.png;*.jpeg;*.dib;*.rle;*.tif;*.jpg;*.ico;*.wmf;*.emf'")
		+ НСтр("ru = '|Все файлы(*.*)|*.*'")
		+ НСтр("ru = '|Формат bmp(*.bmp*;*.dib;*.rle)|*.bmp;*.dib;*.rle'")
		+ НСтр("ru = '|Формат GIF(*.gif*)|*.gif'")
		+ НСтр("ru = '|Формат JPEG(*.jpeg;*.jpg)|*.jpeg;*.jpg'")
		+ НСтр("ru = '|Формат PNG(*.png*)|*.png'")
		+ НСтр("ru = '|Формат TIFF(*.tif)|*.tif'")
		+ НСтр("ru = '|Формат icon(*.ico)|*.ico'")
		+ НСтр("ru = '|Формат метафайл(*.wmf;*.emf)|*.wmf;*.emf'");
								
	АдресВременногоХранилищаФайла = "";
	
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ВыборФайла.МножественныйВыбор = Ложь;
	ВыборФайла.Заголовок = НСтр("ru = 'Выбор картинки'");
	ВыборФайла.Фильтр = Фильтр;
	
	Результат = ВыборФайла.Выбрать();
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ПутьФайла = ВыборФайла.ПолноеИмяФайла;   
	
	ДобавитьКартинкуВЛинукс(ПутьФайла);
		
КонецПроцедуры

&НаКлиенте    
Процедура ДобавитьКартинкуВЛинукс(ПутьФайла)

	АдресВоВременномХранилище = "";
	
	ПомещаемыеФайлы = Новый Массив;
	Описание = Новый ОписаниеПередаваемогоФайла(ПутьФайла, "");
	ПомещаемыеФайлы.Добавить(Описание);
	ПомещенныеФайлы = Новый Массив;
	Если Не ПоместитьФайлы(ПомещаемыеФайлы, ПомещенныеФайлы,, Ложь, УникальныйИдентификатор) Тогда
		ВызватьИсключение
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось поместить файл: ""%1"" в хранилище'"),
				ПутьФайла);
	КонецЕсли;
	
	Если ПомещенныеФайлы.Количество() = 1 Тогда
		АдресВоВременномХранилище = ПомещенныеФайлы[0].Хранение;
	Иначе
		ВызватьИсключение
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось поместить файл: ""%1"" в хранилище'"),
				ПутьФайла);
	КонецЕсли;
	
	УникальныйИдентификаторФайла = Новый УникальныйИдентификатор; 
	УникальныйИдентификаторФайла = Строка(УникальныйИдентификаторФайла);
	
	HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
	HTMLДокумент.execCommand("InsertImage", Ложь,  АдресВоВременномХранилище);
	
	Файл = Новый Файл(ПутьФайла);
	
	ОписаниеФайла = Новый Структура("Адрес, УникальныйИдентификаторФайла, ВремяИзменения, ИмяФайла, Размер, Ссылка",
		АдресВоВременномХранилище, УникальныйИдентификаторФайла,
		Файл.ПолучитьВремяИзменения(), Файл.Имя, Файл.Размер(), 
		Неопределено);
		
	СписокДобавленныхФайлов.Добавить(ОписаниеФайла);  
	
	Записать();
	Прочитать();
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьТекстИзБуфера(Команда)
	ВыполнитьHTMLКоманду("paste");
КонецПроцедуры

&НаКлиенте
Процедура Вырезать(Команда)
	ВыполнитьHTMLКоманду("cut");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВставитьОдинДень(Команда)
	
	ШаблонТекста = "[1 день]";
	ВставитьШаблонТекста(ШаблонТекста);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьДваДня(Команда)
	
	ШаблонТекста = "[2 дня]";
	ВставитьШаблонТекста(ШаблонТекста);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьТриДня(Команда)
	
	ШаблонТекста = "[3 дня]";
	ВставитьШаблонТекста(ШаблонТекста);

КонецПроцедуры

&НаКлиенте
Процедура ВставитьНеделю(Команда)
	
	ШаблонТекста = "[Неделя]";
	ВставитьШаблонТекста(ШаблонТекста);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеАвтопереносаСтрок(Ответ, Параметры) Экспорт

	Если Ответ <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	// преобразуем HTML в текст
	ПолучитьHTMLИзПоляHTMLДокумента();
	ВыполнитьПереключениеВТекст();
	Модифицированность = Истина;
	
	ПродолжитьАвтопереносСтрок();
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьАвтопереносСтрок()
	
	ОбновитьКомандыФорматаТекста();
	
	Элементы.ГруппаСтраницаПолеВвода.Видимость = Истина;
	
	Элементы.ГруппаСтраницыТекстаПисьма.ТекущаяСтраница = Элементы.ГруппаСтраницаПолеВвода;
	Элементы.ФормаАвтопереносСтрок.Пометка = Истина; 
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьПереключениеВHTML()
	
	ТекстПисьма = РаботаС_HTML.ПолучитьТекстHTMLДляИсходящегоПисьмаИзПростогоТекста(ТекстПисьма);
	Объект.ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.HTML");
	ВключитьРежимРедактированияHTML();
	ОбновитьКомандыФорматаТекста();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКомандыФорматаТекста()
	
	Если Объект.ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.HTML") Тогда
		Элементы.ФормаФорматHTML.Пометка = Истина;
		Элементы.ФормаАвтопереносСтрок.Пометка = Ложь;
	Иначе
		Элементы.ФормаФорматHTML.Пометка = Ложь;
		Элементы.ФормаАвтопереносСтрок.Пометка = Истина;
	КонецЕсли;	
	
	Если Объект.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		
		Элементы.ТекстПисьмаHTML.Видимость = Истина;
		
		Элементы.ГруппаСтраницыТекстаПисьма.ТекущаяСтраница = Элементы.ГруппаСтраницаHTML;
		Элементы.ГруппаСтраницаПолеВвода.Видимость = Ложь;
		
	Иначе		
		Элементы.ТекстПисьмаHTML.Видимость = Ложь;
			
		Элементы.ГруппаСтраницаПолеВвода.Видимость = Истина;
		
		Элементы.ГруппаСтраницыТекстаПисьма.ТекущаяСтраница = Элементы.ГруппаСтраницаПолеВвода;
		Элементы.ФормаАвтопереносСтрок.Пометка = Истина; 
			
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьПереключениеВТекст()
	
	ТекстПисьма = РаботаС_HTML.ПолучитьТекстИзHTML(ТекстПисьма);
	
	Объект.ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.ПростойТекст");
	
	ОбновитьКомандыФорматаТекста();
	
	// получаем только файлы картинки в теле HTML - в режиме HTML они не видны как вложения, но при смене формата в текст станут видны
	ИдентификаторыКартинокПисьма.Очистить();
	
	ВложенияКартинки.Очистить();
	СписокДобавленныхФайлов.Очистить();
	//
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ФайлыШаблона = ВстроеннаяПочтаСервер.ПолучитьФайлыПисьма(
			Объект.Ссылка, // Письмо
			Истина, // ФормироватьРазмерПредставление
			ОтображатьУдаленныеФайлы, // ВключатьПомеченныеНаУдаление
			Истина,    // ТолькоСИдентификаторами
			Ложь); // ТолькоБезИдентификаторов  - чтобы картинки в HTML не показывать

		Для каждого ФайлыШаблонаСтрока Из ФайлыШаблона Цикл
			ВложенияСтрока = Вложения.Добавить();
			ЗаполнитьЗначенияСвойств(ВложенияСтрока, ФайлыШаблонаСтрока);
			ВложенияСтрока.Расположение = "Файл";
			
			РегистрыСведений.ИдентификаторыПочтовыхВложений.УдалитьИдентификаторПочтовогоВложения(ФайлыШаблонаСтрока.Ссылка);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВключитьРежимРедактированияHTML()
	
	Если Объект.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		
		Если Найти(ТекстПисьма, "<body contentEditable") = 0 И Найти(ТекстПисьма, "<BODY contentEditable") = 0 Тогда
		
			Если Найти(ТекстПисьма, "<body") <> 0 Или Найти(ТекстПисьма, "<BODY") <> 0 Тогда
				ТекстПисьма = СтрЗаменить(ТекстПисьма, "<body", "<body contentEditable=true");
				ТекстПисьма = СтрЗаменить(ТекстПисьма, "<BODY", "<BODY contentEditable=true");
			Иначе	
				ТекстПисьма = СтрЗаменить(ТекстПисьма, "<html>", "<html><body contentEditable=true>");
				ТекстПисьма = СтрЗаменить(ТекстПисьма, "</html>", "</body></html>");
				
				ТекстПисьма = СтрЗаменить(ТекстПисьма, "<HTML>", "<HTML><BODY contentEditable=true>");
				ТекстПисьма = СтрЗаменить(ТекстПисьма, "</HTML>", "</BODY></HTML>");
			КонецЕсли;	
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьHTMLИзПоляHTMLДокумента()
	
	Если Объект.ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.HTML") Тогда			
		ТекстПисьма = ПолучитьТекстПисьмаВПоле();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьТекстПисьмаВПоле()
	
	HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
	ТекстПисьмаВПоле = "<html><body>" + HTMLДокумент.body.innerHTML + "</body></html>";
	
	Возврат ТекстПисьмаВПоле;
	
КонецФункции

&НаКлиенте
Процедура ТекстПисьмаHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьФайлыШаблона()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ТолькоБезИдентификаторов = (Объект.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML);
		
		ФайлыШаблона = ВстроеннаяПочтаСервер.ПолучитьФайлыПисьма(
			Объект.Ссылка, // Письмо
			Истина, // ФормироватьРазмерПредставление
			ОтображатьУдаленныеФайлы, // ВключатьПомеченныеНаУдаление
			Ложь,    // ТолькоСИдентификаторами
			ТолькоБезИдентификаторов); // ТолькоБезИдентификаторов  - чтобы картинки в HTML не показывать
			
		ВложенияВременнаяТаблица = Вложения.Выгрузить();
		Вложения.Очистить();
		Для каждого ФайлыШаблонаСтрока Из ФайлыШаблона Цикл
			ВложенияСтрока = Вложения.Добавить();
			ЗаполнитьЗначенияСвойств(ВложенияСтрока, ФайлыШаблонаСтрока);
			ВложенияСтрока.Расположение = "Файл";
		КонецЦикла;
		
		Для каждого ВложенияВременнаяТаблицаСтрока Из ВложенияВременнаяТаблица Цикл
			Если ВложенияВременнаяТаблицаСтрока.Расположение <> "Файл" Тогда
				ВложенияСтрока = Вложения.Добавить();
				ЗаполнитьЗначенияСвойств(ВложенияСтрока, ВложенияВременнаяТаблицаСтрока);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	ВключитьРежимРедактированияHTML();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьВложения(ТекущийОбъект)
	
	Для каждого ВложениеСтрока Из ВложенияКартинки Цикл	
		Строка = Вложения.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, ВложениеСтрока);
	КонецЦикла;	
	ВложенияКартинки.Очистить();
	
	Для каждого ВложениеСтрока Из Вложения Цикл
		
		Если ВложениеСтрока.Расположение = "СсылкаНаФайл" Тогда
			
			НовыйФайл = РаботаСФайламиВызовСервера.СкопироватьФайл(ВложениеСтрока.Ссылка, ТекущийОбъект.Ссылка);
			
			Кодировка = РаботаСФайламиВызовСервера.ПолучитьКодировкуВерсииФайла(
				ВложениеСтрока.Ссылка.ТекущаяВерсия);
			Если ЗначениеЗаполнено(Кодировка) Тогда
				РаботаСФайламиВызовСервера.ЗаписатьКодировкуВерсииФайла(НовыйФайл.ТекущаяВерсия, Кодировка);
			КонецЕсли;
			
			// заменяем ссылки на новые файлы
			Для Каждого Строка Из СписокДобавленныхФайлов Цикл
				
				ОписаниеФайла = Строка.Значение;
				Если ОписаниеФайла.Ссылка = ВложениеСтрока.Ссылка Тогда
					ОписаниеФайла.Ссылка = НовыйФайл;
				КонецЕсли;	
					
			КонецЦикла;		
			
		ИначеЕсли ВложениеСтрока.Расположение = "ВременноеХранилище" Тогда
			
			ВстроеннаяПочтаСервер.ДобавитьВложениеПисьмаИзВременногоХранилища(
				ТекущийОбъект.Ссылка, // Письмо
				ВложениеСтрока.Адрес, // АдресВременногоХранилища
				ВложениеСтрока.АдресИзвлеченногоТекста, // АдресВременногоХранилищаТекста
				ВложениеСтрока.Размер,
				ВложениеСтрока.ИмяФайла,
				ТекущаяДата(), // ВремяИзменения
				Неопределено); // Идентификатор - идентификатор картинки
			
			ВложениеСтрока.Расположение = "Файл";
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НайтиИдентификаторПочтовыхВложений(Идентификатор, ВладелецФайла)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИдентификаторыПочтовыхВложений.Файл КАК Файл
		|ИЗ
		|	РегистрСведений.ИдентификаторыПочтовыхВложений КАК ИдентификаторыПочтовыхВложений
		|ГДЕ
		|	ИдентификаторыПочтовыхВложений.Идентификатор = &Идентификатор
		|	И ИдентификаторыПочтовыхВложений.Файл.ВладелецФайла = &ВладелецФайла";
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);	
	Запрос.УстановитьПараметр("ВладелецФайла", ВладелецФайла);	
	
	Таблица = Запрос.Выполнить().Выгрузить();	
	Если Таблица.Количество() <> 0 Тогда
		Возврат Таблица[0].Файл;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции	

&НаКлиенте
Функция ПоместитьФайлыСДискаВоВременноеХранилище()
	
	Попытка
		
		Если Не ПодключитьРасширениеРаботыСФайлами() Тогда
			Возврат Истина;
		КонецЕсли;
	
	Исключение
		
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось подключить расширение работы с файлами по причине:
				|""%1""'"),
			СообщениеОбОшибке);
			
		ПоказатьПредупреждение(, ТекстСообщения);	
		Возврат Истина;
		
	КонецПопытки;
	
	Для каждого ВложенияСтрока Из Вложения Цикл
		
		Если ВложенияСтрока.Расположение <> "НаДиске" Тогда
			Продолжить;
		КонецЕсли;
			
		Пока Истина Цикл
			
			Попытка
				
				ПолныйПутьКФайлу = ВложенияСтрока.ИмяФайлаНаДиске;
				
				//Проверка и копирование файла, если файл занят внешним приложением на редактирование.
				//Далее копия файла будет использовать для помещения в ДО.
				#Если НЕ ВебКлиент Тогда
					Если РаботаСФайламиКлиент.ФайлОткрытВнешнейПрограммойДляРедактирования(ПолныйПутьКФайлу) Тогда
						
						ПутьКИсходномуФайлу = ПолныйПутьКФайлу;
						
						ИсходныйФайл = Новый Файл(ПутьКИсходномуФайлу);
						
						ИмяКаталогаДляРазмещенияКопииФайла = 
						КаталогВременныхФайлов() + Строка(Новый УникальныйИдентификатор);
						
						СоздатьКаталог(ИмяКаталогаДляРазмещенияКопииФайла);
						
						ПолныйПутьКФайлу = ИмяКаталогаДляРазмещенияКопииФайла + "\" + ИсходныйФайл.Имя;
						
						//Копирование файла, если попытка копирования не удалась, то
						//далее для помещения будет использоваться исходный файл.
						Попытка
							КопироватьФайл(ПутьКИсходномуФайлу, ПолныйПутьКФайлу);
						Исключение
							ПолныйПутьКФайлу = ПутьКИсходномуФайлу;
						КонецПопытки;
						
					КонецЕсли;
				#КонецЕсли
				
				АдресВоВременномХранилище = "";
				
				ПомещаемыеФайлы = Новый Массив;
				Описание = Новый ОписаниеПередаваемогоФайла(ПолныйПутьКФайлу, "");
				ПомещаемыеФайлы.Добавить(Описание);
				ПомещенныеФайлы = Новый Массив;
				Если Не ПоместитьФайлы(ПомещаемыеФайлы, ПомещенныеФайлы,, Ложь, УникальныйИдентификатор) Тогда
					ВызватьИсключение
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Не удалось поместить файл: ""%1"" в хранилище'"),
							ПолныйПутьКФайлу);
				КонецЕсли;
				
				Если ПомещенныеФайлы.Количество() = 1 Тогда
					АдресВоВременномХранилище = ПомещенныеФайлы[0].Хранение;
				Иначе
					ВызватьИсключение
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Не удалось поместить файл: ""%1"" в хранилище'"),
							ПолныйПутьКФайлу);
				КонецЕсли;
				
				ВложенияСтрока.Адрес = АдресВоВременномХранилище;
				ВложенияСтрока.Расположение = "ВременноеХранилище";
				НастройкиРаботыСФайлами = ФайловыеФункцииКлиентПовтИсп.ПолучитьОбщиеНастройкиРаботыСФайлами();
				Если Не НастройкиРаботыСФайлами.ИзвлекатьТекстыФайловНаСервере Тогда
					
					ВложенияСтрока.АдресИзвлеченногоТекста =
						ФайловыеФункцииКлиентСервер.ИзвлечьТекстВоВременноеХранилище(
							ПолныйПутьКФайлу,
							УникальныйИдентификатор,
							Ложь); // Отказ
					
				КонецЕсли;
				
				#Если НЕ ВебКлиент Тогда
					Каталог = Новый Файл(ИмяКаталогаДляРазмещенияКопииФайла);
					Если Каталог.Существует() Тогда
						УдалитьФайлы(ИмяКаталогаДляРазмещенияКопииФайла);
					КонецЕсли;
				#КонецЕсли
				
				Прервать;
				
			Исключение
				
				СообщениеОбОшибке = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось загрузить файл ""%1"" по причине:
						|""%2""
						|Попробуйте повторить.'"),
					ПолныйПутьКФайлу,
					СообщениеОбОшибке);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,,
					"Вложения");
					
				Возврат Ложь;	
				
			КонецПопытки;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьЗаписьHTML()
	
	HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
	
	ТекстПисьмаHTMLДляОтменыОтправки = "<html><body>" + HTMLДокумент.body.innerHTML + "</body></html>";
	
	Если ПодключитьРасширениеРаботыСФайлами() Тогда
		
		// в веб клиенте  HTMLДокумент.images недоступен
		#Если Не ВебКлиент Тогда 
		
		ЧислоКартинок = HTMLДокумент.images.length;  
		Для Индекс = 0 По ЧислоКартинок - 1 Цикл  
			
			Изображение = HTMLДокумент.images.item(Индекс);		
			
			Попытка
				Если Найти(ВРег(Изображение.src), ВРег("file://")) = 1 Тогда
					
					ПутьФайла = Сред(Изображение.src, СтрДлина("file://") + 1);
					ПутьФайла = СтрЗаменить(ПутьФайла, "%20", " ");
					
					ПервыйСимвол = Лев(ПутьФайла, 1);
					Если ПервыйСимвол = "/" Или ПервыйСимвол = "\" Тогда
						ПутьФайла = Сред(ПутьФайла, 2);
					КонецЕсли;	
					
					АдресВоВременномХранилище = "";
					
					ПомещаемыеФайлы = Новый Массив;
					Описание = Новый ОписаниеПередаваемогоФайла(ПутьФайла, "");
					ПомещаемыеФайлы.Добавить(Описание);
					ПомещенныеФайлы = Новый Массив;
					Если Не ПоместитьФайлы(ПомещаемыеФайлы, ПомещенныеФайлы,, Ложь, УникальныйИдентификатор) Тогда
						ВызватьИсключение
							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Не удалось поместить файл: ""%1"" в хранилище'"),
								ПутьФайла);
					КонецЕсли;
					
					Если ПомещенныеФайлы.Количество() = 1 Тогда
						АдресВоВременномХранилище = ПомещенныеФайлы[0].Хранение;
					Иначе
						ВызватьИсключение
							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Не удалось поместить файл: ""%1"" в хранилище'"),
								ПутьФайла);
					КонецЕсли;
					
					УникальныйИдентификаторФайла = Новый УникальныйИдентификатор; 
					УникальныйИдентификаторФайла = Строка(УникальныйИдентификаторФайла);
					Изображение.src = "cid:" + УникальныйИдентификаторФайла;
					
					Файл = Новый Файл(ПутьФайла);
					
					ОписаниеФайла = Новый Структура("Адрес, УникальныйИдентификаторФайла, ВремяИзменения, ИмяФайла, Размер, Ссылка",
						АдресВоВременномХранилище, УникальныйИдентификаторФайла,
						Файл.ПолучитьВремяИзменения(), Файл.Имя, Файл.Размер(), 
						Неопределено);
						
					СписокДобавленныхФайлов.Добавить(ОписаниеФайла);
					
				КонецЕсли;	
			Исключение
				СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Почта.ЗаписатьОшибкуВЖурналРегистрации(СообщениеОбОшибке);
			КонецПопытки;
			
		КонецЦикла;	
		
		#КонецЕсли
		
	КонецЕсли;
	
	ТекстПисьмаHTMLПриЗаписи = ПолучитьТекстПисьмаВПоле();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьРезультатДействия()
	
	Элементы.РезультатДействия.СписокВыбора.Очистить();
	
	РезультатыДействий =
		ИнтеграцияЗадач.РезультатыДействийПоОбластиПримененияШаблоновТекстов(
			Объект.ОбластьПрименения);
	
	Для Каждого РезультатДействия Из РезультатыДействий Цикл
		
		Элементы.РезультатДействия.СписокВыбора.Добавить(РезультатДействия);
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Объект.РезультатДействия)
		И Элементы.РезультатДействия.СписокВыбора.НайтиПоЗначению(Объект.РезультатДействия) = Неопределено Тогда
		
		Элементы.РезультатДействия.СписокВыбора.Добавить(Объект.РезультатДействия);
		
	КонецЕсли;
	
	Элементы.РезультатДействия.Видимость = Элементы.РезультатДействия.СписокВыбора.Количество() > 1;
	Элементы.СразуВыполнитьЗадачу.Видимость = Элементы.РезультатДействия.СписокВыбора.Количество() > 0;
КонецПроцедуры

&НаСервере
Процедура ОбновитьСразуВыполнитьЗадачу()
	
	СразуВыполнитьЗадачу = ЗначениеЗаполнено(Объект.РезультатДействия);
	Элементы.РезультатДействия.ТолькоПросмотр = Не СразуВыполнитьЗадачу;
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСсылку(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВставитьСсылку", ЭтотОбъект);
	ПоказатьВводСтроки(ОписаниеОповещения, , НСтр("ru='Ссылка'"),,Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВставитьСсылку(ТекстСсылки, Параметры) Экспорт
	
	Если ЗначениеЗаполнено(ТекстСсылки) Тогда
		HTMLДокумент = Элементы.ТекстПисьмаHTML.Документ; 
		HTMLДокумент.execCommand("createLink", Ложь, ТекстСсылки);
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции_МобильныйКлиент

&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	МК.УстановитьОбщиеСвойстваФормы(ЭтотОбъект);
	Элементы.ГруппаРезультатДействия.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	Элементы.МК_ЗаписатьИЗакрыть.Видимость = Истина;
	МК.ОформитьАкцентнуюКнопку(Элементы.МК_ЗаписатьИЗакрыть);

КонецПроцедуры
	
#КонецОбласти

#КонецОбласти
