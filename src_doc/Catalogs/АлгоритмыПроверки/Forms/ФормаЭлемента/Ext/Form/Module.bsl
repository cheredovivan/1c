#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Протоколирование работы пользователей
	ПротоколированиеРаботыСотрудников.ЗаписатьОткрытие(Объект.Ссылка);
	
	Если Объект.Ссылка.Пустая()
			И (Не Параметры.Свойство("ЗначениеКопирования")
			Или Не ЗначениеЗаполнено(Параметры.ЗначениеКопирования.Наименование)) Тогда
		Если Параметры.Свойство("ТипОбъекта") Тогда
			Объект.ТипОбъекта = Параметры.ТипОбъекта;
			НастроитьКомпоновщикУсловияИЗаполнитьДеревоРеквизитовОбъекта(Объект.ТипОбъекта);
		КонецЕсли;
		Объект.СпособЗаданияУсловия = ВРежимеКонструктора();
		НастроитьКомпоновщикКомбинацииУсловий();
	ИначеЕсли Объект.Ссылка.Пустая()
			И Параметры.Свойство("ЗначениеКопирования")
			И ЗначениеЗаполнено(Параметры.ЗначениеКопирования.Наименование) Тогда
		Настройки = Параметры.ЗначениеКопирования.НастройкаУсловия.Получить();
		НастройкиКомбинацииУсловий = Параметры.ЗначениеКопирования.НастройкаКомбинацииУсловий.Получить();
		НастроитьКомпоновщикУсловияИЗаполнитьДеревоРеквизитовОбъекта(Объект.ТипОбъекта, Настройки);
		НастроитьКомпоновщикКомбинацииУсловий(НастройкиКомбинацииУсловий);
		
	Иначе
		ПоляСХранилищем = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.Ссылка, "НастройкаУсловия, НастройкаКомбинацииУсловий");
		Настройки = ПоляСХранилищем.НастройкаУсловия.Получить();
		НастройкиКомбинацииУсловий = ПоляСХранилищем.НастройкаКомбинацииУсловий.Получить();
		НастроитьКомпоновщикУсловияИЗаполнитьДеревоРеквизитовОбъекта(Объект.ТипОбъекта, Настройки);
		НастроитьКомпоновщикКомбинацииУсловий(НастройкиКомбинацииУсловий);
	КонецЕсли;
	
	Элементы.Страницы.ТекущаяСтраница =
		Элементы["Страница" + ОбщегоНазначения.ИмяЗначенияПеречисления(Объект.СпособЗаданияУсловия)];
	Если Объект.СпособЗаданияУсловия = НаВстроенномЯзыке() Тогда
		Элементы.ГруппаОтветственныйИКомментарий.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.КомпоновщикНастройкиОтборСгруппироватьЭлементыОтбора.Заголовок = НСтр("ru = 'Сгруппировать правила'");
	Элементы.КомпоновщикНастройкиОтборКонтекстноеМенюСгруппироватьЭлементыОтбора.Заголовок =
		НСтр("ru = 'Сгруппировать правила'");
	Элементы.КомпоновщикНастройкиОтборДобавитьЭлементОтбора.Заголовок = НСтр("ru = 'Добавить правило'");
	Элементы.КомпоновщикНастройкиОтборКонтекстноеМенюДобавитьЭлементОтбора.Заголовок = НСтр("ru = 'Добавить правило'");
	
	// Скрываем кнопку "Очистить" у колонки "Поле" в компоновщике отбора, который используется в режиме "Конструктор".
	Элементы.КомпоновщикНастройкиОтборЛевоеЗначение.КнопкаОчистки = Ложь;
	
	// Настройка действия при изменении вида сравнения.
	Элементы.КомпоновщикНастройкиОтборВидСравнения.УстановитьДействие(
		"ПриИзменении", "КомпоновщикНастройкиОтборВидСравненияПриИзменении");
	
	// Настройка действия при начале изменения правого значения сравнения.
	Элементы.КомпоновщикНастройкиОтборПравоеЗначение.УстановитьДействие(
		"НачалоВыбора", "КомпоновщикНастройкиОтборПравоеЗначениеНачалоВыбора");
	
	Элементы.КомпоновщикУсловийНастройкиОтборДобавитьЭлементОтбора.Заголовок = НСтр("ru = 'Добавить условие'");
	Элементы.КомпоновщикУсловийНастройкиОтборКонтекстноеМенюДобавитьЭлементОтбора.Заголовок =
		НСтр("ru = 'Добавить условие'");
	
	Элементы.ГруппаКомпоновщик.Доступность = ЗначениеЗаполнено(Объект.ТипОбъекта);
	Элементы.ПредставлениеПараметров.Видимость = Не Объект.Предопределенный;
	
	ПредставлениеПараметров = "";
	Для Каждого Параметр Из Объект.Параметры Цикл
		ПредставлениеПараметров = ?(ПредставлениеПараметров = "", "", ПредставлениеПараметров + "; ");
		ПредставлениеПараметров = ПредставлениеПараметров 
			+ СтрШаблон("%1: %2", Параметр.Имя, Параметр.Значение);
	КонецЦикла;
	ПредставлениеПараметров = ?(ПредставлениеПараметров = "", 
		НСтр("ru = 'не заданы, задать'"), ПредставлениеПараметров);
	
	Элементы.КомпоновщикНастройкиОтбор.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.КомпоновщикУсловийНастройкиОтбор.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ВставитьРеквизитИзДерева.Доступность = Не ТолькоПросмотр;
	
	Контекст = КонтекстВыраженияНаВстроенномЯзыке();
	РедакторВстроенногоЯзыка.ПриСозданииНаСервере(ЭтотОбъект, "Редактор", Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РедакторВстроенногоЯзыкаКлиент.ПриОткрытии(
		Элементы.РедакторВстроенногоЯзыка,
		Элементы.ВыражениеУсловия,
		ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ТолькоПросмотр = Не ПравоДоступа("Изменение", Метаданные.Справочники.АлгоритмыПроверки)
		Или Объект.Предопределенный;
	
	ОбщегоНазначенияДокументооборот.ПоказатьНадписьПометкиУдаления(
		ЭтотОбъект,
		ТекущийОбъект.ПометкаУдаления,
		Элементы.ГруппаПометкаУдаления.Имя);
	
	Элементы.ГруппаПредопределенный.Видимость = Объект.Предопределенный;
	
	КлючСохраненияПоложенияОкна =
		Строка(Элементы.ГруппаПредопределенный.Видимость)
		+ Строка(Элементы.ГруппаПометкаУдаления.Видимость);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Объект.ВыражениеУсловия = РедакторВстроенногоЯзыкаКлиент.ПолучитьТекст(
		Элементы.РедакторВстроенногоЯзыка, Объект.ВыражениеУсловия);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
	// Проверка на уникальность имени условия.
	РезультатПроверки = ПроверитьУникальностьУсловия();
	Если РезультатПроверки <> Неопределено Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru='Данное Наименование уже указано для другого условия'"),, "Объект.Наименование",, Отказ);
		Возврат;
	КонецЕсли;
	
	Если Объект.СпособЗаданияУсловия = КомбинацияИзДругихУсловий()
			И КомпоновщикУсловий.Настройки.Отбор.Элементы.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Не указано ни одного условия'"),, "КомпоновщикУсловий.Настройки.Отбор",, Отказ);
	ИначеЕсли Объект.СпособЗаданияУсловия = ВРежимеКонструктора()
			И Компоновщик.Настройки.Отбор.Элементы.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Не указано ни одного правила'"),, "Компоновщик.Настройки.Отбор",, Отказ);
	ИначеЕсли Объект.СпособЗаданияУсловия = НаВстроенномЯзыке() И Не ЗначениеЗаполнено(Объект.ВыражениеУсловия) Тогда
		Если Элементы.РедакторВстроенногоЯзыка.Видимость Тогда
			Поле = "Редактор";
		Иначе
			Поле = "Объект.ВыражениеУсловия";
		КонецЕсли;
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Не задано выражение'"),, Поле,, Отказ);
	КонецЕсли;
	
	Если Объект.СпособЗаданияУсловия = КомбинацияИзДругихУсловий() И ПроверитьНаличиеУсловияВДереве(Истина) Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Заполнены не все значения в комбинации условий'"),,
			"КомпоновщикУсловий.Настройки.Отбор",,
			Отказ);
		Возврат;
	КонецЕсли;
	
	Если Объект.СпособЗаданияУсловия = ВРежимеКонструктора() И ПроверитьНаличиеПустыхСтрокОтбораВДереве() Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Среди правил есть некорректно заполненные'"),, "Компоновщик.Настройки.Отбор",, Отказ);
		Возврат;
	КонецЕсли;
	
	Если Объект.СпособЗаданияУсловия = КомбинацияИзДругихУсловий() Тогда
		ТекущийОбъект.ПредставлениеОтбора = Строка(КомпоновщикУсловий.Настройки.Отбор);
	ИначеЕсли Объект.СпособЗаданияУсловия = ВРежимеКонструктора() Тогда
		ТекущийОбъект.ПредставлениеОтбора = Строка(Компоновщик.Настройки.Отбор);
	ИначеЕсли Объект.СпособЗаданияУсловия = НаВстроенномЯзыке() Тогда
		ТекущийОбъект.ПредставлениеОтбора = "";
	КонецЕсли;
	
	ТекущийОбъект.ПредставлениеОтбора = СтрЗаменить(ТекущийОбъект.ПредставлениеОтбора, "( ", "(");
	ТекущийОбъект.ПредставлениеОтбора = СтрЗаменить(ТекущийОбъект.ПредставлениеОтбора, " )", ")");
	
	ТекущийОбъект.НастройкаУсловия = Новый ХранилищеЗначения(Компоновщик.ПолучитьНастройки());
	ТекущийОбъект.НастройкаКомбинацииУсловий = Новый ХранилищеЗначения(КомпоновщикУсловий.ПолучитьНастройки());
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_Условие");
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Протоколирование работы пользователей
	ПротоколированиеРаботыСотрудников.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыСотрудников.ЗаписатьИзменение(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ВыбранПроектИлиПроектнаяЗадача" Тогда
		
		Если ТипЗнч(Параметр) = Тип("Структура") Тогда
			ПредметСсылка = Параметр.ПроектнаяЗадача;
		Иначе
			ПредметСсылка = Параметр;
		КонецЕсли;
		
		НачатьПроверкуВыражения();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособЗаданияУсловияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.СпособЗаданияУсловия) Тогда
		Если Объект.СпособЗаданияУсловия = ВРежимеКонструктора() Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы["СтраницаВРежимеКонструктора"];
		ИначеЕсли Объект.СпособЗаданияУсловия = НаВстроенномЯзыке() Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы["СтраницаНаВстроенномЯзыке"];
		ИначеЕсли Объект.СпособЗаданияУсловия = КомбинацияИзДругихУсловий() Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы["СтраницаКомбинацияИзДругихУсловий"];
		КонецЕсли;
		Элементы.ГруппаОтветственныйИКомментарий.Видимость = (Объект.СпособЗаданияУсловия = НаВстроенномЯзыке());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	СотрудникиКлиент.СотрудникОбработкаВыбора(Объект, "Ответственный", ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПроверитьДоступКТипуДокументаИЗагрузитьНастройки(ВыбранноеЗначение, СтандартнаяОбработка);
	РедакторВстроенногоЯзыкаКлиент.УстановитьКонтекст(
		Элементы.РедакторВстроенногоЯзыка,
		ЭтотОбъект,
		КонтекстВыраженияНаВстроенномЯзыке());
	Если Не СтандартнаяОбработка Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Недостаточно прав для создания алгоритма проверки для предмета типа ""%1"".'"),
			Строка(ВыбранноеЗначение));
		ПоказатьПредупреждение(, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметПриИзменении(Элемент)
	
	НачатьПроверкуВыражения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТипВыбранный = ТипПоТипуОбъекта(Объект.ТипОбъекта);
	
	Если ТипВыбранный = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ВыбиратьТолькоПроектнуюЗадачу", Истина);
		
		Если ТипВыбранный = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда
			ПараметрыФормы.Вставить("ПроектнаяЗадача", ПредметСсылка);
		ИначеЕсли ТипВыбранный = Тип("СправочникСсылка.Проекты") Тогда
			ПараметрыФормы.Вставить("Проект", ПредметСсылка);
		КонецЕсли;
		
		ОткрытьФорму("ОбщаяФорма.ВыборПроектаЗадачи", ПараметрыФормы);
		
	Иначе
		
		ПолноеИмя = ПолноеИмяПоТипу(ТипВыбранный);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПредметСтрокойНажатиеЗавершение", ЭтотОбъект);
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФорму(ПолноеИмя + ".ФормаВыбора", , , , , , ОписаниеОповещения, РежимОткрытияОкна);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПараметровНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	МассивПараметров = Новый Массив;
	Для Каждого Строка Из Объект.Параметры Цикл
		МассивПараметров.Добавить(Новый Структура("Имя, Значение",
			Строка.Имя, Строка.Значение));
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Параметры", МассивПараметров);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("РедактированиеПараметровЗавершено", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.АлгоритмыПроверки.Форма.РедактированиеПараметров",
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РедакторВстроенногоЯзыкаДокументСформирован(Элемент)
	
	Если РедакторВстроенногоЯзыкаКлиент.ДокументСформирован(
			Элемент, ЭтотОбъект, Объект.ВыражениеУсловия, Объект.Предопределенный) Тогда
		ПодключитьОбработчикОжидания("ПослеЗагрузкиРедактора", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедакторВстроенногоЯзыкаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	НажатаКнопкаEsc = Ложь;
	РедакторВстроенногоЯзыкаКлиент.ПриНажатии(
		Элемент,
		ДанныеСобытия,
		СтандартнаяОбработка,
		ЭтотОбъект,
		НажатаКнопкаEsc);
	Если НажатаКнопкаEsc Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоРеквизитовОбъекта

&НаКлиенте
Процедура ДеревоРеквизитовОбъектаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьТекст(СтрШаблон(" %1", Элемент.ТекущиеДанные.ПолныйПуть));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКомпоновщикНастройкиОтбор

&НаКлиенте
Процедура КомпоновщикНастройкиОтборВидСравненияПриИзменении(Элемент)
	
	ЭлементОтбора = Компоновщик.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(
		Элементы.КомпоновщикНастройкиОтбор.ТекущаяСтрока);
	Если ЭлементОтбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭлементОтбора.ВидСравнения <> ВидСравненияКомпоновкиДанных.ВИерархии
			И ЭлементОтбора.ВидСравнения <> ВидСравненияКомпоновкиДанных.НеВИерархии Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭлементОтбораПоддерживаетРабочиеГруппы(ЭлементОтбора) Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементОтбора.ПравоеЗначение = ПредопределенноеЗначение("Справочник.РабочиеГруппы.ПустаяСсылка");
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастройкиОтборПравоеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЭлементОтбора = Компоновщик.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(
		Элементы.КомпоновщикНастройкиОтбор.ТекущаяСтрока);
	Если ЭлементОтбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СправочникСсылка.РабочиеГруппы") Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РежимВыбора", Истина);
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПравоеЗначениеНачалоВыбораПродолжение",
			ЭтотОбъект);
		ОткрытьФорму(
			"Справочник.РабочиеГруппы.ФормаВыбора",
			ПараметрыФормы,
			Элемент,,,,
			ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ИначеЕсли ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СправочникСсылка.ЗначенияСвойствОбъектов") Тогда
		СтандартнаяОбработка = Ложь;
		ИмяДопРеквизита = Строка(ЭлементОтбора.ЛевоеЗначение);
		ИмяДопРеквизита = Сред(ИмяДопРеквизита, СтрНайти(ИмяДопРеквизита, "["));
		ИмяДопРеквизита = Сред(ИмяДопРеквизита, 2, СтрДлина(ИмяДопРеквизита) - 2);
		ВладелецЗначения = ПолучитьВладельцаЗначенияДопРеквизита(ИмяДопРеквизита);

		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("РежимВыбора", Истина);
		ПараметрыФормы.Вставить("Отбор", Новый Структура("Владелец", ВладелецЗначения));
		ОткрытьФорму("Справочник.ЗначенияСвойствОбъектов.Форма.ФормаСписка", ПараметрыФормы, Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастройкиОтборПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКомпоновщикУсловийНастройкиОтбор

&НаКлиенте
Процедура КомпоновщикУсловийНастройкиОтборПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	НовыйЭлементОтбора = КомпоновщикУсловий.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ПолеОтбора = Новый ПолеКомпоновкиДанных("Результат_проверки_условия");
	НовыйЭлементОтбора.ЛевоеЗначение = ПолеОтбора;
	НовыйЭлементОтбора.Использование = Истина;
	НовыйЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикУсловийНастройкиОтборПриИзменении(Элемент)
	
	Если Элемент.ТекущиеДанные.ЛевоеЗначение <> Неопределено Тогда
		ДобавленноеУсловие = Элемент.ТекущиеДанные.ПравоеЗначение;
		Результат = ПроверитьНаличиеУсловияВДереве(Ложь);
		Если Результат Тогда
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Нельзя добавить условие ""%1"". Произошло зацикливание условий.'"),
				ДобавленноеУсловие);
			ПоказатьПредупреждение(, ТекстПредупреждения);
			УдалитьУсловиеИзКомбинацииУсловий(ДобавленноеУсловие);
		КонецЕсли;
	КонецЕсли;
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВставитьРеквизитИзДерева(Команда)
	
	Если Элементы.ДеревоРеквизитовОбъекта.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьТекст(СтрШаблон(" %1", Элементы.ДеревоРеквизитовОбъекта.ТекущиеДанные.ПолныйПуть));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Клиент

&НаКлиенте
Процедура ДобавитьТекст(ДобавляемыйТекст)
	
	Если Элементы.РедакторВстроенногоЯзыка.Видимость Тогда
		РедакторВстроенногоЯзыкаКлиент.УстановитьТекст(
			Элементы.РедакторВстроенногоЯзыка, ДобавляемыйТекст);
	Иначе
		Объект.ВыражениеУсловия = Объект.ВыражениеУсловия + ДобавляемыйТекст;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПроверкуВыражения()
	
	ИтогПроверки = "";
	Если Не ЗначениеЗаполнено(ПредметСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ВыражениеУсловия = РедакторВстроенногоЯзыкаКлиент.ПолучитьТекст(
		Элементы.РедакторВстроенногоЯзыка, Объект.ВыражениеУсловия);
	РезультатВыполнения = ПроверитьВыражениеСервер();
	РедакторВстроенногоЯзыкаКлиент.ОбработатьРезультатВыполнения(Элементы.РедакторВстроенногоЯзыка, РезультатВыполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗагрузкиРедактора()
	
	РедакторВстроенногоЯзыкаКлиент.ПослеЗагрузкиРедактора(Элементы.РедакторВстроенногоЯзыка, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПравоеЗначениеНачалоВыбораПродолжение(Результат, Параметры) Экспорт
	
	ВыбранноеПравоеЗначение = Результат;
	
	ПодключитьОбработчикОжидания("УстановитьПравоеЗначениеЭлементаОтбора", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметСтрокойНажатиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ПредметСсылка = Результат;
		НачатьПроверкуВыражения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыражениеПродолжение(ИзмененныйТекст, Параметры) Экспорт
	
	Если ЗначениеЗаполнено(ИзмененныйТекст) Тогда
		Объект.ВыражениеУсловия = ИзмененныйТекст;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеПараметровЗавершено(Результат, ПараметрыОповещения) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Параметры.Очистить();
	ПредставлениеПараметров = "";
	Для Каждого Параметр Из Результат Цикл
		ЗаполнитьЗначенияСвойств(Объект.Параметры.Добавить(), Параметр);
		ПредставлениеПараметров = ?(ПредставлениеПараметров = "", "", ПредставлениеПараметров + "; ");
		ПредставлениеПараметров = ПредставлениеПараметров + СтрШаблон("%1: %2", Параметр.Имя, Параметр.Значение);
	КонецЦикла;
	ПредставлениеПараметров = ?(ПредставлениеПараметров = "", "<...>", ПредставлениеПараметров);
	
	РедакторВстроенногоЯзыкаКлиент.УстановитьКонтекст(
		Элементы.РедакторВстроенногоЯзыка,
		ЭтотОбъект,
		КонтекстВыраженияНаВстроенномЯзыке());
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПравоеЗначениеЭлементаОтбора()
	
	ЭлементОтбора = Компоновщик.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(
		Элементы.КомпоновщикНастройкиОтбор.ТекущаяСтрока);
	ЭлементОтбора.ПравоеЗначение = ВыбранноеПравоеЗначение;
	
КонецПроцедуры

&НаКлиенте
Функция ЭлементОтбораПоддерживаетРабочиеГруппы(ЭлементОтбора)
	
	ТипыПоля = Неопределено;
		
	Для Каждого ДоступноеПоле Из Компоновщик.Настройки.ДоступныеПоляОтбора.Элементы Цикл
		Если ДоступноеПоле.Поле = ЭлементОтбора.ЛевоеЗначение Тогда
			ТипыПоля = ДоступноеПоле.Тип.Типы();
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ВСоставеТолькоСотрудникиИПользователи = Ложь;
	ВСоставеТолькоСотрудники = Ложь;
	Если ТипыПоля <> Неопределено Тогда
		ВСоставеТолькоСотрудникиИПользователи = (ТипыПоля.Количество() = 2)
			И ТипыПоля.Найти(Тип("СправочникСсылка.Сотрудники")) <> Неопределено
			И ТипыПоля.Найти(Тип("СправочникСсылка.Пользователи")) <> Неопределено;
		ВСоставеТолькоСотрудники = ТипыПоля.Количество() = 1
			И ТипыПоля.Найти(Тип("СправочникСсылка.Сотрудники")) <> Неопределено;
	КонецЕсли;
	
	Если Не ВСоставеТолькоСотрудникиИПользователи
			И Не ВСоставеТолькоСотрудники
			И ТипЗнч(ЭлементОтбора.ПравоеЗначение) <> Тип("СправочникСсылка.Сотрудники") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область КлиентСервер

// Возвращает значение перечисления СпособыЗаданияУсловия "ВРежимеКонструктора".
//
// Возвращаемое значение:
//   ПеречислениеСсылка.СпособыЗаданияУсловия
//
&НаКлиентеНаСервереБезКонтекста
Функция ВРежимеКонструктора()
	
	Возврат ПредопределенноеЗначение("Перечисление.СпособыЗаданияУсловия.ВРежимеКонструктора");
	
КонецФункции

// Возвращает значение перечисления СпособыЗаданияУсловия "КомбинацияИзДругихУсловий".
//
// Возвращаемое значение:
//   ПеречислениеСсылка.СпособыЗаданияУсловия
//
&НаКлиентеНаСервереБезКонтекста
Функция КомбинацияИзДругихУсловий()
	
	Возврат ПредопределенноеЗначение("Перечисление.СпособыЗаданияУсловия.КомбинацияИзДругихУсловий");
	
КонецФункции

// Возвращает значение перечисления СпособыЗаданияУсловия "НаВстроенномЯзыке".
//
// Возвращаемое значение:
//   ПеречислениеСсылка.СпособыЗаданияУсловия
//
&НаКлиентеНаСервереБезКонтекста
Функция НаВстроенномЯзыке()
	
	Возврат ПредопределенноеЗначение("Перечисление.СпособыЗаданияУсловия.НаВстроенномЯзыке");
	
КонецФункции

#КонецОбласти

#Область Сервер

&НаСервере
Процедура ВыполнитьПоискИУдалениеДляОдногоУровня(Элементы, ИмяУсловия)
	
	Для Каждого ЭлементОтбора Из Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			
			ВыполнитьПоискИУдалениеДляОдногоУровня(ЭлементОтбора.Элементы, ИмяУсловия);
			
		ИначеЕсли ЭлементОтбора.ПравоеЗначение <> Неопределено Тогда
			
			СпособЗаданияУсловия = ЭлементОтбора.ПравоеЗначение.СпособЗаданияУсловия;
			
			Если ЭлементОтбора.ПравоеЗначение.Наименование = ИмяУсловия Тогда
				ЭлементОтбора.ПравоеЗначение = Справочники.АлгоритмыПроверки.ПустаяСсылка();
			ИначеЕсли СпособЗаданияУсловия = КомбинацияИзДругихУсловий() Тогда
				ВыполнитьПоискИУдалениеДляОдногоУровня(
					ЭлементОтбора.ПравоеЗначение.НастройкаКомбинацииУсловий.Получить().Отбор.Элементы, ИмяУсловия);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьПоискПустойСтрокиВОдномУровнеНастройкиОтбора(Элементы)
	
	Для Каждого ЭлементОтбора Из Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда		
			Если ВыполнитьПоискПустойСтрокиВОдномУровнеНастройкиОтбора(ЭлементОтбора.Элементы) Тогда
				Возврат Истина;
			КонецЕсли;
		Иначе
			Если (Не ЗначениеЗаполнено(ЭлементОтбора.ПравоеЗначение)
					Или Не ЗначениеЗаполнено(Строка(ЭлементОтбора.ЛевоеЗначение)))
					И ЭлементОтбора.ВидСравнения <> ВидСравненияКомпоновкиДанных.Заполнено
					И ЭлементОтбора.ВидСравнения <> ВидСравненияКомпоновкиДанных.НеЗаполнено Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция ВыполнитьПоискУсловияВОдномУровнеКомбинацииУсловий(Элементы, ИскомоеУсловие)
	
	Для Каждого ЭлементОтбора Из Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			Если ВыполнитьПоискУсловияВОдномУровнеКомбинацииУсловий(ЭлементОтбора.Элементы, ИскомоеУсловие) Тогда
				Возврат Истина;
			КонецЕсли;
		Иначе
			Если (ЗначениеЗаполнено(ИскомоеУсловие) 
					И ЭлементОтбора.ПравоеЗначение = ИскомоеУсловие)
					Или (Не ЗначениеЗаполнено(ИскомоеУсловие) 
					И Не ЗначениеЗаполнено(ЭлементОтбора.ПравоеЗначение)) Тогда
				Возврат Истина;
			ИначеЕсли ЭлементОтбора.ПравоеЗначение <> Неопределено
					И ЭлементОтбора.ПравоеЗначение.СпособЗаданияУсловия = КомбинацияИзДругихУсловий()
					И ВыполнитьПоискУсловияВОдномУровнеКомбинацииУсловий(
						ЭлементОтбора.ПравоеЗначение.НастройкаКомбинацииУсловий.Получить().Отбор.Элементы,
						ИскомоеУсловие) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДеревоРеквизитовОбъекта(ТипОбъекта)
	
	Дерево = РеквизитФормыВЗначение("ДеревоРеквизитовОбъекта");
	Дерево.Строки.Очистить();
	
	ПолныйПуть = "Предмет";
	НоваяСтрокаВладелецФайла = Дерево.Строки.Добавить();
	НоваяСтрокаВладелецФайла.НаименованиеРеквизита = "Предмет";
	НоваяСтрокаВладелецФайла.ТипРеквизита = ТипОбъекта;
	Если СтрНайти(НРег(Строка(ТипОбъекта)), "документ") > 0 Тогда
		Элементы.ДеревоРеквизитовОбъекта.Заголовок = НСтр("ru = 'Реквизиты документа'");
	ИначеЕсли СтрНайти(НРег(Строка(ТипОбъекта)), "файл") > 0 Тогда
		Элементы.ДеревоРеквизитовОбъекта.Заголовок = НСтр("ru = 'Реквизиты файла'");
	Иначе
		Элементы.ДеревоРеквизитовОбъекта.Заголовок = НСтр("ru = 'Реквизиты объекта'");
	КонецЕсли;
	НоваяСтрокаВладелецФайла.ПолныйПуть = ПолныйПуть;
	
	МетаОбъект = Перечисления.ТипыОбъектов.МетаданныеПоТипуОбъекта(ТипОбъекта);
	Для Каждого СтандартныйРеквизит Из МетаОбъект.СтандартныеРеквизиты Цикл
		Строка = НоваяСтрокаВладелецФайла.Строки.Добавить();
		Строка.НаименованиеРеквизита = СтандартныйРеквизит.Имя;
		Строка.ТипРеквизита = СтандартныйРеквизит.Тип;
		Строка.ПолныйПуть = ПолныйПуть + "." + СтандартныйРеквизит.Имя;
	КонецЦикла;
	Для Каждого Реквизит Из МетаОбъект.Реквизиты Цикл
		Строка = НоваяСтрокаВладелецФайла.Строки.Добавить();
		Строка.НаименованиеРеквизита = Реквизит.Имя;
		Строка.ТипРеквизита = Реквизит.Тип;
		Строка.ПолныйПуть = ПолныйПуть + "." + Реквизит.Имя;
	КонецЦикла;
	Для Каждого ТабличнаяЧасть Из МетаОбъект.ТабличныеЧасти Цикл
		СтрокаТабличнаяЧасть = НоваяСтрокаВладелецФайла.Строки.Добавить();
		СтрокаТабличнаяЧасть.НаименованиеРеквизита = ТабличнаяЧасть.Имя;
		СтрокаТабличнаяЧасть.ТипРеквизита = "Табличная часть";
		СтрокаТабличнаяЧасть.ПолныйПуть = ПолныйПуть + "." + ТабличнаяЧасть.Имя + "[0]";
		Для Каждого СтандартныйРеквизитТЧ Из ТабличнаяЧасть.СтандартныеРеквизиты Цикл
			СтрокаРеквизитТЧ = СтрокаТабличнаяЧасть.Строки.Добавить();
			СтрокаРеквизитТЧ.НаименованиеРеквизита = СтандартныйРеквизитТЧ.Имя;
			СтрокаРеквизитТЧ.ТипРеквизита = СтандартныйРеквизитТЧ.Тип;
			СтрокаРеквизитТЧ.ПолныйПуть = ПолныйПуть + "." + ТабличнаяЧасть.Имя + "[0]" + "." + СтандартныйРеквизитТЧ.Имя;
		КонецЦикла;
		Для Каждого РеквизитТЧ Из ТабличнаяЧасть.Реквизиты Цикл
			СтрокаРеквизитТЧ = СтрокаТабличнаяЧасть.Строки.Добавить();
			СтрокаРеквизитТЧ.НаименованиеРеквизита = РеквизитТЧ.Имя;
			СтрокаРеквизитТЧ.ТипРеквизита = РеквизитТЧ.Тип;
			СтрокаРеквизитТЧ.ПолныйПуть = ПолныйПуть + "." + ТабличнаяЧасть.Имя + "[0]" + "." + РеквизитТЧ.Имя;
		КонецЦикла;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоРеквизитовОбъекта");
	
КонецПроцедуры

&НаСервере
Функция КонтекстВыраженияНаВстроенномЯзыке()
	
	Контекст = Новый Соответствие;
	Контекст.Вставить("Результат");
	Для Каждого СтрокаПредмет Из ДеревоРеквизитовОбъекта.ПолучитьЭлементы() Цикл
		РеквизитыПредмета = Новый Структура;
		Для Каждого СтрокаРеквизит Из СтрокаПредмет.ПолучитьЭлементы() Цикл
			РеквизитыПредмета.Вставить(СтрокаРеквизит.НаименованиеРеквизита);
		КонецЦикла;
		Контекст.Вставить(СтрокаПредмет.НаименованиеРеквизита, РеквизитыПредмета);
	КонецЦикла;
	Для Каждого СтрокаПараметра Из Объект.Параметры Цикл
		Контекст.Вставить(СтрокаПараметра.Имя);
	КонецЦикла;
	
	Возврат Контекст;
	
КонецФункции

&НаСервере
Процедура НастроитьКомпоновщикКомбинацииУсловий(Настройки = Неопределено)
	
	СхемаКомпоновкиДанных = Справочники.АлгоритмыПроверки.ПолучитьМакет("Условия");
	
	Если Настройки = Неопределено Тогда
		Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	КонецЕсли;
	
	URLСхемы = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы);
	КомпоновщикУсловий.Инициализировать(ИсточникНастроек);
	КомпоновщикУсловий.ЗагрузитьНастройки(Настройки);
	// Скрываем поле сравнения - в него будет ставиться всегда "Равно".
	Элементы.КомпоновщикУсловийНастройкиОтборВидСравнения.Видимость = Ложь;
	// Делаем недоступным для редактирования поле "Левое значение".
	Элементы.КомпоновщикУсловийНастройкиОтборЛевоеЗначение.ТолькоПросмотр = Истина;
	Элементы.КомпоновщикУсловийНастройкиОтбор.Шапка = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьКомпоновщикУсловияИЗаполнитьДеревоРеквизитовОбъекта(Знач ТипОбъекта, Настройки = Неопределено)
	
	ИндексЗначенияПеречисления = Перечисления.ТипыОбъектов.Индекс(ТипОбъекта);
	ИмяЗначенияПеречисления = Метаданные.Перечисления.ТипыОбъектов.ЗначенияПеречисления[ИндексЗначенияПеречисления].Имя;
	
	СхемаКомпоновкиДанных = Справочники.АлгоритмыПроверки.ПолучитьМакет(ИмяЗначенияПеречисления);
	Если Настройки = Неопределено Тогда
		Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	КонецЕсли;

	URLСхемы = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы);
	Компоновщик.Инициализировать(ИсточникНастроек);
	Компоновщик.ЗагрузитьНастройки(Настройки);
	
	Если ЗначениеЗаполнено(ТипОбъекта) Тогда
		ЗаполнитьДеревоРеквизитовОбъекта(ТипОбъекта);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьДеревоРеквизитовОбъекта()
	
	Дерево = РеквизитФормыВЗначение("ДеревоРеквизитовОбъекта");
	Дерево.Строки.Очистить();
	ЗначениеВРеквизитФормы(Дерево, "ДеревоРеквизитовОбъекта");
	Элементы.ДеревоРеквизитовОбъекта.Заголовок = НСтр("ru = 'Реквизиты объекта'");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолноеИмяПоТипу(Знач ТипВыбранный)
	
	ПолноеИмя = Метаданные.НайтиПоТипу(ТипВыбранный).ПолноеИмя();
	Возврат ПолноеИмя;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьВладельцаЗначенияДопРеквизита(Знач ИмяДопРеквизита)
	
	Возврат ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию(ИмяДопРеквизита);
	
КонецФункции

&НаСервере
Функция ПроверитьВыражениеСервер()
	
	Выражение = Справочники.АлгоритмыПроверки.ПолноеВыражениеУсловия(Объект);
	СмещениеНомераСтрокиОшибки = СтрЧислоСтрок(Объект.ВыражениеУсловия) - СтрЧислоСтрок(Выражение);
	
	РезультатВыполнения = РедакторВстроенногоЯзыка.ПодготовитьВыражениеКВыполнению(
		Выражение, ЭтотОбъект, "РезультатВыполнения");
	РезультатВыполнения.СмещениеНомераСтрокиОшибки = СмещениеНомераСтрокиОшибки;
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Для Каждого ИмяРазделителя Из РаботаВМоделиСервиса.РазделителиКонфигурации() Цикл
			УстановитьБезопасныйРежимРазделенияДанных(ИмяРазделителя, Истина);
		КонецЦикла;
		УстановитьБезопасныйРежим(Истина);
	КонецЕсли;
	
	Результат = Неопределено;
	
	Попытка
		//@skip-check module-unused-local-variable
		Предмет = ПредметСсылка.ПолучитьОбъект(); // Нужен для проверки скрипта - внутри "Выполнить".
		УстановитьБезопасныйРежим(Истина);
		Выполнить(Выражение);
		УстановитьБезопасныйРежим(Ложь);
		
		Если ТипЗнч(Результат) <> Тип("Булево") Тогда
			ВызватьИсключение НСтр("ru = 'Переменной ""Результат"" необходимо присвоить значение типа ""Булево""'");
		КонецЕсли;
	Исключение
		Результат = Ложь;
		Инфо = ИнформацияОбОшибке();
		
		Описание = "";
		Если ТипЗнч(Инфо.Причина) = Тип("ИнформацияОбОшибке") Тогда
			Описание = Инфо.Причина.Описание;
		Иначе
			Описание = Инфо.Описание;
		КонецЕсли;
		
		ИтогПроверки = НСтр("ru = 'Ошибка.'") + Символы.ВК + Описание;
		
		РезультатВыполнения.ИнформацияОбОшибке = Инфо;
	КонецПопытки;
	
	Если ПустаяСтрока(ИтогПроверки) Тогда
		ИтогПроверки = ?(Результат, НСтр("ru = 'Истина'"), НСтр("ru = 'Ложь'"));
	КонецЕсли;
	
	РедакторВстроенногоЯзыка.ОбработатьРезультатВыполнения(РезультатВыполнения, ЭтотОбъект);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаСервере
Процедура ПроверитьДоступКТипуДокументаИЗагрузитьНастройки(Знач ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		ОчиститьДеревоРеквизитовОбъекта();
		Компоновщик.Настройки.Отбор.Элементы.Очистить();
		Возврат;
	КонецЕсли;
	
	МетаОбъект = Перечисления.ТипыОбъектов.МетаданныеПоТипуОбъекта(ВыбранноеЗначение);
	СтандартнаяОбработка = ПравоДоступа("Чтение", МетаОбъект);
	Если СтандартнаяОбработка Тогда
		Элементы.ГруппаКомпоновщик.Доступность = ЗначениеЗаполнено(ВыбранноеЗначение);
		НастроитьКомпоновщикУсловияИЗаполнитьДеревоРеквизитовОбъекта(ВыбранноеЗначение);
		Элементы.КомпоновщикНастройкиОтбор.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьНаличиеПустыхСтрокОтбораВДереве()
	
	Результат = Ложь;
	Если Объект.СпособЗаданияУсловия = ВРежимеКонструктора() Тогда
		ЭлементыНастройкиОтбора = Компоновщик.Настройки.Отбор.Элементы;
		Результат = ВыполнитьПоискПустойСтрокиВОдномУровнеНастройкиОтбора(ЭлементыНастройкиОтбора);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПроверитьНаличиеУсловияВДереве(Знач ИщемПустоеЗначение)
	
	Результат = Ложь;
	Если Объект.СпособЗаданияУсловия = КомбинацияИзДругихУсловий() Тогда
		ЭлементыНастройкиОтбора = КомпоновщикУсловий.Настройки.Отбор.Элементы;
		ИскомоеУсловие = ?(ИщемПустоеЗначение, Неопределено, Объект.Ссылка);
		Результат = ВыполнитьПоискУсловияВОдномУровнеКомбинацииУсловий(ЭлементыНастройкиОтбора, ИскомоеУсловие);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПроверитьУникальностьУсловия()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Условия.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.АлгоритмыПроверки КАК Условия
		|ГДЕ
		|	Условия.Наименование = &Наименование
		|	И Условия.Ссылка <> &Ссылка");
	
	Запрос.УстановитьПараметр("Наименование", Объект.Наименование);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТипПоТипуОбъекта(Знач ТипОбъекта)
	
	Возврат Перечисления.ТипыОбъектов.ТипПоТипуОбъекта(ТипОбъекта);
	
КонецФункции

&НаСервере
Процедура УдалитьУсловиеИзКомбинацииУсловий(Знач ИмяУсловия)
	
	Если Объект.СпособЗаданияУсловия = КомбинацияИзДругихУсловий() Тогда
		ЭлементыНастройкиОтбора = КомпоновщикУсловий.Настройки.Отбор.Элементы;
		ВыполнитьПоискИУдалениеДляОдногоУровня(ЭлементыНастройкиОтбора, ИмяУсловия);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти