
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого ТипОбъекта Из Перечисления.ТипыОбъектов Цикл
		УстановитьОформлениеСписка(ТипОбъекта, СписокТиповОбъектов.УсловноеОформление, "Ссылка");	
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Параметры.ТипОбъекта) Тогда
		
		Элементы.СписокТиповОбъектов.Видимость = Ложь;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Список.Отбор,
			"ТипОбъекта",
			Параметры.ТипОбъекта);
		
	КонецЕсли;
	
	НастроитьФормуДляМобильногоПриНеобходимости();
	
	ПараметрыДанныхВыбора = Новый Структура;
	ПараметрыДанныхВыбора.Вставить("Потребитель", Справочники.АлгоритмыПроверки.ПустаяСсылка());
	ДанныеВыбора = Перечисления.ТипыОбъектов.ПолучитьДанныеВыбора(ПараметрыДанныхВыбора);
	ТипыОбъектовОтбора = Новый Массив;
	Для Каждого ЭлементДанныхВыбора Из ДанныеВыбора.ВыгрузитьЗначения() Цикл
		ТипыОбъектовОтбора.Добавить(ЭлементДанныхВыбора.Значение);
	КонецЦикла;
	СписокТиповОбъектов.Параметры.УстановитьЗначениеПараметра("ОтборСсылки", ТипыОбъектовОтбора);
	
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_Условие" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокТиповОбъектовПриАктивизацииСтроки(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор,
		"ТипОбъекта",
		Элемент.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если НЕ Копирование Тогда
		Отказ = Истина;
		ПараметрыФормы = Новый Структура(
			"ТипОбъекта",
			?(ЗначениеЗаполнено(Параметры.ТипОбъекта),
				Параметры.ТипОбъекта,
				Элементы.СписокТиповОбъектов.ТекущиеДанные.Ссылка));
		ОткрытьФорму("Справочник.АлгоритмыПроверки.ФормаОбъекта", ПараметрыФормы, Элемент);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОформлениеСписка(НайденныйЭлемент, Знач УсловноеОформление, ИмяПоля)

	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = НайденныйЭлемент;
	ЭлементОтбораДанных.Использование = Истина;
	
	МетаОбъект = Перечисления.ТипыОбъектов.МетаданныеПоТипуОбъекта(НайденныйЭлемент, Ложь);
	Если МетаОбъект <> Неопределено И Не ПравоДоступа("Чтение", МетаОбъект) Тогда
		ЭлементОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("Видимость");
		ЭлементОформления.Значение =  Ложь; 
		ЭлементОформления.Использование = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуДляМобильногоПриНеобходимости()

	Если Не ПараметрыСеанса.ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	Элементы.СписокТиповОбъектов.ИспользованиеТекущейСтроки = ИспользованиеТекущейСтрокиТаблицы.ОтображениеВыделения;
	Элементы.СписокТиповОбъектов.Шапка = Ложь;
	Элементы.Список.Шапка = Ложь;
	
	Если Не МК_КлиентСервер.ЭтоМобильныйСБольшимЭкраном() Тогда 
		Элементы.Переместить(Элементы.СписокТиповОбъектов, Элементы.МК_ГруппаТипыОбъектов);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти