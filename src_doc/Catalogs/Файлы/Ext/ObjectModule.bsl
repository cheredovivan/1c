#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
// Обработка события ПередЗаписью
//
Процедура ПередЗаписью(Отказ)
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		ДополнительныеСвойства.Вставить("ЭтоНовый", Истина);
	КонецЕсли;
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("КонвертацияФайлов") Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("РазмещениеФайловВТомах") Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ЗаписьРезультатаРаспознавания") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыСеанса.ЗагрузкаОбработанныхДанныхИзДругойСистемы Тогда
		Возврат;
	КонецЕсли;
	
	ЗанятьФайл = Ложь;
	ДополнительныеСвойства.Свойство("ЗанятьФайл", ЗанятьФайл);
	
	
	ЭтоСлужебныйФайл = Ложь;
	Если ЗначениеЗаполнено(ВладелецФайла) И ДелопроизводствоКлиентСервер.ЭтоДокументПредприятия(ВладелецФайла) Тогда
		
		ЭтоСлужебныйФайл = РегистрыСведений.СлужебныеФайлыДокументов.ЭтоСлужебныйФайл(ВладелецФайла, Ссылка);
		
	КонецЕсли;	
	
	УдалениеСлужебногоФайла = Ложь;
	Если ДополнительныеСвойства.Свойство("УдалениеСлужебногоФайла") Тогда
		УдалениеСлужебногоФайла = ДополнительныеСвойства.УдалениеСлужебногоФайла;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВладелецФайла) Тогда
		
		Если ОбновлениеИнформационнойБазы.ВыполняетсяОбновлениеИнформационнойБазы() Тогда
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Файлы.Ошибка записи файла при обновлении ИБ'",
				     ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка, , Ссылка,
				СтрШаблон(
				НСтр("ru = 'В файле ""%1"" не заполнен ВладелецФайла'"),
				Строка(Ссылка)));
				
		Иначе	
				
			ВызватьИсключение НСтр("ru = 'Нельзя записать файл, если не указан владелец файла.'");
			
		КонецЕсли;	
		
	КонецЕсли;
	
	ЗаписьПодписанногоОбъекта = Ложь;
	Если ДополнительныеСвойства.Свойство("ЗаписьПодписанногоОбъекта") Тогда
		ЗаписьПодписанногоОбъекта = ДополнительныеСвойства.ЗаписьПодписанногоОбъекта;
	КонецЕсли;	  
	
	ВладелецИзменился = Ложь;
	
	Если Не ЭтоНовый() Тогда    
		
		СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, 
			"ПодписанЭП, Зашифрован, ПолноеНаименование, ПометкаУдаления, ВладелецФайла, ТекущаяВерсия.ПолноеНаименование");
		
		ИзменилсяТолькоВладелец = Ложь;
		ВладелецИзменился = ВладелецИзменился(СтруктураРеквизитов.ВладелецФайла);
		
		Если ВладелецИзменился Тогда
			
			СтрокаОшибки = "";
			Если Не РаботаСФайламиПереопределяемый.ВозможноСменитьВладельцаФайла(ЭтотОбъект, СтрокаОшибки) Тогда
				ВызватьИсключение СтрокаОшибки;
			КонецЕсли;
			
			ИменаРеквизитов = Новый Массив;
			Для Каждого Реквизит Из Метаданные.Справочники["Файлы"].Реквизиты Цикл
				ИменаРеквизитов.Добавить(Реквизит.Имя);
			КонецЦикла;
			Для Каждого Реквизит Из Метаданные.Справочники["Файлы"].СтандартныеРеквизиты Цикл
				ИменаРеквизитов.Добавить(Реквизит.Имя);
			КонецЦикла;
			
			ИзменилсяТолькоВладелец = Истина;
			
			ПрежниеЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, ИменаРеквизитов);
			Для Каждого ПрежнееЗначениеРеквизита Из ПрежниеЗначенияРеквизитов Цикл
				ИмяРеквизита = ПрежнееЗначениеРеквизита.Ключ;
				ЗначениеРеквизита = ПрежнееЗначениеРеквизита.Значение;
				
				ЗначенияРеквизитовРазличаются = Ложь;
				Если ТипЗнч(ЗначениеРеквизита) = Тип("ХранилищеЗначения") Тогда
					ЗначенияРеквизитовРазличаются = (ЗначениеРеквизита.Получить() <> ЭтотОбъект[ИмяРеквизита].Получить());
				Иначе
					ЗначенияРеквизитовРазличаются = (ЗначениеРеквизита <> ЭтотОбъект[ИмяРеквизита]);
				КонецЕсли;
				
				Если ИмяРеквизита <> "ВладелецФайла" И ЗначенияРеквизитовРазличаются Тогда
					ИзменилсяТолькоВладелец = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		ЕстьПометкаУдаленияВИБ = СтруктураРеквизитов.ПометкаУдаления;
		УстановленаПометкаУдаления = ПометкаУдаления И Не ЕстьПометкаУдаленияВИБ;
		ДополнительныеСвойства.Вставить("ПредыдущаяПометкаУдаления", ЕстьПометкаУдаленияВИБ);
		
		ИзмененаПометкаУдаления = Ложь;
		Если ДополнительныеСвойства.Свойство("ИзмененаПометкаУдаления") Тогда
			ИзмененаПометкаУдаления = ДополнительныеСвойства.ИзмененаПометкаУдаления;
		Иначе
			ИзмененаПометкаУдаления = (ПометкаУдаления <> ЕстьПометкаУдаленияВИБ);
			ДополнительныеСвойства.Вставить("ИзмененаПометкаУдаления", ИзмененаПометкаУдаления);
		КонецЕсли;
		
		// разрешаем ставить пометку удаления на подписанный файл
		Если Не ПривилегированныйРежим()
			И Не ЗаписьПодписанногоОбъекта
			И Не ИзмененаПометкаУдаления 
			И Не ИзменилсяТолькоВладелец Тогда
			
			СсылкаПодписан = СтруктураРеквизитов.ПодписанЭП;
			СсылкаЗашифрован = СтруктураРеквизитов.Зашифрован;
			
			Если ПодписанЭП И СсылкаПодписан Тогда
				ВызватьИсключение НСтр("ru = 'Подписанный файл нельзя редактировать.'");
			КонецЕсли;	
			
			Если Зашифрован И СсылкаЗашифрован И ПодписанЭП И НЕ СсылкаПодписан Тогда
				ВызватьИсключение НСтр("ru = 'Зашифрованный файл нельзя подписывать.'");
			КонецЕсли;	
			
		КонецЕсли;	
		
		Если Не ПометкаУдаления И Не ТекущаяВерсия.Пустая() Тогда
			
			// Проверим равенство имени файла и его текущей версии
			// Если имена отличаются - имя у версии должно стать как у карточки с файлом
			Если СтруктураРеквизитов.ТекущаяВерсияПолноеНаименование <> ПолноеНаименование Тогда
				Объект = ТекущаяВерсия.ПолучитьОбъект();
				Если Объект <> Неопределено И Объект.Ссылка <> Неопределено Тогда
					ЗаблокироватьДанныеДляРедактирования(Объект.Ссылка);
					УстановитьПривилегированныйРежим(Истина);
					Объект.ПолноеНаименование = ПолноеНаименование;
					Объект.ДополнительныеСвойства.Вставить("ПереименованиеФайла", Истина); // чтобы не сработала подписка СкопироватьРеквизитыВерсииФайловВФайл
					Объект.Записать();
					УстановитьПривилегированныйРежим(Ложь);
				КонецЕсли;	
			КонецЕсли;
			
		КонецЕсли;
		
		Если ИзмененаПометкаУдаления Тогда
			
			// Проверка права "Пометка на удаление".
			Если Не УдалениеСлужебногоФайла И Не РаботаСФайламиПереопределяемый.ПометкаУдаленияРазрешена(Ссылка) Тогда
				ВызватьИсключение СтрШаблон(
					НСтр("ru = 'У вас нет прав для установки пометки удаления файла ""%1"".'"),
					Строка(Ссылка));
			КонецЕсли;
			
			// Попытка установить пометку удаления
			Если УстановленаПометкаУдаления И ЗначениеЗаполнено(Редактирует) Тогда
				ВызватьИсключение СтрШаблон(
				                    НСтр("ru = 'Нельзя удалить файл ""%1"", т.к. он занят для редактирования пользователем ""%2"".'"),
				                    ПолноеНаименование,
				                    Строка(Редактирует) );
			КонецЕсли;
			
			СтрокаОшибки = "";
			Если Не РаботаСФайламиПереопределяемый.ВозможноУдалитьФайл(ЭтотОбъект, СтрокаОшибки) Тогда
				ВызватьИсключение СтрокаОшибки;
			КонецЕсли;
			
		КонецЕсли;
		
		НаименованиеВИБ = СтруктураРеквизитов.ПолноеНаименование;
		Если ПолноеНаименование <> НаименованиеВИБ И (ЗанятьФайл <> Истина) Тогда 
			Если ЗначениеЗаполнено(Редактирует) Тогда
				ВызватьИсключение СтрШаблон(
				                    НСтр("ru = 'Нельзя переименовать файл ""%1"", т.к. он занят для редактирования пользователем ""%2"".'"),
				                    НаименованиеВИБ,
				                    Строка(Редактирует) );
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоНовый() 
		И Не ДополнительныеСвойства.Свойство("ДобавлениеРегШтампа")
		И МиграцияДанныхИзВнешнихСистемСервер.ЭтоОбъектИзДругойСистемыСПроверкой(ВладелецФайла) Тогда
			
		ВызватьИсключение НСтр("ru = 'Нельзя добавлять файл к объекту, загруженному из другой системы.
							   |Обратитесь к администратору.'");
			
	КонецЕсли;	
	
	ВладелецФайлаВИБ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ВладелецФайла");
	Если ВладелецФайлаВИБ <> ВладелецФайла
		И Не ЗаписьПодписанногоОбъекта 
		И Не ДополнительныеСвойства.Свойство("ДобавлениеРегШтампа") Тогда
		
		ВладелецФайлаЭтоДокумент = ДелопроизводствоКлиентСервер.ЭтоДокумент(ВладелецФайлаВИБ);
		Если ВладелецФайлаЭтоДокумент Тогда
			ВладелецФайлаПодписанЭП = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВладелецФайлаВИБ, "ПодписанЭП");
			Если ВладелецФайлаПодписанЭП Тогда
				ВызватьИсключение НСтр("ru = 'Нельзя сменить владельца файла подписанного документа.'");
			КонецЕсли;
		КонецЕсли;
		
		НовыйВладелецФайлаЭтоДокумент = ДелопроизводствоКлиентСервер.ЭтоДокумент(ВладелецФайла);
		Если НовыйВладелецФайлаЭтоДокумент Тогда
			НовыйВладелецФайлаПодписанЭП = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВладелецФайла, "ПодписанЭП");
			Если НовыйВладелецФайлаПодписанЭП Тогда
				ВызватьИсключение 
					СтрШаблон(НСтр("ru = 'Нельзя установить владельцем файла подписанный документ (%1).'"), ВладелецФайла);
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетДоступаКПерсональнымДанным") Тогда		
		
		Если ЭтоНовый() Тогда
			ПерсональныеДанные.ЗаполнитьСписокСубъектовПерсональныхДанныхФайла(ЭтотОбъект);
			Если СубъектыПерсональныхДанных.Количество() <> 0 Тогда
				ДополнительныеСвойства.Вставить("ОбновитьПерсональныеДанныеВерсий", Истина);
			КонецЕсли;	
		ИначеЕсли ВладелецИзменился Тогда
			ПерсональныеДанные.ЗаполнитьСписокСубъектовПерсональныхДанныхФайла(ЭтотОбъект);
			ДополнительныеСвойства.Вставить("ОбновитьПерсональныеДанныеВерсий", Истина);
		Иначе
			Если ПерсональныеДанные.ИзменилсяСписокПерсональныхДанных(ЭтотОбъект) Тогда
				ДополнительныеСвойства.Вставить("ОбновитьПерсональныеДанныеВерсий", Истина);
			КонецЕсли;	
		КонецЕсли;	
		
	КонецЕсли;
		
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		Если ЗначениеЗаполнено(Ссылка) Тогда 
			СсылкаПроект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Проект");
			ДополнительныеСвойства.Вставить("ИзменилсяПроект", СсылкаПроект <> Проект);
		КонецЕсли;	
	КонецЕсли;	
	
	Наименование = СокрЛП(ПолноеНаименование);
	
КонецПроцедуры

Функция ВладелецИзменился(СтарыйВладелец)
	
	Если СтарыйВладелец = ВладелецФайла Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;	
	
КонецФункции	

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		ДатаСоздания = ТекущаяДатаСеанса();
		ХранитьВерсии = Истина;
		ИндексКартинки = ФайловыеФункцииКлиентСервер.ПолучитьИндексПиктограммыФайла(Неопределено);
		
		Автор = Сотрудники.ОсновнойСотрудникПользователя(
			Пользователи.ТекущийПользователь());

		ДатаСоздания = ТекущаяДата();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если МиграцияПриложенийПереопределяемый.ЭтоЗагрузка(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Возможно, выполнена явная регистрация событий при загрузке объекта.
	Если Не ДополнительныеСвойства.Свойство("НеРегистрироватьБизнесСобытия") Тогда
		Если ДополнительныеСвойства.Свойство("ЭтоНовый") И ДополнительныеСвойства.ЭтоНовый Тогда
			
			БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(Ссылка, Справочники.ВидыБизнесСобытий.СозданиеФайла);	
			
		КонецЕсли;	
	КонецЕсли;
	
	// Возможно, выполнена явная регистрация событий при загрузке объекта.
	Если ДополнительныеСвойства.Свойство("ЭтоНовый") И ДополнительныеСвойства.ЭтоНовый Тогда
		
		ПротоколированиеРаботыСотрудников.ЗаписатьСоздание(
			Ссылка, Истина);
		
	КонецЕсли;
	
	ПредыдущаяПометкаУдаления = Ложь;
	Если ДополнительныеСвойства.Свойство("ПредыдущаяПометкаУдаления") Тогда
		ПредыдущаяПометкаУдаления = ДополнительныеСвойства.ПредыдущаяПометкаУдаления;
	КонецЕсли;
	
	Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда
		ПротоколированиеРаботыСотрудников.ЗаписатьПометкуУдаления(Ссылка, ПометкаУдаления);
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ОбновитьПерсональныеДанныеВерсий")
		И ДополнительныеСвойства.ОбновитьПерсональныеДанныеВерсий Тогда
		
		ПерсональныеДанные.ЗаполнитьПерсональныеДанныеВерсийФайла(Ссылка);
		
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ЭтоНовый") И ДополнительныеСвойства.ЭтоНовый Тогда
		
		// При изменении документа/файлов делать непрочтенным
		// тут добавление файла в документ
		Если ТипЗнч(ВладелецФайла) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда
			РегистрыСведений.СведенияОПрочтении.УбратьСведенияОПрочтенииВсемКромеАвтора(ВладелецФайла);
		КонецЕсли;
		
	Иначе
		Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда
			
			// При изменении документа/файлов делать непрочтенным
			// тут удаление/снятие пометки удаления файла в документе
			Если ТипЗнч(ВладелецФайла) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда
				РегистрыСведений.СведенияОПрочтении.УбратьСведенияОПрочтенииВсемКромеАвтора(ВладелецФайла);
			КонецЕсли;
			
		КонецЕсли;	
	КонецЕсли;
	
	ОбработкаОчередиЗаданийСервер.ПриЗаписиФайла(ЭтотОбъект);
	
КонецПроцедуры

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли