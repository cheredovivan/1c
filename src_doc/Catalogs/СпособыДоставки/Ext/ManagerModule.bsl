#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	МультиязычностьКлиентСервер.ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	МультиязычностьКлиентСервер.ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	МультиязычностьСервер.ОбработкаПолученияДанныхВыбора(
		ДанныеВыбора, Параметры, СтандартнаяОбработка, Метаданные.Справочники.СпособыДоставки);
	
	// Локализация
	ИспользоватьМЭДО = ПолучитьФункциональнуюОпцию("ИспользоватьМЭДО");
	ИспользоватьСервис1СShare = ПолучитьФункциональнуюОпцию("ИспользоватьСервис1сShare");
	Если ИспользоватьСервис1СShare Тогда
		ИспользоватьСервис1СShare = ИнтеграцияShareДокументооборот.ЕстьПравоОтправлятьДокументы();
	КонецЕсли;	
	
	Если Не ИспользоватьМЭДО Или Не ИспользоватьСервис1СShare Тогда
		СтандартнаяОбработка = Ложь;
		
		Если ДанныеВыбора = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	СпособыДоставки.Ссылка,
				|	СпособыДоставки.Наименование
				|ИЗ
				|	Справочник.СпособыДоставки КАК СпособыДоставки
				|ГДЕ
				|	СпособыДоставки.Наименование ПОДОБНО &Текст
				|	И СпособыДоставки.ИмяПредопределенныхДанных Не В (&Массив)";
			
			ПометкаУдаления = Неопределено;
			Если Параметры.Отбор.Свойство("ПометкаУдаления", ПометкаУдаления) Тогда
				Запрос.Текст = СтрШаблон("%1 %2 И СпособыДоставки.ПометкаУдаления = &ПометкаУдаления",Запрос.Текст,
					Символы.ВТаб);
				Запрос.УстановитьПараметр("ПометкаУдаления", ПометкаУдаления);
			КонецЕсли;
			
			Массив = Новый Массив();
			Если Не ИспользоватьМЭДО Тогда
				Массив.Добавить("МЭДО");
			КонецЕсли;
			Если Не ИспользоватьСервис1СShare Тогда
				Массив.Добавить("Сервис1СShare");
			КонецЕсли;
			
			Запрос.УстановитьПараметр("Текст", Строка(Параметры.СтрокаПоиска) + "%");
			Запрос.УстановитьПараметр("Массив", Массив);
			
			ДанныеВыбора = Новый СписокЗначений();
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				ДанныеВыбора.Добавить(Выборка.Ссылка, Выборка.Наименование);
			КонецЦикла;
		
		ИначеЕсли ТипЗнч(ДанныеВыбора) = Тип("СписокЗначений") Тогда
			
			Если Не ИспользоватьМЭДО Тогда
				ЭлементМЭДО = ДанныеВыбора.НайтиПоЗначению(МЭДО);
				Если ЭлементМЭДО <> Неопределено Тогда
					ДанныеВыбора.Удалить(ЭлементМЭДО);
				КонецЕсли;
			КонецЕсли;
			Если Не ИспользоватьСервис1СShare Тогда
				ЭлементShare = ДанныеВыбора.НайтиПоЗначению(Сервис1СShare);
				Если ЭлементShare <> Неопределено Тогда
					ДанныеВыбора.Удалить(ЭлементShare);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	// Локализация Конец
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
