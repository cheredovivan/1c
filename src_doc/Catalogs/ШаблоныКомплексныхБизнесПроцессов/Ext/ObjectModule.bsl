#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Проверяет что заполнены поля шаблона
Функция ПолучитьСписокНезаполненныхПолейНеобходимыхДляСтарта() Экспорт
	
	МассивПолей = Новый Массив;
	
	Если ЗначениеЗаполнено(Схема) Тогда
		
		Если Не ОбщегоНазначения.СсылкаСуществует(Схема) Тогда
			МассивПолей.Добавить(НСтр("ru='Схема шаблона отсутствует в базе.'"));
			Возврат МассивПолей;
		КонецЕсли;
		
		ДанныеСхемы = Справочники.СхемыПроцессов.ДанныеСхемыПроцесса(Схема);
		
		РезультатПроверкиСхемы = СхемыПроцессовСервер.СхемаКорректна(ДанныеСхемы.Схема);
		
		Если Не РезультатПроверкиСхемы.НетОшибок Тогда
			МассивПолей.Добавить("Схема");
			Возврат МассивПолей;
		КонецЕсли;
			
		ПараметрыСхемы = 
			Справочники.ПараметрыСхемДляКомплексныхПроцессов.ПараметрыПоСхеме(Схема);
		Если Не ЗначениеЗаполнено(ПараметрыСхемы) Тогда
			МассивПолей.Добавить(НСтр("ru='Параметры схема шаблона отсутствует в базе.'"));
			Возврат МассивПолей;
		КонецЕсли;
		
		ДанныеПараметровСхемы = 
			Справочники.ПараметрыСхемДляКомплексныхПроцессов.ДанныеПараметровСхемы(
			ПараметрыСхемы);
		
		ОшибкиНастроекСхемы = 
			РаботаСКомплекснымиБизнесПроцессамиСервер.ОшибкиЗаполненияНастроекЭлементовСхемы(
			ДанныеПараметровСхемы.НастройкиЭлементов,
			СхемыПроцессовКлиентСервер.ТипыВсехЭлементовПоДаннымСхемы(ДанныеСхемы),
			СхемыПроцессовКлиентСервер.ГрафическаяСхемаПоДаннымСхемы(ДанныеСхемы));
		
		Если ОшибкиНастроекСхемы.Количество() > 0 Тогда
			МассивПолей.Добавить("Схема");
		КонецЕсли;
	
	Иначе
	
		Если Этапы.Количество() = 0 Тогда
			МассивПолей.Добавить("Этапы");
		КонецЕсли;
		
		Для Каждого Шаблон Из Этапы Цикл
			
			Если ЗначениеЗаполнено(Шаблон.ШаблонБизнесПроцесса) Тогда
				
				Если Не ОбщегоНазначения.СсылкаСуществует(Шаблон.ШаблонБизнесПроцесса) Тогда
					ОписаниеОшибки = СтрШаблон(
						НСтр("ru = 'Настройка этапа №""%1"" отсутствует в базе.'", ОбщегоНазначения.КодОсновногоЯзыка()),
						Шаблон.НомерСтроки);
					МассивПолей.Добавить(ОписаниеОшибки);
					Продолжить;
				КонецЕсли;
				
				ШаблонОбъект = Шаблон.ШаблонБизнесПроцесса.ПолучитьОбъект();
				
				МассивПолейШаблона = ШаблонОбъект.ПолучитьСписокНезаполненныхПолейНеобходимыхДляСтарта();
				Если МассивПолейШаблона.Количество() <> 0 Тогда
					
					ВсеПоляСтрокой = БизнесСобытияВызовСервера.МассивПолейВСтроку(МассивПолейШаблона);
					СтрокаПолей = Строка(Шаблон.ШаблонБизнесПроцесса) + ": " + НСтр("ru='Ошибка заполнения: '") + ВсеПоляСтрокой;
					МассивПолей.Добавить(СтрокаПолей);
					
				КонецЕсли;	
				
			КонецЕсли;	
			
		КонецЦикла;
	КонецЕсли;

	Возврат МассивПолей;
	
КонецФункции	

//Формирует текстовое представление бизнес-процесса, создаваемого по шаблону
Функция СформироватьСводкуПоШаблону() Экспорт
	
	Результат = ШаблоныБизнесПроцессов.ПолучитьОбщуюЧастьОписанияШаблона(Ссылка);
	
	Если ЗначениеЗаполнено(НаименованиеБизнесПроцесса) Тогда
		Результат = Результат + НСтр("ru = 'Заголовок'") + ": " + НаименованиеБизнесПроцесса + Символы.ПС;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Описание) Тогда
		Результат = Результат + НСтр("ru = 'Описание'") + ": " + Описание + Символы.ПС;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Важность) Тогда
		Результат = Результат + НСтр("ru = 'Важность'") + ": " + Строка(Важность) + Символы.ПС;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Схема) Тогда
		
		ДлительностьПроцесса = СрокиИсполненияПроцессов.ДлительностьИсполненияПроцесса(ЭтотОбъект);
		ДлительностьПроцессаСтрокой = СрокиИсполненияПроцессовКлиентСервер.ПредставлениеДлительности(
			ДлительностьПроцесса.СрокИсполненияПроцессаДни,
			ДлительностьПроцесса.СрокИсполненияПроцессаЧасы,
			ДлительностьПроцесса.СрокИсполненияПроцессаМинуты);
			
		Если ЗначениеЗаполнено(ДлительностьПроцессаСтрокой) Тогда
			Результат = Результат + Нстр("ru = 'Срок'") + ": "
				+ ДлительностьПроцессаСтрокой;
		КонецЕсли;
	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Заполняет предопределенный шаблон по умолчанию.
// Предназначена для вызова из предопределенного объекта.
// Если объект не предопределенный, то вызов процедуры приведет к исключению.
//
Процедура ЗаполнитьШаблонПоУмолчанию() Экспорт

	Если Ссылка <> Справочники.ШаблоныКомплексныхБизнесПроцессов.ПоУмолчанию Тогда
		ВызватьИсключение
			НСтр("ru = 'Процедура ЗаполнитьШаблонПоУмолчанию предназначена для вызова из предопределенного шаблона.'");
	КонецЕсли;
	
	Автор = Справочники.Сотрудники.ПустаяСсылка();
	Важность = Перечисления.ВариантыВажностиОбъектов.Обычная;
	ВариантМаршрутизации = Перечисления.ВариантыМаршрутизацииЗадач.Последовательно;
	ВладелецШаблона = Неопределено;
	ДобавлятьНаименованиеПредмета = Истина;
	ИдентификаторСсылки = Ссылка.УникальныйИдентификатор();
	ИсходныйШаблон = Справочники.ШаблоныКомплексныхБизнесПроцессов.ПустаяСсылка();
	Комментарий = "";
	КомплексныйПроцесс = БизнесПроцессы.КомплексныйПроцесс.ПустаяСсылка();
	Контролер = Справочники.Сотрудники.ПустаяСсылка();
	НаименованиеБизнесПроцесса = НСтр("ru = 'Обработка '");
	Наименование = РаботаСКомплекснымиБизнесПроцессамиСервер.НаименованиеШаблонаПроцессаПоУмолчанию();
	Описание = "";
	
	Ответственный = Сотрудники.ОсновнойСотрудник();
	Если Не Сотрудники.ЭтоПолноправныйСотрудник(Ответственный) Тогда
		Ответственный = Справочники.Сотрудники.ПустаяСсылка();
	КонецЕсли;
	
	СрокИсполненияПроцесса = Дата(1,1,1);
	
	СрокОтложенногоСтарта = 0;
	
	Схема = РаботаСКомплекснымиБизнесПроцессамиСервер.СхемаШаблонаПроцессаПоУмолчанию();
	
	ТрудозатратыПланКонтролера = 0;
	
	ШаблонВКомплексномПроцессе = Ложь;
	
	Предметы.Очистить();
	ПредметыЗадач.Очистить();
	ПредшественникиЭтапов.Очистить();
	Этапы.Очистить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ПоддержкаМеханизмаОтсутствий

// Получает исполнителей
Функция ПолучитьИсполнителей() Экспорт
	
	МассивИсполнителей = Новый Массив;
	
	Если ЗначениеЗаполнено(Схема) Тогда
				
		ДанныеПараметров =
			Справочники.ПараметрыСхемДляКомплексныхПроцессов.ДанныеПараметровСхемы(
			Справочники.ПараметрыСхемДляКомплексныхПроцессов.ПараметрыПоСхеме(Схема));
		
		ТипыНастроек = Метаданные.ОпределяемыеТипы.ШаблонДействияКомплексногоПроцесса.Тип;
		
		Для Каждого ИмяЭлементаИНастройка Из ДанныеПараметров.НастройкиЭлементов Цикл
			
			НастройкаЭлемента = ИмяЭлементаИНастройка.Значение;
			
			Если Не ТипыНастроек.СодержитТип(ТипЗнч(НастройкаЭлемента))
				Или Не ЗначениеЗаполнено(НастройкаЭлемента) Тогда
				
				Продолжить
			КонецЕсли;
			
			НастройкаЭлементаОбъект = НастройкаЭлемента.ПолучитьОбъект();
			Если НастройкаЭлементаОбъект = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ИсполнителиДействия = НастройкаЭлементаОбъект.ПолучитьИсполнителей();
			Для Каждого Исполнитель Из ИсполнителиДействия Цикл
				МассивИсполнителей.Добавить(Исполнитель);
			КонецЦикла;
			
		КонецЦикла;
		
	Иначе
		
		Для Каждого РабочийЭтап Из Этапы Цикл
			
			РабочийШаблон = РабочийЭтап.ШаблонБизнесПроцесса;
			Если Не ЗначениеЗаполнено(РабочийШаблон) Тогда
				Продолжить;
			КонецЕсли;
			
			ШаблонОбъект = РабочийШаблон.ПолучитьОбъект();
			Если ШаблонОбъект = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ИсполнителиЭтапа = ШаблонОбъект.ПолучитьИсполнителей();
			Для Каждого Исполнитель Из ИсполнителиЭтапа Цикл
				МассивИсполнителей.Добавить(Исполнитель);
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;
	
	Возврат МассивИсполнителей;
	
КонецФункции

#КонецОбласти

// Возвращает ссылку на шаблон.
//
Функция СсылкаНаШаблон() Экспорт
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		СсылкаНаПроцесс = Ссылка;
	Иначе
		СсылкаНаПроцесс = ОпределитьСсылкуДляНовогоШаблона();
	КонецЕсли;
	
	Возврат СсылкаНаПроцесс;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ШаблоныБизнесПроцессов.НачальноеЗаполнениеШаблона(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Ссылка <> Справочники.ШаблоныКомплексныхБизнесПроцессов.ПоУмолчанию Тогда
		ПроверяемыеРеквизиты.Добавить("НаименованиеБизнесПроцесса");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Схема) Тогда
		ПроверяемыеРеквизиты.Добавить("ВариантМаршрутизации");
	КонецЕсли;
	
	КомплексныйПроцессДействия = Неопределено;
	
	ВладелецВерхнегоУровня = РаботаСКомплекснымиБизнесПроцессамиСервер.ВладелецВерхнегоУровня(ЭтотОбъект);
	Если ТипЗнч(ВладелецВерхнегоУровня) = Тип("БизнесПроцессСсылка.КомплексныйПроцесс") Тогда
		КомплексныйПроцессДействия = ВладелецВерхнегоУровня;
	КонецЕсли;
	
	// Проверку заполнения и отказ в случае ошибок, выполняем только 
	// для действий принадлежащих комплексным процессам.
	Если Не ЗначениеЗаполнено(КомплексныйПроцессДействия) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Схема) Тогда
		
		// Проверяем корректность схемы и настроек элементов,
		// только если действие принадлежит выполняющемуся комплексному процессу.
		// Если процесс не выполняется, то проверку действия не выполняем, т.к. схема действия
		// может быть сложной и в процессе настройке нужна возможность записи.
		// Проверка схемы текущего действия будет выполнена перед стартом/продолжением комплексного процесса.
		
		РеквизитыПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			КомплексныйПроцессДействия, "Стартован, Завершен, Состояние");
		
		НастройкиСтарта =
			РегистрыСведений.НастройкиОтложенногоСтартаПроцессов.ПолучитьСведенияОЗапускеПроцесса(
			КомплексныйПроцессДействия);
		АктивнаНастройкаФоновогоСтарта = НастройкиСтарта <> Неопределено
			И НастройкиСтарта.Состояние = Перечисления.СостоянияПроцессовДляЗапуска.ГотовКСтарту;
		
		Если Не РеквизитыПроцесса.Завершен
			И РеквизитыПроцесса.Состояние = Перечисления.СостоянияБизнесПроцессов.Активен
			И (РеквизитыПроцесса.Стартован Или АктивнаНастройкаФоновогоСтарта) Тогда
			
			ДанныеСхемы = Справочники.СхемыПроцессов.ДанныеСхемыПроцесса(Схема);
			
			РезультатПроверкиСхемы = СхемыПроцессовСервер.СхемаКорректна(ДанныеСхемы.Схема);

			Если Не РезультатПроверкиСхемы.НетОшибок Тогда
				ОбщегоНазначения.СообщитьПользователю(
					РезультатПроверкиСхемы.ОписаниеОшибки,
					ЭтотОбъект,,
					"Схема",
					Отказ);
			КонецЕсли;
			
			ПараметрыСхемы = 
				Справочники.ПараметрыСхемДляКомплексныхПроцессов.ПараметрыПоСхеме(Схема);
			Если ЗначениеЗаполнено(ПараметрыСхемы) Тогда
				
				ДанныеПараметровСхемы = 
					Справочники.ПараметрыСхемДляКомплексныхПроцессов.ДанныеПараметровСхемы(
					ПараметрыСхемы);
			
				ОшибкиНастроекСхемы = 
					РаботаСКомплекснымиБизнесПроцессамиСервер.ОшибкиЗаполненияНастроекЭлементовСхемы(
					ДанныеПараметровСхемы.НастройкиЭлементов,
					СхемыПроцессовКлиентСервер.ТипыВсехЭлементовПоДаннымСхемы(ДанныеСхемы),
					СхемыПроцессовКлиентСервер.ГрафическаяСхемаПоДаннымСхемы(ДанныеСхемы));
					
				Для Каждого ОшибкаНастройкиСхемы Из ОшибкиНастроекСхемы Цикл
					ОбщегоНазначения.СообщитьПользователю(
						ОшибкаНастройкиСхемы,
						ЭтотОбъект,,
						"Схема",
						Отказ);
				КонецЦикла;
				
			КонецЕсли;
		
		КонецЕсли;
		
	Иначе
		
		// Проверяем корректность заполнения этапов, если действие принадлежит комплексному процессу.
		// В этом случаях считаем недопустимым сохранение шаблона с некорректными настройками
		// действий.
		
		Если Этапы.Количество() = 0 Тогда
			ПроверяемыеРеквизиты.Добавить("Этапы");
		КонецЕсли;
		
		Для Каждого Этап Из Этапы Цикл
			
			МассивПолей = Этап.ШаблонБизнесПроцесса.ПолучитьОбъект().ПолучитьСписокНезаполненныхПолейНеобходимыхДляСтарта();
			Если МассивПолей.Количество() <> 0 Тогда
				
				Если МассивПолей.Найти("Схема") <> Неопределено Тогда
					
					ТекстСообщения = СтрШаблон(
						НСтр("ru = 'Схема действия %1 ""%2"" настроена неверно. Проверьте схему.'"),
						Этап.НомерСтроки,
						Строка(Этап.ШаблонБизнесПроцесса))
					
				Иначе
					
					СтрокаНезаполненныеПоля = СтрСоединить(МассивПолей, ", ");
					
					ТекстСообщения = СтрШаблон(
						НСтр("ru = 'При настройке этапа %1 ""%2"" не заполнены необходимые для запуска поля: %3.'"),
						Этап.НомерСтроки,
						Строка(Этап.ШаблонБизнесПроцесса),
						СтрокаНезаполненныеПоля);
					
				КонецЕсли;
					
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					"Этапы",,
					Отказ);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОпределитьСсылкуДляНовогоШаблона();
	
	ШаблоныБизнесПроцессов.ШаблонПередЗаписью(ЭтотОбъект, Отказ);
	
	Если ДополнительныеСвойства.Свойство("ВидЗаписи") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Схема) Тогда
		РаботаСКомплекснымиБизнесПроцессамиСервер.ОбновитьПорядокСортировкиЭтапов(ЭтотОбъект);
	КонецЕсли;
	
	ПредыдущаяПометкаУдаления = Ложь;
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ПредыдущаяПометкаУдаления = ОбщегоНазначенияДокументооборот.ЗначениеРеквизитаОбъектаВПривилегированномРежиме(
			Ссылка, "ПометкаУдаления");
	КонецЕсли;
	ДополнительныеСвойства.Вставить("ПредыдущаяПометкаУдаления", ПредыдущаяПометкаУдаления);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если МиграцияДанныхИзВнешнихСистемСервер.ЭтоОбъектИзДругойСистемы(ИсточникДанных)
		Или ПараметрыСеанса.ЗагрузкаОбработанныхДанныхИзДругойСистемы = Истина Тогда
		
		Возврат;
	КонецЕсли;
	
	ОбычнаяЗапись = Истина;
	ТолькоОбновитьРеквизитыСлужебныхШаблонов = Ложь;
	Если ДополнительныеСвойства.Свойство("ВидЗаписи") Тогда
		
		ОбычнаяЗапись = Ложь;
		
		ТолькоОбновитьРеквизитыСлужебныхШаблонов = (ДополнительныеСвойства.ВидЗаписи = 
			"ЗаписьСЗаполнениемСлужебныхРеквизитовКомплексныхПроцессов");
		
		Если Не ТолькоОбновитьРеквизитыСлужебныхШаблонов Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Схема) И ОбычнаяЗапись Или ТолькоОбновитьРеквизитыСлужебныхШаблонов Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		// Обновление служебных реквизитов КП в подчиненных шаблонах.
		Для Каждого Этап Из Этапы Цикл
			
			РеквизитыШаблонаЭтапа = ОбщегоНазначенияДокументооборот.
				ЗначенияРеквизитовОбъектаВПривилегированномРежиме(
					Этап.ШаблонБизнесПроцесса, "ВладелецШаблона, КомплексныйПроцесс");
			
			ВладелецШаблонаЭтапа = РеквизитыШаблонаЭтапа.ВладелецШаблона;
			КомплексныйПроцессЭтапа = РеквизитыШаблонаЭтапа.КомплексныйПроцесс;
			
			Если ВладелецШаблонаЭтапа <> Ссылка
				Или КомплексныйПроцессЭтапа <> КомплексныйПроцесс Тогда
				
				ШаблонОбъект = Этап.ШаблонБизнесПроцесса.ПолучитьОбъект();
				ШаблонОбъект.ВладелецШаблона = Ссылка;
				ШаблонОбъект.ШаблонВКомплексномПроцессе = Истина;
				
				ШаблоныБизнесПроцессов.ЗаписатьШаблон(ШаблонОбъект,
					"ЗаписьСЗаполнениемСлужебныхРеквизитовКомплексныхПроцессов");
				
			КонецЕсли;
		КонецЦикла;
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	Если ОбычнаяЗапись Тогда
	
		ПометкаУдаленияСсылки = ДополнительныеСвойства.ПредыдущаяПометкаУдаления;
		Если ПометкаУдаления <> ПометкаУдаленияСсылки Тогда
			Если ЗначениеЗаполнено(Схема) Тогда
				УстановитьПривилегированныйРежим(Истина);
				Схема.ПолучитьОбъект().УстановитьПометкуУдаления(ПометкаУдаления);
				УстановитьПривилегированныйРежим(Ложь);
			Иначе
				УстановитьПометкуУдаленияЭтапов(ПометкаУдаления);
			КонецЕсли;
		КонецЕсли;
		
		РаботаСКомплекснымиБизнесПроцессамиСервер.
				УдалитьНепривязанныеНастройкиЭлементовСхемы(ЭтотОбъект);
		УдалитьНепривязанныеДействия();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ШаблоныБизнесПроцессов.ПриКопированииШаблонаБизнесПроцесса(ОбъектКопирования);
	
	Если ОбъектКопирования.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторСсылки = УникальныйИдентификаторПустой();
	
	Ответственный = Сотрудники.ОсновнойСотрудник();
	
	Если ЗначениеЗаполнено(ОбъектКопирования.Схема) Тогда
		
		РаботаСКомплекснымиБизнесПроцессамиСервер.СкопироватьСхемуКомплексногоПроцесса(
			ОбъектКопирования, ЭтотОбъект);
					
		Мультипредметность.ОбновитьПредметыЗадачКомплексногоПроцессаПослеИзмененияДействий(
			ЭтотОбъект);
		
	Иначе
	
		Для Каждого ЭтапОбъектаКопирования Из ОбъектКопирования.Этапы Цикл
			Для Каждого ЭтапПроцесса Из Этапы Цикл
				
				Если ЭтапПроцесса.ИдентификаторЭтапа <> ЭтапОбъектаКопирования.ИдентификаторЭтапа
					Или Не ЗначениеЗаполнено(ЭтапОбъектаКопирования.ШаблонБизнесПроцесса.ВладелецШаблона) Тогда
					
					Продолжить;
				КонецЕсли;
				
				КопируемыйШаблонОбъект = ЭтапОбъектаКопирования.ШаблонБизнесПроцесса.ПолучитьОбъект();
				
				ШаблонЯвляетсяЧастьюКомплексногоПроцесса = 
					ЗначениеЗаполнено(КопируемыйШаблонОбъект.КомплексныйПроцесс);
				
				КопируемыйШаблонОбъект.КомплексныйПроцесс = КомплексныйПроцесс;
				
				НовыйШаблон = КопируемыйШаблонОбъект.Скопировать();
				НовыйШаблон.ВладелецШаблона = Справочники.ШаблоныКомплексныхБизнесПроцессов.ПустаяСсылка();
				НовыйШаблон.ШаблонВКомплексномПроцессе = Истина;
				НовыйШаблон.Ответственный = Сотрудники.ОсновнойСотрудник();
				
				Если ЗначениеЗаполнено(КомплексныйПроцесс) Тогда
					Если Не ШаблонЯвляетсяЧастьюКомплексногоПроцесса Тогда
						НовыйШаблон.ИсходныйШаблон = КопируемыйШаблонОбъект.Ссылка;
					КонецЕсли;
				КонецЕсли;
				
				ШаблоныБизнесПроцессов.ЗаписатьШаблон(НовыйШаблон,
					"ЗаписьСЗаполнениемСлужебныхРеквизитовКомплексныхПроцессов");
				
				СтрокиЭтаповШаблона = ПредметыЗадач.НайтиСтроки(
					Новый Структура("ИдентификаторЭтапа", ЭтапПроцесса.ИдентификаторЭтапа));
				
				Для Каждого СтрокаЭтапаШаблона Из СтрокиЭтаповШаблона Цикл
					СтрокаЭтапаШаблона.ШаблонБизнесПроцесса = НовыйШаблон.Ссылка;
				КонецЦикла;
				
				ЭтапПроцесса.ШаблонБизнесПроцесса = НовыйШаблон.Ссылка;
				
			КонецЦикла;
		КонецЦикла;
		
		ПредшественникиЭтапов.Очистить();
		ВариантМаршрутизации = ОбъектКопирования.ВариантМаршрутизации;
		Для Каждого СтрокаПредшественник Из ОбъектКопирования.ПредшественникиЭтапов Цикл
			НоваяСтрока = ПредшественникиЭтапов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПредшественник);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает ссылку для нового, незаписанного шаблона.
//
Функция ОпределитьСсылкуДляНовогоШаблона()
	
	Если ЗначениеЗаполнено(Ссылка) Или ЭтоГруппа Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СсылкаНового = ПолучитьСсылкуНового();
	Если Не ЗначениеЗаполнено(СсылкаНового) Тогда
		
		Если Не ЗначениеЗаполнено(ИдентификаторСсылки) Тогда
			ИдентификаторСсылки = Новый УникальныйИдентификатор;
		КонецЕсли;
		
		СсылкаНового = Справочники.ШаблоныКомплексныхБизнесПроцессов.ПолучитьСсылку(ИдентификаторСсылки);
		
		УстановитьСсылкуНового(СсылкаНового);
	КонецЕсли;
	
	Возврат СсылкаНового;
	
КонецФункции

// Устанавливает пометку удаления этапам шаблона.
// 
// Параметры:
//   Установить - Булево - потека удаления.
//
Процедура УстановитьПометкуУдаленияЭтапов(Установить)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого Этап Из Этапы Цикл
		Если ЗначениеЗаполнено(Этап.ШаблонБизнесПроцесса.ВладелецШаблона) Тогда
			Этап.ШаблонБизнесПроцесса.ПолучитьОбъект().УстановитьПометкуУдаления(Установить);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Установка пометки удаления у шаблонов, которые были созданы, но не прикреплены к комплексному процессу.
// Например, создали шаблон комплексного процесса, добавили действие, а затем не сохранили шаблон комплексного процесса.
Процедура УдалитьНепривязанныеДействия()
	
	Если ЗначениеЗаполнено(Схема) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ШаблонЗапроса = 
		"ВЫБРАТЬ
		|	ШаблоныПроцессов.Ссылка
		|ИЗ
		|	Справочник.%1 КАК ШаблоныПроцессов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШаблоныКомплексныхБизнесПроцессов.Этапы КАК ЭтапыПроцесса
		|		ПО (ЭтапыПроцесса.ШаблонБизнесПроцесса = ШаблоныПроцессов.Ссылка)
		|ГДЕ
		|	ШаблоныПроцессов.ВладелецШаблона = &ВладелецШаблона
		|	И ЭтапыПроцесса.Ссылка ЕСТЬ NULL ";
	ТипыШаблонов = ЭтотОбъект.Метаданные().ТабличныеЧасти.Этапы.Реквизиты.ШаблонБизнесПроцесса.Тип.Типы();
	Для Каждого Тип Из ТипыШаблонов Цикл
		ШаблонОбъект = Новый(Тип);
		Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонЗапроса,
			ШаблонОбъект.Метаданные().Имя);
		Запрос.УстановитьПараметр("ВладелецШаблона", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ШаблонДляУдаления = Выборка.Ссылка.ПолучитьОбъект();
			ШаблонДляУдаления.УстановитьПометкуУдаления(Истина);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли