#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Делопроизводство.СписокДокументовУсловноеОформлениеПомеченныхНаУдаление(Список);
	
	Если ЗначениеЗаполнено(Параметры.ВидОбъекта) Тогда 
		ОбработкаОбъекта = Справочники.ОбработкиОбъектов.ОбработкаОбъекта(Параметры.Документ);
		ДатаОбработки = Дата(1, 1, 1);
		РежимВыбораДействий = Истина;
		Если ЗначениеЗаполнено(ОбработкаОбъекта) Тогда
			ДатаОбработки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбработкаОбъекта, "ДатаСоздания");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДатаОбработки) Тогда 
			ДатаОбработки = ТекущаяДатаСеанса();
		КонецЕсли;
		НастройкиОбработки = ДействияСервер.НастройкиОбработкиВидаОбъекта(Параметры.ВидОбъекта, ДатаОбработки);
		Если НастройкиОбработки.Количество() > 0 Тогда 
			ДоступныеВидыДействия = Новый Массив; 
			Для Каждого СтрНастройка Из НастройкиОбработки Цикл
				Если Параметры.ТекущиеВидыДействий.НайтиПоЗначению(СтрНастройка.ВидДействия) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				ДоступныеВидыДействия.Добавить(СтрНастройка.ВидДействия);
				НастройкаОбработки = СтрНастройка.СсылкаНаНастройку;	
				
			КонецЦикла;
			
			Если ДоступныеВидыДействия.Количество() > 0 Или Параметры.Свойство("ТекущиеВидыДействий") Тогда 
				ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
					Список, "ДоступныеВидыДействия", ДоступныеВидыДействия, Истина);
			КонецЕсли;
			
		ИначеЕсли Параметры.Свойство("ТекущиеВидыДействий") Тогда 
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
				Список, "НеДоступныеВидыДействия", Параметры.ТекущиеВидыДействий, Истина);
		КонецЕсли;
	КонецЕсли;
	
	ВыбиратьТолькоЭлементы = (Параметры.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Элементы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбратьНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьНаКлиенте()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбиратьТолькоЭлементы
		И ТекущиеДанные.ЭтоГруппа Тогда
		
		Возврат;
	КонецЕсли;
	
	Если РежимВыбораДействий Тогда
		СтруктураВозврата = Новый Структура("ВидДействия, НастройкаОбработки",
			Элементы.Список.ТекущаяСтрока, НастройкаОбработки);
		
		ОповеститьОВыборе(СтруктураВозврата);
	Иначе
		ОповеститьОВыборе(Элементы.Список.ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти
