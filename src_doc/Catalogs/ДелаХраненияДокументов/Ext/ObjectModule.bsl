#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(НомерТома) Или ЗначениеЗаполнено(НомерЧасти) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Дела.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ДелаХраненияДокументов КАК Дела
			|ГДЕ
			|	Дела.НоменклатураДел = &НоменклатураДел
			|	И Дела.НомерТома = &НомерТома
			|	И Дела.НомерЧасти = &НомерЧасти
			|	И Дела.Ссылка <> &Ссылка";
		
		Запрос.УстановитьПараметр("НомерТома", НомерТома);
		Запрос.УстановитьПараметр("НомерЧасти", НомерЧасти);
		Запрос.УстановитьПараметр("НоменклатураДел", НоменклатураДел);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			ТекстТомаЧасти = НСтр("ru = 'тома'");
			Если ЗначениеЗаполнено(НомерЧасти) Тогда
				ТекстТомаЧасти = ?(ЗначениеЗаполнено(НомерТома),
					НСтр("ru = 'тома и части'"), НСтр("ru = 'части'"));
			КонецЕсли;
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(НСтр("ru = 'Дело с указанным номером %1 уже существует!'"), ТекстТомаЧасти), 
				ЭтотОбъект, ?(ЗначениеЗаполнено(НомерТома), "НомерТома", "НомерЧасти"),, Отказ);
			Возврат;	
		КонецЕсли;	
	КонецЕсли;
	
	Если ДелоЗакрыто Тогда 
		ПроверяемыеРеквизиты.Добавить("ДатаНачала");
		ПроверяемыеРеквизиты.Добавить("ДатаОкончания");
	КонецЕсли;	
	
	Если ПолучитьФункциональнуюОпцию("ОбязательныйУчетПоМестамХранения")
		И ФормаДокументов <> Перечисления.ВариантыФормДокументов.Электронная Тогда 
		ПроверяемыеРеквизиты.Добавить("МестоХраненияДел");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаОкончания) 
		И ДатаНачала > ДатаОкончания Тогда 
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Дата окончания дела меньше, чем дата начала.'"),
			ЭтотОбъект,
			"ДатаОкончания",, 
			Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	ТомЧастьСтрокой = ДелопроизводствоКлиентСервер.НомерТомаЧастиДляНаименования(НомерТома, НомерЧасти);
	
	РеквизитыНоменклатурыДел = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НоменклатураДел,
		"ПолноеНаименование, Организация, Раздел.Подразделение, ФормаДокументов");
	
	Если ДополнительныеСвойства.Свойство("ЗаголовокНоменклатурыСтарый") Тогда
		АвтоНаименованиеСтарое = ДелопроизводствоКлиентСервер.АвтоЗаголовокДела(
			ДополнительныеСвойства.ЗаголовокНоменклатурыСтарый, ТомЧастьСтрокой);
		Если ПолноеНаименование = АвтоНаименованиеСтарое Тогда
			ПолноеНаименование = ДелопроизводствоКлиентСервер.АвтоЗаголовокДела(
				РеквизитыНоменклатурыДел.ПолноеНаименование, ТомЧастьСтрокой);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПолноеНаименование) Тогда
		ПолноеНаименование = ДелопроизводствоКлиентСервер.АвтоЗаголовокДела(
			РеквизитыНоменклатурыДел.ПолноеНаименование, ТомЧастьСтрокой);
	КонецЕсли;
	
	Наименование = Делопроизводство.НаименованиеДела(ЭтотОбъект, ТомЧастьСтрокой);
	Организация = РеквизитыНоменклатурыДел.Организация;
	Подразделение = РеквизитыНоменклатурыДел.РазделПодразделение;
	Если РеквизитыНоменклатурыДел.ФормаДокументов <> Перечисления.ВариантыФормДокументов.БумажнаяИлиЭлектронная Тогда
		ФормаДокументов = РеквизитыНоменклатурыДел.ФормаДокументов;
	КонецЕсли;
	
	ОтноситсяКНоменклатуреДел.Очистить();
	НоваяСтрока = ОтноситсяКНоменклатуреДел.Добавить();
	НоваяСтрока.НоменклатураДел = НоменклатураДел;
	
	МассивНоменклатурПоПериоду = Делопроизводство.ПолучитьМассивНоменклатурЗаПериод(
		НоменклатураДел, ДатаНачала, ДатаОкончания);
		
	Для Каждого Номенклатура Из МассивНоменклатурПоПериоду Цикл 
		НоваяСтрока = ОтноситсяКНоменклатуреДел.Добавить();
		НоваяСтрока.НоменклатураДел = Номенклатура;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДелоЗакрыто = Ложь;
	КоличествоЛистов = 0;
	ДатаОкончания = '00010101';
	
	ОтноситсяКНоменклатуреДел.Очистить();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда 
		Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	ФормаДокументовПоНоменклатуре = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		НоменклатураДел, "ФормаДокументов");
	Если Не ЗначениеЗаполнено(ФормаДокументов) И ФормаДокументовПоНоменклатуре 
		<> Перечисления.ВариантыФормДокументов.БумажнаяИлиЭлектронная Тогда
		ФормаДокументов = ФормаДокументовПоНоменклатуре;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли