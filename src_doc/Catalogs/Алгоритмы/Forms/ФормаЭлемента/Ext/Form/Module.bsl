#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТолькоПросмотр = Объект.Предопределенный;
	Элементы.ПояснениеПредопределенного.Видимость = Объект.Предопределенный;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Элементы.КлючДляВызова.ОтображениеПредупрежденияПриРедактировании =
			ОтображениеПредупрежденияПриРедактировании.НеОтображать;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

	// Протоколирование работы пользователей
	ПротоколированиеРаботыСотрудников.ЗаписатьОткрытие(Объект.Ссылка);
	
	РедакторВстроенногоЯзыка.ПриСозданииНаСервере(ЭтотОбъект, "Редактор");
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбщегоНазначенияДокументооборот.ПоказатьНадписьПометкиУдаления(
		ЭтотОбъект, ТекущийОбъект.ПометкаУдаления);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	МенятьКлючАвтоматически = Не ЗначениеЗаполнено(Объект.Ссылка);
	
	РедакторВстроенногоЯзыкаКлиент.ПриОткрытии(
		Элементы.РедакторВстроенногоЯзыка,
		Элементы.Выражение,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Объект.Выражение = РедакторВстроенногоЯзыкаКлиент.ПолучитьТекст(
		Элементы.РедакторВстроенногоЯзыка, Объект.Выражение);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// Протоколирование работы пользователей
	ПротоколированиеРаботыСотрудников.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыСотрудников.ЗаписатьИзменение(Объект.Ссылка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если МенятьКлючАвтоматически Тогда
		Объект.КлючДляВызова = АвтоКлючПоНаименованию(Объект.Наименование);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КлючДляВызоваПриИзменении(Элемент)
	
	МенятьКлючАвтоматически = Объект.КлючДляВызова = АвтоКлючПоНаименованию(Объект.Наименование);
	
КонецПроцедуры

&НаКлиенте
Процедура РедакторВстроенногоЯзыкаДокументСформирован(Элемент)
	
	Если РедакторВстроенногоЯзыкаКлиент.ДокументСформирован(
			Элемент, ЭтотОбъект, Объект.Выражение, Объект.Предопределенный) Тогда
		ПодключитьОбработчикОжидания("ПослеЗагрузкиРедактора", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедакторВстроенногоЯзыкаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	НажатаКнопкаEsc = Ложь;
	РедакторВстроенногоЯзыкаКлиент.ПриНажатии(
		Элемент,
		ДанныеСобытия,
		СтандартнаяОбработка,
		ЭтотОбъект,
		НажатаКнопкаEsc);
	Если НажатаКнопкаEsc Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция АвтоКлючПоНаименованию(Наименование)
	
	АвтоКлюч = "";
	
	Подстроки = СтрРазделить(Наименование, " ");
	Для Каждого Подстрока Из Подстроки Цикл
		АвтоКлюч = АвтоКлюч + ВРег(Лев(Подстрока, 1)) + Сред(Подстрока, 2);
	КонецЦикла;
	
	Возврат АвтоКлюч;
	
КонецФункции

&НаКлиенте
Процедура ПослеЗагрузкиРедактора()
	
	РедакторВстроенногоЯзыкаКлиент.ПослеЗагрузкиРедактора(Элементы.РедакторВстроенногоЯзыка, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти