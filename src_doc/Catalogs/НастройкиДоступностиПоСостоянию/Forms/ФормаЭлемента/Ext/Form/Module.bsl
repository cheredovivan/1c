#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Протоколирование работы пользователей
	ПротоколированиеРаботыСотрудников.ЗаписатьОткрытие(Объект.Ссылка);
	
	ДляВсехВидовДокументов = Перечисления.ВариантыНастройкиДоступностиДляВидовДокументов.ДляВсехВидовДокументов;
	
	Поставляемый = Ложь;
	Если Не Объект.Ссылка.Пустая() Тогда
		ОписаниеНастроекДоступности = Делопроизводство.ОписанияНачальногоЗаполненияНастроекДоступностиПоСостояниям();
		Для Каждого ОписаниеНастройки Из ОписаниеНастроекДоступности Цикл
			Если ОписаниеНастройки.Идентификатор = Строка(Объект.Ссылка.УникальныйИдентификатор()) Тогда
				Поставляемый = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Элементы.УстановитьСтандартныеНастройки.Видимость = Поставляемый;
	
	Если Объект.Ссылка.Пустая() Тогда 
		Если Не ЗначениеЗаполнено(Объект.ТипДокумента) Тогда 
			Объект.ТипДокумента = Перечисления.ТипыОбъектов.ДокументыПредприятия;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Объект.ВариантНастройкиДляВидовДокументов) Тогда
			Объект.ВариантНастройкиДляВидовДокументов = ДляВсехВидовДокументов;
		КонецЕсли;
		Если Объект.ИспользоватьДля.Количество() = 0 Тогда
			НоваяСтрока = Объект.ИспользоватьДля.Добавить();
			НоваяСтрока.Участник = Справочники.РабочиеГруппы.ВсеПользователи;
		КонецЕсли;
	КонецЕсли;
	НачальныйТипДокумента = Объект.ТипДокумента;
	
	Если Объект.ТипДокумента = Перечисления.ТипыОбъектов.ДокументыПредприятия Тогда
		Элементы.НастройкиДоступностиВидДействияСостояниеПредставление.ФиксацияВТаблице = ФиксацияВТаблице.Лево;
	КонецЕсли;

	ЗаполнитьВидыДокументов(Объект.Ссылка);
	ДанныеПолейИКоманд = Делопроизводство.ПолучитьИменаПолейИКомандДляНастройкиДоступности();
	ИменаПолейИКоманд.Загрузить(ДанныеПолейИКоманд);
	
	ЗаполнитьКолонкиПолейИКоманд();
	ЗаполнитьНастройкиДоступности();
	
	КоличествоВидов = ВидыДокументов.Количество();
	
	Если Объект.ВариантНастройкиДляВидовДокументов = ДляВсехВидовДокументов Тогда 
		Элементы.ВидыДокументов.Доступность = Ложь;
	КонецЕсли;
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПередЗакрытием(
		Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Модифицированность) Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	КлючеваяОперация = ДелопроизводствоКлиентСервер.КлючеваяОперацияЗаписьНДПС();
	УИДЗамера = ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация,, Ложь);
	ПараметрыЗаписи.Вставить("УИДЗамера", УИДЗамера);
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		
		Если НачальныйТипДокумента <> Объект.ТипДокумента И Объект.Предопределенный Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю(	
				НСтр("ru = 'Нельзя изменить тип документа для предопределенной настройки доступности по состоянию.'"),,
				"Объект.ТипДокумента",,
				Отказ);
			Возврат;
		КонецЕсли;
		
		Если (НачальныйТипДокумента <> Объект.ТипДокумента) 
			И ВидыДокументов.Количество() > 0 
			И Не ПараметрыЗаписи.Свойство("ПоказанВопросОбИзмененияТипаДокумента") Тогда 
			
			ТекстВопроса = НСтр("ru = 'При изменении типа документа будут очищены назначенные виды документов.
				|Продолжить?'");
			ОписаниеОповещения = Новый ОписаниеОповещения(
					"ПередЗаписьюПродолжениеПослеПоказаВопросаОбИзмененияТипаДокумента",
					ЭтотОбъект,
					ПараметрыЗаписи);
	
			ПоказатьВопрос(ОписаниеОповещения,ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
	КонецЕсли;	
	
	КоличествоСтрок = Объект.ИспользоватьДля.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		Строка = Объект.ИспользоватьДля[КоличествоСтрок - Инд];
		Если Не ЗначениеЗаполнено(Строка.Участник) Тогда 
			Объект.ИспользоватьДля.Удалить(Строка);
		КонецЕсли;	
	КонецЦикла;	
	
	КоличествоСтрок = ВидыДокументов.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		Строка = ВидыДокументов[КоличествоСтрок - Инд];
		Если Не ЗначениеЗаполнено(Строка.ВидДокумента) Тогда 
			ВидыДокументов.Удалить(Строка);
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюПродолжениеПослеПоказаВопросаОбИзмененияТипаДокумента(Результат, ПараметрыЗаписи) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда 
		Возврат;
	КонецЕсли;	
	
	ВидыДокументов.Очистить();

	ПараметрыЗаписи.Вставить("ПоказанВопросОбИзмененияТипаДокумента", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
	СохранитьНастройкиДоступности(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьВидыДокументов(ТекущийОбъект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Протоколирование работы пользователей
	ПротоколированиеРаботыСотрудников.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыСотрудников.ЗаписатьИзменение(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(ПараметрыЗаписи.УИДЗамера);
	
	НачальныйТипДокумента = Объект.ТипДокумента;
	
	Если ПараметрыЗаписи.Свойство("Закрыть") Тогда
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Изменение:'"),
			ПолучитьНавигационнуюСсылку(Объект.Ссылка),
			Строка(Объект.Ссылка),
			БиблиотекаКартинок.Информация32);
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Объект.ВариантНастройкиДляВидовДокументов = Перечисления.ВариантыНастройкиДоступностиДляВидовДокументов.ДляВыбранныхВидовДокументов Тогда 
		ПроверяемыеРеквизиты.Добавить("ВидыДокументов");
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантыНастройкиДоступностиДляВидовДокументовПриИзменении(Элемент)
	
	Если Объект.ВариантНастройкиДляВидовДокументов = ДляВсехВидовДокументов Тогда 
		
		Элементы.ВидыДокументов.Доступность = Ложь;
		ВидыДокументов.Очистить();
		КоличествоВидов = 0;
		
	Иначе
		Элементы.ВидыДокументов.Доступность = Истина;
	КонецЕсли;
	
	ОбновитьВидыДействий = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидДокументаПриИзменении(Элемент)
	
	ЗаполнитьНастройкиДоступности(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидДействияПриИзменении(Элемент)
	
	ЗаполнитьНастройкиДоступности();
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если ТекущаяСтраница = Элементы.ГруппаДоступностьПолей И ОбновитьВидыДействий Тогда
		ЗаполнитьНастройкиДоступности();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИспользоватьДля

&НаКлиенте
Процедура ИспользоватьДляОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		Объект.ИспользоватьДля.Очистить();
		Для Каждого ВыбранныйАдресат Из ВыбранноеЗначение Цикл
			ИспользоватьДля = Объект.ИспользоватьДля.Добавить();
			ИспользоватьДля.Участник = ВыбранныйАдресат.Контакт;
		КонецЦикла;
	Иначе
		Элементы.ИспользоватьДля.ТекущиеДанные.Участник = ВыбранноеЗначение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьДляУчастникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимРаботыФормы", 1); // выбор
	ПараметрыФормы.Вставить("УпрощенныйИнтерфейс", Истина);
	ПараметрыФормы.Вставить("ОтображатьСотрудников", Истина);
	ПараметрыФормы.Вставить("ОтображатьРоли", Истина);
	ПараметрыФормы.Вставить("ОтображатьАвтоподстановкиПоДокументам", Истина);
	ПараметрыФормы.Вставить("ВыбиратьКонтейнерыПользователей", Истина);
	ПараметрыФормы.Вставить("ЗаголовокФормы",
		НСтр("ru = 'Выбор сотрудников для настроек доступности'"));
	
	ПараметрыФормы.Вставить("ВыбранныеАдресаты", Элементы.ИспользоватьДля.ТекущиеДанные.Участник);
	
	РаботаСАдреснойКнигойКлиент.ОткрытьАдреснуюКнигу(ПараметрыФормы, Элементы.ИспользоватьДля);		
		
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьДляУчастникАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = СформироватьДанныеВыбораСервер(Текст);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьДляУчастникОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СотрудникиКлиент.ОбработкаВыбораКонтейнера(
		Элемент, ВыбранноеЗначение, СтандартнаяОбработка); 
		
	СотрудникиКлиент.СотрудникОбработкаВыбора(
		Элементы.ИспользоватьДля.ТекущиеДанные, "Участник", ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВидыДокументов

&НаКлиенте
Процедура ВидыДокументовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	КоличествоВидов = ВидыДокументов.Количество();
	ОбновитьВидыДействий = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыДокументовПослеУдаления(Элемент)
	
	КоличествоВидов = ВидыДокументов.Количество();
	ОбновитьВидыДействий = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыДокументовВидДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяФормыВыбора = "Справочник.ВидыДокументов.ФормаВыбора";
	ПараметрыФормы = Новый Структура;
	ТекущиеДанные = Элементы.ВидыДокументов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда 
		ПараметрыФормы.Вставить("ТекущаяСтрока", ТекущиеДанные.ВидДокумента);
	КонецЕсли;
	ПараметрыФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.ГруппыИЭлементы);
	
	Если ЗначениеЗаполнено(ИмяФормыВыбора) Тогда 
		ОткрытьФорму(ИмяФормыВыбора, ПараметрыФормы, Элемент);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкиДоступности

&НаКлиенте
Процедура НастройкиДоступностиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = НастройкиДоступности.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле = Элементы.НастройкиДоступностиВидДействияСостояниеПредставление Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКолонкиПредставлениеДоступности = СтрЗаменить(Поле.Имя, "НастройкиДоступности", "");
	ИмяКолонкиЗначениеДоступности = СтрЗаменить(ИмяКолонкиПредставлениеДоступности, "Строка", "");
	
	Если ИмяКолонкиЗначениеДоступности = "ПустаяКолонка" Тогда
		Возврат;
	КонецЕсли;
	
	ПодчиненныеСтроки = ТекущиеДанные.ПолучитьЭлементы();
	КоличествоСостояний = 3;
	
	Если ПодчиненныеСтроки.Количество() Тогда
		ДоступностьПоля = (ПодчиненныеСтроки[0][ИмяКолонкиЗначениеДоступности] + 1) % КоличествоСостояний;
		Для Каждого Строка Из ПодчиненныеСтроки Цикл
			Строка[ИмяКолонкиЗначениеДоступности] = ДоступностьПоля;
			Строка[ИмяКолонкиПредставлениеДоступности] = ДоступностьСтрокой(ДоступностьПоля);
		КонецЦикла;
	Иначе
		ДоступностьПоля = (ТекущиеДанные[ИмяКолонкиЗначениеДоступности] + 1) % КоличествоСостояний;
		ТекущиеДанные[ИмяКолонкиЗначениеДоступности] = ДоступностьПоля;
		ТекущиеДанные[ИмяКолонкиПредставлениеДоступности] = ДоступностьСтрокой(ДоступностьПоля);
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиДоступностиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиДоступностиПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьСтандартныеНастройки(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"УстановитьСтандартныеНастройкиПродолжение",
		ЭтотОбъект);
	
	Если Объект.НастройкиДоступности.Количество() > 0 Тогда 
		
		ТекстВопроса = НСтр("ru = 'Настройки доступности будут заполнены стандартными значениями.
			|Продолжить?'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)
	
	ЗаписатьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьДляПодобрать(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимРаботыФормы", 2); // подбор
	ПараметрыФормы.Вставить("ОтображатьСотрудников", Истина);
	ПараметрыФормы.Вставить("ОтображатьРоли", Истина);
	ПараметрыФормы.Вставить("ОтображатьАвтоподстановкиПоДокументам", Истина);
	ПараметрыФормы.Вставить("ВыбиратьКонтейнерыПользователей", Истина);
	ПараметрыФормы.Вставить("ЗаголовокФормы",
		НСтр("ru = 'Выбор сотрудников для настроек доступности'"));
	
	ВыбранныеАдресаты = Новый Массив;
	
	Для Каждого ИспользоватьДля Из Объект.ИспользоватьДля Цикл
		ВыбранныйАдресат = Новый Структура;
		ВыбранныйАдресат.Вставить("Контакт",
			ИспользоватьДля.Участник);
		ВыбранныеАдресаты.Добавить(ВыбранныйАдресат);
	КонецЦикла;
	
	ПараметрыФормы.Вставить("ВыбранныеАдресаты", ВыбранныеАдресаты);
	
	РаботаСАдреснойКнигойКлиент.ОткрытьАдреснуюКнигу(ПараметрыФормы, Элементы.ИспользоватьДля);		
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВСтроке0(Команда)
	
	УстановитьВСтрокеНастройки(0);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВСтроке1(Команда)
	
	УстановитьВСтрокеНастройки(1);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВСтроке2(Команда)
	
	УстановитьВСтрокеНастройки(2);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВСтолбце0(Команда)
	
	УстановитьВСтолбцеНастройки(0);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВСтолбце1(Команда)
	
	УстановитьВСтолбцеНастройки(1);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВСтолбце2(Команда)
	
	УстановитьВСтолбцеНастройки(2);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьКолонкиПолейИКоманд()
	
	ИмяРеквизитаТаблицыНастройкиДоступности = "НастройкиДоступности";
	ИмяРеквизитаТаблицыНастройкиДоступностиКэш = "НастройкиДоступностиКэш";
	ПостфиксПредставленияДоступности = "Строка";
	
	ТипЧисло = Тип("Число");
	ТипСтрока = Тип("Строка");
	
	// Создание реквизитов формы для заполнения таблицы настроек
	ДобавляемыеРеквизиты = Новый Массив;
	Для Каждого Строка Из ИменаПолейИКоманд Цикл
		
		Если Строка.ИмяПоляКоманды = "ДопРеквизиты" Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяРеквизитаДоступность = Строка.ИмяПоляКоманды;
		РаботаСФормами.ДобавляемыеРеквизитыДобавить(
			ДобавляемыеРеквизиты,
			ИмяРеквизитаДоступность,
			ТипЧисло,
			ИмяРеквизитаТаблицыНастройкиДоступности);
		
		РаботаСФормами.ДобавляемыеРеквизитыДобавить(
			ДобавляемыеРеквизиты,
			ИмяРеквизитаДоступность,
			ТипЧисло,
			ИмяРеквизитаТаблицыНастройкиДоступностиКэш);
		
		ИмяРеквизитаДоступностьСтрокой = СтрШаблон("%1%2", Строка.ИмяПоляКоманды, ПостфиксПредставленияДоступности);
		РаботаСФормами.ДобавляемыеРеквизитыДобавить(
			ДобавляемыеРеквизиты,
			ИмяРеквизитаДоступностьСтрокой,
			ТипСтрока,
			ИмяРеквизитаТаблицыНастройкиДоступности);
		
		РаботаСФормами.ДобавляемыеРеквизитыДобавить(
			ДобавляемыеРеквизиты,
			ИмяРеквизитаДоступностьСтрокой,
			ТипСтрока,
			ИмяРеквизитаТаблицыНастройкиДоступностиКэш);
		
		КолонкиДереваНастроек.Добавить(ИмяРеквизитаДоступность);
		
	КонецЦикла;
	
	ДопРеквизитыИСведения = ДополнительныеРеквизитыИСведения();
	Для Каждого Элемент Из ДопРеквизитыИСведения Цикл
		
		ИмяРеквизитаДоступность = ИмяДопРеквизита(Элемент.Свойство);
		РаботаСФормами.ДобавляемыеРеквизитыДобавить(
			ДобавляемыеРеквизиты,
			ИмяРеквизитаДоступность,
			ТипЧисло,
			ИмяРеквизитаТаблицыНастройкиДоступности);
		
		РаботаСФормами.ДобавляемыеРеквизитыДобавить(
			ДобавляемыеРеквизиты,
			ИмяРеквизитаДоступность,
			ТипЧисло,
			ИмяРеквизитаТаблицыНастройкиДоступностиКэш);
		
		ИмяРеквизитаДоступностьСтрокой = ИмяДопРеквизита(Элемент.Свойство, ПостфиксПредставленияДоступности);
		РаботаСФормами.ДобавляемыеРеквизитыДобавить(
			ДобавляемыеРеквизиты,
			ИмяРеквизитаДоступностьСтрокой,
			ТипСтрока,
			ИмяРеквизитаТаблицыНастройкиДоступности);
		
		РаботаСФормами.ДобавляемыеРеквизитыДобавить(
			ДобавляемыеРеквизиты,
			ИмяРеквизитаДоступностьСтрокой,
			ТипСтрока,
			ИмяРеквизитаТаблицыНастройкиДоступностиКэш);
		
		КолонкиДереваНастроек.Добавить(ИмяРеквизитаДоступность);
		КолонкиДопРеквизитов.Добавить(ИмяРеквизитаДоступность);
		
	КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	РазмерШрифта = 12;
	
	// Формирование колонок таблицы настроек
	Для Каждого Строка Из ИменаПолейИКоманд Цикл
		
		// Группа колонок "Доп. реквизиты"
		Если Строка.ИмяПоляКоманды = "ДопРеквизиты" Тогда
			
			ИмяЭлементаГруппаДопРеквизиты = СтрШаблон("%1Группа%2", ИмяРеквизитаТаблицыНастройкиДоступности,
				Строка.ИмяПоляКоманды);
			ЭлементГруппаДопРеквизиты = СоздатьГруппуКолонок(
				Элементы,
				ИмяЭлементаГруппаДопРеквизиты,
				Элементы.НастройкиДоступности,
				Элементы.НастройкиДоступностиПустаяКолонка);
			ЭлементГруппаДопРеквизиты.Заголовок = Строка.ПредставлениеПоляКоманды;
			
			РодительЭлемента = ЭлементГруппаДопРеквизиты;
			ВставитьПередЭлементСостояние = Неопределено;
			
			Для Каждого Элемент Из ДопРеквизитыИСведения Цикл
				
				ИмяДопРеквизитаСтрокой = ИмяДопРеквизита(Элемент.Свойство, ПостфиксПредставленияДоступности);
				ИмяЭлементаДопРеквизит = СтрШаблон("%1%2", ИмяРеквизитаТаблицыНастройкиДоступности,
					ИмяДопРеквизитаСтрокой);
				ПутьКДаннымЭлементСостояние = СтрШаблон("%1.%2", ИмяРеквизитаТаблицыНастройкиДоступности,
					ИмяДопРеквизитаСтрокой);
				
				ЭлементСостояние = СоздатьПолеВвода(
					Элементы,
					ИмяЭлементаДопРеквизит,
					РодительЭлемента,
					ПутьКДаннымЭлементСостояние,
					ВставитьПередЭлементСостояние);
				
				ЭлементСостояние.Заголовок = Элемент.Наименование;
				
			КонецЦикла;
			
		Иначе
			
			РодительЭлемента = Элементы.НастройкиДоступности;
			ВставитьПередЭлементСостояние = Элементы.НастройкиДоступностиПустаяКолонка;
			
			ИмяЭлементаСостояние = СтрШаблон("%1%2%3", ИмяРеквизитаТаблицыНастройкиДоступности,
				Строка.ИмяПоляКоманды, ПостфиксПредставленияДоступности);
			ПутьКДаннымЭлементСостояние = СтрШаблон("%1.%2%3", ИмяРеквизитаТаблицыНастройкиДоступности,
				Строка.ИмяПоляКоманды, ПостфиксПредставленияДоступности);
			ЭлементСостояние = СоздатьПолеВвода(
				Элементы,
				ИмяЭлементаСостояние,
				РодительЭлемента,
				ПутьКДаннымЭлементСостояние,
				ВставитьПередЭлементСостояние);
			ЭлементСостояние.Заголовок = Строка.ПредставлениеПоляКоманды;
			ЭлементСостояние.Ширина = СтрДлина(ЭлементСостояние.Заголовок) + 1;
			ЭлементСостояние.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
			ЭлементСостояние.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
			ЭлементСостояние.ГоризонтальноеПоложениеВШапке = ГоризонтальноеПоложениеЭлемента.Центр;
			ЭлементСостояние.РастягиватьПоГоризонтали = Ложь;
			ЭлементСостояние.РежимРедактирования = РежимРедактированияКолонки.ВходПриВводе;
			ЭлементСостояние.Шрифт = Новый Шрифт(, РазмерШрифта, Истина);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтандартныеНастройкиПродолжение(Результат, Параметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		ЗаполнитьНастройкиПоУмолчанию();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиДоступности(Знач ОбновитьВидимостьДопРеквизитов = Ложь, Знач ЗаполнитьПоУмолчанию = Ложь)
	
	ЗаполнитьВидыДействийИСостояния();
	
	ЗаполнитьДеревоНастроек(ЗаполнитьПоУмолчанию);
	
	// Формирование условного оформления.
	УстановитьУсловноеОформление();
	
	// Настройка отбора "Вид документа".
	НастроитьОтборВидДокумента(ЗаполнитьПоУмолчанию);
	
	// Настройка отбора "Вид действия".
	НастроитьОтборВидДействия(ЗаполнитьПоУмолчанию);
	
	// Установка отбора "Виды действий".
	УстановитьОтборВидыДействий(ОбновитьВидимостьДопРеквизитов);
	
	ОбновитьВидыДействий = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВидыДействийИСостояния()
	
	ВидыДействий.Очистить();
	
	Если Объект.ВариантНастройкиДляВидовДокументов = ДляВсехВидовДокументов Тогда
		НовыеВидыДействий = ДействияСервер.ВсеВидыДействий();
	Иначе
		ВыбранныеВидыДокументов = ВидыДокументов.Выгрузить().ВыгрузитьКолонку("ВидДокумента");
		НовыеВидыДействий = ДействияСервер.ДействующиеВидыДействийПоВидамОбъектов(ВыбранныеВидыДокументов);
	КонецЕсли;
	
	СостоянияДокументовВидовДействий = ДействияСервер.СостоянияДокументовВидовДействий(НовыеВидыДействий);
	
	Для Каждого ВидДействия Из НовыеВидыДействий Цикл
		
		СтрокаВидДействия = ВидыДействий.Добавить();
		СтрокаВидДействия.ВидДействия = ВидДействия;
		ИдентификаторВидаДействия = ВидДействия.УникальныйИдентификатор();
		СтрокаВидДействия.ПутьВидаДействия = СтрШаблон("ВидДействия%1", ИдентификаторВидаДействия);
		
		// Заполнение состояний видов действий
		СостоянияДокументов = СостоянияДокументовВидовДействий[СтрокаВидДействия.ВидДействия];
		Для Каждого СостояниеДокумента Из СостоянияДокументов Цикл
			СтрокаСостояниеДокументов = СтрокаВидДействия.СостоянияДокументов.Добавить();
			СтрокаСостояниеДокументов.СостояниеДокумента = СостояниеДокумента;
			ИмяЗначенияПеречисления = ОбщегоНазначения.ИмяЗначенияПеречисления(СостояниеДокумента);
			СтрокаСостояниеДокументов.ПутьСостоянияДокумента = СтрШаблон("%1СостояниеДокумента%2",
				СтрокаВидДействия.ПутьВидаДействия, ИмяЗначенияПеречисления);
		КонецЦикла;
		
	КонецЦикла;
	
	// Без вида действия - "Проект"
	Если ВидДействияИСостояниеДокументов("БезВидаДействияСостояниеДокументовПроект") = Неопределено Тогда
		
		НовыеВидыДействий.Вставить(0, Справочники.ВидыДействий.ПустаяСсылка());
		
		СтрокаВидДействия = ВидыДействий.Вставить(0);
		СтрокаВидДействия.ВидДействия = Справочники.ВидыДействий.ПустаяСсылка();
		СтрокаВидДействия.ПутьВидаДействия = "БезВидаДействия";
		
		СтрокаСостояниеДокументов = СтрокаВидДействия.СостоянияДокументов.Добавить();
		СтрокаСостояниеДокументов.СостояниеДокумента = Перечисления.СостоянияДокументов.Проект;
		СтрокаСостояниеДокументов.ПутьСостоянияДокумента = СтрШаблон("%1СостояниеДокументовПроект",
			СтрокаВидДействия.ПутьВидаДействия);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоНастроек(ЗаполнитьПоУмолчанию = Ложь)
	
	ИмяРеквизитаТаблицыНастройкиДоступности = "НастройкиДоступности";
	ИмяРеквизитаТаблицыНастройкиДоступностиКэш = "НастройкиДоступностиКэш";
	
	НастройкиДоступностиДерево = РеквизитФормыВЗначение(ИмяРеквизитаТаблицыНастройкиДоступности);
	НастройкиДоступностиКэшДерево = РеквизитФормыВЗначение(ИмяРеквизитаТаблицыНастройкиДоступностиКэш);
	ЗаполнитьДеревоПоИсточнику(НастройкиДоступностиКэшДерево, НастройкиДоступностиДерево);
	
	НастройкиДоступностиДерево.Строки.Очистить();
	
	ЕстьКэш = НастройкиДоступностиКэшДерево.Строки.Количество() И ЗаполнитьПоУмолчанию = Ложь;
	
	Если Не ЕстьКэш Или ОбновитьВидыДействий Тогда
		
		СостоянияПоУмолчанию = Новый Структура;
		Для Каждого Колонка Из КолонкиДереваНастроек Цикл
			РеквизитДоступности = Колонка.Значение + "Строка";
			СостоянияПоУмолчанию.Вставить(РеквизитДоступности, ДоступностьСтрокой(0));
		КонецЦикла;
		
		Отбор = Новый Структура("ВидДействия", Справочники.ВидыДействий.ПустаяСсылка());
		Отбор.Вставить("Состояние", Перечисления.СостоянияДокументов.ПустаяСсылка());
		
		// Заполнение строк таблицы настроек
		Для Каждого СтрокаВидДействия Из ВидыДействий Цикл
			
			Если ЗначениеЗаполнено(СтрокаВидДействия.ВидДействия) Тогда
				НоваяСтрока = НастройкиДоступностиДерево.Строки.Добавить();
				НоваяСтрока.ВидДействияСостояние = СтрокаВидДействия.ПутьВидаДействия;
				НоваяСтрока.ВидДействияСостояниеПредставление = СтрокаВидДействия.ВидДействия;
				НоваяСтрока.ВидДействия = СтрокаВидДействия.ВидДействия;
			Иначе
				НоваяСтрока = НастройкиДоступностиДерево;
			КонецЕсли;
			
			Для Каждого СтрокаСостояниеДокументов Из СтрокаВидДействия.СостоянияДокументов Цикл
				
				Подстрока = НоваяСтрока.Строки.Добавить();
				Подстрока.ВидДействияСостояние = СтрокаСостояниеДокументов.ПутьСостоянияДокумента;
				Подстрока.ВидДействияСостояниеПредставление = СтрокаСостояниеДокументов.СостояниеДокумента;
				Подстрока.ВидДействия = СтрокаВидДействия.ВидДействия;
				Подстрока.СостояниеДокумента = СтрокаСостояниеДокументов.СостояниеДокумента;
				ЗаполнитьЗначенияСвойств(Подстрока, СостоянияПоУмолчанию);
				
				Если Не ЗаполнитьПоУмолчанию Тогда
					Отбор.ВидДействия = СтрокаВидДействия.ВидДействия;
				КонецЕсли;
				Отбор.Состояние = СтрокаСостояниеДокументов.СостояниеДокумента;
				ДоступностьПолей = Объект.НастройкиДоступности.НайтиСтроки(Отбор);
				
				Для Каждого НайденнаяНастройка Из ДоступностьПолей Цикл
					
					Если СтрНачинаетсяС(НайденнаяНастройка.ИмяПоляКоманды, "Ad.") Тогда
						ИмяКолонки = СтрЗаменить(НайденнаяНастройка.ИмяПоляКоманды, "Ad.", "Ad_");
						ИмяКолонки = СтрЗаменить(ИмяКолонки, "-", "_");
					ИначеЕсли НайденнаяНастройка.ИмяПоляКоманды = "ДопРеквизиты" Тогда
						ЗаполнитьДоступностьДопРеквизитов(Подстрока, НайденнаяНастройка.Доступность);
					Иначе
						ИмяКолонки = НайденнаяНастройка.ИмяПоляКоманды;
					КонецЕсли;
					
					Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Подстрока, ИмяКолонки) Тогда
						ИмяКолонкиПредставлениеДоступности = СтрШаблон("%1Строка", ИмяКолонки);
						Подстрока[ИмяКолонки] = НайденнаяНастройка.Доступность;
						Подстрока[ИмяКолонкиПредставлениеДоступности] = ДоступностьСтрокой(
							НайденнаяНастройка.Доступность);
					КонецЕсли;
				
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
		Если ОбновитьВидыДействий Тогда
			ЗаполнитьДеревоПоИсточнику(НастройкиДоступностиДерево, НастройкиДоступностиКэшДерево, Ложь);
			ЗначениеВРеквизитФормы(НастройкиДоступностиКэшДерево, ИмяРеквизитаТаблицыНастройкиДоступностиКэш);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЕстьКэш И Не ОбновитьВидыДействий Тогда
		ЗначениеВРеквизитФормы(НастройкиДоступностиКэшДерево, ИмяРеквизитаТаблицыНастройкиДоступности);
		ЗначениеВРеквизитФормы(НастройкиДоступностиКэшДерево, ИмяРеквизитаТаблицыНастройкиДоступностиКэш);
	Иначе
		ЗначениеВРеквизитФормы(НастройкиДоступностиДерево, ИмяРеквизитаТаблицыНастройкиДоступности);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиДоступности(ТекущийОбъект)
	
	ТекущийОбъект.НастройкиДоступности.Очистить();
	
	НастройкиДоступностиДерево = РеквизитФормыВЗначение("НастройкиДоступности");
	НастройкиДоступностиКэшДерево = РеквизитФормыВЗначение("НастройкиДоступностиКэш");
	
	Если НастройкиДоступностиКэшДерево.Строки.Количество() Тогда
		ЗаполнитьДеревоПоИсточнику(НастройкиДоступностиКэшДерево, НастройкиДоступностиДерево);
		ЗаписываемыеЭлементы = НастройкиДоступностиКэшДерево;
	Иначе
		ЗаписываемыеЭлементы = НастройкиДоступностиДерево;
	КонецЕсли;
	
	Для Каждого Колонка Из КолонкиДереваНастроек Цикл
		
		Для Каждого Строка Из ЗаписываемыеЭлементы.Строки Цикл
			
			Если Строка["СостояниеДокумента"] = Перечисления.СостоянияДокументов.Проект
				И Строка[Колонка.Значение] <> 0 Тогда
				
				НоваяСтрока = ТекущийОбъект.НастройкиДоступности.Добавить();
				НоваяСтрока.ИмяПоляКоманды = Колонка.Значение;
				НоваяСтрока.Доступность = Строка[Колонка.Значение];
				НоваяСтрока.ВидДействия = Строка.ВидДействия;
				НоваяСтрока.Состояние = Строка.СостояниеДокумента;
				
				Продолжить;
				
			КонецЕсли;
			
			ЭлементыПодстрок = Строка.Строки;
			
			Для Каждого Подстрока Из ЭлементыПодстрок Цикл
				
				Если Подстрока[Колонка.Значение] = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = ТекущийОбъект.НастройкиДоступности.Добавить();
				НоваяСтрока.ИмяПоляКоманды = Колонка.Значение;
				НоваяСтрока.Доступность = Подстрока[Колонка.Значение];
				НоваяСтрока.ВидДействия = Подстрока.ВидДействия;
				НоваяСтрока.Состояние = Подстрока.СостояниеДокумента;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиПоУмолчанию()
	
	Объект.НастройкиДоступности.Очистить();
	
	ТаблНастройкиДоступности = Неопределено;
	
	ОписаниеНастроекДоступности = Делопроизводство.ОписанияНачальногоЗаполненияНастроекДоступностиПоСостояниям();
	Для Каждого ОписаниеНастройки Из ОписаниеНастроекДоступности Цикл
		Если ОписаниеНастройки.Идентификатор = Строка(Объект.Ссылка.УникальныйИдентификатор()) Тогда
			
			ТаблНастройкиДоступности = Делопроизводство.ПолучитьНастройкиДоступностиПоУмолчанию(
				Объект.ТипДокумента, ОписаниеНастройки.Роль);
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблНастройкиДоступности = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.НастройкиДоступности.Загрузить(ТаблНастройкиДоступности);
	
	ЗаполнитьНастройкиДоступности(Истина, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВидыДокументов(Ссылка)
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда 
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.НастройкиДоступностиДляВидовДокументов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.НастройкаДоступностиПоСостоянию.Установить(Ссылка);
	НаборЗаписей.Прочитать();
	
	ВидыДокументов.Очистить();
	Для Каждого Строка Из НаборЗаписей Цикл
		НоваяСтрока = ВидыДокументов.Добавить();
		НоваяСтрока.ВидДокумента = Строка.ВидДокумента;
	КонецЦикла;
	
КонецПроцедуры
	
&НаСервере
Процедура ЗаписатьВидыДокументов(Ссылка)
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда 
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.НастройкиДоступностиДляВидовДокументов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.НастройкаДоступностиПоСостоянию.Установить(Ссылка);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	ТаблВидыДокументов = РеквизитФормыВЗначение("ВидыДокументов");
	Для Каждого Строка Из ТаблВидыДокументов Цикл
		НоваяСтрока = НаборЗаписей.Добавить();
		НоваяСтрока.ВидДокумента = Строка.ВидДокумента;
		НоваяСтрока.НастройкаДоступностиПоСостоянию = Ссылка;
	КонецЦикла;	
	
	НаборЗаписей.Записать();
	
КонецПроцедуры		

&НаСервере
Функция СформироватьДанныеВыбораСервер(Текст)
	
	ДополнениеТипа = Новый ОписаниеТипов(
		"СправочникСсылка.АвтоподстановкиДляОбъектов, СправочникСсылка.ПолныеРоли,
		|СправочникСсылка.СтруктураПредприятия, СправочникСсылка.РабочиеГруппы,
		|СправочникСсылка.Проекты");
	Возврат СотрудникиВызовСервера.СформироватьДанныеВыбора(
		Текст,
		ДополнениеТипа,
		ПредопределенноеЗначение("Перечисление.ТипыОбъектов.ДокументыПредприятия"));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДоступностьСтрокой(Доступность)
	
	Если Доступность = 0 Тогда
		Возврат НСтр("ru = '●'");
	ИначеЕсли Доступность = 1 Тогда
		Возврат НСтр("ru = '✔'");
	ИначеЕсли Доступность = 2 Тогда
		Возврат НСтр("ru = '✘'");
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформлениеФормы = УсловноеОформление;
	УсловноеОформлениеФормы.Элементы.Очистить();
	ЦветКрасный = Метаданные.ЭлементыСтиля.ЗапрещенноеПравоДоступа.Значение;
	ЦветЗеленый = Метаданные.ЭлементыСтиля.РазрешенноеПравоДоступаЦвет.Значение;
	ЦветСерый = Метаданные.ЭлементыСтиля.НеназначенноеПравоДоступаЦвет.Значение;
	ШрифтОбычный = ШрифтыСтиля.ОбычныйШрифтТекста;
	ИмяРеквизитаТаблицыНастройкиДоступности = "НастройкиДоступности";
	
	Для Каждого Реквизит Из КолонкиДереваНастроек Цикл
		
		ИмяПоляОтбора = СтрШаблон("%1.%2", ИмяРеквизитаТаблицыНастройкиДоступности, Реквизит.Значение);
		ИмяПоляУсловногоОформления = СтрШаблон("%1%2Строка", ИмяРеквизитаТаблицыНастройкиДоступности,
			Реквизит.Значение);
		
		ЭлементУсловногоОформления = УсловноеОформлениеФормы.Элементы.Добавить();
		ЭлементУсловногоОформления.Использование = Истина;
		
		ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораДанных.ПравоеЗначение = 1;
		ЭлементОтбораДанных.Использование = Истина;
		
		ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
		ЭлементЦветаОформления.Значение = ЦветЗеленый; 
		ЭлементЦветаОформления.Использование = Истина;
		
		Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		Поле.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляУсловногоОформления);
		
		ЭлементУсловногоОформления = УсловноеОформлениеФормы.Элементы.Добавить();
		ЭлементУсловногоОформления.Использование = Истина;
		
		ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораДанных.ПравоеЗначение = 2;
		ЭлементОтбораДанных.Использование = Истина;
		
		ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
		ЭлементЦветаОформления.Значение = ЦветКрасный; 
		ЭлементЦветаОформления.Использование = Истина;
		
		Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		Поле.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляУсловногоОформления);
		
		ЭлементУсловногоОформления = УсловноеОформлениеФормы.Элементы.Добавить();
		ЭлементУсловногоОформления.Использование = Истина;
		
		ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораДанных.ПравоеЗначение = 0;
		ЭлементОтбораДанных.Использование = Истина;
		
		ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
		ЭлементЦветаОформления.Значение = ЦветСерый; 
		ЭлементЦветаОформления.Использование = Истина;
		
		ЭлементШрифтОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("Font");
		ЭлементШрифтОформления.Значение = ШрифтОбычный; 
		ЭлементШрифтОформления.Использование = Истина;
		
		Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		Поле.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляУсловногоОформления);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть()
	
	ПараметрыЗаписи = Новый Структура();
	ПараметрыЗаписи.Вставить("Закрыть", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаписатьИЗакрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция ВидДействияИСостояниеДокументов(ПутьСостоянияДокумента)
	
	ВидДействияИСостояниеДокументов = Неопределено;
	Для Каждого СтрокаВидДействия Из ВидыДействий Цикл
		
		НайденныеСтроки = СтрокаВидДействия.СостоянияДокументов.НайтиСтроки(
			Новый Структура("ПутьСостоянияДокумента", ПутьСостоянияДокумента));
		
		Если НайденныеСтроки.Количество() > 0 Тогда
		
			ВидДействияИСостояниеДокументов = Новый Структура;
			ВидДействияИСостояниеДокументов.Вставить("ВидДействия", СтрокаВидДействия.ВидДействия);
			ВидДействияИСостояниеДокументов.Вставить("СостояниеДокумента", НайденныеСтроки[0].СостояниеДокумента);
			
			Прервать;
		
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВидДействияИСостояниеДокументов;
	
КонецФункции

&НаСервере
Функция СоздатьПолеВвода(Элементы, Имя, Родитель, ПутьКДанным, ВставитьПередЭлементом = Неопределено)
	
	СозданныйЭлемент = Элементы.Вставить(Имя, Тип("ПолеФормы"), Родитель, ВставитьПередЭлементом);
	СозданныйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	СозданныйЭлемент.ПутьКДанным = ПутьКДанным;
	СозданныйЭлемент.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
	СозданныйЭлемент.ГоризонтальноеПоложениеВШапке = ГоризонтальноеПоложениеЭлемента.Центр;
	СозданныйЭлемент.ТолькоПросмотр = Истина;
	
	Возврат СозданныйЭлемент;
	
КонецФункции

&НаСервере
Функция СоздатьГруппуКолонок(Элементы, Имя, Родитель, ВставитьПередЭлементом = Неопределено)
	
	СозданныйЭлемент = Элементы.Вставить(Имя, Тип("ГруппаФормы"), Родитель, ВставитьПередЭлементом);
	СозданныйЭлемент.Вид = ВидГруппыФормы.ГруппаКолонок;
	СозданныйЭлемент.Группировка = ГруппировкаКолонок.Горизонтальная;
	СозданныйЭлемент.ГоризонтальноеПоложениеВШапке = ГоризонтальноеПоложениеЭлемента.Центр;
	СозданныйЭлемент.ОтображатьВШапке = Истина;
	
	Возврат СозданныйЭлемент;
	
КонецФункции

&НаСервере
Процедура НастроитьОтборВидДокумента(ЗаполнитьПоУмолчанию = Ложь)
	
	Если Параметры.Свойство("ВидДокумента") И ЗначениеЗаполнено(Параметры.ВидДокумента) Тогда
		ОтборВидДокумента = Параметры.ВидДокумента;
	КонецЕсли;
	
	Если ЗаполнитьПоУмолчанию Тогда
		ОтборВидДокумента = Справочники.ВидыДокументов.ПустаяСсылка();
	КонецЕсли;
	
	Если Объект.ВариантНастройкиДляВидовДокументов =
		Перечисления.ВариантыНастройкиДоступностиДляВидовДокументов.ДляВсехВидовДокументов Тогда
		Элементы.ОтборВидДокумента.РежимВыбораИзСписка = Ложь;
		Элементы.ОтборВидДокумента.СписокВыбора.Очистить();
	ИначеЕсли Объект.ВариантНастройкиДляВидовДокументов =
		Перечисления.ВариантыНастройкиДоступностиДляВидовДокументов.ДляВыбранныхВидовДокументов Тогда
		Элементы.ОтборВидДокумента.РежимВыбораИзСписка = Истина;
		ДанныеСпискаВыбора = ВидыДокументов.Выгрузить().ВыгрузитьКолонку("ВидДокумента");
		Элементы.ОтборВидДокумента.СписокВыбора.ЗагрузитьЗначения(ДанныеСпискаВыбора);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтборВидДействия(ЗаполнитьПоУмолчанию = Ложь)
	
	Если Параметры.Свойство("ВидДействия") И ЗначениеЗаполнено(Параметры.ВидДействия) Тогда
		ОтборВидДействия = Параметры.ВидДействия;
	КонецЕсли;
	
	Если ЗаполнитьПоУмолчанию Тогда
		ОтборВидДокумента = Справочники.ВидыДействий.ПустаяСсылка();
	КонецЕсли;
	
	НастроитьОтборВидДействияСписокВыбора();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтборВидДействияСписокВыбора(ДоступныеВидыДействий = Неопределено)
	
	// Определение видов действий отбора.
	Если ДоступныеВидыДействий = Неопределено Тогда
		ВидыДействийОтбора = ДоступныеВидыДействий();
	Иначе
		ВидыДействийОтбора = ДоступныеВидыДействий;
	КонецЕсли;
	
	// Если отбор вида действия выходит за пределы допустимых видов действий - сбрасываем его.
	Если ЗначениеЗаполнено(ОтборВидДокумента)
		И ЗначениеЗаполнено(ОтборВидДействия)
		И ВидыДействийОтбора.Найти(ОтборВидДействия) = Неопределено Тогда
		ОтборВидДействия = Неопределено;
	КонецЕсли;
	
	// Настройка значений отбора вида действия.
	Элементы.ОтборВидДействия.СписокВыбора.Очистить();
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВидыДействий.Ссылка КАК Ссылка,
		|	ВидыДействий.Представление КАК Представление,
		|	ВидыДействий.Неактуальный КАК Неактуальный
		|ИЗ
		|	Справочник.ВидыДействий КАК ВидыДействий
		|ГДЕ
		|	ВидыДействий.Ссылка В (&ВидыДействийОтбора)
		|	И НЕ ВидыДействий.ЭтоГруппа");
	Запрос.УстановитьПараметр("ВидыДействийОтбора", ВидыДействийОтбора);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПредставлениеЭлементаОтбора = Выборка.Представление;
		Если Выборка.Неактуальный Тогда
			ПредставлениеЭлементаОтбора = ПредставлениеЭлементаОтбора + 
				НСтр("ru = ' (неактуальный)'");
		КонецЕсли;
		Элементы.ОтборВидДействия.СписокВыбора.Добавить(Выборка.Ссылка, ПредставлениеЭлементаОтбора);
	КонецЦикла;

	Элементы.ОтборВидДействия.СписокВыбора.СортироватьПоПредставлению();
	
КонецПроцедуры

&НаСервере
Функция ДоступныеВидыДействий()
	
	ДоступныеВидыДействий = ВидыДействий.Выгрузить().ВыгрузитьКолонку("ВидДействия");
	Если ЗначениеЗаполнено(ОтборВидДокумента) Тогда
		ВидыДействийПоВидуДокумента = ДействияСервер.ДействующиеВидыДействийПоВидамОбъектов(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОтборВидДокумента));
		ДоступныеВидыДействий = ОбщегоНазначенияДокументооборотКлиентСервер.ПересечениеМассивов(
			ДоступныеВидыДействий,
			ВидыДействийПоВидуДокумента);
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ДоступныеВидыДействий,
		Справочники.ВидыДействий.ПустаяСсылка());
	
	Возврат ДоступныеВидыДействий;
	
КонецФункции

&НаСервере
Процедура УстановитьОтборВидыДействий(ОбновитьВидимостьДопРеквизитов = Ложь)
	
	ДоступныеВидыДействий = ДоступныеВидыДействий();
	НастроитьОтборВидДействияСписокВыбора(ДоступныеВидыДействий);
	Если ЗначениеЗаполнено(ОтборВидДействия) Тогда
		ОтборВидыДействий = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОтборВидДействия);
	ИначеЕсли ЗначениеЗаполнено(ОтборВидДокумента) Тогда
		ОтборВидыДействий = ДоступныеВидыДействий;
		ОтборВидыДействий.Добавить(Справочники.ВидыДействий.ПустаяСсылка());
	Иначе
		ОтборВидыДействий = Неопределено;
		КоллекцияЭлементовКэширующегоДерева = НастройкиДоступностиКэш.ПолучитьЭлементы();
		КоллекцияЭлементовКэширующегоДерева.Очистить();
	КонецЕсли;
	
	ЭлементыДерева = НастройкиДоступности.ПолучитьЭлементы();
	ПараметрыОтбора = Новый Структура("ВидДействия");
	
	// Обновление видимости строк состояний действий.
	ВидныВсеВидыДействий = (ОтборВидыДействий = Неопределено);
	Для Каждого СтрокаВидДействия Из ВидыДействий Цикл
		
		ВидДействияВиден = ВидныВсеВидыДействий
			Или ОтборВидыДействий.Найти(СтрокаВидДействия.ВидДействия)<> Неопределено;
		
		Если Не ВидДействияВиден Тогда
			ПараметрыОтбора.ВидДействия = СтрокаВидДействия.ВидДействия;
			Ветка = ОбщегоНазначенияДокументооборотКлиентСервер.НайтиСтрокуДерева(НастройкиДоступности,
				ПараметрыОтбора);
			ЭлементыДерева.Удалить(Ветка);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ОбновитьВидимостьДопРеквизитов Тогда
		
		ДопРеквизиты = ДополнительныеРеквизитыИСведения(ОтборВидДокумента);
		УстановитьВидимостьКолонокДополнительныхРеквизитов(ДопРеквизиты);
		
	КонецЕсли;
	
	// Настройка отображения отборов.
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(Элементы.ОтборВидДокумента,
		ОтборВидДокумента);
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(Элементы.ОтборВидДействия,
		ОтборВидДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиСтрокуВДеревеПоВидуДействияСостоянию(КоллекцияОдногоУровня, ИскомаяСтрока, Индекс) Экспорт
	
	Если ТипЗнч(Индекс) = Тип("Число") И Индекс > -1 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаКоллекции Из КоллекцияОдногоУровня Цикл
		Если СтрокаКоллекции.ВидДействияСостояние = ИскомаяСтрока Тогда
			Индекс = СтрокаКоллекции.ПолучитьИдентификатор();
			Возврат;
		Иначе
			НайтиСтрокуВДеревеПоВидуДействияСостоянию(СтрокаКоллекции.ПолучитьЭлементы(), ИскомаяСтрока, Индекс);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВСтолбцеНастройки(ДоступностьПоля)
	
	ТекущиеДанные = Элементы.НастройкиДоступности.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Поле = Элементы.НастройкиДоступности.ТекущийЭлемент;
	Если Поле = Элементы.НастройкиДоступностиВидДействияСостояниеПредставление Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКолонкиПредставлениеДоступности = СтрЗаменить(Поле.Имя, "НастройкиДоступности", "");
	ИмяКолонкиЗначениеДоступности = СтрЗаменить(ИмяКолонкиПредставлениеДоступности, "Строка", "");
	
	ДоступностьПоляСтрокой = ДоступностьСтрокой(ДоступностьПоля);
	
	Для Каждого СтрНастройки Из НастройкиДоступности.ПолучитьЭлементы() Цикл
		
		ПодчиненныеСтроки = СтрНастройки.ПолучитьЭлементы();
		
		Если ПодчиненныеСтроки.Количество() Тогда
			Для Каждого Строка Из ПодчиненныеСтроки Цикл
				Строка[ИмяКолонкиПредставлениеДоступности] = ДоступностьПоляСтрокой;
				Строка[ИмяКолонкиЗначениеДоступности] = ДоступностьПоля;
			КонецЦикла;
		Иначе
			СтрНастройки[ИмяКолонкиПредставлениеДоступности] = ДоступностьПоляСтрокой;
			СтрНастройки[ИмяКолонкиЗначениеДоступности] = ДоступностьПоля;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВСтрокеНастройки(ДоступностьПоля)
	
	ТекущиеДанные = Элементы.НастройкиДоступности.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДоступностьПоляСтрокой = ДоступностьСтрокой(ДоступностьПоля);
	ВидыДействийСостояния = Новый Массив;
	ТекущийВидДействияСостояние = ТекущиеДанные.ВидДействияСостояние;
	
	Для Каждого ТекСтрока Из Элементы.НастройкиДоступности.ВыделенныеСтроки Цикл
		Строка = НастройкиДоступности.НайтиПоИдентификатору(ТекСтрока);
		ПодчиненныеСтроки = Строка.ПолучитьЭлементы();
		Если ПодчиненныеСтроки.Количество() Тогда
			Для Каждого Подстрока Из ПодчиненныеСтроки Цикл
				ВидыДействийСостояния.Добавить(Подстрока.ВидДействияСостояние);
			КонецЦикла;
		Иначе
			ВидыДействийСостояния.Добавить(Строка.ВидДействияСостояние);
		КонецЕсли;
	КонецЦикла;
	УстановитьСтрокуНаСервере(ВидыДействийСостояния, ДоступностьПоля, ДоступностьПоляСтрокой);
	
	Индекс = -1;
	НайтиСтрокуВДеревеПоВидуДействияСостоянию(НастройкиДоступности.ПолучитьЭлементы(), ТекущийВидДействияСостояние,
		Индекс);
	Если Индекс > -1 Тогда
		Элементы.НастройкиДоступности.ТекущаяСтрока = Индекс;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтрокуНаСервере(Знач ВидыДействийСостояния, Знач ДоступностьПоля, Знач ДоступностьПоляСтрокой)
	
	СтруктураПоиска = Новый Структура("ВидДействияСостояние");
	
	Для Каждого Элемент Из ВидыДействийСостояния Цикл
		
		СтруктураПоиска.ВидДействияСостояние = Элемент;
		Ветка = ОбщегоНазначенияДокументооборотКлиентСервер.НайтиСтрокуДерева(НастройкиДоступности, СтруктураПоиска);
		Если Ветка = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Для Каждого Колонка Из КолонкиДереваНастроек Цикл
			
			ИмяКолонкиПредставлениеДоступности = СтрШаблон("НастройкиДоступности%1Строка", Колонка.Значение);
			Если Элементы[ИмяКолонкиПредставлениеДоступности].Видимость = Ложь Тогда
				Продолжить;
			КонецЕсли;
			
			РеквизитПредставлениеДоступности = СтрШаблон("%1Строка", Колонка.Значение);
			Ветка[Колонка.Значение] = ДоступностьПоля;
			Ветка[РеквизитПредставлениеДоступности] = ДоступностьПоляСтрокой;
			
		КонецЦикла;
			
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДополнительныеРеквизитыИСведения(ОтборВидДокумента = Неопределено)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Наборы.Свойство КАК Свойство,
		|	Наборы.Наименование КАК Наименование
		|ИЗ
		|	(ВЫБРАТЬ
		|		Наборы.Свойство КАК Свойство,
		|		Наборы.Свойство.Наименование КАК Наименование
		|	ИЗ
		|		Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК Наборы
		|	ГДЕ
		|		&ОтборНаборПоВидуДокумента
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Наборы.Свойство,
		|		Наборы.Свойство.Наименование
		|	ИЗ
		|		Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК Наборы
		|	ГДЕ
		|		&ОтборНаборПоВидуДокумента) КАК Наборы
		|УПОРЯДОЧИТЬ ПО
		|	Наименование");
	
	Если ЗначениеЗаполнено(ОтборВидДокумента) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборНаборПоВидуДокумента",
			"Наборы.Ссылка = &ОтборНаборПоВидуДокумента");
		НаборСвойств = ОбщегоНазначенияДокументооборот.ЗначениеРеквизитаОбъектаВПривилегированномРежиме(
			ОтборВидДокумента, "НаборСвойств");
		Запрос.УстановитьПараметр("ОтборНаборПоВидуДокумента", НаборСвойств);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборНаборПоВидуДокумента",
			"Наборы.Ссылка = ЗНАЧЕНИЕ(Справочник.НаборыДополнительныхРеквизитовИСведений.Справочник_ДокументыПредприятия)");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Результат = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		ДанныеДополнительногоСвойства = Новый Структура("Свойство, Наименование",
			Выборка.Свойство, Выборка.Наименование);
		Результат.Добавить(ДанныеДополнительногоСвойства);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьКолонокДополнительныхРеквизитов(ДопРеквизитыИСведения)
	
	ИмяГруппыКолонокДопРеквизиты = "НастройкиДоступностиГруппаДопРеквизиты";
	
	Если ДопРеквизитыИСведения.Количество() = 0 Тогда
		Элементы[ИмяГруппыКолонокДопРеквизиты].Видимость = Ложь;
		Возврат;
	Иначе
		Элементы[ИмяГруппыКолонокДопРеквизиты].Видимость = Истина;
	КонецЕсли;
	
	ИменаОтбираемыхДопРеквизитов = Новый Массив;
	Для Каждого ДопРеквизит Из ДопРеквизитыИСведения Цикл
		ИмяДопРеквизита = ИмяДопРеквизита(ДопРеквизит.Свойство);
		ИменаОтбираемыхДопРеквизитов.Добавить(ИмяДопРеквизита);
	КонецЦикла;
	
	Для Каждого Колонка Из КолонкиДопРеквизитов Цикл
		
		ИмяДопРеквизитаНаФорме = СтрШаблон("НастройкиДоступности%1Строка", Колонка.Значение);
		
		Если ИменаОтбираемыхДопРеквизитов.Найти(Колонка.Значение) = Неопределено Тогда
			Элементы[ИмяДопРеквизитаНаФорме].Видимость = Ложь;
		Иначе
			Элементы[ИмяДопРеквизитаНаФорме].Видимость = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДоступностьДопРеквизитов(СтрокаНастройки, Доступность)
	
	Для Каждого Колонка Из КолонкиДопРеквизитов Цикл
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаНастройки, Колонка.Значение) Тогда
			ИмяКолонкиПредставлениеДоступности = СтрШаблон("%1Строка", Колонка.Значение);
			СтрокаНастройки[Колонка.Значение] = Доступность;
			СтрокаНастройки[ИмяКолонкиПредставлениеДоступности] = ДоступностьСтрокой(Доступность);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяДопРеквизита(Свойство, Постфикс = "")
	
	Имя = "Ad_" + XMLСтрока(Свойство);
	Имя = СтрЗаменить(Имя, "-", "_");
	
	Если ЗначениеЗаполнено(Постфикс) Тогда
		Имя = Имя + Постфикс;
	КонецЕсли;
	
	Возврат Имя;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьДеревоПоИсточнику(Приемник, Источник, ДобавлятьСтроки = Истина)
	
	СтрокиИсточника = Источник.Строки;
	
	Если СтрокиИсточника.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиПриемника = Приемник.Строки;
	
	ОтборСтрок = Новый Структура("ВидДействия, СостояниеДокумента", Справочники.ВидыДействий.ПустаяСсылка(),
		Перечисления.СостоянияДокументов.ПустаяСсылка());
	
	Для Каждого СтрокаДерева Из СтрокиИсточника Цикл
		
		ОтборСтрок.ВидДействия = СтрокаДерева.ВидДействия;
		ОтборСтрок.СостояниеДокумента = СтрокаДерева.СостояниеДокумента;
		
		НайденныеСтроки = СтрокиПриемника.НайтиСтроки(ОтборСтрок);
		
		Если Не НайденныеСтроки.Количество() И Не ДобавлятьСтроки Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не НайденныеСтроки.Количество() Тогда
			НоваяКэширующаяСтрока = СтрокиПриемника.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяКэширующаяСтрока, СтрокаДерева);
			ЗаполнитьДеревоПоИсточнику(НоваяКэширующаяСтрока, СтрокаДерева, ДобавлятьСтроки);
		Иначе
			ЗаполнитьЗначенияСвойств(НайденныеСтроки[0], СтрокаДерева);
			ЗаполнитьДеревоПоИсточнику(НайденныеСтроки[0], СтрокаДерева, ДобавлятьСтроки);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
