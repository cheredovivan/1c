///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Возвращает строку уникального идентификатора
// предопределенной группы ВсеПользователи.
//
// Возвращаемое значение:
//  Строка - строка уникального идентификатора.
//
Функция ИдентификаторГруппыВсеПользователи()
	
	Возврат "515b9805-054f-424e-816e-87afebdac3b6";
	
КонецФункции

// Параметры:
//  ИмяГруппы - Строка - "ВсеВнешниеПользователи", "ВсеПользователи".
//
// Возвращаемое значение:
//  СправочникСсылка.РабочиеГруппы
//  СправочникСсылка.ГруппыВнешнихПользователей
//
Функция СтандартнаяГруппаПользователей(ИмяГруппы) Экспорт
	
	УстановитьОтключениеБезопасногоРежима(Истина);
	УстановитьПривилегированныйРежим(Истина);
	
	Если ИмяГруппы = "ВсеВнешниеПользователи" Тогда
		ИдентификаторГруппы = Справочники.ГруппыВнешнихПользователей.ИдентификаторГруппыВсеВнешниеПользователи();
		ПолноеИмяСправочника = "Справочник.ГруппыВнешнихПользователей";
		МенеджерСправочника = Справочники.ГруппыВнешнихПользователей;
		НаименованиеГруппы = НСтр("ru = 'Все внешние пользователи'", ОбщегоНазначения.КодОсновногоЯзыка());
		
	ИначеЕсли ИмяГруппы = "ВсеПользователи" Тогда
		ИдентификаторГруппы = ИдентификаторГруппыВсеПользователи();
		ПолноеИмяСправочника = "Справочник.РабочиеГруппы";
		МенеджерСправочника = Справочники.РабочиеГруппы;
		НаименованиеГруппы = НСтр("ru = 'Все пользователи'", ОбщегоНазначения.КодОсновногоЯзыка());
	Иначе
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Неизвестное имя стандартной группы ""%1"".'"), ИмяГруппы);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИмяПредопределенныхДанных", ИмяГруппы);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ГруппыПользователей.Ссылка КАК Ссылка
	|ИЗ
	|	&Таблица КАК ГруппыПользователей
	|ГДЕ
	|	ГруппыПользователей.ИмяПредопределенныхДанных = &ИмяПредопределенныхДанных
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Таблица", ПолноеИмяСправочника);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1 И Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ГруппыПользователей.Ссылка КАК Ссылка,
	|	ГруппыПользователей.Наименование КАК Наименование
	|ИЗ
	|	&Таблица КАК ГруппыПользователей
	|ГДЕ
	|	ГруппыПользователей.ИмяПредопределенныхДанных = &ИмяПредопределенныхДанных
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Таблица", ПолноеИмяСправочника);
	
	Блокировка = Новый БлокировкаДанных;
	Блокировка.Добавить(ПолноеИмяСправочника);
	
	НачатьТранзакцию();
	Попытка
		Блокировка.Заблокировать();
		Выборка = Запрос.Выполнить().Выбрать();
		
		СсылкаНового = МенеджерСправочника.ПолучитьСсылку(
			Новый УникальныйИдентификатор(ИдентификаторГруппы));
		
		Если Выборка.Количество() = 1 И Выборка.Следующий() Тогда
			ГруппаОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
		ИначеЕсли Выборка.Количество() > 1 Тогда
			ГруппаОбъект = ГруппаИзДублейПредопределенного(Выборка, НаименованиеГруппы, СсылкаНового);
		Иначе
			ГруппаОбъект = СсылкаНового.ПолучитьОбъект();
			Если ГруппаОбъект = Неопределено Тогда
				ГруппаПоНаименованию = ГруппаПоНаименованию(НаименованиеГруппы, ПолноеИмяСправочника);
				Если ЗначениеЗаполнено(ГруппаПоНаименованию) Тогда
					ГруппаОбъект = ГруппаПоНаименованию.ПолучитьОбъект();
				КонецЕсли;
			КонецЕсли;
			Если ГруппаОбъект = Неопределено Тогда
				ГруппаОбъект = МенеджерСправочника.СоздатьЭлемент();
				ГруппаОбъект.УстановитьСсылкуНового(СсылкаНового);
			КонецЕсли;
		КонецЕсли;
		Если ГруппаОбъект.ИмяПредопределенныхДанных <> ИмяГруппы Тогда
			ГруппаОбъект.ИмяПредопределенныхДанных = ИмяГруппы;
		КонецЕсли;
		Если ГруппаОбъект.Наименование <> НаименованиеГруппы Тогда
			ГруппаОбъект.Наименование = НаименованиеГруппы;
		КонецЕсли;
		Если ЗначениеЗаполнено(ГруппаОбъект.Состав) Тогда
			ГруппаОбъект.Состав.Очистить();
		КонецЕсли;
		Если ЗначениеЗаполнено(ГруппаОбъект.Комментарий) Тогда
			ГруппаОбъект.Комментарий = "";
		КонецЕсли;
		Если ГруппаОбъект.Модифицированность() Тогда
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ГруппаОбъект);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат ГруппаОбъект.Ссылка;
	
КонецФункции

// Для функции СтандартнаяГруппаПользователей.
Функция ГруппаИзДублейПредопределенного(Выборка, НаименованиеГруппы, СсылкаНового)
	
	ГруппаПоПорядку = Неопределено;
	ГруппаПоСсылкеНового = Неопределено;
	ГруппаПоНаименованию = Неопределено;
	
	ОбъектыДляОтвязки = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		ОбъектыДляОтвязки.Вставить(Выборка.Ссылка, Истина);
		Если ГруппаПоПорядку = Неопределено Тогда
			ГруппаПоПорядку = Выборка.Ссылка;
		КонецЕсли;
		Если Выборка.Ссылка = СсылкаНового Тогда
			ГруппаПоСсылкеНового = Выборка.Ссылка;
		ИначеЕсли Выборка.Наименование = НаименованиеГруппы
		        И ГруппаПоНаименованию = Неопределено Тогда
			ГруппаПоНаименованию = Выборка.Ссылка;
		КонецЕсли;
	КонецЦикла;
	
	Если ГруппаПоСсылкеНового <> Неопределено Тогда
		ГруппаОбъект = ГруппаПоСсылкеНового.ПолучитьОбъект();
		ОбъектыДляОтвязки.Удалить(ГруппаПоСсылкеНового);
		
	ИначеЕсли ГруппаПоНаименованию <> Неопределено Тогда
		ГруппаОбъект = ГруппаПоНаименованию.ПолучитьОбъект();
		ОбъектыДляОтвязки.Удалить(ГруппаПоНаименованию);
	Иначе
		ГруппаОбъект = ГруппаПоПорядку.ПолучитьОбъект();
		ОбъектыДляОтвязки.Удалить(ГруппаПоПорядку);
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из ОбъектыДляОтвязки Цикл
		ТекущийОбъект = КлючИЗначение.Ключ.ПолучитьОбъект();
		ТекущийОбъект.ИмяПредопределенныхДанных = "";
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ТекущийОбъект);
	КонецЦикла;
	
	Возврат ГруппаОбъект;
	
КонецФункции

// Для функции СтандартнаяГруппаПользователей.
Функция ГруппаПоНаименованию(Наименование, ПолноеИмяСправочника)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ГруппыПользователей.Ссылка КАК Ссылка
	|ИЗ
	|	&Таблица КАК ГруппыПользователей
	|ГДЕ
	|	ГруппыПользователей.Наименование = &Наименование
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Таблица", ПолноеИмяСправочника);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Смотри также ОбновлениеИнформационнойБазыПереопределяемый.ПриНастройкеНачальногоЗаполненияЭлементов
// 
// Параметры:
//  Настройки - см. ОбновлениеИнформационнойБазыПереопределяемый.ПриНастройкеНачальногоЗаполненияЭлементов.Настройки
//
Процедура ПриНастройкеНачальногоЗаполненияЭлементов(Настройки) Экспорт
	
	Настройки.ПриНачальномЗаполненииЭлемента = Ложь;
	
КонецПроцедуры

// Смотри также ОбновлениеИнформационнойБазыПереопределяемый.ПриНачальномЗаполненииЭлементов
// 
// Параметры:
//   КодыЯзыков - см. ОбновлениеИнформационнойБазыПереопределяемый.ПриНачальномЗаполненииЭлементов.КодыЯзыков
//   Элементы - см. ОбновлениеИнформационнойБазыПереопределяемый.ПриНачальномЗаполненииЭлементов.Элементы
//   ТабличныеЧасти - см. ОбновлениеИнформационнойБазыПереопределяемый.ПриНачальномЗаполненииЭлементов.ТабличныеЧасти
//
Процедура ПриНачальномЗаполненииЭлементов(КодыЯзыков, Элементы, ТабличныеЧасти) Экспорт

	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "ВсеПользователи";
	Элемент.Наименование = НСтр("ru = 'Все сотрудники'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "РуководителиПодразделений";
	Элемент.Наименование = НСтр("ru = 'Руководители подразделений'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Возвращает структуру полей рабочей группы.
//
// Возвращаемое значение:
//   Структура
//     Наименование
//     Родитель
//
Функция ПолучитьСтруктуруРабочихГрупп() Экспорт
	
	СвойстваЭлемента = Новый Структура;
	СвойстваЭлемента.Вставить("Наименование");
	СвойстваЭлемента.Вставить("Родитель");
	
	Возврат СвойстваЭлемента;
	
КонецФункции

// Создает и записывает в БД элемент справочника.
//
// Параметры:
//   СтруктураРабочейГруппы - Структура - структура полей рабочей группы.
//
Функция СоздатьРабочуюГруппу(СтруктураРабочейГруппы) Экспорт
	
	НовыйЭлемент = СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НовыйЭлемент, СтруктураРабочейГруппы);
	НовыйЭлемент.Записать();
	
	Возврат НовыйЭлемент.Ссылка;
	
КонецФункции

// Возвращает состав сотрудников, входящих в указанный контейнер.
//
// Параметры:
//   Контейнер - СправочникСсылка.Сотрудники - контейнер сотрудников.
//
// Возвращаемое значение:
//   Массив - массив значений СправочникСсылка.Сотрудники - состав контейнера.
//
Функция СоставКонтейнераСотрудников(Контейнер) Экспорт
	
	Если Не ЗначениеЗаполнено(Контейнер) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Если Контейнер = Справочники.РабочиеГруппы.ВсеПользователи Тогда
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Сотрудники.Ссылка КАК Ссылка
		    |ИЗ
		    |	Справочник.Сотрудники КАК Сотрудники
		    |ГДЕ
		    |	НЕ Сотрудники.ПометкаУдаления");
	Иначе
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РабочиеГруппыСостав.Участник КАК Участник
			|ИЗ
			|	Справочник.РабочиеГруппы КАК РабочиеГруппы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РабочиеГруппы.Состав КАК РабочиеГруппыСостав
			|		ПО РабочиеГруппы.Ссылка = РабочиеГруппыСостав.Ссылка
			|ГДЕ
			|	РабочиеГруппы.Ссылка В ИЕРАРХИИ(&Контейнер)
			|	И НЕ РабочиеГруппы.ПометкаУдаления
			|	И НЕ РабочиеГруппыСостав.Участник.ПометкаУдаления");
		Запрос.УстановитьПараметр("Контейнер", Контейнер);
	КонецЕсли;
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
	
	Возврат Результат;
	
КонецФункции

// Возвращает контейнеры типа РабочиеГруппы, которым принадлежит указанный сотрудник.
//
// Параметры:
//   Сотрудник - СправочникСсылка.Сотрудники - проверяемый сотрудник.
//
// Возвращаемое значение:
//   Массив - массив значений СправочникСсылка.РабочиеГруппы -
//   	контейнеры, которым принадлежит сотрудник.
//
Функция КонтейнерыСотрудника(Сотрудник) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Сотрудник) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сотрудник, "ПометкаУдаления") = Истина Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РабочиеГруппы.Ссылка КАК Ссылка,
		|	РабочиеГруппы.Родитель КАК Родитель1,
		|	РабочиеГруппы.Родитель.Родитель КАК Родитель2,
		|	РабочиеГруппы.Родитель.Родитель.Родитель КАК Родитель3
		|ИЗ
		|	Справочник.РабочиеГруппы КАК РабочиеГруппы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РабочиеГруппы.Состав КАК РабочиеГруппыСостав
		|		ПО РабочиеГруппы.Ссылка = РабочиеГруппыСостав.Ссылка
		|ГДЕ
		|	РабочиеГруппыСостав.Участник = &Сотрудник
		|	И НЕ РабочиеГруппы.ПометкаУдаления
		|");
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Контейнеры = Новый Массив;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Контейнеры.Добавить(Выборка.Ссылка);
		Если ЗначениеЗаполнено(Выборка.Родитель1) Тогда
			Контейнеры.Добавить(Выборка.Родитель1);
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Родитель2) Тогда
			Контейнеры.Добавить(Выборка.Родитель2);
		КонецЕсли;
		ТекущийРодитель = Выборка.Родитель3;
		Пока ЗначениеЗаполнено(ТекущийРодитель) Цикл
			Контейнеры.Добавить(ТекущийРодитель);
			ТекущийРодитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийРодитель, "Родитель");
		КонецЦикла;
	КонецЦикла;
	Контейнеры.Добавить(Справочники.РабочиеГруппы.ВсеПользователи);
	Контейнеры = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Контейнеры);
	
	Возврат Контейнеры;
	
КонецФункции

// Вернет Истина, у этого объекта метаданных есть функция ПолучитьАдресФото
Функция ЕстьФункцияПолученияФото() Экспорт
	
	Возврат Ложь;
	
КонецФункции

// Возвращает отслеживаемые реквизиты для дополнительного свойства ПредыдущиеЗначенияРеквизитов.
//
// Возвращаемое значение:
//  Строка - Отслеживаемые реквизиты.
//
Функция ОтслеживаемыеРеквизиты() Экспорт
	
	ОтслеживаемыеРеквизиты = "Родитель, Состав, Недействительна";
	
	Возврат ОтслеживаемыеРеквизиты;
	
КонецФункции

#Область ОбновлениеСоставовГруппПользователей

// Конструктор параметров обновления составов групп пользователей.
//
// Возвращаемое значение:
//	Структура:
//		* ЭтоНовый - Булево
//		* ИзмененияСостава - Массив Из СправочникСсылка.Пользователи, СправочникСсылка.Сотрудники.
//		* Родитель - СправочникСсылка.РабочиеГруппы.
//		* ПредыдущийРодитель - СправочникСсылка.РабочиеГруппы.
//
Функция ПараметрыОбновленияСоставовГруппПользователей() Экспорт
	
	ПараметрыОбновления = Новый Структура;
	ПараметрыОбновления.Вставить("ЭтоНовый", Ложь);
	ПараметрыОбновления.Вставить("ИзмененияСостава", Новый Массив);
	ПараметрыОбновления.Вставить("Родитель", ПустаяСсылка());
	ПараметрыОбновления.Вставить("ПредыдущийРодитель", ПустаяСсылка());
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Устанавливает значения параметров обновления составов групп пользователей по данным объекта.
//
// Параметры:
//	Объект - СправочникОбъект.РабочиеГруппы - Объект, для которго необходимо определить параметры обновления.
//
// Возвращаемое значение:
//	Структура: см. ПараметрыОбновленияСоставовГруппПользователей.
//
Функция ЗначенияПараметровСоставовГруппПользователей(Объект) Экспорт
	
	ПараметрыОбновления = ПараметрыОбновленияСоставовГруппПользователей();
	
	ПредыдущиеЗначенияРеквизитов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Объект.ДополнительныеСвойства, "ПредыдущиеЗначенияРеквизитов");
	
	СсылкаНаОбъект = Объект.Ссылка;
	
	Если Объект.ЭтоНовый() Тогда
		Если Объект.ПолучитьСсылкуНового().Пустая() Тогда // Установим ссылку нового, если ее нет
			СсылкаНаОбъект = ПолучитьСсылку(Новый УникальныйИдентификатор);
			Объект.УстановитьСсылкуНового(СсылкаНаОбъект);
		Иначе // Если ссылку нового уже установили ранее, возьмем ее
			СсылкаНаОбъект = Объект.ПолучитьСсылкуНового();
		КонецЕсли;
	КонецЕсли;
	
	Если ПредыдущиеЗначенияРеквизитов = Неопределено Тогда
		ПредыдущиеЗначенияРеквизитов =
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОбъект, ОтслеживаемыеРеквизиты());
	КонецЕсли; 
	
	ПредыдущийСоставРабочейГруппы = Неопределено;
	Если ПредыдущиеЗначенияРеквизитов.Состав <> Неопределено Тогда
		Если ТипЗнч(ПредыдущиеЗначенияРеквизитов.Состав) = Тип("РезультатЗапроса") Тогда
			ПредыдущийСоставРабочейГруппы = ПредыдущиеЗначенияРеквизитов.Состав.Выгрузить()
		Иначе
			ПредыдущийСоставРабочейГруппы = ПредыдущиеЗначенияРеквизитов.Состав;
		КонецЕсли;
	КонецЕсли;
	
	ИзмененияСоставаСотрудники = ПользователиСлужебный.РазличияЗначенийКолонки(
		"Участник", Объект.Состав.Выгрузить(), ПредыдущийСоставРабочейГруппы);
		
	ИзмененияСостава = Сотрудники.ЛюбыеПользователиСотрудников(ИзмененияСоставаСотрудники);
	
	ПараметрыОбновления.ЭтоНовый = Объект.ЭтоНовый();
	ПараметрыОбновления.ИзмененияСостава = ИзмененияСостава;
	ПараметрыОбновления.Родитель = Объект.Родитель;
	ПараметрыОбновления.ПредыдущийРодитель = ПредыдущиеЗначенияРеквизитов.Родитель;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Обновляет составы групп пользователей.
//
// Параметры:
//	РабочаяГруппа - СправочникСсылка.РабочиеГруппы - Рабочая группа , для которой необходимо обновить составы групп пользователей.
//	ПараметрыОбновления - см. ПараметрыОбновленияСоставовГруппПользователей.
//
Процедура ОбновитьСоставыГруппПользователей(РабочаяГруппа, ПараметрыОбновления) Экспорт
	
	ИзмененияСоставов = ПользователиСлужебный.НовыеИзмененияСоставовГрупп();
	
	Если РабочаяГруппа <> ВсеПользователи Тогда
		
		Если ПараметрыОбновления.ПредыдущийРодитель <> ПараметрыОбновления.Родитель Тогда
			Если ЗначениеЗаполнено(ПараметрыОбновления.Родитель) Тогда
				ПользователиСлужебный.ОбновитьИерархиюГрупп(ПараметрыОбновления.Родитель, ИзмененияСоставов, Ложь);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ПараметрыОбновления.ПредыдущийРодитель) Тогда
				ПользователиСлужебный.ОбновитьСоставыИерархическихГруппПользователей(ПараметрыОбновления.ПредыдущийРодитель,
					ИзмененияСоставов);
			КонецЕсли;
		КонецЕсли;
		
		ПользователиСлужебный.ОбновитьСоставыИерархическихГруппПользователей(РабочаяГруппа,
			ИзмененияСоставов);
		
		ПользователиСлужебный.ОбновитьИспользуемостьСоставовГруппПользователей(РабочаяГруппа,
			ИзмененияСоставов);
		
		ПользователиСлужебный.ПослеОбновленияСоставовГруппПользователей(ИзмененияСоставов);
		
	КонецЕсли;
		
	ИнтеграцияПодсистемБСП.ПослеДобавленияИзмененияПользователяИлиГруппы(РабочаяГруппа, ПараметрыОбновления.ЭтоНовый);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДанныхКэширующихОбъектов

// Конструктор параметров обновления данных кэширующих объектов.
//
// Возвращаемое значение:
//	Структура:
//		* ОбъектыДляОбновленияСотрудниковВКонтейнерах - Массив Из СправочникСсылка.РабочиеГруппы - Список объектов, по которым необходимо обновить данные РС СотрудникиВКонтейнерах.
//
Функция ПараметрыОбновленияДанныхКэширующихОбъектов() Экспорт
	
	ПараметрыОбновления = Новый Структура;
	ПараметрыОбновления.Вставить("ОбъектыДляОбновленияСотрудниковВКонтейнерах", Новый Массив);
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Устанавливает значения параметров обновления данных кэширующих объектов по данным объекта.
//
// Параметры:
//	Объект - СправочникОбъект.РабочиеГруппы - Объект, для которго необходимо определить параметры обновления.
//
// Возвращаемое значение:
//	Структура: см. ПараметрыОбновленияДанныхКэширующихОбъектов.
//
Функция ЗначенияПараметровОбновленияДанныхКэширующихОбъектов(Объект) Экспорт
	
	ПараметрыОбновления = ПараметрыОбновленияДанныхКэширующихОбъектов();
	
	ПредыдущиеЗначенияРеквизитов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Объект.ДополнительныеСвойства, "ПредыдущиеЗначенияРеквизитов");
	
	СсылкаНаОбъект = Объект.Ссылка;
	
	Если Объект.ЭтоНовый() Тогда
		Если Объект.ПолучитьСсылкуНового().Пустая() Тогда // Установим ссылку нового, если ее нет
			СсылкаНаОбъект = ПолучитьСсылку(Новый УникальныйИдентификатор);
			Объект.УстановитьСсылкуНового(СсылкаНаОбъект);
		Иначе // Если ссылку нового уже установили ранее, возьмем ее
			СсылкаНаОбъект = Объект.ПолучитьСсылкуНового();
		КонецЕсли;
	КонецЕсли;
	
	Если ПредыдущиеЗначенияРеквизитов = Неопределено Тогда
		ПредыдущиеЗначенияРеквизитов =
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОбъект, ОтслеживаемыеРеквизиты());
	КонецЕсли; 
	
	Если СсылкаНаОбъект <> ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.РабочиеГруппы.ВсеПользователи") Тогда
		Если ЗначениеЗаполнено(ПредыдущиеЗначенияРеквизитов.Родитель)
				И ПредыдущиеЗначенияРеквизитов.Родитель <> Объект.Родитель Тогда
			
			ПараметрыОбновления.ОбъектыДляОбновленияСотрудниковВКонтейнерах.Добавить(
				ПредыдущиеЗначенияРеквизитов.Родитель);
		КонецЕсли;
		
		ПараметрыОбновления.ОбъектыДляОбновленияСотрудниковВКонтейнерах.Добавить(СсылкаНаОбъект);
	КонецЕсли;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Обновляет данные зависимых объектов согласно установленным параметрам.
//
// Параметры:
//	Объект - СправочникОбъект.СтруктураПредприятия - Объект, по данным которого необходимо обновить адресной книги.
//	ПараметрыОбновления - Структура Из КлючИЗначение - см. ПараметрыОбновленияДанныхКэширующихОбъектов.
//
Процедура ОбновитьДанныеКэширующихОбъектов(Объект, ПараметрыОбновления) Экспорт
	
	Если ПараметрыОбновления.ОбъектыДляОбновленияСотрудниковВКонтейнерах.Количество() Тогда
		РегистрыСведений.СотрудникиВКонтейнерах.ОбновитьДанныеКонтейнеров(
			ПараметрыОбновления.ОбъектыДляОбновленияСотрудниковВКонтейнерах);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеАдреснойКниги

// Конструктор параметров обновления адресной книги.
//
// Возвращаемое значение:
//	Структура:
//		* ОбновитьДанныеОбъекта - Булево - Признак обновления данных по объекту.
//		* ОбновитьДанныеОтображенияОбъекта - Булево - Признак обновления данных отображения.
//		* ОбновитьСловаПоискаПоОбъекту - Булево - Признак обновления слов поиска по объекту.
//		* ОбновитьДоступностьВПоискеПоОбъекту - Булево - Признак обновления доступности в результатах поиска.
//		* ОбновитьСоставГруппы - Булево - Признак необходимости обновления данных адресной книги по составу рабочей группы.
//
Функция ПараметрыОбновленияАдреснойКниги() Экспорт
	
	ПараметрыОбновленияАдреснойКниги = Новый Структура;
	ПараметрыОбновленияАдреснойКниги.Вставить("ОбновитьДанныеОбъекта", Ложь);
	ПараметрыОбновленияАдреснойКниги.Вставить("ОбновитьДанныеОтображенияОбъекта", Ложь);
	ПараметрыОбновленияАдреснойКниги.Вставить("ОбновитьСловаПоискаПоОбъекту", Ложь);
	ПараметрыОбновленияАдреснойКниги.Вставить("ОбновитьДоступностьВПоискеПоОбъекту", Ложь);
	ПараметрыОбновленияАдреснойКниги.Вставить("ОбновитьСоставГруппы", Ложь);
	
	Возврат ПараметрыОбновленияАдреснойКниги;
	
КонецФункции

// Устанавливает значения параметров обновления адресной книги по данным объекта.
//
// Параметры:
//	Объект - СправочникОбъект.РабочиеГруппы - Объект, для которго необходимо определить параметры обновления.
//
// Возвращаемое значение:
//	Структура: см. ПараметрыОбновленияАдреснойКниги.
//
Функция ЗначенияПараметровОбновленияАдреснойКнигиПоОбъекту(Объект) Экспорт
	
	ПараметрыОбновленияАдреснойКниги = ПараметрыОбновленияАдреснойКниги();
		
	Если Не РаботаСАдреснойКнигой.ТребуетсяОбновлениеАдреснойКниги(Объект)
		Или Объект.Ссылка = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.РабочиеГруппы.ВсеПользователи") Тогда
		
		Возврат ПараметрыОбновленияАдреснойКниги; 
	КонецЕсли;
	
	Если Объект.ЭтоНовый() Тогда
		ПараметрыОбновленияАдреснойКниги.ОбновитьДанныеОбъекта = Истина;
		ПараметрыОбновленияАдреснойКниги.ОбновитьСоставГруппы = Истина;
		ПараметрыОбновленияАдреснойКниги.ОбновитьСловаПоискаПоОбъекту = Истина;
	Иначе
		ПредыдущиеЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.Ссылка, "Наименование, ПометкаУдаления, Родитель, Недействительна, Состав");
		
		Если ПредыдущиеЗначенияРеквизитов.Родитель <> Объект.Родитель
				Или Объект.Ссылка = РуководителиПодразделений Тогда
			
			ПараметрыОбновленияАдреснойКниги.ОбновитьДанныеОбъекта = Истина;
		КонецЕсли;
		
		Если ПредыдущиеЗначенияРеквизитов.Наименование <> Объект.Наименование Тогда
			ПараметрыОбновленияАдреснойКниги.ОбновитьДанныеОбъекта = Истина;
			ПараметрыОбновленияАдреснойКниги.ОбновитьСловаПоискаПоОбъекту = Истина;
			ПараметрыОбновленияАдреснойКниги.ОбновитьДанныеОтображенияОбъекта = Истина;
		КонецЕсли;
	
		Если ПредыдущиеЗначенияРеквизитов.ПометкаУдаления <> Объект.ПометкаУдаления
				Или ПредыдущиеЗначенияРеквизитов.Недействительна <> Объект.Недействительна Тогда
			
			ПараметрыОбновленияАдреснойКниги.ОбновитьДанныеОбъекта = Истина;
			ПараметрыОбновленияАдреснойКниги.ОбновитьДанныеОтображенияОбъекта = Истина;
			ПараметрыОбновленияАдреснойКниги.ОбновитьДоступностьВПоискеПоОбъекту = Истина;
			ПараметрыОбновленияАдреснойКниги.ОбновитьСоставГруппы = Истина;
		КонецЕсли;
		
		ТекущийСостав = Объект.Состав.ВыгрузитьКолонку("Участник");
		СтарыйСостав = ПредыдущиеЗначенияРеквизитов.Состав.Выгрузить().ВыгрузитьКолонку("Участник");
		
		Для Каждого Участник ИЗ ТекущийСостав Цикл
			Если СтарыйСостав.Найти(Участник) = Неопределено Тогда
				ПараметрыОбновленияАдреснойКниги.ОбновитьСловаПоискаПоОбъекту = Истина;
				ПараметрыОбновленияАдреснойКниги.ОбновитьСоставГруппы = Истина;
				
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Участник ИЗ СтарыйСостав Цикл
			Если ТекущийСостав.Найти(Участник) = Неопределено Тогда
				ПараметрыОбновленияАдреснойКниги.ОбновитьСловаПоискаПоОбъекту = Истина;
				ПараметрыОбновленияАдреснойКниги.ОбновитьСоставГруппы = Истина;
				
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПараметрыОбновленияАдреснойКниги;

КонецФункции

// Обновляет адресную книгу, согласно установленным параметрам.
//
// Параметры:
//	Объект - СправочникОбъект.РабочиеГруппы - Объект, по данным которого необходимо обновить адресной книги.
//	ПараметрыОбновления - Структура Из КлючИЗначение - см. ПараметрыОбновленияАдреснойКниги.
//
Процедура ОбновитьАдреснуюКнигу(Объект, ПараметрыОбновления) Экспорт
	
	Если ПараметрыОбновления.ОбновитьДанныеОбъекта Тогда
		РаботаСАдреснойКнигой.ОбновитьДанныеОбъекта(
			Объект.Ссылка, Объект.Родитель, Справочники.АдреснаяКнига.ПоРабочимГруппам);
	КонецЕсли;
		
	Если ПараметрыОбновления.ОбновитьДанныеОтображенияОбъекта Тогда
		РаботаСАдреснойКнигой.ОбновитьДанныеОтображенияОбъекта(Объект.Ссылка);
	КонецЕсли;
	
	Если ПараметрыОбновления.ОбновитьСловаПоискаПоОбъекту Тогда
		РаботаСАдреснойКнигой.ОбновитьСловаПоискаПоОбъекту(Объект);
	КонецЕсли;
	
	Если ПараметрыОбновления.ОбновитьДоступностьВПоискеПоОбъекту Тогда
		РаботаСАдреснойКнигой.ОбновитьДоступностьВПоиске(Объект.Ссылка);
	КонецЕсли;
	
	Если ПараметрыОбновления.ОбновитьСоставГруппы Тогда
		ТекущийСостав = Объект.Состав.Выгрузить().ВыгрузитьКолонку("Участник");
		Справочники.АдреснаяКнига.РасширитьСписокСотрудниковРолями(ТекущийСостав);
		
		РаботаСАдреснойКнигой.ОбновитьСписокПодчиненныхОбъектов(
			Объект.Ссылка, Объект.Родитель, Справочники.АдреснаяКнига.ПоРабочимГруппам, ТекущийСостав);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти


#КонецЕсли
