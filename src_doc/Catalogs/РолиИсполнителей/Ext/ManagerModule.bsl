#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает структуру полей Роли
//
// Возвращаемое значение:
//   Структура
//     Наименование
//     КраткоеПредставление
//     ВнешняяРоль
//     ИспользуетсяБезОбъектовАдресации
//     ИспользуетсяСОбъектамиАдресации
//     ТипыОсновногоОбъектаАдресации
//     ТипыДополнительногоОбъектаАдресации
//     Комментарий
//
Функция ПолучитьСтруктуруРолиИсполнителей() Экспорт
	
	ПараметрыРолиИсполнителей = Новый Структура;
	ПараметрыРолиИсполнителей.Вставить("Код");
	ПараметрыРолиИсполнителей.Вставить("Наименование");
	ПараметрыРолиИсполнителей.Вставить("КраткоеПредставление");
	ПараметрыРолиИсполнителей.Вставить("ВнешняяРоль");
	ПараметрыРолиИсполнителей.Вставить("ИспользуетсяБезОбъектовАдресации");
	ПараметрыРолиИсполнителей.Вставить("ИспользуетсяСОбъектамиАдресации");
	ПараметрыРолиИсполнителей.Вставить("ТипыОсновногоОбъектаАдресации");
	ПараметрыРолиИсполнителей.Вставить("ТипыДополнительногоОбъектаАдресации");
	ПараметрыРолиИсполнителей.Вставить("Комментарий");

	Возврат ПараметрыРолиИсполнителей;
	
КонецФункции

// Создает и запсывает в БД роль
//
// Параметры:
//   СтруктураРоли - Структура - структура полей роли.
//
// Возвращаемое значение:
//   СправочникСсылка.РолиИсполнителей
//
Функция СоздатьРольИсполнителей(СтруктураРоли) Экспорт
	
	НоваяРоль = СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НоваяРоль, СтруктураРоли);
	НоваяРоль.Записать();
	
	Возврат НоваяРоль.Ссылка;
	
КонецФункции

// Возвращает отслеживаемые реквизиты для дополнительного свойства ПредыдущиеЗначенияРеквизитов.
//
// Возвращаемое значение:
//  Строка - Отслеживаемые реквизиты.
//
Функция ОтслеживаемыеРеквизиты() Экспорт
	
	ОтслеживаемыеРеквизиты = 
		"Наименование, ПометкаУдаления, ИспользуетсяБезОбъектовАдресации, ИспользуетсяСОбъектамиАдресации,
		|ТипыОсновногоОбъектаАдресации, ТипыДополнительногоОбъектаАдресации";
	
	Возврат ОтслеживаемыеРеквизиты;
	
КонецФункции

#Область ОбновлениеПодчиненныхПолныхРолей

// Возвращает признак того, что необходимо обновить подчиненные объекту полные роли.
//
// Параметры:
//	Объект - СправочникОбъект.РолиИсполнителей - Роль, которую необходимо проверить.
//
// Возвращаемое значение:
//	Булево - Признак необходимости обновления.
// 
Функция ТребуетсяОбновитьПодчиненныеРоли(Объект) Экспорт
	
	ПредыдущиеЗначенияРеквизитов =
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Объект.ДополнительныеСвойства, "ПредыдущиеЗначенияРеквизитов");
	
	Если ПредыдущиеЗначенияРеквизитов = Неопределено Тогда
		ПредыдущиеЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.Ссылка,
			"ПометкаУдаления, ИспользуетсяБезОбъектовАдресации, ИспользуетсяСОбъектамиАдресации,
			|ТипыОсновногоОбъектаАдресации, ТипыДополнительногоОбъектаАдресации");	
	КонецЕсли; 
	
	ОбновитьПодчиненныеПолныеРоли = 
		ПредыдущиеЗначенияРеквизитов.ПометкаУдаления <> Объект.ПометкаУдаления
			Или ПредыдущиеЗначенияРеквизитов.ИспользуетсяБезОбъектовАдресации <> Объект.ИспользуетсяБезОбъектовАдресации
			Или ПредыдущиеЗначенияРеквизитов.ИспользуетсяСОбъектамиАдресации <> Объект.ИспользуетсяСОбъектамиАдресации
			Или ПредыдущиеЗначенияРеквизитов.ТипыОсновногоОбъектаАдресации <> Объект.ТипыОсновногоОбъектаАдресации
			Или ПредыдущиеЗначенияРеквизитов.ТипыДополнительногоОбъектаАдресации <> Объект.ТипыДополнительногоОбъектаАдресации;
	
	Возврат ОбновитьПодчиненныеПолныеРоли;
	
КонецФункции

// Выполняет обновление подчиненных полных ролей.
//
// Параметры:
//	Объект - СправочникОбъект.РолиИсполнителей - Роль, для которой необходимо обновить подчиненные полные роли.
// 
Процедура ОбновитьПодчиненныеПолныеРоли(Объект) Экспорт
		
	Справочники.ПолныеРоли.ОбновитьПометкуУдаленияСогласноВладельцу(
		Объект.Ссылка,
		Объект.ПометкаУдаления,
		Объект.ИспользуетсяБезОбъектовАдресации,
		Объект.ИспользуетсяСОбъектамиАдресации,
		Объект.ТипыОсновногоОбъектаАдресации,
		Объект.ТипыДополнительногоОбъектаАдресации);
		
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеАдреснойКниги

// Конструктор параметров обновления адресной книги.
//
// Возвращаемое значение:
//	Структура:
//		* ОбновитьДанныеОбъекта - Булево - Признак обновления данных по объекту.
//		* ОбновитьДанныеОтображенияОбъекта - Булево - Признак обновления данных отображения.
//		* ОбновитьСловаПоискаПоОбъекту - Булево - Признак обновления слов поиска по объекту.
//		* ОбновитьДоступностьВПоискеПоОбъекту - Булево - Признак обновления доступности в результатах поиска.
//		* ОбновитьГруппуВсеПользователи - Булево - Признак обновления группы "Все пользователи".
//
Функция ПараметрыОбновленияАдреснойКниги() Экспорт
	
	ПараметрыОбновленияАдреснойКниги = Новый Структура;
	ПараметрыОбновленияАдреснойКниги.Вставить("ОбновитьДанныеОбъекта", Ложь);
	ПараметрыОбновленияАдреснойКниги.Вставить("ОбновитьДанныеОтображенияОбъекта", Ложь);
	ПараметрыОбновленияАдреснойКниги.Вставить("ОбновитьСловаПоискаПоОбъекту", Ложь);
	ПараметрыОбновленияАдреснойКниги.Вставить("ОбновитьДоступностьВПоискеПоОбъекту", Ложь);
	ПараметрыОбновленияАдреснойКниги.Вставить("ОбновитьГруппуВсеПользователи", Ложь);
	
	Возврат ПараметрыОбновленияАдреснойКниги;
	
КонецФункции

// Устанавливает значения параметров обновления адресной книги по данным объекта.
//
// Параметры:
//	Объект - СправочникОбъект.АвтоподстановкиДляПроцессов - Объект, для которго необходимо определить параметры обновления.
//
// Возвращаемое значение:
//	Структура: см. ПараметрыОбновленияАдреснойКниги.
//
Функция ЗначенияПараметровОбновленияАдреснойКнигиПоОбъекту(Объект) Экспорт
	
	ПараметрыОбновленияАдреснойКниги = ПараметрыОбновленияАдреснойКниги();
		
	Если Не РаботаСАдреснойКнигой.ТребуетсяОбновлениеАдреснойКниги(Объект) Тогда
		Возврат ПараметрыОбновленияАдреснойКниги; 
	КонецЕсли;

	Если Объект.ЭтоНовый() Тогда
		ПараметрыОбновленияАдреснойКниги.ОбновитьДанныеОбъекта = Истина;
		ПараметрыОбновленияАдреснойКниги.ОбновитьСловаПоискаПоОбъекту = Истина;
		ПараметрыОбновленияАдреснойКниги.ОбновитьГруппуВсеПользователи = Истина;
	Иначе
		ПредыдущиеЗначенияРеквизитов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			Объект.ДополнительныеСвойства, "ПредыдущиеЗначенияРеквизитов");
		
		Если ПредыдущиеЗначенияРеквизитов = Неопределено Тогда
			ПредыдущиеЗначенияРеквизитов =
				ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Ссылка, ОтслеживаемыеРеквизиты());	
		КонецЕсли; 
		
		Если ПредыдущиеЗначенияРеквизитов.ПометкаУдаления <> Объект.ПометкаУдаления Тогда
			ПараметрыОбновленияАдреснойКниги.ОбновитьДанныеОбъекта = Истина;
			ПараметрыОбновленияАдреснойКниги.ОбновитьДанныеОтображенияОбъекта = Истина;
			ПараметрыОбновленияАдреснойКниги.ОбновитьДоступностьВПоискеПоОбъекту = Истина;
		КонецЕсли;
		
		Если ПредыдущиеЗначенияРеквизитов.Наименование <> Объект.Наименование Тогда
			ПараметрыОбновленияАдреснойКниги.ОбновитьДанныеОбъекта = Истина;
			ПараметрыОбновленияАдреснойКниги.ОбновитьСловаПоискаПоОбъекту = Истина;
			ПараметрыОбновленияАдреснойКниги.ОбновитьДанныеОтображенияОбъекта = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПараметрыОбновленияАдреснойКниги;

КонецФункции

// Обновляет адресную книгу, согласно установленным параметрам.
//
// Параметры:
//	Объект - СправочникОбъект.РолиИсполнителей - Объект, по данным которого необходимо обновить адресной книги.
//	ПараметрыОбновления - Структура Из КлючИЗначение - см. ПараметрыОбновленияАдреснойКниги.
//
Процедура ОбновитьАдреснуюКнигу(Объект, ПараметрыОбновления) Экспорт
	
	Если ПараметрыОбновления.ОбновитьДанныеОбъекта Тогда
		РаботаСАдреснойКнигой.ОбновитьДанныеОбъекта(Объект.Ссылка, Неопределено, Справочники.АдреснаяКнига.Роли);
	КонецЕсли;
	
	Если ПараметрыОбновления.ОбновитьДанныеОтображенияОбъекта Тогда
		РаботаСАдреснойКнигой.ОбновитьДанныеОтображенияОбъекта(Объект.Ссылка);
	КонецЕсли;
	
	Если ПараметрыОбновления.ОбновитьГруппуВсеПользователи Тогда
		СоставГруппыВсеСотрудники = Сотрудники.ВсеСотрудники();
		Справочники.АдреснаяКнига.РасширитьСписокСотрудниковРолями(СоставГруппыВсеСотрудники, Ложь);
		
		РаботаСАдреснойКнигой.ОбновитьСписокПодчиненныхОбъектов(
			Справочники.РабочиеГруппы.ВсеПользователи,
			Неопределено,
			Справочники.АдреснаяКнига.ПоРабочимГруппам,
			СоставГруппыВсеСотрудники);
	КонецЕсли;
	
	Если ПараметрыОбновления.ОбновитьСловаПоискаПоОбъекту Тогда
		РаботаСАдреснойКнигой.ОбновитьСловаПоискаПоОбъекту(Объект);
	КонецЕсли;
	
	Если ПараметрыОбновления.ОбновитьДоступностьВПоискеПоОбъекту Тогда
		РаботаСАдреснойКнигой.ОбновитьДоступностьВПоиске(Объект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

#Область АдреснаяКнига

// Вернет Истина, у этого модуля менеджера есть функция ПолучитьАдресФото.
// 
// Возвращаемое значение:
//  Булево.
// 
Функция ЕстьФункцияПолученияФото() Экспорт
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
    МультиязычностьКлиентСервер.ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка);
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
    МультиязычностьКлиентСервер.ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка);
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
    МультиязычностьСервер.ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка, Метаданные.Справочники.РолиИсполнителей);
КонецПроцедуры

#КонецОбласти


#КонецЕсли