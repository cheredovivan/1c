
#Область ОбработчикиСобытий

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтавкиНДС.Ссылка КАК Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(СтавкиНДС.Ссылка) КАК Представление
		|ИЗ
		|	Справочник.СтавкиНДС КАК СтавкиНДС
		|ГДЕ
		|	&ОтборДата
		|	И Не СтавкиНДС.ПометкаУдаления
		|	И Не СтавкиНДС.РасчетнаяСтавка
		// Локализация
		|И (СтавкиНДС.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
		|	ИЛИ СтавкиНДС.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка))
		// Локализация Конец
		|";
	
	Если Параметры.Свойство("Дата") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборДата",
			"(СтавкиНДС.НачалоПериода <= &Дата
			|	ИЛИ СтавкиНДС.НачалоПериода = ДАТАВРЕМЯ(1, 1, 1))
			|	И (КОНЕЦПЕРИОДА(СтавкиНДС.КонецПериода, ДЕНЬ) >= &Дата
			|	ИЛИ СтавкиНДС.КонецПериода = ДАТАВРЕМЯ(1, 1, 1))");
		Запрос.УстановитьПараметр("Дата", Параметры.Дата);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборДата", "ИСТИНА");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Пока Выборка.Следующий() Цикл
		Если Не ЗначениеЗаполнено(Параметры.СтрокаПоиска)
			Или СтрНайти(НРег(Выборка.Представление), НРег(Параметры.СтрокаПоиска)) = 1 Тогда
			ДанныеВыбора.Добавить(Выборка.Ссылка, Выборка.Представление);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьКлиентСервер.ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьКлиентСервер.ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьЭлементыПервоначальногоЗаполнения() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СтавкиНДС.БезНДС") = Неопределено Тогда
		СтавкаБезНДС = СоздатьЭлемент();
		СсылкаНаЭлемент = ПолучитьСсылку(Новый УникальныйИдентификатор("de2d6c84-b14a-11ef-9f32-50ebf63e555a"));
		СтавкаБезНДС.УстановитьСсылкуНового(СсылкаНаЭлемент);
		СтавкаБезНДС.Наименование = НСтр("ru = 'Без НДС'");
		СтавкаБезНДС.ИмяПредопределенныхДанных = "БезНДС";
		СтавкаБезНДС.Ставка = 0;
		ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные(СтавкаБезНДС);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СтавкиНДС.НДС0") = Неопределено Тогда
		СтавкаНДС0 = СоздатьЭлемент();
		СсылкаНаЭлемент = ПолучитьСсылку(Новый УникальныйИдентификатор("8bc86906-5a83-4b87-a741-3ce6d385400b"));
		СтавкаНДС0.УстановитьСсылкуНового(СсылкаНаЭлемент);
		СтавкаНДС0.Наименование = НСтр("ru = '0%'");
		СтавкаНДС0.ИмяПредопределенныхДанных = "НДС0";
		СтавкаНДС0.Ставка = 0;
		// Локализация
		СтавкаНДС0.Страна = Справочники.СтраныМира.Россия;
		// Локализация Конец
		ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные(СтавкаНДС0);
	КонецЕсли;
	
	УчетНДСЛокализация.ЗаполнитьСправочникСтавкиНДС();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

