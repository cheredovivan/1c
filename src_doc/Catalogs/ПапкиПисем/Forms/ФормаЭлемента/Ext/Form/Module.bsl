#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.ВидПапки = Перечисления.ВидыПапокПисем.Общая;
		Объект.Ответственный = 
			Сотрудники.ОсновнойСотрудник();
	КонецЕсли;
	
	Если РегистрыСведений.ПапкиПисемБыстрогоДоступа.ЭтоПапкаПисемБыстрогоДоступа(Объект.Ссылка) Тогда
		ВключитьВМоиПапки = Истина;
	Иначе
		ВключитьВМоиПапки = Ложь;
	КонецЕсли;
	
	Элементы.ГруппаОчисткаПапки.Видимость =
		Объект.ВидПапки = Перечисления.ВидыПапокПисем.Общая 
		Или Объект.ВидПапки = Перечисления.ВидыПапокПисем.Корзина;
		
	Если ЗначениеЗаполнено(Объект.Ссылка) И Элементы.ГруппаОчисткаПапки.Видимость Тогда
		
		ПериодУстаревания = РегистрыСведений.НастройкиАвтоочисткиПапокПисем.ПолучитьПериодУстареванияПисем(Объект.Ссылка);
		Если ПериодУстаревания <> Неопределено Тогда
			ВключитьАвтоОчисткуПапки = Истина;
			ПериодУстареванияПисем = ПериодУстаревания;
		КонецЕсли;
		Элементы.ПериодУстареванияПисем.АвтоОтметкаНезаполненного = ВключитьАвтоОчисткуПапки;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("ЭтоНовыйОбъект") И ПараметрыЗаписи.ЭтоНовыйОбъект = Истина
		И ВключитьВМоиПапки Тогда	
		
		РегистрыСведений.ПапкиПисемБыстрогоДоступа.УстановитьБыстрыйДоступДляПапки(ТекущийОбъект.Ссылка, ВключитьВМоиПапки);
		
	КонецЕсли;
	
	РегистрыСведений.НастройкиАвтоочисткиПапокПисем.УстановитьАвтоОчисткуПапки(
		ТекущийОбъект.Ссылка, ПериодУстареванияПисем, ВключитьАвтоОчисткуПапки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ПапкаПисемСохранена", Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтветственныйОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	СотрудникиКлиент.СотрудникОбработкаВыбора(Объект, "Ответственный", ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры 

&НаКлиенте
Процедура ВключитьАвтоОчисткуПапкиПриИзменении(Элемент)
	
	Модифицированность = Истина;
	Элементы.ПериодУстареванияПисем.АвтоОтметкаНезаполненного = ВключитьАвтоОчисткуПапки;
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьВМоиПапкиПриИзменении(Элемент)
	
	ВключитьВМоиПапкиПриИзмененииСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы       

&НаКлиенте
Процедура ПересчитатьСчетчикЧислаПисем(Команда)
	
	Состояние(НСтр("ru='Выполняется пересчет...'"));
	ПересчитатьСчетчикЧислаПисемНаСервере();
	Состояние();
	
	ПоказатьПредупреждение(,НСтр("ru = 'Счетчик обновлен.'"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НастройкаПрав(Команда)
	
	ИмяОсновногоРеквизита = "Объект";
	ИмяПодчиненнойФормы = "ОбщаяФорма.НастройкиПравПапок";
	
	ТекстВопроса = НСтр("ru = 'Данные еще не записаны.
		|Выполнение команды ""Настройка прав"" возможно только после записи данных.
		|Данные будут записаны.'");
	
	ДокументооборотПраваДоступаКлиент.ЗаписатьОбъектЕслиНовыйИОткрытьПодчиненнуюФорму(
		ЭтаФорма, ИмяОсновногоРеквизита, ТекстВопроса, ИмяПодчиненнойФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваДоступа(Команда)
	
	ДокументооборотПраваДоступаКлиент.ОткрытьФормуПравДоступа(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ВключитьВМоиПапкиПриИзмененииСервер()
	
	РегистрыСведений.ПапкиПисемБыстрогоДоступа.УстановитьБыстрыйДоступДляПапки(Объект.Ссылка, ВключитьВМоиПапки);
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьСчетчикЧислаПисемНаСервере()
	
	РегистрыСведений.КоличествоПисемВПапкахИтоги.Заполнить(Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти
