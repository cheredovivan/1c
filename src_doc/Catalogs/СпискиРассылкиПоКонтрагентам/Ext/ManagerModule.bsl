#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Возвращает наименования реквизитов, необходимых для определения прав доступа.
// 
// Возвращаемое значение:
//  Строка -
Функция ПолучитьПоляДоступа() Экспорт
	
	Возврат 
		"Ссылка,
		|ГруппаДоступа,
		|Получатели";
	
КонецФункции

// Заполняет переданный дескриптор доступа
// 
Процедура ЗаполнитьОсновнойДескриптор(ОбъектДоступа, ДескрипторДоступа) Экспорт
	
	ДобавленныеГруппы = Новый Соответствие;
	Если ЗначениеЗаполнено(ОбъектДоступа.ГруппаДоступа) Тогда
		Строка = ДескрипторДоступа.Контрагенты.Добавить();
		Строка.ГруппаДоступа = ОбъектДоступа.ГруппаДоступа;
		ДобавленныеГруппы.Вставить(ОбъектДоступа.ГруппаДоступа, Истина);
	КонецЕсли;
	
	// Контрагенты
	ВсеКонтрагенты = ОбъектДоступа.Получатели.Выгрузить().ВыгрузитьКолонку("Получатель");
	ЗначенияГруппыДоступа = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ВсеКонтрагенты, "ГруппаДоступа");
	Для Каждого КлючИЗначение Из ЗначенияГруппыДоступа Цикл
		ГруппаДоступа = КлючИЗначение.Значение;
		Если ЗначениеЗаполнено(ГруппаДоступа) И ДобавленныеГруппы.Получить(ГруппаДоступа) = Неопределено Тогда
			Строка = ДескрипторДоступа.Контрагенты.Добавить();
			Строка.ГруппаДоступа = ГруппаДоступа;
			ДобавленныеГруппы.Вставить(ГруппаДоступа, Истина);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает признак того, что менеджер содержит метод ЗапросДляРасчетаПрав()
// 
Функция ЕстьМетодЗапросДляРасчетаПрав() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает запрос для расчета прав доступа по дескрипторам объекта.
// 
// Параметры:
//  Дескрипторы - Массив Из СправочникСсылка.ДескрипторыДоступаОбъектов - Дескрипторы, чьи права нужно рассчитать.
//  ИдОбъекта - СправочникСсылка.ИдентификаторыОбъектовМетаданных - Устарел, будет удален.
//  МенеджерОбъектаДоступа - СправочникМенеджер, ДокументМенеджер - Устарел, будет удален.
// 
// Возвращаемое значение:
//	Запрос - Который выберет права доступа для переданного массива дескрипторов.
Функция ЗапросДляРасчетаПрав(Дескрипторы, ИдОбъекта, МенеджерОбъектаДоступа) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СотрудникиВКонтейнерах.Сотрудник КАК Сотрудник,
		|	ИСТИНА КАК Чтение,
		|	МАКСИМУМ(ПраваРолей.Добавление) КАК Добавление,
		|	МАКСИМУМ(ПраваРолей.Изменение) КАК Изменение,
		|	МАКСИМУМ(ПраваРолей.Изменение) КАК Удаление,
		|	МАКСИМУМ(ПраваРолей.ЧтениеБезОграничения) КАК ЧтениеБезОграничения,
		|	МАКСИМУМ(ПраваРолей.ДобавлениеБезОграничения) КАК ДобавлениеБезОграничения,
		|	МАКСИМУМ(ПраваРолей.ИзменениеБезОграничения) КАК ИзменениеБезОграничения
		|ПОМЕСТИТЬ ПраваПоРолям
		|ИЗ
		|	РегистрСведений.ПолномочияСотрудников КАК ПолномочияСотрудников
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.ПрофилиГруппДоступа.Роли КАК ПрофилиГруппДоступаРоли
		|		ПО ПолномочияСотрудников.Полномочия = ПрофилиГруппДоступаРоли.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ПраваРолей КАК ПраваРолей
		|		ПО ПрофилиГруппДоступаРоли.Роль = ПраваРолей.Роль
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
		|		ПО ПолномочияСотрудников.Владелец = СотрудникиВКонтейнерах.Контейнер
		|ГДЕ
		|	ПраваРолей.ОбъектМетаданных = &Идентификатор
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудникиВКонтейнерах.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДескрипторыДоступаОбъектов.Ссылка КАК Дескриптор,
		|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДескрипторыДоступаОбъектовКонтрагенты.ГруппаДоступа), 0) КАК КолГруппДоступа
		|ПОМЕСТИТЬ КолГруппДескрипторов
		|ИЗ
		|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДескрипторыДоступаОбъектов.Контрагенты КАК ДескрипторыДоступаОбъектовКонтрагенты
		|		ПО ДескрипторыДоступаОбъектов.Ссылка = ДескрипторыДоступаОбъектовКонтрагенты.Ссылка
		|ГДЕ
		|	ДескрипторыДоступаОбъектов.Ссылка В(&Дескрипторы)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДескрипторыДоступаОбъектов.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Дескриптор КАК Дескриптор,
		|	ВложенныйЗапрос.Сотрудник КАК Сотрудник,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВложенныйЗапрос.ГруппаДоступа) КАК КолГруппДоступа
		|ПОМЕСТИТЬ КолРазрешенныхГруппПоСотрудникам
		|ИЗ
		|(
		|	ВЫБРАТЬ
		|		ДескрипторыДоступаОбъектовКонтрагенты.Ссылка КАК Дескриптор,
		|		СотрудникиВКонтейнерах.Сотрудник КАК Сотрудник,
		|		ДескрипторыДоступаОбъектовКонтрагенты.ГруппаДоступа КАК ГруппаДоступа
		|	ИЗ
		|		Справочник.ДескрипторыДоступаОбъектов.Контрагенты КАК ДескрипторыДоступаОбъектовКонтрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Справочник.ГруппыДоступаКонтрагентов.Доступ КАК ГруппыДоступаКонтрагентовДоступ
		|			ПО ДескрипторыДоступаОбъектовКонтрагенты.ГруппаДоступа = ГруппыДоступаКонтрагентовДоступ.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
		|			ПО ГруппыДоступаКонтрагентовДоступ.Участник = СотрудникиВКонтейнерах.Контейнер
		|	ГДЕ
		|		ДескрипторыДоступаОбъектовКонтрагенты.Ссылка В(&Дескрипторы)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДескрипторыДоступаОбъектовКонтрагенты.Ссылка,
		|		СотрудникиВКонтейнерах.Сотрудник,
		|		ДескрипторыДоступаОбъектовКонтрагенты.ГруппаДоступа
		|	ИЗ
		|		Справочник.ДескрипторыДоступаОбъектов.Контрагенты КАК ДескрипторыДоступаОбъектовКонтрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		РегистрСведений.РазрешенияДляРазрезовДоступа КАК РазрешенияДляРазрезовДоступа
		|			ПО ДескрипторыДоступаОбъектовКонтрагенты.ГруппаДоступа = РазрешенияДляРазрезовДоступа.РазрезДоступа
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Справочник.КоллекцииЗначенийДоступа.ЗначенияДоступа КАК КоллекцииЗначенийДоступаЗначенияДоступа
		|			ПО РазрешенияДляРазрезовДоступа.Разрешение = КоллекцииЗначенийДоступаЗначенияДоступа.Значение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		РегистрСведений.РазрешенияДляЛокальныхАдминистраторов КАК РазрешенияДляЛокальныхАдминистраторов
		|			ПО КоллекцииЗначенийДоступаЗначенияДоступа.Ссылка = РазрешенияДляЛокальныхАдминистраторов.КоллекцияЗначенийДоступа
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
		|			ПО РазрешенияДляЛокальныхАдминистраторов.Пользователь = СотрудникиВКонтейнерах.Контейнер
		|	ГДЕ
		|		ДескрипторыДоступаОбъектовКонтрагенты.Ссылка В(&Дескрипторы)
		|) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Дескриптор,
		|	ВложенныйЗапрос.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Дескриптор КАК Дескриптор,
		|	ВложенныйЗапрос.Сотрудник КАК Сотрудник,
		|	МАКСИМУМ(ВложенныйЗапрос.Чтение) КАК Чтение,
		|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Изменение,
		|	МАКСИМУМ(ВложенныйЗапрос.Добавление) КАК Добавление,
		|	МАКСИМУМ(ВложенныйЗапрос.Удаление) КАК Удаление,
		|	МАКСИМУМ(ВложенныйЗапрос.УправлениеПравами) КАК УправлениеПравами
		|ПОМЕСТИТЬ НовыеПрава
		|ИЗ
		|(
		|	ВЫБРАТЬ
		|		КолГруппДескрипторов.Дескриптор КАК Дескриптор,
		|		ПраваПоРолям.Сотрудник КАК Сотрудник,
		|		ПраваПоРолям.Чтение КАК Чтение,
		|		ПраваПоРолям.Изменение КАК Изменение,
		|		ПраваПоРолям.Добавление КАК Добавление,
		|		ПраваПоРолям.Удаление КАК Удаление,
		|		ЛОЖЬ КАК УправлениеПравами
		|	ИЗ
		|		КолГруппДескрипторов КАК КолГруппДескрипторов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ КолРазрешенныхГруппПоСотрудникам КАК КолРазрешенныхГруппПоСотрудникам
		|			ПО КолГруппДескрипторов.Дескриптор = КолРазрешенныхГруппПоСотрудникам.Дескриптор
		|				И КолГруппДескрипторов.КолГруппДоступа = КолРазрешенныхГруппПоСотрудникам.КолГруппДоступа
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПраваПоРолям КАК ПраваПоРолям
		|			ПО КолРазрешенныхГруппПоСотрудникам.Сотрудник = ПраваПоРолям.Сотрудник
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		КолГруппДескрипторов.Дескриптор,
		|		ПраваПоРолям.Сотрудник,
		|		ПраваПоРолям.Чтение,
		|		ПраваПоРолям.Изменение,
		|		ПраваПоРолям.Добавление,
		|		ПраваПоРолям.Удаление,
		|		ЛОЖЬ
		|	ИЗ
		|		КолГруппДескрипторов КАК КолГруппДескрипторов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПраваПоРолям КАК ПраваПоРолям
		|			ПО ИСТИНА
		|	ГДЕ
		|		КолГруппДескрипторов.КолГруппДоступа = 0
		|) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Дескриптор,
		|	ВложенныйЗапрос.Сотрудник");
	
	ДокументооборотПраваДоступа.ДополнитьТекстЗапросаПоПравам(Запрос.Текст);
	
	Возврат Запрос;
	
КонецФункции

// Заполняет протокол расчета прав дескрипторов
// 
// Параметры:
//  
//  ПротоколРасчетаПрав - Массив - протокол для заполнения
//  ЗапросПоПравам - Запрос - запрос, который использовался для расчета прав дескрипторов
//  Дескрипторы - Массив - массив дескрипторов, чьи права были рассчитаны
//  
Процедура ЗаполнитьПротоколРасчетаПрав(ПротоколРасчетаПрав, ЗапросПоПравам) Экспорт
	
	Справочники.ДескрипторыДоступаОбъектов.ЗаполнитьПротоколРасчетаПравСтандартно(
		ПротоколРасчетаПрав, ЗапросПоПравам);
	
КонецПроцедуры

#КонецОбласти // ДляВызоваИзДругихПодсистем

#КонецОбласти

#КонецЕсли
