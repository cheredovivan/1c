
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПротоколированиеРаботыСотрудников.ЗаписатьОткрытие(Объект.Ссылка);
	ВидКонтрагентовФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ОтборСтрок = Новый Структура("Получатель");
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		
		Для Каждого Элемент Из ВыбранноеЗначение Цикл
			
			ОтборСтрок.Получатель = Элемент;
			НайденныеСтроки = Объект.Получатели.НайтиСтроки(ОтборСтрок);
			Если НайденныеСтроки.Количество() <> 0 Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = Объект.Получатели.Добавить();
			НоваяСтрока.Получатель = Элемент;
			
		КонецЦикла;
		
	Иначе
		
		ОтборСтрок.Получатель = ВыбранноеЗначение;
		НайденныеСтроки = Объект.Получатели.НайтиСтроки(ОтборСтрок);
		Если НайденныеСтроки.Количество() <> 0 Тогда
			Возврат;
		КонецЕсли;
		
		НоваяСтрока = Объект.Получатели.Добавить();
		НоваяСтрока.Получатель = ВыбранноеЗначение;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПротоколированиеРаботыСотрудников.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыСотрудников.ЗаписатьИзменение(Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПолучатели

&НаКлиенте
Процедура ПолучательПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Получатели.ТекущиеДанные;
	ЮрФизЛицо = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
		ТекущиеДанные.Получатель, "ЮрФизЛицо");
	
	Если ЮрФизЛицо = ВидКонтрагентовФизЛицо Тогда
		Элементы.Адресат.ТолькоПросмотр = Истина;
	Иначе
		Элементы.Адресат.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	ТекущийПолучатель = ТекущиеДанные.Получатель;
	Если ЗначениеЗаполнено(ТекущийПолучатель) И ЮрФизЛицо = ВидКонтрагентовФизЛицо Тогда
		
		Для Каждого Строка Из Объект.Получатели Цикл
			Если (Строка.Получатель = ТекущийПолучатель) И (Строка.НомерСтроки <> ТекущиеДанные.НомерСтроки) Тогда
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Получатель ""%1"" уже выбран.'"),
					ТекущийПолучатель);
				ПоказатьПредупреждение(, ТекстПредупреждения);
				ТекущиеДанные.Получатель = Неопределено;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Получатель) Тогда
		
		ТекущиеДанные.Адресат = РаботаСКонтрагентамиВызовСервера.ОсновноеКонтактноеЛицоКонтрагента(
			ТекущиеДанные.Получатель);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресатПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Получатели.ТекущиеДанные;
	
	ТекущийПолучатель = ТекущиеДанные.Получатель;
	ТекущийАдресат = ТекущиеДанные.Адресат;
	Если ЗначениеЗаполнено(ТекущийАдресат) И ТипЗнч(ТекущийПолучатель) = Тип("СправочникСсылка.Контрагенты") Тогда
		
		Для Каждого Строка Из Объект.Получатели Цикл
			Если (Строка.Получатель = ТекущийПолучатель) И (Строка.Адресат = ТекущийАдресат) И (Строка.НомерСтроки <> ТекущиеДанные.НомерСтроки) Тогда 
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Адресат ""%1"" получателя ""%2"" уже выбран.'"),
					ТекущийАдресат,
					ТекущийПолучатель);
				ПоказатьПредупреждение(, ТекстПредупреждения);
				ТекущиеДанные.Адресат = Неопределено;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Получатели.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	ЮрФизЛицо = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
		ТекущиеДанные.Получатель, "ЮрФизЛицо");
	
	Если ЮрФизЛицо = ВидКонтрагентовФизЛицо Тогда
		Элементы.Адресат.ТолькоПросмотр = Истина;
	Иначе
		Элементы.Адресат.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подобрать(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	
	КлючеваяОперация = РаботаСКонтрагентамиДокументооборотКлиентСервер.КлючеваяОперацияОткрытиеФормыВыбораКонтрагента();
	ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
	
	ОткрытьФорму("Справочник.Контрагенты.ФормаВыбора", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваДоступа(Команда)
	
	ДокументооборотПраваДоступаКлиент.ОткрытьФормуПравДоступа(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти
