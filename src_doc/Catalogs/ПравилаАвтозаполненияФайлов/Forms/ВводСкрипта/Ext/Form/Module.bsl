
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Скрипт", Скрипт);
	
	// Локализация
	Если Параметры.Свойство("ИспользоватьСклонения") И Параметры.ИспользоватьСклонения Тогда 
		Элементы.ФормаДобавитьСклонение.Видимость = Истина;
	Иначе 
		Элементы.ФормаДобавитьСклонение.Видимость = Ложь;
	КонецЕсли;
	// Локализация Конец
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(Скрипт);
	
КонецПроцедуры

// ++ Локализация
&НаКлиенте
Процедура ДобавитьСклонение(Команда)
	
	Скрипт = Скрипт + "
		|// Представление - Строка, в которой содержится предсталвение объекта для склонения;
		|// Падеж - Число- падеж, в который необходимо просклонять представление объекта;
		|// Объект - ОбъектСклонения - Ссылка на объект, реквизит которого склоняется.
		|
		|Представление = СокрЛП(Файл.ВладелецФайла.Создал);
		|Падеж = 2;
		|Создал = Файл.ВладелецФайла.Создал;
		|РезультатОбработки = СклонениеПредставленийОбъектов.ПросклонятьПредставление(
		|	Представление, Падеж, Создал);";
	
КонецПроцедуры
// -- Локализация

&НаКлиенте
Процедура ДобавитьАлгоритм(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьАлгоритмЗавершение", ЭтотОбъект);
	ОткрытьФорму("Справочник.Алгоритмы.ФормаВыбора",, 
		ЭтаФорма, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьАлгоритмЗавершение(Результат, Параметры) Экспорт 
	
	Если Результат <> Неопределено Тогда
		
		КлючДляВызова = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			Результат, "КлючДляВызова");
			
		ТекстВставки = СтрШаблон(
			НСтр("ru = 'ПараметрыАлгоритма = Новый Структура; 		
			|// Укажите параметры для выбранного вами алгоритма.
			|//ПараметрыАлгоритма.Вставить(""Ваш параметр"", ВашеЗначениеПараметра);
			|РезультатОбработки = Справочники.Алгоритмы.Вызвать(""%1"", ПараметрыАлгоритма);'"), 
			КлючДляВызова);	
		Скрипт = Скрипт + Символы.ВК + ТекстВставки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
