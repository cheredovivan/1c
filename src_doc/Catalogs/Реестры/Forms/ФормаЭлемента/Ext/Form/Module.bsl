#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Протоколирование работы пользователей
	ПротоколированиеРаботыСотрудников.ЗаписатьОткрытие(Объект.Ссылка);
	
	ОбновитьКоличествоНаСервере();
	
	ОбновитьТаблицуВидовЭлементов();
	
	ЭтоФиксированныйРеестр = ЗначениеЗаполнено(Объект.Источник);
	Элементы.ДанныеРеестра.ТолькоПросмотр = ЭтоФиксированныйРеестр;
	Элементы.ГруппаВидыЭлементовРеестра.Видимость = Не ЭтоФиксированныйРеестр;
	Элементы.Источник.Видимость = ЭтоФиксированныйРеестр;
	
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбновитьКоличествоНаСервере();
	
	Для Каждого СтрокаТаблицы Из Объект.Доступ Цикл		
		СтрокаТаблицы.Недействителен = НедействительностьПользователя(СтрокаТаблицы.Контейнер);		
	КонецЦикла;
	
	ОбновитьТаблицуВидовЭлементов();
	
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	МультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// Протоколирование работы пользователей
	ПротоколированиеРаботыСотрудников.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыСотрудников.ЗаписатьИзменение(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ОбщегоНазначенияДокументооборотКлиент.УдалитьПустыеСтрокиТаблицы(
		Объект.ВидыЭлементовРеестра,
		"ВидЭлементовРеестра");
	ОбщегоНазначенияДокументооборотКлиент.УдалитьДублиСтрокТаблицы(
		Объект.ВидыЭлементовРеестра,
		"ВидЭлементовРеестра");
	ОбщегоНазначенияДокументооборотКлиент.УдалитьПустыеСтрокиТаблицы(
		Объект.Доступ,
		"Контейнер");
	ОбщегоНазначенияДокументооборотКлиент.УдалитьДублиСтрокТаблицы(
		Объект.Доступ,
		"Контейнер");
	
	ОбновитьКоличествоНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьПовторноИспользуемыеЗначения();
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДанныеРеестраПриИзменении(Элемент)
	
	ДанныеРеестраПриИзмененииНаСервере();
	
	ОбновитьКоличествоНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)
    МультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВидыЭлементовРеестра

&НаКлиенте
Процедура ВидыЭлементовРеестраПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ВидЭлементовРеестра) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ВидЭлементовРеестра = ВидЭлементаПустаяСсылка;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыЭлементовРеестраПриИзменении(Элемент)
	
	ОбновитьКоличествоНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДоступ

&НаКлиенте
Процедура ДоступПриАктивизацииСтроки(Элемент)
	
	ЗаполнитьЗначениеТипаКонтейнерДоступа();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если Не ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьКоличествоНаКлиенте();
		
КонецПроцедуры


&НаКлиенте
Процедура ДоступПриИзменении(Элемент)
	
	ОбновитьКоличествоНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступКонтейнерПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Доступ.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Недействителен = НедействительностьПользователя(
		ТекущиеДанные.Контейнер);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступКонтейнерОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СотрудникиКлиент.ОбработкаВыбораКонтейнера(
		Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступКонтейнерАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСРабочимиГруппамиКлиент.ДокументРабочаяГруппаУчастникАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступКонтейнерНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимРаботыФормы", 1);
	ПараметрыФормы.Вставить("УпрощенныйИнтерфейс", Истина);
	ПараметрыФормы.Вставить("ОтображатьСотрудников", Истина);
	ПараметрыФормы.Вставить("ОтображатьРоли", Истина);
	ПараметрыФормы.Вставить("ВыбиратьКонтейнерыПользователей", Истина);
	
	ТекущиеДанные = Элементы.Доступ.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.Контейнер) Тогда
		ПараметрыФормы.Вставить("ВыбранныеАдресаты", ТекущиеДанные.Контейнер);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ЗаголовокФормы", НСтр("ru = 'Выбор сотрудника'"));
	ПараметрыФормы.Вставить("ЗаголовокСпискаАдреснойКниги", НСтр("ru = 'Все сотрудники'"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВыбораДоступа", ЭтотОбъект);
	
	РаботаСАдреснойКнигойКлиент.ВыбратьАдресатов(ПараметрыФормы, Элементы.Доступ, ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьСписок(Команда)
	
	Если Модифицированность Тогда
		Если Не Записать() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	РаботаСРеестрамиКлиент.ОткрытьРеестр(Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДанныеРеестраПриИзмененииНаСервере()
	
	ОписаниеТиповВидаЭлементов = Перечисления.ДанныеРеестров.ОписаниеТиповВидаЭлементов(Объект.ДанныеРеестра);
	
	КоличествоЭлементов = Объект.ВидыЭлементовРеестра.Количество();
	Для Индекс = 1 По КоличествоЭлементов Цикл
		
		СтрокаТаблицы = Объект.ВидыЭлементовРеестра[КоличествоЭлементов - Индекс];
		
		ТипВидаЭлементов = ТипЗнч(СтрокаТаблицы.ВидЭлементовРеестра);
		Если ОписаниеТиповВидаЭлементов.СодержитТип(ТипВидаЭлементов) Тогда
			Продолжить;
		КонецЕсли;
		
		Объект.ВидыЭлементовРеестра.Удалить(СтрокаТаблицы);
		
	КонецЦикла;
	
	ОбновитьТаблицуВидовЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗначениеТипаКонтейнерДоступа()

	ТекущиеДанные = Элементы.Доступ.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Контейнер <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Контейнер = ПредопределенноеЗначение("Справочник.Сотрудники.ПустаяСсылка");
	
КонецПроцедуры

&НаСервереБезКонтекста 
Функция НедействительностьПользователя(ПользовательСсылка)
	
	НедействительностьПользователя = Ложь;
	
	Если Не ЗначениеЗаполнено(ПользовательСсылка) Тогда
		Возврат НедействительностьПользователя;
	КонецЕсли;
	
	Если ТипЗнч(ПользовательСсылка) = Тип("СправочникСсылка.Пользователи") Тогда
		
		НедействительностьПользователя =
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				ПользовательСсылка,
				"Недействителен");
		
	ИначеЕсли ТипЗнч(ПользовательСсылка) = Тип("СправочникСсылка.Сотрудники") Тогда
		
		НедействительностьПользователя =
			Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				ПользовательСсылка,
				"Действует");
		
	Иначе
		
		НедействительностьПользователя = Ложь;
		
	КонецЕсли;
	
	Возврат НедействительностьПользователя;
	
КонецФункции

&НаСервере
Процедура ОбновитьТаблицуВидовЭлементов()
	
	ОписаниеТиповВидаЭлементов = Перечисления.ДанныеРеестров.ОписаниеТиповВидаЭлементов(Объект.ДанныеРеестра);
	ВидЭлементаПустаяСсылка = ОписаниеТиповВидаЭлементов.ПривестиЗначение(Неопределено);
	
	Элементы.ГруппаВидыЭлементовРеестра.Заголовок =
		Перечисления.ДанныеРеестров.СинонимВидовЭлементов(Объект.ДанныеРеестра);
	Элементы.Источник.Заголовок =
		Перечисления.ДанныеРеестров.СинонимВидовЭлементов(Объект.ДанныеРеестра);
	Элементы.ВидыЭлементовРеестра.Доступность = ЗначениеЗаполнено(Объект.ДанныеРеестра);
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьКоличествоНаСервере()
	
	КоличествоВидыЭлементовРеестра = Объект.ВидыЭлементовРеестра.Количество();
	КоличествоДоступ = Объект.Доступ.Количество();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКоличествоНаКлиенте()
	
	КоличествоВидыЭлементовРеестра = Объект.ВидыЭлементовРеестра.Количество();
	КоличествоДоступ = Объект.Доступ.Количество();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыбораДоступа(ВыбранноеЗначение, ДопПараметры) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Истина;
	СотрудникиКлиент.ОбработкаВыбораКонтейнера(
		Элементы.Доступ, ВыбранноеЗначение, СтандартнаяОбработка);
	Если СтандартнаяОбработка = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Доступ.ТекущиеДанные.Контейнер = ВыбранноеЗначение;
	
КонецПроцедуры

#КонецОбласти