#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьДатуИВремяВСрокахЗадач = ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач");
	ТекущийПользователь = Пользователи.ТекущийПользователь();
		
	Предмет = Параметры.Предмет;
	ПредметПриИзмененииСервере();
	
	Элементы.Предмет.ТолькоПросмотр = ЗначениеЗаполнено(Параметры.Предмет);
	Элементы.Предмет.КнопкаВыбора = Не Элементы.Предмет.ТолькоПросмотр;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РазвернутьДействия();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПредметПриИзмененииСервере()
	
	Заголовок = НСтр("ru = 'История обработки: '");
	
	Если ЗначениеЗаполнено(Предмет) Тогда
		
		Заголовок = Заголовок + Строка(Предмет);
			
		ПрочитатьДействия();
		
	Иначе
		
		Действия.ПолучитьЭлементы().Очистить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДействия()
	
	ДеревоОбработки = ДействияСервер.ДеревоОбработкиСИсторией(Предмет);
	
	СтрокиДереваКПереносу = Новый Массив;
	Для Каждого СтрокаОбработки Из ДеревоОбработки.Строки Цикл
		СтрокиДереваКПереносу.Добавить(СтрокаОбработки);
	КонецЦикла;
	
	СтрокиВФормеПоСтрокамДерева = Новый Соответствие();
	
	Пока СтрокиДереваКПереносу.Количество() > 0 Цикл
		
		ПереносимаяСтрока = СтрокиДереваКПереносу[0]; // СтрокаДереваЗначений
		СтрокиДереваКПереносу.Удалить(0);
		
		РодительСтроки = ПереносимаяСтрока.Родитель;
		Если РодительСтроки = Неопределено Тогда
			НоваяСтрокаФормы = Действия.ПолучитьЭлементы().Добавить();
		Иначе
			РодительСтрокиФормы = СтрокиВФормеПоСтрокамДерева[РодительСтроки]; // ДанныеФормыДерево
			НоваяСтрокаФормы = РодительСтрокиФормы.ПолучитьЭлементы().Добавить();
		КонецЕсли;
		
		СтрокиВФормеПоСтрокамДерева[ПереносимаяСтрока] = НоваяСтрокаФормы;
		ЗаполнитьЗначенияСвойств(НоваяСтрокаФормы, ПереносимаяСтрока);
		
		Для Каждого ДочерняяСтрока Из ПереносимаяСтрока.Строки Цикл
			СтрокиДереваКПереносу.Добавить(ДочерняяСтрока);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДействия()
	
	ДействияКлиент.РазвернутьДействия(Элементы.Действия, Действия);
	
КонецПроцедуры

&НаКлиенте
Процедура ДействияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДействияКлиент.ДействияВыбор(ЭтотОбъект,
		Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, Истина);
	
КонецПроцедуры

#КонецОбласти