#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	РеквизитыДанных = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Данные.Ссылка, "Документ,Исполнитель");
	Предмет = РеквизитыДанных.Документ;
	Исполнитель = РеквизитыДанных.Исполнитель;
	
	Если ДелопроизводствоКлиентСервер.ЭтоДокументПредприятия(Предмет) Тогда	
		Представление = СтрШаблон(НСтр("ru = 'Данные ознакомления с документом %1 пользователя %2'"), 
			Предмет,
			Исполнитель);
	Иначе
		Представление = СтрШаблон(НСтр("ru = 'Данные ознакомления с материалами мероприятия %1 пользователя %2'"), 
			Предмет,
			Исполнитель);
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Возвращает массив имен ключевых реквизитов
// 
// Параметры:
//  Версия - Число - Номер версии подписи
//         - Неопределено - Актуальная версия подписи
// 
// Возвращаемое значение:
//  Массив из Строка
Функция ПолучитьИменаКлючевыхРеквизитов(Версия = Неопределено) Экспорт
	
	Если Версия = Неопределено Тогда
		
		Возврат ИменаКлючевыхРеквизитовПоУмолчанию();
		
	Иначе
		
		Если Версия = 5 Тогда
			Возврат ИменаКлючевыхРеквизитовВерсии5();
		Иначе
			Возврат ИменаКлючевыхРеквизитовПоУмолчанию();
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

// Возвращает массив версий файлов для подписания и проверки ЭП
// 
// Параметры:
//  ДанныеОзнакомления - СправочникСсылка.ДанныеОзнакомленияПодПодпись
// 
// Возвращаемое значение:
//  Массив Из Структура:
//    * ВерсияФайла - СправочникСсылка.ВерсииФайлов
//    * ХэшСуммаВерсииФайла - Строка
//
Функция ДанныеВерсийФайловДляПодписанияИПроверкиЭП(ДанныеОзнакомления) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеОзнакомленияПодПодписьВерсииФайлов.НомерСтроки КАК НомерСтроки,
		|	ДанныеОзнакомленияПодПодписьВерсииФайлов.ВерсияФайла КАК ВерсияФайла,
		|	ДанныеОзнакомленияПодПодписьВерсииФайлов.ХэшСуммаВерсииФайла КАК ХэшСуммаВерсииФайла
		|ИЗ
		|	Справочник.ДанныеОзнакомленияПодПодпись.ВерсииФайлов КАК ДанныеОзнакомленияПодПодписьВерсииФайлов
		|ГДЕ
		|	ДанныеОзнакомленияПодПодписьВерсииФайлов.Ссылка = &ДанныеОзнакомления
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	Запрос.УстановитьПараметр("ДанныеОзнакомления", ДанныеОзнакомления);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДанныеВерсий = Новый Массив();
	Пока Выборка.Следующий() Цикл
		
		ДанныеВерсии = Новый Структура;
		ДанныеВерсии.Вставить("ВерсияФайла", Выборка.ВерсияФайла);
		ДанныеВерсии.Вставить("ХэшСуммаВерсииФайла", Выборка.ХэшСуммаВерсииФайла);
		
		ДанныеВерсий.Добавить(ДанныеВерсии);
		
	КонецЦикла;
	
	Возврат ДанныеВерсий;
	
КонецФункции

// Возвращает двоичные данные ознакомления под подпись
// 
// Параметры:
//  ДанныеОзнакомления - СправочникСсылка.ДанныеОзнакомленияПодПодпись
//  ВерсияПодписи - Число - Версия подписи
//                - Неопределено - актуальная версия подписи
// 
// Возвращаемое значение:
//  ДвоичныеДанные - Двоичные данные ознакомления под подпись
Функция ДвоичныеДанныеОзнакомленияПодПодпись(ДанныеОзнакомления, ВерсияПодписи) Экспорт
	
	Возврат РаботаСЭП.ПолучитьДвоичныеДанныеОбъекта(ДанныеОзнакомления, ВерсияПодписи);
	
КонецФункции

// Возвращает таблицу с ссылками на версии файлов и хэш суммы этих версий
//
// Параметры:
//  Документ - СправочникСсылка.ДокументыПредприятия
//  ТолькоНеОригиналы - Булево - Если Истина, то функция вернет только файлы не оригиналы скан-копий
//
// Возвращаемое значение:
//  ТаблицаЗначений - 
// * Файл - СправочникСсылка.Файлы
// * ВерсияФайла - СправочникСсылка.ВерсииФайлов
// * ХэшСуммаВерсииФайла - Число
// * ЯвляетсяОригиналом - Булево
//
Функция ВерсииФайловПоДокументуИИхХэшСуммы(Документ, ТолькоНеОригиналы=Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Файлы.Ссылка КАК Файл,
		|	Файлы.ТекущаяВерсия КАК ВерсияФайла,
		|	0 КАК ХэшСуммаВерсииФайла,
		|	ЕСТЬNULL(СведенияОФайлахДокументооборот.ЯвляетсяОригиналом, ЛОЖЬ) КАК ЯвляетсяОригиналом
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СлужебныеФайлыДокументов КАК СлужебныеФайлыДокументов
		|		ПО Файлы.Ссылка = СлужебныеФайлыДокументов.Файл
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОФайлахДокументооборот КАК СведенияОФайлахДокументооборот
		|		ПО (СведенияОФайлахДокументооборот.Файл = Файлы.Ссылка)
		|ГДЕ
		|	Файлы.ВладелецФайла = &Документ
		|	И СлужебныеФайлыДокументов.Файл ЕСТЬ NULL
		|	И НЕ Файлы.ПометкаУдаления
		|	И (НЕ &ТолькоНеОригиналы
		|			ИЛИ НЕ ЕСТЬNULL(СведенияОФайлахДокументооборот.ЯвляетсяОригиналом, ЛОЖЬ))";
	
	Запрос.Параметры.Вставить("Документ", Документ);
	Запрос.Параметры.Вставить("ТолькоНеОригиналы", ТолькоНеОригиналы);
	
	ВерсииФайловИИхХэшСуммы = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаВерсииФайлов Из ВерсииФайловИИхХэшСуммы Цикл
		СтрокаВерсииФайлов.ХэшСуммаВерсииФайла = Справочники.ВерсииФайлов.ХэшСуммаВерсииФайлаCRC32(
			СтрокаВерсииФайлов.ВерсияФайла);
	КонецЦикла; 
	
	Возврат ВерсииФайловИИхХэшСуммы;
	
КонецФункции

// Возвращает массив актуальных версий файлов документа/мероприятия
//
// Параметры:
//  Документ - ОпределяемыйТип.ПредметДействия
//
// Возвращаемое значение:
//  Массив Из СправочникСсылка.ВерсииФайлов
//
Функция ТекущиеВерсииФайловДокумента(Предмет) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Файлы.Ссылка КАК Файл,
	|	Файлы.ТекущаяВерсия КАК ВерсияФайла
	|ИЗ
	|	Справочник.Файлы КАК Файлы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СлужебныеФайлыДокументов КАК СлужебныеФайлыДокументов
	|		ПО Файлы.Ссылка = СлужебныеФайлыДокументов.Файл
	|ГДЕ
	|	Файлы.ВладелецФайла = &Предмет
	|	И СлужебныеФайлыДокументов.Файл ЕСТЬ NULL
	|	И Не Файлы.ПометкаУдаления";
	
	Запрос.Параметры.Вставить("Предмет", Предмет);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВерсияФайла");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Имена ключевых реквизитов для актуальной версии подписи
// 
// Возвращаемое значение:
//  Массив из Строка
Функция ИменаКлючевыхРеквизитовПоУмолчанию()
	
	МассивИмен = Новый Массив();
	МассивИмен.Добавить("Документ");
	МассивИмен.Добавить("Исполнитель");
	МассивИмен.Добавить("ВерсииФайлов");
	
	Возврат МассивИмен;
	
КонецФункции

// Имена ключевых реквизитов для актуальной версии подписи
// 
// Возвращаемое значение:
//  Массив из Строка
Функция ИменаКлючевыхРеквизитовВерсии5()
	
	МассивИмен = Новый Массив();
	МассивИмен.Добавить("Документ");
	МассивИмен.Добавить("ДатаОзнакомления"); 
	МассивИмен.Добавить("Исполнитель");
	МассивИмен.Добавить("ВерсииФайлов");
	
	Возврат МассивИмен;
	
КонецФункции

#КонецОбласти

#КонецЕсли