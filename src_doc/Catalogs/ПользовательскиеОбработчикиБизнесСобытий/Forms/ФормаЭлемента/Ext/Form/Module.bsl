#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Объект.Включен = Истина;
		
		Если Не ЗначениеЗаполнено(Объект.ВыполняемыйКод) Тогда
			Объект.ВыполняемыйКод =
				"// Этот фрагмент кода на встроенном языке будет выполнен 
				|// автоматически регламентным заданием 
				|//  ""Обработка произошедших бизнес-событий"".
				|// Если при выполнении этого кода возникнет ошибка, то информация об этом
				|// будет записана в журнал регистрации.
				|//
				|// Можно обращаться к переменной Событие(поля: Автор ВидСобытия Дата Источник КонтекстСобытия)
				|//  КонтекстСобытия - ХранилищеЗначения (внутри может быть сериализованная ЗаписьXML, 
				|//  созданная детектором бизнес события)
				| 
				|//Пример обработки
				|//Если ДелопроизводствоКлиентСервер.ЭтоДокумент(Событие.Источник) Тогда
				|//  ДокументОбъект  = Событие.Источник.ПолучитьОбъект();
				|//  ДокументОбъект.НеДействует = Истина;
				|//  ДокументОбъект.Записать();
				|//КонецЕсли;";
		КонецЕсли;
		
	КонецЕсли;
	
	РедакторВстроенногоЯзыка.ПриСозданииНаСервере(ЭтотОбъект, "Редактор1,Редактор2");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	МассивЭлементовРедактор = Новый Массив;
	МассивЭлементовРедактор.Добавить(Элементы.РедакторВстроенногоЯзыка1);
	МассивЭлементовРедактор.Добавить(Элементы.РедакторВстроенногоЯзыка2);
	
	МассивЭлементовТекстовыйДокумент = Новый Массив;
	МассивЭлементовТекстовыйДокумент.Добавить(Элементы.ВыполняемыйКод);
	МассивЭлементовТекстовыйДокумент.Добавить(Элементы.ВыполняемыйКодОтменыОбработки);
	
	РедакторВстроенногоЯзыкаКлиент.ПриОткрытии(
		МассивЭлементовРедактор,
		МассивЭлементовТекстовыйДокумент,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Объект.ВыполняемыйКод = РедакторВстроенногоЯзыкаКлиент.ПолучитьТекст(
		Элементы.РедакторВстроенногоЯзыка1, Объект.ВыполняемыйКод);
	Объект.ВыполняемыйКодОтменыОбработки = РедакторВстроенногоЯзыкаКлиент.ПолучитьТекст(
		Элементы.РедакторВстроенногоЯзыка2, Объект.ВыполняемыйКодОтменыОбработки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтветственныйОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	СотрудникиКлиент.СотрудникОбработкаВыбора(Объект, "Ответственный", ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры 

&НаКлиенте
Процедура РедакторВстроенногоЯзыка1ДокументСформирован(Элемент)
	
	Если РедакторВстроенногоЯзыкаКлиент.ДокументСформирован(
			Элемент, ЭтотОбъект, Объект.ВыполняемыйКод, Объект.Предопределенный) Тогда
		ПодключитьОбработчикОжидания("ПослеЗагрузкиРедактора1", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедакторВстроенногоЯзыка1ПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)

	НажатаКнопкаEsc = Ложь;
	
	РедакторВстроенногоЯзыкаКлиент.ПриНажатии(
		Элемент,
		ДанныеСобытия,
		СтандартнаяОбработка,
		ЭтотОбъект,
		НажатаКнопкаEsc);
		
	Если НажатаКнопкаEsc Тогда
		Закрыть();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура РедакторВстроенногоЯзыка2ДокументСформирован(Элемент)
	
	Если РедакторВстроенногоЯзыкаКлиент.ДокументСформирован(
			Элемент, ЭтотОбъект, Объект.ВыполняемыйКодОтменыОбработки, Объект.Предопределенный) Тогда
		ПодключитьОбработчикОжидания("ПослеЗагрузкиРедактора2", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедакторВстроенногоЯзыка2ПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	РедакторВстроенногоЯзыкаКлиент.ПриНажатии(
		Элемент,
		ДанныеСобытия,
		СтандартнаяОбработка,
		ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеЗагрузкиРедактора1()
	
	РедакторВстроенногоЯзыкаКлиент.ПослеЗагрузкиРедактора(Элементы.РедакторВстроенногоЯзыка1, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗагрузкиРедактора2()
	
	РедакторВстроенногоЯзыкаКлиент.ПослеЗагрузкиРедактора(Элементы.РедакторВстроенногоЯзыка2, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти