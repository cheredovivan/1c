
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПротоколированиеРаботыСотрудников.ЗаписатьОткрытие(Объект.Ссылка);	
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОбновитьПолныйПуть();
	КонецЕсли;	
	
	УчитыватьПодпапки = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Элементы.ПодсчитатьСУчетомПодпапок.Доступность = Ложь;
		Элементы.ОткрытьСписокПапокИДокументов.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ЗаполнитьИнформациюОСодержимом();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Элементы.ПодсчитатьСУчетомПодпапок.Доступность = Истина;
	Элементы.ОткрытьСписокПапокИДокументов.Доступность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", НЕ ЗначениеЗаполнено(ТекущийОбъект.Ссылка));	
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПротоколированиеРаботыСотрудников.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыСотрудников.ЗаписатьИзменение(Объект.Ссылка);
	ОбновитьПолныйПуть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	
	ОбновитьПолныйПуть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	СотрудникиКлиент.СотрудникОбработкаВыбора(Объект, "Ответственный", ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьСписокПапокИДокументов(Команда)
	
	ПараметрыФормы = Новый Структура("Папка", Объект.Ссылка);
	
	ОткрытьФорму("Справочник.ДокументыПредприятия.Форма.ФормаСпискаСПапками", ПараметрыФормы, ,Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсчитатьСУчетомПодпапок(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПодсчитатьСУчетомПодпапокПродолжение",
		ЭтотОбъект);
	
	ПоказатьВопрос(ОписаниеОповещения,
		НСтр("ru = 'Сейчас будет выполнен подсчет содержимого папки с учетом подпапок.
			|Эта операция может занять продолжительное время. Продолжить?'"),
		РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваДоступа(Команда)
	
	ДокументооборотПраваДоступаКлиент.ОткрытьФормуПравДоступа(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Подписаться(Команда)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("ОбъектПодписки", Объект.Ссылка);
		ОткрытьФорму("ОбщаяФорма.ПодпискаНаУведомленияПоОбъекту", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьПолныйПуть()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПолныйПуть = "";
	
	ПапкаРодитель = Объект.Родитель;
	Пока ЗначениеЗаполнено(ПапкаРодитель) Цикл
		
		ПолныйПуть = Строка(ПапкаРодитель) + "\" + ПолныйПуть;
		
		ПапкаРодитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПапкаРодитель, "Родитель");
		Если Не ЗначениеЗаполнено(ПапкаРодитель) Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	ПолныйПуть = ПолныйПуть + Объект.Наименование;
	
	Если Не ПустаяСтрока(ПолныйПуть) Тогда
		ПолныйПуть = """" + ПолныйПуть + """";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнформациюОСодержимом()    
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЧислоВерсий = 0;
	ЧислоДокументов = 0;
	ЧислоФайлов = 0;
	ЧислоПапок = 0;
	РазмерВерсий = 0;
	РазмерФайлов = 0;
	
	Запрос = Новый Запрос;
	
	Если УчитыватьПодпапки Тогда
	
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	ПапкиДокументов.Ссылка КАК Ссылка
		               |ПОМЕСТИТЬ ВременнаяТаблицаПапок
		               |ИЗ
		               |	Справочник.ПапкиДокументов КАК ПапкиДокументов
		               |ГДЕ
		               |	ПапкиДокументов.Ссылка В ИЕРАРХИИ(&ПапкаКорневая)
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	Ссылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	КОЛИЧЕСТВО(ВременнаяТаблицаПапок.Ссылка) КАК ЧислоПапок
		               |ИЗ
		               |	ВременнаяТаблицаПапок КАК ВременнаяТаблицаПапок
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(ДокументыПредприятия.Ссылка) КАК ЧислоДокументов
		               |ИЗ
		               |	Справочник.ДокументыПредприятия КАК ДокументыПредприятия
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПапок КАК ВременнаяТаблицаПапок
		               |		ПО (ВременнаяТаблицаПапок.Ссылка = ДокументыПредприятия.Папка)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(Файлы.Ссылка) КАК ЧислоФайлов,
		               |	ЕСТЬNULL(СУММА(Файлы.ТекущаяВерсияРазмер), 0) КАК РазмерФайлов
		               |ИЗ
		               |	Справочник.Файлы КАК Файлы
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДокументыПредприятия КАК ДокументыПредприятия
		               |		ПО Файлы.ВладелецФайла = ДокументыПредприятия.Ссылка
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПапок КАК ВременнаяТаблицаПапок
		               |		ПО (ВременнаяТаблицаПапок.Ссылка = ДокументыПредприятия.Папка)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(ВерсииФайлов.Ссылка) КАК ЧислоВерсий,
		               |	ЕСТЬNULL(СУММА(ВерсииФайлов.Размер), 0) КАК РазмерВерсий
		               |ИЗ
		               |	Справочник.Файлы КАК Файлы
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДокументыПредприятия КАК ДокументыПредприятия
		               |		ПО Файлы.ВладелецФайла = ДокументыПредприятия.Ссылка
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВерсииФайлов КАК ВерсииФайлов
		               |		ПО (ВерсииФайлов.Владелец = Файлы.Ссылка)
		               |			И (ВерсииФайлов.ФайлУдален = ЛОЖЬ)
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПапок КАК ВременнаяТаблицаПапок
		               |		ПО (ВременнаяТаблицаПапок.Ссылка = ДокументыПредприятия.Папка)";
				   
	Иначе  // без подпапок
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	&ПапкаКорневая КАК Ссылка
		               |ПОМЕСТИТЬ ВременнаяТаблицаПапок
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(ПапкиДокументов.Ссылка) КАК ЧислоПапок
		               |ИЗ
		               |	Справочник.ПапкиДокументов КАК ПапкиДокументов
		               |ГДЕ
		               |	ПапкиДокументов.Родитель = &ПапкаКорневая
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(ДокументыПредприятия.Ссылка) КАК ЧислоДокументов
		               |ИЗ
		               |	Справочник.ДокументыПредприятия КАК ДокументыПредприятия
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПапок КАК ВременнаяТаблицаПапок
		               |		ПО (ВременнаяТаблицаПапок.Ссылка = ДокументыПредприятия.Папка)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(Файлы.Ссылка) КАК ЧислоФайлов,
		               |	ЕСТЬNULL(СУММА(Файлы.ТекущаяВерсияРазмер), 0) КАК РазмерФайлов
		               |ИЗ
		               |	Справочник.Файлы КАК Файлы
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДокументыПредприятия КАК ДокументыПредприятия
		               |		ПО Файлы.ВладелецФайла = ДокументыПредприятия.Ссылка
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПапок КАК ВременнаяТаблицаПапок
		               |		ПО (ВременнаяТаблицаПапок.Ссылка = ДокументыПредприятия.Папка)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(ВерсииФайлов.Ссылка) КАК ЧислоВерсий,
		               |	ЕСТЬNULL(СУММА(ВерсииФайлов.Размер), 0) КАК РазмерВерсий
		               |ИЗ
		               |	Справочник.Файлы КАК Файлы
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДокументыПредприятия КАК ДокументыПредприятия
		               |		ПО Файлы.ВладелецФайла = ДокументыПредприятия.Ссылка
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВерсииФайлов КАК ВерсииФайлов
		               |		ПО (ВерсииФайлов.Владелец = Файлы.Ссылка)
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПапок КАК ВременнаяТаблицаПапок
		               |		ПО (ВременнаяТаблицаПапок.Ссылка = ДокументыПредприятия.Папка)";
		
	КонецЕсли;		
				   
	Запрос.УстановитьПараметр("ПапкаКорневая", Объект.Ссылка);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = МассивРезультатов[1].Выгрузить();
	ЧислоПапок = Выборка[0].ЧислоПапок;
	Если УчитыватьПодпапки Тогда
		ЧислоПапок = ЧислоПапок - 1;
	КонецЕсли;

	Выборка = МассивРезультатов[2].Выгрузить();
	ЧислоДокументов = Выборка[0].ЧислоДокументов;
	
	Выборка = МассивРезультатов[3].Выгрузить();
	ЧислоФайлов = Выборка[0].ЧислоФайлов;
	РазмерФайлов = Выборка[0].РазмерФайлов;
	
	Выборка = МассивРезультатов[4].Выгрузить();
	ЧислоВерсий = Выборка[0].ЧислоВерсий;
	РазмерВерсий = Выборка[0].РазмерВерсий;

	КоличествоВерсий = ?(ЧислоВерсий > 0, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)'"), 
		ЧислоВерсий, ФайловыеФункцииКлиентСервер.ПолучитьСтрокуСРазмеромФайлаДляКарточкиПапки(РазмерВерсий)), "-");
	КоличествоПодпапок = ?(ЧислоПапок > 0, Строка(ЧислоПапок), "-");
	КоличествоДокументов = ?(ЧислоДокументов > 0, Строка(ЧислоДокументов), "-");
	КоличествоФайлов = ?(ЧислоФайлов > 0, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)'"), 
		ЧислоФайлов, ФайловыеФункцииКлиентСервер.ПолучитьСтрокуСРазмеромФайлаДляКарточкиПапки(РазмерФайлов)), "-");

КонецПроцедуры

&НаКлиенте
Процедура ПодсчитатьСУчетомПодпапокПродолжение(Результат, Параметры) Экспорт 

	Если Результат = КодВозвратаДиалога.Да Тогда
		Состояние(НСтр("ru='Выполняется подсчет содержимого папки с учетом подпапок. Пожалуйста, подождите...'"));
		УчитыватьПодпапки = Истина;
		ЗаполнитьИнформациюОСодержимом();
		Элементы.ГруппаИнформация.Заголовок = НСтр("ru='Папка содержит (с учетом подпапок)'");
		Элементы.ПодсчитатьСУчетомПодпапок.Доступность = Ложь;
		Состояние();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти