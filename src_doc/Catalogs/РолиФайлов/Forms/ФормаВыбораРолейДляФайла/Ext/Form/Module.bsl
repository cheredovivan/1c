#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивФайлов = Параметры.МассивФайлов;
	СписокФайлов.ЗагрузитьЗначения(МассивФайлов);
	Файл = МассивФайлов[0];
	
	// Прочитаем РолиФайловДокументов для файла
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РолиФайловДокументов.Роль КАК Роль,
		|	РолиФайловДокументов.Роль.Наименование КАК РольНаименование
		|ИЗ
		|	РегистрСведений.РолиФайловДокументов КАК РолиФайловДокументов
		|ГДЕ
		|	РолиФайловДокументов.Файл = &Файл
		|
		|УПОРЯДОЧИТЬ ПО
		|	РольНаименование";
	Запрос.УстановитьПараметр("Файл", Файл);
	РолиВыбр = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Роль");
	Если РолиВыбр.Количество() <> 0 Тогда
		ВыбраннаяРоль = РолиВыбр[0];
	КонецЕсли;	
	
	ВладелецФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Файл, "ВладелецФайла");
	Если ТипЗнч(ВладелецФайла) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда
		
		ВидДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВладелецФайла, "ВидДокумента");
		
		Если ТипЗнч(ВидДокумента) = Тип("СправочникСсылка.ВидыДокументов") Тогда
			
			РолиФайлов = Делопроизводство.РолиФайловДляВидаДокументов(ВидДокумента);
			
			Если РолиФайлов.Количество() = 0 И Не ЗначениеЗаполнено(ВыбраннаяРоль) Тогда
				Отказ = Истина;
				СтандартнаяОбработка = Ложь;
				Сообщить(НСтр("ru = 'Для данного вида документов не настроены роли файлов.'"));
			КонецЕсли;	         
			
			РолиВыбр = РолиФайлов.ВыгрузитьКолонку("Роль");
			
			// Прочитаем РолиФайлов
			Запрос = Новый Запрос;
			Запрос.Текст =
				"ВЫБРАТЬ
				|	РолиФайлов.Ссылка КАК Роль,
				|	РолиФайлов.Наименование КАК Наименование,
				|	РолиФайлов.Комментарий КАК Комментарий
				|ИЗ
				|	Справочник.РолиФайлов КАК РолиФайлов
				|ГДЕ
				|	РолиФайлов.ПометкаУдаления = ЛОЖЬ
				|	И РолиФайлов.Ссылка В (&Роли)
				|
				|УПОРЯДОЧИТЬ ПО
				|	Наименование";
			Запрос.УстановитьПараметр("Роли", РолиВыбр);
			РолиВсе = Запрос.Выполнить().Выгрузить();
			
			Для Каждого Стр Из РолиВсе Цикл
				
				Представление = Стр.Наименование;
				Если ЗначениеЗаполнено(Стр.Комментарий) Тогда
					Представление = Стр.Наименование + ", " + Стр.Комментарий;
				КонецЕсли;	
				
				ДоступныеРоли.Добавить(Стр.Роль, Представление);
				
			КонецЦикла;	
			
		КонецЕсли;	
		
	КонецЕсли;	
		
	Если ДоступныеРоли.Количество() = 0 Тогда
		
		// Прочитаем РолиФайлов
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	РолиФайлов.Ссылка КАК Роль,
			|	РолиФайлов.Наименование КАК Наименование
			|ИЗ
			|	Справочник.РолиФайлов КАК РолиФайлов
			|ГДЕ
			|	РолиФайлов.ПометкаУдаления = ЛОЖЬ
			|	И НЕ РолиФайлов.Ссылка В (&Роли)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Наименование";
		Запрос.УстановитьПараметр("Роли", РолиВыбр);
		РолиВсе = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Роль");
		ДоступныеРоли.ЗагрузитьЗначения(РолиВсе);
		
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ВыбраннаяРоль) Тогда
		ИдТекСтроки = Неопределено;
		Для Каждого Стр Из ДоступныеРоли Цикл
			Если Стр.Значение = ВыбраннаяРоль Тогда
				ИдТекСтроки = Стр.ПолучитьИдентификатор();
			КонецЕсли;	
		КонецЦикла;	
		
		Если ЗначениеЗаполнено(ИдТекСтроки) Тогда
			Элементы.ДоступныеРоли.ТекущаяСтрока = ИдТекСтроки;
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДоступныеРолиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	// закрыть форму
	ГотовоВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	ГотовоВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура ГотовоВыполнить()
	ВыбраннаяРоль = Элементы.ДоступныеРоли.ТекущиеДанные.Значение;
	ГотовоНаСервере();
	Оповестить("РолиФайловВыбраны", Файл);
	Закрыть();
КонецПроцедуры

&НаСервере
Процедура ГотовоНаСервере()
	
	Для Каждого Стр Из СписокФайлов Цикл
		РаботаСФайламиВызовСервера.ЗаменитьРольФайла(Стр.Значение, ВыбраннаяРоль);
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти