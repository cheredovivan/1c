#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ИмяРеквизитаОбъектаДО", ИмяРеквизитаОбъектаДО);
	Параметры.Свойство("ПредставлениеРеквизитаОбъектаДО", ПредставлениеРеквизитаОбъектаДО);
	Параметры.Свойство("ТипРеквизитаОбъектаДО", ТипРеквизитаОбъектаДО);
	
	Параметры.Свойство("Вариант", Вариант);
	Параметры.Свойство("ЭтоПолноправныйПользователь", ЭтоПолноправныйПользователь);
	Параметры.Свойство("ВычисляемоеВыражение", ВычисляемоеВыражение);
	Параметры.Свойство("ПравилаЗаполненияСтрокТаблицы", ПравилаЗаполненияСтрокТаблицы);
	Параметры.Свойство("МестоВыполненияВыражения", МестоВыполненияВыражения);
	Параметры.Свойство("ИмяРеквизитаОбъектаИС", ИмяРеквизитаОбъектаИС);
	Параметры.Свойство("ПредставлениеРеквизитаОбъектаИС", ПредставлениеРеквизитаОбъектаИС);
	Параметры.Свойство("Обновлять", Обновлять);
	
	Параметры.Свойство("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	Параметры.Свойство("ПредставлениеИС", ПредставлениеИС);
	Параметры.Свойство("ИмяМакета", ИмяМакета);
	Параметры.Свойство("ПредставлениеОбъектаИС", ПредставлениеОбъектаИС);
	Параметры.Свойство("ПолноеИмяОбъектаИС", ПолноеИмяОбъектаИС);
	Параметры.Свойство("ПолноеИмяОбъектаДО", ПолноеИмяОбъектаДО);
	Параметры.Свойство("ВидДокумента", ВидДокумента);
	
	Параметры.Свойство("ВозможноЗаполнениеИзШаблона", ВозможноЗаполнениеИзШаблона);
	
	Если Параметры.Свойство("Подсказка") И ЗначениеЗаполнено(Параметры.Подсказка) Тогда
		Элементы.ПредставлениеРеквизитаОбъектаДО.Подсказка = Параметры.Подсказка;
		Элементы.ПредставлениеРеквизитаОбъектаДО.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	КонецЕсли;
	
	РеквизитОбъекта = ПредопределенноеЗначение("Перечисление.ВариантыПравилЗаполненияРеквизитов.РеквизитОбъекта");
	ВыражениеНаВстроенномЯзыке =
		ПредопределенноеЗначение("Перечисление.ВариантыПравилЗаполненияРеквизитов.ВыражениеНаВстроенномЯзыке");
	РеквизитТаблицы = ПредопределенноеЗначение("Перечисление.ВариантыПравилЗаполненияРеквизитов.РеквизитТаблицы");
	НеЗаполнять = ПредопределенноеЗначение("Перечисление.ВариантыПравилЗаполненияРеквизитов.ПустаяСсылка");
	ИзШаблона = ПредопределенноеЗначение("Перечисление.ВариантыПравилЗаполненияРеквизитов.ИзШаблона");
	
	Если ВозможноЗаполнениеИзШаблона Тогда
		
		Элементы.Вариант.СписокВыбора.Вставить(3, ИзШаблона, НСтр("ru = 'Из шаблона'"));
		
		Если Параметры.ШаблонЗапрещаетИзменение Тогда
			ШаблонЗапрещаетИзменение = Истина;
			Элементы.Вариант.СписокВыбора.Удалить(Элементы.Вариант.СписокВыбора.НайтиПоЗначению(НеЗаполнять));
			Вариант = ИзШаблона;
			Элементы.Вариант.Доступность = Ложь;
			ИнформационнаяНадпись = НСтр("ru = 'Шаблон запрещает изменение заданных в нем реквизитов.'");
			Элементы.ОК.Доступность = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьЗаголовокКомандыНастройкиТаблицы(ПравилаЗаполненияСтрокТаблицы);
	
	Если МестоВыполненияВыражения = Перечисления.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеИС Тогда
		Элементы.ВычисляемоеВыражение.Подсказка =
			СтрШаблон(НСтр("ru = 'Выражение будет выполняться в базе %1'"), ПредставлениеИС);
	Иначе
		Элементы.ВычисляемоеВыражение.Подсказка =
			НСтр("ru = 'Выражение будет выполняться в базе 1С:Документооборот'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Вариант = РеквизитОбъекта Тогда
		ПроверяемыеРеквизиты.Добавить("ИмяРеквизитаОбъектаИС");
		
	ИначеЕсли Вариант = ВыражениеНаВстроенномЯзыке Тогда
		ПроверяемыеРеквизиты.Добавить("ВычисляемоеВыражение");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ВычисляемоеВыражениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ПустаяСтрока(ПолноеИмяОбъектаИС) Тогда
		ПоказатьПредупреждение(, СтрШаблон(НСтр("ru = 'Сначала выберите объект %1'"), ПредставлениеИС));
		Возврат
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	ПараметрыФормы.Вставить("ПредставлениеИС", ПредставлениеИС);
	ПараметрыФормы.Вставить("ИмяМакета", ИмяМакета);
	ПараметрыФормы.Вставить("ВычисляемоеВыражение", ВычисляемоеВыражение);
	ПараметрыФормы.Вставить("МестоВыполненияВыражения", МестоВыполненияВыражения);
	ПараметрыФормы.Вставить("ПолноеИмяОбъектаИС", ПолноеИмяОбъектаИС);
	ПараметрыФормы.Вставить("ПолноеИмяОбъектаДО", ПолноеИмяОбъектаДО);
	ПараметрыФормы.Вставить("ВидДокумента", ВидДокумента);
	ПараметрыФормы.Вставить("ЭтоТаблица", Истина);
	ПараметрыФормы.Вставить("ИмяРеквизитаОбъектаДО", ИмяРеквизитаОбъектаДО);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьВычисляемоеВыражениеЗавершение", ЭтотОбъект);
	
	ОткрытьФорму(
		"Справочник.ПравилаЗагрузкиДанныхВДО.Форма.ВыражениеНаВстроенномЯзыке",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВычисляемоеВыражениеЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ВычисляемоеВыражение = Результат.ВычисляемоеВыражение;
		МестоВыполненияВыражения = Результат.МестоВыполненияВыражения;
		
		Если МестоВыполненияВыражения =
				ПредопределенноеЗначение("Перечисление.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеИС") Тогда
			Элементы.ВычисляемоеВыражение.Подсказка =
				СтрШаблон(НСтр("ru = 'Выражение будет выполняться в базе %1'"), ПредставлениеИС);
		Иначе
			Элементы.ВычисляемоеВыражение.Подсказка =
				НСтр("ru = 'Выражение будет выполняться в базе 1С:Документооборот'");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеРеквизитаОбъектаИСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ПустаяСтрока(ПолноеИмяОбъектаИС) Тогда
		ПоказатьПредупреждение(, СтрШаблон(НСтр("ru = 'Сначала выберите объект %1'"), ПредставлениеИС));
		Возврат
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	ПараметрыФормы.Вставить("ИмяМакета", ИмяМакета);
	ПараметрыФормы.Вставить("ПредставлениеОбъектаИС", ПредставлениеОбъектаИС);
	ПараметрыФормы.Вставить("ПолноеИмяОбъектаИС", ПолноеИмяОбъектаИС);
	ПараметрыФормы.Вставить("ЭтоТаблица", Истина);
	ПараметрыФормы.Вставить("Таблица", "");
	ПараметрыФормы.Вставить("ИмяРеквизитаОбъектаИС", ИмяРеквизитаОбъектаИС);
	ПараметрыФормы.Вставить("ПредставлениеРеквизитаОбъектаДО", ПредставлениеРеквизитаОбъектаДО);
	
	Оповещение = Новый ОписаниеОповещения("ВыборРеквизитаИнтегрированнойСистемыЗавершение", ЭтотОбъект);
	
	ОткрытьФорму(
		"Справочник.ПравилаЗагрузкиДанныхВДО.Форма.ВыборРеквизитаИнтегрированнойСистемы",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборРеквизитаИнтегрированнойСистемыЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Результат.Свойство("Имя", ИмяРеквизитаОбъектаИС);
		Результат.Свойство("Представление", ПредставлениеРеквизитаОбъектаИС);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Вариант", Вариант);
	Результат.Вставить("Обновлять", Обновлять);
	Результат.Вставить("ИмяРеквизитаОбъектаДО", ИмяРеквизитаОбъектаДО);
	Результат.Вставить("ЭтоТаблица", Истина);
	Результат.Вставить("ИмяРеквизитаОбъектаИС");
	Результат.Вставить("ПредставлениеРеквизитаОбъектаИС");
	Результат.Вставить("ВычисляемоеВыражение");
	Результат.Вставить("ПравилаЗаполненияСтрокТаблицы");
	Результат.Вставить("МестоВыполненияВыражения");
	Результат.Вставить("Картинка");
	Результат.Вставить("Пояснение");
	
	Если Вариант = РеквизитОбъекта Тогда
		
		Результат.ИмяРеквизитаОбъектаИС = ИмяРеквизитаОбъектаИС;
		Результат.ПредставлениеРеквизитаОбъектаИС = ПредставлениеРеквизитаОбъектаИС;
		Результат.Картинка = 1;
		
	ИначеЕсли Вариант = РеквизитТаблицы Тогда
		
		Результат.ПравилаЗаполненияСтрокТаблицы = ПравилаЗаполненияСтрокТаблицы;
		Результат.Пояснение = НСтр("ru = 'Заполняется по правилам для отдельных реквизитов таблицы'");
		Результат.Картинка = 1;
		
	ИначеЕсли Вариант = ВыражениеНаВстроенномЯзыке Тогда
		
		Результат.ВычисляемоеВыражение = ВычисляемоеВыражение;
		Результат.МестоВыполненияВыражения = ?(ЗначениеЗаполнено(МестоВыполненияВыражения),
			МестоВыполненияВыражения,
			ПредопределенноеЗначение("Перечисление.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеДО"));
		Результат.Картинка = 3;
		
	ИначеЕсли Вариант = ИзШаблона Тогда
		
		Результат.Пояснение = НСтр("ru = 'Заполняется из шаблона'");
		Результат.Картинка = 5;
		
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьЗаполнениеТаблицы(Команда)
	
	НастроитьЗаполнениеНесколькихСтрокТаблицы();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НастроитьЗаполнениеНесколькихСтрокТаблицы()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПравилаЗаполненияСтрокТаблицы", ПравилаЗаполненияСтрокТаблицы);
	ПараметрыФормы.Вставить("ПолноеИмяОбъектаДО", ПолноеИмяОбъектаДО);
	ПараметрыФормы.Вставить("ВидДокумента", ВидДокумента);
	ПараметрыФормы.Вставить("ИмяРеквизитаОбъектаДО", ИмяРеквизитаОбъектаДО);
	ПараметрыФормы.Вставить("ПредставлениеРеквизитаОбъектаДО", ПредставлениеРеквизитаОбъектаДО);
	ПараметрыФормы.Вставить("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	ПараметрыФормы.Вставить("ПредставлениеИС", ПредставлениеИС);
	ПараметрыФормы.Вставить("ИмяМакета", ИмяМакета);
	ПараметрыФормы.Вставить("ПредставлениеОбъектаИС", ПредставлениеОбъектаИС);
	ПараметрыФормы.Вставить("ПолноеИмяОбъектаИС", ПолноеИмяОбъектаИС);
	ПараметрыФормы.Вставить("ПолноеИмяОбъектаДО", ПолноеИмяОбъектаДО);
	ПараметрыФормы.Вставить("ОбновлятьРодитель", Обновлять);
	ПараметрыФормы.Вставить("ЭтоПолноправныйПользователь", ЭтоПолноправныйПользователь);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("НастроитьЗаполнениеНесколькихСтрокТаблицыЗавершение", ЭтотОбъект);
	
	ОткрытьФорму(
		"Справочник.ПравилаЗагрузкиДанныхВДО.Форма.ЗаполнениеНесколькихСтрокТаблицы",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьЗаполнениеНесколькихСтрокТаблицыЗавершение(ТаблицаОбъектаДО, ПараметрыОповещения) Экспорт
	
	Если ТипЗнч(ТаблицаОбъектаДО) = Тип("ДанныеФормыКоллекция") Тогда
		НастроитьЗаполнениеНесколькихСтрокТаблицыЗавершениеНаСервере(ТаблицаОбъектаДО);
	КонецЕсли;
	
	ОбновитьЗаголовокКомандыНастройкиТаблицы(ПравилаЗаполненияСтрокТаблицы);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЗаполнениеНесколькихСтрокТаблицыЗавершениеНаСервере(ТаблицаОбъектаДО)
	
	Если ТаблицаОбъектаДО.Количество() > 0 Тогда
		КолонкиДляВыгрузки = Новый Массив;
		МассивРеквизитовТаблицыОбъектаДО = Справочники.ПравилаЗагрузкиДанныхВДО.РеквизитыТаблицыОбъектаДО(
			ПолноеИмяОбъектаДО,
			ИмяРеквизитаОбъектаДО,
			ВидДокумента);
		Для Каждого Элемент Из МассивРеквизитовТаблицыОбъектаДО Цикл
			Для Каждого Реквизит Из РаботаСИнтегрированнымиСистемами.МассивРеквизитовХранилищаПравил() Цикл
				КолонкиДляВыгрузки.Добавить(СтрШаблон(Реквизит.ШаблонИмени, Элемент.Имя));
			КонецЦикла;
		КонецЦикла;
		
		ПравилаЗаполненияСтрокТаблицы = Новый ХранилищеЗначения(
			ТаблицаОбъектаДО.Выгрузить(, СтрСоединить(КолонкиДляВыгрузки, ",")));
	Иначе
		ПравилаЗаполненияСтрокТаблицы = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаголовокКомандыНастройкиТаблицы(ПравилаЗаполненияСтрокТаблицы)
	
	КоличествоСтрок = РаботаСИнтегрированнымиСистемамиВызовСервера.КоличествоСтрокЗаполненияТаблицы(
		ПравилаЗаполненияСтрокТаблицы);
	
	Если КоличествоСтрок = 0 Тогда
		ТекущееСостояние = НСтр("ru = 'не настроено'");
	Иначе
		ТекущееСостояние = СтрШаблон(
			НСтр("ru = '%1 %2'"),
			?(КоличествоСтрок = 1, НСтр("ru = 'настроена'"), НСтр("ru = 'настроено'")),
			ПолучитьСклоненияСтрокиПоЧислу(НСтр("ru = 'строка'"), КоличествоСтрок)[0]);
	КонецЕсли;
	
	Элементы.НастроитьЗаполнение.Заголовок = СтрШаблон(
		НСтр("ru = 'Настроить заполнение таблицы (%1)'"),
		ТекущееСостояние);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступность()
	
	Элементы.ПредставлениеРеквизитаОбъектаИС.Доступность = (Вариант = РеквизитОбъекта);
	Элементы.ПредставлениеРеквизитаОбъектаИС.АвтоОтметкаНезаполненного = (Вариант = РеквизитОбъекта);
	Элементы.ПредставлениеРеквизитаОбъектаИС.ОтметкаНезаполненного = (Вариант = РеквизитОбъекта)
		И Не ЗначениеЗаполнено(ИмяРеквизитаОбъектаИС);
	Элементы.НастроитьЗаполнение.Доступность = (Вариант = РеквизитТаблицы);
	
	Элементы.ВычисляемоеВыражение.Доступность = ЭтоПолноправныйПользователь И (Вариант = ВыражениеНаВстроенномЯзыке);
	Элементы.ВычисляемоеВыражение.АвтоОтметкаНезаполненного = (Вариант = ВыражениеНаВстроенномЯзыке);
	Элементы.ВычисляемоеВыражение.ОтметкаНезаполненного = (Вариант = ВыражениеНаВстроенномЯзыке)
		И Не ЗначениеЗаполнено(ВычисляемоеВыражение);
	
	Элементы.Обновлять.Доступность = Не ШаблонЗапрещаетИзменение;
	Обновлять = Обновлять И Не ШаблонЗапрещаетИзменение;
	
КонецПроцедуры

#КонецОбласти
