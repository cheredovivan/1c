#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	Параметры.Свойство("ИмяМакета", ИмяМакета);
	Параметры.Свойство("ПолноеИмяОбъектаИС", ПолноеИмяОбъектаИС);
	Параметры.Свойство("ЭтоТаблица", ЭтоТаблица);
	Параметры.Свойство("Таблица", Таблица);
	Параметры.Свойство("ИмяРеквизитаОбъектаИС", ИмяРеквизитаОбъектаИС);
	Параметры.Свойство("ПредставлениеРеквизитаОбъектаДО", ПредставлениеРеквизитаОбъектаДО);
	
	Если ЭтоТаблица Тогда
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Выбор таблицы %1'"), Параметры.ПредставлениеОбъектаИС);
	Иначе
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Выбор реквизита %1'"), Параметры.ПредставлениеОбъектаИС);
	КонецЕсли;
	
	ЗаполнитьДеревоРеквизитов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоРеквизитов

&НаКлиенте
Процедура ДеревоРеквизитовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьЗначениеРеквизита();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоРеквизитовВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ВыбратьЗначениеРеквизита();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбратьЗначениеРеквизита();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьЗначениеРеквизита()
	
	ТекущиеДанные = Элементы.ДеревоРеквизитов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено
			Или (ТекущиеДанные.Имя = "" И Не ТекущиеДанные.ЭтоДополнительныйРеквизитИС)
			Или ТекущиеДанные.Недоступно Тогда
		Возврат;
	КонецЕсли;
	
	Имя = "";
	Представление = "";
	Если ЗначениеЗаполнено(ТекущиеДанные.Таблица) Тогда
		Для Каждого ЭлементДерева Из ДеревоРеквизитов.ПолучитьЭлементы() Цикл
			Если ЭлементДерева.ЭтоТаблица И ЭлементДерева.Имя = ТекущиеДанные.Таблица Тогда
				Имя = СтрШаблон("%1.%2", ЭлементДерева.Имя, ТекущиеДанные.Имя);
				Представление = СтрШаблон("%1.%2", ЭлементДерева.Представление, ТекущиеДанные.Представление);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Имя = "" Тогда
		Имя = ТекущиеДанные.Имя;
		Представление = ТекущиеДанные.Представление;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Имя", Имя);
	Результат.Вставить("Представление", Представление);
	Результат.Вставить("ЭтоТаблица", ТекущиеДанные.ЭтоТаблица);
	Результат.Вставить("Таблица", ТекущиеДанные.Таблица);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоРеквизитов()
	
	Если Не ЗначениеЗаполнено(ПолноеИмяОбъектаИС) Тогда
		ДеревоРеквизитов.ПолучитьЭлементы().Очистить();
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИнтегрированнаяСистема) Тогда
		СтруктураМетаданных = РаботаСИнтегрированнымиСистемами.СписокРеквизитовОбъектаИнтегрированнойСистемы(
			ИнтегрированнаяСистема, ПолноеИмяОбъектаИС);
	Иначе
		СтруктураМетаданных = РаботаСИнтегрированнымиСистемами.СписокРеквизитовОбъектаИнтегрированнойСистемы(
			ИмяМакета, ПолноеИмяОбъектаИС);
		Элементы.ДеревоРеквизитовТип.Видимость = Ложь;
	КонецЕсли;
	
	Если СтруктураМетаданных = Неопределено Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не удалось получить список реквизитов объекта %1'"), ПолноеИмяОбъектаИС);
	КонецЕсли;
	
	Дерево = РеквизитФормыВЗначение("ДеревоРеквизитов");
	
	Реквизиты = Неопределено;
	ТабличныеЧасти = Неопределено;
	Для Каждого ЭлементСпискаМетаданных Из СтруктураМетаданных Цикл
		Если ЭлементСпискаМетаданных.Имя = "Реквизиты" Тогда
			Реквизиты = ЭлементСпискаМетаданных.Строки;
		ИначеЕсли ЭлементСпискаМетаданных.Имя = "ТабличныеЧасти" Тогда
			ТабличныеЧасти = ЭлементСпискаМетаданных.Строки;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЭтоТаблица И ЗначениеЗаполнено(Реквизиты) Тогда
		
		Для Каждого Реквизит Из Реквизиты Цикл
			НоваяСтрока = Дерево.Строки.Добавить();
			НоваяСтрока.Имя = Реквизит.Имя;
			НоваяСтрока.Представление = Реквизит.Синоним;
			НоваяСтрока.Тип = ПредставлениеТипа(Реквизит.СписокТипов);
			Если СтрНайти(Реквизит.Имя, "ДопРеквизит_") = 1 Тогда
				НоваяСтрока.ЭтоДопРеквизит = Истина;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если (ЭтоТаблица Или ЗначениеЗаполнено(Таблица)) И ЗначениеЗаполнено(ТабличныеЧасти) Тогда
		
		Для Каждого ТабличнаяЧасть Из ТабличныеЧасти Цикл
			
			СтрокиДерева = Дерево.Строки;
			
			Если ЭтоТаблица Тогда
				
				НоваяСтрока = СтрокиДерева.Добавить();
				НоваяСтрока.Имя = ТабличнаяЧасть.Имя;
				НоваяСтрока.Представление = ТабличнаяЧасть.Синоним;
				НоваяСтрока.Тип = НСтр("ru = 'Таблица'");
				НоваяСтрока.ЭтоТаблица = Истина;
				
			Иначе
				
				НоваяСтрока = СтрокиДерева.Добавить();
				НоваяСтрока.Имя = ТабличнаяЧасть.Имя;
				НоваяСтрока.Представление = ТабличнаяЧасть.Синоним;
				НоваяСтрока.Тип = НСтр("ru = 'Таблица'");
				НоваяСтрока.ЭтоТаблица = Истина;
				НоваяСтрока.Недоступно = Истина;
				НоваяСтрока.Выделено = (Таблица = ТабличнаяЧасть.Имя);
				
				СтрокиДерева = НоваяСтрока.Строки;
				
				Для Каждого Реквизит Из ТабличнаяЧасть.Строки Цикл
					
					НоваяСтрока = СтрокиДерева.Добавить();
					НоваяСтрока.Имя = Реквизит.Имя;
					НоваяСтрока.Представление = Реквизит.Синоним;
					НоваяСтрока.Тип = ПредставлениеТипа(Реквизит.СписокТипов);
					НоваяСтрока.Таблица = ТабличнаяЧасть.Имя;
					НоваяСтрока.Недоступно = (Таблица <> ТабличнаяЧасть.Имя);
					
				КонецЦикла;
				
				СтрокиДерева.Сортировать("Представление");
				
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Дерево.Строки.Сортировать("ЭтоТаблица, ЭтоДопРеквизит, Представление");
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоРеквизитов");
	
	УстановитьТекущуюСтроку();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущуюСтроку()
	
	// Найдем таблицу.
	Если ЗначениеЗаполнено(Таблица) Тогда
		Для Каждого ЭлементДерева Из ДеревоРеквизитов.ПолучитьЭлементы() Цикл
			Если ЭлементДерева.ЭтоТаблица И ЭлементДерева.Имя = Таблица Тогда
				Элементы.ДеревоРеквизитов.ТекущаяСтрока = ЭлементДерева.ПолучитьИдентификатор();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Найдем ранее выбранный элемент.
	Если ЗначениеЗаполнено(ИмяРеквизитаОбъектаИС) Тогда
		Для Каждого ЭлементДерева Из ДеревоРеквизитов.ПолучитьЭлементы() Цикл
			Если ЭлементДерева.Имя = ИмяРеквизитаОбъектаИС И ЭлементДерева.Таблица = Таблица Тогда
				Элементы.ДеревоРеквизитов.ТекущаяСтрока = ЭлементДерева.ПолучитьИдентификатор();
				Возврат;
			КонецЕсли;
			Если Не ЭлементДерева.ЭтоТаблица Тогда
				Продолжить;
			КонецЕсли;
			Для Каждого ПодчиненныйЭлемент Из ЭлементДерева.ПолучитьЭлементы() Цикл
				ПолноеИмяРеквизита = СтрШаблон("%1.%2", ПодчиненныйЭлемент.Таблица, ПодчиненныйЭлемент.Имя);
				Если ПолноеИмяРеквизита = ИмяРеквизитаОбъектаИС Тогда
					Элементы.ДеревоРеквизитов.ТекущаяСтрока = ПодчиненныйЭлемент.ПолучитьИдентификатор();
					Возврат;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	// Найдем подходящий по представлению.
	Для Каждого ЭлементДерева Из ДеревоРеквизитов.ПолучитьЭлементы() Цикл
		Если ЭлементДерева.Представление = ПредставлениеРеквизитаОбъектаДО И ЭлементДерева.Таблица = Таблица Тогда
			Элементы.ДеревоРеквизитов.ТекущаяСтрока = ЭлементДерева.ПолучитьИдентификатор();
			Возврат;
		КонецЕсли;
		Если Не ЭлементДерева.ЭтоТаблица Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого ПодчиненныйЭлемент Из ЭлементДерева.ПолучитьЭлементы() Цикл
			Если ПодчиненныйЭлемент.Представление = ПредставлениеРеквизитаОбъектаДО
					И ПодчиненныйЭлемент.Таблица = Таблица Тогда
				Элементы.ДеревоРеквизитов.ТекущаяСтрока = ПодчиненныйЭлемент.ПолучитьИдентификатор();
				Возврат;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеТипа(СписокТипов)
	
	ПредельноеКоличествоТипов = 3;
	Если СписокТипов.Количество() > ПредельноеКоличествоТипов Тогда
		Возврат НСтр("ru = 'Составной тип'");
	Иначе
		МассивПредставлений = Новый Массив;
		Для Каждого Тип Из СписокТипов Цикл
			МассивПредставлений.Добавить(Тип.Представление);
		КонецЦикла;
		Возврат СтрСоединить(МассивПредставлений, ", ");
	КонецЕсли;
	
КонецФункции

#КонецОбласти