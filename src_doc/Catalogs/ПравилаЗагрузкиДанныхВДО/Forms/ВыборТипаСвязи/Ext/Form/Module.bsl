#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = СтрШаблон(
		НСтр("ru = 'Выбор типа связи с реквизитом ""%1""'"),
		Параметры.ПредставлениеРеквизитаОбъектаИС);
	
	ИнтегрированнаяСистема = Параметры.ИнтегрированнаяСистема;
	
	Для Каждого ТипОбъектаИС Из Параметры.ТипыОбъектаИС Цикл
		ТипыОбъектаИС.Добавить(ТипОбъектаИС.Значение, ТипОбъектаИС.Представление);
	КонецЦикла;
	
	СтрокаСсылкаИз = СсылкаИз.Добавить();
	СтрокаСсылкаИз.ВидДокументаДО = Параметры.ВидДокумента;
	СтрокаСсылкаИз.ТипОбъектаДО = Параметры.ТипОбъектаДО;
	СтрокаСсылкаИз.СсылкаИзПредставление = Параметры.ПредставлениеОбъектаДО;
	
	ОбновитьСсылкаНа();
	
	Для Каждого СуществующаяСвязь Из Параметры.СуществующиеСвязи Цикл
		Отбор = Новый Структура;
		Отбор.Вставить("ТипОбъектаДО", СуществующаяСвязь.ТипОбъектаДО);
		Отбор.Вставить("ВидДокументаДО", СуществующаяСвязь.ВидДокументаДО);
		Для Каждого НайденнаяСтрока Из СсылкаНа.НайтиСтроки(Отбор) Цикл
			НайденнаяСтрока.ТипСвязи = СуществующаяСвязь.ТипСвязи;
		КонецЦикла;
		ЗаполнитьЗначенияСвойств(ВсеКомментарии.Добавить(), СуществующаяСвязь);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСсылкаНа

&НаКлиенте
Процедура СсылкаНаПриАктивизацииСтроки(Элемент)
	
	ПараметрыСвязи = ТекущиеПараметрыСвязи();
	Если ПараметрыСвязи = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ОбновитьТипыСвязей(ПараметрыСвязи);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТипыСвязей

&НаКлиенте
Процедура ТипыСвязейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ТипыСвязейТипСвязи" Тогда
		
		ТипыСвязейТекущиеДанные = Элементы.ТипыСвязей.ТекущиеДанные;
		Если ТипыСвязейТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ПриВыбореТипаСвязи(Не ТипыСвязейТекущиеДанные.СоздаватьСвязь, ТипыСвязейТекущиеДанные);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипыСвязейКомментарийПриИзменении(Элемент)
	
	ТипыСвязейТекущиеДанные = Элементы.ТипыСвязей.ТекущиеДанные;
	Если ТипыСвязейТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СсылкаНаТекущиеДанные = Элементы.СсылкаНа.ТекущиеДанные;
	Если СсылкаНаТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ТипСвязи", ТипыСвязейТекущиеДанные.ТипСвязи);
	Отбор.Вставить("ТипОбъектаДО", СсылкаНаТекущиеДанные.ТипОбъектаДО);
	Отбор.Вставить("ВидДокументаДО", СсылкаНаТекущиеДанные.ВидДокументаДО);
	НайденныеСтроки = ВсеКомментарии.НайтиСтроки(Отбор);
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		СтрокаКомментарий = НайденныеСтроки[0];
	Иначе
		СтрокаКомментарий = ВсеКомментарии.Добавить();
		СтрокаКомментарий.ТипСвязи = ТипыСвязейТекущиеДанные.ТипСвязи;
		СтрокаКомментарий.ТипОбъектаДО = СсылкаНаТекущиеДанные.ТипОбъектаДО;
		СтрокаКомментарий.ВидДокументаДО = СсылкаНаТекущиеДанные.ВидДокументаДО;
	КонецЕсли;
	
	СтрокаКомментарий.Комментарий = ТипыСвязейТекущиеДанные.Комментарий;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипыСвязейСоздаватьСвязьПриИзменении(Элемент)
	
	ТипыСвязейТекущиеДанные = Элементы.ТипыСвязей.ТекущиеДанные;
	Если ТипыСвязейТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПриВыбореТипаСвязи(ТипыСвязейТекущиеДанные.СоздаватьСвязь, ТипыСвязейТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ПравилаСозданияСвязей = Новый Массив;
	
	Для Каждого СтрокаСсылкаНа Из СсылкаНа Цикл
		Если ЗначениеЗаполнено(СтрокаСсылкаНа.ТипСвязи) Тогда
			ПравилоСозданияСвязи = Новый Структура;
			ПравилоСозданияСвязи.Вставить("ТипОбъектаДО", СтрокаСсылкаНа.ТипОбъектаДО);
			ПравилоСозданияСвязи.Вставить("ВидДокументаДО", СтрокаСсылкаНа.ВидДокументаДО);
			ПравилоСозданияСвязи.Вставить("ТипСвязи", СтрокаСсылкаНа.ТипСвязи);
			УказанныйКомментарий = УказанныйКомментарий(
				ВсеКомментарии,
				СтрокаСсылкаНа.ТипСвязи,
				СтрокаСсылкаНа.ТипОбъектаДО,
				СтрокаСсылкаНа.ВидДокументаДО);
			ПравилоСозданияСвязи.Вставить("Комментарий", УказанныйКомментарий);
			ПравилаСозданияСвязей.Добавить(ПравилоСозданияСвязи);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(Новый Структура("ПравилаСозданияСвязей", ПравилаСозданияСвязей));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНастройкуСвязи(Команда)
	
	СсылкаНаТекущиеДанные = Элементы.СсылкаНа.ТекущиеДанные;
	Если СсылкаНаТекущиеДанные = Неопределено Или СсылкаНа.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не указан объект, связь с которым настраивается'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("СсылкаИз, СсылкаНа");
	Если ЗначениеЗаполнено(СсылкаИз[0].ВидДокументаДО) Тогда
		ПараметрыФормы.СсылкаИз = СсылкаИз[0].ВидДокументаДО;
	Иначе
		ПараметрыФормы.СсылкаИз = ПустаяСсылкаОбъектаДО(СсылкаИз[0].ТипОбъектаДО);
	КонецЕсли;
	Если ЗначениеЗаполнено(СсылкаНаТекущиеДанные.ВидДокументаДО) Тогда
		ПараметрыФормы.СсылкаНа = СсылкаНаТекущиеДанные.ВидДокументаДО;
	Иначе
		ПараметрыФормы.СсылкаНа = ПустаяСсылкаОбъектаДО(СсылкаНаТекущиеДанные.ТипОбъектаДО);
	КонецЕсли;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("СоздатьНастройкуСвязиЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("РегистрСведений.НастройкаСвязей.Форма.ФормаЗаписи", ПараметрыФормы,,,,, ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПравилоИнтеграции(Команда)
	
	Если ТипыОбъектаИС.Количество() > 1 Тогда
		Оповещение = Новый ОписаниеОповещения("ЗавершениеВыбораТипа", ЭтотОбъект);
		ПоказатьВыборИзСписка(Оповещение, ТипыОбъектаИС, Элементы.СоздатьПравилоИнтеграции);
	Иначе
		СоздатьПравилоИнтеграцииПослеВыбораТипаОбъектаИС(ТипыОбъектаИС[0].Значение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Клиент

&НаКлиенте
Процедура ЗавершениеВыбораТипа(ТипОбъектаИС, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ТипОбъектаИС) = Тип("ЭлементСпискаЗначений") Тогда
		СоздатьПравилоИнтеграцииПослеВыбораТипаОбъектаИС(ТипОбъектаИС.Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриВыбореТипаСвязи(Знач СоздаватьСвязь, ТипыСвязейТекущиеДанные)
	
	Для Каждого Строка Из ТипыСвязей Цикл
		Строка.СоздаватьСвязь = Ложь;
	КонецЦикла;
	
	СсылкаНаТекущиеДанные = Элементы.СсылкаНа.ТекущиеДанные;
	Если СсылкаНаТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СоздаватьСвязь Тогда
		СсылкаНаТекущиеДанные.ТипСвязи = ТипыСвязейТекущиеДанные.ТипСвязи;
		ТипыСвязейТекущиеДанные.СоздаватьСвязь = СоздаватьСвязь;
	Иначе
		СсылкаНаТекущиеДанные.ТипСвязи = ПредопределенноеЗначение("Справочник.ТипыСвязей.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНастройкуСвязиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПараметрыСвязи = ТекущиеПараметрыСвязи();
	Если ПараметрыСвязи = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ОбновитьТипыСвязей(ПараметрыСвязи);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПравилоИнтеграцииПослеВыбораТипаОбъектаИС(ТипОбъектаИС)
	
	ПараметрыФормы = Новый Структура("ТипОбъектаИС", ТипОбъектаИС);
	ПараметрыФормы.Вставить("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("СоздатьПравилоИнтеграцииЗавершение", ЭтотОбъект);
	
	СоздаватьНовоеПравилоВДО = Ложь;
	ПравилаЗагрузкиВДОИспользуемыеВИС = ПравилаЗагрузкиВДОИспользуемыеВИС(ТипОбъектаИС, СоздаватьНовоеПравилоВДО);
	
	Если СоздаватьНовоеПравилоВДО Тогда
		ОткрываемаяФорма = "Справочник.ПравилаЗагрузкиДанныхВДО.Форма.ФормаЭлемента";
		ПараметрыФормы.Вставить("СоздаватьСвязанноеПравилоИнтеграции", Истина);
		ПараметрыФормы.Вставить("СписокВыбораОбъектовДО", Новый СписокЗначений);
		ПараметрыФормы.СписокВыбораОбъектовДО.Добавить("Справочник.ДокументыПредприятия", НСтр("ru = 'Документ'"));
		ПараметрыФормы.СписокВыбораОбъектовДО.Добавить("Справочник.Мероприятия", НСтр("ru = 'Мероприятие'"));
	Иначе
		ОткрываемаяФорма = "Справочник.ПравилаЗагрузкиДанныхВДО.Форма.ФормаВыбораПравилаБезСвязи";
		ПараметрыФормы.Вставить("ПравилаЗагрузкиВДОИспользуемыеВИС", ПравилаЗагрузкиВДОИспользуемыеВИС);
	КонецЕсли;
	
	ОткрытьФорму(
		ОткрываемаяФорма,
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор,,,
		ОповещениеОЗакрытии,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПравилоИнтеграцииЗавершение(ПравилоЗагрузкиВДО, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ПравилоЗагрузкиВДО) Тогда
		СоздатьСвязанноеПравилоИнтеграцииВИС(ПравилоЗагрузкиВДО);
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
	ОбновитьСсылкаНа();
	
КонецПроцедуры

&НаКлиенте
Функция ТекущиеПараметрыСвязи()
	
	СсылкаНаТекущиеДанные = Элементы.СсылкаНа.ТекущиеДанные;
	
	Если СсылкаНаТекущиеДанные = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыСвязи = Новый Структура;
	
	Если СсылкаИз.Количество() > 0 Тогда
		ПараметрыСвязи.Вставить("СсылкаИзВидДокументаДО", СсылкаИз[0].ВидДокументаДО);
		ПараметрыСвязи.Вставить("СсылкаИзТипОбъектаДО", СсылкаИз[0].ТипОбъектаДО);
	КонецЕсли;
	
	ПараметрыСвязи.Вставить("СсылкаНаВидДокументаДО", СсылкаНаТекущиеДанные.ВидДокументаДО);
	ПараметрыСвязи.Вставить("СсылкаНаТипОбъектаДО", СсылкаНаТекущиеДанные.ТипОбъектаДО);
	ПараметрыСвязи.Вставить("ТипСвязи", СсылкаНаТекущиеДанные.ТипСвязи);
	
	Возврат ПараметрыСвязи;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция УказанныйКомментарий(ВсеКомментарии, ТипСвязи, ТипОбъектаДО, ВидДокументаДО)
	
	УказанныйКомментарий = "";
	
	НайденныеСтроки = ВсеКомментарии.НайтиСтроки(
		Новый Структура("ТипСвязи, ТипОбъектаДО, ВидДокументаДО", ТипСвязи, ТипОбъектаДО, ВидДокументаДО));
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		УказанныйКомментарий = НайденныеСтроки[0].Комментарий;
	КонецЕсли;
	
	Возврат УказанныйКомментарий;
	
КонецФункции

#КонецОбласти

#Область Сервер

&НаСервере
Процедура ОбновитьСсылкаНа()
	
	Для Каждого ТипОбъектаИС Из ТипыОбъектаИС Цикл
		
		СтруктураМетаданных = РаботаСИнтегрированнымиСистемамиПовтИсп.СтруктураМетаданныхОбъектаИнтегрированнойСистемы(
			ИнтегрированнаяСистема,
			ТипОбъектаИС.Значение);
		
		Для Каждого ПравилоИнтеграции Из СтруктураМетаданных.ПравилаИнтеграции Цикл
			
			Если ПравилоИнтеграции.ТипОбъектаДО = "DMCorrespondent" Тогда
				Продолжить;
			КонецЕсли;
			
			ТипОбъектаДО = "";
			ОбработкаЗапросовXDTO.ПолучитьТипДОПоТипуXDTO(ПравилоИнтеграции.ТипОбъектаДО, ТипОбъектаДО);
			ПредставлениеОбъектаДО = Метаданные.НайтиПоПолномуИмени(ТипОбъектаДО).ПредставлениеОбъекта;
			
			ВидДокументаСсылка = Справочники.ВидыДокументов.ПустаяСсылка();
			Если ЗначениеЗаполнено(ПравилоИнтеграции.ВидДокументаID) Тогда
				ИдентификаторВидаДокумента = Новый УникальныйИдентификатор(ПравилоИнтеграции.ВидДокументаID);
				ВидДокументаСсылка = Справочники.ВидыДокументов.ПолучитьСсылку(ИдентификаторВидаДокумента);
				Если Не ОбщегоНазначения.СсылкаСуществует(ВидДокументаСсылка) Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Отбор = Новый Структура;
			Отбор.Вставить("ТипОбъектаДО", ТипОбъектаДО);
			Отбор.Вставить("ВидДокументаДО", ВидДокументаСсылка);
			НайденныеСтроки = СсылкаНа.НайтиСтроки(Отбор);
			
			Если НайденныеСтроки.Количество() = 0 Тогда
				СтрокаОбъектДО = СсылкаНа.Добавить();
				СтрокаОбъектДО.ТипОбъектаДО = ТипОбъектаДО;
				СтрокаОбъектДО.ВидДокументаДО = ВидДокументаСсылка;
				МассивПредставлениеОбъектаДО = Новый Массив;
				МассивПредставлениеОбъектаДО.Добавить(ПредставлениеОбъектаДО);
				Если ЗначениеЗаполнено(ВидДокументаСсылка) Тогда
					МассивПредставлениеОбъектаДО.Добавить(Строка(ВидДокументаСсылка));
				КонецЕсли;
				СтрокаОбъектДО.СсылкаНаПредставление = СтрСоединить(МассивПредставлениеОбъектаДО, ", ");
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	СсылкаНа.Сортировать("СсылкаНаПредставление");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТипыСвязей(Знач ТекущиеПараметрыСвязи)
	
	ТипыСвязей.Очистить();
	
	ПараметрСсылкаИз = Новый Массив;
	Если ЗначениеЗаполнено(ТекущиеПараметрыСвязи.СсылкаИзВидДокументаДО) Тогда
		ПараметрСсылкаИз.Добавить(ТекущиеПараметрыСвязи.СсылкаИзВидДокументаДО);
		ПараметрСсылкаИз.Добавить(Справочники.ВидыДокументов.ПустаяСсылка());
	Иначе
		ПараметрСсылкаИз.Добавить(ПустаяСсылкаОбъектаДО(ТекущиеПараметрыСвязи.СсылкаИзТипОбъектаДО));
	КонецЕсли;
	
	ПараметрСсылкаНа = Новый Массив;
	Если ЗначениеЗаполнено(ТекущиеПараметрыСвязи.СсылкаНаВидДокументаДО) Тогда
		ПараметрСсылкаНа.Добавить(ТекущиеПараметрыСвязи.СсылкаНаВидДокументаДО);
		ПараметрСсылкаНа.Добавить(Справочники.ВидыДокументов.ПустаяСсылка());
	Иначе
		ПараметрСсылкаНа.Добавить(ПустаяСсылкаОбъектаДО(ТекущиеПараметрыСвязи.СсылкаНаТипОбъектаДО));
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	НастройкаСвязей.ТипСвязи КАК ТипСвязи,
		|	НастройкаСвязей.СсылкаИз КАК СсылкаИз,
		|	НастройкаСвязей.СсылкаНа КАК СсылкаНа
		|ИЗ
		|	РегистрСведений.НастройкаСвязей КАК НастройкаСвязей
		|ГДЕ
		|	НастройкаСвязей.СсылкаИз В (&СсылкаИз)
		|	И НастройкаСвязей.СсылкаНа В (&СсылкаНа)");
	Запрос.УстановитьПараметр("СсылкаИз", ПараметрСсылкаИз);
	Запрос.УстановитьПараметр("СсылкаНа", ПараметрСсылкаНа);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НайденныеСтроки = ТипыСвязей.НайтиСтроки(Новый Структура("ТипСвязи", Выборка.ТипСвязи));
		Если НайденныеСтроки.Количество() = 0
				И СвязиОбъектов.ЭтоПодходящийТипСвязи(Выборка.СсылкаИз, Выборка.СсылкаНа, Выборка.ТипСвязи) Тогда
			СтрокаТипСвязи = ТипыСвязей.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТипСвязи, Выборка);
			СтрокаТипСвязи.Комментарий = УказанныйКомментарий(
				ВсеКомментарии,
				СтрокаТипСвязи.ТипСвязи,
				ТекущиеПараметрыСвязи.СсылкаНаТипОбъектаДО,
				ТекущиеПараметрыСвязи.СсылкаНаВидДокументаДО);
			Если Выборка.ТипСвязи = ТекущиеПараметрыСвязи.ТипСвязи Тогда
				СтрокаТипСвязи.СоздаватьСвязь = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПравилаЗагрузкиВДОИспользуемыеВИС(Знач ТипОбъектаИС, СоздаватьНовоеПравилоВДО = Неопределено)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПравилаЗагрузкиДанныхВДО.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПравилаЗагрузкиДанныхВДО КАК ПравилаЗагрузкиДанныхВДО
		|ГДЕ
		|	НЕ ПравилаЗагрузкиДанныхВДО.ПометкаУдаления
		|	И ПравилаЗагрузкиДанныхВДО.ТипОбъектаДО <> ""Справочник.Контрагенты""
		|	И ПравилаЗагрузкиДанныхВДО.ТипОбъектаИС = &ТипОбъектаИС
		|	И ПравилаЗагрузкиДанныхВДО.УзелИнтегрированнойСистемы = &ИнтегрированнаяСистема");
	Запрос.УстановитьПараметр("ТипОбъектаИС", ТипОбъектаИС);
	Запрос.УстановитьПараметр("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	
	ПравилаЗагрузкиВДО = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ПравилаЗагрузкиВДОИспользуемыеВИС = Новый СписокЗначений;
	
	Если ПравилаЗагрузкиВДО.Количество() > 0 Тогда
		ДанныеДляПодключения = РаботаСИнтегрированнымиСистемамиПовтИсп.ДанныеДляПодключения(ИнтегрированнаяСистема);
		ПакетЗапросов = РаботаСИнтегрированнымиСистемами.СоздатьОбъект(ДанныеДляПодключения, "DMILBatchRequest");
		
		Для Каждого ПравилоЗагрузкиВДО Из ПравилаЗагрузкиВДО Цикл
			Запрос = РаботаСИнтегрированнымиСистемами.СоздатьОбъект(
				ДанныеДляПодключения,
				"DMILRelatedIntegrationRulesExistenceRequest");
			Запрос.relatedDataLoadingRule = Строка(ПравилоЗагрузкиВДО.УникальныйИдентификатор());
			ПакетЗапросов.requests.Добавить(Запрос);
		КонецЦикла;
		
		ОтветНаЗапросыПакета = ДанныеДляПодключения.Прокси.execute(ПакетЗапросов);
		РаботаСИнтегрированнымиСистемами.ПроверитьВозвратВебСервиса(ДанныеДляПодключения, ОтветНаЗапросыПакета);
		Для Индекс = 0 По ОтветНаЗапросыПакета.responses.Количество() - 1 Цикл
			Ответ = ОтветНаЗапросыПакета.responses[Индекс];
			Если Ответ.exist Тогда
				ПравилаЗагрузкиВДОИспользуемыеВИС.Добавить(ПравилаЗагрузкиВДО[Индекс]);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СоздаватьНовоеПравилоВДО = (ПравилаЗагрузкиВДО.Количество() = ПравилаЗагрузкиВДОИспользуемыеВИС.Количество());
	
	Возврат ПравилаЗагрузкиВДОИспользуемыеВИС;
	
КонецФункции

&НаСервере
Функция ПустаяСсылкаОбъектаДО(Знач ПредставлениеОбъектаДО)
	
	МенеджерОбъектаДО = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПредставлениеОбъектаДО);
	
	Возврат МенеджерОбъектаДО.ПустаяСсылка();
	
КонецФункции

&НаСервере
Процедура СоздатьСвязанноеПравилоИнтеграцииВИС(Знач ПравилоЗагрузкиВДО)
	
	РеквизитыПравила = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПравилоЗагрузкиВДО,
		"ВидДокумента, ТипОбъектаДО, ТипОбъектаИС, ПредставлениеОбъектаДО, Комментарий");
	РаботаСИнтегрированнымиСистемамиВызовСервера.СоздатьСвязанноеПравилоИнтеграцииВИС(
		ИнтегрированнаяСистема,
		ПравилоЗагрузкиВДО,
		РеквизитыПравила.ВидДокумента,
		РеквизитыПравила.ТипОбъектаДО,
		РеквизитыПравила.ТипОбъектаИС,
		РеквизитыПравила.ПредставлениеОбъектаДО,
		РеквизитыПравила.Комментарий);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти