#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	Параметры.Свойство("ПредставлениеИС", ПредставлениеИС);
	Параметры.Свойство("ИмяМакета", ИмяМакета);
	
	Если ЗначениеЗаполнено(ИнтегрированнаяСистема) Тогда
		Если ЗначениеЗаполнено(ПредставлениеИС) Тогда
			Элементы.МестоВыполненияВыражения.СписокВыбора.Очистить();
			Элементы.МестоВыполненияВыражения.СписокВыбора.Добавить(
				Перечисления.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеДО,
				НСтр("ru = 'На стороне 1С:Документооборот'"));
			Элементы.МестоВыполненияВыражения.СписокВыбора.Добавить(
				Перечисления.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеИС,
				СтрШаблон(НСтр("ru = 'На стороне %1'"), ПредставлениеИС));
		КонецЕсли;
	Иначе
		Элементы.МестоВыполненияВыражения.Видимость = Ложь;
	КонецЕсли;
	
	Параметры.Свойство("ПолноеИмяОбъектаДО", ПолноеИмяОбъектаДО);
	Параметры.Свойство("ПолноеИмяОбъектаИС", ПолноеИмяОбъектаИС);
	Параметры.Свойство("ВидДокумента", ВидДокумента);
	Параметры.Свойство("ИмяРеквизитаОбъектаДО", ИмяРеквизитаОбъектаДО);
	Параметры.Свойство("ЭтоТаблица", ЭтоТаблица);
	Параметры.Свойство("Таблица", Таблица);
	Параметры.Свойство("ВычисляемоеВыражение", ВычисляемоеВыражение);
	Параметры.Свойство("МестоВыполненияВыражения", МестоВыполненияВыражения);
	Параметры.Свойство("ДоступнаТекущаяСтрока", ДоступнаТекущаяСтрока);
	
	Если Не ЗначениеЗаполнено(МестоВыполненияВыражения) Тогда
		МестоВыполненияВыражения = Перечисления.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеДО;
	КонецЕсли;
	
	ЗаполнитьИнструкцию();
	
	Контекст = КонтекстВыраженияНаВстроенномЯзыке(ПолноеИмяОбъектаДО, ПолноеИмяОбъектаИС);
	РедакторВстроенногоЯзыка.ПриСозданииНаСервере(ЭтотОбъект, "Редактор", Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РедакторВстроенногоЯзыкаКлиент.ПриОткрытии(
		Элементы.РедакторВстроенногоЯзыка,
		Элементы.ВычисляемоеВыражение,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И Не ЗакрытиеПодтверждено Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Закрыть окно ввода выражения без сохранения?
			|Все внесенные изменения будут потеряны.'");
		ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНет(ОписаниеОповещения, ТекстВопроса);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИнструкцияПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
#Если Не ВебКлиент Тогда
	Если ЭтоРаскрываемаяГруппа(ДанныеСобытия.Button) Или ЭтоРаскрываемаяГруппа(ДанныеСобытия.Element) Тогда
		// Если пользователь нажал на раскрываемую группу, то требуется только стандартная обработка.
		Возврат;
	КонецЕсли;
	
	// В противном случае - обработаем нажатие на гиперссылку.
	СтандартнаяОбработка = Ложь;
#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура МестоВыполненияВыраженияПриИзменении(Элемент)
	
	ЗаполнитьИнструкцию();
	РедакторВстроенногоЯзыкаКлиент.УстановитьКонтекст(
		Элементы.РедакторВстроенногоЯзыка,
		ЭтотОбъект,
		КонтекстВыраженияНаВстроенномЯзыке(ПолноеИмяОбъектаДО, ПолноеИмяОбъектаИС));
	
КонецПроцедуры

&НаКлиенте
Процедура РедакторВстроенногоЯзыкаДокументСформирован(Элемент)
	
	Если РедакторВстроенногоЯзыкаКлиент.ДокументСформирован(Элемент, ЭтотОбъект, ВычисляемоеВыражение) Тогда
		ПодключитьОбработчикОжидания("ПослеЗагрузкиРедактора", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедакторВстроенногоЯзыкаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	НажатаКнопкаEsc = Ложь;
	РедакторВстроенногоЯзыкаКлиент.ПриНажатии(
		Элемент,
		ДанныеСобытия,
		СтандартнаяОбработка,
		ЭтотОбъект,
		НажатаКнопкаEsc);
	Если НажатаКнопкаEsc Тогда
		ПриНажатииКлавишиОтмена();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	РезультатВыполнения = ПроверитьВыражениеНаВстроенномЯзыке(
		РедакторВстроенногоЯзыкаКлиент.ПолучитьТекст(Элементы.РедакторВстроенногоЯзыка, ВычисляемоеВыражение),
		ПолноеИмяОбъектаДО,
		ПолноеИмяОбъектаИС);
	Если Не РезультатВыполнения.Успешно Тогда
		ОчиститьСообщения();
		РедакторВстроенногоЯзыкаКлиент.ОбработатьРезультатВыполнения(Элементы.РедакторВстроенногоЯзыка, РезультатВыполнения);
		Возврат;
	КонецЕсли;
	
	Ответ = Новый Структура;
	Ответ.Вставить("ВычисляемоеВыражение",
		РедакторВстроенногоЯзыкаКлиент.ПолучитьТекст(Элементы.РедакторВстроенногоЯзыка, ВычисляемоеВыражение));
	Ответ.Вставить("МестоВыполненияВыражения", МестоВыполненияВыражения);
	
	ЗакрытиеПодтверждено = Истина;
	Закрыть(Ответ);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	ПриНажатииКлавишиОтмена();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОчистить(Команда)
	
	ТекстВРедакторе =
		РедакторВстроенногоЯзыкаКлиент.ПолучитьТекст(Элементы.РедакторВстроенногоЯзыка, ВычисляемоеВыражение);
	Если Не ПустаяСтрока(ТекстВРедакторе) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("КомандаОчиститьЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Очистить введенное выражение?'");
		ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНет(ОписаниеОповещения, ТекстВопроса);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПроверить(Команда)
	
	ОчиститьСообщения();
	
	РезультатВыполнения = ПроверитьВыражениеНаВстроенномЯзыке(
		РедакторВстроенногоЯзыкаКлиент.ПолучитьТекст(Элементы.РедакторВстроенногоЯзыка, ВычисляемоеВыражение),
		ПолноеИмяОбъектаДО,
		ПолноеИмяОбъектаИС);
	РедакторВстроенногоЯзыкаКлиент.ОбработатьРезультатВыполнения(Элементы.РедакторВстроенногоЯзыка, РезультатВыполнения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ВывестиИмяРеквизитаДО(ИнструкцияМассив, СтруктураРеквизита, ИмяРеквизита)
	
	Если ТипЗнч(СтруктураРеквизита.Тип) = Тип("СписокЗначений")
				И СтруктураРеквизита.Тип.Количество() > 0
				И Лев(СтруктураРеквизита.Тип[0], 2) = "DM" Тогда
			ИнструкцияМассив.Добавить("<td><a href=""#");
			ИнструкцияМассив.Добавить(СтруктураРеквизита.Тип[0]);
			ИнструкцияМассив.Добавить(""">");
			ИнструкцияМассив.Добавить(ИмяРеквизита);
			ИнструкцияМассив.Добавить("</a></td>");
		Иначе
			ИнструкцияМассив.Добавить("<td>");
			ИнструкцияМассив.Добавить(ИмяРеквизита);
			ИнструкцияМассив.Добавить("</td>");
		КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВывестиПримерВыражения(ИнструкцияМассив, ЗаголовокРаздела, ПримерВыражения)
	
	Если ИнструкцияМассив[ИнструкцияМассив.ВГраница()] <> "</details>" Тогда
		ИнструкцияМассив.Добавить("<p>");
	КонецЕсли;
	
	ДобавитьСворачиваемуюГруппу(ИнструкцияМассив, Истина);
	ИнструкцияМассив.Добавить("<summary class=""pointer""><u>");
	ИнструкцияМассив.Добавить(ЗаголовокРаздела);
	ИнструкцияМассив.Добавить("</u></summary>");
	ИнструкцияМассив.Добавить("<blockquote><pre>");
	ИнструкцияМассив.Добавить(ПримерВыражения);
	ИнструкцияМассив.Добавить("</pre></blockquote>");
	ДобавитьСворачиваемуюГруппу(ИнструкцияМассив, Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиРеквизит(ИнструкцияМассив, СоставРеквизитов, ВыводРеквизитовОбъектаДО, СтруктураРеквизита,
		ВыведенЗаголовокДопРеквизитов, ИдентификаторДопРеквизита, ИмяДопРеквизитаИС)
	
	Если ЗначениеЗаполнено(СтруктураРеквизита.Таблица) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ВыводРеквизитовОбъектаДО
			И СтрНайти(СтруктураРеквизита.Имя, "ДопРеквизит_") = 1
			И МестоВыполненияВыражения = Перечисления.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеДО Тогда
		ИмяДопРеквизитаИС = СтруктураРеквизита.Имя;
	КонецЕсли;
	
	Если СтруктураРеквизита.ЭтоДополнительныйРеквизит И Не ВыведенЗаголовокДопРеквизитов Тогда
		ИнструкцияМассив.Добавить("<tr>");
		
		ИнструкцияМассив.Добавить("<td><b>");
		ИнструкцияМассив.Добавить(НСтр("ru = 'Дополнительные реквизиты:'"));
		ИнструкцияМассив.Добавить("</b></td>");
		
		ИнструкцияМассив.Добавить("</tr>");
		ВыведенЗаголовокДопРеквизитов = Истина;
	КонецЕсли;
	
	Если СтруктураРеквизита.ЭтоТаблица Тогда
		ИнструкцияМассив.Добавить("<tr>");
		
		ИнструкцияМассив.Добавить("<td><b>");
		ИнструкцияМассив.Добавить(СтрШаблон(НСтр("ru = 'Табличная часть %1'"), СтруктураРеквизита.Имя));
		ИнструкцияМассив.Добавить("</b></td>");
		
		ИнструкцияМассив.Добавить("<td><b>");
		ИнструкцияМассив.Добавить(СтруктураРеквизита.Представление);
		ИнструкцияМассив.Добавить("</b></td>");
		
		ИнструкцияМассив.Добавить("</tr>");
		
		РеквизитыТаблицы = СоставРеквизитов.НайтиСтроки(Новый Структура("Таблица", СтруктураРеквизита.Имя));
		Для Каждого РеквизитТаблицы Из РеквизитыТаблицы Цикл
			ИнструкцияМассив.Добавить("<tr>");
			
			ВывестиИмяРеквизитаДО(ИнструкцияМассив, РеквизитТаблицы, РеквизитТаблицы.Имя);
			
			ИнструкцияМассив.Добавить("<td>");
			ИнструкцияМассив.Добавить(РеквизитТаблицы.Представление);
			ИнструкцияМассив.Добавить("</td>");
			
			ИнструкцияМассив.Добавить("</tr>");
		КонецЦикла;
		Возврат;
	КонецЕсли;
	
	Если СтруктураРеквизита.ЭтоДополнительныйРеквизит Тогда
		Если ВыводРеквизитовОбъектаДО Тогда
			ИдентификаторДопРеквизита = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				СтруктураРеквизита.ДополнительныйРеквизит,
				"ИдентификаторДляФормул");
			ИмяРеквизита = СтрШаблон("ID: ""%1""<br>ИдентификаторДляФормул: ""%2""</br>",
				СтруктураРеквизита.ДополнительныйРеквизит.УникальныйИдентификатор(),
				ИдентификаторДопРеквизита);
		Иначе
			ИдентификаторДопРеквизита = ИдентификаторДопРеквизита(СтруктураРеквизита.Имя);
			ИмяРеквизита = СтрШаблон("ID: ""%1""", ИдентификаторДопРеквизита);
		КонецЕсли;
	Иначе
		ИмяРеквизита = СтруктураРеквизита.Имя;
	КонецЕсли;
	
	ИнструкцияМассив.Добавить("<tr>");
	
	ВывестиИмяРеквизитаДО(ИнструкцияМассив, СтруктураРеквизита, ИмяРеквизита);
	
	ИнструкцияМассив.Добавить("<td>");
	ИнструкцияМассив.Добавить(СтруктураРеквизита.Представление);
	ИнструкцияМассив.Добавить("</td>");
	
	ИнструкцияМассив.Добавить("</tr>");
	
КонецПроцедуры

&НаСервере
Процедура ВывестиТаблицуРеквизитов(ИнструкцияМассив, СоставРеквизитов, ВыводРеквизитовОбъектаДО = Ложь)
	
	ИнструкцияМассив.Добавить("<table>");
	
	ИнструкцияМассив.Добавить("<tr>");
	
	ИнструкцияМассив.Добавить("<td><b>");
	ИнструкцияМассив.Добавить(НСтр("ru ='Имя реквизита'"));
	ИнструкцияМассив.Добавить("</b></td>");
	
	ИнструкцияМассив.Добавить("<td><b>");
	ИнструкцияМассив.Добавить(НСтр("ru ='Синоним'"));
	ИнструкцияМассив.Добавить("</b></td>");
	
	ИнструкцияМассив.Добавить("</tr>");
	
	ВыведенЗаголовокДопРеквизитов = Ложь;
	ИдентификаторДопРеквизита = "00000000-0000-0000-0000-000000000000";
	ИмяДопРеквизитаИС = "";
	
	Для Каждого СтруктураРеквизита Из СоставРеквизитов Цикл
		ВывестиРеквизит(
			ИнструкцияМассив,
			СоставРеквизитов,
			ВыводРеквизитовОбъектаДО,
			СтруктураРеквизита,
			ВыведенЗаголовокДопРеквизитов,
			ИдентификаторДопРеквизита,
			ИмяДопРеквизитаИС);
	КонецЦикла;
	
	ИнструкцияМассив.Добавить("</table>");
	
	ИнструкцияМассив.Добавить("</pre></blockquote>");
	ДобавитьСворачиваемуюГруппу(ИнструкцияМассив, Ложь);
	
	// Выведем примеры выражений.
	ИнструкцияМассив.Добавить("<p>");
	Если ВыведенЗаголовокДопРеквизитов Тогда
		Если ВыводРеквизитовОбъектаДО Тогда
			ЗаголовокРаздела = НСтр("ru = 'Пример обращения к дополнительным реквизитам приемника'");
			ПримерВыражения =
				"ЗначениеДопРеквизита = Неопределено;
				|Для Каждого Строка Из Параметры.Приемник.ДополнительныеРеквизиты Цикл
				|	Если Строка.Свойство.ИдентификаторДляФормул = ""ИдентификаторДопРеквизита"" Тогда
				|		ЗначениеДопРеквизита = Строка.Значение;
				|	КонецЕсли;
				|КонецЦикла;";
		Иначе
			ЗаголовокРаздела = НСтр("ru = 'Пример обращения к дополнительным реквизитам источника'");
			Если ЭтоТаблица Тогда
				ПримерВыражения =
					"ЗначениеДопРеквизита = Неопределено;
					|Для Каждого Строка Из Параметры.Источник.ДополнительныеРеквизиты Цикл
					|	Если СокрЛП(Строка.Свойство.УникальныйИдентификатор()) = ""ИдентификаторДопРеквизита"" Тогда
					|		ЗначениеДопРеквизита = Строка.Значение;
					|	КонецЕсли;
					|КонецЦикла;";
			Иначе
				ПримерВыражения =
					"Для Каждого Строка Из Параметры.Источник.ДополнительныеРеквизиты Цикл
					|	Если СокрЛП(Строка.Свойство.УникальныйИдентификатор()) = ""ИдентификаторДопРеквизита"" Тогда
					|		Параметры.Результат = Строка.Значение;
					|	КонецЕсли;
					|КонецЦикла;";
			КонецЕсли;
		КонецЕсли;
		ПримерВыражения = СтрЗаменить(ПримерВыражения, "ИдентификаторДопРеквизита", ИдентификаторДопРеквизита);
		ВывестиПримерВыражения(ИнструкцияМассив, ЗаголовокРаздела, ПримерВыражения);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ИмяДопРеквизитаИС) Тогда
		ЗаголовокРаздела = НСтр("ru = 'Пример обращения к дополнительным реквизитам источника'");
		Если ЭтоТаблица Тогда
			ПримерВыражения = "ЗначениеДопРеквизита = Параметры.Источник.ИмяДопРеквизитаИС;";
		Иначе
			ПримерВыражения = "Параметры.Результат = Параметры.Источник.ИмяДопРеквизитаИС;";
		КонецЕсли;
		ПримерВыражения = СтрЗаменить(ПримерВыражения, "ИмяДопРеквизитаИС", ИмяДопРеквизитаИС);
		ВывестиПримерВыражения(ИнструкцияМассив, ЗаголовокРаздела, ПримерВыражения);
	КонецЕсли;
	
	Если Не ВыводРеквизитовОбъектаДО
			И МестоВыполненияВыражения = Перечисления.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеДО Тогда
		Если ЭтоТаблица Тогда
			Если ЭтоТаблицаУчастниковДействия Тогда
				ЗаголовокРаздела = НСтр("ru = 'Пример заполнения участников действия обработки документа'");
				ПримерВыражения =
					"Параметры.Результат = Новый ТаблицаЗначений;
					|Параметры.Результат.Колонки.Добавить(""ИдентификаторЭтапа"");
					|Параметры.Результат.Колонки.Добавить(""Участник"");
					|Параметры.Результат.Колонки.Добавить(""Защищенный"");
					|Параметры.Результат.Колонки.Добавить(""Срок"");
					|Параметры.Результат.Колонки.Добавить(""Описание"");
					|Для Каждого СтрокаСотрудник Из Параметры.Источник.Сотрудники Цикл
					|	УчастникДействия = Параметры.Результат.Добавить();
					|	УчастникДействия.ИдентификаторЭтапа = ""78054c9f-7133-4b4c-a778-0b61af777bee"";
					|	УчастникДействия.Участник = СтрокаСотрудник.Сотрудник;
					|	УчастникДействия.Защищенный = Истина;
					|	УчастникДействия.Срок = КалендарныеГрафикиДокументооборот.ПолучитьДатуПоОсновномуКалендарю(ТекущаяДата(), 3);
					|	УчастникДействия.Описание = ""Загружен через интеграцию"";
					|КонецЦикла;";
				ВывестиПримерВыражения(ИнструкцияМассив, ЗаголовокРаздела, ПримерВыражения);
				Если ДоступенВыборПравилаОбработки Тогда
					ЗаголовокРаздела = НСтр("ru = 'Пример добавления участника действия, с учетом разреза правила обработки'");
					ПримерВыражения =
						"Параметры.Результат = Новый ТаблицаЗначений;
						|Параметры.Результат.Колонки.Добавить(""Участник"");
						|// Добавим руководители из интегрированного документа, только если
						|// текущее правило обработки не равно разрезу ""Дирекция"", поскольку
						|// в разрезе ""Дирекция"" нужный участник действия уже указан.
						|РазрезПравилаОбработкиПоПодразделениюДирекция = ""9e85e9a6-2128-11f0-bd17-a036bce7bcec"";
						|Если Параметры.ТекущееПравилоОбработки <> РазрезПравилаОбработкиПоПодразделениюДирекция Тогда
						|	УчастникДействия = Параметры.Результат.Добавить();
						|	УчастникДействия.Участник = Параметры.Источник.Руководитель;
						|КонецЕсли;";
					ВывестиПримерВыражения(ИнструкцияМассив, ЗаголовокРаздела, ПримерВыражения);
				КонецЕсли;
			Иначе
				ЗаголовокРаздела = НСтр("ru = 'Пример заполнения табличной части 1С:Документооборота'");
				ПримерВыражения =
					"Параметры.Результат = Новый ТаблицаЗначений;
					|Параметры.Результат.Колонки.Добавить(""Сторона"");
					|Параметры.Результат.Колонки.Добавить(""КонтактноеЛицо"");
					|Параметры.Результат.Колонки.Добавить(""Наименование"");
					|СтрокаСтороны = Параметры.Результат.Добавить();
					|СтрокаСтороны.Сторона = Параметры.Источник.Организация;
					|СтрокаСтороны.КонтактноеЛицо = Параметры.Источник.Менеджер;
					|СтрокаСтороны.Наименование = Справочники.НаименованияСторон.НайтиПоНаименованию(""Заказчик"");
					|СтрокаСтороны = Параметры.Результат.Добавить();
					|СтрокаСтороны.Сторона = Параметры.Источник.Контрагент;
					|СтрокаСтороны.КонтактноеЛицо = Параметры.Источник.КонтактноеЛицо;
					|СтрокаСтороны.Наименование = Справочники.НаименованияСторон.НайтиПоНаименованию(""Продавец"");";
				ВывестиПримерВыражения(ИнструкцияМассив, ЗаголовокРаздела, ПримерВыражения);
			КонецЕсли;
		Иначе
			ЗаголовокРаздела = НСтр("ru = 'Пример заполнения реквизита 1С:Документооборота'");
			ПримерВыражения =
				"Если Параметры.Источник.Ответственный <> Неопределено Тогда
				|	ПользовательДО = Справочники.Пользователи.НайтиПоНаименованию(Параметры.Источник.Ответственный.name);
				|КонецЕсли;
				|Параметры.Результат = Сотрудники.ОсновнойСотрудникПользователя(ПользовательДО);";
			ВывестиПримерВыражения(ИнструкцияМассив, ЗаголовокРаздела, ПримерВыражения);
			ЗаголовокРаздела = НСтр("ru = 'Пример создания в 1С:Документообороте нового элемента НСИ'");
			ПримерВыражения =
				"ИНН = ""7730643014""; КПП = ""771301001"";
				|ИдентификаторВнешнегоОбъекта = СтрШаблон(""%1_%2"", ИНН, КПП);
				|ТипВнешнегоОбъекта = ""Справочник.Контрагенты"";
				|КонтрагентXDTO = ОбработкаЗапросовXDTO.СоздатьВнешнийОбъектXDTO(
				|	""DMCorrespondent"", ИдентификаторВнешнегоОбъекта, ТипВнешнегоОбъекта);
				|КонтрагентXDTO.name = ""ООО 1С-Софт"";
				|КонтрагентXDTO.inn = ИНН;
				|КонтрагентXDTO.kpp = КПП;
				|Параметры.Результат = КонтрагентXDTO;";
			ВывестиПримерВыражения(ИнструкцияМассив, ЗаголовокРаздела, ПримерВыражения);
		КонецЕсли;
	КонецЕсли;
	
	ИнструкцияМассив.Добавить("<p>");
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПодсказкуЗаполненияТаблицы(ИнструкцияМассив)
	
	ИнструкцияМассив.Добавить(СтрШаблон(
		НСтр("ru = 'Для заполнения таблицы результат должен иметь тип %1 с колонками:'"),
		"<b>ТаблицаЗначений</b>"));
	ИнструкцияМассив.Добавить("</p><table>");
	
	ИнструкцияМассив.Добавить("<tr><td>");
	ИнструкцияМассив.Добавить("<b>");
	ИнструкцияМассив.Добавить(НСтр("ru ='Имя реквизита'"));
	ИнструкцияМассив.Добавить("</b></td>");
	
	ИнструкцияМассив.Добавить("<td><b>");
	ИнструкцияМассив.Добавить(НСтр("ru ='Синоним'"));
	ИнструкцияМассив.Добавить("</b></td>");
	ИнструкцияМассив.Добавить("</tr>");
	
	РеквизитыОбъектаДО = Справочники.ПравилаЗагрузкиДанныхВДО.РеквизитыОбъектаДО(ПолноеИмяОбъектаДО, ВидДокумента);
	СтрокиТаблицы = РеквизитыОбъектаДО.НайтиСтроки(Новый Структура("Таблица", ИмяРеквизитаОбъектаДО));
	СпискиВыбораРеквизитов = Новый Соответствие;
	Для Каждого Элемент Из СтрокиТаблицы Цикл
		ИнструкцияМассив.Добавить("<tr><td>");
		ИнструкцияМассив.Добавить(Элемент.Имя);
		ИнструкцияМассив.Добавить("</td>");
		ИнструкцияМассив.Добавить("<td>");
		ИнструкцияМассив.Добавить(Элемент.Представление);
		ИнструкцияМассив.Добавить("</td></tr>");
		Если Элемент.СвязиПараметровВыбора.Количество() > 0
				И Элемент.СвязиПараметровВыбора[0].СписокВыбора.Количество() > 0 Тогда
			МассивЗначений = Новый Массив;
			Для Каждого ЭлементСпискаВыбора Из Элемент.СвязиПараметровВыбора[0].СписокВыбора Цикл
				МассивЗначений.Добавить(
					СтрШаблон("%1	%2", ЭлементСпискаВыбора.Значение, ЭлементСпискаВыбора.Представление));
			КонецЦикла;
			СпискиВыбораРеквизитов.Вставить(
				СтрШаблон(НСтр("ru = 'Список возможных значений в колонке %1'"), Элемент.Имя),
				МассивЗначений);
		КонецЕсли;
	КонецЦикла;
	
	ИнструкцияМассив.Добавить("</table>");
	
	Для Каждого Элемент Из СпискиВыбораРеквизитов Цикл
		ВывестиПримерВыражения(ИнструкцияМассив, Элемент.Ключ, СтрСоединить(Элемент.Значение, Символы.ПС));
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПодсказкуОбращенияКИсточнику(ИнструкцияМассив)
	
	ИнструкцияМассив.Добавить("<p>");
	ИнструкцияМассив.Добавить(СтрШаблон(
		НСтр("ru = 'К объекту %1 можно обращаться через свойство %2 переменной %3.'"),
		?(ЗначениеЗаполнено(ПредставлениеИС), ПредставлениеИС, НСтр("ru = 'интегрированной системы'")),
		"<b>Источник</b>",
		"<b>Параметры</b>"));
	ИнструкцияМассив.Добавить("</p>");
	
	ДобавитьСворачиваемуюГруппу(ИнструкцияМассив, Истина);
	ИнструкцияМассив.Добавить("<summary class=""pointer""><u><b>");
	ИнструкцияМассив.Добавить(НСтр("ru = 'Реквизиты источника'"));
	ИнструкцияМассив.Добавить("</b></u></summary>");
	ИнструкцияМассив.Добавить("<blockquote><pre>");
	
	РеквизитыОбъектаИС = РаботаСИнтегрированнымиСистемами.СписокРеквизитовОбъектаИнтегрированнойСистемы(
		?(ЗначениеЗаполнено(ИнтегрированнаяСистема), ИнтегрированнаяСистема, ИмяМакета),
		ПолноеИмяОбъектаИС);
	
	СоставРеквизитов = Справочники.ПравилаЗагрузкиДанныхВДО.КоллекцияРеквизитовОбъектаДО();
	
	// Реквизиты.
	Для Каждого МетаданныеРеквизит Из РеквизитыОбъектаИС[0].Строки Цикл
		НоваяСтрока = СоставРеквизитов.Добавить();
		НоваяСтрока.Имя = МетаданныеРеквизит.Имя;
		НоваяСтрока.Представление = МетаданныеРеквизит.Синоним;
		Если МестоВыполненияВыражения = Перечисления.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеИС Тогда
			НоваяСтрока.ЭтоДополнительныйРеквизит = (СтрНайти(МетаданныеРеквизит.Имя, "ДопРеквизит_") = 1);
		КонецЕсли;
	КонецЦикла;
	
	// Табличные части.
	Если РеквизитыОбъектаИС.Количество() = 2 Тогда
		Для Каждого МетаданныеТаблица Из РеквизитыОбъектаИС[1].Строки Цикл
			НоваяСтрока = СоставРеквизитов.Добавить();
			НоваяСтрока.Имя = МетаданныеТаблица.Имя;
			НоваяСтрока.ТаблицаДляСортировки = МетаданныеТаблица.Имя;
			НоваяСтрока.Представление = МетаданныеТаблица.Синоним;
			НоваяСтрока.ЭтоТаблица = Истина;
			
			Для Каждого МетаданныеРеквизит Из МетаданныеТаблица.Строки Цикл
				НоваяСтрока = СоставРеквизитов.Добавить();
				НоваяСтрока.Имя = МетаданныеРеквизит.Имя;
				НоваяСтрока.Представление = МетаданныеРеквизит.Синоним;
				НоваяСтрока.Таблица = МетаданныеТаблица.Имя;
				НоваяСтрока.ТаблицаДляСортировки = МетаданныеТаблица.Имя;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	СоставРеквизитов.Сортировать("ЭтоДополнительныйРеквизит, ТаблицаДляСортировки, ЭтоТаблица УБЫВ, Имя");
	
	ВывестиТаблицуРеквизитов(ИнструкцияМассив, СоставРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПодсказкуОбращенияКПриемнику(ИнструкцияМассив)
	
	ИнструкцияМассив.Добавить("<p>");
	ИнструкцияМассив.Добавить(СтрШаблон(
		НСтр("ru = 'К объекту 1С:Документооборота можно обращаться через свойство %1 переменной %2.'"),
		"<b>Приемник</b>",
		"<b>Параметры</b>"));
	ИнструкцияМассив.Добавить("</p>");
	
	ДобавитьСворачиваемуюГруппу(ИнструкцияМассив, Истина);
	ИнструкцияМассив.Добавить("<summary class=""pointer""><u><b>");
	ИнструкцияМассив.Добавить(НСтр("ru = 'Реквизиты приемника'"));
	ИнструкцияМассив.Добавить("</b></u></summary>");
	ИнструкцияМассив.Добавить("<blockquote><pre>");
	
	РеквизитыОбъектаДО = Справочники.ПравилаЗагрузкиДанныхВДО.РеквизитыОбъектаДО(ПолноеИмяОбъектаДО,, Истина);
	
	РеквизитыОбъектаДО.Сортировать("ЭтоДополнительныйРеквизит, ЭтоТаблица, Имя");
	
	ВывестиТаблицуРеквизитов(ИнструкцияМассив, РеквизитыОбъектаДО, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПодсказкуОбращенияКТекущейСтрокеТаблицы(ИнструкцияМассив)
	
	ИнструкцияМассив.Добавить("<p>");
	ИнструкцияМассив.Добавить(СтрШаблон(
		НСтр("ru ='К данным текущей строки табличной части источника ""%1"", из которой выполняется
		|заполнение табличной части приемника, можно обращаться через свойство %2 переменной %3.'"),
			Таблица,
			"<b>ТекущаяСтрока</b>",
			"<b>Параметры</b>"));
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПодсказкуОбращенияКТекущемуДействию(ИнструкцияМассив)
	
	ВидДействияГУИД = СтрЗаменить(СтрЗаменить(ИмяРеквизитаОбъектаДО, "Действие_", ""), "_", "-");
	НастройкиПоВидуДействия = Новый Массив;
	ВсеНастройки = ПравилаОбработкиСервер.НастройкиИВидыДействийПравилаОбработки(ВидДокумента);
	Для Каждого СтрокаСвязь Из ВсеНастройки.СвязьНастроекИВидовДействий Цикл
		Если Строка(СтрокаСвязь.ВидДействия.УникальныйИдентификатор()) = ВидДействияГУИД Тогда
			НастройкиПоВидуДействия.Добавить(СтрокаСвязь);
		КонецЕсли;
	КонецЦикла;
	ДоступенВыборПравилаОбработки = (НастройкиПоВидуДействия.Количество() > 1);
	Если ДоступенВыборПравилаОбработки Тогда
		ИнструкцияМассив.Добавить("<p>");
		ИнструкцияМассив.Добавить(СтрШаблон(
			НСтр("ru ='К ссылке на правило обработки, по которому было заполнено текущее действие,
			|можно обращаться через свойство %1 переменной %2. В нем хранится строка,
			|содержащая уникальный идентификатор правила обработки.'"),
				"<b>ТекущееПравилоОбработки</b>",
				"<b>Параметры</b>"));
		МассивЗначений = Новый Массив;
		Для Каждого СтрокаСвязь Из НастройкиПоВидуДействия Цикл
			ПредставлениеОбработки = ПравилаОбработкиСервер.ПредставлениеОбработки(СтрокаСвязь.ПравилоОбработки);
			Если Не ЗначениеЗаполнено(ПредставлениеОбработки) Тогда
				ПредставлениеОбработки = СокрЛП(СтрокаСвязь.ПравилоОбработки);
			КонецЕсли;
			МассивЗначений.Добавить(
				СтрШаблон("%1	%2", СтрокаСвязь.ПравилоОбработки.УникальныйИдентификатор(), ПредставлениеОбработки));
		КонецЦикла;
		ВывестиПримерВыражения(
			ИнструкцияМассив,
			НСтр("ru = 'Список идентификаторов возможных правил обработки'"),
			СтрСоединить(МассивЗначений, Символы.ПС));
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьСворачиваемуюГруппу(ИнструкцияМассив, ОткрывающийТег)
	
	Если ОбщегоНазначения.ЭтоLinuxКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Если ОткрывающийТег Тогда
		ИнструкцияМассив.Добавить("<details>");
	Иначе
		ИнструкцияМассив.Добавить("</details>");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнструкцию()
	
	ИнструкцияМассив = Новый Массив;
	
	ИнструкцияМассив.Добавить(
		"<html>
		|<style type=""text/css"">
		|	body {
		|		overflow:    auto;
		|		margin-top:  12px;
		|		margin-left: 20px;
		|		font-family: MS Shell Dlg, Microsoft Sans Serif, sans-serif;
		|		font-size:   8pt;}
		|	table {
		|		width:       450px;
		|		font-family: MS Shell Dlg, Microsoft Sans Serif, sans-serif;
		|		font-size:   8pt;}
		|	td {vertical-align: top;}
		|	p {
		|		margin-top: 7px;}
		|	.pointer { cursor: pointer; }
		|</style>
		|<body>");
	
	Заголовок = НСтр("ru = 'Выражение на встроенном языке'");
	
	ИнструкцияМассив.Добавить("<p>");
	ИнструкцияМассив.Добавить(СтрШаблон(
		НСтр("ru = 'Результат вычисления выражения на встроенном языке 1С:Предприятия
			|должен присваиваться свойству %1 переменной %2.'"),
		"<b>Результат</b>",
		"<b>Параметры</b>"));
	ИнструкцияМассив.Добавить(" ");
	
	Если ЭтоТаблица Тогда
		ДобавитьПодсказкуЗаполненияТаблицы(ИнструкцияМассив);
	КонецЕсли;
	
	ДоступенВыборПравилаОбработки = Ложь;
	ЭтоТаблицаУчастниковДействия = (СтрНайти(ИмяРеквизитаОбъектаДО, "Действие_") = 1);
	Если ЭтоТаблицаУчастниковДействия
			И МестоВыполненияВыражения = Перечисления.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеДО Тогда
		ДобавитьПодсказкуОбращенияКТекущемуДействию(ИнструкцияМассив);
	КонецЕсли;
	
	Если ДоступнаТекущаяСтрока Тогда
		ДобавитьПодсказкуОбращенияКТекущейСтрокеТаблицы(ИнструкцияМассив);
	КонецЕсли;
	
	ДобавитьПодсказкуОбращенияКИсточнику(ИнструкцияМассив);
	
	Если МестоВыполненияВыражения = Перечисления.МестаВыполненияВыраженийНаВстроенномЯзыке.НаСторонеДО Тогда
		ДобавитьПодсказкуОбращенияКПриемнику(ИнструкцияМассив);
	КонецЕсли;
	
	ИнструкцияМассив.Добавить("</body></html>");
	
	Инструкция = СтрСоединить(ИнструкцияМассив);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИдентификаторДопРеквизита(ИмяДопРеквизита)
	
	ИдентификаторДопРеквизита = СтрЗаменить(ИмяДопРеквизита, "ДопРеквизит_", "");
	ИдентификаторДопРеквизита = СтрШаблон("%1-%2-%3-%4-%5",
		Лев(ИдентификаторДопРеквизита, 8),
		Сред(ИдентификаторДопРеквизита, 9, 4),
		Сред(ИдентификаторДопРеквизита, 13, 4),
		Сред(ИдентификаторДопРеквизита, 17, 4),
		Сред(ИдентификаторДопРеквизита, 21, 12));
	
	Возврат ИдентификаторДопРеквизита;
	
КонецФункции

&НаКлиенте
Процедура КомандаОчиститьЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		РедакторВстроенногоЯзыкаКлиент.ОчиститьТекст(Элементы.РедакторВстроенногоЯзыка, ВычисляемоеВыражение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция КонтекстВыраженияНаВстроенномЯзыке(Знач ПолноеИмяОбъектаДО, Знач ПолноеИмяОбъектаИС)
	
	Возврат Справочники.ПравилаЗагрузкиДанныхВДО.КонтекстВыраженияНаВстроенномЯзыке(
		МестоВыполненияВыражения,
		?(ЗначениеЗаполнено(ИнтегрированнаяСистема), ИнтегрированнаяСистема, ИмяМакета),
		ПолноеИмяОбъектаДО,
		ПолноеИмяОбъектаИС,
		Таблица,
		ДоступенВыборПравилаОбработки);
	
КонецФункции

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗакрытиеПодтверждено = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗагрузкиРедактора()
	
	РедакторВстроенногоЯзыкаКлиент.ПослеЗагрузкиРедактора(Элементы.РедакторВстроенногоЯзыка, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриНажатииКлавишиОтмена()
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция ПроверитьВыражениеНаВстроенномЯзыке(Знач Выражение, Знач ПолноеИмяОбъектаДО, Знач ПолноеИмяОбъектаИС)
	
	РезультатВыполнения = РедакторВстроенногоЯзыка.ПодготовитьВыражениеКВыполнению(Выражение, ЭтотОбъект);
	Попытка
		Справочники.ПравилаЗагрузкиДанныхВДО.ПроверитьВыражениеПравилаЗагрузки(
			Выражение,
			МестоВыполненияВыражения,
			?(ЗначениеЗаполнено(ИнтегрированнаяСистема), ИнтегрированнаяСистема, ИмяМакета),
			ПолноеИмяОбъектаДО,
			ПолноеИмяОбъектаИС,
			Таблица,
			РезультатВыполнения.ПараметрыВыражения);
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Ошибок не обнаружено'"));
	Исключение
		РезультатВыполнения.ИнформацияОбОшибке = ИнформацияОбОшибке();
	КонецПопытки;
	РедакторВстроенногоЯзыка.ОбработатьРезультатВыполнения(РезультатВыполнения, ЭтотОбъект);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Функция ЭтоРаскрываемаяГруппа(ЭлементHTML)
	
	Попытка
		ЭтоРаскрываемаяГруппа = (ТипЗнч(ЭлементHTML) = Тип("ВнешнийОбъект"))
			И (ЭлементHTML.tagName = "U"
				Или ЭлементHTML.className = "pointer"
				Или ЭлементHTML.textContent = НСтр("ru = 'Реквизиты источника'")
				Или ЭлементHTML.textContent = НСтр("ru = 'Реквизиты приемника'"));
	Исключение
		ЭтоРаскрываемаяГруппа = Ложь;
	КонецПопытки;
	
	Возврат ЭтоРаскрываемаяГруппа;
	
КонецФункции

#КонецОбласти
