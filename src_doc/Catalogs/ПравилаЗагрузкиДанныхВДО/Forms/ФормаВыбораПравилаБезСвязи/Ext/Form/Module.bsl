#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ИнтегрированнаяСистема) Тогда
		ИнтегрированнаяСистема = Параметры.ИнтегрированнаяСистема;
	КонецЕсли;
	
	ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Ложь;
	ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТипОбъектаДО");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ЭлементОтбора.ПравоеЗначение = "Справочник.Контрагенты";
	ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	Если ЗначениеЗаполнено(Параметры.ТипОбъектаИС) Тогда
		ТипОбъектаИС = Параметры.ТипОбъектаИС;
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТипОбъектаИС");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = Параметры.ТипОбъектаИС;
		ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли;
	
	Если Параметры.ПравилаЗагрузкиВДОИспользуемыеВИС.Количество() > 0 Тогда
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Ссылка");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
		ЭлементОтбора.ПравоеЗначение = Параметры.ПравилаЗагрузкиВДОИспользуемыеВИС;
		ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Отказ = Истина;
	
	ПараметрыФормы = ПараметрыФормыПравилаЗагрузкиДанныхВДО();
	Если Копирование Тогда
		ПараметрыФормы.Вставить("ЗначениеКопирования", Элементы.Список.ТекущаяСтрока);
	КонецЕсли;
	
	ФормаОбъекта = ОткрытьФорму(
		"Справочник.ПравилаЗагрузкиДанныхВДО.Форма.ФормаЭлемента",
		ПараметрыФормы,,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	Если ФормаОбъекта = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения(
		"СписокПередНачаломДобавленияЗавершение",
		ЭтотОбъект,
		Новый Структура("ФормаОбъекта", ФормаОбъекта));
	
	ФормаОбъекта.ОписаниеОповещенияОЗакрытии = ОповещениеОЗакрытии;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ПараметрыФормы = ПараметрыФормыПравилаЗагрузкиДанныхВДО();
	ПараметрыФормы.Вставить("Ключ", Элементы.Список.ТекущаяСтрока);
	
	ОткрытьФорму(
		"Справочник.ПравилаЗагрузкиДанныхВДО.Форма.ФормаЭлемента",
		ПараметрыФормы,,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПараметрыФормыПравилаЗагрузкиДанныхВДО()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъектаИС", ТипОбъектаИС);
	ПараметрыФормы.Вставить("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	ПараметрыФормы.Вставить("ЗадаватьВопросОСозданииСвязанногоПравилаИнтеграции", Ложь);
	ПараметрыФормы.Вставить("СписокВыбораОбъектовДО", Новый СписокЗначений);
	ПараметрыФормы.СписокВыбораОбъектовДО.Добавить("Справочник.ДокументыПредприятия", НСтр("ru = 'Документ'"));
	ПараметрыФормы.СписокВыбораОбъектовДО.Добавить("Справочник.Мероприятия", НСтр("ru = 'Мероприятие'"));
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаКлиенте
Процедура СписокПередНачаломДобавленияЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	ПравилоЗагрузкиСсылка = ПараметрыОповещения.ФормаОбъекта.Объект.Ссылка;
	Если ЗначениеЗаполнено(ПравилоЗагрузкиСсылка) Тогда
		Закрыть(ПравилоЗагрузкиСсылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти