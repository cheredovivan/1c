#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СоздаватьСвязанноеПравилоИнтеграции = Параметры.СоздаватьСвязанноеПравилоИнтеграции;
	ЗадаватьВопросОСозданииСвязанногоПравилаИнтеграции = Истина;
	Если Параметры.Свойство("ЗадаватьВопросОСозданииСвязанногоПравилаИнтеграции") Тогда
		ЗадаватьВопросОСозданииСвязанногоПравилаИнтеграции = Параметры.ЗадаватьВопросОСозданииСвязанногоПравилаИнтеграции;
	КонецЕсли;
	
	Если Параметры.СписокВыбораОбъектовДО.Количество() > 0 Тогда
		Для Каждого ТипОбъектаДО Из Параметры.СписокВыбораОбъектовДО Цикл
			СписокВыбораОбъектовДО.Добавить(ТипОбъектаДО.Значение, ТипОбъектаДО.Представление);
		КонецЦикла;
	Иначе
		СписокВыбораОбъектовДО.Добавить("Справочник.ДокументыПредприятия", НСтр("ru = 'Документ'"));
		СписокВыбораОбъектовДО.Добавить("Справочник.Контрагенты", НСтр("ru = 'Контрагент'"));
		СписокВыбораОбъектовДО.Добавить("Справочник.Мероприятия", НСтр("ru = 'Мероприятие'"));
	КонецЕсли;
	
	ДоступенВыборОбъектаИС = Истина;
	Если Параметры.Свойство("ТипОбъектаИС") Тогда
		ДоступенВыборОбъектаИС = Ложь;
		Если Не ЗначениеЗаполнено(Объект.ТипОбъектаИС) Тогда
			Объект.ТипОбъектаИС = Параметры.ТипОбъектаИС;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Параметры.Свойство("ЗначениеКопирования", ЗначениеКопирования);
		Параметры.Свойство("ПредставлениеИС", ПредставлениеИС);
		Параметры.Свойство("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
		Параметры.Свойство("ИмяМакета", ИмяМакета);
		Параметры.Свойство("ИзМакета", Объект.ИзМакета);
		
		Если ЗначениеЗаполнено(ИмяМакета) Тогда
			Объект.УзелИнтегрированнойСистемы = ИмяМакета;
		Иначе
			Объект.УзелИнтегрированнойСистемы = ИнтегрированнаяСистема;
		КонецЕсли;
	Иначе
		Если Объект.ИзМакета Тогда
			ИмяМакета = Объект.УзелИнтегрированнойСистемы;
			СписокИС = Новый СписокЗначений;
			РаботаСИнтегрированнымиСистемамиВызовСервера.ОбновитьСписокИС(СписокИС);
			ДанныеИС = СписокИС.НайтиПоЗначению(ИмяМакета);
			Если ДанныеИС <> Неопределено Тогда
				ПредставлениеИС = ДанныеИС.Представление;
			КонецЕсли;
		Иначе
			ИнтегрированнаяСистема = Объект.УзелИнтегрированнойСистемы;
			ПредставлениеИС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				Объект.УзелИнтегрированнойСистемы,
				"Наименование");
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ТипОбъектаДО) Тогда
		ОбъектДокументооборота = СписокВыбораОбъектовДО.НайтиПоЗначению(Объект.ТипОбъектаДО);
		Если ОбъектДокументооборота <> Неопределено Тогда
			ПредставлениеОбъектаДО = ОбъектДокументооборота.Представление;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПредставлениеИС) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = СтрШаблон(НСтр("ru = 'Правило загрузки данных в 1С:Документооборот из %1'"), ПредставлениеИС);
		
		Если Объект.ИзМакета Тогда
			Элементы.ТипОбъектаИС.Заголовок = СтрШаблон(НСтр("ru = 'Документ %1'"), ПредставлениеИС);
		Иначе
			Элементы.ТипОбъектаИС.Заголовок = СтрШаблон(НСтр("ru = 'Объект %1'"), ПредставлениеИС);
		КонецЕсли;
		Элементы.ДекорацияНетДоступаКИС.Заголовок = СтрШаблон(
			НСтр("ru = 'Нет доступа к %1.'"),
			ПредставлениеИС);
	КонецЕсли;
	
	Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаИСДоступна;
	
	СтарыйШаблон = Объект.Шаблон;
	
	ГрифДоступаПоУмолчанию = Константы.ГрифДоступаПоУмолчанию.Получить();
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	Для НомерСтроки = 0 По Объект.ПравилаЗаполненияРеквизитовДО.Количество() - 1 Цикл
		Если ТекущийОбъект.ПравилаЗаполненияРеквизитовДО[НомерСтроки].Вариант = ВариантРеквизитТаблицы() Тогда
			Объект.ПравилаЗаполненияРеквизитовДО[НомерСтроки].ПравилаЗаполненияСтрокТаблицыДанныеФормы =
				ТекущийОбъект.ПравилаЗаполненияРеквизитовДО[НомерСтроки].ПравилаЗаполненияСтрокТаблицы;
		КонецЕсли;
	КонецЦикла;
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбработатьФормуСогласноВерсииСервиса();
	УстановитьДоступность();
	ОбновитьЗаголовокСтраницыДобавленияПечатныхФорм();
	ОбновитьЗаголовокСтраницыСозданияСвязей();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбщегоНазначенияДокументооборот.ПоказатьНадписьПометкиУдаления(
		ЭтотОбъект, ТекущийОбъект.ПометкаУдаления);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Для НомерСтроки = 0 По Объект.ПравилаЗаполненияРеквизитовДО.Количество() - 1 Цикл
		ТекущийОбъект.ПравилаЗаполненияРеквизитовДО[НомерСтроки].ПравилаЗаполненияСтрокТаблицы =
			Объект.ПравилаЗаполненияРеквизитовДО[НомерСтроки].ПравилаЗаполненияСтрокТаблицыДанныеФормы;
	КонецЦикла;
	
	ТекущийОбъект.ПрисоединяемыеПечатныеФормы.Очистить();
	Для Каждого Строка Из ПрисоединяемыеПечатныеФормы Цикл
		Если Строка.Использовать Тогда
			НоваяСтрока = ТекущийОбъект.ПрисоединяемыеПечатныеФормы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЕсли;
	КонецЦикла;
	
	ТекущийОбъект.ПредставлениеОбъектаИС = ПредставлениеОбъектаИС;
	ОбновитьПолноеПредставлениеОбъектаДО(ТекущийОбъект, ПредставлениеОбъектаДО);
	
	Справочники.ПравилаЗагрузкиДанныхВДО.ПроверитьПравило(ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// Проверка заполнения реквизитов табличной части.
	Для Каждого Строка Из ПравилаЗаполненияРеквизитовДО.ПолучитьЭлементы() Цикл
		
		Если Строка.ОбязательноеЗаполнение И Не ПравилоЗаполнено(Строка, ПравилаЗаполненияРеквизитовДО) Тогда
			СообщитьОбОшибке(Строка);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Если Строка.ЭтоТаблица
				И Не Отказ
				И ПравилоЗаполнено(Строка, ПравилаЗаполненияРеквизитовДО)
				И (Строка.Вариант = ВариантРеквизит() Или Строка.Вариант = ВариантРеквизитТаблицы()) Тогда
			Для Каждого ПодчиненнаяСтрока Из Строка.ПолучитьЭлементы() Цикл
				Если ПодчиненнаяСтрока.ОбязательноеЗаполнение
						И Не ПравилоЗаполнено(ПодчиненнаяСтрока, ПравилаЗаполненияРеквизитовДО) Тогда
					СообщитьОбОшибке(ПодчиненнаяСтрока);
					Отказ = Истина;
					Возврат;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	// Проверка заполнения печатных форм.
	Если ИспользоватьРолиФайлов Тогда
		Для Каждого Строка Из ПрисоединяемыеПечатныеФормы Цикл
			Если Строка.Использовать И Не ЗначениеЗаполнено(Строка.РольФайла) Тогда
				СообщитьОбОшибкеУстановкиРоли(Строка);
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(ИнтегрированнаяСистема)
			И (СоздаватьСвязанноеПравилоИнтеграции Или ЗадаватьВопросОСозданииСвязанногоПравилаИнтеграции) Тогда
		ДанныеДляПодключения = РаботаСИнтегрированнымиСистемамиПовтИсп.ДанныеДляПодключения(ИнтегрированнаяСистема);
		
		Запрос = РаботаСИнтегрированнымиСистемами.СоздатьОбъект(
			ДанныеДляПодключения, "DMILRelatedIntegrationRulesExistenceRequest");
		Запрос.relatedDataLoadingRule = Строка(Объект.Ссылка.УникальныйИдентификатор());
		Ответ = ДанныеДляПодключения.Прокси.execute(Запрос);
		РаботаСИнтегрированнымиСистемами.ПроверитьВозвратВебСервиса(ДанныеДляПодключения, Ответ);
		
		ПараметрыЗаписи.Вставить("СуществуетСвязанноеПравилоИнтеграции", Ответ.exist);
	КонецЕсли;
	
	ПеренестиПравилаВОбъект(Объект, ПравилаЗаполненияРеквизитовДО);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПослеЗаписи(Знач ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("СуществуетСвязанноеПравилоИнтеграции")
			И Не ПараметрыЗаписи.СуществуетСвязанноеПравилоИнтеграции Тогда
		
		Если Не СоздаватьСвязанноеПравилоИнтеграции И ЗадаватьВопросОСозданииСвязанногоПравилаИнтеграции Тогда
			// Нужно задать вопрос пользователю.
			ТекстВопроса = СтрШаблон(НСтр("ru = 'Создать в %1 связанное правило интеграции?'"), ПредставлениеИС);
			Ответ = Ждать ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНетАсинх(
				ТекстВопроса,
				ОбщегоНазначенияДокументооборотКлиент.ПараметрыВопросаДаНет(КодВозвратаДиалога.Да));
			СоздаватьСвязанноеПравилоИнтеграции = (Ответ = КодВозвратаДиалога.Да);
		КонецЕсли;
		
		Если СоздаватьСвязанноеПравилоИнтеграции Тогда
			РаботаСИнтегрированнымиСистемамиВызовСервера.СоздатьСвязанноеПравилоИнтеграцииВИС(
				ИнтегрированнаяСистема,
				Объект.Ссылка,
				Объект.ВидДокумента,
				Объект.ТипОбъектаДО,
				Объект.ТипОбъектаИС,
				Объект.ПредставлениеОбъектаДО,
				Объект.Комментарий);
			ОбновитьПовторноИспользуемыеЗначения();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЗначениеЗаполнено(ПредставлениеОбъектаИС) Тогда
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, Элементы.ТипОбъектаИС.Заголовок);
		Сообщение.Поле = "ПредставлениеОбъектаИС";
		Сообщение.Сообщить();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПредставлениеОбъектаДО) Тогда
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, Элементы.ТипОбъектаДО.Заголовок);
		Сообщение.Поле = "ПредставлениеОбъектаДО";
		Сообщение.Сообщить();
	КонецЕсли;
	
	Для Каждого Строка Из ПравилаЗаполненияРеквизитовДО.ПолучитьЭлементы() Цикл
		
		Если Строка.ИмяРеквизитаОбъектаДО = "ВидДокумента"
				И Строка.Вариант <> ВариантШаблон()
				И ЗначениеЗаполнено(Строка.ЗначениеРеквизитаДО)
				И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.ЗначениеРеквизитаДО, "ЗапретитьСозданиеДокументовНеПоШаблону") Тогда
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = СтрШаблон(
				НСтр("ru = 'В настройках вида документа ""%1"" установлен запрет на создание документов не по шаблону,
							|но шаблон документа не выбран.'"),
				Строка.ЗначениеРеквизитаДО);
			Сообщение.Поле = "Объект.Шаблон";
			Сообщение.Сообщить();
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененШаблонДокумента" И Параметр.Шаблон = Объект.Шаблон Тогда
		ПриИзмененииШаблона();
	ИначеЕсли ИмяСобытия = "ЗаписанШаблонДокумента" И Параметр.Шаблон = Объект.Шаблон Тогда
		ЗаполнитьСтруктуруРеквизитовОбъектаДОПоПравилу();
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_ДополнительныеРеквизитыИСведения"
			Или ИмяСобытия = "Запись_НаборыДополнительныхРеквизитовИСведений" Тогда
		ОбновитьПовторноИспользуемыеЗначения();
		ЗаполнитьСтруктуруРеквизитовОбъектаДОПоПравилу();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ОбщегоНазначенияДокументооборотКлиент.ВставитьВОписаниеОповещенияОЗакрытииСсылкуНаОбъект(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияНастройкиАвторизацииНажатие(Элемент)
	
	Оповещение = Новый ОписаниеОповещения("ДекорацияНастройкиАвторизацииНажатиеЗавершение", ЭтотОбъект);
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	ПараметрыФормы.Вставить("ПредставлениеИС", ПредставлениеИС);
	
	ОткрытьФорму("Обработка.ПанельАдминистрированияБесшовнойИнтеграции.Форма.АвторизацияВИнтегрированнойСистеме",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкиАвторизацииНажатиеЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ОбработатьФормуСогласноВерсииСервиса();
		ОбновитьЗаголовокСтраницыДобавленияПечатныхФорм();
		ОбновитьЗаголовокСтраницыСозданияСвязей();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаДОАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	
	Для Каждого ЭлементВыбора Из СписокВыбораОбъектовДО Цикл
		Если СтрНайти(ВРег(ЭлементВыбора.Представление), ВРег(Текст)) > 0 Тогда
			ДанныеВыбора.Добавить(ЭлементВыбора.Значение, ЭлементВыбора.Представление);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаДОНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачальныйТипОбъектаДО = Объект.ТипОбъектаДО;
	
	Оповещение = Новый ОписаниеОповещения("ТипОбъектаДОНачалоВыбораЗавершение",
		ЭтотОбъект, НачальныйТипОбъектаДО);
	
	ПоказатьВыборИзСписка(Оповещение, СписокВыбораОбъектовДО, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаДОНачалоВыбораЗавершение(ВыбранноеЗначение, НачальныйТипОбъектаДО) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ТипОбъектаДО = ВыбранноеЗначение.Значение;
	ПредставлениеОбъектаДО = ВыбранноеЗначение.Представление;
	
	Если НачальныйТипОбъектаДО <> Объект.ТипОбъектаДО Тогда
		Если Объект.ТипОбъектаДО = "Справочник.ДокументыПредприятия" Тогда
			ЗаполнитьСтруктуруРеквизитовОбъектаДОПоПравилу();
		Иначе
			Объект.Шаблон = ПредопределенноеЗначение("Справочник.ШаблоныДокументов.ПустаяСсылка");
			ПриИзмененииШаблона();
		КонецЕсли;
		Модифицированность = Истина;
		ЗаполнитьПоУмолчаниюНаСервере();
		
		ОбновитьПолноеПредставлениеОбъектаДО(Объект, ПредставлениеОбъектаДО);
		
		Объект.СозданиеСвязей.Очистить();
		ЗаполнитьСписокСозданияСвязей();
		ОбновитьЗаголовокСтраницыСозданияСвязей();
	КонецЕсли;
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаДООбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	НачальныйТипОбъектаДО = Объект.ТипОбъектаДО;
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ВыбранныйЭлемент = СписокВыбораОбъектовДО.НайтиПоЗначению(ВыбранноеЗначение);
		Объект.ТипОбъектаДО = ВыбранныйЭлемент.Значение;
		ПредставлениеОбъектаДО = ВыбранныйЭлемент.Представление;
		Если НачальныйТипОбъектаДО <> Объект.ТипОбъектаДО Тогда
			Если Объект.ТипОбъектаДО = "Справочник.ДокументыПредприятия" Тогда
				ЗаполнитьСтруктуруРеквизитовОбъектаДОПоПравилу();
			Иначе
				Объект.Шаблон = ПредопределенноеЗначение("Справочник.ШаблоныДокументов.ПустаяСсылка");
				ПриИзмененииШаблона();
			КонецЕсли;
			Модифицированность = Истина;
			ЗаполнитьПоУмолчаниюНаСервере();
			
			ОбновитьПолноеПредставлениеОбъектаДО(Объект, ПредставлениеОбъектаДО);
			
			Объект.СозданиеСвязей.Очистить();
			ЗаполнитьСписокСозданияСвязей();
			ОбновитьЗаголовокСтраницыСозданияСвязей();
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаДООкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	НачальныйТипОбъектаДО = Объект.ТипОбъектаДО;
	
	ДанныеВыбора = Новый СписокЗначений;
	Для Каждого ЭлементВыбора Из СписокВыбораОбъектовДО Цикл
		Если СтрНайти(ВРег(ЭлементВыбора.Представление), ВРег(Текст)) > 0 Тогда
			ДанныеВыбора.Добавить(ЭлементВыбора.Значение, ЭлементВыбора.Представление);
		КонецЕсли;
	КонецЦикла;
	
	Если ДанныеВыбора.Количество() = 1 Тогда
		
		ВыбранноеЗначение = ДанныеВыбора[0];
		
		Объект.ТипОбъектаДО = ВыбранноеЗначение.Значение;
		ПредставлениеОбъектаДО = ВыбранноеЗначение.Представление;
		
		Если НачальныйТипОбъектаДО <> Объект.ТипОбъектаДО Тогда
			Если Объект.ТипОбъектаДО = "Справочник.ДокументыПредприятия" Тогда
				ЗаполнитьСтруктуруРеквизитовОбъектаДОПоПравилу();
			Иначе
				Объект.Шаблон = ПредопределенноеЗначение("Справочник.ШаблоныДокументов.ПустаяСсылка");
				ПриИзмененииШаблона();
			КонецЕсли;
			Модифицированность = Истина;
			ЗаполнитьПоУмолчаниюНаСервере();
			
			ОбновитьПолноеПредставлениеОбъектаДО(Объект, ПредставлениеОбъектаДО);
			
			Объект.СозданиеСвязей.Очистить();
			ЗаполнитьСписокСозданияСвязей();
			ОбновитьЗаголовокСтраницыСозданияСвязей();
		КонецЕсли;
		
	Иначе
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаДООчистка(Элемент, СтандартнаяОбработка)
	
	Модифицированность = Истина;
	
	Объект.ТипОбъектаДО = "";
	
	Объект.Шаблон = ПредопределенноеЗначение("Справочник.ШаблоныДокументов.ПустаяСсылка");
	ПриИзмененииШаблона();
	
	ЗаполнитьПоУмолчаниюНаСервере();
	
	ОбновитьПолноеПредставлениеОбъектаДО(Объект, ПредставлениеОбъектаДО);
	
	Объект.СозданиеСвязей.Очистить();
	ЗаполнитьСписокСозданияСвязей();
	ОбновитьЗаголовокСтраницыСозданияСвязей();
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаИСАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	
	Для Каждого ЭлементВыбора Из СписокМетаданныхИС Цикл
		Если СтрНайти(ВРег(ЭлементВыбора.Представление), ВРег(Текст)) > 0 Тогда
			ДанныеВыбора.Добавить(ЭлементВыбора.Значение, ЭлементВыбора.Представление);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаИСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ТипОбъектаИСНачалоВыбораЗавершение", ЭтотОбъект);
	ПараметрФормы = Новый Структура;
	ПараметрФормы.Вставить("ТекущаяСтрока", Объект.ТипОбъектаИС);
	ПараметрФормы.Вставить("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	ПараметрФормы.Вставить("ИмяМакета", ИмяМакета);
	
	ОткрытьФорму("Справочник.ПравилаЗагрузкиДанныхВДО.Форма.ВыборОбъектаМетаданных",
		ПараметрФормы, ЭтотОбъект ,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаИСНачалоВыбораЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Объект.ТипОбъектаИС = Результат.Имя;
		ПредставлениеОбъектаИС = Результат.Синоним;
		Модифицированность = Истина;
		ПриИзмененииОбъектаИС();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаИСОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		ВыбранныйЭлемент = СписокМетаданныхИС.НайтиПоЗначению(ВыбранноеЗначение);
		ПредставлениеОбъектаИС =
			Сред(ВыбранныйЭлемент.Представление, СтрНайти(ВыбранныйЭлемент.Представление, ":") + 2);
		Объект.ТипОбъектаИС = ВыбранныйЭлемент.Значение;
		Модифицированность = Истина;
		ПриИзмененииОбъектаИС();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаИСОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ДанныеВыбора = Новый СписокЗначений;
	Для Каждого ЭлементВыбора Из СписокМетаданныхИС Цикл
		Если СтрНайти(ВРег(ЭлементВыбора.Представление), ВРег(Текст)) > 0 Тогда
			ДанныеВыбора.Добавить(ЭлементВыбора.Значение, ЭлементВыбора.Представление);
		КонецЕсли;
	КонецЦикла;
	
	Если ДанныеВыбора.Количество() = 1 Тогда
		ПредставлениеОбъектаИС = Сред(ДанныеВыбора[0].Представление, СтрНайти(ДанныеВыбора[0].Представление, ":") + 2);
		Объект.ТипОбъектаИС = ДанныеВыбора[0].Значение;
		Модифицированность = Истина;
		ПриИзмененииОбъектаИС();
	Иначе
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаИСОчистка(Элемент, СтандартнаяОбработка)
	
	Модифицированность = Истина;
	
	Объект.ТипОбъектаИС = "";
	ПриИзмененииОбъектаИС();
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонПриИзменении(Элемент)
	
	ПриИзмененииШаблона();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипФайловСохраненияПечатныхФормОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипФайловСохраненияПечатныхФормПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.ТипФайловСохраненияПечатныхФорм) Тогда
		Объект.ТипФайловСохраненияПечатныхФорм =
			РаботаСИнтегрированнымиСистемамиВызовСервера.ТипФайлаСохраняемойПечатнойФормыПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонСоздание(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ШаблонСозданиеЗавершение", ЭтотОбъект);
	ТекстВопроса = НСтр(
		"ru = 'Новый шаблон можно добавить только в карточке вида документа на закладке «Создание документов».
			|Какой вид документа использовать?'");
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить("СоздатьНовый", НСтр("ru = 'Создать новый'"));
	Кнопки.Добавить("ИзменитьСуществующий", НСтр("ru = 'Изменить существующий'"));
	Кнопки.Добавить(КодВозвратаДиалога.Отмена);
	ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки);
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонСозданиеЗавершение(Ответ, ПараметрыОповещения) Экспорт
	
	Если Ответ = "СоздатьНовый" Тогда
		ФормаОбъекта = ОткрытьФорму(
			"Справочник.ВидыДокументов.Форма.ФормаЭлемента",,
			ЭтотОбъект,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
		Если ФормаОбъекта = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ОповещениеОЗакрытии = Новый ОписаниеОповещения(
			"ПослеСозданияВидаДокумента",
			ЭтотОбъект,
			Новый Структура("ФормаОбъекта", ФормаОбъекта));
		
		ФормаОбъекта.ОписаниеОповещенияОЗакрытии = ОповещениеОЗакрытии;
	ИначеЕсли Ответ = "ИзменитьСуществующий" Тогда
		ОткрытьФорму("ОбщаяФорма.ВидыДокументов",, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПрисоединяемыеПечатныеФормы

&НаКлиенте
Процедура ПрисоединяемыеПечатныеФормыИспользоватьПриИзменении(Элемент)
	
	ОбновитьЗаголовокСтраницыДобавленияПечатныхФорм();
	ПеренестиПечатныеФормыВОбъект();
	
	ТекущиеДанные = Элементы.ПрисоединяемыеПечатныеФормы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.Обновлять = ТекущиеДанные.Использовать;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрисоединяемыеПечатныеФормыРольФайлаПриИзменении(Элемент)
	
	ПеренестиПечатныеФормыВОбъект();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрисоединяемыеПечатныеФормыОбновлятьПриИзменении(Элемент)
	
	ПеренестиПечатныеФормыВОбъект();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПравилаЗаполненияРеквизитовДО

&НаКлиенте
Процедура ПравилаЗаполненияРеквизитовДОПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПравилаЗаполненияРеквизитовДОПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.ПравилаЗаполненияРеквизитовДО.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Родитель = ТекущиеДанные.ПолучитьРодителя();
	Если Родитель <> Неопределено Тогда
		Если Родитель.Вариант = ВариантВыражение() Тогда
			ПоказатьПредупреждение(,
				НСтр("ru = 'Таблица заполняется целиком из выражения на встроенном языке.
					|Настройка заполнения отдельных реквизитов таблицы недоступна.'"));
			Возврат;
		КонецЕсли;
		Если Родитель.Вариант = ВариантРеквизитТаблицы()
				И ТипЗнч(Родитель.ПравилаЗаполненияСтрокТаблицы) = Тип("ХранилищеЗначения")
				И РаботаСИнтегрированнымиСистемамиВызовСервера.КоличествоСтрокЗаполненияТаблицы(
					Родитель.ПравилаЗаполненияСтрокТаблицы) > 1 Тогда
			ПоказатьПредупреждение(,
				НСтр("ru = 'Заполнение нескольких строк настраивается для таблицы в целом.
					|Настройка заполнения отдельных реквизитов таблицы недоступна.'"));
			Возврат;
		КонецЕсли;
		Если Родитель.Вариант = ВариантШаблон() Тогда
			ПоказатьПредупреждение(,
				НСтр("ru = 'Таблица заполняется целиком из шаблона.
					|Настройка заполнения отдельных реквизитов таблицы недоступна.'"));
			Возврат;
		КонецЕсли;
		Если Родитель.Вариант = ВариантПустой() Тогда
			ПоказатьПредупреждение(,
				НСтр("ru = 'Вначале нужно выбрать вариант заполнения таблицы.'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ID");
	ПараметрыФормы.Вставить("ИмяРеквизитаОбъектаДО");
	ПараметрыФормы.Вставить("ПредставлениеРеквизитаОбъектаДО");
	ПараметрыФормы.Вставить("Подсказка");
	ПараметрыФормы.Вставить("ТипРеквизитаОбъектаДО");
	ПараметрыФормы.Вставить("Таблица");
	ПараметрыФормы.Вставить("Вариант");
	ПараметрыФормы.Вставить("ВычисляемоеВыражение");
	ПараметрыФормы.Вставить("ПравилаЗаполненияСтрокТаблицы");
	ПараметрыФормы.Вставить("МестоВыполненияВыражения");
	ПараметрыФормы.Вставить("ЗначениеРеквизитаДО");
	ПараметрыФормы.Вставить("ДополнительныйРеквизитДО");
	ПараметрыФормы.Вставить("ИмяРеквизитаОбъектаИС");
	ПараметрыФормы.Вставить("ПредставлениеРеквизитаОбъектаИС");
	ПараметрыФормы.Вставить("Обновлять");
	ПараметрыФормы.Вставить("ВозможноЗаполнениеИзШаблона");
	ПараметрыФормы.Вставить("ШаблонЗначение");
	ПараметрыФормы.Вставить("ШаблонЗапрещаетИзменение", ШаблонЗапрещаетИзменение);
	
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, ТекущиеДанные);
	
	Родитель = ТекущиеДанные.ПолучитьРодителя();
	ПараметрыФормы.Вставить("Зависимый", (Родитель <> Неопределено));
	ПараметрыФормы.Вставить("ОбновлятьРодитель", ?(Родитель <> Неопределено, Родитель.Обновлять, Неопределено));
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Таблица) Тогда
		// Параметр ТаблицаИС нужен для ограничения списка выбора реквизитов объекта ИС.
		// Если выбран вариант заполнения таблицы ДО из таблицы объекта ИС, то заполнять реквизиты
		// таблицы ДО можно либо из реквизитов выбранной таблицы ИС, либо из не табличных реквизитов.
		Если Родитель.Вариант = ВариантРеквизит() Тогда
			ПараметрыФормы.Вставить("ТаблицаИС", Родитель.ИмяРеквизитаОбъектаИС);
		Иначе
			ПараметрыФормы.Вставить("ТаблицаИС", "");
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	ПараметрыФормы.Вставить("ПредставлениеИС", ПредставлениеИС);
	ПараметрыФормы.Вставить("ИмяМакета", ИмяМакета);
	ПараметрыФормы.Вставить("ПредставлениеОбъектаИС", ПредставлениеОбъектаИС);
	ПараметрыФормы.Вставить("ПолноеИмяОбъектаДО", Объект.ТипОбъектаДО);
	ПараметрыФормы.Вставить("ПолноеИмяОбъектаИС", Объект.ТипОбъектаИС);
	ПараметрыФормы.Вставить("ВидДокумента", Объект.ВидДокумента);
	
	Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ПараметрыФормы.ЗначениеРеквизитаДО) Тогда
		ПараметрыФормы.ЗначениеРеквизитаДО = Новый УникальныйИдентификатор(ПараметрыФормы.ЗначениеРеквизитаДО);
	КонецЕсли;
	
	ПараметрыОтбораЗначения = Новый Структура;
	Для Каждого Элемент Из ТекущиеДанные.СвязиПараметровВыбора Цикл
		Если Элемент.Значение.СписокВыбора.Количество() > 0 Тогда
			ПараметрыОтбораЗначения.Вставить("СписокВыбора", Элемент.Значение.СписокВыбора);
		КонецЕсли;
		
		Отбор = "Владелец";
		МассивИмяОтбора = СтрРазделить(Элемент.Значение.Имя, ".");
		Если МассивИмяОтбора.Количество() = 2 И МассивИмяОтбора[0] = "Отбор" Тогда
			Отбор = МассивИмяОтбора[1];
		КонецЕсли;
		
		Таблица = "";
		ИмяРеквизита = "";
		МассивПуть = СтрРазделить(Элемент.Значение.ПутьКДанным, ".");
		Если МассивПуть.Количество() = 1 Тогда
			ИмяРеквизита = МассивПуть[0];
		ИначеЕсли МассивПуть.Количество() = 2 Тогда
			Таблица = МассивПуть[0];
			ИмяРеквизита = МассивПуть[1];
		КонецЕсли;
		
		НайденныеСтроки = ПравилаЗаполненияРеквизитовДОНайтиСтроки(ИмяРеквизита, Таблица);
		
		Если НайденныеСтроки.Количество() = 1 И ЗначениеЗаполнено(НайденныеСтроки[0].ЗначениеРеквизитаДО) Тогда
			ПараметрыОтбораЗначения.Вставить(Отбор, НайденныеСтроки[0].ЗначениеРеквизитаДО);
		КонецЕсли;
	КонецЦикла;
	ПараметрыФормы.Вставить("ПараметрыОтбораЗначения", ПараметрыОтбораЗначения);
	
	ПараметрыФормы.Вставить("ЭтоПолноправныйПользователь", ЭтоПолноправныйПользователь);
	
	Если ТекущиеДанные.ЭтоТаблица Тогда
		ОткрываемаяФорма = "Справочник.ПравилаЗагрузкиДанныхВДО.Форма.ФормаПравилаДляТаблицы"
	Иначе
		ОткрываемаяФорма = "Справочник.ПравилаЗагрузкиДанныхВДО.Форма.ФормаПравила"
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПравилаЗаполненияРеквизитовДОПередНачаломИзмененияЗавершение",
		ЭтотОбъект, ТекущиеДанные);
	
	ОткрытьФорму(
		ОткрываемаяФорма,
		ПараметрыФормы,
		ЭтотОбъект,,,,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПравилаЗаполненияРеквизитовДОПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.ПравилаЗаполненияРеквизитовДО.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ШаблонЗапрещаетИзменение И ТекущиеДанные.Вариант = ВариантШаблон() Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Выбранный шаблон запрещает изменение реквизитов.'"));
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ИмяРеквизитаОбъектаДО = "ВидДокумента" И ТекущиеДанные.Вариант = ВариантШаблон() Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Вид документа указан в шаблоне и не может быть изменен.'"));
		Возврат;
	КонецЕсли;
	
	Если ПравилоЗаполнено(ТекущиеДанные) Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ПравилаЗаполненияРеквизитовДОПередУдалениемЗавершение", ЭтотОбъект, ТекущиеДанные);
		ТекстВопроса = НСтр("ru = 'Очистить правило заполнения?'");
		ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНет(Оповещение, ТекстВопроса,,, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПравилаЗаполненияРеквизитовДОПередУдалениемЗавершение(Результат, ТекущиеДанные) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьПравилоЗаполнения(ТекущиеДанные);
	ПриИзмененииПравилаЗаполненияРеквизита(ТекущиеДанные);
	ПеренестиПравилаВОбъект(Объект, ПравилаЗаполненияРеквизитовДО);
	
	Если ТекущиеДанные.ИмяРеквизитаОбъектаДО = "ВидДокумента" Тогда
		Объект.ВидДокумента = ПредопределенноеЗначение("Справочник.ВидыДокументов.ПустаяСсылка");
		ЗаполнитьСтруктуруРеквизитовОбъектаДОПоПравилу();
		ЗаполнитьПоУмолчаниюНаСервере(Ложь);
		
		ОбновитьПолноеПредставлениеОбъектаДО(Объект, ПредставлениеОбъектаДО);
		
		Объект.СозданиеСвязей.Очистить();
		ЗаполнитьСписокСозданияСвязей();
		ОбновитьЗаголовокСтраницыСозданияСвязей();
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСозданиеСвязей

&НаКлиенте
Процедура СозданиеСвязейПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеСвязейПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.СозданиеСвязей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.СоздаватьСвязь Тогда
		Оповещение = Новый ОписаниеОповещения(
			"СозданиеСвязейПередУдалениемЗавершение", ЭтотОбъект, ТекущиеДанные);
		ТекстВопроса = НСтр("ru = 'Очистить настройку автоматического создания связи?'");
		ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНет(Оповещение, ТекстВопроса,,, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеСвязейПередУдалениемЗавершение(Результат, ТекущиеДанные) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		ТекущиеДанные.СоздаватьСвязь = Истина;
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ПредставлениеТипаСвязи = "";
	ТекущиеДанные.СоздаватьСвязь = Ложь;
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяРеквизитаОбъектаИС", ТекущиеДанные.ИмяРеквизитаОбъектаИС);
	Отбор.Вставить("ТабличнаяЧастьОбъектаИС", ТекущиеДанные.ТабличнаяЧастьОбъектаИС);
	НастройкиСозданияСвязей = Объект.СозданиеСвязей.НайтиСтроки(Отбор);
	Для Каждого НастройкаСозданияСвязей Из НастройкиСозданияСвязей Цикл
		Объект.СозданиеСвязей.Удалить(НастройкаСозданияСвязей);
	КонецЦикла;
	
	Модифицированность = Истина;
	
	ОбновитьЗаголовокСтраницыСозданияСвязей();
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеСвязейПередНачаломИзменения(Элемент, Отказ)
	
	СозданиеСвязейТекущиеДанные = Элементы.СозданиеСвязей.ТекущиеДанные;
	Если СозданиеСвязейТекущиеДанные = Неопределено Или СозданиеСвязейТекущиеДанные.ЭтоТабличнаяЧасть Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущийЭлемент.Имя = "СозданиеСвязейСоздаватьСвязь" И СозданиеСвязейТекущиеДанные.СоздаватьСвязь Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	ВыбратьТипСвязи(СозданиеСвязейТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеСвязейСоздаватьСвязьПриИзменении(Элемент)
	
	СозданиеСвязейТекущиеДанные = Элементы.СозданиеСвязей.ТекущиеДанные;
	Если СозданиеСвязейТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СозданиеСвязейТекущиеДанные.СоздаватьСвязь Тогда
		ВыбратьТипСвязи(СозданиеСвязейТекущиеДанные);
	Иначе
		Оповещение = Новый ОписаниеОповещения(
			"СозданиеСвязейПередУдалениемЗавершение", ЭтотОбъект, СозданиеСвязейТекущиеДанные);
		ТекстВопроса = НСтр("ru = 'Очистить настройку автоматического создания связи?'");
		ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНет(Оповещение, ТекстВопроса,,, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ЗаполнитьПоУмолчанию(Знач Команда)
	
	НужноЗадатьВопрос = Ложь;
	
	Для Каждого Строка Из ПравилаЗаполненияРеквизитовДО.ПолучитьЭлементы() Цикл
		Если ЗначениеЗаполнено(Строка.ВычисляемоеВыражение)
				Или ЗначениеЗаполнено(Строка.ПравилаЗаполненияСтрокТаблицы)
				Или ЗначениеЗаполнено(Строка.ЗначениеРеквизитаДО)
				Или ЗначениеЗаполнено(Строка.ИмяРеквизитаОбъектаИС) Тогда
			НужноЗадатьВопрос = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НужноЗадатьВопрос Тогда
		Ответ = Ждать ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНетАсинх(
			НСтр("ru = 'Правила будут заполнены по умолчанию. Заполнить?'"),
			ОбщегоНазначенияДокументооборотКлиент.ПараметрыВопросаДаНет(КодВозвратаДиалога.Нет));
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЗаполнитьПоУмолчаниюНаСервере(,Истина);
		КонецЕсли;
	Иначе
		ЗаполнитьПоУмолчаниюНаСервере(,Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Скопировать(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПредставлениеИС", ПредставлениеИС);
	ПараметрыФормы.Вставить("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	ПараметрыФормы.Вставить("ИмяМакета", ИмяМакета);
	ПараметрыФормы.Вставить("ИзМакета", ЗначениеЗаполнено(ИмяМакета));
	ПараметрыФормы.Вставить("ЗначениеКопирования", Объект.Ссылка);
	
	ОткрытьФорму("Справочник.ПравилаЗагрузкиДанныхВДО.Форма.ФормаЭлемента", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Клиент

&НаКлиенте
Процедура ВыбратьТипСвязи(СозданиеСвязейТекущиеДанные)
	
	Если Объект.ТипОбъектаДО = "Справочник.ДокументыПредприятия" И Не ЗначениеЗаполнено(Объект.ВидДокумента) Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПравилаЗаполненияРеквизитов;
		ПоказатьПредупреждение(,
			НСтр("ru = 'Для настройки создания связей нужно выбрать шаблон, либо вид документа.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормыВыбора = Новый Структура;
	ПараметрыФормыВыбора.Вставить("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	ПараметрыФормыВыбора.Вставить("ТипОбъектаДО", Объект.ТипОбъектаДО);
	ПараметрыФормыВыбора.Вставить("ВидДокумента", Объект.ВидДокумента);
	ПараметрыФормыВыбора.Вставить("ПредставлениеОбъектаДО", Объект.ПредставлениеОбъектаДО);
	ПараметрыФормыВыбора.Вставить("ТипыОбъектаИС", СозданиеСвязейТекущиеДанные.ТипыОбъектаИС);
	ПараметрыФормыВыбора.Вставить("ПредставлениеРеквизитаОбъектаИС",
		СозданиеСвязейТекущиеДанные.ПредставлениеРеквизитаОбъектаИС);
	
	СуществующиеСвязи = СуществующиеСвязи(
		СозданиеСвязейТекущиеДанные.ИмяРеквизитаОбъектаИС,
		СозданиеСвязейТекущиеДанные.ТабличнаяЧастьОбъектаИС);
	ПараметрыФормыВыбора.Вставить("СуществующиеСвязи", СуществующиеСвязи);
	
	ОткрытьФорму(
		"Справочник.ПравилаЗагрузкиДанныхВДО.Форма.ВыборТипаСвязи",
		ПараметрыФормыВыбора,
		ЭтотОбъект,,,,
		Новый ОписаниеОповещения("ВыбратьТипСвязиЗавершение", ЭтотОбъект, СозданиеСвязейТекущиеДанные),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТипСвязиЗавершение(Результат, СозданиеСвязейТекущиеДанные) Экспорт
	
	Если Результат = Неопределено Или Результат = КодВозвратаДиалога.Отмена Тогда
		СозданиеСвязейТекущиеДанные.СоздаватьСвязь =
			ЗначениеЗаполнено(СозданиеСвязейТекущиеДанные.ПредставлениеТипаСвязи);
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяРеквизитаОбъектаИС", СозданиеСвязейТекущиеДанные.ИмяРеквизитаОбъектаИС);
	Отбор.Вставить("ТабличнаяЧастьОбъектаИС", СозданиеСвязейТекущиеДанные.ТабличнаяЧастьОбъектаИС);
	НайденныеСтроки = Объект.СозданиеСвязей.НайтиСтроки(Отбор);
	
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		Объект.СозданиеСвязей.Удалить(НайденнаяСтрока);
	КонецЦикла;
	
	Для Каждого ПравилоСозданияСвязей Из Результат.ПравилаСозданияСвязей Цикл
		СтрокаСозданиеСвязей = Объект.СозданиеСвязей.Добавить();
		СтрокаСозданиеСвязей.СоздаватьСвязь = Истина;
		ЗаполнитьЗначенияСвойств(
			СтрокаСозданиеСвязей,
			ПравилоСозданияСвязей,
			"ТипОбъектаДО, ВидДокументаДО, ТипСвязи, Комментарий");
		ЗаполнитьЗначенияСвойств(
			СтрокаСозданиеСвязей,
			СозданиеСвязейТекущиеДанные,
			"ИмяРеквизитаОбъектаИС, ТабличнаяЧастьОбъектаИС");
	КонецЦикла;
	
	СозданиеСвязейТекущиеДанные.ПредставлениеТипаСвязи = ПредставлениеТипаСвязи(Результат.ПравилаСозданияСвязей);
	СозданиеСвязейТекущиеДанные.СоздаватьСвязь = ЗначениеЗаполнено(СозданиеСвязейТекущиеДанные.ПредставлениеТипаСвязи);
	
	Модифицированность = Истина;
	
	ОбновитьЗаголовокСтраницыСозданияСвязей();
	
КонецПроцедуры

&НаКлиенте
Асинх Функция ИзменениеРеквизитаРазрешено(Знач Результат)
	
	Если Результат.Вариант <> ВариантПустой() И Результат.ИмяРеквизитаОбъектаДО = "РегистрационныйНомер" Тогда
		ТекстВопроса = НСтр(
			"ru = 'При установке регистрационного номера документ будет автоматически зарегистрирован, и станет недоступным для изменения.
			|Продолжить?'");
		Ответ = Ждать ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНетАсинх(ТекстВопроса);
		Возврат (Ответ = КодВозвратаДиалога.Да);
	КонецЕсли;
	
	Если Результат.Вариант <> ВариантПустой() И Результат.ИмяРеквизитаОбъектаДО = "ДатаРегистрации" Тогда
		ТекстВопроса = НСтр(
			"ru = 'При установке даты регистрации документ будет автоматически зарегистрирован, и станет недоступным для изменения.
			|Продолжить?'");
		Ответ = Ждать ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНетАсинх(ТекстВопроса);
		Возврат (Ответ = КодВозвратаДиалога.Да);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьЗаголовокСтраницыДобавленияПечатныхФорм()
	
	Количество = 0;
	Для Каждого СтрокаФормы Из ПрисоединяемыеПечатныеФормы Цикл
		Количество = Количество + ?(СтрокаФормы.Использовать, 1, 0);
	КонецЦикла;
	
	ЗаголовокЗакладки = НСтр("ru = 'Добавление печатных форм'");
	Если Количество > 0 Тогда
		ЗаголовокЗакладки = СтрШаблон("%1 (%2)", ЗаголовокЗакладки, Количество);
	КонецЕсли;
	Элементы.ГруппаПрисоединяемыеПечатныеФормы.Заголовок = ЗаголовокЗакладки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовокСтраницыСозданияСвязей()
	
	Количество = 0;
	Для Каждого СтрокаФормы Из СозданиеСвязей.ПолучитьЭлементы() Цикл
		Количество = Количество + ?(СтрокаФормы.СоздаватьСвязь, 1, 0);
		Для Каждого ПодчиненнаяСтрока Из СтрокаФормы.ПолучитьЭлементы() Цикл
			Количество = Количество + ?(ПодчиненнаяСтрока.СоздаватьСвязь, 1, 0);
		КонецЦикла;
	КонецЦикла;
	
	ЗаголовокЗакладки = НСтр("ru = 'Создание связей'");
	Если Количество > 0 Тогда
		ЗаголовокЗакладки = СтрШаблон("%1 (%2)", ЗаголовокЗакладки, Количество);
	КонецЕсли;
	Элементы.ГруппаСозданиеСвязей.Заголовок = ЗаголовокЗакладки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПравилоЗаполнения(Правило, ДоступноРедактирование = Неопределено)
	
	ЗаполнитьЗначенияСвойств(Правило, СтруктураДляОчисткиПравила(Правило.Вариант));
	
	Если ДоступноРедактирование = Неопределено Тогда
		Родитель = Неопределено;
		Если ТипЗнч(Правило) = Тип("ДанныеФормыЭлементДерева") Тогда
			Родитель = Правило.ПолучитьРодителя();
		ИначеЕсли ТипЗнч(Правило) = Тип("СтрокаДереваЗначений") Тогда
			Родитель = Правило.Родитель;
		КонецЕсли;
		Правило.ДоступноРедактирование = (Родитель = Неопределено)
			Или ДоступноРедактированиеПодчиненных(Родитель.Вариант, Родитель.ПравилаЗаполненияСтрокТаблицы);
	Иначе
		Правило.ДоступноРедактирование = ДоступноРедактирование;
	КонецЕсли;
	
	Если Правило.ЭтоТаблица Тогда
		ПодчиненныеПравила = Неопределено;
		Если ТипЗнч(Правило) = Тип("ДанныеФормыЭлементДерева") Тогда
			ПодчиненныеПравила = Правило.ПолучитьЭлементы();
		ИначеЕсли ТипЗнч(Правило) = Тип("СтрокаДереваЗначений") Тогда
			ПодчиненныеПравила = Правило.Строки;
		КонецЕсли;
		
		Если ПодчиненныеПравила <> Неопределено Тогда
			Для Каждого ПодчиненноеПравило Из ПодчиненныеПравила Цикл
				ОчиститьПравилоЗаполнения(ПодчиненноеПравило);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСозданияВидаДокумента(Результат, ПараметрыОповещения) Экспорт
	
	ВидДокументаСсылка = ПараметрыОповещения.ФормаОбъекта.Объект.Ссылка;
	Если ЗначениеЗаполнено(ВидДокументаСсылка) Тогда
		ШаблонСсылка = ШаблонСсылка(ВидДокументаСсылка);
		Если ЗначениеЗаполнено(ШаблонСсылка) Тогда
			Объект.Шаблон = ШаблонСсылка;
			ПриИзмененииШаблона();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПравилаЗаполненияРеквизитовДОПередНачаломИзмененияЗавершение(Знач Результат, Знач ТекущиеДанные) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Ждать ИзменениеРеквизитаРазрешено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, Результат);
	Модифицированность = Истина;
	
	Если ТекущиеДанные.ЭтоТаблица Тогда
		
		// Обновим подчиненные строки таблицы.
		ДоступноРедактированиеПодчиненных = ДоступноРедактированиеПодчиненных(
			ТекущиеДанные.Вариант,
			ТекущиеДанные.ПравилаЗаполненияСтрокТаблицы);
		Если ДоступноРедактированиеПодчиненных Тогда
			ПравилаЗаполненияСтрок =
				РаботаСИнтегрированнымиСистемамиВызовСервера.ПравилаЗаполненияСтрокТаблицыИзХранилища(
					Результат.ПравилаЗаполненияСтрокТаблицы,
					?(ЗначениеЗаполнено(ИнтегрированнаяСистема), ИнтегрированнаяСистема, ИмяМакета),
					Объект.ТипОбъектаДО,
					Объект.ТипОбъектаИС,
					ТекущиеДанные.ИмяРеквизитаОбъектаДО,
					ТекущиеДанные.Обновлять,
					Объект.ВидДокумента);
		КонецЕсли;
		
		Для Каждого ПодчиненноеПравило Из ТекущиеДанные.ПолучитьЭлементы() Цикл
			ПодчиненноеПравило.Обновлять = ТекущиеДанные.Обновлять;
			ПодчиненноеПравило.ДоступноРедактирование = ДоступноРедактированиеПодчиненных;
			
			Если ДоступноРедактированиеПодчиненных Тогда
				
				Если ТекущиеДанные.Вариант = ВариантРеквизит() Тогда
					// Таблица объекта ДО заполняется из таблицы объекта ИС.
					Если ПодчиненноеПравило.Вариант = ВариантРеквизит()
							И СтрНайти(ПодчиненноеПравило.ИмяРеквизитаОбъектаИС, ".") > 0
							И Не СтрНачинаетсяС(ПодчиненноеПравило.ИмяРеквизитаОбъектаИС,
								ТекущиеДанные.ИмяРеквизитаОбъектаИС) Тогда
						// Реквизит заполняется из реквизита другой таблицы.
						ОчиститьПравилоЗаполнения(ПодчиненноеПравило, ДоступноРедактированиеПодчиненных);
						
					ИначеЕсли ПодчиненноеПравило.Вариант = ВариантПустой()
							Или ПодчиненноеПравило.Вариант = ВариантРеквизитТаблицы()
							Или ПодчиненноеПравило.Вариант = ВариантШаблон()
							Или (ПодчиненноеПравило.Вариант = ВариантВыражение()
								И Не ЗначениеЗаполнено(ПодчиненноеПравило.ВычисляемоеВыражение)) Тогда
						ОчиститьПравилоЗаполнения(ПодчиненноеПравило, ДоступноРедактированиеПодчиненных);
						
					КонецЕсли;
					
				ИначеЕсли ТекущиеДанные.Вариант = ВариантРеквизитТаблицы() Тогда
					// Таблица объекта ДО заполняется по правилам для отдельных реквизитов.
					Если ПравилаЗаполненияСтрок.Количество() = 0 Тогда
						// Настройка заполнения не задана.
						ОчиститьПравилоЗаполнения(ПодчиненноеПравило, ДоступноРедактированиеПодчиненных);
					ИначеЕсли ПравилаЗаполненияСтрок.Количество() = 1 Тогда
						// Задана настройка заполнения первой строки. Перенесем настройки в форму.
						ЗаполнитьЗначенияСвойств(
							ПодчиненноеПравило,
							ПравилаЗаполненияСтрок[0][ПодчиненноеПравило.ИмяРеквизитаОбъектаДО]);
					КонецЕсли;
				КонецЕсли;
				
			Иначе
				
				ОчиститьПравилоЗаполнения(ПодчиненноеПравило, Ложь);
				ПодчиненноеПравило.Вариант = ТекущиеДанные.Вариант;
				Если ПодчиненноеПравило.Вариант = ВариантРеквизитТаблицы() Тогда
					ПодчиненноеПравило.Пояснение = НСтр("ru = 'Заполнение реквизита настроено в правиле заполнения таблицы'");
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
		
		Элементы.ПравилаЗаполненияРеквизитовДО.Развернуть(ТекущиеДанные.ПолучитьИдентификатор());
		
	КонецЕсли;
	
	ПриИзмененииПравилаЗаполненияРеквизита(ТекущиеДанные);
	ПеренестиПравилаВОбъект(Объект, ПравилаЗаполненияРеквизитовДО);
	
	Если ТекущиеДанные.ИмяРеквизитаОбъектаДО = "ВидДокумента" И ТекущиеДанные.Вариант = ВариантЗначение() Тогда
		ПравилоЗаполненияВидаДокументаЗавершениеИзменения(ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПравилоЗаполненияВидаДокументаЗавершениеИзменения(Знач ТекущиеДанные)
	
	Если Объект.ВидДокумента <> ТекущиеДанные.ЗначениеРеквизитаДО Тогда
		Объект.ВидДокумента = ТекущиеДанные.ЗначениеРеквизитаДО;
		ЗаполнитьСтруктуруРеквизитовОбъектаДОПоПравилу();
		ЗаполнитьПоУмолчаниюНаСервере(Ложь);
		
		ОбновитьПолноеПредставлениеОбъектаДО(Объект, ПредставлениеОбъектаДО);
		
		Объект.СозданиеСвязей.Очистить();
		ЗаполнитьСписокСозданияСвязей();
		ОбновитьЗаголовокСтраницыСозданияСвязей();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ВидДокумента) И Не ЗначениеЗаполнено(Объект.Шаблон) Тогда
		ШаблонСсылка = ШаблонСсылка(Объект.ВидДокумента);
		Если ЗначениеЗаполнено(ШаблонСсылка) Тогда
			ТекстВопроса = СтрШаблон(
				НСтр("ru = 'Для вида документа ""%1"" есть подходящий шаблон создания документов.
					|Использовать этот шаблон?'"),
				Объект.ВидДокумента);
			Ответ = Ждать ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНетАсинх(
				ТекстВопроса,
				ОбщегоНазначенияДокументооборотКлиент.ПараметрыВопросаДаНет(КодВозвратаДиалога.Да));
			Если Ответ = КодВозвратаДиалога.Да Тогда
				Объект.Шаблон = ШаблонСсылка;
				ПриИзмененииШаблона();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПриИзмененииОбъектаИС()
	
	Объект.ПрисоединяемыеПечатныеФормы.Очистить();
	Объект.СозданиеСвязей.Очистить();
	
	ЗаполнятьИзШаблона = Ложь;
	ЗаполнятьИзОбъектаИС = Ложь;
	ПриИзмененииОбъектаИС = Истина;
	
	Если ЗначениеЗаполнено(Объект.ТипОбъектаИС) И ЗначениеЗаполнено(ПредставлениеОбъектаИС) Тогда
		ТекстВопроса = СтрШаблон(
			НСтр("ru = 'Установить заполнение реквизитов из объекта ""%1""?'"),
			ПредставлениеОбъектаИС);
		Ответ = Ждать ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНетАсинх(ТекстВопроса);
		ЗаполнятьИзОбъектаИС = (Ответ = КодВозвратаДиалога.Да);
	КонецЕсли;
	
	ЗаполнитьПоУмолчаниюНаСервере(ЗаполнятьИзШаблона, ЗаполнятьИзОбъектаИС, ПриИзмененииОбъектаИС);
	
	ЗаполнитьСписокПечатныхФорм();
	ОбновитьЗаголовокСтраницыДобавленияПечатныхФорм();
	ЗаполнитьСписокСозданияСвязей();
	ОбновитьЗаголовокСтраницыСозданияСвязей();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПравилаЗаполненияРеквизита(ТекущиеДанные)
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Таблица) Тогда
		Родитель = ТекущиеДанные.ПолучитьРодителя();
		Если Родитель <> Неопределено И Родитель.Вариант = ВариантРеквизитТаблицы() Тогда
			ПоместитьПравилоЗаполненияРеквизитаСтрокиВХранилище(
				ТекущиеДанные.ПолучитьИдентификатор(),
				Родитель.ПравилаЗаполненияСтрокТаблицы);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПриИзмененииШаблона()
	
	ВидДокументаИзменился = ОбновитьДанныеИзШаблона();
	ЗаполнитьСтруктуруРеквизитовОбъектаДОПоПравилу();
	
	Если ВидДокументаИзменился Тогда
		ОбновитьПолноеПредставлениеОбъектаДО(Объект, ПредставлениеОбъектаДО);
		
		Объект.СозданиеСвязей.Очистить();
		ЗаполнитьСписокСозданияСвязей();
		ОбновитьЗаголовокСтраницыСозданияСвязей();
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ЗаполнитьПоУмолчаниюНаСервере(Ложь);
	
	Если Объект.Шаблон <> СтарыйШаблон И ЗначениеЗаполнено(Объект.Шаблон) И Не ШаблонЗапрещаетИзменение Тогда
		Ответ = Ждать ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНетАсинх(
			НСтр("ru = 'Установить заполнение реквизитов из шаблона?'"));
		Если Ответ = КодВозвратаДиалога.Да Тогда
			УстановитьЗаполнениеРеквизитовИзШаблона(Объект.Шаблон);
		КонецЕсли;
	КонецЕсли;
	
	СтарыйШаблон = Объект.Шаблон;
	
КонецПроцедуры

&НаКлиенте
Процедура СообщитьОбОшибке(Строка)
	
	Элементы.ПравилаЗаполненияРеквизитовДО.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПравилаЗаполненияРеквизитов;
	
	Сообщение = Новый СообщениеПользователю();
	Сообщение.Текст = СтрШаблон(
		НСтр("ru = 'Не установлено правило для обязательного %1 ""%2""'"),
		?(Строка.ЭтоТаблица, НСтр("ru = 'списка'"), НСтр("ru = 'реквизита'")),
		Строка.ПредставлениеРеквизитаОбъектаДО);
	Сообщение.Сообщить();
	
КонецПроцедуры

&НаКлиенте
Процедура СообщитьОбОшибкеУстановкиРоли(Строка)
	
	Элементы.ПрисоединяемыеПечатныеФормы.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПрисоединяемыеПечатныеФормы;
	
	Сообщение = Новый СообщениеПользователю();
	Сообщение.Текст = СтрШаблон(
		НСтр("ru = 'Не указана роль файла для печатной формы ""%1""'"),
		Строка.ПредставлениеКоманды);
	Сообщение.Сообщить();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступность()
	
	Элементы.ТипОбъектаИС.Доступность = ДоступенВыборОбъектаИС И ЗначениеЗаполнено(Объект.ТипОбъектаДО);
	Элементы.ТипФайловСохраненияПечатныхФорм.Доступность = ЗначениеЗаполнено(Объект.ТипОбъектаДО);
	Элементы.ПрисоединяемыеПечатныеФормы.Доступность = ЗначениеЗаполнено(Объект.ТипОбъектаДО);
	Элементы.Шаблон.Доступность = (Объект.ТипОбъектаДО = "Справочник.ДокументыПредприятия");
	
КонецПроцедуры

#КонецОбласти

#Область КлиентСервер

// Возвращает значение ПеречислениеСсылка.ВариантыПравилЗаполненияРеквизитов.ВыражениеНаВстроенномЯзыке
//
&НаКлиентеНаСервереБезКонтекста
Функция ВариантВыражение()
	
	Возврат ПредопределенноеЗначение("Перечисление.ВариантыПравилЗаполненияРеквизитов.ВыражениеНаВстроенномЯзыке");
	
КонецФункции

// Возвращает значение ПеречислениеСсылка.ВариантыПравилЗаполненияРеквизитов.УказанноеЗначение
//
&НаКлиентеНаСервереБезКонтекста
Функция ВариантЗначение()
	
	Возврат ПредопределенноеЗначение("Перечисление.ВариантыПравилЗаполненияРеквизитов.УказанноеЗначение");
	
КонецФункции

// Возвращает значение ПеречислениеСсылка.ВариантыПравилЗаполненияРеквизитов.ПустаяСсылка
//
&НаКлиентеНаСервереБезКонтекста
Функция ВариантПустой()
	
	Возврат ПредопределенноеЗначение("Перечисление.ВариантыПравилЗаполненияРеквизитов.ПустаяСсылка");
	
КонецФункции

// Возвращает значение ПеречислениеСсылка.ВариантыПравилЗаполненияРеквизитов.РеквизитОбъекта
//
&НаКлиентеНаСервереБезКонтекста
Функция ВариантРеквизит()
	
	Возврат ПредопределенноеЗначение("Перечисление.ВариантыПравилЗаполненияРеквизитов.РеквизитОбъекта");
	
КонецФункции

// Возвращает значение ПеречислениеСсылка.ВариантыПравилЗаполненияРеквизитов.РеквизитТаблицы
//
&НаКлиентеНаСервереБезКонтекста
Функция ВариантРеквизитТаблицы()
	
	Возврат ПредопределенноеЗначение("Перечисление.ВариантыПравилЗаполненияРеквизитов.РеквизитТаблицы");
	
КонецФункции

// Возвращает значение ПеречислениеСсылка.ВариантыПравилЗаполненияРеквизитов.ИзШаблона
//
&НаКлиентеНаСервереБезКонтекста
Функция ВариантШаблон()
	
	Возврат ПредопределенноеЗначение("Перечисление.ВариантыПравилЗаполненияРеквизитов.ИзШаблона");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПолноеПредставлениеОбъектаДО(ТекущийОбъект, ПредставлениеОбъектаДО)
	
	МассивПредставлениеОбъектаДО = Новый Массив;
	МассивПредставлениеОбъектаДО.Добавить(ПредставлениеОбъектаДО);
	Если ЗначениеЗаполнено(ТекущийОбъект.ВидДокумента) Тогда
		МассивПредставлениеОбъектаДО.Добавить(Строка(ТекущийОбъект.ВидДокумента));
	КонецЕсли;
	
	ТекущийОбъект.ПредставлениеОбъектаДО = СтрСоединить(МассивПредставлениеОбъектаДО, ", ");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПеренестиПравилаВОбъект(Объект, ПравилаЗаполненияРеквизитовДО, Модифицированность = Неопределено)
	
	Если Модифицированность <> Неопределено Тогда
		ПравилаДо = Объект.ПравилаЗаполненияРеквизитовДО.Выгрузить();
	КонецЕсли;
	
	Объект.ПравилаЗаполненияРеквизитовДО.Очистить();
	ЭлементыДерева = ПравилаЗаполненияРеквизитовДО.ПолучитьЭлементы();
	
	Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
		
		Если ПравилоЗаполнено(ЭлементДерева) Тогда
			
			НоваяСтрока = Объект.ПравилаЗаполненияРеквизитовДО.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементДерева);
			Если ТипЗнч(ЭлементДерева.ЗначениеРеквизитаДО) = Тип("Строка") Тогда
				НоваяСтрока.ЗначениеРеквизитаДОСтрока = ЭлементДерева.ЗначениеРеквизитаДО;
			КонецЕсли;
			НоваяСтрока.ПравилаЗаполненияСтрокТаблицыДанныеФормы = ЭлементДерева.ПравилаЗаполненияСтрокТаблицы;
			
			Если ЭлементДерева.ЭтоТаблица Тогда
				Для Каждого ПодчиненноеПравило Из ЭлементДерева.ПолучитьЭлементы() Цикл
					Если ПравилоЗаполнено(ПодчиненноеПравило) И ЭлементДерева.Вариант <> ВариантРеквизитТаблицы() Тогда
						НоваяСтрока = Объект.ПравилаЗаполненияРеквизитовДО.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, ПодчиненноеПравило);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Объект.ПравилаЗаполненияРеквизитовДО.Сортировать("Порядок");
	
	Если Модифицированность <> Неопределено Тогда
		ПравилаПосле = Объект.ПравилаЗаполненияРеквизитовДО.Выгрузить();
	КонецЕсли;
	
#Если Сервер Тогда
	Если Модифицированность <> Неопределено Тогда
		Модифицированность = Модифицированность
			Или Не ОбщегоНазначения.КоллекцииИдентичны(
				ПравилаДо,
				ПравилаПосле,,
				"Порядок, ИсходныйНомерСтроки, ПравилаЗаполненияСтрокТаблицыДанныеФормы",
				Истина)
			Или Не ПравилаЗаполненияСтрокТаблицыИдентичны(ПравилаДо, ПравилаПосле);
	КонецЕсли;
#КонецЕсли
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПравилаЗаполненияСтрокТаблицыИдентичны(ПравилаДо, ПравилаПосле)
	
	КолВоСтрокДо = ПравилаДо.Количество();
	КолВоСтрокПосле = ПравилаПосле.Количество();
	
	Если КолВоСтрокДо <> КолВоСтрокПосле Тогда
		Возврат Ложь;
	КонецЕсли;
	
#Если Сервер Тогда
	Для НомерСтроки = 0 По КолВоСтрокДо - 1 Цикл
		Если ПравилаДо[НомерСтроки].Вариант <> ПравилаПосле[НомерСтроки].Вариант Тогда
			Возврат Ложь;
		КонецЕсли;
		Если ПравилаДо[НомерСтроки].Вариант <> ВариантРеквизитТаблицы() Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ПравилаДо[НомерСтроки].ПравилаЗаполненияСтрокТаблицыДанныеФормы) <> Тип("ХранилищеЗначения") Тогда
			Возврат Ложь;
		КонецЕсли;
		Если ТипЗнч(ПравилаПосле[НомерСтроки].ПравилаЗаполненияСтрокТаблицыДанныеФормы) <> Тип("ХранилищеЗначения") Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ПравилаЗаполненияСтрокТаблицыДо = ПравилаДо[НомерСтроки].ПравилаЗаполненияСтрокТаблицыДанныеФормы.Получить();
		ПравилаЗаполненияСтрокТаблицыПосле = ПравилаПосле[НомерСтроки].ПравилаЗаполненияСтрокТаблицыДанныеФормы.Получить();
		Если ТипЗнч(ПравилаЗаполненияСтрокТаблицыДо) <> Тип("ТаблицаЗначений") Тогда
			Возврат Ложь;
		КонецЕсли;
		Если ТипЗнч(ПравилаЗаполненияСтрокТаблицыПосле) <> Тип("ТаблицаЗначений") Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ПравилаИдентичны = ОбщегоНазначения.КоллекцииИдентичны(
			ПравилаЗаполненияСтрокТаблицыДо,
			ПравилаЗаполненияСтрокТаблицыПосле);
		Если Не ПравилаИдентичны Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
#КонецЕсли
	
	Возврат Истина;
	
КонецФункции

// Возвращает Истина, если правило выгрузки заполнено. Для таблиц проверяются и подчиненные правила
//
&НаКлиентеНаСервереБезКонтекста
Функция ПравилоЗаполнено(Правило, Правила = Неопределено)
	
	Если Правило.ЭтоТаблица Тогда
		// Таблица.
		Если Правило.Вариант = ВариантРеквизит() И ЗначениеЗаполнено(Правило.ИмяРеквизитаОбъектаИС) Тогда
			Возврат Истина;
			
		ИначеЕсли Правило.Вариант = ВариантРеквизитТаблицы() И ЗначениеЗаполнено(Правило.ПравилаЗаполненияСтрокТаблицы) Тогда
			Возврат Истина;
			
		ИначеЕсли Правило.Вариант = ВариантВыражение() И ЗначениеЗаполнено(Правило.ВычисляемоеВыражение) Тогда
			Возврат Истина;
			
		ИначеЕсли Правило.Вариант = ВариантШаблон() Тогда
			Возврат Истина;
			
		Иначе
			Возврат Ложь;
			
		КонецЕсли;
		
	ИначеЕсли Правило.Таблица <> "" Тогда
		// Реквизит таблицы.
		Если Правило.Вариант = ВариантРеквизит() И ЗначениеЗаполнено(Правило.ИмяРеквизитаОбъектаИС) Тогда
			Возврат Истина;
			
		ИначеЕсли Правило.Вариант = ВариантЗначение() И ЗначениеЗаполнено(Правило.ЗначениеРеквизитаДО) Тогда
			Возврат Истина;
			
		ИначеЕсли Правило.Вариант = ВариантВыражение() И ЗначениеЗаполнено(Правило.ВычисляемоеВыражение) Тогда
			Возврат Истина;
			
		ИначеЕсли Правило.Вариант = ВариантШаблон() Тогда
			Возврат Истина;
			
		ИначеЕсли Правило.Вариант = ВариантРеквизитТаблицы() Тогда
			Возврат Не Правило.ДоступноРедактирование;
			
		Иначе
			Возврат Ложь;
			
		КонецЕсли;
		
	Иначе
		// Обычный реквизит.
		Возврат ЗначениеЗаполнено(Правило.Вариант)
			И (ЗначениеЗаполнено(Правило.ИмяРеквизитаОбъектаИС)
				Или ЗначениеЗаполнено(Правило.ЗначениеРеквизитаДО)
				Или ЗначениеЗаполнено(Правило.ВычисляемоеВыражение)
				Или Правило.Вариант = ВариантШаблон());
		
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеТипаСвязи(НастройкиСозданияСвязей)
	
	ПредставленияСвязей = Новый Массив;
	
	Для Каждого НастройкаСозданияСвязей Из НастройкиСозданияСвязей Цикл
		ПредставлениеСвязи = НастройкаСозданияСвязей.ТипСвязи;
		Если ПредставленияСвязей.Найти(ПредставлениеСвязи) = Неопределено Тогда
			ПредставленияСвязей.Добавить(ПредставлениеСвязи);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрСоединить(ПредставленияСвязей, ", ");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтруктураДляОчисткиПравила(Вариант)
	
	СтруктураДляОчисткиПравила = Новый Структура;
	
	Если Вариант = ВариантВыражение() Тогда
		СтруктураДляОчисткиПравила.Вставить("ID", "");
	КонецЕсли;
	
	СтруктураДляОчисткиПравила.Вставить("Вариант", ВариантПустой());
	СтруктураДляОчисткиПравила.Вставить("ВычисляемоеВыражение", "");
	СтруктураДляОчисткиПравила.Вставить("ПравилаЗаполненияСтрокТаблицы", Неопределено);
	СтруктураДляОчисткиПравила.Вставить("ЗначениеРеквизитаДО", Неопределено);
	СтруктураДляОчисткиПравила.Вставить("ПредставлениеЗначенияРеквизитаДО", "");
	СтруктураДляОчисткиПравила.Вставить("ИмяРеквизитаОбъектаИС", "");
	СтруктураДляОчисткиПравила.Вставить("Картинка", 0);
	СтруктураДляОчисткиПравила.Вставить("МестоВыполненияВыражения",
		ПредопределенноеЗначение("Перечисление.МестаВыполненияВыраженийНаВстроенномЯзыке.ПустаяСсылка"));
	СтруктураДляОчисткиПравила.Вставить("Обновлять", Ложь);
	СтруктураДляОчисткиПравила.Вставить("Пояснение", "");
	СтруктураДляОчисткиПравила.Вставить("ПредставлениеРеквизитаОбъектаИС", "");
	
	Возврат СтруктураДляОчисткиПравила;
	
КонецФункции

#КонецОбласти

#Область Сервер

&НаСервере
Процедура ДополнитьПравилаЗаполнения(ПравилаЗаполнения, РеквизитыОбъектаИС, ИмяРеквизитаОбъектаДО, ИмяРеквизитаИС)
	
	Если ПравилаЗаполнения.Получить(ИмяРеквизитаОбъектаДО) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого РеквизитИС Из РеквизитыОбъектаИС Цикл
		Если РеквизитИС.Имя = ИмяРеквизитаИС Тогда
			ПравилаЗаполнения.Вставить(ИмяРеквизитаОбъектаДО,
				Новый Структура("Имя, Представление",
					РеквизитИС.Имя,
					РеквизитИС.Синоним));
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ДоступноРедактированиеПодчиненных(РодительВариант, РодительПравилаЗаполненияСтрокТаблицы)
	
	ЗаполнениеИзНесколькихСтрок = (РодительВариант = ВариантРеквизитТаблицы()
		И РаботаСИнтегрированнымиСистемамиВызовСервера.КоличествоСтрокЗаполненияТаблицы(
			РодительПравилаЗаполненияСтрокТаблицы) > 1);
	
	Возврат (Не ЗаполнениеИзНесколькихСтрок
			И РодительВариант <> ВариантВыражение()
			И РодительВариант <> ВариантШаблон()
			И РодительВариант <> ВариантПустой());
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПоУмолчаниюНаСервере(Знач ЗаполнятьИзШаблона = Истина, Знач ЗаполнятьИзОбъектаИС = Ложь,
		Знач ПриИзмененииОбъектаИС = Ложь)
	
	РеквизитыОбъектаИС = Новый Массив;
	
	Если ЗначениеЗаполнено(Объект.ТипОбъектаИС) Тогда
		Если ЗначениеЗаполнено(ИнтегрированнаяСистема) Тогда
			СтруктураМетаданных = РаботаСИнтегрированнымиСистемами.СписокРеквизитовОбъектаИнтегрированнойСистемы(
				ИнтегрированнаяСистема, Объект.ТипОбъектаИС);
		Иначе
			СтруктураМетаданных = РаботаСИнтегрированнымиСистемами.СписокРеквизитовОбъектаИнтегрированнойСистемы(
				ИмяМакета, Объект.ТипОбъектаИС);
		КонецЕсли;
		
		Для Каждого ЭлементСпискаМетаданных Из СтруктураМетаданных Цикл
			Если ЭлементСпискаМетаданных.Имя = "Реквизиты" Тогда
				РеквизитыОбъектаИС = ЭлементСпискаМетаданных.Строки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ПравилаЗаполнения = Новый Соответствие;
	
	// Приоритетные соответствия
	Если РеквизитыОбъектаИС.Количество() > 0 Тогда
		Если Объект.ТипОбъектаДО = "Справочник.ДокументыПредприятия" Тогда
			ДополнитьПравилаЗаполнения(ПравилаЗаполнения, РеквизитыОбъектаИС, "Заголовок", "Представление");
			
		ИначеЕсли Объект.ТипОбъектаДО = "Справочник.Контрагенты" Тогда
			ДополнитьПравилаЗаполнения(ПравилаЗаполнения, РеквизитыОбъектаИС, "Наименование", "Представление");
			
		ИначеЕсли Объект.ТипОбъектаДО = "Справочник.Мероприятия" Тогда
			ДополнитьПравилаЗаполнения(ПравилаЗаполнения, РеквизитыОбъектаИС, "Наименование", "Представление");
			
		КонецЕсли;
	КонецЕсли;
	
	ЭлементыДерева = ПравилаЗаполненияРеквизитовДО.ПолучитьЭлементы();
	Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
		Если (ЭлементДерева.ИмяРеквизитаОбъектаДО = "ВидДокумента")
				Или (ПриИзмененииОбъектаИС И ЭлементДерева.Вариант = ВариантШаблон()) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПриИзмененииОбъектаИС И ЭлементДерева.Вариант <> ВариантЗначение() Тогда
			ОчиститьПравилоЗаполненияНаСервере(ЭлементДерева);
		КонецЕсли;
		
		Если ЭлементДерева.ЭтоДополнительныйРеквизитДО Или ЭлементДерева.ЭтоТаблица Тогда
			Продолжить;
		КонецЕсли;
		
		// Заполнение по совпадению имени или синонима
		Для Каждого РеквизитИС Из РеквизитыОбъектаИС Цикл
			Если ((ЭлементДерева.ИмяРеквизитаОбъектаДО = РеквизитИС.Имя)
						Или (ЭлементДерева.ПредставлениеРеквизитаОбъектаДО = РеквизитИС.Синоним))
					И ПравилаЗаполнения.Получить(ЭлементДерева.ИмяРеквизитаОбъектаДО) = Неопределено Тогда
				ПравилаЗаполнения.Вставить(ЭлементДерева.ИмяРеквизитаОбъектаДО,
					Новый Структура("Имя, Представление",
						РеквизитИС.Имя,
						РеквизитИС.Синоним));
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если ЗаполнятьИзШаблона Тогда
		УстановитьЗаполнениеРеквизитовИзШаблона(Объект.Шаблон);
	КонецЕсли;
	
	Если ЗаполнятьИзОбъектаИС Тогда
		
		// Вторичные соответствия
		Если РеквизитыОбъектаИС.Количество() > 0 Тогда
			Если Объект.ТипОбъектаДО = "Справочник.ДокументыПредприятия" Тогда
				
				ДополнитьПравилаЗаполнения(
					ПравилаЗаполнения,
					РеквизитыОбъектаИС,
					"Заголовок",
					"Наименование");
				ДополнитьПравилаЗаполнения(
					ПравилаЗаполнения,
					РеквизитыОбъектаИС,
					"Сумма",
					"СуммаДокумента");
				ДополнитьПравилаЗаполнения(
					ПравилаЗаполнения,
					РеквизитыОбъектаИС,
					"Валюта",
					"ВалютаВзаиморасчетов");
				ДополнитьПравилаЗаполнения(
					ПравилаЗаполнения,
					РеквизитыОбъектаИС,
					"Валюта",
					"ВалютаРасчетов");
				ДополнитьПравилаЗаполнения(
					ПравилаЗаполнения,
					РеквизитыОбъектаИС,
					"Валюта",
					"ВалютаДокумента");
				ДополнитьПравилаЗаполнения(
					ПравилаЗаполнения,
					РеквизитыОбъектаИС,
					"Плательщик",
					"Организация");
				ДополнитьПравилаЗаполнения(
					ПравилаЗаполнения,
					РеквизитыОбъектаИС,
					"Получатель",
					"Контрагент");
				ДополнитьПравилаЗаполнения(
					ПравилаЗаполнения,
					РеквизитыОбъектаИС,
					"Подразделение",
					"ПодразделениеОрганизации");
				ДополнитьПравилаЗаполнения(
					ПравилаЗаполнения,
					РеквизитыОбъектаИС,
					"Подготовил",
					"Автор");
				
			ИначеЕсли Объект.ТипОбъектаДО = "Справочник.Контрагенты" Тогда
				
				ДополнитьПравилаЗаполнения(
					ПравилаЗаполнения,
					РеквизитыОбъектаИС,
					"НаименованиеПолное",
					"ПолноеНаименование");
				ДополнитьПравилаЗаполнения(
					ПравилаЗаполнения,
					РеквизитыОбъектаИС,
					"ФизЛицо",
					"ФизическоеЛицо");
				ДополнитьПравилаЗаполнения(
					ПравилаЗаполнения,
					РеквизитыОбъектаИС,
					"ЮрФизЛицо",
					"ЮридическоеФизическоеЛицо");
				
			ИначеЕсли Объект.ТипОбъектаДО = "Справочник.Мероприятия" Тогда
				
				ДополнитьПравилаЗаполнения(
					ПравилаЗаполнения,
					РеквизитыОбъектаИС,
					"Подразделение",
					"ПодразделениеОрганизации");
				ДополнитьПравилаЗаполнения(
					ПравилаЗаполнения,
					РеквизитыОбъектаИС,
					"Подготовил",
					"Автор");
				
			КонецЕсли;
		КонецЕсли;
		
		Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
			Если ЭлементДерева.ИмяРеквизитаОбъектаДО = "ВидДокумента"
					Или ЭлементДерева.Вариант = ВариантШаблон()
					Или (ЭлементДерева.Вариант = ВариантЗначение()
						И ЗначениеЗаполнено(ЭлементДерева.ЗначениеРеквизитаДО))
					Или (ЭлементДерева.Вариант = ВариантВыражение()
						И ЗначениеЗаполнено(ЭлементДерева.ВычисляемоеВыражение)) Тогда
				Продолжить;
			КонецЕсли;
			
			ЗначениеЗаполнения = ПравилаЗаполнения.Получить(ЭлементДерева.ИмяРеквизитаОбъектаДО);
			Если ЗначениеЗаполнено(ЗначениеЗаполнения) Тогда
				ЭлементДерева.Вариант = ВариантРеквизит();
				ЭлементДерева.ИмяРеквизитаОбъектаИС = ЗначениеЗаполнения.Имя;
				ЭлементДерева.ПредставлениеРеквизитаОбъектаИС = ЗначениеЗаполнения.Представление;
				ЭлементДерева.Картинка = 1;
				ЭлементДерева.Обновлять = Истина;
			КонецЕсли;
			
			Если ЭлементДерева.ИмяРеквизитаОбъектаДО = "ГрифДоступа" И ЗначениеЗаполнено(ГрифДоступаПоУмолчанию) Тогда
				ОчиститьПравилоЗаполненияНаСервере(ЭлементДерева);
				ЭлементДерева.Вариант = ВариантЗначение();
				ЭлементДерева.ЗначениеРеквизитаДО = ГрифДоступаПоУмолчанию;
				ЭлементДерева.Картинка = 2;
				ЭлементДерева.Обновлять = Ложь;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	ПеренестиПравилаВОбъект(Объект, ПравилаЗаполненияРеквизитовДО);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораОбъектовИС()
	
	ОбъектыИС = РаботаСИнтегрированнымиСистемамиПовтИсп.СтруктураМетаданныхИнтегрированнойСистемы(
		Объект.УзелИнтегрированнойСистемы,
		Истина);
	
	Для Каждого ТипМетаданных Из ОбъектыИС.Строки Цикл
		Для Каждого ОбъектМетаданных Из ТипМетаданных.Строки Цикл
			СписокМетаданныхИС.Добавить(ОбъектМетаданных.Имя, ПредставлениеОбъектаМетаданных(ОбъектМетаданных, ТипМетаданных));
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПечатныхФорм()
	
	ОбновитьРолиФайлов();
	
	Если Не ЗначениеЗаполнено(Объект.ТипФайловСохраненияПечатныхФорм) Тогда
		Объект.ТипФайловСохраненияПечатныхФорм =
			РаботаСИнтегрированнымиСистемамиВызовСервера.ТипФайлаСохраняемойПечатнойФормыПоУмолчанию();
	КонецЕсли;
	
	ПрисоединяемыеПечатныеФормы.Очистить();
	
	Если ЗначениеЗаполнено(Объект.ТипОбъектаИС) И ЗначениеЗаполнено(ИнтегрированнаяСистема) Тогда
		
		Элементы.ПрисоединяемыеПечатныеФормыПредставление.Заголовок =
			СтрШаблон(НСтр("ru = 'Печатная форма из объекта ""%1""'"), ПредставлениеОбъектаИС);
		
		ТаблицаПечатныхФорм = РаботаСИнтегрированнымиСистемами.СписокПечатныхФормОбъектаИнтегрированнойСистемы(
			ИнтегрированнаяСистема,
			Объект.ТипОбъектаИС);
		
		Для Каждого СтруктураПечатнойФормы Из ТаблицаПечатныхФорм Цикл
			НоваяСтрока = ПрисоединяемыеПечатныеФормы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураПечатнойФормы);
			
			СохраненныеДанные = Неопределено;
			СтрокиПечатнойФормы = Объект.ПрисоединяемыеПечатныеФормы.НайтиСтроки(
				Новый Структура("ИмяКоманды, МенеджерПечати, ДополнительныеПараметры",
					НоваяСтрока.ИмяКоманды,
					НоваяСтрока.МенеджерПечати,
					НоваяСтрока.ДополнительныеПараметры));
			Если СтрокиПечатнойФормы.Количество() > 0 Тогда
				СохраненныеДанные = СтрокиПечатнойФормы[0];
			Иначе
				СтрокиПечатнойФормы = Объект.ПрисоединяемыеПечатныеФормы.НайтиСтроки(
					Новый Структура("ИмяКоманды, МенеджерПечати, ДополнительныеПараметры",
						НоваяСтрока.ИмяКоманды,
						НоваяСтрока.МенеджерПечати,
						""));
				Если СтрокиПечатнойФормы.Количество() > 0 Тогда
					СохраненныеДанные = СтрокиПечатнойФормы[0];
				КонецЕсли;
			КонецЕсли;
			
			Если СохраненныеДанные <> Неопределено Тогда
				НоваяСтрока.Использовать = Истина;
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СохраненныеДанные, "Обновлять, РольФайла");
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Элементы.ГруппаПрисоединяемыеПечатныеФормы.Видимость = (ПрисоединяемыеПечатныеФормы.Количество() > 0);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСозданияСвязей()
	
	СозданиеСвязей.ПолучитьЭлементы().Очистить();
	
	Если ПоддерживаетсяАвтоматическоеСозданиеСвязей()
			И ЗначениеЗаполнено(Объект.ТипОбъектаИС)
			И ЗначениеЗаполнено(ИнтегрированнаяСистема) Тогда
		
		ДеревоСозданияСвязей = РеквизитФормыВЗначение("СозданиеСвязей");
		
		Элементы.СозданиеСвязейПредставлениеРеквизитаОбъектаИС.Заголовок = СтрШаблон(
			НСтр("ru = 'Реквизит объекта ""%1"" (значение из которого можно связать с объектом 1С:Документооборота)'"),
			ПредставлениеОбъектаИС);
		
		ВсеОбъектыИС = РаботаСИнтегрированнымиСистемамиПовтИсп.СтруктураМетаданныхИнтегрированнойСистемы(
			ИнтегрированнаяСистема,
			Истина);
		СтруктураОбъектаИС = РаботаСИнтегрированнымиСистемамиПовтИсп.СтруктураМетаданныхОбъектаИнтегрированнойСистемы(
			ИнтегрированнаяСистема,
			Объект.ТипОбъектаИС);
		
		// Добавим подходящие реквизиты объекта ИС.
		РеквизитыОбъектаИС = Новый Массив;
		Для Каждого ЭлементСпискаМетаданных Из СтруктураОбъектаИС.Строки Цикл
			Если ЭлементСпискаМетаданных.Имя = "Реквизиты" Тогда
				РеквизитыОбъектаИС = ЭлементСпискаМетаданных.Строки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Для Каждого РеквизитОбъектаИС Из РеквизитыОбъектаИС Цикл
			ТипыРеквизитаОбъектаИС = ТипыРеквизитаОбъектаИС(РеквизитОбъектаИС, ВсеОбъектыИС);
			Если ТипыРеквизитаОбъектаИС.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			// Реквизит подходит для создания связи.
			ЗаполнитьСтрокуДереваСозданияСвязей(
				ДеревоСозданияСвязей.Строки.Добавить(),
				РеквизитОбъектаИС,
				ТипыРеквизитаОбъектаИС);
		КонецЦикла;
		
		// Добавим подходящие табличные части объекта ИС.
		ТабличныеЧастиОбъектаИС = Новый Массив;
		Для Каждого ЭлементСпискаМетаданных Из СтруктураОбъектаИС.Строки Цикл
			Если ЭлементСпискаМетаданных.Имя = "ТабличныеЧасти" Тогда
				ТабличныеЧастиОбъектаИС = ЭлементСпискаМетаданных.Строки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Для Каждого ТабличнаяЧасть Из ТабличныеЧастиОбъектаИС Цикл
			СтрокаТабличнаяЧасть = Неопределено;
			Для Каждого РеквизитТабличнойЧасти Из ТабличнаяЧасть.Строки Цикл
				ТипыРеквизитаОбъектаИС = ТипыРеквизитаОбъектаИС(РеквизитТабличнойЧасти, ВсеОбъектыИС);
				Если ТипыРеквизитаОбъектаИС.Количество() = 0 Тогда
					Продолжить;
				КонецЕсли;
				// Реквизит подходит для создания связи.
				Если СтрокаТабличнаяЧасть = Неопределено Тогда
					СтрокаТабличнаяЧасть = ДеревоСозданияСвязей.Строки.Добавить();
					СтрокаТабличнаяЧасть.ИмяРеквизитаОбъектаИС = ТабличнаяЧасть.Имя;
					СтрокаТабличнаяЧасть.ПредставлениеРеквизитаОбъектаИС = ТабличнаяЧасть.Синоним;
					СтрокаТабличнаяЧасть.ЭтоТабличнаяЧасть = Истина;
				КонецЕсли;
				ЗаполнитьСтрокуДереваСозданияСвязей(
					СтрокаТабличнаяЧасть.Строки.Добавить(),
					РеквизитТабличнойЧасти,
					ТипыРеквизитаОбъектаИС,
					ТабличнаяЧасть.Имя);
			КонецЦикла;
			Если СтрокаТабличнаяЧасть <> Неопределено Тогда
				СтрокаТабличнаяЧасть.Строки.Сортировать("ПредставлениеРеквизитаОбъектаИС");
			КонецЕсли;
		КонецЦикла;
		
		ДеревоСозданияСвязей.Строки.Сортировать("ЭтоТабличнаяЧасть, ЭтоДопРеквизит, ПредставлениеРеквизитаОбъектаИС");
		
		ЗначениеВРеквизитФормы(ДеревоСозданияСвязей, "СозданиеСвязей");
		
	КонецЕсли;
	
	Элементы.ГруппаСозданиеСвязей.Видимость = (СозданиеСвязей.ПолучитьЭлементы().Количество() > 0);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтрокуДереваСозданияСвязей(СтрокаСозданияСвязей, РеквизитОбъектаИС, ТипыРеквизитаОбъектаИС,
		ТабличнаяЧасть = "")
	
	СтрокаСозданияСвязей.ТипыОбъектаИС = ТипыРеквизитаОбъектаИС;
	СтрокаСозданияСвязей.ПредставлениеРеквизитаОбъектаИС = РеквизитОбъектаИС.Синоним;
	СтрокаСозданияСвязей.ИмяРеквизитаОбъектаИС = РеквизитОбъектаИС.Имя;
	СтрокаСозданияСвязей.ТабличнаяЧастьОбъектаИС = ТабличнаяЧасть;
	Если СтрНайти(РеквизитОбъектаИС.Имя, "ДопРеквизит_") = 1 Тогда
		СтрокаСозданияСвязей.ЭтоДопРеквизит = Истина;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяРеквизитаОбъектаИС", РеквизитОбъектаИС.Имя);
	Отбор.Вставить("ТабличнаяЧастьОбъектаИС", ТабличнаяЧасть);
	НастройкиСозданияСвязей = Объект.СозданиеСвязей.НайтиСтроки(Отбор);
	
	Если НастройкиСозданияСвязей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаСозданияСвязей.ПредставлениеТипаСвязи = ПредставлениеТипаСвязи(НастройкиСозданияСвязей);
	
	Отбор = Новый Структура;
	Отбор.Вставить("СоздаватьСвязь", Истина);
	Отбор.Вставить("ИмяРеквизитаОбъектаИС", РеквизитОбъектаИС.Имя);
	Отбор.Вставить("ТабличнаяЧастьОбъектаИС", ТабличнаяЧасть);
	НастройкиСозданияСвязей = Объект.СозданиеСвязей.НайтиСтроки(Отбор);
	
	Если НастройкиСозданияСвязей.Количество() > 0 Тогда
		СтрокаСозданияСвязей.СоздаватьСвязь = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтруктуруРеквизитовОбъектаДОПоПравилу()
	
	ОбновитьРолиФайлов();
	
	ЭлементыДерева = ПравилаЗаполненияРеквизитовДО.ПолучитьЭлементы();
	ЭлементыДерева.Очистить();
	
	Если ЗначениеЗаполнено(Объект.Шаблон) Тогда
		ШаблонОбъект = Объект.Шаблон.ПолучитьОбъект();
		РеквизитыШаблона = Метаданные.Справочники.ШаблоныДокументов.Реквизиты;
		ТабличныеЧастиШаблона = Метаданные.Справочники.ШаблоныДокументов.ТабличныеЧасти;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеКопирования) Тогда
		ОбъектПравилаЗаполненияРеквизитовДО = ЗначениеКопирования.ПравилаЗаполненияРеквизитовДО;
		ЗначениеКопирования = Неопределено;
	Иначе
		ОбъектПравилаЗаполненияРеквизитовДО = РеквизитФормыВЗначение("Объект").ПравилаЗаполненияРеквизитовДО;
	КонецЕсли;
	
	ДоступноРедактирование = Новый Соответствие;
	ПравилаЗаполненияСтрокТаблицыИзХранилища = Новый Соответствие;
	
	Реквизиты = Справочники.ПравилаЗагрузкиДанныхВДО.РеквизитыОбъектаДО(Объект.ТипОбъектаДО, Объект.ВидДокумента);
	Для Каждого Строка Из Реквизиты Цикл
		
		Если Строка.ЭтоТаблица Тогда
			НоваяСтрока = ЭлементыДерева.Добавить();
			НоваяСтрока.ЭтоТаблица = Истина;
			УровеньДерева = НоваяСтрока.ПолучитьЭлементы();
		Иначе
			Если Строка.Таблица = "" Тогда
				УровеньДерева = ЭлементыДерева;
			КонецЕсли;
			НоваяСтрока = УровеньДерева.Добавить();
			НоваяСтрока.Таблица = Строка.Таблица;
		КонецЕсли;
		
		НоваяСтрока.ИмяРеквизитаОбъектаДО = Строка.Имя;
		НоваяСтрока.ПредставлениеРеквизитаОбъектаДО = Строка.Представление;
		Если ЗначениеЗаполнено(Строка.Подсказка) И Строка.Подсказка <> Строка.Представление Тогда
			НоваяСтрока.Подсказка = Строка.Подсказка;
		КонецЕсли;
		НоваяСтрока.ТипРеквизитаОбъектаДО = Строка.Тип;
		НоваяСтрока.ЭтоДополнительныйРеквизитДО = Строка.ЭтоДополнительныйРеквизит;
		НоваяСтрока.ДополнительныйРеквизитДО = Строка.ДополнительныйРеквизит;
		НоваяСтрока.ОбязательноеЗаполнение = Строка.ОбязательноеЗаполнение;
		НоваяСтрока.СвязиПараметровВыбора.ЗагрузитьЗначения(Строка.СвязиПараметровВыбора);
		НоваяСтрока.Порядок = Строка.Порядок;
		НоваяСтрока.ИмяМетодаДляЗаполнения = Строка.ИмяМетодаДляЗаполнения;
		НоваяСтрока.ИмяПоляПроверкиДоступностиПоСостоянию = Строка.ИмяПоляПроверкиДоступностиПоСостоянию;
		НоваяСтрока.ИмяМетодаПроверкиДоступностиПоСостоянию = Строка.ИмяМетодаПроверкиДоступностиПоСостоянию;
		
		// Найдем подходящую строку в табличной части.
		ПараметрыОтбора = Новый Структура("ЭтоДополнительныйРеквизитДО", Строка.ЭтоДополнительныйРеквизит);
		Если Строка.ЭтоДополнительныйРеквизит Тогда
			ПараметрыОтбора.Вставить("ДополнительныйРеквизитДО", Строка.ДополнительныйРеквизит);
		Иначе
			ПараметрыОтбора.Вставить("ЭтоТаблица", Строка.ЭтоТаблица);
			ПараметрыОтбора.Вставить("Таблица", Строка.Таблица);
			ПараметрыОтбора.Вставить("ИмяРеквизитаОбъектаДО", Строка.Имя);
		КонецЕсли;
		НайденныеСтроки = ОбъектПравилаЗаполненияРеквизитовДО.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() > 0 Тогда
			ИсключаяСвойства = Новый Массив;
			ИсключаяСвойства.Добавить("ИмяМетодаДляЗаполнения");
			ИсключаяСвойства.Добавить("ИмяПоляПроверкиДоступностиПоСостоянию");
			ИсключаяСвойства.Добавить("ИмяМетодаПроверкиДоступностиПоСостоянию");
			Если НайденныеСтроки[0].Вариант <> ВариантРеквизитТаблицы() Тогда
				ИсключаяСвойства.Добавить("ПравилаЗаполненияСтрокТаблицы");
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(
				НоваяСтрока,
				НайденныеСтроки[0],,
				СтрСоединить(ИсключаяСвойства, ","));
			Если Не ПустаяСтрока(НайденныеСтроки[0].ЗначениеРеквизитаДОСтрока) Тогда
				НоваяСтрока.ЗначениеРеквизитаДО = НайденныеСтроки[0].ЗначениеРеквизитаДОСтрока;
			КонецЕсли;
		КонецЕсли;
		
		Родитель = НоваяСтрока.ПолучитьРодителя();
		Если Родитель = Неопределено Тогда
			РодительID = "БезРодителя";
		Иначе
			РодительID = Родитель.ID;
			Если ПравилаЗаполненияСтрокТаблицыИзХранилища[РодительID] = Неопределено Тогда
				ПравилаЗаполненияСтрокТаблицыИзХранилища[РодительID] =
					РаботаСИнтегрированнымиСистемамиВызовСервера.ПравилаЗаполненияСтрокТаблицыИзХранилища(
						Родитель.ПравилаЗаполненияСтрокТаблицы,
						?(ЗначениеЗаполнено(ИнтегрированнаяСистема), ИнтегрированнаяСистема, ИмяМакета),
						Объект.ТипОбъектаДО,
						Объект.ТипОбъектаИС,
						Родитель.ИмяРеквизитаОбъектаДО,
						Родитель.Обновлять,
						Объект.ВидДокумента);
			КонецЕсли;
		КонецЕсли;
		
		Если ДоступноРедактирование[РодительID] = Неопределено Тогда
			ДоступноРедактирование[РодительID] = (Родитель = Неопределено)
				Или ДоступноРедактированиеПодчиненных(Родитель.Вариант, Родитель.ПравилаЗаполненияСтрокТаблицы);
		КонецЕсли;
		
		НоваяСтрока.ДоступноРедактирование = ДоступноРедактирование[РодительID];
		Если Не ДоступноРедактирование[РодительID] Тогда
			ОчиститьПравилоЗаполненияНаСервере(НоваяСтрока, Ложь);
			НоваяСтрока.Вариант = Родитель.Вариант;
			Если НоваяСтрока.Вариант = ВариантРеквизитТаблицы() Тогда
				НоваяСтрока.Пояснение = НСтр("ru = 'Заполнение реквизита настроено в правиле заполнения таблицы'");
			КонецЕсли;
		КонецЕсли;
		
		Если НоваяСтрока.ДоступноРедактирование
				И РодительID <> "БезРодителя"
				И ПравилаЗаполненияСтрокТаблицыИзХранилища[РодительID].Количество() = 1 Тогда
			ЗаполнитьЗначенияСвойств(
				НоваяСтрока,
				ПравилаЗаполненияСтрокТаблицыИзХранилища[РодительID][0][НоваяСтрока.ИмяРеквизитаОбъектаДО]);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.Шаблон)
				И ((Строка.ЭтоТаблица И ТабличныеЧастиШаблона.Найти(Строка.Имя) <> Неопределено)
					Или (Строка.Таблица = "" И РеквизитыШаблона.Найти(Строка.Имя) <> Неопределено)
					Или (Строка.Имя = "Получатель" И Строка.Таблица = "")
					Или (Строка.Имя = "Комментарий" И Строка.Таблица = "")
					Или (Строка.Имя = "СтатьиДДС" И Строка.Таблица = "")) Тогда
			
			НоваяСтрока.ВозможноЗаполнениеИзШаблона = Истина;
			
			Если ШаблонЗапрещаетИзменение Или (Строка.Имя = "ВидДокумента" И Строка.Таблица = "") Тогда
				ОчиститьПравилоЗаполненияНаСервере(НоваяСтрока, ДоступноРедактирование[РодительID]);
				НоваяСтрока.Вариант = ВариантШаблон();
			КонецЕсли;
			
			Если РеквизитыШаблона.Найти(Строка.Имя) <> Неопределено И ЗначениеЗаполнено(ШаблонОбъект[Строка.Имя]) Тогда
				НоваяСтрока.ШаблонЗначение = СокрЛП(ШаблонОбъект[Строка.Имя]);
			КонецЕсли;
			
			Если Строка.Имя = "Получатель" И Строка.Таблица = "" Тогда
				СтрокиПолучатель = ШаблонОбъект.Стороны.НайтиСтроки(
					Новый Структура("Наименование", Справочники.НаименованияСторон.Получатель));
				Если СтрокиПолучатель.Количество() = 1 И ЗначениеЗаполнено(СтрокиПолучатель[0].Сторона) Тогда
					НоваяСтрока.ШаблонЗначение = СокрЛП(СтрокиПолучатель[0].Сторона);
				КонецЕсли;
			КонецЕсли;
			
			Если Строка.Имя = "Комментарий" И Строка.Таблица = "" Тогда
				НоваяСтрока.ШаблонЗначение = СокрЛП(ШаблонОбъект.КомментарийКДокументу);
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не НоваяСтрока.ВозможноЗаполнениеИзШаблона И НоваяСтрока.Вариант = ВариантШаблон()
				И НоваяСтрока.Таблица = "" Тогда
			ОчиститьПравилоЗаполненияНаСервере(НоваяСтрока, ДоступноРедактирование[РодительID]);
		КонецЕсли;
		
		Если НоваяСтрока.Вариант = ВариантРеквизит() Тогда
			НоваяСтрока.Картинка = 1;
			НоваяСтрока.ПредставлениеРеквизитаОбъектаИС =
				РаботаСИнтегрированнымиСистемами.ПредставлениеРеквизитаОбъектаИнтегрированнойСистемы(
					?(ЗначениеЗаполнено(ИнтегрированнаяСистема), ИнтегрированнаяСистема, ИмяМакета),
					Объект.ТипОбъектаИС,
					НоваяСтрока.ИмяРеквизитаОбъектаИС,,
					ПредставлениеИС,
					Объект.ПредставлениеОбъектаИС);
			
		ИначеЕсли НоваяСтрока.Вариант = ВариантРеквизитТаблицы() И НоваяСтрока.ЭтоТаблица Тогда
			НоваяСтрока.Картинка = 1;
			НоваяСтрока.Пояснение = НСтр("ru = 'Заполняется по правилам для отдельных реквизитов таблицы'");
			
		ИначеЕсли НоваяСтрока.Вариант = ВариантЗначение() Тогда
			НоваяСтрока.Картинка = 2;
			
		ИначеЕсли НоваяСтрока.Вариант = ВариантВыражение() И ЗначениеЗаполнено(НоваяСтрока.ВычисляемоеВыражение) Тогда
			НоваяСтрока.Картинка = 3;
			
		ИначеЕсли НоваяСтрока.Вариант = ВариантШаблон() И НоваяСтрока.Таблица = "" Тогда
			НоваяСтрока.Картинка = 5;
			Если ЗначениеЗаполнено(НоваяСтрока.ШаблонЗначение) Тогда
				НоваяСтрока.Пояснение = СтрШаблон(НСтр("ru = '%1 (Заполняется из шаблона)'"),
					НоваяСтрока.ШаблонЗначение);
			Иначе
				НоваяСтрока.Пояснение = НСтр("ru = 'Заполняется из шаблона'");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПеренестиПравилаВОбъект(Объект, ПравилаЗаполненияРеквизитовДО, Модифицированность);
	
КонецПроцедуры

&НаСервере
Функция ОбновитьДанныеИзШаблона()
	
	СтарыйВидДокумента = Объект.ВидДокумента;
	
	ШаблонЗапрещаетИзменение = Ложь;
	Объект.ВидДокумента = Справочники.ВидыДокументов.ПустаяСсылка();
	
	Для Каждого Строка Из Объект.ПравилаЗаполненияРеквизитовДО Цикл
		Если Строка.ИмяРеквизитаОбъектаДО = "ВидДокумента" И Строка.Вариант = ВариантЗначение() Тогда
			Объект.ВидДокумента = Строка.ЗначениеРеквизитаДО;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Объект.Шаблон) Тогда
		РеквизитыШаблона = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.Шаблон,
			"ВидДокумента, ЗапретитьИзменятьРеквизитыИзШаблона");
		Объект.ВидДокумента = РеквизитыШаблона.ВидДокумента;
		ШаблонЗапрещаетИзменение = РеквизитыШаблона.ЗапретитьИзменятьРеквизитыИзШаблона;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтарыйШаблон) И Не ЗначениеЗаполнено(Объект.Шаблон)
			И Объект.ТипОбъектаДО = "Справочник.ДокументыПредприятия" Тогда
		ВидДокументаИзСтарогоШаблона = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтарыйШаблон, "ВидДокумента");
		Если ЗначениеЗаполнено(ВидДокументаИзСтарогоШаблона) Тогда
			Для Каждого Строка Из Объект.ПравилаЗаполненияРеквизитовДО Цикл
				Если Строка.ИмяРеквизитаОбъектаДО = "ВидДокумента" И Строка.Вариант = ВариантШаблон() Тогда
					Строка.Вариант = ВариантЗначение();
					Строка.ЗначениеРеквизитаДО = ВидДокументаИзСтарогоШаблона;
					Объект.ВидДокумента = ВидДокументаИзСтарогоШаблона;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат (Объект.ВидДокумента <> СтарыйВидДокумента);
	
КонецФункции

&НаСервере
Процедура ОбновитьРолиФайлов()
	
	СписокВыбораРолей = Элементы.ПрисоединяемыеПечатныеФормыРольФайла.СписокВыбора;
	СписокВыбораРолей.Очистить();
	
	Если Объект.ТипОбъектаДО = "Справочник.ДокументыПредприятия"
			И ЗначениеЗаполнено(Объект.ВидДокумента)
			И ПолучитьФункциональнуюОпцию("ИспользоватьРолиФайлов") Тогда
		РолиФайлов = Делопроизводство.РолиФайловДляВидаДокументов(Объект.ВидДокумента);
		Для Каждого Строка Из РолиФайлов Цикл
			СписокВыбораРолей.Добавить(Строка.Роль);
		КонецЦикла;
	КонецЕсли;
	
	ИспользоватьРолиФайлов = (СписокВыбораРолей.Количество() > 0);
	Элементы.ПрисоединяемыеПечатныеФормыРольФайла.Видимость = ИспользоватьРолиФайлов;
	
	БылиИзменения = Ложь;
	Для Каждого Строка Из ПрисоединяемыеПечатныеФормы Цикл
		Если ЗначениеЗаполнено(Строка.РольФайла)
				И СписокВыбораРолей.НайтиПоЗначению(Строка.РольФайла) = Неопределено Тогда
			Строка.РольФайла = Справочники.РолиФайлов.ПустаяСсылка();
			БылиИзменения = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если БылиИзменения Тогда
		ПеренестиПечатныеФормыВОбъект();
	КонецЕсли;
	
КонецПроцедуры

// Изменяет форму согласно доступности сервиса ДО и номеру его версии.
//
&НаСервере
Процедура ОбработатьФормуСогласноВерсииСервиса()
	
	Если Объект.ИзМакета Тогда
		ВерсияСервиса = "";
	Иначе
		ТекстСообщенияОбОшибке = "";
		ВерсияСервиса = РаботаСИнтегрированнымиСистемамиВызовСервера.ВерсияСервиса(
			ИнтегрированнаяСистема,
			ТекстСообщенияОбОшибке);
		Если Не ПустаяСтрока(ТекстСообщенияОбОшибке) Тогда
			Сообщить(ТекстСообщенияОбОшибке);
		КонецЕсли;
	КонецЕсли;
	
	Если ВерсияСервиса = "0.0.0.0" Тогда
		Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаИСНедоступна;
	Иначе
		Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаИСДоступна;
		
		Если ЗначениеЗаполнено(Объект.ТипОбъектаИС) Тогда
			ПредставлениеОбъектаИС = РаботаСИнтегрированнымиСистемами.ПредставлениеОбъектаИнтегрированнойСистемы(
				Объект.УзелИнтегрированнойСистемы,
				Объект.ТипОбъектаИС);
		КонецЕсли;
		
		ОбновитьДанныеИзШаблона();
		ЗаполнитьСтруктуруРеквизитовОбъектаДОПоПравилу();
		ЗаполнитьСписокВыбораОбъектовИС();
		ЗаполнитьСписокПечатныхФорм();
		ЗаполнитьСписокСозданияСвязей();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьПравилоЗаполненияНаСервере(Правило, ДоступноРедактирование = Неопределено)
	
	ЗаполнитьЗначенияСвойств(Правило, СтруктураДляОчисткиПравила(Правило.Вариант));
	
	Если ДоступноРедактирование = Неопределено Тогда
		Родитель = Неопределено;
		Если ТипЗнч(Правило) = Тип("ДанныеФормыЭлементДерева") Тогда
			Родитель = Правило.ПолучитьРодителя();
		ИначеЕсли ТипЗнч(Правило) = Тип("СтрокаДереваЗначений") Тогда
			Родитель = Правило.Родитель;
		КонецЕсли;
		Правило.ДоступноРедактирование = (Родитель = Неопределено)
			Или ДоступноРедактированиеПодчиненных(Родитель.Вариант, Родитель.ПравилаЗаполненияСтрокТаблицы);
	Иначе
		Правило.ДоступноРедактирование = ДоступноРедактирование;
	КонецЕсли;
	
	Если Правило.ЭтоТаблица Тогда
		ПодчиненныеПравила = Неопределено;
		Если ТипЗнч(Правило) = Тип("ДанныеФормыЭлементДерева") Тогда
			ПодчиненныеПравила = Правило.ПолучитьЭлементы();
		ИначеЕсли ТипЗнч(Правило) = Тип("СтрокаДереваЗначений") Тогда
			ПодчиненныеПравила = Правило.Строки;
		КонецЕсли;
		
		Если ПодчиненныеПравила <> Неопределено Тогда
			Для Каждого ПодчиненноеПравило Из ПодчиненныеПравила Цикл
				ОчиститьПравилоЗаполненияНаСервере(ПодчиненноеПравило);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиПечатныеФормыВОбъект()
	
	ПечатныеФормыДо = Объект.ПрисоединяемыеПечатныеФормы.Выгрузить();
	
	Объект.ПрисоединяемыеПечатныеФормы.Очистить();
	Для Каждого Строка Из ПрисоединяемыеПечатныеФормы Цикл
		Если Строка.Использовать Тогда
			НоваяСтрока = Объект.ПрисоединяемыеПечатныеФормы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЕсли;
	КонецЦикла;
	
	ПечатныеФормыПосле = Объект.ПрисоединяемыеПечатныеФормы.Выгрузить();
	
	Модифицированность = Модифицированность
		Или Не ОбщегоНазначения.КоллекцииИдентичны(ПечатныеФормыДо, ПечатныеФормыПосле,, "ИсходныйНомерСтроки");
	
КонецПроцедуры

&НаСервере
Функция ПоддерживаетсяАвтоматическоеСозданиеСвязей()
	
	ПоддерживаетсяАвтоматическоеСозданиеСвязей = ЗначениеЗаполнено(ИнтегрированнаяСистема)
		И ЗначениеЗаполнено(Объект.ТипОбъектаИС)
		И (Объект.ТипОбъектаДО = "Справочник.ДокументыПредприятия" Или Объект.ТипОбъектаДО = "Справочник.Мероприятия")
		И РаботаСИнтегрированнымиСистемамиПовтИсп.ДоступенФункционалВерсииСервиса(ИнтегрированнаяСистема, "3.0.3.3");
	
	Возврат ПоддерживаетсяАвтоматическоеСозданиеСвязей;
	
КонецФункции

&НаСервере
Процедура ПоместитьПравилоЗаполненияРеквизитаСтрокиВХранилище(ИдентификаторСтроки, ПравилаЗаполненияСтрокТаблицы)
	
	СтрокаДереваФормы = ПравилаЗаполненияРеквизитовДО.НайтиПоИдентификатору(ИдентификаторСтроки);
	РаботаСИнтегрированнымиСистемами.ПоместитьПравилоЗаполненияРеквизитаСтрокиВХранилище(
		СтрокаДереваФормы,
		ПравилаЗаполненияСтрокТаблицы,
		Объект.ТипОбъектаДО,
		Объект.ВидДокумента);
	
КонецПроцедуры

&НаСервере
Функция ПравилаЗаполненияРеквизитовДОНайтиСтроки(ИмяРеквизита, Таблица)
	
	НайденныеСтроки = Новый Массив;
	Для Каждого Строка Из Объект.ПравилаЗаполненияРеквизитовДО Цикл
		Если Строка.ИмяРеквизитаОбъектаДО = ИмяРеквизита И Строка.Таблица = Таблица Тогда
			НайденныеСтроки.Добавить(Новый Структура("ЗначениеРеквизитаДО", Строка.ЗначениеРеквизитаДО));
		ИначеЕсли Строка.Вариант = ВариантРеквизитТаблицы()
				И Строка.ИмяРеквизитаОбъектаДО = Таблица
				И ТипЗнч(Строка.ПравилаЗаполненияСтрокТаблицыДанныеФормы) = Тип("ХранилищеЗначения") Тогда
			ПодчиненныеСтроки = Строка.ПравилаЗаполненияСтрокТаблицыДанныеФормы.Получить();
			Если ТипЗнч(ПодчиненныеСтроки) = Тип("ТаблицаЗначений") И ПодчиненныеСтроки.Количество() = 1 Тогда
				ПодчиненнаяСтрока = Новый Структура(
					"ЗначениеРеквизитаДО",
					ПодчиненныеСтроки[0][ИмяРеквизита+"_ЗначениеРеквизитаДО"]);
				НайденныеСтроки.Добавить(ПодчиненнаяСтрока);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НайденныеСтроки;
	
КонецФункции

&НаСервере
Функция ПредставлениеОбъектаМетаданных(ОбъектМетаданных, ТипМетаданных)
	
	Если ПустаяСтрока(ОбъектМетаданных.Синоним) Тогда
		Возврат ОбъектМетаданных.Имя
	Иначе
		ПредставлениеТипа = НСтр("ru = 'Документ'");
		Если ТипМетаданных.Имя = "Справочник" Тогда
			ПредставлениеТипа = НСтр("ru = 'Справочник'");
		ИначеЕсли ТипМетаданных.Имя = "ПланВидовХарактеристик" Тогда
			ПредставлениеТипа = НСтр("ru = 'План видов характеристик'");
		ИначеЕсли ТипМетаданных.Имя = "БизнесПроцесс" Тогда
			ПредставлениеТипа = НСтр("ru = 'Бизнес-процесс'");
		ИначеЕсли ТипМетаданных.Имя = "Задача" Тогда
			ПредставлениеТипа = НСтр("ru = 'Задача'");
		КонецЕсли;
		Возврат СтроковыеФункции.ФорматированнаяСтрока(
			"<span style=""color: ЦветНеАктивнойСтроки"">%1:</span> %2",
			ПредставлениеТипа,
			ОбъектМетаданных.Синоним);
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция РеквизитПоддерживаетИнтеграцию(ТипРеквизита, ОбъектыИС)
	
	Для Каждого ТипМетаданных Из ОбъектыИС.Строки Цикл
		Для Каждого ОбъектМетаданных Из ТипМетаданных.Строки Цикл
			Если ТипРеквизита.Значение = ОбъектМетаданных.Имя Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция СуществующиеСвязи(Знач ИмяРеквизитаОбъектаИС, Знач ТабличнаяЧастьОбъектаИС)
	
	СуществующиеСвязи = ОбщегоНазначения.ТаблицаЗначенийВМассив(Объект.СозданиеСвязей.Выгрузить());
	
	КоличествоСтрок = СуществующиеСвязи.Количество();
	Для Индекс = 1 По КоличествоСтрок Цикл
		ИндексСтроки = КоличествоСтрок - Индекс;
		Строка = СуществующиеСвязи[ИндексСтроки];
		Если Строка.ИмяРеквизитаОбъектаИС <> ИмяРеквизитаОбъектаИС
				Или Строка.ТабличнаяЧастьОбъектаИС <> ТабличнаяЧастьОбъектаИС Тогда
			СуществующиеСвязи.Удалить(ИндексСтроки);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СуществующиеСвязи;
	
КонецФункции

&НаСервере
Функция ТипыРеквизитаОбъектаИС(ОписаниеРеквизитаОбъектаИС, ВсеОбъектыИС)
	
	ТипыРеквизитаОбъектаИС = Новый СписокЗначений;
	Для Каждого ТипРеквизита Из ОписаниеРеквизитаОбъектаИС.СписокТипов Цикл
		Если РеквизитПоддерживаетИнтеграцию(ТипРеквизита, ВсеОбъектыИС) Тогда
			ТипыРеквизитаОбъектаИС.Добавить(ТипРеквизита.Значение, ТипРеквизита.Представление);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТипыРеквизитаОбъектаИС;
	
КонецФункции

&НаСервере
Процедура УстановитьЗаполнениеРеквизитовИзШаблона(Знач Шаблон)
	
	Если Не ЗначениеЗаполнено(Шаблон) Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонОбъект = Шаблон.ПолучитьОбъект();
	РеквизитыШаблона = Метаданные.Справочники.ШаблоныДокументов.Реквизиты;
	ТабличныеЧастиШаблона = Метаданные.Справочники.ШаблоныДокументов.ТабличныеЧасти;
	
	ЭлементыДерева = ПравилаЗаполненияРеквизитовДО.ПолучитьЭлементы();
	
	Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
		
		Если ЭлементДерева.ИмяРеквизитаОбъектаДО = "ВидДокумента" И ЭлементДерева.Таблица = "" Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполняетсяИзШаблона = Ложь;
		ШаблонЗначение = Неопределено;
		
		Если ЭлементДерева.ЭтоТаблица
				И ТабличныеЧастиШаблона.Найти(ЭлементДерева.ИмяРеквизитаОбъектаДО) <> Неопределено
				И ШаблонОбъект[ЭлементДерева.ИмяРеквизитаОбъектаДО].Количество() > 0 Тогда
			ЗаполняетсяИзШаблона = Истина;
			
		ИначеЕсли ЭлементДерева.Таблица = ""
				И РеквизитыШаблона.Найти(ЭлементДерева.ИмяРеквизитаОбъектаДО) <> Неопределено
				И ЗначениеЗаполнено(ШаблонОбъект[ЭлементДерева.ИмяРеквизитаОбъектаДО]) Тогда
			ЗаполняетсяИзШаблона = Истина;
			ШаблонЗначение = ШаблонОбъект[ЭлементДерева.ИмяРеквизитаОбъектаДО];
			
		ИначеЕсли ЭлементДерева.ИмяРеквизитаОбъектаДО = "Получатель" И ЭлементДерева.Таблица = "" Тогда
			Для Каждого СторонаСтрока Из ШаблонОбъект.Стороны Цикл
				Если СторонаСтрока.Наименование = Справочники.НаименованияСторон.Получатель
						И ЗначениеЗаполнено(СторонаСтрока.Сторона) Тогда
					ЗаполняетсяИзШаблона = Истина;
					ШаблонЗначение = СторонаСтрока.Сторона;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ЭлементДерева.ИмяРеквизитаОбъектаДО = "СтатьиДДС" И ЭлементДерева.Таблица = ""
				И ЗначениеЗаполнено(ШаблонОбъект.СтатьяДвиженияДенежныхСредств) Тогда
			ЗаполняетсяИзШаблона = Истина;
			
		КонецЕсли;
		
		Если ЗаполняетсяИзШаблона Тогда
			Если ПравилоЗаполнено(ЭлементДерева) Тогда
				ОчиститьПравилоЗаполненияНаСервере(ЭлементДерева);
			КонецЕсли;
			
			ЭлементДерева.Вариант = ВариантШаблон();
			ЭлементДерева.Картинка = 5;
			ЭлементДерева.ШаблонЗначение = ШаблонЗначение;
			Если ЗначениеЗаполнено(ЭлементДерева.ШаблонЗначение) Тогда
				ЭлементДерева.Пояснение = СтрШаблон(НСтр("ru = '%1 (Заполняется из шаблона)'"),
					ЭлементДерева.ШаблонЗначение);
			Иначе
				ЭлементДерева.Пояснение = НСтр("ru = 'Заполняется из шаблона'");
			КонецЕсли;
			
			Если ЭлементДерева.ЭтоТаблица Тогда
				ПодчиненныеПравила = ЭлементДерева.ПолучитьЭлементы();
				Для Каждого ПодчиненноеПравило Из ПодчиненныеПравила Цикл
					ПодчиненноеПравило.Вариант = ВариантШаблон();
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ШаблонСсылка(Знач ВидДокументаСсылка)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 2
		|	ШаблоныДокументов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ШаблоныДокументов КАК ШаблоныДокументов
		|ГДЕ
		|	ШаблоныДокументов.ВидДокумента = &ВидДокумента
		|	И НЕ ШаблоныДокументов.ПометкаУдаления");
	Запрос.УстановитьПараметр("ВидДокумента", ВидДокументаСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти