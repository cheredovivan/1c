#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
 
// Получает приложение по коду приложения и пользователю
//  Параметры:
//   КодПриложения - Строка - Код приложения
//   Пользователь - СправочникСсылка.Пользователи - Пользователь приложения
//	Возвращаемое значение:
//   СправочникСсылка.ПользователиМобильногоПриложения - Пользователь МП
//
Функция ПолучитьПриложение(КодПриложения, Пользователь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КодПриложения", КодПриложения);
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПользователиМобильногоПриложения.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПользователиМобильногоПриложения КАК ПользователиМобильногоПриложения
		|ГДЕ
		|	ПользователиМобильногоПриложения.Код = &КодПриложения";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		НовыйЭлемент = СоздатьЭлемент();
		НовыйЭлемент.Пользователь = Пользователь;
		НовыйЭлемент.Код = КодПриложения;
		НовыйЭлемент.ДатаСоздания = ТекущаяДатаСеанса();
		НовыйЭлемент.Записать();
		Ссылка = НовыйЭлемент.Ссылка;
		
		// Привязка новой очереди сообщений и нового мобильного клиента
		МенеджерЗаписиОчередей = РегистрыСведений.МП_ОчередиСообщенийОбмена.СоздатьМенеджерЗаписи();
		МенеджерЗаписиОчередей.ПользовательМобильногоПриложения = Ссылка;
		МенеджерЗаписиОчередей.Записать();
	Иначе
		Ссылка = РезультатЗапроса.Выгрузить()[0].Ссылка;
	КонецЕсли;
	
	Возврат Ссылка;

КонецФункции

// Позволяет узнать, использует ли пользователь мобильное приложение
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи, Неопределено - Пользователь (по умолчанию - текущий)
//
// Возвращаемое значение:
//   Булево - Истина, если пользователь найден, Ложь в противном случае
//
Функция ЭтоПользовательМП(Пользователь = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПользователиМобильногоПриложения.Пользователь КАК Пользователь
		|ИЗ
		|	Справочник.ПользователиМобильногоПриложения КАК ПользователиМобильногоПриложения
		|ГДЕ
		|	ПользователиМобильногоПриложения.Пользователь = &Пользователь";
	
	Возврат Не Запрос.Выполнить().Пустой();

КонецФункции

// Возвращает все активные приложения пользователя
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи, Неопределено - Целевой пользователь
//  	(если не задан - вычисляется текущий)
//
// Возвращаемое значение:
//  Массив - Массив всех активных приложений пользователя
//
Функция АктивныеПриложенияПользователя(Пользователь = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПользователиМобильногоПриложения.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПользователиМобильногоПриложения КАК ПользователиМобильногоПриложения
		|ГДЕ
		|	ПользователиМобильногоПриложения.Пользователь = &Пользователь
		|	И НЕ ПользователиМобильногоПриложения.ПометкаУдаления";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");

КонецФункции

// Получает всех пользователей мобильного приложения
// Параметры
//  ИсключитьПомеченныеНаУдаление - Булево - Включить запрос помеченных на удаление (по умолчанию - Ложь)
//
// Возвращаемое значение:
//  Массив - пользователи МП
//
Функция ВсеПользователиМП(ИсключитьПомеченныеНаУдаление = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПользователиМобильногоПриложения.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПользователиМобильногоПриложения КАК ПользователиМобильногоПриложения
		|ГДЕ
		|	&УсловиеПоИсключениюПомеченных";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&УсловиеПоИсключениюПомеченных" ,
		?(ИсключитьПомеченныеНаУдаление, "Не ПользователиМобильногоПриложения.ПометкаУдаления", "Истина"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Данные.Ссылка) Тогда

		Сведения = РегистрыСведений.МП_СведенияОПользователях.ПолучитьСведения(Данные.Ссылка);

		Если Сведения <> Неопределено Тогда
			
			СтандартнаяОбработка = Ложь;
			Представление = СтрШаблон(
				"%1, акт. %2", Сведения.Описание, Сведения.ДатаПоследнейАктивности);
			
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
