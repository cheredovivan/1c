#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Объект.Включен = Истина;
		
		Если Не ЗначениеЗаполнено(Объект.ВыполняемыйКод) Тогда
			Объект.ВыполняемыйКод = 
				"// Этот фрагмент кода на встроенном языке будет выполняться 
				|// автоматически регламентным заданием 
				|//  ""Обработка детекторов бизнес-событий"" раз в минуту.
				|// Если при выполнении этого кода возникнет ошибка, то информация об этом
				|// будет записана в журнал регистрации.
				|// Следите за тем, чтобы в этом коде не было бесконечных циклов,
				|// т.к. это может привести к замедлению работы сервера.
				|// Не выполняйте в этом коде длительные операции, т.к. это может
				|// привести к замедлению работы сервера.
				| 
				|//Пример обработки
				|//Если СобытиеПроизошло Тогда
				|//Результат = Истина;
				|//Иначе
				|//Результат = Ложь;
				|//КонецЕсли;";
		КонецЕсли;
		
	КонецЕсли;
	
	
	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		Элементы.УзелОбмена.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Контекст = Новый Соответствие;
	Контекст.Вставить("Результат");
	Контекст.Вставить("ПараметрыВозврата");
	РедакторВстроенногоЯзыка.ПриСозданииНаСервере(ЭтотОбъект, "Редактор", Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	РедакторВстроенногоЯзыкаКлиент.ПриОткрытии(
		Элементы.РедакторВстроенногоЯзыка,
		Элементы.ВыполняемыйКод,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Объект.ВыполняемыйКод = РедакторВстроенногоЯзыкаКлиент.ПолучитьТекст(
		Элементы.РедакторВстроенногоЯзыка, Объект.ВыполняемыйКод);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
КонецПроцедуры 

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы


&НаКлиенте
Процедура ОтветственныйОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	СотрудникиКлиент.СотрудникОбработкаВыбора(Объект, "Ответственный", ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РедакторВстроенногоЯзыкаДокументСформирован(Элемент)
	
	Если РедакторВстроенногоЯзыкаКлиент.ДокументСформирован(
			Элемент, ЭтотОбъект, Объект.ВыполняемыйКод, Объект.Предопределенный) Тогда
		ПодключитьОбработчикОжидания("ПослеЗагрузкиРедактора", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедакторВстроенногоЯзыкаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	НажатаКнопкаEsc = Ложь;
	РедакторВстроенногоЯзыкаКлиент.ПриНажатии(
		Элемент,
		ДанныеСобытия,
		СтандартнаяОбработка,
		ЭтотОбъект,
		НажатаКнопкаEsc);
	Если НажатаКнопкаEsc Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьКод(Команда)
	
	ОчиститьСообщения();
	
	ТекстУспешногоЗавершения = "";
	РезультатВыполнения = ПроверитьКодНаСервере(
		РедакторВстроенногоЯзыкаКлиент.ПолучитьТекст(Элементы.РедакторВстроенногоЯзыка, Объект.ВыполняемыйКод),
		ТекстУспешногоЗавершения);
	РедакторВстроенногоЯзыкаКлиент.ОбработатьРезультатВыполнения(Элементы.РедакторВстроенногоЯзыка, РезультатВыполнения);
	
	Если Не Элементы.РедакторВстроенногоЯзыка.Видимость И РезультатВыполнения.Успешно Тогда
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.УстановитьТекст(ТекстУспешногоЗавершения);
		ТекстовыйДокумент.Показать(НСтр("ru = 'Результат выполнения выражения на встроенном языке'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеЗагрузкиРедактора()
	
	РедакторВстроенногоЯзыкаКлиент.ПослеЗагрузкиРедактора(Элементы.РедакторВстроенногоЯзыка, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ПроверитьКодНаСервере(Знач Выражение, ТекстУспешногоЗавершения)
	
	РезультатВыполнения = РедакторВстроенногоЯзыка.ПодготовитьВыражениеКВыполнению(
		Выражение, ЭтотОбъект, "РезультатВыполнения");
	
	Результат = Ложь;
	ПараметрыВозврата = Новый Структура;
	
	УстановитьПривилегированныйРежим(Истина);
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Для Каждого ИмяРазделителя Из РаботаВМоделиСервиса.РазделителиКонфигурации() Цикл
			УстановитьБезопасныйРежимРазделенияДанных(ИмяРазделителя, Истина);
		КонецЦикла;
		УстановитьБезопасныйРежим(Истина);
	КонецЕсли;
	
	Попытка
		
		Выполнить(Выражение);
		
		МассивТекста = Новый Массив;
		МассивТекста.Добавить(СтрШаблон(НСтр("ru = 'Результат = %1'"), Строка(Результат)));
		МассивТекста.Добавить(НСтр("ru = 'ПараметрыВозврата:'"));
		Для Каждого Пара Из ПараметрыВозврата Цикл
			МассивТекста.Добавить(СтрШаблон("%1 = %2", Строка(Пара.Ключ), Строка(Пара.Значение)));
		КонецЦикла;
		ТекстУспешногоЗавершения = СтрСоединить(МассивТекста, Символы.ПС);
		
	Исключение
		РезультатВыполнения.ИнформацияОбОшибке = ИнформацияОбОшибке();
	КонецПопытки;
	
	РедакторВстроенногоЯзыка.ОбработатьРезультатВыполнения(РезультатВыполнения, ЭтотОбъект);
	
	Возврат РезультатВыполнения;
	
КонецФункции


#КонецОбласти