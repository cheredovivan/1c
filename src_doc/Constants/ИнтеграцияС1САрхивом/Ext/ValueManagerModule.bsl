#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Если обмен был выключен и его включили, выполняется:
	// - регистрация к обмену всех объектов НСИ
	// - отправка сообщения о поддерживаемой версии формата.
	
	Если Значение <> Истина Тогда
		Возврат;
	КонецЕсли;
	ПредыдущееЗначение = Константы.ИнтеграцияС1САрхивом.Получить();
	Если ПредыдущееЗначение = Истина Тогда
		Возврат
	КонецЕсли;
	
	УзелАрхив = ПланыОбмена.ОбменНСИСАрхивом.УзелАрхив();
	
	// Если включена миграция и блокировка обмена с внешними ресурсами,
	// регистрация данных к обмену не выполняется.
	Если МиграцияДанныхИзВнешнихСистемСервер.БлокироватьОбменСВнешнимиРесурсами() Тогда
		Возврат;
	КонецЕсли;
	
	ОбменСАрхивом.СформироватьСообщениеОВерсииФормата();
	
	Для Каждого ЭлементСостава Из Метаданные.ПланыОбмена.ОбменНСИСАрхивом.Состав Цикл
		ПланыОбмена.ЗарегистрироватьИзменения(УзелАрхив, ЭлементСостава.Метаданные);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли