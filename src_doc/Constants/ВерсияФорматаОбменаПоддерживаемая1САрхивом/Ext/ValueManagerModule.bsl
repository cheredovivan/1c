#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Информация для отправки данных, обмен которыми поддержан в новой версии.
	ПредыдущееЗначение = Константы.ВерсияФорматаОбменаПоддерживаемая1САрхивом.Получить();
	Если Не ЗначениеЗаполнено(ПредыдущееЗначение) Или Значение = ПредыдущееЗначение Тогда
		Возврат;
	КонецЕсли;
	ВерсияДляОтправкиПредыдущая = ОбменСАрхивом.ВерсияФорматаДляОтправкиСообщений();
	ДополнительныеСвойства.Вставить("ПредыдущееЗначение",  ПредыдущееЗначение);
	ДополнительныеСвойства.Вставить("ВерсияДляОтправкиПредыдущая",  ВерсияДляОтправкиПредыдущая);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("ВерсияДляОтправкиПредыдущая") Тогда
		Возврат;
	КонецЕсли;
	
	// Отправка данных, обмен которыми поддержан в новой версии.
	ВерсияДляОтправкиТекущая = ОбменСАрхивом.ВерсияФорматаДляОтправкиСообщений();
	ВерсияДляОтправкиПредыдущая = ДополнительныеСвойства.ВерсияДляОтправкиПредыдущая;
	ОбменСАрхивом.ЗарегистрироватьНовыеДанныеВОбмене(ВерсияДляОтправкиПредыдущая, ВерсияДляОтправкиТекущая);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли