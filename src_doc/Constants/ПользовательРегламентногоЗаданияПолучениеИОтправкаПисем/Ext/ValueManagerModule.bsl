#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПользователя = "";
	
	Если ЗначениеЗаполнено(Значение) Тогда
		
		ДанныеПользователя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Значение,
			"ПометкаУдаления, Недействителен, ИдентификаторПользователяИБ");
		
		ПроверкаНеНужна = Ложь;
		Если ДанныеПользователя.Недействителен = Неопределено И ПараметрыСеанса.ЗагрузкаОбработанныхДанныхИзДругойСистемы Тогда
			ПроверкаНеНужна = Истина;
		КонецЕсли;	

		Если Не ПроверкаНеНужна Тогда
		
			Если Не ДанныеПользователя.ПометкаУдаления
				И Не ДанныеПользователя.Недействителен
				И Не ПустаяСтрока(ДанныеПользователя.ИдентификаторПользователяИБ) Тогда
				
				ИдентификаторПользователяИБ = ДанныеПользователя.ИдентификаторПользователяИБ;
				
				ПользовательИБ =
					ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ИдентификаторПользователяИБ);
				
				Если ПользовательИБ <> Неопределено Тогда
					
					Если ПараметрыСеанса.ЗагрузкаОбработанныхДанныхИзДругойСистемы Тогда
						
						ИмяПользователя = ПользовательИБ.Имя;
						
					Иначе	
						
						// проверки тут - только если это не загрузка из 21
					
						Если ПользовательИБ.Роли.Содержит(Метаданные.Роли.ПолныеПрава) Тогда
							
							ИмяПользователя = ПользовательИБ.Имя;
							
						Иначе
							
							Если Не ПараметрыСеанса.ЗагрузкаОбработанныхДанныхИзДругойСистемы Тогда
								ВызватьИсключение НСтр("ru = '
									|Данный пользователь не может быть выбран для регламентного задания ""Получение и отправка писем"".'");
							КонецЕсли;	
							
						КонецЕсли;
						
					КонецЕсли;
					
				Иначе
					
					Если Не ПараметрыСеанса.ЗагрузкаОбработанныхДанныхИзДругойСистемы Тогда
						ВызватьИсключение НСтр("ru = '
							|Данный пользователь не может быть выбран для регламентного задания ""Получение и отправка писем"".'");
					КонецЕсли;	
					
				КонецЕсли;
				
			Иначе
				
				ВызватьИсключение НСтр("ru = '
					|Данный пользователь не может быть выбран для регламентного задания ""Получение и отправка писем"".'");
				
			КонецЕсли;
		
		КонецЕсли;	
		
	КонецЕсли;
	
	Если Не ОбщегоНазначения.РазделениеВключено() И ИмяПользователя <> "" Тогда
	
		ИзменитьПользователяРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОтправкаПисемПоВнутреннейМаршрутизации, ИмяПользователя);
		ИзменитьПользователяРегламентногоЗадания(Метаданные.РегламентныеЗадания.ДиспетчерПотоковДоставкиПочты, ИмяПользователя);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ИзменитьПользователяРегламентногоЗадания(Метаданные, ИмяПользователя)
	
	РегламентноеЗадание = РегламентныеЗадания.НайтиПредопределенное(Метаданные);
	РегламентноеЗадание.ИмяПользователя = ИмяПользователя;
	РегламентноеЗадание.Записать();
	
КонецПроцедуры

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли