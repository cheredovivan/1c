#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриИзмененииЗначения(НовоеЗначение) Экспорт
	
	Если Не РаботаВМоделиСервиса.РазделениеВключено() Тогда
		
		Если НовоеЗначение Тогда
			РегистрыСведений.ПлюсНастройки.ВключитьИспользование();
		Иначе
			РегистрыСведений.ПлюсНастройки.ВыключитьИспользование();
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОбластиДанных.ОбластьДанныхВспомогательныеДанные КАК ОбластьДанных
	|ИЗ
	|	РегистрСведений.ОбластиДанных КАК ОбластиДанных
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлюсНастройки КАК Настройка1СПлюс
	|		ПО ОбластиДанных.ОбластьДанныхВспомогательныеДанные = Настройка1СПлюс.ОбластьДанныхВспомогательныеДанные
	|ГДЕ
	|	ЕСТЬNULL(Настройка1СПлюс.СервисИспользуется, НЕОПРЕДЕЛЕНО) <> &НовоеЗначение
	|	И ОбластиДанных.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбластейДанных.Используется)");
	Запрос.УстановитьПараметр("НовоеЗначение", НовоеЗначение);
	
	РезультатЗапросаОбласти = Запрос.Выполнить();
	Если РезультатЗапросаОбласти.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоРазделенныйСеанс = РаботаВМоделиСервиса.ДоступноИспользованиеРазделенныхДанных();

	Выборка = РезультатЗапросаОбласти.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ТекущийКодОбластиДанных = Выборка.ОбластьДанных;
		
		Если Не ЭтоРазделенныйСеанс  Тогда
			РаботаВМоделиСервиса.ВойтиВОбластьДанных(ТекущийКодОбластиДанных);
		КонецЕсли;
		
		Блокировка = Новый БлокировкаДанных;
		
		Блокировка.Добавить("Константа.ПлюсСервисДоступенДляИспользования");
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОбластиДанных");
		ЭлементБлокировки.УстановитьЗначение("ОбластьДанныхВспомогательныеДанные", ТекущийКодОбластиДанных);
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПлюсНастройки");
		ЭлементБлокировки.УстановитьЗначение("ОбластьДанныхВспомогательныеДанные", ТекущийКодОбластиДанных);
		
		НачатьТранзакцию();
		Попытка

			Блокировка.Заблокировать();
		
			Если НовоеЗначение Тогда
				РегистрыСведений.ПлюсНастройки.ВключитьИспользование();
			Иначе
				РегистрыСведений.ПлюсНастройки.ВыключитьИспользование();
			КонецЕсли;
				
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			
			Если РаботаВМоделиСервиса.ДоступноИспользованиеРазделенныхДанных() Тогда
				РаботаВМоделиСервиса.ВыйтиИзОбластиДанных();
			КонецЕсли;
			
			ВызватьИсключение;
		КонецПопытки;
			
		Если Не ЭтоРазделенныйСеанс Тогда
			РаботаВМоделиСервиса.ВыйтиИзОбластиДанных();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли