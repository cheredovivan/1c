
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Фиксирует признак использования обновленной архитектуры адресной книги.
//
// Параметры:
//	Использовать - Булево - Признак использования.
//
Процедура УстановитьИспользованиеОбновленнойАрхитектурыАдреснойКниги(Использовать) Экспорт
	
	ПараметрыОбновления = ПолучитьПараметрыОбновленияАдреснойКниги();
	
	НовыеПараметры = НовыеПараметрыОбновленияАдреснойКниги(
		Использовать,
		ПараметрыОбновления.ОтложенноеОбновлениеАдреснойКниги,
		ПараметрыОбновления.ЧислоПотоковОбновленияАдреснойКниги);
	
	Константы.ПараметрыОбновленияАдреснойКниги.Установить(Новый ХранилищеЗначения(НовыеПараметры));
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

// Фиксирует признак использования отложенного обновления адресной книги.
//
// Параметры:
//	Использовать - Булево - Признак использования.
//
Процедура УстановитьИспользованиеОтложенногоОбновленияАдреснойКниги(Использовать) Экспорт
	
	ПараметрыОбновления = ПолучитьПараметрыОбновленияАдреснойКниги();
	
	НовыеПараметры = НовыеПараметрыОбновленияАдреснойКниги(
		ПараметрыОбновления.ИспользоватьОбновленнуюАрхитектуруАдреснойКниги,
		Использовать,
		ПараметрыОбновления.ЧислоПотоковОбновленияАдреснойКниги);
	
	Константы.ПараметрыОбновленияАдреснойКниги.Установить(Новый ХранилищеЗначения(НовыеПараметры));
	
	РегламентныеЗаданияСервер.УстановитьИспользованиеРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ОбновлениеАдреснойКниги, Использовать);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

// Фиксирует число потоков отложенного обновления адресной книги.
//
// Параметры:
//	ЧислоПотоков - Число - Число потоков отложенного обновления.
//
Процедура УстановитьЧислоПотоковОтложенногоОбновленияАдреснойКниги(ЧислоПотоков) Экспорт
	
	ПараметрыОбновления = ПолучитьПараметрыОбновленияАдреснойКниги();
	
	НовыеПараметры = НовыеПараметрыОбновленияАдреснойКниги(
		ПараметрыОбновления.ИспользоватьОбновленнуюАрхитектуруАдреснойКниги,
		ПараметрыОбновления.ОтложенноеОбновлениеАдреснойКниги,
		ЧислоПотоков);
	
	Константы.ПараметрыОбновленияАдреснойКниги.Установить(Новый ХранилищеЗначения(НовыеПараметры));
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

// Возвращает значение признака использования обновленной архитектуры адресной книги.
//
// Возвращаемое значение:
//	Булево.
//
Функция ИспользоватьОбновленнуюАрхитектуруАдреснойКниги() Экспорт
	
	ПараметрыОбновления = ПолучитьПараметрыОбновленияАдреснойКниги();
	Возврат ПараметрыОбновления.ИспользоватьОбновленнуюАрхитектуруАдреснойКниги;
	
КонецФункции

// Возвращает признак использования отложенного обновления адресной книги.
//
// Возвращаемое значение:
//	Булево.
//
Функция ОтложенноеОбновлениеАдреснойКниги() Экспорт
	
	ПараметрыОбновления = ПолучитьПараметрыОбновленияАдреснойКниги();
	Возврат ПараметрыОбновления.ОтложенноеОбновлениеАдреснойКниги;
	
КонецФункции

// Возвращает число потоков отложенного обновления адресной книги.
//
// Возвращаемое значение:
//	Дата - Начальная граница исторических данных.
//
Функция ЧислоПотоковОбновленияАдреснойКниги() Экспорт
	
	ПараметрыОбновления = ПолучитьПараметрыОбновленияАдреснойКниги();
	Возврат ПараметрыОбновления.ЧислоПотоковОбновленияАдреснойКниги;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает параметры обновления адресной книги.
//
// Возвращаемое значение:
//	Структура - см. НовыеПараметрыОбновленияАдреснойКниги.
//
Функция ПолучитьПараметрыОбновленияАдреснойКниги()
	
	ПараметрыОбновления = Константы.ПараметрыОбновленияАдреснойКниги.Получить().Получить();
	Если ПараметрыОбновления = Неопределено Тогда
		ПараметрыОбновления = НовыеПараметрыОбновленияАдреснойКниги();
	Иначе
		ИспользоватьОбновленнуюАрхитектуруАдреснойКниги = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ПараметрыОбновления, "ИспользоватьОбновленнуюАрхитектуруАдреснойКниги", Ложь);
		ОтложенноеОбновлениеАдреснойКниги = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ПараметрыОбновления, "ОтложенноеОбновлениеАдреснойКниги", Ложь);
		ЧислоПотоковОбновленияАдреснойКниги = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ПараметрыОбновления, "ЧислоПотоковОбновленияАдреснойКниги", 5);
			
		ПараметрыОбновления = НовыеПараметрыОбновленияАдреснойКниги(
			ИспользоватьОбновленнуюАрхитектуруАдреснойКниги,
			ОтложенноеОбновлениеАдреснойКниги,
			ЧислоПотоковОбновленияАдреснойКниги);
	КонецЕсли;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Констуктор параметров обновления адресной книги.
//
// Параметры:
//	ИспользоватьОбновленнуюАрхитектуруАдреснойКниги - Булево - Признак использования обновленной архитектуры.
//	ОтложенноеОбновлениеАдреснойКниги - Булево - Признак использования отложенного обновления адресной книги.
//	ЧислоПотоковОбновленияАдреснойКниги - Число - Число потоков отложенного обновления.
//	
// Возвращаемое значение:
//	ФиксированнаяСтруктура:
//		* ИспользоватьОбновленнуюАрхитектуруАдреснойКниги - Булево.
//		* ОтложенноеОбновлениеАдреснойКниги - Булево.
//		* ЧислоПотоковОбновленияАдреснойКниги - Число.
//
Функция НовыеПараметрыОбновленияАдреснойКниги(
		ИспользоватьОбновленнуюАрхитектуруАдреснойКниги = Ложь,
		ОтложенноеОбновлениеАдреснойКниги = Ложь,
		ЧислоПотоковОбновленияАдреснойКниги = 5)
	
	ПараметрыОбновления = Новый Структура;
	ПараметрыОбновления.Вставить(
		"ИспользоватьОбновленнуюАрхитектуруАдреснойКниги",
		ИспользоватьОбновленнуюАрхитектуруАдреснойКниги);
	ПараметрыОбновления.Вставить(
		"ОтложенноеОбновлениеАдреснойКниги",
		ОтложенноеОбновлениеАдреснойКниги);
	ПараметрыОбновления.Вставить(
		"ЧислоПотоковОбновленияАдреснойКниги",
		ЧислоПотоковОбновленияАдреснойКниги);
		
	ПараметрыОбновления = Новый ФиксированнаяСтруктура(ПараметрыОбновления);
	
	Возврат ПараметрыОбновления;
	
КонецФункции

#КонецОбласти

#КонецЕсли
