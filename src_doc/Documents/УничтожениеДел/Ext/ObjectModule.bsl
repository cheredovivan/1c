#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ВключенаИнтеграцияСАрхивом = ПолучитьФункциональнуюОпцию("ИнтеграцияС1САрхивом");
	
	Если ВключенаИнтеграцияСАрхивом = Истина
		И ЗагруженИз1САрхива
		И Не ДополнительныеСвойства.Свойство("ИнтеграцияДокументаСАрхивом") Тогда
		
		ТекстСообщения = НСтр(
			"ru = 'Документ загружен из 1С:Архива. Редактирование на стороне 1С:Документооборота недоступно.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СостоянияДелХраненияДокументов.ДелоХраненияДокументов КАК Дело,
		|	СостоянияДелХраненияДокументов.Регистратор
		|ИЗ
		|	РегистрСведений.СостоянияДелХраненияДокументов КАК СостоянияДелХраненияДокументов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПередачаДелВАрхив КАК ПередачаДелВАрхив
		|		ПО СостоянияДелХраненияДокументов.Регистратор = ПередачаДелВАрхив.Ссылка
		|		И ПередачаДелВАрхив.СтатусВыгрузкиВАрхив = ЗНАЧЕНИЕ(Перечисление.СтатусыОбменаСАрхивом.ПринятВАрхив)";
				
	ТаблицаДелВ1САрхиве = Запрос.Выполнить().Выгрузить();	
	
	ИспользоватьУчетПоОрганизациям = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям");
	
	Для Каждого Строка Из ДелаХраненияДокументов Цикл
		Если ЗначениеЗаполнено(Строка.ДелоХраненияДокументов) Тогда 
			РеквизитыДела = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Строка.ДелоХраненияДокументов,
				"Организация, Подразделение, ФормаДокументов, ДелоЗакрыто, ДатаОкончания, 
				|НоменклатураДел.ГрифДоступа, НоменклатураДел.СрокХранения, НоменклатураДел.КатегорияДела");
				
			Если ИспользоватьУчетПоОрганизациям 
				И РеквизитыДела.Организация <> Организация Тогда 
				ТекстСообщения = СтрШаблон(
					РедакцииКонфигурацииКлиентСервер.ОшибкаПередачаДелВАрхив(),
					Строка.ДелоХраненияДокументов,
					Организация);
					
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					"ДелаХраненияДокументов[" + Формат(Строка.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].ДелоХраненияДокументов",, 
					Отказ);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Подразделение) И РеквизитыДела.Подразделение <> Подразделение Тогда 
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Дело ""%1"" не относится к подразделению ""%2"".'"),
					Строка.ДелоХраненияДокументов, Подразделение);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,
					"ДелаХраненияДокументов[" + Формат(Строка.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].ДелоХраненияДокументов",, 
					Отказ);
			КонецЕсли;
		
			Если РеквизитыДела.ФормаДокументов <> ФормаДокументов Тогда 
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Дело ""%1"" не соответствует выбранной форме ""%2"".'"),
					Строка.ДелоХраненияДокументов, ФормаДокументов);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,
					"ДелаХраненияДокументов[" + Формат(Строка.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].ДелоХраненияДокументов",, 
					Отказ);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ГрифДоступа) И РеквизитыДела.НоменклатураДелГрифДоступа <> ГрифДоступа Тогда 
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Дело ""%1"" не соответствует грифу доступа ""%2"".'"),
					Строка.ДелоХраненияДокументов, ГрифДоступа);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,
					"ДелаХраненияДокументов[" + Формат(Строка.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].ДелоХраненияДокументов",, 
					Отказ);
			КонецЕсли;
		
			Если РеквизитыДела.ДелоЗакрыто = Ложь Тогда 
				ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Дело ""%1"" не закрыто.'"),
					Строка.ДелоХраненияДокументов);
				
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					"ДелаХраненияДокументов[" + Формат(Строка.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].ДелоХраненияДокументов",, 
					Отказ);
			КонецЕсли;
			
			СрокХранения = РеквизитыДела.НоменклатураДелСрокХранения;
			Если ТипЗнч(СрокХранения) = Тип("Число") Тогда 
				ХранитсяПолныхЛет = Год(Дата) - Год(РеквизитыДела.ДатаОкончания) - 1;
				Если ХранитсяПолныхЛет < СрокХранения Тогда 
					ТекстСообщения = СтрШаблон(
						НСтр("ru = 'Для дела ""%1"" не истек срок хранения.'"),
						Строка.ДелоХраненияДокументов);
					
					ОбщегоНазначения.СообщитьПользователю(
						ТекстСообщения,
						ЭтотОбъект,
						"ДелаХраненияДокументов[" + Формат(Строка.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].ДелоХраненияДокументов",, 
						Отказ);
				КонецЕсли;	
			КонецЕсли;	
			
			КатегорияДела = РеквизитыДела.НоменклатураДелКатегорияДела;
			Если КатегорияДела = Перечисления.КатегорииДел.Постоянное Тогда 
				ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Дело ""%1"" имеет постоянный срок хранения.'"),
					Строка.ДелоХраненияДокументов);
					
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					"ДелаХраненияДокументов[" + Формат(Строка.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].ДелоХраненияДокументов",, 
					Отказ);
			КонецЕсли;
			
			НайденнаяСтрока = ТаблицаДелВ1САрхиве.Найти(Строка.ДелоХраненияДокументов, "Дело");
			Если НайденнаяСтрока <> Неопределено Тогда
				ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Дело ""%1"" передано в 1С:Архив документом ""%2"". Удаление возможно только в 1С:Архиве.'"),
					Строка.ДелоХраненияДокументов, НайденнаяСтрока.Регистратор);
					
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					"ДелаХраненияДокументов[" + Формат(Строка.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].ДелоХраненияДокументов",, 
					Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда 
		Ответственный = Сотрудники.ОсновнойСотрудник();
		Если Не ЗначениеЗаполнено(Организация) Тогда 
			Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		КонецЕсли;
	КонецЕсли;	
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Массив") Тогда
		
		ЗначенияРеквизитовДел = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
			ДанныеЗаполнения, "ФормаДокументов, Организация, Подразделение");
		
		ФормаДокументовДел = Неопределено;
		ОрганизацияДел = Неопределено;
		ПодразделениеДел = Неопределено;
		НесколькоПодразделений = Ложь;
		
		Для Каждого ДелоТом Из ДанныеЗаполнения Цикл
			
			РеквизитыДела = ЗначенияРеквизитовДел[ДелоТом];
			
			Если ФормаДокументовДел = Неопределено Тогда
				ФормаДокументовДел = РеквизитыДела.ФормаДокументов;
			КонецЕсли;
			Если ОрганизацияДел = Неопределено Тогда
				ОрганизацияДел = РеквизитыДела.Организация;
			КонецЕсли;
			Если РеквизитыДела.ФормаДокументов <> ФормаДокументовДел
				Или РеквизитыДела.Организация <> ОрганизацияДел Тогда
				Продолжить;
			КонецЕсли;
			
			Если ПодразделениеДел = Неопределено Тогда
				ПодразделениеДел = РеквизитыДела.Подразделение;
			ИначеЕсли ПодразделениеДел <> РеквизитыДела.Подразделение Тогда
				НесколькоПодразделений = Истина;
			КонецЕсли;
			
			НоваяСтрока = ДелаХраненияДокументов.Добавить();
			НоваяСтрока.ДелоХраненияДокументов = ДелоТом;
			
		КонецЦикла;
		
		ФормаДокументов = ФормаДокументовДел;
		Организация = ОрганизацияДел;
		Если Не НесколькоПодразделений Тогда
			Подразделение = ПодразделениеДел;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.СостоянияДелХраненияДокументов.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДелаХраненияДокументов.НомерСтроки КАК НомерСтроки,
		|	ДелаХраненияДокументов.Ссылка.Дата КАК Период,
		|	ДелаХраненияДокументов.ДелоХраненияДокументов КАК ДелоХраненияДокументов,
		|	ЗНАЧЕНИЕ(Перечисление.СостоянияДелХраненияДокументов.Уничтожено) КАК Состояние,
		|	СостоянияСрезПоследних.Состояние КАК СостояниеРанее
		|ИЗ
		|	Документ.УничтожениеДел.ДелаХраненияДокументов КАК ДелаХраненияДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияДелХраненияДокументов.СрезПоследних(&Период, ДелоХраненияДокументов В (&ДелаХраненияДокументов)) КАК СостоянияСрезПоследних
		|		ПО ДелаХраненияДокументов.ДелоХраненияДокументов = СостоянияСрезПоследних.ДелоХраненияДокументов
		|ГДЕ
		|	ДелаХраненияДокументов.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Новый Граница(МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("ДелаХраненияДокументов", ДелаХраненияДокументов.ВыгрузитьКолонку("ДелоХраненияДокументов"));
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.СостояниеРанее = Перечисления.СостоянияДелХраненияДокументов.Уничтожено Тогда 
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Дело ""%1"" было уничтожено ранее.'"),
				Выборка.ДелоХраненияДокументов);
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				"ДелаХраненияДокументов[" + Формат(Выборка.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].ДелоХраненияДокументов",, 
				Отказ);
		КонецЕсли;
		
		Движение = Движения.СостоянияДелХраненияДокументов.Добавить();
		Движение.Период = Выборка.Период;
		Движение.ДелоХраненияДокументов = Выборка.ДелоХраненияДокументов;
		Движение.Состояние = Выборка.Состояние;
		Движение.Активность = Не Ссылка.ПометкаУдаления;
		
		// Зафиксируем состояние уничтожения для всех документов в делах.
		Делопроизводство.ЗаписатьСостояниеДелаВДокументы(Выборка.ДелоХраненияДокументов, Выборка.Состояние);
		АрхивныеОперацииСервер.ОбработатьУничтожениеДокументов(Выборка.ДелоХраненияДокументов, Дата);
		
	КонецЦикла;
	Делопроизводство.УстановитьИспользуетсяПередачаДелХранения();
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	АрхивныеОперацииСервер.ОтменитьУничтожениеДокументов(Ссылка);
	
	// Отменим состояние уничтожения для всех документов в делах.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДелаХраненияДокументов.ДелоХраненияДокументов КАК ДелоХраненияДокументов,
		|	СостоянияСрезПоследних.Состояние КАК СостояниеРанее
		|ИЗ
		|	Документ.УничтожениеДел.ДелаХраненияДокументов КАК ДелаХраненияДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияДелХраненияДокументов.СрезПоследних(&Период, ДелоХраненияДокументов В
		|			(&ДелаХраненияДокументов)) КАК СостоянияСрезПоследних
		|		ПО ДелаХраненияДокументов.ДелоХраненияДокументов = СостоянияСрезПоследних.ДелоХраненияДокументов
		|ГДЕ
		|	ДелаХраненияДокументов.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Новый Граница(МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("ДелаХраненияДокументов", ДелаХраненияДокументов.ВыгрузитьКолонку("ДелоХраненияДокументов"));
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Делопроизводство.ЗаписатьСостояниеДелаВДокументы(Выборка.ДелоХраненияДокументов, Выборка.СостояниеРанее);
	КонецЦикла;
	
	Делопроизводство.СнятьИспользуетсяПередачаДелХранения(Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли