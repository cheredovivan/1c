
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Если сотрудник переводился, то он должен видеть также и свои предыдущие отчеты, поэтому недействительных
	// тоже учитываем:
	Список.Параметры.УстановитьЗначениеПараметра("Сотрудники", Сотрудники.СотрудникиПользователя( , Ложь));
	
	Список.Параметры.УстановитьЗначениеПараметра("Понедельник", Перечисления.ДниНедели.Понедельник);
	Список.Параметры.УстановитьЗначениеПараметра("Вторник", 	Перечисления.ДниНедели.Вторник);
	Список.Параметры.УстановитьЗначениеПараметра("Среда", 		Перечисления.ДниНедели.Среда);
	Список.Параметры.УстановитьЗначениеПараметра("Четверг", 	Перечисления.ДниНедели.Четверг);
	Список.Параметры.УстановитьЗначениеПараметра("Пятница", 	Перечисления.ДниНедели.Пятница);
	Список.Параметры.УстановитьЗначениеПараметра("Суббота", 	Перечисления.ДниНедели.Суббота);
	Список.Параметры.УстановитьЗначениеПараметра("Воскресенье", Перечисления.ДниНедели.Воскресенье);
	
	ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
	Если ЭтоМобильныйКлиент Тогда
		МК_НастроитьЭлементыФормы();
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура МК_Создать(Команда)
	
	ОткрытьФорму("Документ.ЕжедневныйОтчет.ФормаОбъекта",, Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#Область СлужебныеПроцедурыИФункции_МобильныйКлиент

&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Нет;
	ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Половинный;
	Заголовок = НСтр("ru = 'Ежедневные отчеты'");
	
	Элементы.МК_Создать.Видимость = Истина;
	
	Элементы.Список.Шапка = Ложь;
	Элементы.МК_ГруппаКолонок.Видимость = Истина;
	
	Элементы.Автор.Видимость = Ложь;
	Элементы.НачалоДня.Видимость = Ложь;
	Элементы.ОкончаниеДня.Видимость = Ложь;
	Элементы.ДеньНедели.Видимость = Ложь;
	
	Элементы.Переместить(Элементы.Дата, Элементы.МК_ПерваяСтрока);
	
	Элементы.Переместить(Элементы.ДлительностьРабот, Элементы.МК_ДлительностьОбщая);
	Элементы.Переместить(Элементы.ДлительностьРабочая, Элементы.МК_ДлительностьРабочая);
	
	МК_ОформитьКолонкуДлительности(Элементы.ДлительностьРабот);
	МК_ОформитьКолонкуДлительности(Элементы.ДлительностьРабочая);
	
	Элементы.МК_ДлительностьОбщаяЗаголовок.ЦветТекста = ЦветаСтиля.МК_ЦветАвтораИсполнителя;
	Элементы.МК_ДлительностьОбщаяЗаголовок.Шрифт = ШрифтыСтиля.МелкийШрифтТекста;
	Элементы.МК_ДлительностьОбщаяЗаголовок.Ширина = 4;
	
	Элементы.МК_ДлительностьРабочаяЗаголовок.ЦветТекста = ЦветаСтиля.МК_ЦветАвтораИсполнителя;
	Элементы.МК_ДлительностьРабочаяЗаголовок.Шрифт = ШрифтыСтиля.МелкийШрифтТекста;
	Элементы.МК_ДлительностьРабочаяЗаголовок.Ширина = 6;
	
	Элементы.Дата.Формат = НСтр("ru='ДФ=''dd MMMM yyyy'''");
	
КонецПроцедуры

&НаСервере
Процедура МК_ОформитьКолонкуДлительности(Колонка)
	
	Колонка.ЦветТекста = ЦветаСтиля.МК_ЦветАвтораИсполнителя;
	Колонка.Шрифт = ШрифтыСтиля.МелкийШрифтТекста;
	Колонка.Ширина = 3;
	Колонка.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Лево;
	Колонка.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
	
КонецПроцедуры
	
#КонецОбласти

#КонецОбласти
