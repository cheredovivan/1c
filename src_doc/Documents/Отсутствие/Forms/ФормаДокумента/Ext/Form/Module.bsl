#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
	Если ЭтоМобильныйКлиент Тогда
		МК_ЭлементыСтиля = МК.ЭлементыСтиля();
	КонецЕсли;
	
	// Режим просмотра
	УстановитьТолькоПросмотр();
	
	// Виды отсутствия
	РежимВыбораВидаОтсутствия = Справочники.ВидыОтсутствий.ПолучитьРежимВыбораВидаОтсутствия();
	Элементы.ВидОтсутствия.БыстрыйВыбор = РежимВыбораВидаОтсутствия.БыстрыйВыбор;
	
	// Заголовок формы
	ОбновитьЗаголовокФормы();
	
	// Заместители
	ОбновитьЗамещает();
	
	ОбновитьЭтоУдаленнаяРабота();
	
	НачальноеЗначениеДатаНачала = Объект.ДатаНачала;
	НачальноеЗначениеДатаОкончания = Объект.ДатаОкончания;
	
	ПроверятьОтсутствие = Отсутствия.ПредупреждатьОбОтсутствии();
	ПроверитьОтсутствие = Не ЗначениеЗаполнено(Объект.Ссылка);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ВстроеннаяПочтаИспользуется = ВстроеннаяПочтаСервер.ВстроеннаяПочтаИспользуется();
		ЛегкаяПочтаИспользуется = ВстроеннаяПочтаСервер.ЛегкаяПочтаИспользуется();
		
		ЗаписьНового = Истина;
		
		ВопросСоздатьПисьмоБольшеНеСпрашивать =
			Отсутствия.ПолучитьПерсональнуюНастройку("ВопросСоздатьПисьмоБольшеНеСпрашивать");
		ВопросСоздатьПисьмоВариантОтвета =
			Отсутствия.ПолучитьПерсональнуюНастройку("ВопросСоздатьПисьмоВариантОтвета");
		ПредложитьОтправитьПисьмо =
			(Не ВопросСоздатьПисьмоБольшеНеСпрашивать Или ВопросСоздатьПисьмоВариантОтвета)
				И (ВстроеннаяПочтаИспользуется Или ЛегкаяПочтаИспользуется);
		
		ВопросСоздатьПравилоБольшеНеСпрашивать =
			Отсутствия.ПолучитьПерсональнуюНастройку("ВопросСоздатьПравилоБольшеНеСпрашивать");
		ВопросСоздатьПравилоВариантОтвета =
			Отсутствия.ПолучитьПерсональнуюНастройку("ВопросСоздатьПравилоВариантОтвета");
		ПредложитьСоздатьПравилоОбработкиПисем = 
			(Не ВопросСоздатьПравилоБольшеНеСпрашивать Или ВопросСоздатьПравилоВариантОтвета)
				И ВстроеннаяПочтаИспользуется;
		
	Иначе 	
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			СписокЗамещающих, "Отсутствие", Объект.Ссылка);
		
	КонецЕсли;
	
	ИспользоватьГрафикиРаботы = ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы");
	
	// Видимость гиперссылки кто ввел запись.
	Элементы.АвторГиперссылка.Видимость = Объект.Сотрудник <> Объект.Автор
		И Не Элементы.ПредставлениеЧасовогоПоясаСотрудника.Видимость;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаПечать;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Параметры.Свойство("СотрудникЗаписиКалендаря", СотрудникЗаписиКалендаря);
	ОбновитьДанныеСвязаннойЗаписиКалендаря();
	
	ОбновитьМестныеВременаНаСервере();
	ОбновитьВидимостьПредставленияМестногоЧасовогоПоясаНаСервере();
	ОбновитьПредставлениеЧасовогоПоясаСотрудника();
	
	ПротоколированиеРаботыСотрудников.ЗаписатьОткрытие(Объект.Ссылка);
	
	//МобильныйКлиент
	Если ЭтоМобильныйКлиент Тогда
		МК_НастроитьЭлементыФормы();
		ОбновитьЗаголовокЗаместителей(ЭтотОбъект);
	КонецЕсли;
	//Конец МобильныйКлиент
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбновитьЭтоУдаленнаяРабота();
	ОбновитьМестныеВременаНаСервере();
	ОбновитьПредставлениеЧасовогоПоясаСотрудника();
	ОбновитьВидимостьПредставленияМестногоЧасовогоПоясаНаСервере();
	
	ОбщегоНазначенияДокументооборот.ПоказатьНадписьПометкиУдаления(
		ЭтотОбъект, ТекущийОбъект.ПометкаУдаления);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВывестиДлительностьСобытия();
	ЗаполнитьСпискиВыбора();
	УстановитьДоступностьВремени();
	ЗаполнитьИнтервалыНапоминания();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПередЗакрытием(
		Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Модифицированность) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗаписьНового Тогда
		
		Если Объект.БудуРазбиратьЗадачи Тогда
			ПредложитьСоздатьПравилоОбработкиПисем = Ложь;
		КонецЕсли;
		
		Если ОтсутствияКлиент.ОбработкаЗаписиНового(
				ЭтотОбъект,
				Объект.Ссылка,
				ПредложитьОтправитьПисьмо,
				ВопросСоздатьПисьмоБольшеНеСпрашивать,
				ВопросСоздатьПисьмоВариантОтвета,
				ПредложитьСоздатьПравилоОбработкиПисем,
				ВопросСоздатьПравилоБольшеНеСпрашивать,
				ВопросСоздатьПравилоВариантОтвета) Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПриЗакрытии(ЗавершениеРаботы) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка)
		И ((УстановитьНапоминание И (ИзмененоНапоминание Или ИзмененоВремя Или Не УстановленоНапоминание))
			Или (Не УстановитьНапоминание И УстановленоНапоминание)) Тогда
		ОбработатьИзменениеНапоминанияНаСервере();
		ОбработатьИзменениеНапоминанияНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ЗамещающиеИПомощники" Тогда
		ОбновитьЗамещает();
	КонецЕсли;
	
	Если ИмяСобытия = "СозданиеНаОсновании_ИсходящееПисьмо" И Параметр = Объект.Ссылка Тогда
		ПредложитьОтправитьПисьмо = Ложь;
	КонецЕсли;
	
	Если ИмяСобытия = "СозданиеНаОсновании_ПравилоОбработкиПисем" И Параметр = Объект.Ссылка Тогда
		ПредложитьСоздатьПравилоОбработкиПисем = Ложь;
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_ВидОтсутствия" И Параметр = Объект.ВидОтсутствия Тогда
		ОбновитьЗаголовокФормы();
	КонецЕсли;
	
	Если ИмяСобытия = "ВыполненаОбработкаЗаписиНового" И Параметр = Объект.Ссылка Тогда
		ПредложитьОтправитьПисьмо = Ложь;
		ПредложитьСоздатьПравилоОбработкиПисем = Ложь;
		Если Открыта() Тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_НапоминанияПользователя_Документооборот"
		И Источник = СвязаннаяЗаписьКалендаря
		И ПараметрыНапоминанияДокументооборота(Параметр) 
		И ИспользоватьНапоминанияПользователя Тогда
		
		Напоминание = Параметр;
		УстановитьНадписьНапоминания(
			Параметр,
			УстановитьНапоминание,
			УстановленоНапоминание,
			СрокНапоминанияПоУмолчанию,
			ИнтервалВремениСтрокой,
			СпособУстановкиВремениНапоминания,
			ВремяНапоминания);
		УстановитьДоступностьЭлементовФормы();
		ЗаполнитьИнтервалыНапоминания();
		
	ИначеЕсли ИмяСобытия = "Удаление_НапоминанияПользователя_Документооборот"
		И Источник = СвязаннаяЗаписьКалендаря
		И ИспользоватьНапоминанияПользователя Тогда
		
		Напоминание = Неопределено;
		УстановитьНадписьНапоминания(
			Неопределено,
			УстановитьНапоминание,
			УстановленоНапоминание,
			СрокНапоминанияПоУмолчанию,
			ИнтервалВремениСтрокой,
			СпособУстановкиВремениНапоминания,
			ВремяНапоминания);
		УстановитьДоступностьЭлементовФормы();
		ЗаполнитьИнтервалыНапоминания();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	УИДЗамера = ОценкаПроизводительностиКлиент.ЗамерВремени();
	ПараметрыЗаписи.Вставить("УИДЗамера", УИДЗамера);
	
	Если Не ПараметрыЗаписи.Свойство("ЗакрытьПослеЗаписи") Тогда
		ПараметрыЗаписи.Вставить("ЗакрытьПослеЗаписи", Ложь);
	КонецЕсли;
	
	Если Не ПроверитьВозможностьОтсутствия(ПараметрыЗаписи) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не ПроверитьПересечениеОтсутствий(ПараметрыЗаписи) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбработатьИзменениеНапоминанияНаСервере();
	ОбновитьДанныеСвязаннойЗаписиКалендаря();
	
	Если ЗначениеЗаполнено(СвязаннаяЗаписьКалендаря) Тогда
		СинхронизацияКалендарей.ПослеЗаписиСобытияКалендаря(СвязаннаяЗаписьКалендаря);
	КонецЕсли;
	
	ОбновитьМестныеВременаНаСервере();
	
	ПротоколированиеРаботыСотрудников.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыСотрудников.ЗаписатьИзменение(Объект.Ссылка);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокЗамещающих, "Отсутствие", Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	КлючеваяОперация = ОтсутствияКлиентСервер.КлючеваяОперацияЗаписьОтсутствия();
	ОценкаПроизводительностиКлиент.УстановитьКлючевуюОперациюЗамера(ПараметрыЗаписи.УИДЗамера, КлючеваяОперация);
	
	ПараметрыОповестить = Новый Структура("Ссылка, Сотрудник", Объект.Ссылка, Объект.Сотрудник);
	Оповестить("Запись_Отсутствие", ПараметрыОповестить);
	
	ТекстОповещения = ?(ПараметрыЗаписи.ЭтоНовыйОбъект, НСтр("ru = 'Создание:'"), НСтр("ru = 'Изменение:'"));
	ПоказатьОповещениеПользователя(
		ТекстОповещения,
		ПолучитьНавигационнуюСсылку(Объект.Ссылка),
		Строка(Объект.Ссылка),
		БиблиотекаКартинок.Информация32);
	
	ОбработатьИзменениеНапоминанияНаКлиенте();
	
	Если ПараметрыЗаписи.Свойство("ЭтоЗаписьПередДобавлениемЗамещающего")
		И ПараметрыЗаписи.ЭтоЗаписьПередДобавлениемЗамещающего Тогда
		ОткрытьФормуДобавленияЗамещающего();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтработанаПриИзменении(Элемент)
	
	ПриИзмененииОтработана();
	УстановитьДоступностьЭлементовФормы();
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ИзмененныеЗаписиКалендаря = Новый Массив;
		ИзмененныеЗаписиКалендаря.Добавить(СвязаннаяЗаписьКалендаря);
		Оповестить("Запись_ЗаписьКалендаря", ИзмененныеЗаписиКалендаря, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОтсутствияПриИзменении(Элемент)
	
	ВидОтсутствияПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	
	ПроверитьОтсутствие = Истина;
	СотрудникПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СотрудникиКлиент.ВыбратьСотрудникаИзАдреснойКниги(Элемент, Объект.Сотрудник);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗамещаетНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Не ПроверитьЗаполнение() Тогда
			Возврат;
		КонецЕсли;
		Если Не Записать() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СотрудникиКлиент.ВыбратьСотрудникаИзАдреснойКниги(Элемент, Замещает);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗамещаетОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Не ПроверитьЗаполнение() Тогда
			Возврат;
		КонецЕсли;
		Записать(Новый Структура("ЭтоЗаписьПередДобавлениемЗамещающего", Истина));
	Иначе
		Если ЗначениеЗаполнено(Замещение) Тогда
			ИзменитьЗамещающегоНаСервере(ВыбранноеЗначение);
			Замещает = ВыбранноеЗначение;
		Иначе
			ОткрытьФормуДобавленияЗамещающего(ВыбранноеЗначение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗамещаетОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(, Замещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗамещаетПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Замещает) Тогда
		ОчисткаЗамещающего();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	
	ОбновитьВременаСеанса();
	
	ПроверитьОтсутствие = Истина;
	ИзмененоВремя = Истина;
	
	РаботаСРабочимКалендаремКлиентСервер.СкорректироватьДатуНачалаИОкончания(
		Объект.ДатаНачала, Объект.ДатаОкончания, Объект.ВесьДень,
		НачальноеЗначениеДатаНачала, НачальноеЗначениеДатаОкончания, , , Ложь);
	
	ОбновитьМестныеВременаНаКлиенте();
	ЗаполнитьСпискиВыбора();
	ВывестиДлительностьСобытия();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Объект.ДатаНачала = Дата(1,1,1);
	Объект.ДатаОкончания = Дата(1,1,1);
	
	РаботаСРабочимКалендаремКлиентСервер.СкорректироватьДатуНачалаИОкончания(
		Объект.ДатаНачала, Объект.ДатаОкончания, Объект.ВесьДень,
		НачальноеЗначениеДатаНачала, НачальноеЗначениеДатаОкончания, , ,Ложь);
	
	ОбновитьМестныеВременаНаКлиенте();
	ЗаполнитьСпискиВыбора();
	ВывестиДлительностьСобытия();
	
	ПроверитьОтсутствие = Истина;
	ИзмененоВремя = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаВремяПриИзменении(Элемент)
	
	ОбновитьВременаСеанса();
	
	ПроверитьОтсутствие = Истина;
	ИзмененоВремя = Истина;
	
	РаботаСРабочимКалендаремКлиентСервер.СкорректироватьДатуНачалаИОкончания(
		Объект.ДатаНачала, Объект.ДатаОкончания, Объект.ВесьДень,
		НачальноеЗначениеДатаНачала, НачальноеЗначениеДатаОкончания, , , Ложь);
	
	ОбновитьМестныеВременаНаКлиенте();
	ЗаполнитьСпискиВыбора();
	ВывестиДлительностьСобытия();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаВремяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Элемент.СписокВыбора;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаВремяОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Объект.ДатаНачала = Дата(1,1,1);
	Объект.ДатаОкончания = Дата(1,1,1);
	
	РаботаСРабочимКалендаремКлиентСервер.СкорректироватьДатуНачалаИОкончания(
		Объект.ДатаНачала, Объект.ДатаОкончания, Объект.ВесьДень,
		НачальноеЗначениеДатаНачала, НачальноеЗначениеДатаОкончания, , , Ложь);
	
	ОбновитьМестныеВременаНаКлиенте();
	ЗаполнитьСпискиВыбора();
	ВывестиДлительностьСобытия();
	
	ПроверитьОтсутствие = Истина;
	ИзмененоВремя = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаВремяАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		РаботаСРабочимКалендаремКлиент.СформироватьДанныеВыбораВремени(
			Текст, ДанныеВыбора, МестнаяДатаНачала);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаВремяОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.СформироватьДанныеВыбораВремени(
		Текст, ДанныеВыбора, МестнаяДатаНачала);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	
	ОбновитьВременаСеанса();
	
	ПроверитьОтсутствие = Истина;
	ИзмененоВремя = Истина;
	
	РаботаСРабочимКалендаремКлиентСервер.СкорректироватьДатуНачалаИОкончания(
		Объект.ДатаНачала, Объект.ДатаОкончания, Объект.ВесьДень,
		НачальноеЗначениеДатаНачала, НачальноеЗначениеДатаОкончания, , , Ложь);
	
	ОбновитьМестныеВременаНаКлиенте();
	ЗаполнитьСпискиВыбора();
	ВывестиДлительностьСобытия();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Объект.ДатаНачала = Дата(1,1,1);
	Объект.ДатаОкончания = Дата(1,1,1);
	
	РаботаСРабочимКалендаремКлиентСервер.СкорректироватьДатуНачалаИОкончания(
		Объект.ДатаНачала, Объект.ДатаОкончания, Объект.ВесьДень,
		НачальноеЗначениеДатаНачала, НачальноеЗначениеДатаОкончания, , , Ложь);
	
	ОбновитьМестныеВременаНаКлиенте();
	ЗаполнитьСпискиВыбора();
	ВывестиДлительностьСобытия();
	
	ПроверитьОтсутствие = Истина;
	ИзмененоВремя = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияВремяПриИзменении(Элемент)
	
	ОбновитьВременаСеанса();
	
	ПроверитьОтсутствие = Истина;
	ИзмененоВремя = Истина;
	
	РаботаСРабочимКалендаремКлиентСервер.СкорректироватьДатуНачалаИОкончания(
		Объект.ДатаНачала, Объект.ДатаОкончания, Объект.ВесьДень,
		НачальноеЗначениеДатаНачала, НачальноеЗначениеДатаОкончания, , , Ложь);
	
	ОбновитьМестныеВременаНаКлиенте();
	ЗаполнитьСпискиВыбора();
	ВывестиДлительностьСобытия();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияВремяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Элемент.СписокВыбора;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияВремяОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Объект.ДатаНачала = Дата(1,1,1);
	Объект.ДатаОкончания = Дата(1,1,1);
	
	РаботаСРабочимКалендаремКлиентСервер.СкорректироватьДатуНачалаИОкончания(
		Объект.ДатаНачала, Объект.ДатаОкончания, Объект.ВесьДень,
		НачальноеЗначениеДатаНачала, НачальноеЗначениеДатаОкончания, , , Ложь);
	
	ОбновитьМестныеВременаНаКлиенте();
	ЗаполнитьСпискиВыбора();
	ВывестиДлительностьСобытия();
	
	ПроверитьОтсутствие = Истина;
	ИзмененоВремя = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияВремяАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		РаботаСРабочимКалендаремКлиент.СформироватьДанныеВыбораВремени(
			Текст, ДанныеВыбора, МестнаяДатаОкончания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияВремяОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСРабочимКалендаремКлиент.СформироватьДанныеВыбораВремени(
		Текст, ДанныеВыбора, МестнаяДатаОкончания);
	
КонецПроцедуры

&НаКлиенте
Процедура ВесьДеньПриИзменении(Элемент)
	
	Если Объект.ВесьДень Тогда
		
		РаботаСРабочимКалендаремКлиентСервер.СкорректироватьДатуНачалаИОкончания(
			Объект.ДатаНачала,
			Объект.ДатаОкончания,
			Объект.ВесьДень,
			НачальноеЗначениеДатаНачала,
			НачальноеЗначениеДатаОкончания,
			Ложь,
			Истина);
		ОбновитьМестныеВременаНаКлиенте();
		
	Иначе
		
		Если ИспользоватьГрафикиРаботы Тогда
			СтруктураВозврата = ОтсутствияВызовСервера.ПолучитьНачалоИОкончаниеРабочегоДня(
				СотрудникиВызовСервера.ЛюбойПользовательСотрудника(Объект.Сотрудник),
				МестнаяДатаНачала,
				МестнаяДатаОкончания);
			МестнаяДатаНачала = НачалоДня(МестнаяДатаНачала) + СтруктураВозврата.НачалоДня;
			МестнаяДатаОкончания = НачалоДня(МестнаяДатаОкончания) + СтруктураВозврата.ОкончаниеДня;
		Иначе
			МестнаяДатаНачала = НачалоДня(МестнаяДатаНачала) + 32400;
			МестнаяДатаОкончания = НачалоДня(МестнаяДатаОкончания) + 64800;
		КонецЕсли;
		
		ОбновитьВременаСеанса();
		
		НачальноеЗначениеДатаНачала = Объект.ДатаНачала;
		НачальноеЗначениеДатаОкончания = Объект.ДатаОкончания;
		
	КонецЕсли;
	
	ЗаполнитьСпискиВыбора();
	ВывестиДлительностьСобытия();
	УстановитьДоступностьВремени();
	ОбновитьВидимостьПредставленияМестногоЧасовогоПоясаНаКлиенте();
	
	ПроверитьОтсутствие = Истина;
	ИзмененоВремя = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНапоминаниеПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалВремениСтрокойПриИзменении(Элемент)
	
	ЗаполнитьИнтервалыНапоминания();
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалВремениСтрокойОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = НСтр("ru = 'Другое...'") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура();
		
		ПараметрыФормы.Вставить("СпособУстановкиВремениНапоминания", СпособУстановкиВремениНапоминания);
		ПараметрыФормы.Вставить("ИнтервалВремениСтрокой", ИнтервалВремениСтрокой);
		ПараметрыФормы.Вставить("ВремяНапоминания",
			?(СпособУстановкиВремениНапоминания 
				= ПредопределенноеЗначение("Перечисление.СпособыУстановкиВремениНапоминания.ВУказанноеВремя"),
				ВремяНапоминания,
				Объект.ДатаНачала));
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ИнтервалВремениСтрокойОбработкаВыбораЗавершение", ЭтотОбъект);
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		Результат = ОткрытьФорму("РегистрСведений.НапоминанияПользователя.Форма.НапоминаниеДокументооборот", ПараметрыФормы, , , , , ОписаниеОповещения, РежимОткрытияОкна);
		
	ИначеЕсли ИнтервалВремениСтрокой <> ВыбранноеЗначение Тогда
		
		ИзмененоНапоминание = Истина;
		СпособУстановкиВремениНапоминания = ПредопределенноеЗначение("Перечисление.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета");
		
	Иначе
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалВремениСтрокойОбработкаВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.СпособУстановкиВремениНапоминания
		= ПредопределенноеЗначение("Перечисление.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета") Тогда
		
		Если УстановитьНапоминание = Ложь ИЛИ ИнтервалВремениСтрокой <> Результат.ИнтервалВремениСтрокой Тогда
			УстановитьНапоминание = Истина;
			ИзмененоНапоминание = Истина;
		КонецЕсли;
		СпособУстановкиВремениНапоминания = Результат.СпособУстановкиВремениНапоминания;
		ИнтервалВремениСтрокой = Результат.ИнтервалВремениСтрокой;
		ВремяНапоминания = Дата(1,1,1);
		
	ИначеЕсли Результат.СпособУстановкиВремениНапоминания =
		ПредопределенноеЗначение("Перечисление.СпособыУстановкиВремениНапоминания.ВУказанноеВремя") Тогда
		
		Если УстановитьНапоминание = Ложь ИЛИ ВремяНапоминания <> Результат.ВремяНапоминания Тогда
			УстановитьНапоминание = Истина;
			ИзмененоНапоминание = Истина;
		КонецЕсли;
		СпособУстановкиВремениНапоминания = Результат.СпособУстановкиВремениНапоминания;
		ВремяНапоминания = Результат.ВремяНапоминания;
		ИнтервалВремениСтрокой =
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'в %2 %1'"),
				Формат(ВремяНапоминания, "ДЛФ=D"),
				Формат(ВремяНапоминания, "ДФ=ЧЧ:мм"));
		
	КонецЕсли;
	
	УстановитьДоступностьЭлементовФормы();
	ЗаполнитьИнтервалыНапоминания();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	СотрудникиКлиент.СотрудникОбработкаВыбора(Объект, "Сотрудник", ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗаместители

&НаКлиенте
Процедура ЗаместителиПриИзменении(Элемент)
	
	ОбновитьЗамещает();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаместителиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Отказ = Истина;
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Не ПроверитьЗаполнение() Тогда
			Возврат;
		КонецЕсли;
		Записать(Новый Структура("ЭтоЗаписьПередДобавлениемЗамещающего", Истина));
	Иначе
		ОткрытьФормуДобавленияЗамещающего();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаместителиПослеУдаления(Элемент)
	
	Если ЭтоМобильныйКлиент Тогда
		ОбновитьЗаголовокЗаместителей(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ДобавитьЗаместителя(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Не ПроверитьЗаполнение() Тогда
			Возврат;
		КонецЕсли;
		Записать(Новый Структура("ЭтоЗаписьПередДобавлениемЗамещающего", Истина));
	Иначе
		ОткрытьФормуДобавленияЗамещающего();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписать(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("ЗакрытьПослеЗаписи", Ложь);
	ЗаписатьКлиент(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("ЗакрытьПослеЗаписи", Истина);
	ЗаписатьКлиент(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура Подписаться(Команда)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("ОбъектПодписки", Объект.Ссылка);
		ОткрытьФорму("ОбщаяФорма.ПодпискаНаУведомленияПоОбъекту", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветЖелтый(Команда)
	
	УстановитьЦвет(ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Желтый"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветЗеленый(Команда)
	
	УстановитьЦвет(ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Зеленый"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветКрасный(Команда)
	
	УстановитьЦвет(ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Красный"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветНет(Команда)
	
	УстановитьЦвет(ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Нет"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветОранжевый(Команда)
	
	УстановитьЦвет(ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Оранжевый"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветСиний(Команда)
	
	УстановитьЦвет(ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Синий"));
	
КонецПроцедуры

&НаКлиенте
Процедура МК_ПоказатьСкрытьЗаместителей(Команда)
	
	Элементы.Заместители.Видимость = Не Элементы.Заместители.Видимость;
	МК_НастроитьОтображениеЗаместителей(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура МК_ДобавитьЗаместителя(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Не ПроверитьЗаполнение() Тогда
			Возврат;
		КонецЕсли;
		Записать(Новый Структура("ЭтоЗаписьПередДобавлениемЗамещающего", Истина));
	Иначе
		ОткрытьФормуДобавленияЗамещающего();
	КонецЕсли;
	
	Элементы.Заместители.Видимость = Истина;
	ОбновитьЗаголовокЗаместителей(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СотрудникПриИзмененииСервер()
	
	ПредложитьСоздатьПравилоОбработкиПисем = Не ЗначениеЗаполнено(Объект.Ссылка)
		И Сотрудники.ЭтоСотрудникПользователя(Объект.Сотрудник);
	
	ОбновитьЗаголовокФормы();
	ОбновитьПредставлениеЧасовогоПоясаСотрудника();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаголовокФормы()
	
	Заголовок = "";
	
	Если ЗначениеЗаполнено(Объект.ВидОтсутствия) Тогда
		Заголовок = Объект.ВидОтсутствия;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Сотрудник) Тогда
		ДобавитьЗначениеКСтрокеЧерезРазделитель(Заголовок, " – ", Объект.Сотрудник);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Заголовок) Тогда
		Заголовок = НСтр("ru = 'Отсутствие'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВидОтсутствияПриИзмененииСервер()
	
	ОбновитьЗаголовокФормы();
	
	ЭтоБылаУдаленнаяРабота = ЭтоУдаленнаяРабота;
	ОбновитьЭтоУдаленнаяРабота();
	Если ЭтоБылаУдаленнаяРабота И Не ЭтоУдаленнаяРабота Тогда
		Объект.БудуРазбиратьЗадачи = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭтоУдаленнаяРабота()
	
	Если ЗначениеЗаполнено(Объект.ВидОтсутствия) Тогда
		ЭтоУдаленнаяРабота = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Объект.ВидОтсутствия,
			"ЭтоУдаленнаяРабота");
	Иначе
		ЭтоУдаленнаяРабота = Ложь;
	КонецЕсли;
	
	Если ЭтоУдаленнаяРабота Тогда
		Объект.БудуРазбиратьЗадачи = Истина;
	КонецЕсли;
	
	ТекстПодсказкиНаСвязи = 
		НСтр("ru = 'При удаленной работе необходимо оставаться на связи.
			|Если вы будете недоступны – выберите другую причину отсутствия.'");
	
	Элементы.Замещает.Подсказка = ?(ЭтоУдаленнаяРабота, ТекстПодсказкиНаСвязи, "");
	Элементы.Замещает.ОтображениеПодсказки =
		?(ЭтоУдаленнаяРабота, ОтображениеПодсказки.Кнопка, ОтображениеПодсказки.Нет);
	Элементы.Замещает.Доступность = Не ЭтоУдаленнаяРабота;
	Элементы.ДобавитьЗаместителя.Доступность = Не ЭтоУдаленнаяРабота;
	
	Если ЭтоМобильныйКлиент Тогда
		Элементы.МК_ДобавитьЗаместителя.Доступность = Не ЭтоУдаленнаяРабота;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗамещает(НовыйОдинЗаместитель = Неопределено)
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокЗамещающих, "Отсутствие",
		?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Ссылка, Неопределено));
	
	КоличествоЗамещающих = 0;
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ВыборкаПоЗамещающим = Отсутствия.ВыборкаЗамещающихПоОтсутствию(Объект.Ссылка);
		КоличествоЗамещающих = ВыборкаПоЗамещающим.Количество();
		Если КоличествоЗамещающих = 1 Тогда
			ВыборкаПоЗамещающим.Следующий();
			Замещает = ВыборкаПоЗамещающим.Замещающий;
			Замещение = ВыборкаПоЗамещающим.Ссылка;
			Элементы.Замещает.Подсказка = ВыборкаПоЗамещающим.Комментарий;
		КонецЕсли;
	КонецЕсли;
	
	Если НовыйОдинЗаместитель = Неопределено Тогда
		ОдинЗаместитель = (КоличествоЗамещающих <= 1);
	Иначе
		ОдинЗаместитель = НовыйОдинЗаместитель;
	КонецЕсли;
	
	Элементы.ГруппаОдинЗаместитель.Видимость = ОдинЗаместитель;
	Элементы.Заместители.Видимость = Не ОдинЗаместитель;

	Если Замещает = Неопределено Тогда 
		Замещает = Справочники.Сотрудники.ПустаяСсылка();
	КонецЕсли;
	
	ПравоДобавленияЗамещающиеИПомощники =
		ПравоДоступа("Добавление", Метаданные.Справочники.ЗамещающиеИПомощники);
	Если Не ПравоДобавленияЗамещающиеИПомощники Тогда
		Если КоличествоЗамещающих = 0 Тогда
			Элементы.ГруппаОдинЗаместитель.Видимость = Ложь;
			Элементы.Заместители.Видимость = Ложь;
		Иначе
			Элементы.ГруппаОдинЗаместитель.ТолькоПросмотр = Истина;
			Элементы.ДобавитьЗаместителя.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.Заместители.ТолькоПросмотр = ТолькоПросмотр
		Или Не ПравоДобавленияЗамещающиеИПомощники;
	
	//МобильныйКлиент
	Если ЭтоМобильныйКлиент Тогда
		Элементы.ГруппаОдинЗаместитель.Видимость = Ложь;
		ЕстьЗаместители = КоличествоЗамещающих > 0;
		Если ТолькоПросмотр Тогда
			Элементы.МК_ГруппаЗаместители.Видимость = ЕстьЗаместители;
		Иначе
			Элементы.МК_ГруппаЗаместители.Видимость = ПравоДобавленияЗамещающиеИПомощники;
		КонецЕсли;
		Элементы.МК_ДобавитьЗаместителя.Видимость = ПравоДобавленияЗамещающиеИПомощники;
		ОбновитьЗаголовокЗаместителей(ЭтотОбъект);
	КонецЕсли;
	//Конец МобильныйКлиент
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСпискиВыбора()
	
	// Заполнение списка выбора даты начала
	Элементы.ДатаНачалаВремя.СписокВыбора.Очистить();
	
	Если ЗначениеЗаполнено(МестнаяДатаНачала) Тогда 
		ТекДата = НачалоДня(МестнаяДатаНачала);
	Иначе
		ТекДата = НачалоДня(ТекущаяДата());
	КонецЕсли;
	
	Для Инд = 1 По 48 Цикл
		
		Элементы.ДатаНачалаВремя.СписокВыбора.Добавить(ТекДата, Формат(ТекДата, "ДФ=ЧЧ:мм"));
		Если МестнаяДатаНачала > ТекДата И МестнаяДатаНачала < ТекДата + 1800 Тогда
			Элементы.ДатаНачалаВремя.СписокВыбора.Добавить(
				МестнаяДатаНачала, Формат(МестнаяДатаНачала, "ДФ=ЧЧ:мм"));
		КонецЕсли;
		
		ТекДата = ТекДата + 1800;
		
	КонецЦикла;
	
	// Заполнение списка выбора даты окончания
	Элементы.ДатаОкончанияВремя.СписокВыбора.Очистить();
	
	СобытиеВПределахОдногоДня = ЗначениеЗаполнено(МестнаяДатаНачала)
		И (НачалоДня(МестнаяДатаНачала) = НачалоДня(МестнаяДатаОкончания)
			ИЛИ НЕ ЗначениеЗаполнено(МестнаяДатаОкончания))
		И МестнаяДатаНачала < МестнаяДатаОкончания;
	
	Если СобытиеВПределахОдногоДня Тогда
		
		ТекДата = РаботаСРабочимКалендаремКлиентСервер.КонецПолучаса(МестнаяДатаНачала);
		Если МестнаяДатаОкончания > ТекДата - 1800 И МестнаяДатаОкончания < ТекДата Тогда
			Элементы.ДатаОкончанияВремя.СписокВыбора.Добавить(
				МестнаяДатаОкончания, Формат(МестнаяДатаОкончания, "ДФ=ЧЧ:мм"));
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(МестнаяДатаОкончания) Тогда
		ТекДата = НачалоДня(МестнаяДатаОкончания);
	Иначе
		ТекДата = НачалоДня(ТекущаяДата());
	КонецЕсли;
	
	Для Инд = 1 По 48 Цикл
		
		Если СобытиеВПределахОдногоДня И ТекДата > КонецДня(МестнаяДатаНачала) Тогда
			Прервать;
		КонецЕсли;
		
		Элементы.ДатаОкончанияВремя.СписокВыбора.Добавить(ТекДата, Формат(ТекДата, "ДФ=ЧЧ:мм"));
		Если МестнаяДатаОкончания > ТекДата И МестнаяДатаОкончания < ТекДата + 1800 Тогда
			Элементы.ДатаОкончанияВремя.СписокВыбора.Добавить(
				МестнаяДатаОкончания, Формат(МестнаяДатаОкончания, "ДФ=ЧЧ:мм"));
		КонецЕсли;
		ТекДата = ТекДата + 1800;
		
	КонецЦикла;
	
	Элементы.ДатаОкончанияВремя.СписокВыбора.Добавить(ТекДата, "00:00");
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиДлительностьСобытия()
	
	ДлительностьСтр = "";
	
	Если Не ЗначениеЗаполнено(Объект.ДатаНачала) Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда 
		Возврат;
	КонецЕсли;
	
	ДлительностьСек = Объект.ДатаОкончания - Объект.ДатаНачала;
	
	Если Объект.ВесьДень Тогда
		ДлительностьСек = ДлительностьСек + 1;
	КонецЕсли;
	
	Дней = Цел(ДлительностьСек / 86400); // 86400 - число секунд в сутках
	ПодписьДней = ДелопроизводствоКлиентСервер.ПолучитьПодписьДней(Дней);
	
	Часов = Цел((ДлительностьСек - Дней * 86400) / 3600); // 86400 - число секунд в сутках
	ПодписьЧасов = ДелопроизводствоКлиентСервер.ПолучитьПодписьЧасов(Часов);
	
	Минут = Цел((ДлительностьСек - Дней * 86400 - Часов * 3600) / 60); // 86400 - число секунд в сутках
	ПодписьМинут = ДелопроизводствоКлиентСервер.ПолучитьПодписьМинут(Минут);
	
	Если Дней > 0 Тогда 
		ДлительностьСтр = ДлительностьСтр + Строка(Дней) + " " + ПодписьДней;
	КонецЕсли;
	
	Если Часов > 0 Тогда 
		
		Если Дней > 0 Тогда
			ДлительностьСтр = ДлительностьСтр + " ";
		КонецЕсли;
		
		ДлительностьСтр = ДлительностьСтр + Строка(Часов) + " " + ПодписьЧасов;
	КонецЕсли;
	
	Если Минут > 0 Тогда 
		
		Если Дней > 0 Или Часов > 0 Тогда
			ДлительностьСтр = ДлительностьСтр + " ";
		КонецЕсли;
		
		ДлительностьСтр = ДлительностьСтр + Строка(Минут) + " " + ПодписьМинут;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьВремени()
	
	Элементы.ДатаНачалаВремя.Доступность = Не Объект.ВесьДень;
	Элементы.ДатаОкончанияВремя.Доступность = Не Объект.ВесьДень;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюПослеВопросаОВозможностиОтсутствия(Результат, ПараметрыЗаписи) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьКлиент(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюПослеПредупрежденияОПересечениеОтсутствий(Результат, ПараметрыЗаписи) Экспорт
	
	Если Результат <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьКлиент(ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТолькоПросмотр()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоОбъектИзДругойСистемы =
		МиграцияДанныхИзВнешнихСистемСервер.ЭтоОбъектИзДругойСистемы(Объект.ИсточникДанных);
	Элементы.ГруппаОбъектИзДругойСистемы.Видимость = ЭтоОбъектИзДругойСистемы;
	
	ТолькоПросмотр = Не ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(Объект.Ссылка).Изменение
		Или ЭтоОбъектИзДругойСистемы;
	
	Элементы.ФормаЗаписатьИЗакрыть.Видимость = Не ТолькоПросмотр;
	Элементы.ФормаЗаписатьИЗакрыть.КнопкаПоУмолчанию = Не ТолькоПросмотр;
	Элементы.ФормаЗакрыть.Видимость = ТолькоПросмотр;
	Элементы.ФормаЗакрыть.КнопкаПоУмолчанию = ТолькоПросмотр;
	
	Элементы.ФормаЗаписать.Видимость = Не ТолькоПросмотр;
	Элементы.ГруппаСоздатьНаОсновании.Видимость = Не ТолькоПросмотр;
	Элементы.ДобавитьЗаместителя.Видимость = Не ТолькоПросмотр;
	
	Элементы.Заместители.ТолькоПросмотр = ТолькоПросмотр
		Или Не ПравоДоступа("Добавление", Метаданные.Справочники.ЗамещающиеИПомощники);
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьПересечениеОтсутствий(ПараметрыЗаписи)
	
	ОписаниеОповещения =
		Новый ОписаниеОповещения("ПередЗаписьюПослеПредупрежденияОПересечениеОтсутствий",
			ЭтотОбъект, ПараметрыЗаписи);
	
	Возврат ОтсутствияКлиент.ПроверитьПересечениеОтсутствий(
		Объект.Ссылка,
		Объект.ДатаНачала,
		Объект.ДатаОкончания,
		Объект.Сотрудник,
		Объект.БудуРазбиратьЗадачи,
		ОписаниеОповещения);
	
КонецФункции

&НаКлиенте
Функция ПроверитьВозможностьОтсутствия(ПараметрыЗаписи)
	
	Если Объект.БудуРазбиратьЗадачи
		Или Не ПроверитьОтсутствие Тогда
		Возврат Истина;
	КонецЕсли;
	
	ПроверитьОтсутствие = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗаписьюПослеВопросаОВозможностиОтсутствия",
		ЭтотОбъект, ПараметрыЗаписи);
	
	Возврат ОтсутствияКлиент.ПроверитьВозможностьОтсутствия(
		Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Сотрудник,
		ОписаниеОповещения, ПроверятьОтсутствие);
	
КонецФункции

&НаКлиенте
Процедура ЗаписатьКлиент(ПараметрыЗаписи)
	
	Если Записать(ПараметрыЗаписи) Тогда
		
		Если ПараметрыЗаписи.Свойство("ЗакрытьПослеЗаписи") И Не ПараметрыЗаписи.ЗакрытьПослеЗаписи Тогда
			Возврат;
		КонецЕсли;
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦвет(НовыйЦвет)
	
	Если Цвет = НовыйЦвет Тогда
		Возврат;
	КонецЕсли;
	
	Цвет = НовыйЦвет;
	ПриИзмененииЦвета();
	
	Если ЭтоМобильныйКлиент Тогда
		МК_ОформитьКомандыУстановкиЦвета(ЭтотОбъект);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ИзмененныеЗаписиКалендаря = Новый Массив;
		ИзмененныеЗаписиКалендаря.Добавить(СвязаннаяЗаписьКалендаря);
		Оповестить("Запись_ЗаписьКалендаря", ИзмененныеЗаписиКалендаря, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииЦвета()
	
	Если Не ЭтоМобильныйКлиент Тогда
		УстановитьПометкуКомандЦвета();
	КонецЕсли;
	
	РаботаСРабочимКалендаремСервер.УстановитьЦветЗаписиКалендаря(СвязаннаяЗаписьКалендаря, Цвет);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПометкуКомандЦвета()
	
	Элементы.УстановитьЦветКрасный.Пометка = (Цвет = Перечисления.ЦветаРабочегоКалендаря.Красный);
	Элементы.УстановитьЦветСиний.Пометка = (Цвет = Перечисления.ЦветаРабочегоКалендаря.Синий);
	Элементы.УстановитьЦветЖелтый.Пометка = (Цвет = Перечисления.ЦветаРабочегоКалендаря.Желтый);
	Элементы.УстановитьЦветЗеленый.Пометка = (Цвет = Перечисления.ЦветаРабочегоКалендаря.Зеленый);
	Элементы.УстановитьЦветОранжевый.Пометка = (Цвет = Перечисления.ЦветаРабочегоКалендаря.Оранжевый);
	Элементы.УстановитьЦветНет.Пометка = (Цвет = Перечисления.ЦветаРабочегоКалендаря.Нет);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура МК_ОформитьКомандыУстановкиЦвета(Форма)

	Элементы = Форма.Элементы;
	Цвет = Форма.Цвет;
	ЭлементыСтиля = Форма.МК_ЭлементыСтиля;
	
	МК_КлиентСервер.ОформитьДополнительнуюКнопкуПоСостоянию(ЭлементыСтиля,
		Элементы.МК_УстановитьЦветКрасный,
		Цвет = ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Красный"));
	
	МК_КлиентСервер.ОформитьДополнительнуюКнопкуПоСостоянию(ЭлементыСтиля,
		Элементы.МК_УстановитьЦветЖелтый,
		Цвет = ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Желтый"));
	
	МК_КлиентСервер.ОформитьДополнительнуюКнопкуПоСостоянию(ЭлементыСтиля,
		Элементы.МК_УстановитьЦветСиний,
		Цвет = ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Синий"));
	
	МК_КлиентСервер.ОформитьДополнительнуюКнопкуПоСостоянию(ЭлементыСтиля,
		Элементы.МК_УстановитьЦветЗеленый,
		Цвет = ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Зеленый"));
	
	МК_КлиентСервер.ОформитьДополнительнуюКнопкуПоСостоянию(ЭлементыСтиля,
		Элементы.МК_УстановитьЦветОранжевый,
		Цвет = ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Оранжевый"));
	
	МК_КлиентСервер.ОформитьДополнительнуюКнопкуПоСостоянию(ЭлементыСтиля,
		Элементы.МК_УстановитьЦветНет,
		Цвет = ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Нет"));
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьЦвет()
	
	Цвет = РаботаСРабочимКалендаремСервер.ПолучитьЦветСобытияКалендаря(СвязаннаяЗаписьКалендаря);
	
	Если Не ЭтоМобильныйКлиент Тогда
		УстановитьПометкуКомандЦвета();
	Иначе
		МК_ОформитьКомандыУстановкиЦвета(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьОтработана()
	
	Отработана = РаботаСРабочимКалендаремСервер.ЗаписьКалендаряОтработана(СвязаннаяЗаписьКалендаря);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеСвязаннойЗаписиКалендаря()
	
	СвязаннаяЗаписьКалендаря = Справочники.ЗаписиРабочегоКалендаря.СвязаннаяЗаписьКалендаря(
		Объект.Ссылка,
		СотрудникЗаписиКалендаря);
	Если Не ЗначениеЗаполнено(СвязаннаяЗаписьКалендаря) Тогда
		Элементы.ГруппаДанныеЗаписиКалендаря.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаДанныеЗаписиКалендаря.Видимость = Истина;
	Элементы.ГруппаЦветИНадпись.Видимость = Не ЭтоМобильныйКлиент;
	Если ЭтоМобильныйКлиент Тогда
		Элементы.МК_ГруппаЦвета.Видимость = Истина;
	КонецЕсли;
	
	ПрочитатьЦвет();
	ПрочитатьОтработана();
	ИспользоватьНапоминанияПользователя = ПолучитьФункциональнуюОпцию("ИспользоватьНапоминанияПользователя");
	
	Если ИспользоватьНапоминанияПользователя Тогда 
		СрокНапоминанияПоУмолчанию =
			ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиНапоминаний",
			"СрокНапоминанияПоУмолчанию",
			15);
		УстанавливатьНапоминаниеАвтоматически =
			ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиНапоминаний",
			"УстанавливатьНапоминаниеАвтоматически",
			Истина);
		ПрочитатьНапоминание();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНапоминание()
	
	Если Не ИспользоватьНапоминанияПользователя Тогда
		Возврат;
	КонецЕсли;
	
	ИзмененоНапоминание = Ложь;
	
	Если ЗначениеЗаполнено(СвязаннаяЗаписьКалендаря) Тогда
		
		Напоминание = ПолучитьПараметрыНапоминанияПоИсточнику(СвязаннаяЗаписьКалендаря);
		УстановитьНадписьНапоминания(
			Напоминание,
			УстановитьНапоминание,
			УстановленоНапоминание,
			СрокНапоминанияПоУмолчанию,
			ИнтервалВремениСтрокой,
			СпособУстановкиВремениНапоминания,
			ВремяНапоминания);
		
	Иначе
		
		УстановитьНапоминание = УстанавливатьНапоминаниеАвтоматически;
		УстановленоНапоминание = Ложь;
		ИнтервалВремениСтрокой = НапоминанияПользователяДокументооборотКлиентСервер.ПредставлениеВремениДокументооборот(СрокНапоминанияПоУмолчанию * 60);
		СпособУстановкиВремениНапоминания = Перечисления.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета;
		ВремяНапоминания = Дата(1,1,1);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеНапоминанияНаСервере()
	
	Если Не ИспользоватьНапоминанияПользователя Тогда
		Возврат;
	КонецЕсли;
	
	Если УстановитьНапоминание И (ИзмененоНапоминание Или ИзмененоВремя Или Не УстановленоНапоминание) Тогда
		
		// Отключение старого напоминания, если оно было установлено ранее
		Если УстановленоНапоминание И (ИзмененоНапоминание Или ИзмененоВремя) Тогда
			НапоминанияПользователяСлужебный.ОтключитьНапоминание(Напоминание, Ложь);
			НапоминаниеСтарое = Напоминание;
			Напоминание = Неопределено;
			ИзмененоНапоминание = Ложь;
			ИзмененоВремя = Ложь;
		КонецЕсли;
		
		Если СпособУстановкиВремениНапоминания = Перечисления.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета Тогда
			// Подключение напоминания относительно времени предмета
			ИнтервалВремениНапоминания = НапоминанияПользователяДокументооборот.ИнтервалВремени(ИнтервалВремениСтрокой);
			Напоминание = НапоминанияПользователяДокументооборот.ПодключитьНапоминаниеДоВремениПредмета(
				Строка(СвязаннаяЗаписьКалендаря), ИнтервалВремениНапоминания, СвязаннаяЗаписьКалендаря, "ДатаНачала");
			ОбновитьЗаписьВКешеНапоминаний = Истина;
			УстановленоНапоминание = Истина;
			
		ИначеЕсли СпособУстановкиВремениНапоминания = Перечисления.СпособыУстановкиВремениНапоминания.ВУказанноеВремя Тогда
			// Подключение напоминания в указанное время
			Напоминание = НапоминанияПользователяСлужебный.ПодключитьПроизвольноеНапоминание(
				Строка(СвязаннаяЗаписьКалендаря),
				ВремяНапоминания,
				0,
				СвязаннаяЗаписьКалендаря);
			ОбновитьЗаписьВКешеНапоминаний = Истина;
			УстановленоНапоминание = Истина;
			
		КонецЕсли;
		
	ИначеЕсли Не УстановитьНапоминание И УстановленоНапоминание Тогда
		
		// Отключение старого напоминания
		НапоминанияПользователяСлужебный.ОтключитьНапоминание(Напоминание);
		НапоминаниеСтарое = Напоминание;
		Напоминание = Неопределено;
		УстановленоНапоминание = Ложь;
		
	КонецЕсли;
	
	УстановитьНадписьНапоминания(Напоминание, УстановитьНапоминание, УстановленоНапоминание,
		СрокНапоминанияПоУмолчанию, ИнтервалВремениСтрокой,
		СпособУстановкиВремениНапоминания, ВремяНапоминания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеНапоминанияНаКлиенте()
	
	Если Не ИспользоватьНапоминанияПользователя Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбновитьЗаписьВКешеНапоминаний Тогда
		НапоминанияПользователяКлиент.ОбновитьЗаписьВКэшеОповещений(Напоминание);
		Оповестить("Запись_НапоминанияПользователя_Документооборот", Напоминание, Напоминание.Источник);
		ОбновитьЗаписьВКешеНапоминаний = Ложь;
	ИначеЕсли НапоминаниеСтарое <> Неопределено Тогда
		НапоминанияПользователяКлиент.УдалитьЗаписьИзКэшаОповещений(НапоминаниеСтарое);
		Оповестить("Удаление_НапоминанияПользователя_Документооборот", , НапоминаниеСтарое.Источник);
		НапоминаниеСтарое = Неопределено;
	КонецЕсли;
	УстановитьДоступностьЭлементовФормы();
	ЗаполнитьИнтервалыНапоминания();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИнтервалыНапоминания()
	
	Если ИспользоватьНапоминанияПользователя Тогда
		
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Очистить();
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'при наступлении события'"));
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'за 5 минут'"));
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'за 10 минут'"));
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'за 15 минут'"));
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'за 30 минут'"));
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'за 1 час'"));
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'за 2 часа'"));
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'за 3 часа'"));
		Элементы.ИнтервалВремениСтрокой.СписокВыбора.Добавить(НСтр("ru = 'Другое...'"));
		
		Если Элементы.ИнтервалВремениСтрокой.СписокВыбора.НайтиПоЗначению(ИнтервалВремениСтрокой) = Неопределено Тогда
			Элементы.ИнтервалВремениСтрокой.СписокВыбора.Вставить(0, ИнтервалВремениСтрокой);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьЭлементовФормы()
	
	Если ИспользоватьНапоминанияПользователя Тогда
		Элементы.ИнтервалВремениСтрокой.Доступность = УстановитьНапоминание;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыНапоминанияПоИсточнику(Источник)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НапоминанияПользователя.Пользователь,
	|	НапоминанияПользователя.ВремяСобытия,
	|	НапоминанияПользователя.Источник,
	|	НапоминанияПользователя.СрокНапоминания КАК СрокНапоминания,
	|	НапоминанияПользователя.Описание КАК Описание,
	|	2 КАК ИндексКартинки,
	|	НапоминанияПользователя.СпособУстановкиВремениНапоминания,
	|	НапоминанияПользователя.ИнтервалВремениНапоминания,
	|	НапоминанияПользователя.ИмяРеквизитаИсточника
	|ИЗ
	|	РегистрСведений.НапоминанияПользователя КАК НапоминанияПользователя
	|ГДЕ
	|	НапоминанияПользователя.Пользователь = &Пользователь
	|	И НапоминанияПользователя.Источник = &Источник";
	
	Запрос.УстановитьПараметр("Пользователь", ПользователиКлиентСервер.ТекущийПользователь());
	Запрос.УстановитьПараметр("Источник", Источник);
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПараметрыНапоминания = Новый Структура("Пользователь,Источник,ВремяСобытия,СрокНапоминания,
		|Описание,СпособУстановкиВремениНапоминания,ИнтервалВремениНапоминания,ИмяРеквизитаИсточника");
	
	Результат = Неопределено;
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыНапоминания, Выборка);
		Результат = ПараметрыНапоминания;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьНадписьНапоминания(
	Напоминание,
	УстановитьНапоминание,
	УстановленоНапоминание,
	СрокНапоминанияПоУмолчанию,
	ИнтервалВремениСтрокой,
	СпособУстановкиВремениНапоминания,
	ВремяНапоминания)
	
	Если Напоминание <> Неопределено Тогда
		
		УстановитьНапоминание = Истина;
		УстановленоНапоминание = Истина;
		
		Если Напоминание.СпособУстановкиВремениНапоминания = ПредопределенноеЗначение("Перечисление.СпособыУстановкиВремениНапоминания.ВУказанноеВремя") Тогда
			
			ИнтервалВремениСтрокой =
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'в %2 %1'"),
					Формат(Напоминание.ВремяСобытия, "ДЛФ=D"),
					Формат(Напоминание.ВремяСобытия, "ДФ=ЧЧ:мм"));
			СпособУстановкиВремениНапоминания = ПредопределенноеЗначение("Перечисление.СпособыУстановкиВремениНапоминания.ВУказанноеВремя");
			ВремяНапоминания = Напоминание.ВремяСобытия;
			
		ИначеЕсли Напоминание.СпособУстановкиВремениНапоминания = ПредопределенноеЗначение("Перечисление.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета") Тогда
			
			Если Напоминание.ИнтервалВремениНапоминания >= 0 И Напоминание.ИмяРеквизитаИсточника = "ДатаНачала" Тогда
				
				ИнтервалВремениСтрокой = НапоминанияПользователяДокументооборотКлиентСервер.ПредставлениеВремениДокументооборот(Напоминание.ИнтервалВремениНапоминания);
				
			ИначеЕсли Напоминание.ИнтервалВремениНапоминания <> 0 И Напоминание.ИмяРеквизитаИсточника = "ДатаОкончания" Тогда
				
				ИнтервалВремениСтрокой = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'за %1 до окончания'"),
					НапоминанияПользователяДокументооборотКлиентСервер.ПредставлениеВремени(Напоминание.ИнтервалВремениНапоминания));
				
			ИначеЕсли Напоминание.ИнтервалВремениНапоминания = 0 И Напоминание.ИмяРеквизитаИсточника = "ДатаОкончания" Тогда
				
				ИнтервалВремениСтрокой = НСтр("ru = 'при окончании события'");
				
			КонецЕсли;
			
			СпособУстановкиВремениНапоминания = ПредопределенноеЗначение("Перечисление.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета");
			ВремяНапоминания = Дата(1,1,1);
			
		ИначеЕсли Напоминание.СпособУстановкиВремениНапоминания = ПредопределенноеЗначение("Перечисление.СпособыУстановкиВремениНапоминания.Периодически") Тогда
			
			ИнтервалВремениСтрокой = НСтр("ru = 'по заданному расписанию'");
			СпособУстановкиВремениНапоминания = ПредопределенноеЗначение("Перечисление.СпособыУстановкиВремениНапоминания.Периодически");
			ВремяНапоминания = Дата(1,1,1);
			
		КонецЕсли;
		
	Иначе
		
		УстановитьНапоминание = Ложь;
		УстановленоНапоминание = Ложь;
		ИнтервалВремениСтрокой = НапоминанияПользователяДокументооборотКлиентСервер.ПредставлениеВремениДокументооборот(СрокНапоминанияПоУмолчанию * 60);
		СпособУстановкиВремениНапоминания = ПредопределенноеЗначение("Перечисление.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета");
		ВремяНапоминания = Дата(1,1,1);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыНапоминанияДокументооборота(ПараметрыНапоминания)
	
	Если ПараметрыНапоминания.Свойство("Пользователь")
		И ПараметрыНапоминания.Свойство("Источник")
		И ПараметрыНапоминания.Свойство("ВремяСобытия")
		И ПараметрыНапоминания.Свойство("СпособУстановкиВремениНапоминания")
		И ПараметрыНапоминания.Свойство("ИнтервалВремениНапоминания")
		И ПараметрыНапоминания.Свойство("ИмяРеквизитаИсточника") Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ПриИзмененииОтработана()
	
	Если Отработана Тогда
		УстановитьНапоминание = Ложь;
		Цвет = Перечисления.ЦветаРабочегоКалендаря.Нет;
	КонецЕсли;
	
	РаботаСРабочимКалендаремСервер.УстановитьОтработанаЗаписьКалендаря(СвязаннаяЗаписьКалендаря, Отработана);
	ПриИзмененииЦвета();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьМестныеВременаНаСервере()
	
	Если ПараметрыПреобразованияМестногоВремени = Неопределено Тогда
		ПараметрыПреобразованияМестногоВремени = РаботаСЧасовымиПоясами.ПараметрыПреобразованияМестногоВремени();
		ПредставлениеМестногоЧасовогоПояса = ПараметрыПреобразованияМестногоВремени.ПредставлениеМестногоЧасовогоПояса;
	КонецЕсли;
	
	Если Объект.ВесьДень Тогда
		МестнаяДатаНачала = Объект.ДатаНачала;
		МестнаяДатаОкончания = Объект.ДатаОкончания;
	Иначе
		МестнаяДатаНачала = РаботаСЧасовымиПоясамиКлиентСервер.ПривестиКМестномуВремени(
			Объект.ДатаНачала,
			ПараметрыПреобразованияМестногоВремени);
		МестнаяДатаОкончания = РаботаСЧасовымиПоясамиКлиентСервер.ПривестиКМестномуВремени(
			Объект.ДатаОкончания,
			ПараметрыПреобразованияМестногоВремени);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьМестныеВременаНаКлиенте()
	
	Если Объект.ВесьДень Тогда
		МестнаяДатаНачала = Объект.ДатаНачала;
		МестнаяДатаОкончания = Объект.ДатаОкончания;
	Иначе
		МестнаяДатаНачала = РаботаСЧасовымиПоясамиКлиентСервер.ПривестиКМестномуВремени(
			Объект.ДатаНачала,
			ПараметрыПреобразованияМестногоВремени);
		МестнаяДатаОкончания = РаботаСЧасовымиПоясамиКлиентСервер.ПривестиКМестномуВремени(
			Объект.ДатаОкончания,
			ПараметрыПреобразованияМестногоВремени);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВременаСеанса()
	
	Если Объект.ВесьДень Тогда
		Объект.ДатаНачала = МестнаяДатаНачала;
		Объект.ДатаОкончания = МестнаяДатаОкончания;
	Иначе
		Объект.ДатаНачала = РаботаСЧасовымиПоясамиКлиентСервер.ПривестиКВремениСеанса(
			МестнаяДатаНачала,
			ПараметрыПреобразованияМестногоВремени);
		Объект.ДатаОкончания = РаботаСЧасовымиПоясамиКлиентСервер.ПривестиКВремениСеанса(
			МестнаяДатаОкончания,
			ПараметрыПреобразованияМестногоВремени);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеЧасовогоПоясаСотрудника()
	
	ЧасовойПоясСотрудника = РаботаСЧасовымиПоясамиПовтИсп.ЧасовойПоясПользователя(Объект.Сотрудник);
	МестныйЧасовойПояс = РаботаСЧасовымиПоясами.МестныйЧасовойПояс();
	
	Если ЧасовойПоясСотрудника <> МестныйЧасовойПояс Тогда
		ПредставлениеЧасовогоПоясаСотрудника = РаботаСЧасовымиПоясами.ПредставлениеЧасовогоПоясаДО(ЧасовойПоясСотрудника);
	Иначе
		ПредставлениеЧасовогоПоясаСотрудника = "";
	КонецЕсли;
	
	Элементы.ПредставлениеЧасовогоПоясаСотрудника.Видимость = ЗначениеЗаполнено(ПредставлениеЧасовогоПоясаСотрудника);
	Элементы.АвторГиперссылка.Видимость = Объект.Сотрудник <> Объект.Автор
		И Не Элементы.ПредставлениеЧасовогоПоясаСотрудника.Видимость;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьПредставленияМестногоЧасовогоПоясаНаСервере()
	
	Элементы.ПредставлениеМестногоЧасовогоПояса.Видимость = ЗначениеЗаполнено(ПредставлениеМестногоЧасовогоПояса)
		И Не Объект.ВесьДень;
	Элементы.ДлительностьСтр.Видимость = Не Элементы.ПредставлениеМестногоЧасовогоПояса.Видимость;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимостьПредставленияМестногоЧасовогоПоясаНаКлиенте()
	
	Элементы.ПредставлениеМестногоЧасовогоПояса.Видимость = ЗначениеЗаполнено(ПредставлениеМестногоЧасовогоПояса)
		И Не Объект.ВесьДень;
	Элементы.ДлительностьСтр.Видимость = Не Элементы.ПредставлениеМестногоЧасовогоПояса.Видимость;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьЗамещающегоНаСервере(НовыйЗамещающий)
	
	ЗамещениеОбъект = Замещение.ПолучитьОбъект();
	Если ЗамещениеОбъект.Замещающий <> НовыйЗамещающий Тогда
		ЗамещениеОбъект.Замещающий = НовыйЗамещающий;
		ЗамещениеОбъект.Записать();
		ОбновитьЗамещает();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуДобавленияЗамещающего(Замещающий = Неопределено)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ВидЗамещения", ПредопределенноеЗначение("Перечисление.ВидыЗамещения.Замещающие"));
	ЗначенияЗаполнения.Вставить("Основание", Объект.Ссылка);
	ЗначенияЗаполнения.Вставить("Сотрудник", Объект.Сотрудник);
	ЗначенияЗаполнения.Вставить("ДатаНачала", Объект.ДатаНачала);
	ЗначенияЗаполнения.Вставить("ДатаОкончания", Объект.ДатаОкончания);
	ЗначенияЗаполнения.Вставить("Создал", Объект.Автор);
	Если ЗначениеЗаполнено(Замещающий) Тогда
		ЗначенияЗаполнения.Вставить("Замещающий", Замещающий);
	КонецЕсли; 
	
	ПараметрыФормыЗамещения = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОткрытьФорму("Справочник.ЗамещающиеИПомощники.ФормаОбъекта", ПараметрыФормыЗамещения);
	
КонецПроцедуры

&НаСервере
Процедура ОчисткаЗамещающего()
	
	Если Не ЗначениеЗаполнено(Замещение) Тогда
		Возврат;
	КонецЕсли;
	
	ЗамещениеОбъект = Замещение.ПолучитьОбъект();
	ЗамещениеОбъект.УстановитьПометкуУдаления(Истина);
	
	Замещение = Неопределено;
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции_МобильныйКлиент

&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	// Общее.
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
	
	Элементы.МК_ГруппаЗаместители.Объединенная = Ложь;
	
	// Командная панель.
	Элементы.ФормаЗаписать.ПоложениеВКоманднойПанели =
		ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели;
	
	Элементы.ФормаЗаписать.Отображение = ОтображениеКнопки.Картинка;
	
	Элементы.ФормаПодписаться.ПоложениеВКоманднойПанели =
		ПоложениеКнопкиВКоманднойПанели.ВДополнительномПодменю;
	
	Элементы.ФормаСправка.ПоложениеВКоманднойПанели =
		ПоложениеКнопкиВКоманднойПанели.ВДополнительномПодменю;
	
	// Нижний блок.
	Элементы.МК_НижнийБлок.Видимость = Истина;
	МК.ПреобразоватьКнопкуВАкцентную(ЭтотОбъект,
		МК_ЭлементыСтиля,
		Элементы.ФормаЗаписатьИЗакрыть,
		Элементы.МК_НижнийБлок);
	
	// Заместители.
	Элементы.Заместители.Шрифт = ШрифтыСтиля.МелкийШрифтТекста;
	Элементы.Заместители.Шапка = Ложь;
	Элементы.Заместители.ВариантУправленияВысотой = ВариантУправленияВысотойТаблицы.ПоСодержимому;
	Элементы.Заместители.МаксимальнаяВысотаВСтрокахТаблицы = 3;
	
	Если Элементы.МК_ГруппаЗаместители.Видимость Тогда
		Элементы.Переместить(Элементы.Заместители, Элементы.МК_ГруппаЗаместители);
		Элементы.Переместить(Элементы.ЗамещающиеЗамещающий, Элементы.МК_ЗаместителиДанные);
		Элементы.Переместить(Элементы.ЗамещающиеКомментарий, Элементы.МК_ЗаместителиДанные);
		Элементы.ЗамещающиеЗамещающий.Высота = 1;
		Элементы.ЗамещающиеКомментарий.ЦветТекста = ЦветаСтиля.МК_ЦветАвтораИсполнителя;
		МК.УстановитьПолюУсловноеОформлениеСкрытНеВидим(СписокЗамещающих, "Комментарий", Ложь);
		МК_НастроитьОтображениеЗаместителей(ЭтотОбъект);
	КонецЕсли;
	
	Элементы.МК_ГруппаДатаНачалаОкончания.Видимость = Истина;
	Элементы.ДекорацияРазделительВремени.Видимость = Ложь;
	Элементы.ПредставлениеМестногоЧасовогоПояса.Видимость = Ложь;

	Элементы.ГруппаСотрудник.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаДата.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаВидОтсутствия.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаЦвет.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	Элементы.ГруппаВидОтсутствия.ВыравниваниеЭлементовИЗаголовков = 
		ВариантВыравниванияЭлементовИЗаголовков.ЭлементыПравоЗаголовкиЛево;
		
	Элементы.МК_ДекорацияОтступДат.Видимость = Ложь;

	Элементы.ДатаНачала.Заголовок = НСтр("ru='Начало:'");
	Элементы.ДатаНачала.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ДатаНачала.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ДатаНачалаВремя.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ДатаНачала.Ширина = 10;
	Элементы.Переместить(Элементы.ДатаНачала, Элементы.МК_ГруппаДатаНачала);
	Элементы.Переместить(Элементы.ДатаНачалаВремя, Элементы.МК_ГруппаДатаНачала);
	
	Элементы.ДатаОкончания.Заголовок = НСтр("ru='Конец:'");
	Элементы.ДатаОкончания.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ДатаОкончания.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ДатаОкончанияВремя.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ДатаОкончания.Ширина = 10;
	Элементы.Переместить(Элементы.ДатаОкончания, Элементы.МК_ГруппаДатаОкончания);
	Элементы.Переместить(Элементы.ДатаОкончанияВремя, Элементы.МК_ГруппаДатаОкончания);

	Элементы.Переместить(Элементы.ГруппаДлительность, Элементы.ГруппаДата, Элементы.ГруппаДатаНачалаОкончания);
	Элементы.Переместить(Элементы.ДлительностьСтр, Элементы.ГруппаДлительность, Элементы.ВесьДень);
	Элементы.ВесьДень.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ВесьДень.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	
	Элементы.Сотрудник.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	Элементы.Сотрудник.Заголовок = НСтр("ru='Кто:'");
	Элементы.АвторГиперссылка.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	Элементы.АвторГиперссылка.Заголовок = НСтр("ru='Ввёл запись:'");
	
	Элементы.ПредставлениеЧасовогоПоясаСотрудника.Видимость = Ложь;
	
	Элементы.Комментарий.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	Элементы.Комментарий.Заголовок = НСтр("ru='Комментарий:'");
	Элементы.Комментарий.МаксимальнаяШирина = 0;
	Элементы.Комментарий.ПодсказкаВвода = "";
	Элементы.Комментарий.РастягиватьПоВертикали = Истина;
	Элементы.Комментарий.АвтоМаксимальнаяВысота = Ложь;
	Элементы.Комментарий.МаксимальнаяВысота = 0;
	Элементы.Комментарий.Высота = 1;
	
	Элементы.Отработана.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ДекорацияЗаголовокЦвет.Видимость = Истина;
	
	Элементы.ВидОтсутствия.Заголовок = НСтр("ru='Причина:'");
	
	Элементы.МК_ГруппаДатаНачалаОкончания.Группировка =
		ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно;

	Элементы.МК_ДекорацияОтступДат.Видимость = Истина;
	
	// Уведомления о событии.
	Если ИспользоватьНапоминанияПользователя Тогда
		Элементы.УстановитьНапоминание.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
		Элементы.ИнтервалВремениСтрокой.Доступность = УстановитьНапоминание;
		Элементы.ИнтервалВремениСтрокой.Ширина = 5;
		Элементы.ИнтервалВремениСтрокой.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
		Элементы.ГруппаНапоминаниеОтносительноДатыНачала.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	КонецЕсли;
	
	Элементы.ГруппаЦвет.Группировка =
		ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура МК_НастроитьОтображениеЗаместителей(Форма)
	
	Если Форма.КоличествоЗамещающих = 0 Тогда
		Форма.Элементы.Заместители.Видимость = Ложь;
	КонецЕсли;
	
	ОбновитьЗаголовокЗаместителей(Форма);

	МК_КлиентСервер.ОформитьКнопкуСворачиваемойГруппы(
		Форма.МК_ЭлементыСтиля,
		Форма.Элементы.МК_ПоказатьСкрытьЗаместителей,
		Форма.Элементы.Заместители.Видимость,
		Ложь);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЗаголовокЗаместителей(Форма)
	
	Форма.Элементы.МК_ПоказатьСкрытьЗаместителей.Заголовок =
		СтрШаблон(НСтр("ru = 'Заместители (%1) %2'"),
			Формат(Форма.КоличествоЗамещающих, "ЧН=0; ЧГ="),
			Символы.НПП);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
