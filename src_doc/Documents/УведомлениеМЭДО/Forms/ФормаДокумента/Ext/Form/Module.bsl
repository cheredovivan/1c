#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриСозданииНаСервереРедакцииКонфигурации();
	
	Если Объект.Ссылка.Пустая() И Параметры.ЭтоПрограммноеСоздание Тогда
		
		Документ = Параметры.Документ; // ОпределяемыйТип.ПредметМЭДО
		Если Не МЭДОПереопределяемый.ЭтоВходящийДокумент(Документ) Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Отменено. Уведомление можно создать только на входящий документ, на исходящий и др. нельзя!'"),
				, , , Истина);
			Возврат;
		КонецЕсли;
		
		// Создается новое исходящее уведомление на входящий документ:
		Объект.Дата = ТекущаяДатаСеанса();
		Объект.Документ = Документ;
		СтруктураСостояния = РегистрыСведений.СостоянияДокументовМЭДО.СостояниеДокумента(
			Объект.Документ,
			Перечисления.СостоянияДокументовМЭДО.ДокументПолучен,
			"ИдентификаторДокумента",
			Объект.Дата);
		Объект.ИдентификаторДокумента = СтруктураСостояния.ИдентификаторДокумента;
		Объект.ТипУведомления = Параметры.ТипУведомления;
		Объект.Направление = Перечисления.НаправленияСообщенийМЭДО.Исходящее;
		Реквизиты = МЭДОПереопределяемый.ТребуемыеДанныеВходящегоДокумента(
			Объект.Документ, "Организация, Контрагент", МЭДОСтруктурыДанных.НовыйЛегкийОтвет());
		Объект.Организация = Реквизиты.Организация;
		Объект.Контрагент = Реквизиты.Контрагент;
		Объект.ВерсияМЭДО = ВерсияМЭДОКонтрагента(Объект.Контрагент);
		Объект.Ответственный = МЭДОПереопределяемый.ТекущийОтветственный();
		
		Если Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОбОтказеВРегистрации Тогда
			Если ТипЗнч(Параметры.ОтказВРегистрации) = Тип("Структура") Тогда
				НоваяСтрока = Объект.ОтказВРегистрации.Добавить();
				НоваяСтрока.ПричинаОтказа = Параметры.ОтказВРегистрации.ПричинаОтказаСсылка;
			КонецЕсли;
			
			Если МЭДОПовтИсп.ВариантАвтосозданияУведомленийМЭДО()
				= Перечисления.ВариантыАвтосозданияУведомленийМЭДО.СоздаватьИОтправлять Тогда
				Объект.ГотовоКОтправке = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Модифицированность = Истина;
	КонецЕсли;
	
	ЗаполнитьПоляПоТипуУведомления();
	
	УправлениеДоступностьюВидимостью();
	УстановитьВидимостьЗакладок();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбщегоНазначенияДокументооборот.ПоказатьНадписьПометкиУдаления(ЭтотОбъект, ТекущийОбъект.ПометкаУдаления);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Не ЗначениеЗаполнено(Объект.Документ)
		И Объект.ТипУведомления <> ПредопределенноеЗначение("Перечисление.ТипыУведомленийМЭДО.ОбОтказеВРегистрации")
		Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не заполнен Документ'"), , "Объект.Документ", "", Отказ);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Отклик = Новый Структура();
	Отклик.Вставить("ПредметСообщения", Объект.Ссылка);
	Отклик.Вставить("Документ", Объект.Документ);
	Оповестить("Запись_УведомлениеМЭДО", Отклик, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГотовоКОтправкеПриИзменении(Элемент)
	
	Элементы.ОтправитьПоМЭДО.Доступность = Объект.ГотовоКОтправке;
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеКонвертаНажатие(Элемент)
	
	ТекстовыйДокумент = ТекстовыйДокументСДаннымиКонверта(Объект.Ссылка);
	ТекстовыйДокумент.Показать("envelope.ini");
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеСообщенияНажатие(Элемент)
	
	ТекстовыйДокумент = ТекстовыйДокументСДаннымиСообщения(Объект.Ссылка);
	ТекстовыйДокумент.Показать("" + Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	Объект.ВерсияМЭДО = ВерсияМЭДОКонтрагента(Объект.ВерсияМЭДО);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипУведомленияПриИзменении(Элемент)
	
	СообщениеОбОшибке = "";
	ТипУведомленияПриИзмененииНаСервере(СообщениеОбОшибке);
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		ПоказатьПредупреждение(, СообщениеОбОшибке);
	КонецЕсли;
	
КонецПроцедуры

#Область ТЧ_ПринятиеКИсполнению

// Работа с полями Комментарий
&НаКлиенте
Процедура КомментарийРуководителяСекретаряНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Комментарий", Элемент.ТекстРедактирования);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводКомментарияРуководителяСекретаряЗавершение", ЭтотОбъект);
	РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму("ОбщаяФорма.ВводКомментария", ПараметрыФормы,,,,, ОписаниеОповещения, РежимОткрытияОкна);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийРуководителяПодразделенияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Комментарий", Элемент.ТекстРедактирования);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводКомментарияРуководителяПодразделенияЗавершение", ЭтотОбъект);
	РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму("ОбщаяФорма.ВводКомментария", ПараметрыФормы,,,,, ОписаниеОповещения, РежимОткрытияОкна);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийИсполнителяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Комментарий", Элемент.ТекстРедактирования);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводКомментарияИсполнителяЗавершение", ЭтотОбъект);
	РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму("ОбщаяФорма.ВводКомментария", ПараметрыФормы,,,,, ОписаниеОповещения, РежимОткрытияОкна);
	
КонецПроцедуры

// Работа с полями Юрлицо
&НаКлиенте
Процедура ЮрлицоИсполнительПриИзменении(Элемент)
	
	ЮрлицоИсполнительПриИзмененииНаСервере();
	
КонецПроцедуры

// Работа с полями, зависящими от Юр.лиц
&НаКлиенте
Процедура РуководительСекретарьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ТипЗнч(Объект.ПринятиеКИсполнению[0].Юрлицо) = МЭДОВызовСервера.ТипКонтрагенты() Тогда
		СтандартнаяОбработка = Ложь;
		МЭДОКлиентПереопределяемый.ОткрытьВыборКонтактногоЛица(
			Объект.ПринятиеКИсполнению[0].Юрлицо,
			Объект.ПринятиеКИсполнению[0].РуководительСекретарь,
			Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РуководительПодразделенияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ТипЗнч(Объект.ПринятиеКИсполнению[0].Юрлицо) = МЭДОВызовСервера.ТипКонтрагенты() Тогда
		СтандартнаяОбработка = Ложь;
		МЭДОКлиентПереопределяемый.ОткрытьВыборКонтактногоЛица(
			Объект.ПринятиеКИсполнению[0].Юрлицо,
			Объект.ПринятиеКИсполнению[0].РуководительПодразделения,
			Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ТипЗнч(Объект.ПринятиеКИсполнению[0].Юрлицо) = МЭДОВызовСервера.ТипКонтрагенты() Тогда
		СтандартнаяОбработка = Ложь;
		МЭДОКлиентПереопределяемый.ОткрытьВыборКонтактногоЛица(
			Объект.ПринятиеКИсполнению[0].Юрлицо,
			Объект.ПринятиеКИсполнению[0].Исполнитель,
			Элемент);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ЮрлицоПодготовкаДокладаПриИзменении(Элемент)
	
	Если ТипЗнч(Объект.ПодготовкаДоклада[0].Юрлицо) = МЭДОВызовСервера.ТипКонтрагенты() Тогда
		Объект.ПодготовкаДоклада[0].Ответственный = МЭДОВызовСервера.КонтактноеЛицоПустаяСсылка();
	Иначе
		Объект.ПодготовкаДоклада[0].Ответственный = МЭДОВызовСервера.СотрудникПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЮрлицоНаправлениеДокладаПриИзменении(Элемент)
	
	Если ТипЗнч(Объект.НаправлениеДоклада[0].Юрлицо) = Тип("СправочникСсылка.Контрагенты") Тогда
		Объект.НаправлениеДоклада[0].Ответственный = МЭДОВызовСервера.КонтактноеЛицоПустаяСсылка();
	Иначе
		Объект.НаправлениеДоклада[0].Ответственный = МЭДОВызовСервера.СотрудникПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЮрлицоХодИсполненияПриИзменении(Элемент)
	
	Если ТипЗнч(Объект.ХодИсполнения[0].Юрлицо) = Тип("СправочникСсылка.Контрагенты") Тогда
		Объект.ХодИсполнения[0].Ответственный = МЭДОВызовСервера.КонтактноеЛицоПустаяСсылка();
	Иначе
		Объект.ХодИсполнения[0].Ответственный = МЭДОВызовСервера.СотрудникПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ОтветственныйПодготовкаДокладаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ТипЗнч(Объект.ПодготовкаДоклада[0].Юрлицо) = МЭДОВызовСервера.ТипКонтрагенты() Тогда
		СтандартнаяОбработка = Ложь;
		МЭДОКлиентПереопределяемый.ОткрытьВыборКонтактногоЛица(
			Объект.ПодготовкаДоклада[0].Юрлицо,
			Объект.ПодготовкаДоклада[0].Ответственный,
			Элемент,
			НСтр("ru = 'Юрлицо'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйНаправлениеДокладаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ТипЗнч(Объект.НаправлениеДоклада[0].Юрлицо) = МЭДОВызовСервера.ТипКонтрагенты() Тогда
		СтандартнаяОбработка = Ложь;
		МЭДОКлиентПереопределяемый.ОткрытьВыборКонтактногоЛица(
			Объект.НаправлениеДоклада[0].Юрлицо,
			Объект.НаправлениеДоклада[0].Ответственный,
			Элемент,
			НСтр("ru = 'Юрлицо'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйХодИсполненияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ТипЗнч(Объект.ХодИсполнения[0].Юрлицо) = МЭДОВызовСервера.ТипКонтрагенты() Тогда
		СтандартнаяОбработка = Ложь;
		МЭДОКлиентПереопределяемый.ОткрытьВыборКонтактногоЛица(
			Объект.ХодИсполнения[0].Юрлицо,
			Объект.ХодИсполнения[0].Ответственный,
			Элемент,
			НСтр("ru = 'Юрлицо'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПостановкаНаКонтроль_КонтрольныеПунктыДокумента

// Постановка на контроль контрольные пункты документа юрлицо при изменении.
// 
// Параметры:
//  Элемент - ПолеФормы -
&НаКлиенте
Процедура ПостановкаНаКонтроль_КонтрольныеПунктыДокументаЮрлицоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПостановкаНаКонтроль_КонтрольныеПунктыДокумента.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ТекущиеДанные.Юрлицо) = МЭДОВызовСервера.ТипКонтрагенты() Тогда
		ТекущиеДанные.Ответственный = МЭДОВызовСервера.КонтактноеЛицоПустаяСсылка();
	Иначе
		ТекущиеДанные.Ответственный = МЭДОВызовСервера.СотрудникПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПостановкаНаКонтроль_КонтрольныеПунктыДокументаОтветственныйНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ПостановкаНаКонтроль_КонтрольныеПунктыДокумента.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ТекущиеДанные.Юрлицо) = МЭДОВызовСервера.ТипКонтрагенты() Тогда
		СтандартнаяОбработка = Ложь;
		МЭДОКлиентПереопределяемый.ОткрытьВыборКонтактногоЛица(
			ТекущиеДанные.Юрлицо,
			ТекущиеДанные.Ответственный,
			Элемент,
			НСтр("ru = 'Юрлицо'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьПоМЭДО(Команда)
	
	ОчиститьСообщения();
	
	Если Не Объект.ГотовоКОтправке Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Необходимо установить флаг ""Готово к отправке""'"),
			Объект.Ссылка,
			"ГотовоКОтправке",
			"Объект");
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Или Модифицированность Тогда 
		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеОтвета = МЭДОКлиент.НовыйЛегкийОтвет();
	МЭДОВызовСервера.ОтправитьИсходящиеУведомления(
		Объект.Организация,
		ДанныеОтвета,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка));
	
	МЭДОКлиент.ПоказатьРезультатОтправки(НСтр("ru = 'Уведомление отправлено'"), ДанныеОтвета);
	Прочитать();
	Элементы.ОтправитьПоМЭДО.Доступность = Объект.ГотовоКОтправке;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ТипУведомленияПриИзмененииНаСервере(СообщениеОбОшибке)
	
	УстановитьВидимостьЗакладок();
	СообщениеОбОшибке = ЗаполнитьПоляПоТипуУведомления();
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьПоляПоТипуУведомления()
	
	Если Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОПостановкеНаКонтроль Тогда
		
		Если Объект.ПостановкаНаКонтроль.Количество() = 0 Тогда
			Объект.ПостановкаНаКонтроль.Добавить();
		КонецЕсли;
		
	ИначеЕсли Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОРегистрации Тогда
		
		Если Объект.Регистрация.Количество() = 0 Тогда
			Объект.Регистрация.Добавить();
		КонецЕсли;
		
		Если Объект.Направление = Перечисления.НаправленияСообщенийМЭДО.Исходящее Тогда
			// Если документ уже зарегистрирован, то проставим те же данные, если же нет, то запретим это делать.
			Реквизиты = МЭДОПереопределяемый.ТребуемыеДанныеВходящегоДокумента(
				Объект.Документ, "ДатаРегистрации, РегистрационныйНомер", МЭДОСтруктурыДанных.НовыйЛегкийОтвет());
			Если Не ЗначениеЗаполнено(Реквизиты.РегистрационныйНомер) Тогда
				Возврат НСтр("ru = 'Документ еще не зарегистрирован. Перед отправкой уведомления о регистрации нужно сначала зарегистрировать его'");
			КонецЕсли;
			
			СтрокаТЧ = Объект.Регистрация[0];
			Если МЭДО.ПрисвоитьОтличающееся(СтрокаТЧ.ДатаРегистрации, НачалоДня(Реквизиты.ДатаРегистрации)) Тогда
				Модифицированность = Истина;
			КонецЕсли;
			Если МЭДО.ПрисвоитьОтличающееся(СтрокаТЧ.РегистрационныйНомер, Реквизиты.РегистрационныйНомер) Тогда
				Модифицированность = Истина;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОбОтказеВРегистрации Тогда
		
		Если Объект.ОтказВРегистрации.Количество() = 0 Тогда
			Объект.ОтказВРегистрации.Добавить();
		КонецЕсли;
	
	ИначеЕсли Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОПринятииКИсполнению Тогда
		
		Если Объект.ПринятиеКИсполнению.Количество() = 0 Тогда
			Объект.ПринятиеКИсполнению.Добавить();
			Объект.ПринятиеКИсполнению[0].Юрлицо = МЭДОПереопределяемый.КонтрагентПустаяСсылка();
			ЮрлицоИсполнительПриИзмененииНаСервере();
		КонецЕсли;
	
	ИначеЕсли Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОПодготовкеДоклада Тогда
		
		Если Объект.ПодготовкаДоклада.Количество() = 0 Тогда
			Объект.ПодготовкаДоклада.Добавить();
			Объект.ПодготовкаДоклада[0].Юрлицо = МЭДОПереопределяемый.КонтрагентПустаяСсылка();
			Объект.ПодготовкаДоклада[0].Ответственный = МЭДОВызовСервера.КонтактноеЛицоПустаяСсылка();
		КонецЕсли;
	
	ИначеЕсли Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОНаправленииДоклада Тогда
		
		Если Объект.НаправлениеДоклада.Количество() = 0 Тогда
			Объект.НаправлениеДоклада.Добавить();
			Объект.НаправлениеДоклада[0].Юрлицо = МЭДОПереопределяемый.КонтрагентПустаяСсылка();
			Объект.НаправлениеДоклада[0].Ответственный = МЭДОВызовСервера.КонтактноеЛицоПустаяСсылка();
		КонецЕсли;
		
	ИначеЕсли Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОХодеИсполнения Тогда
		
		Если Объект.ХодИсполнения.Количество() = 0 Тогда
			Объект.ХодИсполнения.Добавить();
			Объект.ХодИсполнения[0].Юрлицо = МЭДОПереопределяемый.КонтрагентПустаяСсылка();
			Объект.ХодИсполнения[0].Ответственный = МЭДОВызовСервера.КонтактноеЛицоПустаяСсылка();
		КонецЕсли;
		
		Если Объект.Направление = Перечисления.НаправленияСообщенийМЭДО.Исходящее Тогда
			// Если документ уже зарегистрирован, то проставим те же данные, если же нет, то запретим это делать.
			Реквизиты = МЭДОПереопределяемый.ТребуемыеДанныеВходящегоДокумента(
				Объект.Документ, "ДатаРегистрации, РегистрационныйНомер", МЭДОСтруктурыДанных.НовыйЛегкийОтвет());
			Если Не ЗначениеЗаполнено(Реквизиты.РегистрационныйНомер) Тогда
				Возврат НСтр("ru = 'Документ еще не зарегистрирован. Перед отправкой уведомления о ходе исполнения нужно сначала зарегистрировать его'");
			КонецЕсли;
			
			СтрокаТЧ = Объект.ХодИсполнения[0];
			Если МЭДО.ПрисвоитьОтличающееся(СтрокаТЧ.ДатаРегистрации, НачалоДня(Реквизиты.ДатаРегистрации)) Тогда
				Модифицированность = Истина;
			КонецЕсли;
			Если МЭДО.ПрисвоитьОтличающееся(СтрокаТЧ.РегистрационныйНомер, Реквизиты.РегистрационныйНомер) Тогда
				Модифицированность = Истина;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОбОпубликовании Тогда
		
		Если Объект.Опубликование.Количество() = 0 Тогда
			Объект.Опубликование.Добавить();		
		КонецЕсли;
		
		Если Объект.Направление = Перечисления.НаправленияСообщенийМЭДО.Исходящее Тогда
			// Если документ уже зарегистрирован, то проставим те же данные, если же нет, то запретим это делать.
			Реквизиты = МЭДОПереопределяемый.ТребуемыеДанныеВходящегоДокумента(
				Объект.Документ, "ДатаРегистрации, РегистрационныйНомер", МЭДОСтруктурыДанных.НовыйЛегкийОтвет());
			Если Не ЗначениеЗаполнено(Реквизиты.РегистрационныйНомер) Тогда
				Возврат НСтр("ru = 'Документ еще не зарегистрирован. Перед отправкой уведомления об опубликовании нужно сначала зарегистрировать его'");
			КонецЕсли;
			
			СтрокаТЧ = Объект.Опубликование[0];
			Если МЭДО.ПрисвоитьОтличающееся(СтрокаТЧ.ДатаРегистрации, НачалоДня(Реквизиты.ДатаРегистрации)) Тогда
				Модифицированность = Истина;
			КонецЕсли;
			Если МЭДО.ПрисвоитьОтличающееся(СтрокаТЧ.РегистрационныйНомер, Реквизиты.РегистрационныйНомер) Тогда
				Модифицированность = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

&НаСервере
Процедура УправлениеДоступностьюВидимостью()
	
	ЕстьПолныеПрава = Пользователи.ЭтоПолноправныйПользователь();
	Элементы.ОтправитьПоМЭДО.Доступность = Объект.ГотовоКОтправке;
	Если Не ЕстьПолныеПрава Тогда
		Элементы.ГруппаТехническиеДанные.Видимость = Ложь;
	КонецЕсли;
	
	// Эти поля изменяются только программно:
	Элементы.ГруппаВерх2.ТолькоПросмотр = Не ЕстьПолныеПрава;
	Элементы.ГруппаПочтиНиз.ТолькоПросмотр = Не ЕстьПолныеПрава;
	Элементы.Отправлено.ТолькоПросмотр = Не ЕстьПолныеПрава;
	
	ЭтоИсходящее = (Объект.Направление = Перечисления.НаправленияСообщенийМЭДО.Исходящее);
	ЭтоВходящее = (Объект.Направление = Перечисления.НаправленияСообщенийМЭДО.Входящее);
	Элементы.Отправлено.Видимость = ЭтоИсходящее;
	Элементы.ГотовоКОтправке.Видимость = ЭтоИсходящее;
	Элементы.ОтправитьПоМЭДО.Видимость = ЭтоИсходящее;
	
	ТолькоПросмотр = ЭтоВходящее И Не ЕстьПолныеПрава; // Входящее нельзя редактировать полностью.
	
	// Поля "Подробно" только во входящем:
	Элементы.ПостановкаНаКонтроль_КонтрольныеПунктыДокументаОтветственныйПредставление.Видимость = Не ЭтоИсходящее;
	Элементы.РуководительСекретарьПредставление.Видимость = ЭтоВходящее;
	Элементы.РуководительПодразделенияПредставление.Видимость = ЭтоВходящее;
	Элементы.ИсполнительПредставление.Видимость = ЭтоВходящее;
	Элементы.ОтветственныйПредставлениеПодготовкаДоклада.Видимость = ЭтоВходящее;
	Элементы.ОтветственныйПредставлениеНаправлениеДоклада.Видимость = ЭтоВходящее;
	Элементы.ОтветственныйПредставлениеХодИсполнения.Видимость = ЭтоВходящее;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЗакладок()
	
	Элементы.ГруппаПостановкаНаКонтроль.Видимость = 
		(Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОПостановкеНаКонтроль);
	Элементы.ГруппаРегистрация.Видимость =
		(Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОРегистрации);
	Элементы.ГруппаОтказВРегистрации.Видимость =
		(Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОбОтказеВРегистрации);
	Элементы.ГруппаПринятиеКИсполнению.Видимость =
		(Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОПринятииКИсполнению);
	Элементы.ГруппаПодготовкаДоклада.Видимость =
		(Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОПодготовкеДоклада);
	Элементы.ГруппаНаправлениеДоклада.Видимость =
		(Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОНаправленииДоклада);
	Элементы.ГруппаХодИсполнения.Видимость =
		(Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОХодеИсполнения);
	Элементы.ГруппаОпубликование.Видимость =
		(Объект.ТипУведомления = Перечисления.ТипыУведомленийМЭДО.ОбОпубликовании);
	
КонецПроцедуры

// Текстовый документ с данными сообщения.
// 
// Параметры:
//  Уведомление - ДокументСсылка.УведомлениеМЭДО - Уведомление
// 
// Возвращаемое значение:
//  ТекстовыйДокумент
&НаСервереБезКонтекста
Функция ТекстовыйДокументСДаннымиСообщения(Уведомление)
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	Хранилище = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Уведомление, "ДанныеСообщения");
	Если Хранилище = Неопределено Тогда
		Возврат ТекстовыйДокумент;
	КонецЕсли;
	
	ДвоичныеДанные = Хранилище.Получить(); // ДвоичныеДанные, Неопределено
	Если ДвоичныеДанные = Неопределено Тогда
		Возврат ТекстовыйДокумент;
	КонецЕсли;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	ТекстовыйДокумент.Прочитать(ИмяВременногоФайла, "UTF-8");
	УдалитьФайлы(ИмяВременногоФайла);
	Возврат ТекстовыйДокумент;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстовыйДокументСДаннымиКонверта(Уведомление)
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	Хранилище = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Уведомление, "ДанныеКонверта");
	Если Хранилище = Неопределено Тогда
		Возврат ТекстовыйДокумент;
	КонецЕсли;
	
	ДвоичныеДанные = Хранилище.Получить(); // ДвоичныеДанные, Неопределено
	Если ДвоичныеДанные = Неопределено Тогда
		Возврат ТекстовыйДокумент;
	КонецЕсли;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("ini");
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	ТекстовыйДокумент.Прочитать(ИмяВременногоФайла, "windows-1251");
	УдалитьФайлы(ИмяВременногоФайла);
	Возврат ТекстовыйДокумент;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервереРедакцииКонфигурации()
	
	Элементы.Организация.Заголовок = МЭДОПереопределяемый.Организация();
	
КонецПроцедуры

#Область ТЧ_ПринятиеКИсполнению

// Ввод комментария руководителя секретаря завершение.
// 
// Параметры:
//  Результат - Неопределено,Строка
//  ДополнительныеПараметры - Структура
&НаКлиенте
Процедура ВводКомментарияРуководителяСекретаряЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Строка") Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ПринятиеКИсполнению[0].КомментарийРуководителяСекретаря = Результат;
	Модифицированность = Истина;
	
КонецПроцедуры

// Ввод комментария руководителя подразделения завершение.
// 
// Параметры:
//  Результат - Неопределено,Строка
//  ДополнительныеПараметры - Структура
&НаКлиенте
Процедура ВводКомментарияРуководителяПодразделенияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Строка") Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ПринятиеКИсполнению[0].КомментарийРуководителяПодразделения = Результат;
	Модифицированность = Истина;
	
КонецПроцедуры

// Ввод комментария исполнителя завершение.
// 
// Параметры:
//  Результат - Неопределено,Строка
//  ДополнительныеПараметры - Структура
&НаКлиенте
Процедура ВводКомментарияИсполнителяЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Строка") Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ПринятиеКИсполнению[0].КомментарийИсполнителя = Результат;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЮрлицоИсполнительПриИзмененииНаСервере()
	
	Если ТипЗнч(Объект.ПринятиеКИсполнению[0].Юрлицо) = МЭДОПереопределяемый.ТипКонтрагенты() Тогда
		ПустойТип = МЭДОПереопределяемый.КонтактноеЛицоПустаяСсылка();
	Иначе
		ПустойТип = МЭДОПереопределяемый.СотрудникПустаяСсылка();
	КонецЕсли;
	
	Объект.ПринятиеКИсполнению[0].РуководительСекретарь = ПустойТип;
	Объект.ПринятиеКИсполнению[0].РуководительПодразделения = ПустойТип;
	Объект.ПринятиеКИсполнению[0].Исполнитель = ПустойТип;
	
КонецПроцедуры

#КонецОбласти

&НаСервереБезКонтекста
Функция ВерсияМЭДОКонтрагента(Контрагент)
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Перечисления.ВерсииФорматаМЭДО.ПоследняяВерсия();
	КонецЕсли;
	
	Настройки = РегистрыСведений.НастройкиКонтрагентовМЭДО.НастройкиКонтрагента(
		Контрагент, МЭДОСтруктурыДанных.НовыйЛегкийОтвет());
	Возврат МЭДО.ЗаполненноеЗначение(
		Настройки.ВерсияФорматаМЭДО, Перечисления.ВерсииФорматаМЭДО.ПоследняяВерсия());
	
КонецФункции

#КонецОбласти