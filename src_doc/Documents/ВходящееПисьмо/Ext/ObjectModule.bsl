#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовоеПисьмо", Ссылка.Пустая());
	
	Если ДополнительныеСвойства.ЭтоНовоеПисьмо Тогда
		НомерСпособаАдресации = ВстроеннаяПочтаСервер.ПолучитьСпособАдресацииОбъекта(ЭтотОбъект);
	КонецЕсли;		
	
	ПредыдущаяПометкаУдаления = Ложь;
	Если Не Ссылка.Пустая() Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ПометкаУдаления, Папка");
		ПредыдущаяПометкаУдаления = ЗначенияРеквизитов.ПометкаУдаления;
		ДополнительныеСвойства.Вставить("ПредыдущаяПапка", ЗначенияРеквизитов.Папка);
		ДополнительныеСвойства.Вставить("ПредыдущаяПометкаУдаления", ЗначенияРеквизитов.ПометкаУдаления);
		
		Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда 
			РаботаСФайламиВызовСервера.ПометитьНаУдалениеПриложенныеФайлы(Ссылка, ПометкаУдаления);
		КонецЕсли;
		
		Если Папка <> ЗначенияРеквизитов.Папка
			И Не Пользователи.ЭтоПолноправныйПользователь() Тогда
			ДополнительныеСвойства.Вставить("ПроверитьПравоУдаленияИзПапки",
				ЗначениеЗаполнено(ЗначенияРеквизитов.Папка));
			ДополнительныеСвойства.Вставить("ПроверитьПравоДобавленияВПапку",
				ЗначениеЗаполнено(Папка));
		КонецЕсли;
		
	КонецЕсли;
	ДополнительныеСвойства.Вставить("ПредыдущаяПометкаУдаления", ПредыдущаяПометкаУдаления);
	
	ВстроеннаяПочтаСервер.ПрименитьПравила(ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Папка) Тогда
		Отказ = Истина;
		ВызватьИсключение НСтр("ru = 'Не указана папка письма'");
	КонецЕсли;    
	
	ОтправительСтрока = Строка(ОтправительАдресат);
	Если Не ЗначениеЗаполнено(ОтправительСтрока) Тогда
		ОтправительСтрока = "<>";
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	ПредыдущаяПапка = Неопределено;
	ДополнительныеСвойства.Свойство("ПредыдущаяПапка", ПредыдущаяПапка);
	
	ПредыдущаяПометкаУдаления = Ложь;
	ДополнительныеСвойства.Свойство("ПредыдущаяПометкаУдаления", ПредыдущаяПометкаУдаления);
	
	ПредыдущаяПометкаУдаления = Ложь;
	Если ДополнительныеСвойства.Свойство("ПредыдущаяПометкаУдаления") Тогда
		ПредыдущаяПометкаУдаления = ДополнительныеСвойства.ПредыдущаяПометкаУдаления;
	КонецЕсли;
	
	Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда
		ПротоколированиеРаботыСотрудников.ЗаписатьПометкуУдаления(Ссылка, ПометкаУдаления);
	КонецЕсли;	
	
	// Расширение РГ предмета.
	Если РаботаСРабочимиГруппами.ПоОбъектуВедетсяАвтоматическоеЗаполнениеРабочейГруппы(Предмет) Тогда
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	СведенияОбАдресатах.Контакт КАК Контакт
			|ИЗ
			|	РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
			|ГДЕ
			|	СведенияОбАдресатах.АдресатСообщения В(&ВсеАдресаты)
			|	И СведенияОбАдресатах.Контакт ССЫЛКА Справочник.Сотрудники");
		ВсеАдресаты = ПолучателиПисьма.ВыгрузитьКолонку("Адресат");
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеАдресаты,
			ПолучателиКопий.ВыгрузитьКолонку("Адресат"), Истина);
		Запрос.УстановитьПараметр("ВсеАдресаты", ВсеАдресаты);
		Выборка = Запрос.Выполнить().Выбрать();
		ТаблицаНабора = РаботаСРабочимиГруппами.ПолучитьРабочуюГруппуДокумента(Предмет);
		КолСтрокДоДобавления = ТаблицаНабора.Количество();
		Пока Выборка.Следующий() Цикл
			Сотрудник = Выборка.Контакт;
			РаботаСРабочимиГруппами.ДобавитьУчастникаВТаблицуНабора(
				ТаблицаНабора, Сотрудник, Ложь);
		КонецЦикла;
		Если ТаблицаНабора.Количество() > КолСтрокДоДобавления Тогда
			РаботаСРабочимиГруппами.ПерезаписатьРабочуюГруппуОбъекта(Предмет, ТаблицаНабора, Истина);
		КонецЕсли;
	КонецЕсли;      
	
	НеЗаписыватьОперациюСПисьмом = Ложь;
	Если ДополнительныеСвойства.Свойство("НеЗаписыватьОперациюСПисьмом") Тогда
		НеЗаписыватьОперациюСПисьмом = ДополнительныеСвойства.НеЗаписыватьОперациюСПисьмом;
	КонецЕсли;
	Если НеЗаписыватьОперациюСПисьмом = Ложь Тогда
		
		Если ДополнительныеСвойства.ЭтоНовоеПисьмо Тогда
			
			Если ЗначениеЗаполнено(Папка) Тогда
				
				РегистрыСведений.КоличествоПисемВПапкахИтоги.ДобавитьИзменение(
					Ссылка, Истина, Папка, , ПометкаУдаления);
					
			КонецЕсли;
			
		Иначе
			
			Если (Папка <> ПредыдущаяПапка И ЗначениеЗаполнено(Папка))
				Или (ПометкаУдаления <> ПредыдущаяПометкаУдаления) Тогда
				
				РегистрыСведений.КоличествоПисемВПапкахИтоги.ДобавитьИзменение(
					Ссылка, Ложь, Папка, ПредыдущаяПапка, ПометкаУдаления, ПредыдущаяПометкаУдаления);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;	 
	
	Если ПараметрыСеанса.ЗагрузкаОбработанныхДанныхИзДругойСистемы Тогда          
		
		// добавление, и еще удаление   
		ЭтоИзменения = Истина;              
		Если ДополнительныеСвойства.Свойство("ЭтоИзменения") Тогда
			ЭтоИзменения = ДополнительныеСвойства.ЭтоИзменения;
		КонецЕсли;	
		
		Если ЭтоИзменения = Ложь Тогда // исторические
			РегистрыСведений.ОчередьПостОбработкиЗагрузки.ДобавитьВОчередьОбновленияКоличестваПисем(Папка);
		КонецЕсли;		
	
	КонецЕсли;	 
	
КонецПроцедуры

// Возвращает структуру "ТипТекста, Кодировка, Текст"
//
Функция ПолучитьСодержаниеПисьма() Экспорт
	
	Результат = Новый Структура("Текст, ТипТекста, Кодировка");
	
	Если ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		
		Результат.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML;
		
		ТекстПисьма = ТекстПисьмаHTMLХранилище.Получить();
		
		Если ТекстПисьма = Неопределено Тогда
			ТекстПисьма = "";
		КонецЕсли;
		
		Результат.Текст = ТекстПисьма;
		
	ИначеЕсли ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст
		Или Не ЗначениеЗаполнено(ТипТекста) Тогда
		
		Результат.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст;
		
		ТекстПисьма = ТекстПисьмаПростойТекстХранилище.Получить();
		
		Если ТекстПисьма = Неопределено Тогда
			ТекстПисьма = "";
		КонецЕсли;
		
		Результат.Текст = ТекстПисьма;
		
	КонецЕсли;
	
	Результат.Кодировка = Кодировка;
	
	Возврат Результат;
	
КонецФункции

// Устанавливает текст, тип текста и кодировку письма.
//
Процедура УстановитьСодержаниеПисьма(
	Знач ТекстПисьма,
	Знач ТипТекстаПисьма = Неопределено,
	Знач КодировкаПисьма = Неопределено) Экспорт
	
	Если Не ЭтоНовый() И ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст Тогда
		РегистрыСведений.HTMLПредставленияСодержанияПисем.Удалить(Ссылка);
	КонецЕсли;
	
	ТипТекста = ?(ЗначениеЗаполнено(ТипТекстаПисьма),
		ТипТекстаПисьма,
		Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст);
	
	Кодировка = ?(ЗначениеЗаполнено(КодировкаПисьма),
		КодировкаПисьма,
		"utf-8");
	
	Почта.ИсправитьПереводыСтрок(ТекстПисьма);
	
	Если ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		
		ТекстПисьмаHTMLХранилище = Новый ХранилищеЗначения(ТекстПисьма, Новый СжатиеДанных);
		
		ПростойТекст = РаботаС_HTML.ПолучитьТекстИзHTML(ТекстПисьма);
		ТекстПисьмаПростойТекстХранилище = Новый ХранилищеЗначения(ПростойТекст, Новый СжатиеДанных);
		
	Иначе
		ТекстПисьмаПростойТекстХранилище = Новый ХранилищеЗначения(ТекстПисьма, Новый СжатиеДанных);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает текст письма в виде простого текста.
Функция ПолучитьТекстовоеПредставлениеСодержанияПисьма() Экспорт
	
	Результат = "";
	
	Если ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		
		ТекстHTML = ТекстПисьмаHTMLХранилище.Получить();
		
		Если ТекстHTML = Неопределено Тогда
			
			Результат = "";
			
		Иначе
			
			Результат = РаботаС_HTML.ПолучитьТекстИзHTML(ТекстHTML, Кодировка);
			
		КонецЕсли;
		
	Иначе
		
		Результат = ТекстПисьмаПростойТекстХранилище.Получить();
		
		Если Результат = Неопределено Тогда
			
			Результат = "";
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ВстроеннаяПочтаСервер.ПередУдалениемПисьма(ЭтотОбъект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли