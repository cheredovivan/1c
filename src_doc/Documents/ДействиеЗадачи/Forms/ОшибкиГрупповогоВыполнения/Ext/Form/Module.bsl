#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗадачиСОшибками.Очистить();
	
	Для Каждого Стр Из Параметры.ЗадачиНельзяВыполнить Цикл
		
		НовСтр = ЗадачиСОшибками.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Стр);
		
		НовСтр = ВсеЗадачи.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Стр);
		НовСтр.ЭтоОшибка = Истина;
		
	КонецЦикла;
	
	Для Каждого ДействиеЗадачи Из Параметры.ДействияЗадач Цикл
		
		ПараметрыОтбора = Новый Структура("Задача", ДействиеЗадачи);
		Строки = ЗадачиСОшибками.НайтиСтроки(ПараметрыОтбора);
		Если Строки.Количество() = 0 Тогда
			
			ДействияЗадачМожноВыполнить.Добавить(ДействиеЗадачи);
			
			НовСтр = ВсеЗадачи.Добавить();
			НовСтр.Задача = ДействиеЗадачи;
			
		КонецЕсли;	
		
	КонецЦикла;	
	
	РезультатДействия = Параметры.РезультатДействия;
	
	Элементы.ФормаВыполнитьЧтоМожно.Видимость = (ДействияЗадачМожноВыполнить.Количество() <> 0); 
	
	Элементы.ДекорацияОписание.Заголовок = 
		 СтрШаблон(НСтр("ru = 'Отменено выполнение всех задач (%1). Задач с ошибками: %2.'"), 
		 ВсеЗадачи.Количество(), ЗадачиСОшибками.Количество());
	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВсеЗадачи

&НаКлиенте
Процедура ВсеЗадачиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрДанные = ВсеЗадачи.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если СтрДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Данные = СтрДанные.Задача;
	ПоказатьЗначение(, Данные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьЧтоМожно(Команда)
	
	МассивДействий = ДействияЗадачМожноВыполнить.ВыгрузитьЗначения();
	
	РаботаСЗадачамиКлиент.ВыполнитьДействиеЗадачи(
		МассивДействий, РезультатДействия, ЭтотОбъект);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗакрыть(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
