#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Делопроизводство.СписокДокументовУсловноеОформлениеПомеченныхНаУдаление(Список);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ОбщегоНазначенияДокументооборотКлиентСервер.УстановитьОтборПоказыватьУдаленные(
		Список, ПоказыватьУдаленные, Элементы.ПоказыватьУдаленные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	ОбщегоНазначенияДокументооборотКлиентСервер.УстановитьОтборПоказыватьУдаленные(
		Список, ПоказыватьУдаленные, Элементы.ПоказыватьУдаленные);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура СохранитьНаДиск(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументВыгрузка = ТекущиеДанные.Ссылка;
	
	ПоляВыгрузки = ПоляДокумента_Сервер(ДокументВыгрузка);
	
	Если РаботаСОбращениямиВызовСервера.ЕстьОшибкиВВыгрузкеВЦелом(
		ПоляВыгрузки.ДокументыОбращения, ПоляВыгрузки.Организация) Тогда
		Возврат;
	КонецЕсли;
	ПроблемныеОбъекты = РаботаСОбращениямиВызовСервера.ОшибкиВОбращениях(ПоляВыгрузки.ДокументыОбращения);
	Если ПроблемныеОбъекты.Количество() > 0 Тогда
		РаботаСОбращениямиКлиент.ОткрытьПроблемныеОбъекты(ПроблемныеОбъекты);
		Возврат;
	КонецЕсли;
	
	ПолноеИмяФайла = Ждать РаботаСОбращениямиКлиент.ФайлВыбранныйВДиалоге_Асинх(
		РежимДиалогаВыбораФайла.Сохранение,
		РаботаСОбращениямиКлиент.НачальноеИмяВыгрузки(ПоляВыгрузки.Номер, ПоляВыгрузки.Дата),
		РаботаСОбращениямиКлиент.ФильтрZip());
	Если Не ЗначениеЗаполнено(ПолноеИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	Ответ = РаботаСОбращениямиВызовСервера.СоздатьДвоичныеДанныеВыгрузки(
		ПоляВыгрузки.ДокументыОбращения,
		ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор),
		ДокументВыгрузка);
	Если Ответ.ПроблемныеОбъекты.Количество() > 0 Тогда
		РаботаСОбращениямиКлиент.ОткрытьПроблемныеОбъекты(Ответ.ПроблемныеОбъекты);
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(Ответ.АдресВХранилище);
	Если ТипЗнч(ДвоичныеДанныеФайла) = Тип("ДвоичныеДанные") Тогда
		Ждать ДвоичныеДанныеФайла.ЗаписатьАсинх(ПолноеИмяФайла);
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Файл выгрузки записан на диск'"),
			,
			ПолноеИмяФайла,
			БиблиотекаКартинок.Информация32);
	Иначе
		// Перестраховка, в случае ошибки должно быть конкретное сообщение, выше:
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Неожиданная ошибка, выгрузка не вернула ""ДвоичныеДанные""'") , ДокументВыгрузка);
		Возврат;
	КонецЕсли;
	
	Если Не ПоляВыгрузки.Выгружена Тогда
		ЗаписатьФлагВыгруженаССТУ_Сервер(ДокументВыгрузка, Истина);
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Установлен признак ""Выгружена""'"),
			ПолучитьНавигационнуюСсылку(ДокументВыгрузка),
			"" + ДокументВыгрузка,
			БиблиотекаКартинок.Информация32);
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПоляДокумента_Сервер(ДокументВыгрузка)
	
	Поля = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ДокументВыгрузка, "Обращения, Организация, Выгружена, Номер, Дата");
	ДокументыОбращения = Поля.Обращения.Выгрузить().ВыгрузитьКолонку("Обращение");
	Поля.Удалить("Обращения");
	Поля.Вставить("ДокументыОбращения", ДокументыОбращения);
	Возврат Поля;
	
КонецФункции

// Записать флаг "Выгружена" в документ "Выгрузка в ССТУ".
// 
// Параметры:
//  ДокументВыгрузкаССТУ - ДокументСсылка.ВыгрузкаВССТУ -
//  Выгружена - Булево - Значение флага, которое надо записать
&НаСервереБезКонтекста
Процедура ЗаписатьФлагВыгруженаССТУ_Сервер(Знач ДокументВыгрузкаССТУ, Знач Выгружена)

	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить("Документ.ВыгрузкаВССТУ");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ДокументВыгрузкаССТУ);
		Блокировка.Заблокировать();
		
		ДокументОбъект = ДокументВыгрузкаССТУ.ПолучитьОбъект();
		Если ДокументОбъект.Выгружена <> Выгружена Тогда
			ДокументОбъект.Выгружена = Выгружена;
			ДокументОбъект.Записать();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Не удалось установить признак ""%1"" в ""%2""'"),
			Метаданные.Документы.ВыгрузкаВССТУ.Реквизиты.Выгружена.Синоним,
			ДокументВыгрузкаССТУ);
	КонецПопытки;

КонецПроцедуры

#КонецОбласти