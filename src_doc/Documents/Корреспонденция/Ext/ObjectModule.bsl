
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

#Область ОбновлениеКэширующихДанных

// Обрабатывает изменение влияющих данных, формирует очередь обновления кэширующих данных.
//
Процедура ОбработатьИзменениеВлияющихДанных() Экспорт
	
	ВлияющийОбъектМетаданных = Метаданные.Документы.Корреспонденция.ПолноеИмя();
	КлючВлияющихДанных = Ссылка;
	
	ЗависимыйОбъектМетаданных = Метаданные.Документы.ДанныеМоегоДокумента.ПолноеИмя();
	ВлияющиеРеквизиты = Документы.ДанныеМоегоДокумента.ВлияющиеРеквизитыКорреспонденции();
	ОбновлениеКэширующихДанных.ОбработатьИзменениеВлияющихДанных(
		ЭтотОбъект,
		ЗависимыйОбъектМетаданных,
		ВлияющийОбъектМетаданных,
		КлючВлияющихДанных,
		ВлияющиеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияДокументооборот.УстановитьДополнительноеСвойствоПредыдущиеЗначенияРеквизитов(ЭтотОбъект);
	
	ЗначенияПоРеквизитам = Новый Соответствие();
	АдресатыДляСписков = ОбщегоНазначенияДокументооборот.СтрСоединитьОграниченноеКолво(
		Корреспонденты.ВыгрузитьКолонку("Адресат"), 250);
	ЗначенияПоРеквизитам.Вставить("АдресатыДляСписков", АдресатыДляСписков);
	ЗначенияПоРеквизитам.Вставить("ВидОбращения", ВидОбращения);
	ЗначенияПоРеквизитам.Вставить("ИсходящаяДата", ДатаКонтрагента);
	ЗначенияПоРеквизитам.Вставить("ИсходящийНомер", НомерКонтрагента);
	ЗначенияПоРеквизитам.Вставить("Переадресовавший", Переадресовавший);
	ЗначенияПоРеквизитам.Вставить("СпособПолучения", СпособПолучения);
	ЗначенияПоРеквизитам.Вставить("Дубликат", ЭтоДубликатОбращения);
	ЗначенияПоРеквизитам.Вставить("Повторное", ЭтоПовторноеОбращение);
	Если ВидКорреспонденции = Перечисления.ВидыКорреспонденции.Исходящая
		И ДополнительныеСвойства.Свойство("ВидДокументаКэш")
		И ДополнительныеСвойства.ВидДокументаКэш.УчитыватьВходящийНомерИДатуПолучателя Тогда
		
		ИсходящийНомерИДата = РаботаСКорреспонденцией.ИсходящийНомерИДатаКорреспонденции(Корреспонденты);
		Если ЗначениеЗаполнено(ИсходящийНомерИДата) Тогда
			ЗначенияПоРеквизитам.Вставить("ИсходящийНомерИДата", ИсходящийНомерИДата);
		КонецЕсли;
		
	ИначеЕсли ВидКорреспонденции = Перечисления.ВидыКорреспонденции.Входящая Тогда
		
		Если ЗначениеЗаполнено(НомерКонтрагента)
			И ЗначениеЗаполнено(ДатаКонтрагента) Тогда
			ИсходящийНомерИДата = СтрШаблон(
				НСтр("ru = '%1 от %2'"), СокрЛП(НомерКонтрагента), Формат(ДатаКонтрагента, "ДЛФ=D"));
			ЗначенияПоРеквизитам.Вставить("ИсходящийНомерИДата", ИсходящийНомерИДата);
		КонецЕсли;
			
	КонецЕсли;
	Делопроизводство.ЗаписатьДанныеДокумента(Основание, ЗначенияПоРеквизитам);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьИзменениеВлияющихДанных();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДатаСоздания = ТекущаяДатаСеанса();
	Создал = Сотрудники.ОсновнойСотрудникПользователя();
	
	НомерКонтрагента = "";
	ДатаКонтрагента = Неопределено;
	
	Для Каждого Строка Из Корреспонденты Цикл
		Строка.Отправлен 	  = Ложь;
		Строка.ДатаОтправки	  = '00010101';
		Строка.СпособОтправки = Справочники.СпособыДоставки.ПустаяСсылка();
		Строка.НомерКонтрагента  = "";
		Строка.ДатаКонтрагента   = '00010101';
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		
		ДатаСоздания = ТекущаяДатаСеанса();
		Создал = Сотрудники.ОсновнойСотрудникПользователя();
		Дата = ДатаСоздания;
		
		НомерКонтрагента = "";
		ДатаКонтрагента = '00010101';
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Корреспонденты.Количество() = 0 
	И Не РаботаСКорреспонденциейЛокализация.ПропуститьПроверкуЗаполненияКонтрагентов(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты) Тогда
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'В корреспонденции по документу ""%1"" не указан контрагент!'"),
			Основание);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Основание, "Контрагент",, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли