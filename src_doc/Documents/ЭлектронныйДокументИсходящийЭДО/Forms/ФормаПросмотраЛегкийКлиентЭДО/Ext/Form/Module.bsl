
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект);
		
		ЗаполнитьДанныеНовогоДокумента();
		
		ОбновитьДанныеДляПросмотраНовогоДокумента();
		
	КонецЕсли;
	
	ЕстьПравоАдминистрирования = ОбщегоНазначенияБЭД.ЕстьПравоАдминистрирования();
	
	Если ЗначениеЗаполнено(Объект.Ссылка) 
		И РаботаСПрочтениямиВызовСервера.УстановитьСвойствоПрочтен(Объект.Ссылка, Истина) Тогда
		ОповеститьОПрочтении = Истина;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаПанельКоманд;
	ПараметрыРазмещения.Источники = Новый Массив;
	Для Каждого ДанныеОбъектаУчета Из ОбъектыУчетаДокумента Цикл
		ПараметрыРазмещения.Источники.Добавить(ДанныеОбъектаУчета.ОбъектУчета.Метаданные());
	КонецЦикла;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды 
	
	ОбменСКонтрагентамиПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Параметры, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект);

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	ТипДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидДокумента, "ТипДокумента");
	
	ТекущийДокументПакета = Объект.Ссылка;
	
	ТекущийИдентификаторДокумента = Объект.Ссылка.УникальныйИдентификатор();

	ОбновитьДанныеДляПросмотраДокумента();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОбменСКонтрагентамиПереопределяемый.ПриЧтенииНаСервереФормыПросмотраЭлектронногоДокумента(ЭтотОбъект, ТекущийОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЛегкийИнтерфейсДокументовЭДОКлиент.НастроитьБлокировкуПанелиКомандКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;
	
	ОбработатьРезультатПодготовкиДанныхДляПросмотраПоФайлуXML();

	ОжидатьПолученияАсинхронныхДанных();

	Если ОповеститьОПрочтении Тогда
		Оповестить("ПрочтениеОбновитьСписок", Объект.Ссылка);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияСостоянияЭДО()
		И ЗначениеЗаполнено(Объект.Ссылка)
		И Параметр <> Неопределено Тогда
		
		ОбработатьОповещение = Истина;
		
		ДанныеПроверки = ИнтерфейсДокументовЭДОКлиент.НовыеДанныеПроверкиОповещения();
		ДанныеПроверки.ЭлектронныйДокумент = Объект.Ссылка;
		
		ИнтерфейсДокументовЭДОКлиент.ПриОбработкеОповещенияФормыПросмотраЭД(ДанныеПроверки, Параметр, 
			ОбработатьОповещение);
		
		Если ОбработатьОповещение Тогда
			ОбновитьДанныеПослеВыполненияДействийПоЭДО();
		КонецЕсли;
		
		Если Не ОбработатьОповещение
			И ЛегкийИнтерфейсДокументовЭДОКлиент.ТребуетсяОбновлениеСвязанныхДокументовПриИзмененииСостоянияЭДО(
			ЭтотОбъект, Параметр) Тогда
			ОбновитьДанныеПослеИзмененияСвязанныхДокументов();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияСостоянияЭДО()
		И ЛегкийИнтерфейсДокументовЭДОКлиент.ТребуетсяОбновлениеСвязанныхДокументовПриИзмененииСостоянияЭДО(
			ЭтотОбъект, Параметр) Тогда
		
		ОбновитьДанныеПослеИзмененияСвязанныхДокументов();
		
	ИначеЕсли ИмяСобытия = "ЗакрытьФормуДлительнойОперации" Тогда
			
		ЛегкийИнтерфейсДокументовЭДОКлиент.НастроитьБлокировкуПанелиКомандКарточкиДокумента(ЭтотОбъект, Ложь);
		
	ИначеЕсли ИмяСобытия = МашиночитаемыеДоверенностиКлиент.ИмяСобытияИсправлениеОшибкиПроверкиПолномочий() Тогда
		
		Для Каждого ИсправленнаяОшибка Из Параметр Цикл
			Если ИсправленнаяОшибка.СсылкаНаОбъект = Объект.Ссылка Тогда
				ОбновитьДанныеПослеИзмененияПодписейДокумента();
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ИмяСобытия = ПакетыДокументовЭДОКлиент.ИмяСобытияДобавленияДокументаВПакет() Тогда
		
		ОбновитьДанныеПослеДобавленияДокументаВПакет(Источник, Параметр);

	ИначеЕсли ИмяСобытия = ПакетыДокументовЭДОКлиент.ИмяСобытияУдалениеДокументаИзПакета() 
		И Источник = ИдентификаторПакета Тогда
		
		ОбновитьДанныеПослеУдаленияДокументаИзПакета();
		
	ИначеЕсли ИмяСобытия = ЛегкийИнтерфейсДокументовЭДОКлиент.ИмяСобытияИзменениеОбщегоСостоянияДокумента()
		И (Параметр = Объект.Ссылка Или Параметр = ЭтотОбъект) Тогда
			
		ОбновитьДанныеПослеИзмененияОбщегоСостояния();
	
	ИначеЕсли ИмяСобытия = ЛегкийИнтерфейсДокументовЭДОКлиент.ИмяСобытияИзменениеСвязанныхДокументов()
		И ЛегкийИнтерфейсДокументовЭДОКлиент.ТребуетсяОбновлениеСвязанныхДокументовПриИзмененииСвязанныхДокументов(
			ЭтотОбъект, Параметр) Тогда
		
		ОбновитьДанныеПослеИзмененияСвязанныхДокументов();
		
	КонецЕсли;
	
	ОбменСКонтрагентамиКлиентПереопределяемый.ОбработкаОповещения(
		ЭтотОбъект, ИмяСобытия, Параметр, Источник);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ПередЗаписьюНаСервереПоИнтеграциям(Отказ, ТекущийОбъект, ПараметрыЗаписи);
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ПриЗаписиНаСервереПоИнтеграциям(Отказ, ТекущийОбъект, ПараметрыЗаписи);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПослеЗаписиНаСервереПоИнтеграциям(ТекущийОбъект, ПараметрыЗаписи);
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ОбменСКонтрагентамиПереопределяемый.ПослеЗаписиНаСервереФормыПросмотраЭлектронногоДокумента(
		ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	ОбменСКонтрагентамиКлиентПереопределяемый.ОбработкаНавигационнойСсылкиФормыПросмотраЭлектронногоДокумента(
		ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрыСобытия = ИнтерфейсДокументовЭДОКлиент.НовыеПараметрыСобытияОбновленияСостоянияЭДО();
	ПараметрыСобытия.ЭлектронныеДокументы.Добавить(Объект.Ссылка);
	Оповестить(ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияСостоянияЭДО());
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НадписьОшибкаФормированияПредставленияОбработкаНавигационнойСсылки(Элемент,
	НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьЖурналРегистрации" Тогда
		ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьЖурналРегистрацииПоОшибкамПредставленияИзКарточкиДокумента(ЭтотОбъект,
			СтандартнаяОбработка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗаполнитьДанныеДокументаПриИзмененииКлючевыхРеквизитов();
	Модифицированность = Истина;
	ОжидатьПолученияАсинхронныхДанных();

	ЛегкийИнтерфейсДокументовЭДОКлиент.ОбновитьКлючевыеРеквизитыВДокументахПредварительногоПакетаКарточкиДокумента(
		ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.КэшироватьЗначенияРеквизитовВПредварительномПакетеКарточкиДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, 
	СтандартнаяОбработка)
	ОчиститьДоговорПослеВыбораОрганизации(ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ЗаполнитьДанныеДокументаПриИзмененииКлючевыхРеквизитов();
	Модифицированность = Истина;
	ОжидатьПолученияАсинхронныхДанных();
	
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОбновитьКлючевыеРеквизитыВДокументахПредварительногоПакетаКарточкиДокумента(
		ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.КэшироватьЗначенияРеквизитовВПредварительномПакетеКарточкиДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СопроводительнаяЗапискаТекстПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ИзмененаСопроводительнаяЗаписка = Истина;
	ЛегкийИнтерфейсДокументовЭДОКлиент.КэшироватьЗначенияРеквизитовВПредварительномПакетеКарточкиДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("Контрагент", Объект.Контрагент);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеВыбораДоговораКонтрагента", ЭтотОбъект);
		
	ИнтеграцияЭДОКлиент.ОткрытьФормуВыбораДоговора(ПараметрыФормы, Элемент, ОповещениеОЗавершении,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	
	ЗаполнитьДанныеДокументаПриИзмененииКлючевыхРеквизитов();
	Модифицированность = Истина;
	ОжидатьПолученияАсинхронныхДанных();
	
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОбновитьКлючевыеРеквизитыВДокументахПредварительногоПакетаКарточкиДокумента(
		ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.КэшироватьЗначенияРеквизитовВПредварительномПакетеКарточкиДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ИзмененВидДокумента = Истина;
	ИзмененМаршрутПодписания = Истина;
	ОкноОтправкиВФормализованномВидеПоказано = Ложь;
	
	ЛегкийИнтерфейсДокументовЭДОКлиент.КэшироватьЗначенияРеквизитовВПредварительномПакетеКарточкиДокумента(ЭтотОбъект);
	
	ВидДокументаПриИзмененииНаСервере();
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерДокументаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ЛегкийИнтерфейсДокументовЭДОКлиент.КэшироватьЗначенияРеквизитовВПредварительномПакетеКарточкиДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаДокументаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ЛегкийИнтерфейсДокументовЭДОКлиент.КэшироватьЗначенияРеквизитовВПредварительномПакетеКарточкиДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДокументаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ЛегкийИнтерфейсДокументовЭДОКлиент.КэшироватьЗначенияРеквизитовВПредварительномПакетеКарточкиДокумента(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовШапкиФормы

#Область ОбработчикиСобытийЭлементовТаблицыФормыСоставПакета

&НаКлиенте
Процедура СоставПакетаПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("СоставПакетаПослеАктивизацииСтроки", 0.1, Истина);
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыСоставПакета

#Область ОбработчикиСобытийЭлементовТаблицыФормыПодписи

&НаКлиенте
Процедура ПодписиПриАктивизацииСтроки(Элемент)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОбработатьАктивациюСтрокиВТаблицеПодписейКарточкиДокумента(ЭтотОбъект, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПодписиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗакрытияРезультатаПроверкиПодписи", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОбработатьВыборВТаблицеПодписейИзКарточкиДокумента(ЭтотОбъект, Элемент, Поле, 
		СтандартнаяОбработка, ОповещениеОЗавершении);
		
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыПодписи

#Область ОбработчикиКомандФормы

#Область ОбработчикиКомандФормыОсновные

#Область ВыполнениеДействийПоЭДО

&НаКлиенте
Процедура Утвердить(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("УтвердитьПослеЗаписи", ЭтотОбъект, Истина);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеУтвердитьИзКарточкиДокумента(ЭтотОбъект,
			ЭтоПакетДокументов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьОтправить(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ПодписатьОтправитьПослеЗаписи", ЭтотОбъект, Истина);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействияПодписатьОтправитьИзКарточкиДокумента(ЭтотОбъект, 
			ЭтоПакетДокументов);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подписать(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ПодписатьПослеЗаписи", ЭтотОбъект, Истина);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеПодписатьИзКарточкиДокумента(ЭтотОбъект,
			ЭтоПакетДокументов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отправить(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ОтправитьПослеЗаписи", ЭтотОбъект, Истина);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтправитьИзКарточкиДокумента(ЭтотОбъект,
			ЭтоПакетДокументов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отклонить(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ОтклонитьПослеЗаписи", ЭтотОбъект, Истина);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьИзКарточкиДокумента(ЭтотОбъект,
			ЭтоПакетДокументов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьПодписание(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ОтклонитьПодписаниеПослеЗаписи", ЭтотОбъект, Истина);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьПодписаниеИзКарточкиДокумента(ЭтотОбъект,
			ЭтоПакетДокументов);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПовторно(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ОтправитьПовторноПослеЗаписи", ЭтотОбъект, Истина);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтправитьПовторноИзКарточкиДокумента(ЭтотОбъект,
			ЭтоПакетДокументов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьАннулирование(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ПринятьАннулированиеПослеЗаписи", ЭтотОбъект, Истина);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеПринятьАннулированиеИзКарточкиДокумента(ЭтотОбъект, 
			ЭтоПакетДокументов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьАннулирование(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ОтклонитьАннулированиеПослеЗаписи", ЭтотОбъект, Истина);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьАннулированиеИзКарточкиДокумента(ЭтотОбъект, 
			ЭтоПакетДокументов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Аннулировать(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("АннулироватьПослеЗаписи", ЭтотОбъект, Истина);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеАннулироватьИзКарточкиДокумента(ЭтотОбъект,
			ЭтоПакетДокументов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Завершить(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ЗавершитьПослеЗаписи", ЭтотОбъект, Истина);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеЗакрытьПринудительноИзКарточкиДокумента(ЭтотОбъект,
			ЭтоПакетДокументов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВернутьВРаботу(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ВернутьВРаботуПослеЗаписи", ЭтотОбъект, Истина);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеВернутьВРаботуИзКарточкиДокумента(ЭтотОбъект, 
			ЭтоПакетДокументов);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ВыполнениеДействийПоЭДО

#Область ВыгрузкаИПечать

&НаКлиенте
Процедура НапечататьЭлектронныеДокументы(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("НапечататьЭлектронныеДокументыПослеЗаписи", ЭтотОбъект);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьФормуПечатиИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДокументооборотЦеликом(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ВыгрузитьДокументооборотЦеликомПослеЗаписи", ЭтотОбъект);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыгрузитьДокументооборотЦеликомИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЭлектронныеДокументыВФорматеPDF(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ВыгрузитьЭлектронныеДокументыВФорматеPDFПослеЗаписи", ЭтотОбъект);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыгрузитьДокументыВФорматеPDFИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЭлектронныеДокументыДляФНС(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ВыгрузитьЭлектронныеДокументыДляФНСПослеЗаписи", ЭтотОбъект);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыгрузитьДокументыДляФНСИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПротоколОбмена(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ПоказатьПротоколОбменаПослеЗаписи", ЭтотОбъект);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ИнтерфейсДокументовЭДОКлиент.ПоказатьПротоколОбмена(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ВыгрузкаИПечать

#Область ДействияСПакетом

&НаКлиенте
Процедура ДобавитьВПакетФайлСКомпьютера(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ДобавитьВПакетФайлСКомпьютераПослеЗаписи", ЭтотОбъект);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		СпособыДобавления = ПакетыДокументовЭДОКлиент.СпособыДобавленияДокументаВПакет();
		ПакетыДокументовЭДОКлиент.ПослеВыбораСпособаДобавленияДокументаВПакет(
			СпособыДобавления.ДобавитьВПакетФайлСКомпьютера, ПараметрыДобавленияВПакет());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВПакетПрисоединенныйФайл(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ДобавитьВПакетПрисоединенныйФайлПослеЗаписи", ЭтотОбъект);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		СпособыДобавления = ПакетыДокументовЭДОКлиент.СпособыДобавленияДокументаВПакет();
		ПакетыДокументовЭДОКлиент.ПослеВыбораСпособаДобавленияДокументаВПакет(
			СпособыДобавления.ДобавитьВПакетПрисоединенныйФайл, ПараметрыДобавленияВПакет());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВПакетДокументИнформационнойБазы(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ДобавитьВПакетДокументИнформационнойБазыПослеЗаписи", ЭтотОбъект);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		СпособыДобавления = ПакетыДокументовЭДОКлиент.СпособыДобавленияДокументаВПакет();
		ПакетыДокументовЭДОКлиент.ПослеВыбораСпособаДобавленияДокументаВПакет(
			СпособыДобавления.ДобавитьВПакетДокументИнформационнойБазы, ПараметрыДобавленияВПакет());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ДействияСПакетом

&НаКлиенте
Процедура Редактировать(Команда)
	ОткрытьОбъектУчетаЭлектронногоДокумента();
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормыОсновные

#Область ОбработчикиКомандФормыЕще

#Область ВыполнениеДействийПоЭДОСДокументом

&НаКлиенте
Процедура УтвердитьДокумент(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("УтвердитьПослеЗаписи", ЭтотОбъект, Ложь);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеУтвердитьИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьОтправитьДокумент(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ПодписатьОтправитьПослеЗаписи", ЭтотОбъект, Ложь);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействияПодписатьОтправитьИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьДокумент(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ПодписатьПослеЗаписи", ЭтотОбъект, Ложь);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеПодписатьИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьДокумент(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ОтправитьПослеЗаписи", ЭтотОбъект, Ложь);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтправитьИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьДокумент(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ОтклонитьПослеЗаписи", ЭтотОбъект, Ложь);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьПодписаниеДокумент(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ОтклонитьПодписаниеПослеЗаписи", ЭтотОбъект, Ложь);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьПодписаниеИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПовторноДокумент(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ОтправитьПовторноПослеЗаписи", ЭтотОбъект, Ложь);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтправитьПовторноИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьАннулированиеДокумент(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ПринятьАннулированиеПослеЗаписи", ЭтотОбъект, Ложь);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеПринятьАннулированиеИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьАннулированиеДокумент(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ОтклонитьАннулированиеПослеЗаписи", ЭтотОбъект, Ложь);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьАннулированиеИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АннулироватьДокумент(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("АннулироватьПослеЗаписи", ЭтотОбъект, Ложь);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеАннулироватьИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьДокумент(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ЗавершитьПослеЗаписи", ЭтотОбъект, Ложь);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеЗакрытьПринудительноИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВернутьВРаботуДокумент(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ВернутьВРаботуПослеЗаписи", ЭтотОбъект, Ложь);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеВернутьВРаботуИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти // ВыполнениеДействийПоЭДОСДокументом

#Область НастройкиОтображения

&НаКлиенте
Процедура ОтображатьБанковскиеРеквизиты(Кнопка)

	ВыводитьБанковскиеРеквизиты = Не ВыводитьБанковскиеРеквизиты;
	Элементы.КнопкаОтображатьБанковскиеРеквизиты.Пометка = ВыводитьБанковскиеРеквизиты;

	ОбновитьВизуализациюДокумента();

КонецПроцедуры

&НаКлиенте
Процедура ОтображатьДополнительнуюИнформацию(Команда)

	ОтключитьВыводДопДанных = Не ОтключитьВыводДопДанных;
	Элементы.КнопкаОтображатьДополнительнуюИнформацию.Пометка = Не ОтключитьВыводДопДанных;

	ОбновитьВизуализациюДокумента();

КонецПроцедуры

&НаКлиенте
Процедура ОтображатьОбластьКопияВерна(Команда)

	ОтключитьВыводКопияВерна = Не ОтключитьВыводКопияВерна;
	Элементы.КнопкаОтображатьОбластьКопияВерна.Пометка = Не ОтключитьВыводКопияВерна;

	ОбновитьВизуализациюДокумента();

КонецПроцедуры

&НаКлиенте
Процедура ТранслитерироватьИмяФайла(Команда)
	
	ОтключитьТранслитерацию = Не ОтключитьТранслитерацию;
	Элементы.КнопкаТранслитерироватьИмяФайла.Пометка = Не ОтключитьТранслитерацию;

	ОбновитьДанныеПослеИзмененияНастройкиТранслитерации();
	
КонецПроцедуры

#КонецОбласти // НастройкиОтображения

&НаКлиенте
Процедура Переформировать(Команда)
	ПереформироватьДокумент();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФормализованныйДокументXMLСДиска(Команда)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьФормализованныйДокументXMLСДискаПослеЗагрузки",
		ЭтотОбъект);
	ИнтерфейсДокументовЭДОКлиент.ЗагрузитьФормализованныйДокументXMLСДиска(ОповещениеОЗавершении);

КонецПроцедуры

&НаКлиенте
Процедура СменитьОтветственного(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("СменитьОтветственногоПослеЗаписи", ЭтотОбъект);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеСменыОтветственного", ЭтотОбъект);
		ЛегкийИнтерфейсДокументовЭДОКлиент.СменитьОтветственногоИзКарточкиДокумента(ЭтотОбъект, ОповещениеОЗавершении);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьОтветственногоПакет(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("СменитьОтветственногоПакетПослеЗаписи", ЭтотОбъект);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеСменыОтветственного", ЭтотОбъект);
		ЛегкийИнтерфейсДокументовЭДОКлиент.СменитьОтветственногоИзКарточкиДокумента(
			ЭтотОбъект, ОповещениеОЗавершении, Истина);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СоздатьПроизвольныйДокумент(Команда)
	ИнтерфейсДокументовЭДОКлиент.ОткрытьНовыйПроизвольныйЭлектронныйДокументНаОсновании(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодписи(Команда)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеПроверкиПодписейДокумента", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьПроверкуПодписейИзКарточкиДокумента(ЭтотОбъект, ОповещениеОЗавершении);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСертификат(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ПоказатьСертификатИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодписьПоДоверенностиВручную(Команда)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеПроверкиПодписейДокумента", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.ПроверитьПодписьПоДоверенностиВручнуюИзКарточкиДокумента(ЭтотОбъект,
		ОповещениеОЗавершении);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОтправкуПолучение(Команда)

	Прочитать();
	ОжидатьПолученияАсинхронныхДанных();
	
	УчетныеЗаписиОбмена = Новый Массив;
	УчетныеЗаписиОбмена.Добавить(Объект.ИдентификаторОрганизации);
	ИнтерфейсДокументовЭДОКлиент.ОтправитьПолучитьДокументыЭДО(ЭтотОбъект,, УчетныеЗаписиОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуЭлектронногоДокумента(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ОткрытьКарточкуЭлектронногоДокументаПослеЗаписи", ЭтотОбъект);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ИнтерфейсДокументовЭДОКлиент.ОткрытьКарточкуЭлектронногоДокумента(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКонтейнерДокумента(Команда)
	
	Если Не ЗначениеЗаполнено(СообщениеЭДО) Тогда
		СообщениеЭДО = СообщениеЭДОИнформацииОтправителя(Объект.Ссылка);
	КонецЕсли;

	ИнтерфейсДокументовЭДОКлиент.ОткрытьТранспортныйКонтейнерДокумента(СообщениеЭДО, СостояниеЭДО);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЖурналСобытийЭДО(Команда)
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ОткрытьЖурналСобытийЭДОПослеЗаписи", ЭтотОбъект);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЭлектронныеДокументыЭДОКлиент.ОткрытьЖурналДействийПоЭДО(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеДокумент(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПометитьНаУдалениеДокументПослеОтветаНаВопрос", ЭтотОбъект);
	ОбщегоНазначенияБЭДКлиент.ЗадатьВопросНаУстановкуПометкиУдаления(Объект.ПометкаУдаления, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьДокументИзПакета(Команда)
	
	Если РежимСозданияПакетаПоПредварительнымДанным Тогда
		ЛегкийИнтерфейсДокументовЭДОКлиент.УдалитьДокументИзПредварительногоПакетаКарточкиДокумента(ЭтотОбъект,
			ТекущийИдентификаторДокумента);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.УдалитьДокументИзПакета(ИдентификаторПакета, ТекущийДокументПакета);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиОтправки(Команда)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеИзмененияНастроекОтправкиИлиФормирования", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьНастройкиОтправкиИзКарточкиДокумента(ЭтотОбъект, ОповещениеОЗавершении);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиФормирования(Команда)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеИзмененияНастроекОтправкиИлиФормирования", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьНастройкиФормированияИзКарточкиДокумента(
		ЭтотОбъект, ОповещениеОЗавершении);

КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормыЕще

#Область ОбработчикиКомандФормыСодержимое

&НаКлиенте
Процедура СостояниеОбработкаНажатия(Команда)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(КомментарийСостояния, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	
	Если РежимСозданияПакетаПоПредварительнымДанным Тогда
		ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьФайлПредварительногоПакетаКарточкиДокумента(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("ОткрытьФайлПослеЗаписи", ЭтотОбъект);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьФайлИнформацииОтправителяДокумента(
			Объект.Ссылка, Объект.ЭтоОблачныйЭДО, УникальныйИдентификатор);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьЗаписку(Команда)
	СопроводительнаяЗапискаТекст = "";
	Модифицированность = Истина;
	ИзмененаСопроводительнаяЗаписка = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВизуализацию(Команда)
	ПеречитатьКэшВизуализации = Истина;
	ОбновитьДанныеЭлектронногоДокумента();
	ПеречитатьКэшВизуализации = Ложь;
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормыСодержимое

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	ОбменСКонтрагентамиКлиентПереопределяемый.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти // ПодключаемыеКоманды

&НаКлиенте
Процедура ДополнительноеСостояниеОбработкаНажатия(Команда)
	
	Если Не ЗначениеЗаполнено(КомандыДополнительногоСостояния) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработчикКоманды = Неопределено;
	Значение          = Неопределено;
	ИдентификаторКоманды = СтрЗаменить(ТекущийЭлемент.Имя, "ДополнительноеСостояние_", "");
	Для Каждого СвязаннаяКоманда Из КомандыДополнительногоСостояния Цикл
		Если СвязаннаяКоманда.Идентификатор = ИдентификаторКоманды Тогда
			ОбработчикКоманды = СвязаннаяКоманда.Обработчик;
			Значение          = СвязаннаяКоманда.Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если Не ЗначениеЗаполнено(ОбработчикКоманды) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура("Форма", ЭтотОбъект);
	Оповещение = ОбщегоНазначенияБЭДКлиент.ОписаниеОповещенияПоПутиКОбработчику(ОбработчикКоманды, ПараметрыОповещения);
	ВыполнитьОбработкуОповещения(Оповещение, Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСвязанныйДокумент1(Команда)
	ПоказатьЗначение(, СвязанныеДокументы[0].Значение);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСвязанныйДокумент2(Команда)
	ПоказатьЗначение(, СвязанныеДокументы[1].Значение);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокСвязанныхДокументов(Команда)
	ПараметрыФормы = Новый Структура("СвязанныеДокументы", СвязанныеДокументы);
	ОткрытьФорму("ОбщаяФорма.СвязанныеДокументыБЭД", ПараметрыФормы);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСвязанныйДокумент(Команда)

	Если Не ЗначениеЗаполнено(КомандыСозданияСвязанныхДокументов) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработчикКоманды = Неопределено;
	Значение          = Неопределено;
	ИдентификаторКоманды = СтрЗаменить(ТекущийЭлемент.Имя, "СоздатьСвязанныйДокумент_", "");
	Для Каждого СвязаннаяКоманда Из КомандыСозданияСвязанныхДокументов Цикл
		Если СвязаннаяКоманда.Идентификатор = ИдентификаторКоманды Тогда
			ОбработчикКоманды = СвязаннаяКоманда.Обработчик;
			Значение          = СвязаннаяКоманда.Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если Не ЗначениеЗаполнено(ОбработчикКоманды) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура("Форма", ЭтотОбъект);
	Оповещение = ОбщегоНазначенияБЭДКлиент.ОписаниеОповещенияПоПутиКОбработчику(ОбработчикКоманды, ПараметрыОповещения);
	ВыполнитьОбработкуОповещения(Оповещение, Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьПолеПредпросмотраPDFФайлов(Команда)
	Элементы.ВключитьПолеПредпросмотраPDFФайлов.Пометка = Не Элементы.ВключитьПолеПредпросмотраPDFФайлов.Пометка;
	ВключитьПолеПредпросмотраPDFФайловНаСервере(Элементы.ВключитьПолеПредпросмотраPDFФайлов.Пометка);
	ОбновитьВизуализациюДокумента();
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеДанных

&НаСервере
Процедура ОбновитьДанныеДляПросмотраДокумента()
	
	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельКарточки, ПанельПометкиНаУдаление, ПанельКомандИсходящегоДокументаЛегкийКлиентЭДО, ПанельПакета,
		|ПанельСостоянияЛегкийКлиентЭДО, ПанельЗакладокИсходящегоДокументаЛегкийКлиентЭДО, ПанельВизуализации,
		|ПанельКомандНастроекВизуализации, ПанельПредставленияФайла, ПанельПодписей,
		|ПанельСопроводительнойЗапискиИсходящегоДокумента, ПанельРеквизитов, ПанельРеквизитовПакета,
		|ПанельСвязанныхДокументовЛегкийКлиентЭДО");
	
	ОбновитьДанныеКомпонентов(ДанныеКомпонентов);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеДляПросмотраНовогоДокумента()

	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельКарточки, ПанельПометкиНаУдаление, ПанельКомандИсходящегоДокументаЛегкийКлиентЭДО, ПанельПакета,
		|ПанельСостоянияЛегкийКлиентЭДО, ПанельЗакладокИсходящегоДокументаЛегкийКлиентЭДО, ПанельВизуализации,
		|ПанельКомандНастроекВизуализации, ПанельПредставленияФайла, ПанельПодписей,
		|ПанельСопроводительнойЗапискиИсходящегоДокумента, ПанельРеквизитов, ПанельРеквизитовПакета,
		|ПанельСвязанныхДокументовЛегкийКлиентЭДО");
	
	ОбновитьДанныеКомпонентовПредварительныйПросмотр(ДанныеКомпонентов);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеЭлектронногоДокумента()
	
	Прочитать();
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеКомпонентов(ДанныеКомпонентов)

	Если ОбщегоНазначения.ЭтоВебКлиент() Тогда
		ИнтерфейсДокументовЭДО.УдалитьРеквизитыВыводаPDFДокумента(ЭтотОбъект, СведенияОPdfДокументе);
	КонецЕсли;

	ПараметрыЗаполнения = 
		ЛегкийИнтерфейсДокументовЭДО.ПараметрыЗаполненияДанныхКомпонентовПросмотраДокумента(ЭтотОбъект);
	ПараметрыЗаполнения.НастройкиФормы.ПеречитатьКэшВизуализации = ПеречитатьКэшВизуализации;
	ОперацияПолученияДанных = ЛегкийИнтерфейсДокументовЭДО.ЗаполнитьДанныеКомпонентовПросмотраДокумента(
		ДанныеКомпонентов, ПараметрыЗаполнения);
	
	ЛегкийИнтерфейсДокументовЭДО.НастроитьПанелиПросмотраКарточкиДокумента(ЭтотОбъект, ДанныеКомпонентов);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеКомпонентовПредварительныйПросмотр(ДанныеКомпонентов)

	ПараметрыЗаполнения = 
		ЛегкийИнтерфейсДокументовЭДО.ПараметрыЗаполненияДанныхКомпонентовПредварительногоПросмотраДокумента(ЭтотОбъект);
	
	ОперацияПолученияДанных = ЛегкийИнтерфейсДокументовЭДО.ЗаполнитьДанныеКомпонентовПросмотраДокумента(
		ДанныеКомпонентов, ПараметрыЗаполнения);
	
	ЛегкийИнтерфейсДокументовЭДО.НастроитьПанелиПросмотраКарточкиДокумента(ЭтотОбъект, ДанныеКомпонентов);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВизуализациюДокумента()

	ОбновитьВизуализациюДокументаНаСервере();

	ОжидатьПолученияАсинхронныхДанных();

КонецПроцедуры

&НаСервере
Процедура ОбновитьВизуализациюДокументаНаСервере()

	ЛегкийИнтерфейсДокументовЭДО.СохранитьНастройкиОтображенияВизуализацииКарточкиДокумента(ЭтотОбъект);

	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельВизуализации, ПанельКомандНастроекВизуализации, ПанельПредставленияФайла");
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОбновитьДанныеКомпонентов(ДанныеКомпонентов);
	Иначе
		ОбновитьДанныеКомпонентовПредварительныйПросмотр(ДанныеКомпонентов);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПослеВыполненияДействийПоЭДО()
	
	Прочитать();
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПослеИзмененияПодписейДокумента()
	
	ОбновитьДанныеПослеИзмененияПодписейДокументаНаСервере();
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеИзмененияПодписейДокументаНаСервере()
	
	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельКомандИсходящегоДокументаЛегкийКлиентЭДО, ПанельСостоянияЛегкийКлиентЭДО,
		|ПанельЗакладокИсходящегоДокументаЛегкийКлиентЭДО, ПанельВизуализации, ПанельКомандНастроекВизуализации,
		|ПанельПодписей");
	
	ОбновитьДанныеКомпонентов(ДанныеКомпонентов);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПослеДобавленияДокументаВПакет(НовыйИдентификаторПакета, ДокументыПакета)
	
	Если Не ЗначениеЗаполнено(ИдентификаторПакета) Тогда
		ИдентификаторПакета = НовыйИдентификаторПакета;
	КонецЕсли;
	
	Если НовыйИдентификаторПакета <> ИдентификаторПакета Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоДокументовВПакете = ДокументыПакета.Количество();
	ТекущийДокументПакета = ДокументыПакета[КоличествоДокументовВПакете - 1];
	ТекущийИдентификаторДокумента = ТекущийДокументПакета.УникальныйИдентификатор();
	
	ОбновитьДанныеПослеДобавленияДокументаВПакетНаСервере();
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеДобавленияДокументаВПакетНаСервере()
	
	СоставПакета.Очистить();
	
	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента("ПанельПакета,
		|ПанельРеквизитовПакета");
	
	ОбновитьДанныеКомпонентов(ДанныеКомпонентов);
	
	ПоказатьДокументПакета();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПослеУдаленияДокументаИзПакета()
	
	ОбновитьДанныеПослеУдаленияДокументаИзПакетаНаСервере();
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеУдаленияДокументаИзПакетаНаСервере()
	
	Для Каждого ДокументПакета Из СоставПакета Цикл
		Если ДокументПакета.ЭлектронныйДокумент <> ТекущийДокументПакета Тогда
			ТекущийДокументПакета = ДокументПакета.ЭлектронныйДокумент;
			ТекущийИдентификаторДокумента = ТекущийДокументПакета.УникальныйИдентификатор();
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	СоставПакета.Очистить();
	
	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента("ПанельПакета,
		|ПанельРеквизитовПакета");
	
	ОбновитьДанныеКомпонентов(ДанныеКомпонентов);
	
	ПоказатьДокументПакета();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПослеИзмененияНастройкиТранслитерации()
	
	ОбновитьДанныеПослеИзмененияНастройкиТранслитерацииНаСервере();
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеИзмененияНастройкиТранслитерацииНаСервере()
	
	ОбщегоНазначения.ХранилищеНастроекДанныхФормСохранить(ИмяФормы, "ОтключитьТранслитерацию", ОтключитьТранслитерацию);

	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельКомандИсходящегоДокументаЛегкийКлиентЭДО, ПанельВизуализации");
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОбновитьДанныеКомпонентов(ДанныеКомпонентов);
	Иначе
		ОбновитьДанныеКомпонентовПредварительныйПросмотр(ДанныеКомпонентов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПослеИзмененияОбщегоСостояния()

	ОбновитьДанныеПослеИзмененияОбщегоСостоянияНаСервере();
	
	ОжидатьПолученияАсинхронныхДанных();

КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеИзмененияОбщегоСостоянияНаСервере()

	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельСостоянияЛегкийКлиентЭДО");
	
	ОбновитьДанныеКомпонентов(ДанныеКомпонентов);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПослеИзмененияСвязанныхДокументов()

	ОбновитьДанныеПослеИзмененияСвязанныхДокументовНаСервере();
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеИзмененияСвязанныхДокументовНаСервере()

	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельСвязанныхДокументовЛегкийКлиентЭДО");
	
	ОбновитьДанныеКомпонентов(ДанныеКомпонентов);

КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеИзмененияКлючевыхРеквизитовНаСервере()

	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента("ПанельПакета,
		|ПанельРеквизитовПакета");
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОбновитьДанныеКомпонентовПредварительныйПросмотр(ДанныеКомпонентов);
	Иначе
		ОбновитьДанныеКомпонентов(ДанныеКомпонентов);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОбИзмененииСостоянияДокумента()

	ПараметрыСобытия = ИнтерфейсДокументовЭДОКлиент.НовыеПараметрыСобытияОбновленияСостоянияЭДО();
	ПараметрыСобытия.ЭлектронныеДокументы.Добавить(Объект.Ссылка);
	Оповестить(ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияСостоянияЭДО(), ПараметрыСобытия);
	
КонецПроцедуры

&НаСервере
Процедура ВидДокументаПриИзмененииНаСервере()
	
	ЗаполнитьДанныеДокументаПриИзмененииКлючевыхРеквизитов();
	
	ОбновитьДанныеПослеИзмененияКлючевыхРеквизитовНаСервере();
	
КонецПроцедуры

#Область АсинхронноеОбновлениеДанных

&НаКлиенте
Процедура ОжидатьПолученияАсинхронныхДанных()

	ДлительнаяОперация = ОперацияПолученияДанных; // См. ДлительныеОперации.ВыполнитьФункцию

	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаполнитьАсинхронныеДанныеПослеОжидания", ЭтотОбъект);

	Оповещение = Новый ОписаниеОповещения(
		"ОжидатьПолученияДанныхКомпонентовПросмотраПослеДлительнойОперации",
		ЛегкийИнтерфейсДокументовЭДОКлиент, ОповещениеОЗавершении);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАсинхронныеДанныеПослеОжидания(Результат, ДополнительныеПараметры) Экспорт

	Если ОперацияПолученияДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Результат.Успех Тогда
		ОперацияПолученияДанных = Результат.ОперацияПолученияДанных;
		ОбновитьДанныеПослеЗаполненияАсинхронныхДанных();
		ЛегкийИнтерфейсДокументовЭДОКлиент.НастроитьБлокировкуПанелиКомандКарточкиДокумента(ЭтотОбъект, Ложь);
		
		Если ЗначениеЗаполнено(АдресХранилищаPdf) Тогда
			ПредставлениеДокумента = ПолучитьИзВременногоХранилища(АдресХранилищаPdf);
			ИнтерфейсДокументовЭДОКлиент.ВывестиPDFДокументНаФорму(ЭтотОбъект, СведенияОPdfДокументе,
				ПредставлениеДокумента);
			УдалитьИзВременногоХранилища(АдресХранилищаPdf);
			АдресХранилищаPdf = "";
		КонецЕсли;
	Иначе
		ОперацияПолученияДанных = Неопределено;
		ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(Результат.КонтекстДиагностики);
	КонецЕсли;
	
	Если ОповеститьОбИзмененииСвязанныхДокументов Тогда
		ЛегкийИнтерфейсДокументовЭДОКлиент.ОповеститьОбИзмененииСвязанныхДокументовИзКарточкиДокумента(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеЗаполненияАсинхронныхДанных()

	ДанныеКомпонентов = 
		ЛегкийИнтерфейсДокументовЭДО.РезультатЗаполненияДанныхКомпонентовПросмотраДокументаАсинхронноВФоне(
			ОперацияПолученияДанных);
	
	ОперацияПолученияДанных = Неопределено;
	
	Если ДанныеКомпонентов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЛегкийИнтерфейсДокументовЭДО.НастроитьПанелиПросмотраКарточкиДокумента(ЭтотОбъект, ДанныеКомпонентов);
	Если ЗначениеЗаполнено(ДанныеКомпонентов.ПанельПредставленияФайла) Тогда
		АдресХранилищаPdf = ПоместитьВоВременноеХранилище(ДанныеКомпонентов.ПанельПредставленияФайла.Данные);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // АсинхронноеОбновлениеДанных

#КонецОбласти // ОбновлениеДанных

#Область ЗаполнениеДанных

&НаСервере
Процедура ЗаполнитьДанныеНовогоДокумента()
	
	Если ЗначениеЗаполнено(Параметры.ПросмотрПодготовленныхДанных) Тогда
		ЗаполнитьДокументПоПодготовленнымДанным(Параметры.ПросмотрПодготовленныхДанных);
	ИначеЕсли ЗначениеЗаполнено(Параметры.ПросмотрФайла) Тогда
		ЗаполнитьДокументПоФайлу(Параметры.ПросмотрФайла);
	ИначеЕсли ЗначениеЗаполнено(Параметры.ПросмотрПакета) Тогда
		ЗаполнитьПакетДокументовПоФайлам(Параметры.ПросмотрПакета);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ПросмотрПодготовленныхДанных - Структура:
//  * ОбъектУчета - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  * НастройкиФормирования - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыеНастройкиФормированияЭлектронногоДокументаОбъектаУчета
//  * АдресДанных - Строка
&НаСервере
Процедура ЗаполнитьДокументПоПодготовленнымДанным(ПросмотрПодготовленныхДанных)
	
	Если ПросмотрПодготовленныхДанных.НастройкиФормирования.ЭтоОблачныйЭДО Тогда
		ЗаполнитьДокументПоПодготовленнымДаннымОблачногоЭДО(ПросмотрПодготовленныхДанных);
	Иначе
		ЗаполнитьДокументПоПодготовленнымДаннымВстроенногоЭДО(ПросмотрПодготовленныхДанных);
	КонецЕсли;
	
КонецПроцедуры

#Область ЗаполнениеПодготовленныхДанныхВстроенногоЭДО

// Параметры:
//  ПросмотрПодготовленныхДанных - См. ЗаполнитьДокументПоПодготовленнымДанным.ПросмотрПодготовленныхДанных
&НаСервере
Процедура ЗаполнитьДокументПоПодготовленнымДаннымВстроенногоЭДО(ПросмотрПодготовленныхДанных)
	
	НастройкиФормирования = ПросмотрПодготовленныхДанных.НастройкиФормирования;
	ДанныеПредварительногоПросмотра = ИнтерфейсДокументовЭДО.ДанныеПредварительногоПросмотраПоАдресу(
		ПросмотрПодготовленныхДанных.АдресДанных);
	УдалитьИзВременногоХранилища(ПросмотрПодготовленныхДанных.АдресДанных);
	
	АдресОписанийСообщений = ПоместитьВоВременноеХранилище(ДанныеПредварительногоПросмотра.ОписанияСообщений,
		УникальныйИдентификатор);
	
	Если НастройкиФормирования.Направление = Перечисления.НаправленияЭДО.Внутренний Тогда
		ЗаполнитьДокументПоПодготовленнымДаннымВнутреннегоЭДО(
			НастройкиФормирования.НастройкиВнутреннегоЭДО, ДанныеПредварительногоПросмотра);
	Иначе
		ЗаполнитьДокументПоПодготовленнымДаннымИсходящегоЭДО(
			НастройкиФормирования.НастройкиОтправки, ДанныеПредварительногоПросмотра);
	КонецЕсли;
	Если ПросмотрПодготовленныхДанных.Свойство("АдресПредупреждений")
		И ЗначениеЗаполнено(ПросмотрПодготовленныхДанных.АдресПредупреждений) Тогда
		МассивУведомленийПользователю = ПолучитьИзВременногоХранилища(ПросмотрПодготовленныхДанных.АдресПредупреждений);
		Для Каждого УведомлениеПользователю Из МассивУведомленийПользователю Цикл
			ОбщегоНазначения.СообщитьПользователю(УведомлениеПользователю);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  НастройкиВнутреннегоЭДО - См. ИнтерфейсДокументовЭДО.НастройкиВнутреннегоЭДО
//  ДанныеПредварительногоПросмотра - См. ИнтерфейсДокументовЭДО.ДанныеПредварительногоПросмотраПоАдресу
&НаСервере
Процедура ЗаполнитьДокументПоПодготовленнымДаннымВнутреннегоЭДО(НастройкиВнутреннегоЭДО,
	ДанныеПредварительногоПросмотра)
	
	Для Каждого ОписаниеСообщения Из ДанныеПредварительногоПросмотра.ОписанияСообщений Цикл
		
		СодержаниеСообщения = ОписаниеСообщения.Данные.Содержание;
		
		Если ОписаниеСообщения.ТипЭлементаРегламента = 
			Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя Тогда
		
			Объект.ВидДокумента = НастройкиВнутреннегоЭДО.ВидДокумента;
			Объект.ИдентификаторДокументооборота = ЭлектронныеДокументыЭДО.НовыйИдентификаторДокументооборота();
			Объект.НомерДокумента = СодержаниеСообщения.НомерДокумента;
			Объект.ДатаДокумента = СодержаниеСообщения.ДатаДокумента;
			Объект.Организация = НастройкиВнутреннегоЭДО.Организация;
			Объект.ОбменБезПодписи = Ложь;
			Объект.ТипРегламента = СодержаниеСообщения.ТипРегламента;
			Объект.ТребуетсяИзвещение = Ложь;
			Объект.ТребуетсяПодтверждение = Ложь;
			Объект.ВыгружатьДополнительныеСведения = Ложь;
			
			МаршрутРедактируетсяПриСоздании =
				НастройкиВнутреннегоЭДО.МаршрутПодписания = МаршрутыПодписанияБЭД.МаршрутУказыватьПриСоздании();
			Если МаршрутРедактируетсяПриСоздании Тогда
				Объект.МаршрутПодписания = Неопределено;
			Иначе
				Объект.МаршрутПодписания = НастройкиВнутреннегоЭДО.МаршрутПодписания;
			КонецЕсли;
			
			Объект.ВидПодписи = НастройкиВнутреннегоЭДО.ВидПодписи;
			Объект.СпособОбмена = Перечисления.СпособыОбменаЭД.Внутренний;
			
			Основания.ЗагрузитьЗначения(ДанныеПредварительногоПросмотра.Основания);
			Для Каждого ОбъектУчета Из ДанныеПредварительногоПросмотра.Основания Цикл
				НовыйОбъектУчета = ОбъектыУчетаДокумента.Добавить();
				НовыйОбъектУчета.ОбъектУчета = ОбъектУчета;
			КонецЦикла;
			
		КонецЕсли;
	
	КонецЦикла;
	
	ТипДокумента = Перечисления.ТипыДокументовЭДО.Внутренний;
	МаршрутУказанВНастройках = ЗначениеЗаполнено(Объект.МаршрутПодписания);
	
	Объект.Ответственный = 
		ИнтеграцияЭДО.ОтветственныйПоДокументуЭДО(Объект.Организация, Объект.Контрагент, Объект.ДоговорКонтрагента);
	
КонецПроцедуры

// Параметры:
//  НастройкиОтправки - См. НастройкиЭДО.НастройкиОтправки
//  ДанныеПредварительногоПросмотра - См. ИнтерфейсДокументовЭДО.ДанныеПредварительногоПросмотраПоАдресу
&НаСервере
Процедура ЗаполнитьДокументПоПодготовленнымДаннымИсходящегоЭДО(НастройкиОтправки, ДанныеПредварительногоПросмотра)
	
	ПрикладныеВидыДокументов = ЭлектронныеДокументыЭДО.ПрикладныеВидыДокументов();
	
	Если ПрикладныеВидыДокументов.Найти(НастройкиОтправки.ВидДокумента) = Неопределено Тогда
		ТипДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкиОтправки.ВидДокумента, "ТипДокумента");
	Иначе
		ТипДокумента = Перечисления.ТипыДокументовЭДО.Прикладной;
	КонецЕсли;
	
	МодульОбменСГИСЭПД = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСГИСЭПД") Тогда
		МодульОбменСГИСЭПД = ОбщегоНазначения.ОбщийМодуль("ОбменСГИСЭПД");
	КонецЕсли;
	
	Для Каждого ОписаниеСообщения Из ДанныеПредварительногоПросмотра.ОписанияСообщений Цикл
	
		Если ОписаниеСообщения.ТипЭлементаРегламента <> Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя
			И (МодульОбменСГИСЭПД <> Неопределено 
			И Не МодульОбменСГИСЭПД.ЭтоТитулОтправителя(ОписаниеСообщения.ТипЭлементаРегламента)) Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеСообщения = ОписаниеСообщения.Данные;
		СодержаниеСообщения = ДанныеСообщения.Содержание;
			
		Если ЗначениеЗаполнено(СодержаниеСообщения) Тогда
			Объект.НомерДокумента = СодержаниеСообщения.НомерДокумента;
			Объект.ДатаДокумента = СодержаниеСообщения.ДатаДокумента;
			Объект.СуммаДокумента = СодержаниеСообщения.СуммаДокумента;
			Объект.ТипРегламента = СодержаниеСообщения.ТипРегламента;
			
			СуммаНДСДокументаИзДобавляемогоФайла = СодержаниеСообщения.СуммаНДСДокумента;
			СтавкаНДСДокументаИзДобавляемогоФайла = СодержаниеСообщения.СтавкаНДСДокумента;
			ВалютаДокументаИзДобавляемогоФайла = СодержаниеСообщения.ВалютаДокумента;
		Иначе
			Объект.ТипРегламента = Перечисления.ТипыРегламентовЭДО.Неформализованный;
		КонецЕсли;
		
		Объект.ИдентификаторДокументооборота = ЭлектронныеДокументыЭДО.НовыйИдентификаторДокументооборота();
		Объект.ВидДокумента = НастройкиОтправки.ВидДокумента;
		Объект.ИдентификаторОрганизации = НастройкиОтправки.ИдентификаторОтправителя;
		Объект.ИдентификаторКонтрагента = НастройкиОтправки.ИдентификаторПолучателя;
		Объект.Организация = НастройкиОтправки.Отправитель;
		Объект.Контрагент  = НастройкиОтправки.Получатель;
		Объект.ДоговорКонтрагента = НастройкиОтправки.Договор;
		Объект.СпособОбмена = НастройкиОтправки.СпособОбмена;
		Объект.ОбменБезПодписи = НастройкиОтправки.ОбменБезПодписи;
		Объект.ТребуетсяИзвещение = НастройкиОтправки.ТребуетсяИзвещениеОПолучении;
		Объект.ТребуетсяПодтверждение = НастройкиОтправки.ТребуетсяОтветнаяПодпись;
		Объект.ВыгружатьДополнительныеСведения = НастройкиОтправки.ВыгружатьДополнительныеСведения;
		Объект.МаршрутПодписания = НастройкиОтправки.МаршрутПодписания;
		Объект.ВидПодписи = Перечисления.ВидыЭлектронныхПодписей.УсиленнаяКвалифицированная;
		Объект.ФорматОсновногоТитула = НастройкиОтправки.Формат;
		
		МаршрутРедактируетсяПриСоздании = 
			НастройкиОтправки.МаршрутПодписания = МаршрутыПодписанияБЭД.МаршрутУказыватьПриСоздании();
		Если МаршрутРедактируетсяПриСоздании Тогда
			Объект.МаршрутПодписания = Неопределено;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеПредварительногоПросмотра.СвязующийОбъектУчета) Тогда
			Объект.ИдентификаторСвязи = 
				Строка(ДанныеПредварительногоПросмотра.СвязующийОбъектУчета.УникальныйИдентификатор());
		КонецЕсли;
		
		Основания.ЗагрузитьЗначения(ДанныеПредварительногоПросмотра.Основания);
		Для Каждого ОбъектУчета Из ДанныеПредварительногоПросмотра.Основания Цикл
			НовыйОбъектУчета = ОбъектыУчетаДокумента.Добавить();
			НовыйОбъектУчета.ОбъектУчета = ОбъектУчета;
		КонецЦикла;
		
		ЭлектронныеДокументыЭДО.ЗагрузитьИдентификаторыСвязанныхОбъектовУчета(Объект,
			ДанныеПредварительногоПросмотра.СвязанныеОбъектыУчета);
			
	КонецЦикла;
	
	МаршрутУказанВНастройках = ЗначениеЗаполнено(Объект.МаршрутПодписания);
	
	Объект.Ответственный = 
		ИнтеграцияЭДО.ОтветственныйПоДокументуЭДО(Объект.Организация, Объект.Контрагент, Объект.ДоговорКонтрагента);
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеПодготовленныхОблачногоЭДО

// Параметры:
//  ПросмотрПодготовленныхДанных - См. ЗаполнитьДокументПоПодготовленнымДанным.ПросмотрПодготовленныхДанных
&НаСервере
Процедура ЗаполнитьДокументПоПодготовленнымДаннымОблачногоЭДО(ПросмотрПодготовленныхДанных)
	
	Если ЭлектронныеДокументыЭДО.ЕстьИнтеграцияОблачногоЭДО() Тогда
		ОбщийМодульИнтерфейсДокументовЭДОИнтеграцияОблака = 
			ОбщегоНазначения.ОбщийМодуль("ИнтерфейсДокументовЭДОИнтеграцияОблака");
		ОбщийМодульИнтерфейсДокументовЭДОИнтеграцияОблака.ЗаполнитьДокументПоПодготовленнымДанным(ЭтотОбъект,
			ПросмотрПодготовленныхДанных);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеПоФайлу

// Параметры:
//  ПросмотрФайла - См. ЛегкийИнтерфейсДокументовЭДО.НовыеПараметрыПросмотраДокументаПоФайлу
&НаСервере
Процедура ЗаполнитьДокументПоФайлу(ПросмотрФайла)
	
	ЗаполнитьВложенияПоАдресуОписанияФайла(ПросмотрФайла.АдресОписанияФайла);
	
	Если ЗначениеЗаполнено(ПросмотрФайла.ОбъектУчета) Тогда
		ЗаполнитьДокументПоОбъектуУчета(ПросмотрФайла.ОбъектУчета, ПросмотрФайла.ВидДокумента);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПросмотрФайла.РеквизитыДокументаИзСодержанияФайла) Тогда
		ЗаполнитьДокументРеквизитамиИзСодержанияФайла(ПросмотрФайла.РеквизитыДокументаИзСодержанияФайла);
	КонецЕсли;
	
	ЗаполнитьДокументЗначениямиПоУмолчанию();
	
	ЗаполнитьДанныеДокументаПриИзмененииКлючевыхРеквизитов();
	
КонецПроцедуры

// Параметры:
//  АдресОписанияФайла - Строка
&НаСервере
Процедура ЗаполнитьВложенияПоАдресуОписанияФайла(АдресОписанияФайла)
	
	ОписаниеФайла = ПолучитьИзВременногоХранилища(АдресОписанияФайла); // См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла

	УдалитьИзВременногоХранилища(АдресОписанияФайла);

	ЗаполнитьВложенияИзОписанияФайла(ОписаниеФайла);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВложенияИзОписанияФайла(ОписаниеФайла)
	
	НовыеФайлыТитула = ИнтерфейсДокументовЭДОКлиентСервер.НовыеФайлыТитулаДокументаДляФормирования();
	НовыеФайлыТитула.Основной = ОписаниеФайла;
	
	АдресФайловНовогоДокумента = ПоместитьВоВременноеХранилище(НовыеФайлыТитула, УникальныйИдентификатор);
	ИмяДобавляемогоФайла = РаботаСФайламиБЭД.ПреобразоватьИмяФайлаСУчетомТранслитерации(
		ОписаниеФайла.ИмяФайла, ОтключитьТранслитерацию);
	РасширениеДобавляемогоФайла = ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ИмяДобавляемогоФайла);
	
КонецПроцедуры

// Параметры:
//  ОбъектУчета - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
&НаСервере
Процедура ЗаполнитьДокументПоОбъектуУчета(ОбъектУчета, ВидДокумента)
	
	Если Не ЗначениеЗаполнено(ОбъектУчета) Тогда
		Возврат;
	КонецЕсли;
		
	НовыйОбъектУчета = ОбъектыУчетаДокумента.Добавить();
	НовыйОбъектУчета.ОбъектУчета = ОбъектУчета;
	Основания.Добавить(ОбъектУчета);
	
	Если ТипЗнч(ОбъектУчета) = Тип("ДокументСсылка.ЭлектронныйДокументВходящийЭДО")
		Или ТипЗнч(ОбъектУчета) = Тип("ДокументСсылка.ЭлектронныйДокументИсходящийЭДО") Тогда
		
		СвойстваДокумента = ЭлектронныеДокументыЭДО.СвойстваДокумента(ОбъектУчета,
			"Организация, Контрагент, ДоговорКонтрагента");
		Объект.Организация = СвойстваДокумента.Организация;
		Объект.Контрагент = СвойстваДокумента.Контрагент;
		Объект.ДоговорКонтрагента = СвойстваДокумента.ДоговорКонтрагента;
		
	ИначеЕсли ТипЗнч(ОбъектУчета) = Тип("СправочникСсылка.МЧД003") Тогда
		
		РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОбъектУчета, "ДатаВыдачи, НомерДоверенности");
		Объект.НомерДокумента = РеквизитыОснования.НомерДоверенности;
		Объект.ДатаДокумента = РеквизитыОснования.ДатаВыдачи;
		
		ОписаниеОбъектаУчета = ЭлектронныеДокументыЭДО.ОписаниеОбъектаУчета(ОбъектУчета, ВидДокумента);
		
		ЗаполнитьДокументПоОписаниюОбъектаУчета(ОписаниеОбъектаУчета);
		
	Иначе
	
		РеквизитыОснования = ИнтеграцияЭДО.ОписаниеОснованияЭлектронногоДокумента(ОбъектУчета);
		Объект.Организация = РеквизитыОснования.Организация;
		Объект.Контрагент = РеквизитыОснования.Контрагент;
		Объект.НомерДокумента = РеквизитыОснования.Номер;
		Объект.ДатаДокумента = РеквизитыОснования.Дата;
		Объект.СуммаДокумента = РеквизитыОснования.СуммаДокумента;
		
		ОписаниеОбъектаУчета = ЭлектронныеДокументыЭДО.ОписаниеОбъектаУчета(ОбъектУчета, ВидДокумента);
	
		ЗаполнитьДокументПоОписаниюОбъектаУчета(ОписаниеОбъектаУчета);
	
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ОписаниеОбъектаУчета - см. ЭлектронныеДокументыЭДО.ОписаниеОбъектаУчета
&НаСервере
Процедура ЗаполнитьДокументПоОписаниюОбъектаУчета(ОписаниеОбъектаУчета)
	
	КоличествоДокументовЭДО = ОписаниеОбъектаУчета.Количество();
	
	Если КоличествоДокументовЭДО = 0 Тогда
		Возврат;
	КонецЕсли;
		
	Если КоличествоДокументовЭДО = 1 Тогда
		
		ОписаниеДокументаЭДО = ОписаниеОбъектаУчета[0];
		
		Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
			Объект.Организация = ОписаниеДокументаЭДО.Организация;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
			Объект.Контрагент = ОписаниеДокументаЭДО.Контрагент;
		КонецЕсли;
		
		Объект.ДоговорКонтрагента = ОписаниеДокументаЭДО.Договор;
		
		ТипДокумента = ОписаниеДокументаЭДО.ТипДокумента;
		
		Возврат;
		
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Организация", Объект.Организация);
	СтруктураПоиска.Вставить("Контрагент", Объект.Контрагент);
	НайденныеСтроки = ОписаниеОбъектаУчета.НайтиСтроки(СтруктураПоиска);
	
	Если НайденныеСтроки.Количество() Тогда
		
		ДоговорыКонтрагентов = Новый Массив;
		
		Для Каждого СтрокаОписания Из НайденныеСтроки Цикл
			ДоговорыКонтрагентов.Добавить(СтрокаОписания.Договор);
		КонецЦикла;
		
		ДоговорыКонтрагентов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ДоговорыКонтрагентов);
		Если ДоговорыКонтрагентов.Количество() = 1 Тогда
			Объект.ДоговорКонтрагента = ДоговорыКонтрагентов[0];
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДокументЗначениямиПоУмолчанию()
	
	Если Не ЗначениеЗаполнено(ТипДокумента) Тогда
		ТипДокумента = Перечисления.ТипыДокументовЭДО.Прочее;
	КонецЕсли;
	
	// Если не было предварительной подготовки данных, то документ не может иметь тип "Договорный документ"
	Если ТипДокумента = Перечисления.ТипыДокументовЭДО.ДоговорныйДокумент Тогда
		Объект.ВидДокумента = ЭлектронныеДокументыЭДО.ВидДокументаПоТипу(Перечисления.ТипыДокументовЭДО.Прочее);
	КонецЕсли;
	
	ТипыДокументовПроизвольногоФормата = ЭлектронныеДокументыЭДО.ТипыДокументовПроизвольногоФормата();
	Если ТипыДокументовПроизвольногоФормата.Найти(ТипДокумента) = Неопределено Тогда
		ТипДокумента = Перечисления.ТипыДокументовЭДО.Прочее;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ВидДокумента) Тогда
		Объект.ВидДокумента = ЭлектронныеДокументыЭДО.ВидДокументаПоТипу(ТипДокумента);
	КонецЕсли;
	
	ИспользуетсяНесколькоОрганизаций = ИнтеграцияЭДО.ИспользуетсяНесколькоОрганизаций();
	Если Не ЗначениеЗаполнено(Объект.Организация) И ИспользуетсяНесколькоОрганизаций Тогда
		Объект.Организация = ИнтеграцияЭДО.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ДатаДокумента) Тогда
		Объект.ДатаДокумента = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Объект.ТипРегламента = Перечисления.ТипыРегламентовЭДО.Неформализованный;
	
	Если ТипДокумента <> Перечисления.ТипыДокументовЭДО.МашиночитаемаяДоверенность Тогда
		СвязанныеОбъектыУчета = ОбъектыУчетаДокумента.Выгрузить(, "ОбъектУчета");
		ЭлектронныеДокументыЭДО.ЗагрузитьИдентификаторыСвязанныхОбъектовУчета(Объект, СвязанныеОбъектыУчета);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ответственный) Тогда
		Объект.Ответственный = 
			ИнтеграцияЭДО.ОтветственныйПоДокументуЭДО(Объект.Организация, Объект.Контрагент, Объект.ДоговорКонтрагента);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  РеквизитыДокумента - Структура:
//                       * НомерДокумента - Строка
//                       * ДатаДокумента - Дата
//                       * СуммаДокумента - Число
//                       * ВалютаДокумента - Строка
//                       * СуммаНДСДокумента - Число
//                       * СтавкаНДСДокумента - Строка
//                       * ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
//                       * ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
//
&НаСервере
Процедура ЗаполнитьДокументРеквизитамиИзСодержанияФайла(РеквизитыДокумента)
	
	Объект.НомерДокумента = РеквизитыДокумента.НомерДокумента;
	Объект.ДатаДокумента = РеквизитыДокумента.ДатаДокумента;
	Объект.СуммаДокумента = РеквизитыДокумента.СуммаДокумента;
	ТипДокумента = РеквизитыДокумента.ТипДокумента;
	СуммаНДСДокументаИзДобавляемогоФайла = РеквизитыДокумента.СуммаНДСДокумента;
	СтавкаНДСДокументаИзДобавляемогоФайла = РеквизитыДокумента.СтавкаНДСДокумента;
	ВалютаДокументаИзДобавляемогоФайла = РеквизитыДокумента.ВалютаДокумента;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеДокументаПриИзмененииКлючевыхРеквизитов()
	
	КлючНастройкиОтправки = НастройкиЭДОКлиентСервер.НовыйКлючНастроекОтправки();
	КлючНастройкиОтправки.ВидДокумента = Объект.ВидДокумента;
	КлючНастройкиОтправки.Отправитель = Объект.Организация;
	КлючНастройкиОтправки.Получатель = Объект.Контрагент;
	КлючНастройкиОтправки.Договор = Объект.ДоговорКонтрагента;
	
	НастройкиОтправки = НастройкиЭДО.НастройкиОтправки(КлючНастройкиОтправки);
	
	Объект.ТребуетсяПодтверждение = 
		?(ЗначениеЗаполнено(НастройкиОтправки), НастройкиОтправки.ТребуетсяОтветнаяПодпись, Ложь);
	
	Объект.ТребуетсяИзвещение = 
		?(ЗначениеЗаполнено(НастройкиОтправки), НастройкиОтправки.ТребуетсяИзвещениеОПолучении, Ложь);
		
	Объект.ИдентификаторОрганизации = 
		?(ЗначениеЗаполнено(НастройкиОтправки), НастройкиОтправки.ИдентификаторОтправителя, "");
	
	Объект.ИдентификаторКонтрагента = 
		?(ЗначениеЗаполнено(НастройкиОтправки), НастройкиОтправки.ИдентификаторПолучателя, "");
	
	Объект.СпособОбмена = ?(ЗначениеЗаполнено(НастройкиОтправки), НастройкиОтправки.СпособОбмена,
		Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО);
	
	Объект.ОбменБезПодписи = ?(ЗначениеЗаполнено(НастройкиОтправки), НастройкиОтправки.ОбменБезПодписи, Ложь);
	
	Объект.МаршрутПодписания =
		?(ЗначениеЗаполнено(НастройкиОтправки), НастройкиОтправки.МаршрутПодписания, Неопределено);
	
	Объект.ФорматОсновногоТитула = ?(ЗначениеЗаполнено(НастройкиОтправки) 
		И Объект.ТипРегламента <> Перечисления.ТипыРегламентовЭДО.Неформализованный, НастройкиОтправки.Формат, "");
	
	Объект.ВидПодписи = Перечисления.ВидыЭлектронныхПодписей.УсиленнаяКвалифицированная;
	
	ЭтоУчетнаяЗаписьОблачногоЭДО = УчетныеЗаписиЭДО.ЭтоУчетнаяЗаписьОблачногоЭДО(Объект.ИдентификаторОрганизации);
	
	Объект.ЭтоОблачныйЭДО = ЭтоУчетнаяЗаписьОблачногоЭДО;
	
	Если Не ЗначениеЗаполнено(Объект.ИдентификаторДокумента) Тогда
		Объект.ИдентификаторДокумента = ЭлектронныеДокументыЭДОКлиентСервер.НовыйИдентификаторДокумента();
	КонецЕсли;
	
	Если Не ЭтоУчетнаяЗаписьОблачногоЭДО И Не ЗначениеЗаполнено(Объект.ИдентификаторДокументооборота) Тогда
		Объект.ИдентификаторДокументооборота = ЭлектронныеДокументыЭДО.НовыйИдентификаторДокументооборота();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеДокументаПоПодготовленнымДаннымИзФайлаXML(ДанныеИзФайла)

	СодержаниеФайла = ДанныеИзФайла.СодержаниеФайла;
	
	Объект.ТипРегламента = СодержаниеФайла.ТипРегламента;
	Объект.НомерДокумента = СодержаниеФайла.НомерДокумента;
	Объект.ДатаДокумента = СодержаниеФайла.ДатаДокумента;
	Объект.СуммаДокумента = СодержаниеФайла.СуммаДокумента;
	
	СуммаНДСДокументаИзДобавляемогоФайла = СодержаниеФайла.СуммаНДСДокумента;
	СтавкаНДСДокументаИзДобавляемогоФайла = СодержаниеФайла.СтавкаНДСДокумента;
	ВалютаДокументаИзДобавляемогоФайла = СодержаниеФайла.ВалютаДокумента;

	НастройкиОтправки = ДанныеИзФайла.НастройкиОтправки;
	Если Не ЗначениеЗаполнено(НастройкиОтправки) Тогда
		Возврат;
	КонецЕсли;

	Объект.Организация = НастройкиОтправки.Отправитель;
	Объект.Контрагент = НастройкиОтправки.Получатель;
	Объект.ДоговорКонтрагента = НастройкиОтправки.Договор;
	Объект.ВидДокумента = НастройкиОтправки.ВидДокумента;
	
	Объект.ТребуетсяПодтверждение = НастройкиОтправки.ТребуетсяОтветнаяПодпись;
	Объект.ТребуетсяИзвещение = НастройкиОтправки.ТребуетсяИзвещениеОПолучении;
	Объект.ИдентификаторОрганизации = НастройкиОтправки.ИдентификаторОтправителя;
	Объект.ИдентификаторКонтрагента = НастройкиОтправки.ИдентификаторПолучателя;
	Объект.СпособОбмена = НастройкиОтправки.СпособОбмена;
	Объект.ОбменБезПодписи = НастройкиОтправки.ОбменБезПодписи;
	Объект.МаршрутПодписания = НастройкиОтправки.МаршрутПодписания;
	Объект.ФорматОсновногоТитула = НастройкиОтправки.Формат;
	Объект.ВидПодписи = Перечисления.ВидыЭлектронныхПодписей.УсиленнаяКвалифицированная;
	
	МаршрутРедактируетсяПриСоздании = 
		НастройкиОтправки.МаршрутПодписания = МаршрутыПодписанияБЭД.МаршрутУказыватьПриСоздании();
	Если Объект.МаршрутПодписания = МаршрутыПодписанияБЭД.МаршрутУказыватьПриСоздании() Тогда
		Объект.МаршрутПодписания = Неопределено;
	КонецЕсли;

	ЗаполнитьДанныеДокументаПриИзмененииКлючевыхРеквизитов();

КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеПакетаДокументов

// Параметры:
//  ПросмотрПакета - Массив Из См. ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеФайлаДляПараметровПредварительногоПакетаДокументов
&НаСервере
Процедура ЗаполнитьПакетДокументовПоФайлам(ПросмотрПакета)
	
	РежимСозданияПакетаПоПредварительнымДанным = Истина;
	
	ТипДокументаПрочее = Перечисления.ТипыДокументовЭДО.Прочее;
	ВидДокументаПрочее = ЭлектронныеДокументыЭДО.ВидДокументаПоТипу(ТипДокументаПрочее);
	ТипРегламентаНеформализованный = Перечисления.ТипыРегламентовЭДО.Неформализованный;
	
	ЭтоПервыйДокументПакета = Истина;
	Для Каждого ДанныеФайла Из ПросмотрПакета Цикл
		
		НовыйПредварительныйДокументПакета = ПредварительныйПакетДокументов.Добавить();
		
		ОписаниеФайла = ПолучитьИзВременногоХранилища(ДанныеФайла.АдресОписанияФайла); // См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
		УдалитьИзВременногоХранилища(ДанныеФайла.АдресОписанияФайла);
		
		НовыйПредварительныйДокументПакета.ИдентификаторДокумента = Новый УникальныйИдентификатор();
		НовыйПредварительныйДокументПакета.Организация = Объект.Организация;
		НовыйПредварительныйДокументПакета.АдресОписанияФайла = ПоместитьВоВременноеХранилище(ОписаниеФайла,
			УникальныйИдентификатор);
		НовыйПредварительныйДокументПакета.ИмяФайла = ОписаниеФайла.ИмяФайла;
		НовыйПредварительныйДокументПакета.ВидДокумента = ВидДокументаПрочее;
		НовыйПредварительныйДокументПакета.ТипДокумента = ТипДокументаПрочее;
		
		Если ЭтоПервыйДокументПакета Тогда
			ТекущийИдентификаторДокумента = НовыйПредварительныйДокументПакета.ИдентификаторДокумента;
			ЗаполнитьДанныеДляПервогоДокументаПакета(ОписаниеФайла, ДанныеФайла.РеквизитыИзСодержанияФайла);
		КонецЕсли;
		
		ЭтоПервыйДокументПакета = Ложь;
		
		НовыйПредварительныйДокументПакета.ТипРегламента = ТипРегламентаНеформализованный;
		
		Если Не ЗначениеЗаполнено(ДанныеФайла.РеквизитыИзСодержанияФайла) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(НовыйПредварительныйДокументПакета, ДанныеФайла.РеквизитыИзСодержанияФайла,
			"ВидДокумента, ТипДокумента, ДатаДокумента, НомерДокумента, СуммаДокумента");
			
		РасширениеФайла = ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ОписаниеФайла.ИмяФайла);
		Если РаботаСФайламиБЭДКлиентСервер.ЭтоXML(РасширениеФайла) Тогда
			ЗаполнитьДанныеДокументаПакетаПоДаннымФайлаXML(НовыйПредварительныйДокументПакета, ОписаниеФайла);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

// Параметры:
// ОписаниеФайла - См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
// РеквизитыДокументаИзСодержанияФайла - Неопределено
//                                     - Структура:
//                                       * НомерДокумента - Строка
//                                       * ДатаДокумента - Дата
//                                       * СуммаДокумента - Число
//                                       * ВалютаДокумента - Строка
//                                       * СуммаНДСДокумента - Число
//                                       * СтавкаНДСДокумента - Строка
//                                       * ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
//                                       * ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
&НаСервере
Процедура ЗаполнитьДанныеДляПервогоДокументаПакета(ОписаниеФайла, РеквизитыДокументаИзСодержанияФайла)
	
	АдресФайловНовогоДокумента = ПоместитьВоВременноеХранилище(
		Новый Структура("Основной, Дополнительный", ОписаниеФайла), УникальныйИдентификатор);
	ИмяДобавляемогоФайла = ОписаниеФайла.ИмяФайла;
	РасширениеДобавляемогоФайла = ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ИмяДобавляемогоФайла);
	
	Если ЗначениеЗаполнено(РеквизитыДокументаИзСодержанияФайла) Тогда
		ЗаполнитьДокументРеквизитамиИзСодержанияФайла(РеквизитыДокументаИзСодержанияФайла);
	КонецЕсли;
	
	ЗаполнитьДокументЗначениямиПоУмолчанию();
	
	ЗаполнитьДанныеДокументаПриИзмененииКлючевыхРеквизитов();
	
КонецПроцедуры

// Параметры:
//  ДанныеДокументаПакета - ДанныеФормыЭлементКоллекции Из См. ПредварительныйПакетДокументов
//  ОписаниеФайла - См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
&НаСервере
Процедура ЗаполнитьДанныеДокументаПакетаПоДаннымФайлаXML(ДанныеДокументаПакета, ОписаниеФайла)

	ВыбранныйФайл = Новый Структура("Хранение, Имя", ПоместитьВоВременноеХранилище(ОписаниеФайла.ДвоичныеДанные,
		УникальныйИдентификатор), ОписаниеФайла.ИмяФайла);
	
	ИдентификаторДокументаПредварительногоПакета = ДанныеДокументаПакета.ИдентификаторДокумента;
	
	РезультатПодготовкиДанных =
		ИнтерфейсДокументовЭДО.ПодготовитьДанныеПредварительногоПросмотраПоФайлуXML(ВыбранныйФайл);
	
	ОбработатьРезультатПодготовкиДанныхДляПросмотраПоФайлуXMLНаСервере(РезультатПодготовкиДанных,
		ДанныеДокументаПакета);
	
КонецПроцедуры

// Параметры:
//  РезультатПодготовкиДанных - См. ИнтерфейсДокументовЭДО.ПодготовитьДанныеПредварительногоПросмотраПоФайлуXML
//  ДанныеДокументаПакета - Неопределено
//                        - ДанныеФормыЭлементКоллекции Из См. ПредварительныйПакетДокументов
&НаСервере
Процедура ОбработатьРезультатПодготовкиДанныхДляПросмотраПоФайлуXMLНаСервере(РезультатПодготовкиДанных,
	ДанныеДокументаПакета = Неопределено)
	
	Если РезультатПодготовкиДанных.ДлительнаяОперация <> Неопределено Тогда
		ОперацияПодготовкиДанныхДляПросмотраПоФайлуXML = РезультатПодготовкиДанных.ДлительнаяОперация;
		Возврат;
	КонецЕсли;
		
	Если Не РезультатПодготовкиДанных.Ошибка И Не РезультатПодготовкиДанных.ДокументРаспознан Тогда
		Возврат;
	КонецЕсли;
		
	Если ДанныеДокументаПакета = Неопределено Тогда
		ДанныеДокументаПакета = ЛегкийИнтерфейсДокументовЭДО.ДанныеПредварительногоДокументаПакетаПоИдентификатору(
			ЭтотОбъект, ИдентификаторДокументаПредварительногоПакета);
	КонецЕсли;
		
	Если РезультатПодготовкиДанных.ДокументРаспознан Тогда
		ПросмотрПодготовленныхДанных = Новый Структура;
		ПросмотрПодготовленныхДанных.Вставить("ОбъектУчета");
		ПросмотрПодготовленныхДанных.Вставить("НастройкиФормирования", РезультатПодготовкиДанных.НастройкиФормирования);
		ПросмотрПодготовленныхДанных.Вставить("АдресДанных", РезультатПодготовкиДанных.АдресДанных);
		ПросмотрПодготовленныхДанных.Вставить("АдресПредупреждений",
			РезультатПодготовкиДанных.АдресСведенийОУведомлениях);
			
		ЗаполнитьДокументПоПодготовленнымДанным(ПросмотрПодготовленныхДанных);
		
		ЗаполнитьЗначенияСвойств(ДанныеДокументаПакета, Объект, "Организация, Контрагент, НомерДокумента, ДатаДокумента,
			|СуммаДокумента, ТипРегламента");
		ДанныеДокументаПакета.ТипДокумента = ТипДокумента;
		Возврат;
	КонецЕсли;
	
	ДанныеДокументаПакета.РезультатПодготовкиДанныхДляПросмотра = РезультатПодготовкиДанных;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПодготовкиДанныхДляПросмотраПоФайлуXML()
	
	Если Не ЗначениеЗаполнено(ПредварительныйПакетДокументов) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОперацияПодготовкиДанныхДляПросмотраПоФайлуXML <> Неопределено Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ОбработатьРезультатПодготовкиДанныхДляПросмотраПоФайлуXMLПослеДлительнойОперации", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ОперацияПодготовкиДанныхДляПросмотраПоФайлуXML, Оповещение);
		ОперацияПодготовкиДанныхДляПросмотраПоФайлуXML = Неопределено;
		Возврат;
	КонецЕсли;
	
	ИдентификаторыДокументовКУдалению = Новый Массив;
	Для Каждого ДанныеПредварительногоДокументаПакета Из ПредварительныйПакетДокументов Цикл
		ЕстьОшибки = ОбработатьРезультатПодготовкиДанныхПредварительногоДокументаПакета(
			ДанныеПредварительногоДокументаПакета);
		Если ЕстьОшибки Тогда
			ИдентификаторыДокументовКУдалению.Добавить(ДанныеПредварительногоДокументаПакета.ИдентификаторДокумента);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ИдентификаторДокумента Из ИдентификаторыДокументовКУдалению Цикл
		ЛегкийИнтерфейсДокументовЭДОКлиент.УдалитьДокументИзПредварительногоПакетаКарточкиДокумента(ЭтотОбъект,
			ИдентификаторДокумента);
	КонецЦикла;
	
	Если ПредварительныйПакетДокументов.Количество() = 0 Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
		
	ПоказатьДокументПакета();
		
КонецПроцедуры

&НаКлиенте
Функция ОбработатьРезультатПодготовкиДанныхПредварительногоДокументаПакета(ДанныеПредварительногоДокументаПакета)
	
	ЕстьОшибки = Ложь;
	
	РезультатПодготовкиДанныхДляПросмотра = ДанныеПредварительногоДокументаПакета.РезультатПодготовкиДанныхДляПросмотра;
	
	Если РезультатПодготовкиДанныхДляПросмотра = Неопределено Тогда
		Возврат ЕстьОшибки;
	КонецЕсли;
	
	ОписанияОшибок = ПолучитьИзВременногоХранилища(РезультатПодготовкиДанныхДляПросмотра.АдресСведенийОбОшибках);
	Для Каждого Ошибка Из ОписанияОшибок Цикл
		ЕстьОшибки = Истина;
		
		Если ЗначениеЗаполнено(Ошибка.Данные) Тогда
			ИнтерфейсДокументовЭДОКлиент.ОбработатьНаличиеСуществующегоДокументаДляВыбранногоФайлаXML(Ошибка.Данные,,
				ИмяДобавляемогоФайла);
			Продолжить;
		КонецЕсли;
		
		ПоказатьПредупреждение(, Ошибка.Текст);
	КонецЦикла;
	
	ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(РезультатПодготовкиДанныхДляПросмотра.КонтекстДиагностики);
	
	Возврат ЕстьОшибки;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультатПодготовкиДанныхДляПросмотраПоФайлуXMLПослеДлительнойОперации(ДлительнаяОперация,
	Контекст) Экспорт
	
	РезультатПодготовкиДанных = 
		ИнтерфейсДокументовЭДОВызовСервера.РезультатПодготовкиДанныхПросмотраПоФайлуXMLПослеДлительнойОперации(
			ДлительнаяОперация);
			
	ОбработатьРезультатПодготовкиДанныхДляПросмотраПоФайлуXMLНаСервере(РезультатПодготовкиДанных);
	
	ОбработатьРезультатПодготовкиДанныхДляПросмотраПоФайлуXML();

КонецПроцедуры

#КонецОбласти // ЗаполнениеПакетаДокументов

#КонецОбласти // ЗаполнениеДанных

#Область Запись

&НаКлиенте
Процедура ЗаписатьДокумент()
	ОбработчикЗавершения = Новый ОписаниеОповещения("ПослеВыполненияЗаписиДокумента", ЭтотОбъект);
	НачатьЗаписьДокумента(ОбработчикЗавершения);
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗаписьДокумента(ОповещениеОЗавершении)

	ОчиститьСообщения();
	
	ЭтоНовыйДокумент = Не ЗначениеЗаполнено(Объект.Ссылка);
	
	РезультатЗаписи = ЗаписатьДокументНаСервере();
	
	Если РезультатЗаписи.ТребуетсяФормализованныйФормат
		И Не РезультатЗаписи.РезультатПроверкиНастроек.Отказ Тогда
		Обработчик = Новый ОписаниеОповещения("ПоказатьОкноПредложенияОтправкиВФормализованномВидеПослеОтветаНаВопрос",
			ЭтотОбъект, ОповещениеОЗавершении);
			
		ИнтерфейсДокументовЭДОКлиент.ПоказатьОкноПредложенияОтправкиВФормализованномВиде(
			Объект.ВидДокумента, Обработчик);
		Возврат;
	КонецЕсли;
	
	Если Не РезультатЗаписи.Отказ И РежимСозданияПакетаПоПредварительнымДанным Тогда
		ПараметрыОповещения = Новый Структура("ОбработчикЗавершения, ЭтоНовыйДокумент",
			ОповещениеОЗавершении, ЭтоНовыйДокумент);
		ОбработчикПослеФормированияПакета = Новый ОписаниеОповещения(
			"ОбновитьДанныеПослеОбъединенияДокументовВПакетПоПредварительнымДанным", ЭтотОбъект, ПараметрыОповещения);
			
		ЛегкийИнтерфейсДокументовЭДОКлиент.СоздатьДокументыПакетаПоПредварительнымДаннымИзКарточкиДокумента(ЭтотОбъект,
			ОбработчикПослеФормированияПакета);
		Возврат;
	КонецЕсли;
	
	Если Не РезультатЗаписи.Отказ Тогда
		ПослеЗаписиПоИнтеграциям(ОповещениеОЗавершении, ЭтоНовыйДокумент);
		Возврат;
	КонецЕсли;
	
	Если РезультатЗаписи.Отказ И ЗначениеЗаполнено(РезультатЗаписи.РезультатПроверкиПредварительногоПакета) Тогда
		РезультатПроверки = РезультатЗаписи.РезультатПроверкиПредварительногоПакета;
		Если РезультатПроверки.ПроверкаПревышенияМаксимальногоКоличества.Превышено Тогда
			ТекстСообщения = ПакетыДокументовЭДОКлиент.ТекстОПревышенииКоличестваДокументовПриСохраненииПакета(
				РезультатПроверки.ПроверкаПревышенияМаксимальногоКоличества.ПревышаемыеОграничения);
		Иначе
			ТекстСообщения = ПакетыДокументовЭДОКлиент.ТекстОПревышенииМаксимальногоРазмераПакетаПриСохраненииПакета(
				РезультатПроверки.ПроверкаПревышенияМаксимальногоРазмера.ПревышаемыеОграничения);
		КонецЕсли;
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если РезультатЗаписи.Отказ И ЗначениеЗаполнено(РезультатЗаписи.ОграниченияОбъемаПакетаДокументов) Тогда
		ТекстСообщения = ПакетыДокументовЭДОКлиент.ТекстОПревышенииМаксимальногоРазмераПакетаСохранениеДокумента(
			РезультатЗаписи.ОграниченияОбъемаПакетаДокументов);
			
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если РезультатЗаписи.РезультатПроверкиНастроек.Отказ Тогда
		Оповещение = Новый ОписаниеОповещения("ВыполнитьПервичнуюНастройкуПередЗаписью",
			ЭтотОбъект, ОповещениеОЗавершении);
			
		Если РезультатЗаписи.РезультатПроверкиНастроек.ЭтоВнутреннийЭДО Тогда
			
			НастройкиЭДОКлиент.НастроитьВнутреннийЭлектронныйДокументооборот(
				Объект.Организация, Объект.ВидДокумента, Оповещение);
		
		ИначеЕсли РезультатЗаписи.РезультатПроверкиНастроек.ФормированиеЗапрещено = Истина Тогда 
			ДанныеДокумента = Новый Структура("Организация, Контрагент, ДоговорКонтрагента",
				Объект.Организация, Объект.Контрагент, Объект.ДоговорКонтрагента);

			ИнтерфейсДокументовЭДОКлиент.ПоказатьПроблемуЗапрещеноФормированиеДокумента(ДанныеДокумента, Оповещение);
		Иначе
			
			НастройкиЭДОКлиент.НастроитьОбменСКонтрагентом(
				РезультатЗаписи.РезультатПроверкиНастроек.КлючНастроекОтправки, Оповещение);
			
		КонецЕсли;
		
		Возврат;
	КонецЕсли;

	ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(РезультатЗаписи.КонтекстДиагностики);

	ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Ложь);
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьДокументНаСервере()
	
	РезультатЗаписи = Новый Структура;
	РезультатЗаписи.Вставить("Отказ", Ложь);
	РезультатЗаписи.Вставить("РезультатПроверкиЗаполнения", НовыйРезультатПроверкиЗаполнения());
	РезультатЗаписи.Вставить("РезультатПроверкиНастроек", 
		ИнтерфейсДокументовЭДО.НовыйРезультатПроверкиНастроекОтправки());
	РезультатЗаписи.Вставить("КонтекстДиагностики", ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики());
	РезультатЗаписи.Вставить("ТребуетсяФормализованныйФормат", Ложь);
	РезультатЗаписи.Вставить("ОграниченияОбъемаПакетаДокументов", Неопределено);
	РезультатЗаписи.Вставить("РезультатПроверкиПредварительногоПакета", Неопределено);
	
	РезультатПроверкиЗаполнения = ПроверитьЗаполнениеДокумента();
	Если РезультатПроверкиЗаполнения.Отказ Тогда
		РезультатЗаписи.Отказ = Истина;
		РезультатЗаписи.РезультатПроверкиЗаполнения = РезультатПроверкиЗаполнения;
		Возврат РезультатЗаписи;
	КонецЕсли;
	
	Если РежимСозданияПакетаПоПредварительнымДанным Тогда
		РезультатПроверкиПредварительногоПакета = 
			ЛегкийИнтерфейсДокументовЭДО.ПроверитьОграниченияДляФормированияПакетаПоПредварительнымДаннымПриЗаписи(
				ЭтотОбъект);
		
		Если РезультатПроверкиПредварительногоПакета.ПроверкаПревышенияМаксимальногоКоличества.Превышено
			Или РезультатПроверкиПредварительногоПакета.ПроверкаПревышенияМаксимальногоРазмера.Превышено Тогда
			РезультатЗаписи.Отказ = Истина;
			РезультатЗаписи.РезультатПроверкиПредварительногоПакета = РезультатПроверкиПредварительногоПакета;
			Возврат РезультатЗаписи;
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоАдресВременногоХранилища(АдресФайловНовогоДокумента) Тогда
		ДанныеФайлов = ПолучитьИзВременногоХранилища(АдресФайловНовогоДокумента);
		РезультатПроверкиПревышенияРазмераПакета = ЭлектронныеДокументыЭДО.ПроверкаПревышенияМаксимальногоРазмераФайла(
			Объект.ИдентификаторКонтрагента, Объект.ИдентификаторОрганизации, ДанныеФайлов.Основной.ДвоичныеДанные);
		
		Если РезультатПроверкиПревышенияРазмераПакета.Превышено Тогда
			РезультатЗаписи.Отказ = Истина;
			РезультатЗаписи.ОграниченияОбъемаПакетаДокументов = 
				РезультатПроверкиПревышенияРазмераПакета.ПревышаемыеОграничения;
			Возврат РезультатЗаписи;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Или Модифицированность Тогда
		РезультатПроверкиНастроек = ПроверитьНастройки();

		Если РезультатПроверкиНастроек.Отказ Тогда
			РезультатЗаписи.Отказ = Истина;
			РезультатЗаписи.РезультатПроверкиНастроек = РезультатПроверкиНастроек;
			Возврат РезультатЗаписи;
		КонецЕсли;
	КонецЕсли;
	
	РезультатПроверкиЗаполнения = ПроверитьЗаполнениеДокументаПослеПроверкиНастроек();
	Если РезультатПроверкиЗаполнения.Отказ Тогда
		РезультатЗаписи.Отказ = Истина;
		РезультатЗаписи.РезультатПроверкиЗаполнения = РезультатПроверкиЗаполнения;
		Возврат РезультатЗаписи;
	КонецЕсли;
	
	Если Не ОкноОтправкиВФормализованномВидеПоказано
		И ТребуетсяФормализованныйФормат(Объект.ТипРегламента, Объект.ВидДокумента) Тогда 
		РезультатЗаписи.ТребуетсяФормализованныйФормат = Истина;
		Возврат РезультатЗаписи;
	КонецЕсли;
	
	Если Не Записать() Тогда
		РезультатЗаписи.Отказ = Истина;
		Возврат РезультатЗаписи;
	КонецЕсли;
	
	Возврат РезультатЗаписи;
	
КонецФункции

&НаСервере
Процедура ИнициализироватьПараметрыЗаписи(ПараметрыЗаписи)
		
	НовыеПараметрыЗаписи = НовыеПараметрыЗаписиДокумента();
	НовыеПараметрыЗаписи.ЭтоНовыйДокумент = Не ЗначениеЗаполнено(Объект.Ссылка);
	НовыеПараметрыЗаписи.КонтекстДиагностики = ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики();
	НовыеПараметрыЗаписи.ИзменениеВидаДокумента = ИзмененВидДокумента И ЗначениеЗаполнено(Объект.Ссылка);
	НовыеПараметрыЗаписи.ИзменениеСопроводительнойЗаписки = ИзмененаСопроводительнаяЗаписка
		И ЗначениеЗаполнено(Объект.Ссылка);
		
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыЗаписи, НовыеПараметрыЗаписи);
		
КонецПроцедуры

// Возвращаемое значение:
//  Структура:
// * ЭтоНовыйДокумент - Булево
// * КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// * ОписанияСообщений - Неопределено,Массив Из См. ЭлектронныеДокументыЭДО.НовоеОписаниеСообщения
// * ИзменениеВидаДокумента - Булево
// * ИзменениеСопроводительнойЗаписки - Булево
// * ИзменениеМаршрутаПодписания - Булево
&НаСервереБезКонтекста
Функция НовыеПараметрыЗаписиДокумента()
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("ЭтоНовыйДокумент", Ложь);
	ПараметрыЗаписи.Вставить("КонтекстДиагностики", ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики());
	ПараметрыЗаписи.Вставить("ОписанияСообщений", Неопределено);
	ПараметрыЗаписи.Вставить("ИзменениеВидаДокумента", Ложь);
	ПараметрыЗаписи.Вставить("ИзменениеСопроводительнойЗаписки", Ложь);
	ПараметрыЗаписи.Вставить("ИзменениеМаршрутаПодписания", Ложь);
	Возврат ПараметрыЗаписи;
	
КонецФункции

&НаСервере
Функция ПроверитьЗаполнениеДокумента()
	
	Результат = НовыйРезультатПроверкиЗаполнения();
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = ИнтерфейсДокументовЭДОКлиентСервер.ТекстСообщенияОНезаполненностиРеквизитовДокумента(
			НСтр("ru='Организация'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект.Ссылка, "Организация", "Объект", Результат.Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ВидДокумента) Тогда
		ТекстСообщения = ИнтерфейсДокументовЭДОКлиентСервер.ТекстСообщенияОНезаполненностиРеквизитовДокумента(
			НСтр("ru='Вид'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект.Ссылка, "ВидДокумента", "Объект", Результат.Отказ);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Объект.Контрагент) 
		И Не ЭлектронныеДокументыЭДО.ЭтоВнутреннийВидДокумента(Объект.ВидДокумента) Тогда
		ТекстСообщения = ИнтерфейсДокументовЭДОКлиентСервер.ТекстСообщенияОНезаполненностиРеквизитовДокумента(
			НСтр("ru='Контрагент'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект.Ссылка, "Контрагент", "Объект", Результат.Отказ);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПроверитьЗаполнениеДокументаПослеПроверкиНастроек()
	
	Результат = НовыйРезультатПроверкиЗаполнения();
	
	Если Не ЗначениеЗаполнено(Объект.МаршрутПодписания) И Не ЗначениеЗаполнено(Объект.СписокПодписантов) Тогда
		ТекстСообщения = ИнтерфейсДокументовЭДОКлиентСервер.ТекстСообщенияОНезаполненностиРеквизитовДокумента(
			НСтр("ru='Маршрут подписания'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "ПредставлениеМаршрута",, Результат.Отказ);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция НовыйРезультатПроверкиЗаполнения()
	
	Результат = Новый Структура;
	Результат.Вставить("Отказ", Ложь);
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПроверитьНастройки()

	РеквизитыДокументаДляПроверкиНастроек = Новый Структура;
	РеквизитыДокументаДляПроверкиНастроек.Вставить("Организация", Объект.Организация);
	РеквизитыДокументаДляПроверкиНастроек.Вставить("Контрагент", Объект.Контрагент);
	РеквизитыДокументаДляПроверкиНастроек.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
	РеквизитыДокументаДляПроверкиНастроек.Вставить("ВидДокумента", Объект.ВидДокумента);
	Возврат ИнтерфейсДокументовЭДО.ПроверитьНастройкиОтправкиПоРеквизитамДокумента(
		РеквизитыДокументаДляПроверкиНастроек);

КонецФункции

&НаСервере
Функция ОписанияСообщенийДокумента()
	
	ОписанияСообщений = Новый Массив;
	
	Если ЭтоАдресВременногоХранилища(АдресОписанийСообщений) Тогда
		
		ОписанияСообщений = ПолучитьИзВременногоХранилища(АдресОписанийСообщений);
		
		Для Каждого ОписаниеСообщения Из ОписанияСообщений Цикл
			Если ОписаниеСообщения.ТипЭлементаРегламента =
				Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя Тогда
				ОписаниеСообщения.ДополнительнаяИнформация = СопроводительнаяЗапискаТекст;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
	
		ОписаниеСообщения = ЭлектронныеДокументыЭДО.НовоеОписаниеСообщения();
		
		ОписаниеСообщения.ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя;
		
		Если ТипДокумента = Перечисления.ТипыДокументовЭДО.Внутренний Тогда
			ОписаниеСообщения.Направление = Перечисления.НаправленияЭДО.Внутренний;
		ИначеЕсли ЭлектронныеДокументыЭДО.ЭтоТипДокументаИнтеркампани(ТипДокумента) Тогда
			ОписаниеСообщения.Направление = Перечисления.НаправленияЭДО.Интеркампани;
		Иначе
			ОписаниеСообщения.Направление = Перечисления.НаправленияЭДО.Исходящий;
		КонецЕсли;
		
		ОписаниеСообщения.ДополнительнаяИнформация = СопроводительнаяЗапискаТекст;
		
		ОписаниеСообщения.ВидСообщения = Объект.ВидДокумента;
		
		Если ЭтоАдресВременногоХранилища(АдресФайловНовогоДокумента) Тогда
			ДанныеФайлов = ПолучитьИзВременногоХранилища(АдресФайловНовогоДокумента);
			ОписаниеСообщения.Данные.Документ.ДвоичныеДанные = ДанныеФайлов.Основной.ДвоичныеДанные;
			ОписаниеСообщения.Данные.Документ.ИмяФайла = ДанныеФайлов.Основной.ИмяФайла;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИмяПроизвольногоФайла) Тогда
			ОписаниеСообщения.Данные.Документ.ИмяФайла = ИмяПроизвольногоФайла;
		КонецЕсли;
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСГИСЭПД") Тогда
			МодульОбменСГИСЭПД = ОбщегоНазначения.ОбщийМодуль("ОбменСГИСЭПД");
			Если МодульОбменСГИСЭПД.ЭтоДокументЭПД(ТипДокумента) Тогда
				ОписаниеСообщения.ТипЭлементаРегламента = МодульОбменСГИСЭПД.ПервыйТитулДокумента(ТипДокумента);
			КонецЕсли;
		КонецЕсли;
		
		ОписанияСообщений.Добавить(ОписаниеСообщения);
		
	КонецЕсли;
	
	Для Каждого ОписаниеСообщения Из ОписанияСообщений Цикл
		
		Если ЗначениеЗаполнено(Объект.Ссылка) И Не ЗначениеЗаполнено(ОписаниеСообщения.Ссылка) Тогда
			ОписаниеСообщения.Ссылка = СообщениеЭДОИнформацииОтправителя(Объект.Ссылка);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОписаниеСообщения.Данные.Содержание) Тогда
			Продолжить;
		КонецЕсли;
		
		СодержаниеСообщения = ЭлектронныеДокументыЭДО.НовоеСодержаниеСообщения();
		
		СодержаниеСообщения.ТипРегламента = Перечисления.ТипыРегламентовЭДО.Неформализованный;
		СодержаниеСообщения.НомерДокумента = Объект.НомерДокумента;
		СодержаниеСообщения.ДатаДокумента = Объект.ДатаДокумента;
		СодержаниеСообщения.СуммаДокумента = Объект.СуммаДокумента;
		СодержаниеСообщения.ЕстьМаркировка = Ложь;
		
		ОписаниеСообщения.Данные.Содержание = СодержаниеСообщения;
		
	КонецЦикла;
	
	Возврат ОписанияСообщений;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьПервичнуюНастройкуПередЗаписью(Результат, ОповещениеОЗавершении) Экспорт
	
	Если Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Ложь);
	Иначе
		ЗаполнитьДанныеДокументаПриИзмененииКлючевыхРеквизитов();
		НачатьЗаписьДокумента(ОповещениеОЗавершении);
		ОжидатьПолученияАсинхронныхДанных();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияЗаписиДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаКлиенте
Функция ТребуетсяЗаписатьДокумент()
	Возврат Не ЗначениеЗаполнено(Объект.Ссылка) Или Модифицированность;
КонецФункции

&НаСервере
Процедура УстановитьНовыйНомерДокумента(ТекущийОбъект)
	
	Если Не ТекущийОбъект.ЭтоНовый() Или ЗначениеЗаполнено(ТекущийОбъект.НомерДокумента)
		Или ЕстьВозможностьОтраженияВУчете Или РежимСозданияПакетаПоПредварительнымДанным Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ТекущийОбъект.Номер) Тогда
		ТекущийОбъект.УстановитьНовыйНомер();
	КонецЕсли;
	
	ТекущийОбъект.НомерДокумента = СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(ТекущийОбъект.Номер, "0");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СообщениеЭДОИнформацииОтправителя(Знач ЭлектронныйДокумент)
	Возврат ЭлектронныеДокументыЭДО.СообщениеОтправителя(ЭлектронныйДокумент);
КонецФункции

// Параметры:
//  РезультатСоздания - Неопределено
//                    - Структура:
//                      * ИдентификаторПакета - Неопределено - если пакет не удалось создать.
//                                            - УникальныйИдентификатор
//                      * КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  ПараметрыОповещения - Структура:
//                        * ОбработчикЗавершения - ОписаниеОповещения
//                        * ЭтоНовыйДокумент - Булево
//
&НаКлиенте
Процедура ОбновитьДанныеПослеОбъединенияДокументовВПакетПоПредварительнымДанным(РезультатСоздания,
	ПараметрыОповещения) Экспорт
	
	Если РезультатСоздания = Неопределено Тогда
		Возврат;
	КонецЕсли;

	РежимСозданияПакетаПоПредварительнымДанным = Ложь;
	ИдентификаторПакета = РезультатСоздания.ИдентификаторПакета;
	СоставПакета.Очистить();
	ПредварительныйПакетДокументов.Очистить();
	
	ПослеЗаписиПоИнтеграциям(ПараметрыОповещения.ОбработчикЗавершения, ПараметрыОповещения.ЭтоНовыйДокумент);
	
КонецПроцедуры

#Область ПроверкаВозможностиОтправкиВФормализованномВиде

&НаКлиенте
Процедура ПоказатьОкноПредложенияОтправкиВФормализованномВидеПослеОтветаНаВопрос(РезультатОтветаНаВопрос,
	ОповещениеОЗавершении) Экспорт
	
	Если РезультатОтветаНаВопрос = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатОтветаНаВопрос.БольшеНеЗадаватьЭтотВопрос Тогда
		ИнтерфейсДокументовЭДОКлиент.ЗаписатьНастройкуНеПоказыватьОкноПредложенияОтправкиВФормализованномВиде();
	КонецЕсли;
	
	Если РезультатОтветаНаВопрос.Значение = КодВозвратаДиалога.Да 
		Или РезультатОтветаНаВопрос.Значение = КодВозвратаДиалога.Нет Тогда
		ОкноОтправкиВФормализованномВидеПоказано = Истина;
	КонецЕсли;
	
	Если РезультатОтветаНаВопрос.Значение = КодВозвратаДиалога.Да Тогда
		Если ТребуетсяЗаписатьДокумент() Тогда
			ЗаписатьДокумент();
		КонецЕсли;
		Если ЗначениеЗаполнено(ОбъектыУчетаДокумента) И Не ЗначениеЗаполнено(ИдентификаторПакета) Тогда
			ПереформироватьДокумент();
		Иначе
			Оповещение = Новый ОписаниеОповещения("ОткрытьПодборОбъектовУчетаПослеОтветаНаВопрос", ЭтотОбъект);
			ТекстВопроса = НСтр("ru='Для отправки документа в формализованном виде необходимо указать объект учета. 
				|Выполнить подбор?'");
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
	КонецЕсли;
	
	Если РезультатОтветаНаВопрос.Значение = КодВозвратаДиалога.Нет Тогда
		НачатьЗаписьДокумента(ОповещениеОЗавершении);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТребуетсяФормализованныйФормат(Знач ТипРегламента, Знач ВидДокумента)
	
	Результат = Ложь;
	
	ТребуетсяПоказатьОкно = ИнтерфейсДокументовЭДО.ТребуетсяПоказыватьОкноПредложенияОтправкиВФормализованномВиде();
	Если Не ТребуетсяПоказатьОкно Тогда
		Возврат Результат;
	КонецЕсли;

	Если ТипРегламента = Перечисления.ТипыРегламентовЭДО.Неформализованный
		И ЭлектронныеДокументыЭДО.ДляВидаДокументаЕстьФормализованныйФорматФНС(ВидДокумента) Тогда
			
		ВидФормализованногоДокумента = ЭлектронныеДокументыЭДО.ВидДокументаФормализованногоФорматаФНС(ВидДокумента);
		Если ЗначениеЗаполнено(ИнтеграцияЭДО.СписокОперацийВидаДокумента(ВидФормализованногоДокумента)) Тогда
			Результат = Истина;
		КонецЕсли;
			
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьПодборОбъектовУчетаПослеОтветаНаВопрос(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Оповещение = Новый ОписаниеОповещения("ПереформироватьДокументПослеПодбораОбъектовУчета", ЭтотОбъект);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЭлектронныйДокумент", Объект.Ссылка);
		ПараметрыФормы.Вставить("ВидДокумента", Объект.ВидДокумента);
		ПараметрыФормы.Вставить("ДокументРаспознан", ЕстьВозможностьОтраженияВУчете);
		
		ИнтерфейсДокументовЭДОКлиент.ОткрытьПодборОбъектовУчетаЭлектронногоДокумента(ПараметрыФормы, Оповещение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ПроверкаВозможностиОтправкиВФормализованномВиде

#Область ОбработчикиСобытийЗаписиПоИнтеграциям

&НаСервере
Процедура ПередЗаписьюНаСервереПоИнтеграциям(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ИнициализироватьПараметрыЗаписи(ПараметрыЗаписи);
	
	Если ТекущийОбъект.ЭтоОблачныйЭДО Тогда
		ПередЗаписьюНаСервереИнтеграцияОблачногоЭДО(Отказ, ТекущийОбъект, ПараметрыЗаписи);
	Иначе
		ПередЗаписьюНаСервереИнтеграцияВстроенногоЭДО(Отказ, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервереИнтеграцияОблачногоЭДО(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.ЭтоНовыйДокумент Тогда
		УстановитьНовыйНомерДокумента(ТекущийОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервереИнтеграцияВстроенногоЭДО(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.ЭтоНовыйДокумент Тогда
		УстановитьНовыйНомерДокумента(ТекущийОбъект);
		
		ПараметрыЗаписи.ОписанияСообщений = ОписанияСообщенийДокумента();
			
		ЭлектронныеДокументыЭДО.ПередЗаписьюНовогоДокумента(ТекущийОбъект, ПараметрыЗаписи.ОписанияСообщений);
	КонецЕсли;
	
	Если ПараметрыЗаписи.ИзменениеСопроводительнойЗаписки
		И Не ЗначениеЗаполнено(ПараметрыЗаписи.ОписанияСообщений) Тогда
		ПараметрыЗаписи.ОписанияСообщений = ОписанияСообщенийДокумента();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервереПоИнтеграциям(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ТекущийОбъект.ЭтоОблачныйЭДО Тогда
		ПриЗаписиНаСервереИнтеграцияОблачногоЭДО(Отказ, ТекущийОбъект, ПараметрыЗаписи);
	Иначе
		ПриЗаписиНаСервереИнтеграцияВстроенногоЭДО(Отказ, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервереИнтеграцияОблачногоЭДО(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если Не ПараметрыЗаписи.ЭтоНовыйДокумент Тогда
		Возврат;
	КонецЕсли;
	
	СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.НеСформирован;
	
	РегистрыСведений.СостоянияДокументовЭДО.УстановитьСостояние(ТекущийОбъект.Ссылка, СостояниеЭДО);
	
	Если ЗначениеЗаполнено(ОбъектыУчетаДокумента) Тогда
		ОбъектыУчета = ОбъектыУчетаДокумента.Выгрузить().ВыгрузитьКолонку("ОбъектУчета");
		ИнтеграцияЭДО.УстановитьАктуальныйЭлектронныйДокумент(ОбъектыУчета, ТекущийОбъект.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервереИнтеграцияВстроенногоЭДО(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если ПараметрыЗаписи.ЭтоНовыйДокумент Тогда
		ЭлектронныеДокументыЭДО.ПриЗаписиНовогоДокумента(ТекущийОбъект, ПараметрыЗаписи.ОписанияСообщений, 
			ПараметрыЗаписи.КонтекстДиагностики, ОбъектыУчетаДокумента.Выгрузить().ВыгрузитьКолонку("ОбъектУчета"));
	КонецЕсли;
	
	Если ПараметрыЗаписи.ИзменениеВидаДокумента Тогда
		ЭлектронныеДокументыЭДО.ПриИзмененииВидаДокумента(ТекущийОбъект.Ссылка, ТекущийОбъект.ВидДокумента, Отказ);
	КонецЕсли;
	
	Если ПараметрыЗаписи.ИзменениеСопроводительнойЗаписки Тогда
		ЭлектронныеДокументыЭДО.ПриИзмененииСопроводительнойЗаписки(ПараметрыЗаписи.ОписанияСообщений, Отказ);
	КонецЕсли;
	
	Если ПараметрыЗаписи.ИзменениеМаршрутаПодписания Тогда
		ЭлектронныеДокументыЭДО.ПриИзмененииМаршрутаПодписания(ТекущийОбъект, Отказ);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервереПоИнтеграциям(ТекущийОбъект, ПараметрыЗаписи)
	
	ИзмененВидДокумента = Ложь;
	ИзмененаСопроводительнаяЗаписка = Ложь;
	ИзмененМаршрутПодписания = Ложь;
	
	Если ТекущийОбъект.ЭтоОблачныйЭДО Тогда
		ПослеЗаписиНаСервереИнтеграцияОблачногоЭДО(ТекущийОбъект, ПараметрыЗаписи);
	Иначе
		ПослеЗаписиНаСервереИнтеграцияВстроенногоЭДО(ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервереИнтеграцияОблачногоЭДО(ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ПараметрыЗаписи.ЭтоНовыйДокумент Тогда
		ОбновитьДанныеДляПросмотраДокумента();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервереИнтеграцияВстроенногоЭДО(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.ЭтоНовыйДокумент Тогда
		ЭлектронныеДокументыЭДО.ПослеЗаписиНовогоДокумента(ТекущийОбъект, ПараметрыЗаписи.КонтекстДиагностики);
	КонецЕсли;
	
	ОбновитьДанныеДляПросмотраДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиПоИнтеграциям(ОповещениеОЗавершении, ЭтоНовыйДокумент)
	Если Объект.ЭтоОблачныйЭДО Тогда
		ПослеЗаписиИнтеграцияОблачногоЭДО(ОповещениеОЗавершении, ЭтоНовыйДокумент);
	Иначе
		ПослеЗаписиИнтеграцияВстроенногоЭДО(ОповещениеОЗавершении, ЭтоНовыйДокумент);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиИнтеграцияОблачногоЭДО(ОповещениеОЗавершении, ЭтоНовыйДокумент)

	Если ЭтоНовыйДокумент Тогда
		ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(Объект.Ссылка);
	КонецЕсли;
	
	ОповещениеПослеВыполненияДействияПоЭДО = Новый ОписаниеОповещения(
		"ПослеВыполненияДействияПоЭДОИнтеграцияОблачногоЭДО", ЭтотОбъект, ОповещениеОЗавершении);
	
	ПараметрыВыполнения = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	
	ЗаполнитьПараметрыФормированияДокументаЭДО(ПараметрыВыполнения, ЭтоНовыйДокумент);
	
	ВыполнитьДействияПоЭДО(ПараметрыВыполнения, ЭтоПакетДокументов, ОповещениеПослеВыполненияДействияПоЭДО);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиИнтеграцияВстроенногоЭДО(ОповещениеОЗавершении, ЭтоНовыйДокумент)
	
	ОповеститьОбИзмененииСостоянияДокумента();
	
	Если ЭтоНовыйДокумент Тогда
		ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(Объект.Ссылка);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Истина);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЗаписиПоИнтеграциям

#Область ИнтеграцияОблачногоЭДО

&НаКлиенте
Процедура ЗаполнитьПараметрыФормированияДокументаЭДО(ПараметрыВыполненияДействийПоЭДО, ЭтоНовыйДокумент)
	
	ОписаниеДокументаПоФайлам = ОписаниеДокументаДляФормированияПоФайлам();
	
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ОписанияДокументовПоФайлам.Добавить(ОписаниеДокументаПоФайлам);
	
	Если ЭтоНовыйДокумент Тогда
		ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(ПараметрыВыполненияДействийПоЭДО.НаборДействий,
			ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Сформировать"));
	Иначе
		ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(ПараметрыВыполненияДействийПоЭДО.НаборДействий,
			ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Переформировать"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОписаниеДокументаДляФормированияПоФайлам()
	
	ОписаниеДокумента = ИнтерфейсДокументовЭДОКлиентСервер.НовоеОписаниеДокументаДляФормированияПоФайлам();
	
	ОписаниеДокумента.Идентификатор = Объект.ИдентификаторДокумента;
	ОписаниеДокумента.Организация = Объект.Организация;
	ОписаниеДокумента.Контрагент = Объект.Контрагент;
	ОписаниеДокумента.Договор = Объект.ДоговорКонтрагента;
	ОписаниеДокумента.ВидДокумента = Объект.ВидДокумента;
	ОписаниеДокумента.Номер = Объект.НомерДокумента;
	ОписаниеДокумента.Дата = Объект.ДатаДокумента;
	ОписаниеДокумента.Сумма = Объект.СуммаДокумента;
	
	ОписаниеДокумента.СопроводительнаяЗаписка = СопроводительнаяЗапискаТекст;
	
	Если Не ЭтоАдресВременногоХранилища(АдресФайловНовогоДокумента) Тогда
		Возврат ОписаниеДокумента;
	КонецЕсли;
	
	ФайлыДокумента = ПолучитьИзВременногоХранилища(АдресФайловНовогоДокумента);
	
	НовыеФайлыТитула = ИнтерфейсДокументовЭДОКлиентСервер.НовыеФайлыТитулаДокументаДляФормирования();
	НовыеФайлыТитула.Основной = ФайлыДокумента.Основной;
	НовыеФайлыТитула.Дополнительный = ФайлыДокумента.Дополнительный;
	
	ОписаниеДокумента.ФайлыТитулов.Добавить(НовыеФайлыТитула);
	
	Возврат ОписаниеДокумента;
	
КонецФункции

#КонецОбласти // ИнтеграцияОблачногоЭДО

#КонецОбласти // Запись

#Область ВыполнениеДействийПоЭДО

&НаКлиенте
Процедура УтвердитьПослеЗаписи(Результат, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ВыполнитьДействиеДляПакета = Ложь;
	Если ЭтоПакетнаяОбработка Тогда
		ВыполнитьДействиеДляПакета = ЭтоПакетДокументов;
	КонецЕсли;
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеУтвердитьИзКарточкиДокумента(ЭтотОбъект,
		ВыполнитьДействиеДляПакета);

КонецПроцедуры

&НаКлиенте
Процедура ПодписатьОтправитьПослеЗаписи(Результат, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ВыполнитьДействиеДляПакета = Ложь;
	Если ЭтоПакетнаяОбработка Тогда
		ВыполнитьДействиеДляПакета = ЭтоПакетДокументов;
	КонецЕсли;
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействияПодписатьОтправитьИзКарточкиДокумента(ЭтотОбъект, 
		ВыполнитьДействиеДляПакета);

КонецПроцедуры

&НаКлиенте
Процедура ПодписатьПослеЗаписи(Результат, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ВыполнитьДействиеДляПакета = Ложь;
	Если ЭтоПакетнаяОбработка Тогда
		ВыполнитьДействиеДляПакета = ЭтоПакетДокументов;
	КонецЕсли;
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеПодписатьИзКарточкиДокумента(ЭтотОбъект,
		ВыполнитьДействиеДляПакета);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПослеЗаписи(Результат, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ВыполнитьДействиеДляПакета = Ложь;
	Если ЭтоПакетнаяОбработка Тогда
		ВыполнитьДействиеДляПакета = ЭтоПакетДокументов;
	КонецЕсли;
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтправитьИзКарточкиДокумента(ЭтотОбъект,
		ВыполнитьДействиеДляПакета);

КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьПослеЗаписи(Результат, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ВыполнитьДействиеДляПакета = Ложь;
	Если ЭтоПакетнаяОбработка Тогда
		ВыполнитьДействиеДляПакета = ЭтоПакетДокументов;
	КонецЕсли;
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьИзКарточкиДокумента(ЭтотОбъект,
		ВыполнитьДействиеДляПакета);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПовторноПослеЗаписи(Результат, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ВыполнитьДействиеДляПакета = Ложь;
	Если ЭтоПакетнаяОбработка Тогда
		ВыполнитьДействиеДляПакета = ЭтоПакетДокументов;
	КонецЕсли;
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтправитьПовторноИзКарточкиДокумента(ЭтотОбъект,
		ВыполнитьДействиеДляПакета);

КонецПроцедуры

&НаКлиенте
Процедура ПринятьАннулированиеПослеЗаписи(Результат, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ВыполнитьДействиеДляПакета = Ложь;
	Если ЭтоПакетнаяОбработка Тогда
		ВыполнитьДействиеДляПакета = ЭтоПакетДокументов;
	КонецЕсли;
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеПринятьАннулированиеИзКарточкиДокумента(ЭтотОбъект, 
		ВыполнитьДействиеДляПакета);

КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьАннулированиеПослеЗаписи(Результат, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ВыполнитьДействиеДляПакета = Ложь;
	Если ЭтоПакетнаяОбработка Тогда
		ВыполнитьДействиеДляПакета = ЭтоПакетДокументов;
	КонецЕсли;
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьАннулированиеИзКарточкиДокумента(ЭтотОбъект, 
		ВыполнитьДействиеДляПакета);

КонецПроцедуры

&НаКлиенте
Процедура АннулироватьПослеЗаписи(Результат, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ВыполнитьДействиеДляПакета = Ложь;
	Если ЭтоПакетнаяОбработка Тогда
		ВыполнитьДействиеДляПакета = ЭтоПакетДокументов;
	КонецЕсли;
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеАннулироватьИзКарточкиДокумента(ЭтотОбъект,
		ВыполнитьДействиеДляПакета);

КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПослеЗаписи(Результат, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ВыполнитьДействиеДляПакета = Ложь;
	Если ЭтоПакетнаяОбработка Тогда
		ВыполнитьДействиеДляПакета = ЭтоПакетДокументов;
	КонецЕсли;
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеЗакрытьПринудительноИзКарточкиДокумента(ЭтотОбъект,
		ВыполнитьДействиеДляПакета);

КонецПроцедуры

&НаКлиенте
Процедура ВернутьВРаботуПослеЗаписи(Результат, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ВыполнитьДействиеДляПакета = Ложь;
	Если ЭтоПакетнаяОбработка Тогда
		ВыполнитьДействиеДляПакета = ЭтоПакетДокументов;
	КонецЕсли;
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеВернутьВРаботуИзКарточкиДокумента(ЭтотОбъект, 
		ВыполнитьДействиеДляПакета);

КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьПодписаниеПослеЗаписи(Результат, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ВыполнитьДействиеДляПакета = Ложь;
	Если ЭтоПакетнаяОбработка Тогда
		ВыполнитьДействиеДляПакета = ЭтоПакетДокументов;
	КонецЕсли;
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьПодписаниеИзКарточкиДокумента(ЭтотОбъект,
		ВыполнитьДействиеДляПакета);

КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияДействийПоЭДО(Результат, ПараметрыОповещения) Экспорт
	
	ИнтерфейсДокументовЭДОКлиент.ПослеВыполненияДействийПоЭДО(Результат, ПараметрыОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияДействияПоЭДОИнтеграцияОблачногоЭДО(Результат, ОповещениеОЗавершении) Экспорт
	
	ОбновитьДанныеПослеВыполненияДействийПоЭДО();
	
	ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Истина);
	
КонецПроцедуры

#КонецОбласти // ВыполнениеДействийПоЭДО

#Область Переформирование

&НаКлиенте
Процедура ПереформироватьДокумент(Настройки = Неопределено, Комментарий = "")
	ПереформироватьДокументПоИнтеграциям(Настройки, Комментарий);
КонецПроцедуры

&НаКлиенте
Процедура ПереформироватьДокументПоИнтеграциям(Настройки, Комментарий)
	Если Объект.ЭтоОблачныйЭДО Тогда
		ПереформироватьДокументИнтеграцияОблачногоЭДО(Настройки, Комментарий);
	Иначе
		ПереформироватьДокументИнтеграцияВстроенногоЭДО(Настройки, Комментарий);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПереформироватьДокументИнтеграцияОблачногоЭДО(Настройки, Комментарий)

	Если ТребуетсяЗаписатьДокумент() Тогда
		НачатьЗаписьДокумента(Новый ОписаниеОповещения);
	КонецЕсли;

	ПараметрыВыполнения = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	НаборДействий = ПараметрыВыполнения.НаборДействий;
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.Переформировать"));
	
	ВыполнитьДействияПоЭДО(ПараметрыВыполнения, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереформироватьДокументИнтеграцияВстроенногоЭДО(Настройки, Комментарий)

	Если Не ЗначениеЗаполнено(СообщениеЭДО) Тогда
		СообщениеЭДО = СообщениеЭДОИнформацииОтправителя(Объект.Ссылка);
	КонецЕсли;
	
	ПараметрыПереформирования = ИнтерфейсДокументовЭДОВызовСервера.ПараметрыПереформированияДокумента(СообщениеЭДО);
	ПараметрыПереформирования.Вставить("ИдентификаторПакета", ИдентификаторПакета);
	
	Если ЗначениеЗаполнено(Настройки) Тогда
		ПараметрыПереформирования.Вставить("Настройки", Настройки);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Комментарий) Тогда
		ПараметрыПереформирования.Вставить("Комментарий", Комментарий);
	КонецЕсли;

	Оповещение = Новый ОписаниеОповещения("ПереформироватьДокументПослеВыбораОснования", 
		ЭтотОбъект, ПараметрыПереформирования);
	Если ОбъектыУчетаДокумента.Количество() > 1
		И Объект.ТипРегламента <> ПредопределенноеЗначение("Перечисление.ТипыРегламентовЭДО.УПД") Тогда
		Основания.ПоказатьВыборЭлемента(Оповещение, НСтр("ru='Выберите документ для переформирования'"));
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, Основания[0]);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереформироватьДокументПослеПодбораОбъектовУчета(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	Если ОбъектыУчетаДокумента.Количество() Тогда
		ПереформироватьДокумент();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПереформироватьДокументПослеВыбораОснования(Результат, ПараметрыПереформирования) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		НачатьЗаписьДокумента(Новый ОписаниеОповещения);
	КонецЕсли;
	
	ПараметрыПереформирования.Вставить("ОбъектУчета", Результат.Значение);
		
	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияПереформированияДокумента", 
		ЭтотОбъект, ПараметрыПереформирования);
			
	ИнтерфейсДокументовЭДОКлиент.ПереформироватьДокумент(Оповещение, ПараметрыПереформирования);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияПереформированияДокумента(Результат, Контекст) Экспорт
	
	Если ОбработкаНеисправностейБЭДКлиентСервер.ЕстьОшибки(Результат.КонтекстДиагностики) Тогда
		ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(Результат.КонтекстДиагностики);
		Возврат;
	КонецЕсли;
	
	Если Результат.Переформирован Или Результат.СформированоИсправление Тогда
		ОбновитьДанныеПослеПереформированияДокумента(Результат.СформированныеДокументы);
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Переформирование'"),, 
			НСтр("ru = 'Сформирован новый документ'"), 
			БиблиотекаКартинок.ЭмблемаСервиса1СЭДО48, СтатусОповещенияПользователя.Информация);
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Документ переформирован'"));
		
		ОповеститьОбИзмененииСостоянияДокумента();
	Иначе
		Прочитать();
		
		Шаблон = НСтр("ru='Для документа ""%1"" уже существует электронный документ в формализованном виде'");
		ТекстСообщения = СтрШаблон(Шаблон, Результат.Контекст.Параметры.ОбъектУчета);
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, Результат.Контекст.Параметры.ОбъектУчета);
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеПереформированияДокумента(СформированныеДокументы = Неопределено)
	
	Если СформированныеДокументы <> Неопределено 
		И СформированныеДокументы.Количество() Тогда
			
		Для Каждого СформированныйДокумент Из СформированныеДокументы Цикл
			АктуальныйЭлектронныйДокумент = СформированныйДокумент.Ключ;
		КонецЦикла;
	Иначе
		АктуальныйЭлектронныйДокумент = ИнтеграцияЭДО.АктуальныйЭлектронныйДокументОбъектаУчета(
			Основания.ВыгрузитьЗначения()[0], Объект.ВидДокумента, Объект.Контрагент, Объект.ДоговорКонтрагента);
	КонецЕсли;

	Если ЗначениеЗаполнено(АктуальныйЭлектронныйДокумент) Тогда
		СоставПакета.Очистить();
		ТекущийДокументПакета = Неопределено;
		ТекущийИдентификаторДокумента = Неопределено;
		СообщениеЭДО = Неопределено;
		ОбъектДокумента = АктуальныйЭлектронныйДокумент.ПолучитьОбъект();
		ЗначениеВРеквизитФормы(ОбъектДокумента, "Объект");
		Прочитать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // Переформирование

#Область РаботаСПакетом

&НаКлиенте
Процедура СоставПакетаПослеАктивизацииСтроки()
	
	ТекущиеДанные = Элементы.СоставПакета.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
		Или (Не ЗначениеЗаполнено(ТекущиеДанные.ЭлектронныйДокумент)
		Или ТекущиеДанные.ЭлектронныйДокумент = Объект.Ссылка)
		И ТекущиеДанные.ИдентификаторДокумента = ТекущийИдентификаторДокумента Тогда
		Возврат;
	КонецЕсли;

	ТекущийДокументПакета = ТекущиеДанные.ЭлектронныйДокумент;
	ТекущийИдентификаторДокумента = ТекущиеДанные.ИдентификаторДокумента;

	Если ОповеститьОПрочтении Тогда
		Оповестить("ПрочтениеОбновитьСписок", ТекущийДокументПакета);
	КонецЕсли;
	
	Если Не РежимСозданияПакетаПоПредварительнымДанным И ТребуетсяЗаписатьДокумент() Тогда
		Обработчик = Новый ОписаниеОповещения("СоставПакетаПослеАктивизацииСтрокиПослеЗаписи", ЭтотОбъект);
		НачатьЗаписьДокумента(Обработчик);
	Иначе
		ПоказатьДокументПакета();
		ОжидатьПолученияАсинхронныхДанных();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьДокументПакета()
	
	АдресОписанийСообщений = "";
	АдресФайловНовогоДокумента = "";
	АдресСДаннымиФайловТитулов = "";
	
	ЛегкийИнтерфейсДокументовЭДО.ПрекратитьОжиданиеПолученияДанныхКомпонентовПросмотраДокумента(ОперацияПолученияДанных);
	
	Если РежимСозданияПакетаПоПредварительнымДанным Тогда
		ПоказатьДокументПредварительногоПакета();
	Иначе
		ЛегкийИнтерфейсДокументовЭДО.ПоказатьДокументПакетаКарточкиДокумента(ЭтотОбъект, ТекущийДокументПакета);
	КонецЕсли;

КонецПроцедуры

&НаСервере	
Процедура ПоказатьДокументПредварительногоПакета()
	
	ДанныеПредварительногоДокумента =
		ЛегкийИнтерфейсДокументовЭДО.ДанныеПредварительногоДокументаПакетаПоИдентификатору(ЭтотОбъект,
			ТекущийИдентификаторДокумента);
	Если ДанныеПредварительногоДокумента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеФайла = ПолучитьИзВременногоХранилища(ДанныеПредварительногоДокумента.АдресОписанияФайла);
	
	АдресФайловНовогоДокумента = ПоместитьВоВременноеХранилище(
		Новый Структура("Основной, Дополнительный", ОписаниеФайла), УникальныйИдентификатор);
	ИмяДобавляемогоФайла = ОписаниеФайла.ИмяФайла;
	РасширениеДобавляемогоФайла = ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ИмяДобавляемогоФайла);
	
	ЗаполнитьЗначенияСвойств(Объект, ДанныеПредварительногоДокумента, "ВидДокумента, ДатаДокумента, ДоговорКонтрагента,
		|Контрагент, НомерДокумента, Организация, СуммаДокумента, ТипРегламента");
	ТипДокумента = ДанныеПредварительногоДокумента.ТипДокумента;
	СопроводительнаяЗапискаТекст = ДанныеПредварительногоДокумента.СопроводительнаяЗаписка;
	ТекущийИдентификаторДокумента = ДанныеПредварительногоДокумента.ИдентификаторДокумента;
	
	ЗаполнитьДанныеДокументаПриИзмененииКлючевыхРеквизитов();
	
	ОбновитьДанныеДляПросмотраНовогоДокумента();
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыДобавленияВПакет()
	
	ПараметрыДобавленияВПакет = ПакетыДокументовЭДОКлиент.НовыеПараметрыДобавленияВПакет();
	ПараметрыДобавленияВПакет.ИдентификаторПакета = ИдентификаторПакета;
	ПараметрыДобавленияВПакет.ЭлектронныйДокумент = Объект.Ссылка;
	ПараметрыДобавленияВПакет.Отправитель = Объект.Организация;
	ПараметрыДобавленияВПакет.Получатель = Объект.Контрагент;
	ПараметрыДобавленияВПакет.Договор = Объект.ДоговорКонтрагента;
	ПараметрыДобавленияВПакет.ОтключитьТранслитерацию = ОтключитьТранслитерацию;
	ПараметрыДобавленияВПакет.ЭтоОблачныйЭДО = Объект.ЭтоОблачныйЭДО;
	
	Возврат ПараметрыДобавленияВПакет;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьВПакетФайлСКомпьютераПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	СпособыДобавления = ПакетыДокументовЭДОКлиент.СпособыДобавленияДокументаВПакет();
	
	ПакетыДокументовЭДОКлиент.ПослеВыбораСпособаДобавленияДокументаВПакет(
		СпособыДобавления.ДобавитьВПакетФайлСКомпьютера, ПараметрыДобавленияВПакет());

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВПакетПрисоединенныйФайлПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	СпособыДобавления = ПакетыДокументовЭДОКлиент.СпособыДобавленияДокументаВПакет();
	
	ПакетыДокументовЭДОКлиент.ПослеВыбораСпособаДобавленияДокументаВПакет(
		СпособыДобавления.ДобавитьВПакетПрисоединенныйФайл, ПараметрыДобавленияВПакет());

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВПакетДокументИнформационнойБазыПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	СпособыДобавления = ПакетыДокументовЭДОКлиент.СпособыДобавленияДокументаВПакет();
	
	ПакетыДокументовЭДОКлиент.ПослеВыбораСпособаДобавленияДокументаВПакет(
		СпособыДобавления.ДобавитьВПакетДокументИнформационнойБазы, ПараметрыДобавленияВПакет());

КонецПроцедуры

&НаКлиенте
Процедура СоставПакетаПослеАктивизацииСтрокиПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьДокументПакета();
	
	ОжидатьПолученияАсинхронныхДанных();

КонецПроцедуры

#КонецОбласти // РаботаСПакетом

#Область ВыгрузкаИПечать

&НаКлиенте
Процедура НапечататьЭлектронныеДокументыПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьФормуПечатиИзКарточкиДокумента(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДокументооборотЦеликомПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыгрузитьДокументооборотЦеликомИзКарточкиДокумента(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЭлектронныеДокументыВФорматеPDFПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыгрузитьДокументыВФорматеPDFИзКарточкиДокумента(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЭлектронныеДокументыДляФНСПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыгрузитьДокументыДляФНСИзКарточкиДокумента(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПротоколОбменаПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ИнтерфейсДокументовЭДОКлиент.ПоказатьПротоколОбмена(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка));

КонецПроцедуры

#КонецОбласти // ВыгрузкаИПечать

#Область ЗагрузкаФормализованногоДокументаXML

&НаКлиенте
Процедура ЗагрузитьФормализованныйДокументXMLСДискаПослеЗагрузки(ПомещенныйФайл, ДополнительныеПараметры) Экспорт
	
	Если ПомещенныйФайл = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТребуетсяЗаписатьДокумент() Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения(
			"ЗагрузитьФормализованныйДокументXMLСДискаПослеЗаписиДокумента", ЭтотОбъект, ПомещенныйФайл);
		НачатьЗаписьДокумента(ОповещениеОЗавершении);
		Возврат;
	КонецЕсли;
	
	ЗагрузитьФормализованныйДокументПоДаннымФайлаXML(ПомещенныйФайл);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФормализованныйДокументXMLСДискаПослеЗаписиДокумента(Результат, ПомещенныйФайл) Экспорт
	Если Результат Тогда
		ЗагрузитьФормализованныйДокументПоДаннымФайлаXML(ПомещенныйФайл);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФормализованныйДокументПоДаннымФайлаXML(ПомещенныйФайл)
	
	РезультатЗагрузки = ЗагрузитьФормализованныйДокументПоДаннымФайлаXMLНаСервере(ПомещенныйФайл);
	
	Если РезультатЗагрузки.Успех Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Файл успешно загружен.'"));
		ОжидатьПолученияАсинхронныхДанных();
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РезультатЗагрузки.СуществующийДокумент) Тогда
		ОбработатьНаличиеСуществующегоДокументаДляВыбранногоФайлаXML(РезультатЗагрузки.СуществующийДокумент);
		Возврат;
	КонецЕсли;
		
	Если РезультатЗагрузки.Ошибки.Количество() = 0 Тогда
		ПоказатьПредупреждение(,
			ИнтерфейсДокументовЭДОКлиентСервер.ТекстСообщенияОбОтсутствииВозможностиЗагрузитьФайлXML());
		Возврат;
	КонецЕсли;
	
	Если РезультатЗагрузки.Ошибки.Количество() > 1 Тогда
		ПараметрыФормы = Новый Структура("ПараметрыОшибки", РезультатЗагрузки.Ошибки);
		ОповещениеОЗавершении = Новый ОписаниеОповещения(
			"ЗагрузитьФормализованныйДокументПоДаннымФайлаXMLПринудительно", ЭтотОбъект, ПомещенныйФайл);

		ОткрытьФорму("Обработка.ИнтерфейсДокументовЭДО.Форма.СравнениеФормализованныхФайлов", ПараметрыФормы,
			ЭтотОбъект,,,, ОповещениеОЗавершении);
		
		Возврат;
	КонецЕсли;
		
	Для Каждого Ошибка Из РезультатЗагрузки.Ошибки Цикл
		Если Ошибка.Значение = Ложь
			И ЗначениеЗаполнено(Ошибка.Ключ) Тогда
			ПоказатьПредупреждение(, Ошибка.Ключ);
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФормализованныйДокументПоДаннымФайлаXMLПринудительно(Ответ, ПомещенныйФайл) Экспорт
	
	Если Ответ <> "Продолжить" Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ЗагрузитьФормализованныйДокументПоДаннымФайлаXMLНаСервере(ПомещенныйФайл, Истина);
	Если Результат.Успех Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Файл успешно загружен.'"));
		ОжидатьПолученияАсинхронныхДанных();
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.СуществующийДокумент) Тогда
		ОбработатьНаличиеСуществующегоДокументаДляВыбранногоФайлаXML(Результат.СуществующийДокумент);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьФормализованныйДокументПоДаннымФайлаXMLНаСервере(ПомещенныйФайл, ПринудительнаяЗагрузка = Ложь)
	
	ДанныеДокумента = Новый Структура("Ссылка, Организация", Объект.Ссылка, Объект.Организация);
	
	Результат = ИнтерфейсДокументовЭДО.ЗагрузитьФормализованныйДокументПоФайлуXML(ДанныеДокумента, ПомещенныйФайл,
		ПринудительнаяЗагрузка);
		
	Если Не Результат.Успех Тогда
		Возврат Результат;
	КонецЕсли;
	
	ОписаниеФайла = РаботаСФайламиБЭД.ОписаниеФайлаПоДаннымПомещенногоФайла(ПомещенныйФайл);
	
	ЗаполнитьВложенияИзОписанияФайла(ОписаниеФайла);
	
	ЗаполнитьДанныеДокументаПоПодготовленнымДаннымИзФайлаXML(Результат.Данные);
	
	ЗаписатьДокументНаСервере();
	Прочитать();
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьНаличиеСуществующегоДокументаДляВыбранногоФайлаXML(СуществующийДокумент)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
	ИнтерфейсДокументовЭДОКлиент.ОбработатьНаличиеСуществующегоДокументаДляВыбранногоФайлаXML(
		СуществующийДокумент, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти // ЗагрузкаФормализованногоДокументаXML

#Область НастройкиОтправкиФормирования

&НаКлиенте
Процедура ПослеИзмененияНастроекОтправкиИлиФормирования(Настройки, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Настройки) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕстьВозможностьОтраженияВУчете И ОбъектыУчетаДокумента.Количество() Тогда
		ПереформироватьДокумент(Настройки, НСтр("ru = 'Изменение настроек формирования'"));
	Иначе
		ПослеИзмененияНастроекОтправкиПроизвольногоДокумента(Настройки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеИзмененияНастроекОтправкиПроизвольногоДокумента(Настройки)
	
	Объект.ИдентификаторОрганизации = Настройки.ИдентификаторОтправителя;
	Объект.ИдентификаторКонтрагента = Настройки.ИдентификаторПолучателя;
	Объект.ТребуетсяИзвещение = Настройки.ТребуетсяИзвещениеОПолучении;
	Объект.ТребуетсяПодтверждение = Настройки.ТребуетсяОтветнаяПодпись;
	
	ЗаписатьДокумент();
	
КонецПроцедуры

#КонецОбласти // НастройкиОтправкиФормирования

#Область ОбработчикиОповещений

&НаКлиенте
Процедура ПослеСменыОтветственного(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Или Не Результат.Успех Тогда
		Возврат;
	КонецЕсли;

	ОбновитьДанныеЭлектронногоДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПроверкиПодписейДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) И Результат.Успех Тогда
		ОбновитьДанныеПослеИзмененияПодписейДокумента();
	КонецЕсли;
	
	ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(Результат.КонтекстДиагностики);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияРезультатаПроверкиПодписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьДанныеПослеИзмененияПодписейДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеДокументПослеОтветаНаВопрос(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено
		Или Ответ.Значение <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьПометкуУдаления();
	
	ОжидатьПолученияАсинхронныхДанных();
	
	Оповестить(ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияТекущихДелЭДО(), , ВладелецФормы);
	
	ОповеститьОбИзмененииСвязанныхДокументов = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораДоговораКонтрагента(ВыбранноеЗначение, Контекст = Неопределено) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ДоговорКонтрагента = ВыбранноеЗначение;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеДокументыУчетаПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ЛегкийИнтерфейсДокументовЭДОКлиент.ПоказатьРасширенныйПодборОбъектовУчетаИзКарточкиДокумента(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СменитьОтветственногоПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеСменыОтветственного", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.СменитьОтветственногоИзКарточкиДокумента(ЭтотОбъект, ОповещениеОЗавершении);

КонецПроцедуры

&НаКлиенте
Процедура СменитьОтветственногоПакетПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеСменыОтветственного", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.СменитьОтветственногоИзКарточкиДокумента(ЭтотОбъект, ОповещениеОЗавершении,
		Истина);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЖурналСобытийЭДОПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ЭлектронныеДокументыЭДОКлиент.ОткрытьЖурналДействийПоЭДО(Объект.Ссылка);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуЭлектронногоДокументаПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ИнтерфейсДокументовЭДОКлиент.ОткрытьКарточкуЭлектронногоДокумента(Объект.Ссылка);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьПолученияАсинхронныхДанных();
	
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьФайлИнформацииОтправителяДокумента(Объект.Ссылка, Объект.ЭтоОблачныйЭДО,
		УникальныйИдентификатор);

КонецПроцедуры

#КонецОбласти // ОбработчикиОповещений

#Область ОбработчикиКоманд

&НаСервере
Процедура ИзменитьПометкуУдаления()
	
	Объект.ПометкаУдаления = Не Объект.ПометкаУдаления;
	Записать();
	ИнтеграцияЭДО.УдалитьОбновитьЧерновик(Объект.Ссылка, Объект.ПометкаУдаления);
	Прочитать();
	
	СвязанныеОбъектыУчета = ОбъектыУчетаДокумента.Выгрузить().ВыгрузитьКолонку("ОбъектУчета");
	ИзменитьПометкуУдаленияСвязанныхОбъектовУчета(СвязанныеОбъектыУчета, Объект.ПометкаУдаления);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьПометкуУдаленияСвязанныхОбъектовУчета(ОбъектыУчета, ПометкаУдаления)
	
	Для Каждого ОбъектУчета Из ОбъектыУчета Цикл 
		ИнтеграцияЭДО.ИзменитьПометкуУдаленияЧерновикаЭлектронногоДокумента(ОбъектУчета, ПометкаУдаления);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьДоговорПослеВыбораОрганизации(ВыбранноеЗначение)
	
	Если Объект.Организация <> ВыбранноеЗначение Тогда
		Объект.ДоговорКонтрагента = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ПараметрыВыполненияДействийПоЭДО - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО
//  ПакетнаяОбработка - Булево
//  ОбработчикЗавершения - Неопределено,ОписаниеОповещения
&НаКлиенте
Процедура ВыполнитьДействияПоЭДО(ПараметрыВыполненияДействийПоЭДО, ПакетнаяОбработка = Ложь,
	ОбработчикЗавершения = Неопределено)

	ПараметрыОповещения = Неопределено;
	Если ОбработчикЗавершения <> Неопределено Тогда
		ПараметрыОповещения = Новый Структура("ОповещениеУспешногоЗавершения", ОбработчикЗавершения);
	КонецЕсли;
	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект, ПараметрыОповещения);
	
	Если ПакетнаяОбработка И ЗначениеЗаполнено(ИдентификаторПакета) Тогда
		ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ПакетыДокументов.Добавить(ИдентификаторПакета);
	Иначе
		ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ЭлектронныеДокументы.Добавить(Объект.Ссылка);
	КонецЕсли;
	
	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ПараметрыВыполненияДействийПоЭДО);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОбъектУчетаЭлектронногоДокумента()
	
	Если Не ЗначениеЗаполнено(ОбъектыУчетаДокумента) Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, ОбъектыУчетаДокумента[0].ОбъектУчета);
	Закрыть();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВключитьПолеПредпросмотраPDFФайловНаСервере(Знач ВключитьПоле)
	НастройкиЭДО.УстановитьИспользованиеПросмотраPDFДокументовЭДО(ВключитьПоле);
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти // ПодключаемыеКоманды

#КонецОбласти // ОбработчикиКоманд

#КонецОбласти // СлужебныеПроцедурыИФункции
