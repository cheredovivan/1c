#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДанныеДоступностиКоманды = РаботаСЗадачами.ДанныеДоступностиКомандыЗадач(
		Параметры.ЗадачиКОтмене,
		Метаданные.Документы.Задача.Команды.Отменить);
	Если Не ДанныеДоступностиКоманды.Доступность Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Невозможно выполнить команду ""%1"" для указанных задач'"),
			Метаданные.Документы.Задача.Команды.Отменить);
	КонецЕсли;
	
	ЗадачиКОтмене.ЗагрузитьЗначения(ДанныеДоступностиКоманды.ПодходящиеЗадачи);
	
	
	ЗадачКОтмене = ЗадачиКОтмене.Количество();
	Если ЗадачКОтмене > 1 Тогда
		
		КлючСохраненияПоложенияОкна = "НесколькоЗадач";
		
		Заголовок = СтрШаблон(НСтр("ru = 'Отмена задач (%1 шт)'"), ЗадачКОтмене);
				
		МассивПредметыКОтмене = РаботаСЗадачами.ПредметыКОтмене(ЗадачиКОтмене.ВыгрузитьЗначения());
		
		Для Каждого КлючИЗначение Из МассивПредметыКОтмене Цикл
			
			ПредметКОтмене = КлючИЗначение.Значение;
			ПредставлениеВидаПредметаКОтмене =
				РаботаСЗадачами.ПредставлениеВидаПредметаКОтмене(ПредметКОтмене);
			
			СтрокаПредметаКОтмене = ПредметыКОтмене.Добавить();
			СтрокаПредметаКОтмене.Значение = ПредметКОтмене;
			СтрокаПредметаКОтмене.Представление = СтрШаблон(
				"%1: %2",
				ПредставлениеВидаПредметаКОтмене,
				Строка(ПредметКОтмене));
			
		КонецЦикла;
		
		Элементы.ПредметКОтмене.Видимость = Ложь;
		Элементы.ПредметыКОтмене.Видимость = Истина;
		
	Иначе
		
		КлючСохраненияПоложенияОкна = "ОднаЗадача";
		
		Заголовок = НСтр("ru = 'Отмена задачи'");
		
		ПредметКОтмене = РаботаСЗадачами.ПредметКОтмене(ЗадачиКОтмене[0].Значение);
		
		Элементы.ПредметКОтмене.Видимость = Истина;
		Элементы.ПредметКОтмене.Заголовок =
			РаботаСЗадачами.ПредставлениеВидаПредметаКОтмене(ПредметКОтмене);
		
		Элементы.ПредметыКОтмене.Видимость = Ложь;
		
	КонецЕсли;
	
	ВсеПодзадачиКОтмене = Новый Массив;
	Для Каждого СтрокаЗадачаКОтмене Из ЗадачиКОтмене Цикл
		
		ЗадачаКОтмене = СтрокаЗадачаКОтмене.Значение;
		
		ПорцияПодзадачКОтмене = РаботаСЗадачами.ПодзадачиКОтмене(ЗадачаКОтмене);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			ВсеПодзадачиКОтмене,
			ПорцияПодзадачКОтмене,
			Истина);
		
	КонецЦикла;
	
	ПраваПоОбъектам = ДокументооборотПраваДоступа.ПраваПользователяПоОднотипнымОбъектам(ВсеПодзадачиКОтмене);
	
	ПраваЧтенияПоЗадачам = Новый Соответствие;
	
	Для Каждого СтрокаПрав Из ПраваПоОбъектам Цикл
		
		Если ПраваЧтенияПоЗадачам[СтрокаПрав.ОбъектДоступа] = Неопределено Тогда
			ПраваЧтенияПоЗадачам[СтрокаПрав.ОбъектДоступа] = Ложь;
		КонецЕсли;
		
		ПраваЧтенияПоЗадачам[СтрокаПрав.ОбъектДоступа] =
			ПраваЧтенияПоЗадачам[СтрокаПрав.ОбъектДоступа] Или СтрокаПрав.Чтение;
		
	КонецЦикла;
	
	ТекстОписаниеСкрыто = НСтр("ru = 'Описание скрыто'");
	
	УстановитьПривилегированныйРежим(Истина);
	РеквизитыЗадач = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		ВсеПодзадачиКОтмене,
		"Заголовок, Номер, Автор, Срок");
	УстановитьПривилегированныйРежим(Ложь);
	
	Для Каждого ПодзадачаКОтмене Из ВсеПодзадачиКОтмене Цикл
		
		РеквизитыЗадачи = РеквизитыЗадач[ПодзадачаКОтмене];
		
		ПредставлениеЗаголовка =
			?(ПраваЧтенияПоЗадачам[ПодзадачаКОтмене] = Истина,
				РеквизитыЗадачи.Заголовок,
				ТекстОписаниеСкрыто);
		ПредставлениеНомера = Документы.Задача.ПредставлениеНомера(РеквизитыЗадачи.Номер);
		
		Если ЗначениеЗаполнено(РеквизитыЗадачи.Срок) Тогда
			ПредставлениеПодзадачи = СтрШаблон(
				НСтр("ru = '%1 № %2 от %3 до %4'"),
				ПредставлениеЗаголовка,
				ПредставлениеНомера,
				РаботаСЗадачамиПовтИсп.ПредставлениеУчастника(РеквизитыЗадачи.Автор),
				Формат(РеквизитыЗадачи.Срок, Метаданные.Документы.Задача.Реквизиты.Срок));
		Иначе
			ПредставлениеПодзадачи = СтрШаблон(
				НСтр("ru = '%1 № %2 от %3'"),
				ПредставлениеЗаголовка,
				ПредставлениеНомера,
				РаботаСЗадачамиПовтИсп.ПредставлениеУчастника(РеквизитыЗадачи.Автор));
		КонецЕсли;
		
		ПодзадачиКОтмене.Добавить(ПодзадачаКОтмене, ПредставлениеПодзадачи);
		
	КонецЦикла;
	
	Элементы.ГруппаПодзадачиКОтмене.Видимость = ПодзадачиКОтмене.Количество() > 0;
	Элементы.ГруппаПодзадачиКОтмене.Заголовок =
		СтрокаСЧислом(
			НСтр("ru = ';При отмене задачи будет отменена %1 подзадача;;При отмене задачи будет отменено %1 подзадачи;При отмене задачи будет отменено %1 подзадач;При отмене задачи будет отменено %1 подзадачи'"),
			ПодзадачиКОтмене.Количество(),
			ВидЧисловогоЗначения.Количественное);
	Если Элементы.ГруппаПодзадачиКОтмене.Видимость Тогда
		КлючСохраненияПоложенияОкна = КлючСохраненияПоложенияОкна + "ЕстьПодзадачи";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПредметыКОтмене

&НаКлиенте
Процедура ПредметыКОтменеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, ДанныеСтроки.Значение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПодзадачиКОтмене

&НаКлиенте
Процедура ПодзадачиКОтменеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, ДанныеСтроки.Значение);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отменить(Команда)
	
	Закрыть(ПричинаОтмены);
	
КонецПроцедуры

#КонецОбласти