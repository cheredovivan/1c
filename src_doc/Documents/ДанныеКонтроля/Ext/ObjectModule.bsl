#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НаКонтроле = НаКонтроле();
	Просрочено = Просрочено();
	
	НаборЗаписей = РегистрыСведений.РеестрКонтролей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Основание.Установить(Основание);
	
	Для Каждого СтрокаПолучателя Из Получатели Цикл
		Запись = НаборЗаписей.Добавить();
		Запись.Пользователь = СтрокаПолучателя.Пользователь;
		Запись.ДатаСортировки = ДатаСортировки;
		Запись.Основание = Основание;
		Запись.НаКонтроле = НаКонтроле;
		Запись.Просрочено = Просрочено;
		Запись.ДатаПостановкиНаКонтроль = ДатаПостановкиНаКонтроль;
		Запись.ИсполнителиСтрокой = ИсполнителиСтрокой;
		Запись.Контролер = Контролер;
		Запись.ЧтоКонтролируем = ЧтоКонтролируем;
		Запись.СнятСКонтроля = СнятСКонтроля;
		Запись.СрокИсполнения = СрокИсполнения;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	НаКонтроле = НаКонтроле();
	Просрочено = Просрочено();
	
	Движения.КоличествоКонтролей.Записывать = Истина;
	Для Каждого СтрокаПолучателя Из Получатели Цикл
		Движение = Движения.КоличествоКонтролей.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Пользователь = СтрокаПолучателя.Пользователь;
		Движение.НаКонтроле = ?(НаКонтроле, 1, 0);
		Движение.Просрочено = ?(Просрочено, 1, 0);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует признак "На контроле".
// 
// Возвращаемое значение:
//  Булево.
// 
Функция НаКонтроле()
	
	НаКонтроле = Не СнятСКонтроля;
	
	Возврат НаКонтроле;
	
КонецФункции

// Формирует признак "На контроле".
// 
// Возвращаемое значение:
//  Булево
// 
Функция Просрочено()
	
	Просрочено = Не СнятСКонтроля И КонтрольПросрочен;
	
	Возврат Просрочено;
	
КонецФункции

#КонецОбласти

#КонецЕсли