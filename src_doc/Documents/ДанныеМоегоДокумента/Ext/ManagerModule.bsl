#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Отражает мои документы по документу немедленно.
// 
// Параметры:
//  ДокументПредприятия - СправочникСсылка.ДокументыПредприятия
// 
Процедура ОтразитьПоДокументуНемедленно(ДокументПредприятия) Экспорт
	
	ЧейДокумент = РегистрыСведений.МоиДокументы.ЧейДокумент(ДокументПредприятия);
	Для Каждого Сотрудник Из ЧейДокумент Цикл
		ОтразитьПоСотрудникуНемедленно(ДокументПредприятия, Сотрудник);
	КонецЦикла;
	
КонецПроцедуры

// Отражает мои документы по документу и сотруднику немедленно.
// 
// Параметры:
//  ДокументПредприятия - СправочникСсылка.ДокументыПредприятия
//  Сотрудник - СправочникСсылка.Сотрудники
// 
Процедура ОтразитьПоСотрудникуНемедленно(ДокументПредприятия, Сотрудник) Экспорт
	
	ПользователиСотрудника = Сотрудники.ПользователиСотрудника(Сотрудник);
	Для Каждого Пользователь Из ПользователиСотрудника Цикл
		//@skip-check query-in-loop
		Отразить(ДокументПредприятия, Пользователь);
	КонецЦикла;
	
КонецПроцедуры

// Отражает мой документ по документу и сотруднику отложенно.
// 
// Параметры:
//  ДокументПредприятия - СправочникСсылка.ДокументыПредприятия
//  Сотрудник - СправочникСсылка.Сотрудники
// 
Процедура ОтразитьПоСотрудникуОтложенно(ДокументПредприятия, Сотрудник) Экспорт
	
	ЗависимыйОбъектМетаданных = Метаданные.Документы.ДанныеМоегоДокумента.ПолноеИмя();
	ВлияющийОбъектМетаданных = Метаданные.Справочники.ДокументыПредприятия.ПолноеИмя();
	КлючВлияющихДанных = ДокументПредприятия;
	ИзмененияВлияющихДанных = Новый Структура("Сотрудник", Сотрудник);
	
	РегистрыСведений.ОчередьОбновленияКэширующихДанных.Добавить(
		ЗависимыйОбъектМетаданных,
		ВлияющийОбъектМетаданных,
		КлючВлияющихДанных,
		ИзмененияВлияющихДанных);
	
КонецПроцедуры

// Отражает мой документ.
// 
// Параметры:
//  ДокументПредприятия - СправочникСсылка.ДокументыПредприятия
//  Пользователь - СправочникСсылка.Пользователи
// 
Процедура Отразить(ДокументПредприятия, Пользователь) Экспорт
	
	УстановитьОтключениеБезопасногоРежима(Истина);
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ДокументПредприятия) Или Не ЗначениеЗаполнено(Пользователь) Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.ДанныеМоегоДокумента");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("ДокументПредприятия", ДокументПредприятия);
		ЭлементБлокировки.УстановитьЗначение("Пользователь", Пользователь);
		Блокировка.Заблокировать();
		
		ДанныеМоегоДокумента = ДанныеМоегоДокумента(ДокументПредприятия, Пользователь);
		
		ДанныеМоегоДокументаСсылка = НайтиДанныеМоегоДокумента(ДокументПредприятия, Пользователь);
		
		Если Не ЗначениеЗаполнено(ДанныеМоегоДокументаСсылка) И ДанныеМоегоДокумента.ПометкаУдаления Тогда
			
			// Данные не нужны, в базе их нет, ничего делать не нужно.
			
		Иначе
			
			Если ЗначениеЗаполнено(ДанныеМоегоДокументаСсылка) Тогда
				ДанныеМоегоДокументаОбъект = ДанныеМоегоДокументаСсылка.ПолучитьОбъект();
			Иначе
				ДанныеМоегоДокументаОбъект = СоздатьДокумент();
			КонецЕсли;
			
			СтарыеДанные = ОбщегоНазначенияДокументооборот.ДанныеОбъекта(ДанныеМоегоДокументаОбъект);
			ЗаполнитьДанныеОбъекта(ДанныеМоегоДокументаОбъект, ДанныеМоегоДокумента);
			НовыеДанные = ОбщегоНазначенияДокументооборот.ДанныеОбъекта(ДанныеМоегоДокументаОбъект);
			ИзмененыДанные = Не ОбщегоНазначенияДокументооборот.ДанныеСовпадают(СтарыеДанные, НовыеДанные);
			
			Если ИзмененыДанные Тогда
				ДанныеМоегоДокументаОбъект.Дата = ТекущаяДатаСеанса();
				Если Не ДанныеМоегоДокументаОбъект.ПометкаУдаления Тогда
					ДанныеМоегоДокументаОбъект.Записать(РежимЗаписиДокумента.Проведение);
				ИначеЕсли ДанныеМоегоДокументаОбъект.ПометкаУдаления И ДанныеМоегоДокументаОбъект.Проведен Тогда
					ДанныеМоегоДокументаОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				ИначеЕсли ДанныеМоегоДокументаОбъект.ПометкаУдаления И Не ДанныеМоегоДокументаОбъект.Проведен Тогда
					ДанныеМоегоДокументаОбъект.Записать(РежимЗаписиДокумента.Запись);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Находит данные моего документа.
// 
// Параметры:
//  ДокументПредприятия - СправочникСсылка.ДокументыПредприятия
//  Пользователь - СправочникСсылка.Пользователи
// 
// Возвращаемое значение:
//  ДокументСсылка.ДанныеМоегоДокумента
// 
Функция НайтиДанныеМоегоДокумента(ДокументПредприятия, Пользователь) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДанныеМоегоДокумента.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ДанныеМоегоДокумента КАК ДанныеМоегоДокумента
		|ГДЕ
		|	ДанныеМоегоДокумента.ДокументПредприятия = &ДокументПредприятия
		|	И ДанныеМоегоДокумента.Пользователь = &Пользователь");
	Запрос.УстановитьПараметр("ДокументПредприятия", ДокументПредприятия);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ДанныеМоегоДокументаСсылка = ВыборкаДетальныеЗаписи.Ссылка;
	Иначе
		ДанныеМоегоДокументаСсылка = ПустаяСсылка();
	КонецЕсли;
	
	Возврат ДанныеМоегоДокументаСсылка;
	
КонецФункции

// Обновляет данные по сроку обновления.
// 
Процедура ОбновитьПоСроку() Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьМоиДокументы") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДанныеМоегоДокумента.ДокументПредприятия КАК ДокументПредприятия,
		|	ДанныеМоегоДокумента.Пользователь КАК Пользователь
		|ИЗ
		|	Документ.ДанныеМоегоДокумента КАК ДанныеМоегоДокумента
		|ГДЕ
		|	ДанныеМоегоДокумента.СрокОбновления < &СрокОбновления");
	Запрос.УстановитьПараметр("СрокОбновления", ТекущаяДатаСеанса());
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Попытка
			//@skip-check query-in-loop
			Отразить(ВыборкаДетальныеЗаписи.ДокументПредприятия, ВыборкаДетальныеЗаписи.Пользователь);
		Исключение
			ЗаписьЖурналаРегистрации(
				РаботаСВиджетами.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Если ТранзакцияАктивна() Тогда
				ВызватьИсключение;
			КонецЕсли;
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

// Влияющие реквизиты документов предприятия.
// 
// Возвращаемое значение:
//  Строка
// 
Функция ВлияющиеРеквизитыДокументовПредприятия() Экспорт
	
	ВлияющиеРеквизиты =
		"ПометкаУдаления, ВидДокумента, ДатаРегистрации, ДатаСоздания, ДатаПодписания,
		|Проект, РезультатПодписания, СрокИсполнения, Наименование, Стороны";
	
	Возврат ВлияющиеРеквизиты;
	
КонецФункции

// Влияющие реквизиты видов документов предприятия.
// 
// Возвращаемое значение:
//  Строка
// 
Функция ВлияющиеРеквизитыВидовДокументовПредприятия() Экспорт
	
	ВлияющиеРеквизиты =
		"ИспользоватьСрокИсполнения, ЯвляетсяВходящейКорреспонденцией, ЯвляетсяИсходящейКорреспонденцией";
	
	Возврат ВлияющиеРеквизиты;
	
КонецФункции

// Влияющие реквизиты корреспонденции.
// 
// Возвращаемое значение:
//  Строка
// 
Функция ВлияющиеРеквизитыКорреспонденции() Экспорт
	
	ВлияющиеРеквизиты = "Корреспонденты";
	
	Возврат ВлияющиеРеквизиты;
	
КонецФункции

// Влияющие реквизиты задач.
// 
// Возвращаемое значение:
//  Строка
// 
Функция ВлияющиеРеквизитыЗадач() Экспорт
	
	ВлияющиеРеквизиты = "Автор, Срок, Проведен, ПометкаУдаления, Приложения";
	
	Возврат ВлияющиеРеквизиты;
	
КонецФункции

// Влияющие реквизиты действий задач.
// 
// Возвращаемое значение:
//  Строка
// 
Функция ВлияющиеРеквизитыДействийЗадач() Экспорт
	
	ВлияющиеРеквизиты = "ОсобыйСрок, СостояниеУчастникаЗадачи, Исполнитель, Проведен, ПометкаУдаления";
	
	Возврат ВлияющиеРеквизиты;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

#Область ОбновлениеКэширующихДанных

// Обрабатывает обновление кэширующих данных.
// 
// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса:
//   * ОтметкаВремени - ОпределяемыйТип.ОтметкаВремени
//   * ЗависимыйОбъектМетаданных - СправочникСсылка.ИдентификаторыОбъектовМетаданных
//   * ВлияющийОбъектМетаданных - СправочникСсылка.ИдентификаторыОбъектовМетаданных
//   * КлючВлияющихДанных - ЛюбаяСсылка
//   * Автор - СправочникСсылка.Пользователи
//   * ЗагрузкаОбработанныхДанныхИзДругойСистемы - Булево
//   * ИзмененияВлияющихДанных - ХранилищеЗначения
//   * Попыток - Число
//   * ДатаКОбработке - Дата
// 
Процедура ОбновитьКэширующиеДанные(Выборка) Экспорт
	
	Если ТипЗнч(Выборка.КлючВлияющихДанных) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда
		
		ИзмененияВлияющихДанных = Выборка.ИзмененияВлияющихДанных.Получить();
		Если ТипЗнч(ИзмененияВлияющихДанных) = Тип("Структура") И ИзмененияВлияющихДанных.Свойство("Сотрудник") Тогда
			ОтразитьПоСотрудникуНемедленно(Выборка.КлючВлияющихДанных, ИзмененияВлияющихДанных.Сотрудник);
		Иначе
			ОтразитьПоДокументуНемедленно(Выборка.КлючВлияющихДанных);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Выборка.КлючВлияющихДанных) = Тип("СправочникСсылка.ВидыДокументов") Тогда
		
		ОбновитьКэширующиеДанныеПоВидуДокумента(Выборка);
		
	ИначеЕсли ТипЗнч(Выборка.КлючВлияющихДанных) = Тип("ДокументСсылка.Корреспонденция") Тогда
		
		Корреспонденция = Выборка.КлючВлияющихДанных; // ДокументСсылка.Корреспонденция
		ДокументПредприятия = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Корреспонденция, "Основание");
		ОтразитьПоДокументуНемедленно(ДокументПредприятия);
		
	ИначеЕсли ТипЗнч(Выборка.КлючВлияющихДанных) = Тип("СправочникСсылка.Сотрудники") Тогда
		
		ОбновитьКэширующиеДанныеПоСотрудники(Выборка);
		
	ИначеЕсли ТипЗнч(Выборка.КлючВлияющихДанных) = Тип("ДокументСсылка.Задача") Тогда
		
		Задача = Выборка.КлючВлияющихДанных; // ДокументСсылка.Задача
		Приложения =
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "Приложения").Выгрузить().ВыгрузитьКолонку("Приложение");
		ПользователиИзКэша = Неопределено;
		Для Каждого Приложение Из Приложения Цикл
			Если ТипЗнч(Приложение) <> Тип("СправочникСсылка.ДокументыПредприятия") Тогда
				Продолжить;
			КонецЕсли;
			Если ПользователиИзКэша = Неопределено Тогда
				РеестрыИзКэша = РаботаСЗадачами.РеестрыИзКэша(Задача);
				ПользователиИзКэша = РаботаСЗадачами.ПользователиРеестров(РеестрыИзКэша, Перечисления.ТипыРеестров.ЗадачиОтМеня);
			КонецЕсли;
			Для Каждого Пользователь Из ПользователиИзКэша Цикл
				Отразить(Приложение, Пользователь);
			КонецЦикла;
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Выборка.КлючВлияющихДанных) = Тип("ДокументСсылка.ДействиеЗадачи") Тогда
		
		ДействиеЗадачи = Выборка.КлючВлияющихДанных; // ДокументСсылка.ДействиеЗадачи
		Задача = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДействиеЗадачи, "Задача");
		Приложения =
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "Приложения").Выгрузить().ВыгрузитьКолонку("Приложение");
		ПользователиИзКэша = Неопределено;
		Для Каждого Приложение Из Приложения Цикл
			Если ТипЗнч(Приложение) <> Тип("СправочникСсылка.ДокументыПредприятия") Тогда
				Продолжить;
			КонецЕсли;
			Если ПользователиИзКэша = Неопределено Тогда
				РеестрыИзКэша = РаботаСЗадачами.РеестрыИзКэша(ДействиеЗадачи);
				ПользователиИзКэша = РаботаСЗадачами.ПользователиРеестров(РеестрыИзКэша, Перечисления.ТипыРеестров.ЗадачиМне);
			КонецЕсли;
			Для Каждого Пользователь Из ПользователиИзКэша Цикл
				Отразить(Приложение, Пользователь);
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует стркутуру данных контроля.
// 
// Возвращаемое значение:
//  Структура:
//   * ПометкаУдаления - Булево
//   * БезОтвета - Булево
//   * ВидДокумента - СправочникСсылка.ВидыДокументов
//   * ДатаСортировки - Дата
//   * ДобавленВручную - Булево
//   * ДокументПредприятия - СправочникСсылка.ДокументыПредприятия
//   * Истекает - Булево
//   * НазначенаЗадачаМне - Булево
//   * ОтправленаЗадачаОтМеня - Булево
//   * Пользователь - СправочникСсылка.Пользователи
//   * ПредставлениеДокумента - Строка
//   * Проект - СправочникСсылка.Проекты
//   * Просрочен - Булево
//   * СрокОбновления - Дата
//   * ЯвляюсьАвторомДокумента - Булево
//   * ЯвляюсьАвторомУтвержденияДокумента - Булево
//   * ЯвляюсьОтветственнымЗаДокумент - Булево
//   * ЯвляюсьПодписантомДокумента - Булево
// 
Функция НовыеДанныеМоегоДокумента()
	
	ДанныеМоегоДокумента = Новый Структура;
	ДанныеМоегоДокумента.Вставить("ПометкаУдаления", Ложь);
	ДанныеМоегоДокумента.Вставить("БезОтвета", Ложь);
	ДанныеМоегоДокумента.Вставить("ВидДокумента", Справочники.ВидыДокументов.ПустаяСсылка());
	ДанныеМоегоДокумента.Вставить("ДатаСортировки", Дата(1, 1, 1));
	ДанныеМоегоДокумента.Вставить("ДобавленВручную", Ложь);
	ДанныеМоегоДокумента.Вставить("ДокументПредприятия", Справочники.ДокументыПредприятия.ПустаяСсылка());
	ДанныеМоегоДокумента.Вставить("Истекает", Ложь);
	ДанныеМоегоДокумента.Вставить("НазначенаЗадачаМне", Ложь);
	ДанныеМоегоДокумента.Вставить("ОтправленаЗадачаОтМеня", Ложь);
	ДанныеМоегоДокумента.Вставить("Пользователь", Справочники.Пользователи.ПустаяСсылка());
	ДанныеМоегоДокумента.Вставить("ПредставлениеДокумента", "");
	ДанныеМоегоДокумента.Вставить("Проект", Справочники.Проекты.ПустаяСсылка());
	ДанныеМоегоДокумента.Вставить("Просрочен", Ложь);
	ДанныеМоегоДокумента.Вставить("СрокОбновления", ОбщегоНазначенияДокументооборотКлиентСервер.МаксимальнаяДата());
	ДанныеМоегоДокумента.Вставить("ЯвляюсьАвторомДокумента", Ложь);
	ДанныеМоегоДокумента.Вставить("ЯвляюсьАвторомУтвержденияДокумента", Ложь);
	ДанныеМоегоДокумента.Вставить("ЯвляюсьОтветственнымЗаДокумент", Ложь);
	ДанныеМоегоДокумента.Вставить("ЯвляюсьПодписантомДокумента", Ложь);
	
	Возврат ДанныеМоегоДокумента;
	
КонецФункции

// Формирует актуальные данные моего документа.
// 
// Параметры:
//  ДокументПредприятия - СправочникСсылка.ДокументыПредприятия
//  Пользователь - СправочникСсылка.Пользователи
// 
// Возвращаемое значение:
//  См. НовыеДанныеМоегоДокумента
// 
Функция ДанныеМоегоДокумента(ДокументПредприятия, Пользователь)
	
	ДанныеМоегоДокумента = НовыеДанныеМоегоДокумента();
	ДанныеМоегоДокумента.ДокументПредприятия = ДокументПредприятия;
	ДанныеМоегоДокумента.Пользователь = Пользователь;
	
	ВлияющиеРеквизитыДокументовПредприятия = ВлияющиеРеквизитыДокументовПредприятия();
	РеквизитыДокументаПредприятия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ДокументПредприятия,
		ВлияющиеРеквизитыДокументовПредприятия);
	Если РеквизитыДокументаПредприятия.ПометкаУдаления = Неопределено Тогда
		РеквизитыДокументаПредприятия = Неопределено;
	КонецЕсли;
	
	Если РеквизитыДокументаПредприятия <> Неопределено Тогда
		ВлияющиеРеквизитыВидовДокументовПредприятия = ВлияющиеРеквизитыВидовДокументовПредприятия();
		РеквизитыВидаДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			РеквизитыДокументаПредприятия.ВидДокумента,
			ВлияющиеРеквизитыВидовДокументовПредприятия);
		Если РеквизитыВидаДокумента.ИспользоватьСрокИсполнения = Неопределено Тогда
			РеквизитыВидаДокумента = Неопределено;
		КонецЕсли;
	Иначе
		РеквизитыВидаДокумента = Неопределено;
	КонецЕсли;
	
	// Влияющие реквизиты учтены в:
	//  - РегистрыСведений.МоиДокументы.ЗначенияПараметровОбновленияДанныхКэширующихОбъектов();
	//  - РегистрыСведений.СотрудникиПользователей.ЗначенияПараметровОбновленияДанныхКэширующихОбъектов().
	Причины = МоиДокументы.ПричиныДобавленияВМоиДокументы(ДокументПредприятия, Пользователь);
	
	ДанныеМоегоДокумента.ПометкаУдаления =
		РеквизитыВидаДокумента = Неопределено
		Или РеквизитыДокументаПредприятия = Неопределено
		Или РеквизитыДокументаПредприятия.ПометкаУдаления
		Или Причины.Количество() = 0;
	Если ДанныеМоегоДокумента.ПометкаУдаления Тогда
		Возврат ДанныеМоегоДокумента;
	КонецЕсли;
	
	// Влияющие реквизиты учтены в РегистрыСведений.ТекущиеСостоянияДокументов.ЗначенияПараметровОбновленияДанныхКэширующихОбъектов().
	ЕстьСостояниеИсполнен = РегистрыСведений.ТекущиеСостоянияДокументов.ЕстьСостояние(
		ДокументПредприятия,
		Перечисления.СостоянияДокументов.Исполнен);
	
	ДанныеМоегоДокумента.ДатаСортировки = Делопроизводство.ДатаУчетаДокумента(РеквизитыДокументаПредприятия);
	ДанныеМоегоДокумента.ВидДокумента = РеквизитыДокументаПредприятия.ВидДокумента;
	ДанныеМоегоДокумента.ПредставлениеДокумента = РеквизитыДокументаПредприятия.Наименование;
	ДанныеМоегоДокумента.Проект = РеквизитыДокументаПредприятия.Проект;
	
	ДанныеМоегоДокумента.ДобавленВручную = Причины.Найти(
		Перечисления.ПричиныДобавленияВМоиДокументы.ДобавленВручную) <> Неопределено;
	ДанныеМоегоДокумента.НазначенаЗадачаМне = Причины.Найти(
		Перечисления.ПричиныДобавленияВМоиДокументы.НазначенаЗадачаМне) <> Неопределено;
	ДанныеМоегоДокумента.ОтправленаЗадачаОтМеня = Причины.Найти(
		Перечисления.ПричиныДобавленияВМоиДокументы.ОтправленаЗадачаОтМеня) <> Неопределено;
	ДанныеМоегоДокумента.ЯвляюсьАвторомДокумента = Причины.Найти(
		Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомДокумента) <> Неопределено;
	ДанныеМоегоДокумента.ЯвляюсьАвторомУтвержденияДокумента = Причины.Найти(
		Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомУтвержденияДокумента) <> Неопределено;
	ДанныеМоегоДокумента.ЯвляюсьОтветственнымЗаДокумент = Причины.Найти(
		Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьОтветственнымЗаДокумент) <> Неопределено;
	ДанныеМоегоДокумента.ЯвляюсьПодписантомДокумента = Причины.Найти(
		Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьПодписантомДокумента) <> Неопределено;
	
	СрокИсполнения = Дата(1, 1, 1);
	Если Не РеквизитыВидаДокумента.ЯвляетсяИсходящейКорреспонденцией
		И РеквизитыВидаДокумента.ИспользоватьСрокИсполнения
		И Не ЕстьСостояниеИсполнен Тогда
		УчестьВСроке(СрокИсполнения, РеквизитыДокументаПредприятия.СрокИсполнения);
	КонецЕсли;
	Если Причины.Найти(Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьПодписантомДокумента) <> Неопределено
		И РеквизитыДокументаПредприятия.РезультатПодписания = Перечисления.РезультатыПодписания.НеПодписано Тогда
		УчестьВСроке(СрокИсполнения, РеквизитыДокументаПредприятия.ДатаПодписания);
	КонецЕсли;
	Если Причины.Найти(Перечисления.ПричиныДобавленияВМоиДокументы.НазначенаЗадачаМне) <> Неопределено Тогда
		// Влияющие реквизиты учтены в:
		//  - Документы.ДанныеМоегоДокумента.ВлияющиеРеквизитыЗадач();
		//  - Документы.ДанныеМоегоДокумента.ВлияющиеРеквизитыДействийЗадач().
		СрокИсполненияЗадачиМне = РаботаСЗадачами.СрокЗадачиМнеПоПриложению(ДокументПредприятия, Пользователь);
		УчестьВСроке(СрокИсполнения, СрокИсполненияЗадачиМне);
	КонецЕсли;
	Если Причины.Найти(Перечисления.ПричиныДобавленияВМоиДокументы.ОтправленаЗадачаОтМеня) <> Неопределено Тогда
		// Влияющие реквизиты учтены в:
		//  - Документы.ДанныеМоегоДокумента.ВлияющиеРеквизитыЗадач();
		//  - Документы.ДанныеМоегоДокумента.ВлияющиеРеквизитыДействийЗадач().
		СрокИсполненияЗадачиОтМеня = РаботаСЗадачами.СрокЗадачиОтМеняПоПриложению(ДокументПредприятия, Пользователь);
		УчестьВСроке(СрокИсполнения, СрокИсполненияЗадачиОтМеня);
	КонецЕсли;
	Если Причины.Найти(Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьПодписантомДокумента) <> Неопределено Тогда
		// Влияющие реквизиты учтены в Документы.ДанныеМоегоДокумента.ВлияющиеРеквизитыДокументовПредприятия().
		СрокПодписанияДокументаСтороной = РаботаСДокументами.СрокПодписанияДокументаСтороной(ДокументПредприятия, Пользователь);
		УчестьВСроке(СрокИсполнения, СрокПодписанияДокументаСтороной);
	КонецЕсли;
	
	МожетБытьПросрочен = ЗначениеЗаполнено(СрокИсполнения);
	ДанныеМоегоДокумента.Просрочен =
		МожетБытьПросрочен
		И СрокИсполнения < ТекущаяДатаСеанса();
	
	ГраницаИстеченияСрока = РаботаСЗадачами.ГраницаИстеченияСрока();
	ДанныеМоегоДокумента.Истекает =
		МожетБытьПросрочен
		И Не ДанныеМоегоДокумента.Просрочен
		И СрокИсполнения < ГраницаИстеченияСрока;
	
	ОжидаемОтвет = Ложь;
	Если (Причины.Найти(Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомДокумента) <> Неопределено
		Или Причины.Найти(Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьОтветственнымЗаДокумент) <> Неопределено)
		И РеквизитыВидаДокумента.ЯвляетсяИсходящейКорреспонденцией
		И ЗначениеЗаполнено(РеквизитыДокументаПредприятия.СрокИсполнения) Тогда
		
		// Влияющие реквизиты учтены в Документы.ДанныеМоегоДокумента.ВлияющиеРеквизитыКорреспонденции().
		ДокументОтправлен = РаботаСДокументами.ДокументОтправлен(ДокументПредприятия);
		
		// Влияющие реквизиты учтены в РегистрыСведений.СвязиОбъектов.ЗначенияПараметровОбновленияДанныхКэширующихОбъектов().
		ПолученОтвет = РаботаСДокументами.ПолученОтветНаДокумент(ДокументПредприятия);
		
		ОжидаемОтвет = ДокументОтправлен И Не ПолученОтвет;
		ДанныеМоегоДокумента.БезОтвета =
			ОжидаемОтвет И РеквизитыДокументаПредприятия.СрокИсполнения < ТекущаяДатаСеанса();
		
	КонецЕсли;
	
	СрокОбновления = Дата(1, 1, 1);
	Если МожетБытьПросрочен И Не ДанныеМоегоДокумента.Просрочен И ДанныеМоегоДокумента.Истекает Тогда
		УчестьВСроке(СрокОбновления, СрокИсполнения);
	ИначеЕсли МожетБытьПросрочен И Не ДанныеМоегоДокумента.Просрочен И Не ДанныеМоегоДокумента.Истекает Тогда
		СрокИстечения = РаботаСЗадачами.СрокИстечения(СрокИсполнения);
		УчестьВСроке(СрокОбновления, СрокИстечения);
	КонецЕсли;
	Если ОжидаемОтвет И Не ДанныеМоегоДокумента.БезОтвета Тогда
		УчестьВСроке(СрокОбновления, РеквизитыДокументаПредприятия.СрокИсполнения);
	КонецЕсли;
	Если ЗначениеЗаполнено(СрокОбновления) Тогда
		ДанныеМоегоДокумента.СрокОбновления = СрокОбновления;
	КонецЕсли;
	
	Возврат ДанныеМоегоДокумента;
	
КонецФункции

// Заполняет данные объекта.
// 
// Параметры:
//  ДанныеМоегоДокументаОбъект - ДокументОбъект.ДанныеМоегоДокумента
//  ДанныеМоегоДокумента - см. НовыеДанныеМоегоДокумента
// 
Процедура ЗаполнитьДанныеОбъекта(ДанныеМоегоДокументаОбъект, ДанныеМоегоДокумента)
	
	ЗаполнитьЗначенияСвойств(ДанныеМоегоДокументаОбъект, ДанныеМоегоДокумента);
	
КонецПроцедуры

// Учитывает частный срок в общем сроке.
// 
// Параметры:
//  СрокОбщий - Дата
//  СрокЧастный - Дата
// 
Процедура УчестьВСроке(СрокОбщий, СрокЧастный)
	
	Если Не ЗначениеЗаполнено(СрокЧастный) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СрокОбщий) Тогда
		СрокОбщий = Мин(СрокОбщий, СрокЧастный)
	Иначе
		СрокОбщий = СрокЧастный;
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает обновление кэширующих данных по виду документа.
// 
// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса:
//   * ОтметкаВремени - ОпределяемыйТип.ОтметкаВремени
//   * ЗависимыйОбъектМетаданных - СправочникСсылка.ИдентификаторыОбъектовМетаданных
//   * ВлияющийОбъектМетаданных - СправочникСсылка.ИдентификаторыОбъектовМетаданных
//   * КлючВлияющихДанных - ЛюбаяСсылка
//   * Автор - СправочникСсылка.Пользователи
//   * ЗагрузкаОбработанныхДанныхИзДругойСистемы - Булево
//   * ИзмененияВлияющихДанных - ХранилищеЗначения
//   * Попыток - Число
//   * ДатаКОбработке - Дата
// 
Процедура ОбновитьКэширующиеДанныеПоВидуДокумента(Выборка)
	
	ИзмененияВлияющихДанных = Выборка.ИзмененияВлияющихДанных.Получить();
	Если Не Выборка.Долгое Тогда
		РегистрыСведений.ОчередьОбновленияКэширующихДанных.Добавить(
			Выборка.ЗависимыйОбъектМетаданных,
			Выборка.ВлияющийОбъектМетаданных,
			Выборка.КлючВлияющихДанных,
			ИзмененияВлияющихДанных,
			Истина);
		Возврат;
	КонецЕсли;
	
	ВидДокументов = Выборка.КлючВлияющихДанных; // СправочникСсылка.ВидыДокументов
	
	ДатаКОбработке = Выборка.ДатаКОбработке;
	Если Не ЗначениеЗаполнено(ДатаКОбработке) Тогда
		ДатаКОбработке = ТекущаяДатаСеанса();
	КонецЕсли;
	
	НомерЦикла = 0;
	ДатаЦикла = ДатаКОбработке;
	
	ЕстьДанныеКОбработке = Истина;
	Пока ЕстьДанныеКОбработке Цикл
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1000
			|	ДокументыПредприятия.Ссылка КАК Ссылка,
			|	ДокументыПредприятия.ДатаСоздания КАК Дата
			|ИЗ
			|	Справочник.ДокументыПредприятия КАК ДокументыПредприятия
			|ГДЕ
			|	ДокументыПредприятия.ВидДокумента = &ВидДокументов
			|	И ДокументыПредприятия.ДатаСоздания <= &ДатаКОбработке
			|
			|УПОРЯДОЧИТЬ ПО
			|	ДокументыПредприятия.ДатаСоздания УБЫВ");
		
		Запрос.УстановитьПараметр("ВидДокументов", ВидДокументов);
		Запрос.УстановитьПараметр("ДатаКОбработке", ДатаКОбработке);
		
		//@skip-check query-in-loop
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ЕстьДанныеКОбработке = Истина;
			
			НачатьТранзакцию();
			Попытка
				
				ОтразитьПоДокументуНемедленно(ВыборкаДетальныеЗаписи.Ссылка);
				
				Если ВыборкаДетальныеЗаписи.Дата <> ДатаКОбработке Тогда
					ДатаКОбработке = ВыборкаДетальныеЗаписи.Дата;
					РегистрыСведений.ОчередьОбновленияКэширующихДанных.ОбновитьДатуКОбработке(
						Выборка.ОтметкаВремени,
						Выборка.ЗависимыйОбъектМетаданных,
						Выборка.ВлияющийОбъектМетаданных,
						Выборка.КлючВлияющихДанных,
						ДатаКОбработке);
				КонецЕсли;
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				ОтменитьТранзакцию();
				ВызватьИсключение;
			КонецПопытки;
			
		КонецЦикла;
		
		ЕстьДанныеКОбработке = ВыборкаДетальныеЗаписи.Количество() = 1000;
		Если ЕстьДанныеКОбработке И ДатаЦикла = ДатаКОбработке Тогда
			ВызватьИсключение НСтр("ru = 'Обнаружено зацикливание.'");
		КонецЕсли;
		
		ДатаЦикла = ДатаКОбработке;
		НомерЦикла = НомерЦикла + 1;
		Если НомерЦикла > 10000 Тогда
			ВызватьИсключение НСтр("ru = 'Обнаружено зацикливание.'");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Обрабатывает обновление кэширующих данных по сотруднику.
// 
// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса:
//   * ОтметкаВремени - ОпределяемыйТип.ОтметкаВремени
//   * ЗависимыйОбъектМетаданных - СправочникСсылка.ИдентификаторыОбъектовМетаданных
//   * ВлияющийОбъектМетаданных - СправочникСсылка.ИдентификаторыОбъектовМетаданных
//   * КлючВлияющихДанных - ЛюбаяСсылка
//   * Автор - СправочникСсылка.Пользователи
//   * ЗагрузкаОбработанныхДанныхИзДругойСистемы - Булево
//   * ИзмененияВлияющихДанных - ХранилищеЗначения
//   * Попыток - Число
//   * ДатаКОбработке - Дата
// 
Процедура ОбновитьКэширующиеДанныеПоСотрудники(Выборка)
	
	ИзмененияВлияющихДанных = Выборка.ИзмененияВлияющихДанных.Получить();
	Если Не Выборка.Долгое Тогда
		РегистрыСведений.ОчередьОбновленияКэширующихДанных.Добавить(
			Выборка.ЗависимыйОбъектМетаданных,
			Выборка.ВлияющийОбъектМетаданных,
			Выборка.КлючВлияющихДанных,
			ИзмененияВлияющихДанных,
			Истина);
		Возврат;
	КонецЕсли;
	
	Сотрудник = Выборка.КлючВлияющихДанных; // СправочникСсылка.Сотрудники
	
	ДатаКОбработке = Выборка.ДатаКОбработке;
	Если Не ЗначениеЗаполнено(ДатаКОбработке) Тогда
		ДатаКОбработке = ТекущаяДатаСеанса();
	КонецЕсли;
	
	НомерЦикла = 0;
	ДатаЦикла = ДатаКОбработке;
	
	ЕстьДанныеКОбработке = Истина;
	Пока ЕстьДанныеКОбработке Цикл
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1000
			|	МоиДокументы.Документ КАК Ссылка,
			|	ДокументыПредприятия.ДатаСоздания КАК Дата
			|ИЗ
			|	РегистрСведений.МоиДокументы КАК МоиДокументы
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДокументыПредприятия КАК ДокументыПредприятия
			|		ПО МоиДокументы.Документ = ДокументыПредприятия.Ссылка
			|ГДЕ
			|	МоиДокументы.Сотрудник = &Сотрудник
			|	И ДокументыПредприятия.ДатаСоздания <= &ДатаКОбработке
			|СГРУППИРОВАТЬ ПО
			|	МоиДокументы.Документ,
			|	ДокументыПредприятия.ДатаСоздания
			|
			|УПОРЯДОЧИТЬ ПО
			|	ДокументыПредприятия.ДатаСоздания УБЫВ");
		
		Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
		Запрос.УстановитьПараметр("ДатаКОбработке", ДатаКОбработке);
		
		//@skip-check query-in-loop
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ЕстьДанныеКОбработке = Истина;
			
			НачатьТранзакцию();
			Попытка
				
				ОтразитьПоСотрудникуНемедленно(ВыборкаДетальныеЗаписи.Ссылка, Сотрудник);
				
				Если ВыборкаДетальныеЗаписи.Дата <> ДатаКОбработке Тогда
					ДатаКОбработке = ВыборкаДетальныеЗаписи.Дата;
					РегистрыСведений.ОчередьОбновленияКэширующихДанных.ОбновитьДатуКОбработке(
						Выборка.ОтметкаВремени,
						Выборка.ЗависимыйОбъектМетаданных,
						Выборка.ВлияющийОбъектМетаданных,
						Выборка.КлючВлияющихДанных,
						ДатаКОбработке);
				КонецЕсли;
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				ОтменитьТранзакцию();
				ВызватьИсключение;
			КонецПопытки;
			
		КонецЦикла;
		
		ЕстьДанныеКОбработке = ВыборкаДетальныеЗаписи.Количество() = 1000;
		Если ЕстьДанныеКОбработке И ДатаЦикла = ДатаКОбработке Тогда
			ВызватьИсключение НСтр("ru = 'Обнаружено зацикливание.'");
		КонецЕсли;
		
		ДатаЦикла = ДатаКОбработке;
		НомерЦикла = НомерЦикла + 1;
		Если НомерЦикла > 10000 Тогда
			ВызватьИсключение НСтр("ru = 'Обнаружено зацикливание.'");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли