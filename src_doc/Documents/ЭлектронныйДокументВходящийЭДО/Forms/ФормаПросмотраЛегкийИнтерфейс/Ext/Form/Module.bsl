#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоМобильныйКлиент = ОбщегоНазначения.ЭтоМобильныйКлиент();
	ЕстьПравоАдминистрирования = ОбщегоНазначенияБЭД.ЕстьПравоАдминистрирования();
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ВызватьИсключение НСтр("ru = 'Документ не предназначен для интерактивного ввода.'");
	КонецЕсли;
	
	Если РаботаСПрочтениямиВызовСервера.УстановитьСвойствоПрочтен(Объект.Ссылка, Истина) Тогда
		ОповеститьОПрочтении = Истина;
	КонецЕсли;
	
	ОбменСКонтрагентамиПереопределяемый.ПриПроверкиГотовностиЭлектронногоДокумента(ЭтотОбъект, ТолькоПросмотр);
	
	Если ЭтоМобильныйКлиент Тогда
		МК_НастроитьЭлементыФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект);

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	ТипДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидДокумента, "ТипДокумента");

	ОбновитьДанныеДляПросмотраДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОжидатьПолученияАсинхронныхДанных();

	Если ОповеститьОПрочтении Тогда
		Оповестить("ПрочтениеОбновитьСписок", Объект.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияСостоянияЭДО()
		И Параметр <> Неопределено Тогда
		
		ОбработатьОповещение = Истина;
		
		ДанныеПроверки = ИнтерфейсДокументовЭДОКлиент.НовыеДанныеПроверкиОповещения();
		ДанныеПроверки.ЭлектронныйДокумент = Объект.Ссылка;
		
		ИнтерфейсДокументовЭДОКлиент.ПриОбработкеОповещенияФормыПросмотраЭД(ДанныеПроверки, Параметр, 
			ОбработатьОповещение);
		
		Если ОбработатьОповещение Тогда
			ОбновитьДанныеПослеВыполненияДействийПоЭДО();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = ЛегкийИнтерфейсДокументовЭДОКлиент.ИмяСобытияИзмененияОбъектовУчетаДокумента()
		И Параметр = Объект.Ссылка Тогда
			
		ОбновитьДанныеПослеИзмененияОбъектовУчета();
			
	ИначеЕсли ИмяСобытия = "ЭлектронныйДокументИсходящий_ПодборДокументаУчета" И Параметр = Объект.Ссылка Тогда

		ОбновитьДанныеПослеИзмененияОбъектовУчета();
		
	ИначеЕсли ИмяСобытия = "ЗакрытьФормуДлительнойОперации" Тогда
			
		ЛегкийИнтерфейсДокументовЭДОКлиент.НастроитьБлокировкуПанелиКомандКарточкиДокумента(ЭтотОбъект, Ложь);
		
	ИначеЕсли ИмяСобытия = МашиночитаемыеДоверенностиКлиент.ИмяСобытияИсправлениеОшибкиПроверкиПолномочий() Тогда
		
		Для Каждого ИсправленнаяОшибка Из Параметр Цикл
			Если ИсправленнаяОшибка.СсылкаНаОбъект = Объект.Ссылка Тогда
				ОбновитьДанныеПослеИзмененияПодписейДокумента();
				Прервать;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ИмяСобытия = "Запись_НаборКонстант" И Источник = "ИспользоватьРазделениеДокументовПоОтветственнымЭДО" Тогда
		ОбновитьДанныеЭлектронногоДокумента();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЗаписи, "ИзменениеВидаДокумента", Ложь) Тогда
		ЭлектронныеДокументыЭДО.ПриИзмененииВидаДокумента(ТекущийОбъект.Ссылка,
			ТекущийОбъект.ВидДокумента, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НадписьОшибкаФормированияПредставленияОбработкаНавигационнойСсылки(Элемент,
	НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьЖурналРегистрации" Тогда
		ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьЖурналРегистрацииПоОшибкамПредставленияИзКарточкиДокумента(
			ЭтотОбъект, СтандартнаяОбработка);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовШапкиФормы

#Область ОбработчикиСобытийЭлементовТаблицыФормыСоставПакета

&НаКлиенте
Процедура СоставПакетаПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("СоставПакетаПослеАктивизацииСтроки", 0.1, Истина);
	
	Если ЭтоМобильныйКлиент Тогда
		Элементы.ГруппаПанельПакета.Скрыть();
		МК_ОбновитьРеквизиты();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыСоставПакета

#Область ОбработчикиСобытийЭлементовТаблицыФормыПодписи

&НаКлиенте
Процедура ПодписиПриАктивизацииСтроки(Элемент)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОбработатьАктивациюСтрокиВТаблицеПодписейКарточкиДокумента(ЭтотОбъект, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПодписиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗакрытияРезультатаПроверкиПодписи", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОбработатьВыборВТаблицеПодписейИзКарточкиДокумента(ЭтотОбъект, Элемент, Поле, 
		СтандартнаяОбработка, ОповещениеОЗавершении);
		
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыПодписи

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура МК_СтраницаДокумент(Команда)

	МК_ПереключитьНаСтраницу(
		ЭтотОбъект, Элементы.ГруппаСтраницыПанельЗакладок, Элементы.СтраницаДокумент, Элементы.МК_КнопкаСтраницаДокумент);
	
КонецПроцедуры

&НаКлиенте
Процедура МК_СтраницаРеквизиты(Команда)
	
	МК_ПереключитьНаСтраницу(
		ЭтотОбъект, Элементы.ГруппаСтраницыПанельЗакладок, Элементы.МК_СтраницаРеквизиты, Элементы.МК_КнопкаСтраницаРеквизиты);

КонецПроцедуры

&НаКлиенте
Процедура МК_СтраницаПодписи(Команда)
	
	МК_ПереключитьНаСтраницу(
		ЭтотОбъект, Элементы.ГруппаСтраницыПанельЗакладок, Элементы.СтраницаПодписи, Элементы.МК_КнопкаСтраницаПодписи);
	
КонецПроцедуры

#Область ОбработчикиКомандФормыОсновные

#Область ВыполнениеДействийПоЭДО

&НаКлиенте
Процедура Утвердить(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеУтвердитьИзКарточкиДокумента(ЭтотОбъект, ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьОтправить(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействияПодписатьОтправитьИзКарточкиДокумента(ЭтотОбъект, 
		ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура Подписать(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеПодписатьИзКарточкиДокумента(ЭтотОбъект, ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура Отправить(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтправитьИзКарточкиДокумента(ЭтотОбъект, ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура Отклонить(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьИзКарточкиДокумента(ЭтотОбъект, ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПовторно(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтправитьПовторноИзКарточкиДокумента(ЭтотОбъект,
		ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура ПринятьАннулирование(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеПринятьАннулированиеИзКарточкиДокумента(ЭтотОбъект, 
		ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьАннулирование(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьАннулированиеИзКарточкиДокумента(ЭтотОбъект, 
		ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура Аннулировать(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеАннулироватьИзКарточкиДокумента(ЭтотОбъект, ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура Завершить(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеЗакрытьПринудительноИзКарточкиДокумента(ЭтотОбъект,
		ЭтоПакетДокументов);
КонецПроцедуры

&НаКлиенте
Процедура ВернутьВРаботу(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеВернутьВРаботуИзКарточкиДокумента(ЭтотОбъект, 
		ЭтоПакетДокументов);
КонецПроцедуры

#КонецОбласти // ВыполнениеДействийПоЭДО

#Область ВыгрузкаИПечать

&НаКлиенте
Процедура НапечататьЭлектронныеДокументы(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьФормуПечатиИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДокументооборотЦеликом(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыгрузитьДокументооборотЦеликомИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЭлектронныеДокументыВФорматеPDF(Команда)
	
	Если Не ЭтоМобильныйКлиент Тогда
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыгрузитьДокументыВФорматеPDFИзКарточкиДокумента(ЭтотОбъект);
	Иначе
		ИнтерфейсМобильногоЭДОКлиентСервер.СообщитьФункциональностьПокаНеДоступна();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЭлектронныеДокументыДляФНС(Команда)
	
	Если Не ЭтоМобильныйКлиент Тогда
		ЛегкийИнтерфейсДокументовЭДОКлиент.ВыгрузитьДокументыДляФНСИзКарточкиДокумента(ЭтотОбъект);
	Иначе
		ИнтерфейсМобильногоЭДОКлиентСервер.СообщитьФункциональностьПокаНеДоступна();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПротоколОбмена(Команда)
	ИнтерфейсДокументовЭДОКлиент.ПоказатьПротоколОбмена(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка));
КонецПроцедуры

#КонецОбласти // ВыгрузкаИПечать

#КонецОбласти // ОбработчикиКомандФормыОсновные

#Область ОбработчикиКомандФормыЕще

#Область ВыполнениеДействийПоЭДОСДокументом

&НаКлиенте
Процедура УтвердитьДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеУтвердитьИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьОтправитьДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействияПодписатьОтправитьИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеПодписатьИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтправитьИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПринятьАннулированиеДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеПринятьАннулированиеИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьАннулированиеДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьАннулированиеИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура АннулироватьДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеАннулироватьИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеЗакрытьПринудительноИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВернутьВРаботуДокумент(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеВернутьВРаботуИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти // ВыполнениеДействийПоЭДОСДокументом

#Область НастройкиОтображения

&НаКлиенте
Процедура ОтображатьБанковскиеРеквизиты(Кнопка)

	ВыводитьБанковскиеРеквизиты = Не ВыводитьБанковскиеРеквизиты;
	Элементы.КнопкаОтображатьБанковскиеРеквизиты.Пометка = ВыводитьБанковскиеРеквизиты;

	ОбновитьВизуализациюДокумента();

КонецПроцедуры

&НаКлиенте
Процедура ОтображатьДополнительнуюИнформацию(Команда)

	ОтключитьВыводДопДанных = Не ОтключитьВыводДопДанных;
	Элементы.КнопкаОтображатьДополнительнуюИнформацию.Пометка = Не ОтключитьВыводДопДанных;

	ОбновитьВизуализациюДокумента();

КонецПроцедуры

&НаКлиенте
Процедура ОтображатьОбластьКопияВерна(Команда)

	ОтключитьВыводКопияВерна = Не ОтключитьВыводКопияВерна;
	Элементы.КнопкаОтображатьОбластьКопияВерна.Пометка = Не ОтключитьВыводКопияВерна;

	ОбновитьВизуализациюДокумента();

КонецПроцедуры

#КонецОбласти // НастройкиОтображения

&НаКлиенте
Процедура СменитьОтветственного(Команда)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеСменыОтветственного", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.СменитьОтветственногоИзКарточкиДокумента(ЭтотОбъект, ОповещениеОЗавершении);
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьОтветственногоПакет(Команда)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеСменыОтветственного", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.СменитьОтветственногоИзКарточкиДокумента(ЭтотОбъект, ОповещениеОЗавершении,
		Истина);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьПроизвольныйДокумент(Команда)
	ИнтерфейсДокументовЭДОКлиент.ОткрытьНовыйПроизвольныйЭлектронныйДокументНаОсновании(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодписи(Команда)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеПроверкиПодписейДокумента", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьПроверкуПодписейИзКарточкиДокумента(ЭтотОбъект, ОповещениеОЗавершении);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСертификат(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ПоказатьСертификатИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодписьПоДоверенностиВручную(Команда)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеПроверкиПодписейДокумента", ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДОКлиент.ПроверитьПодписьПоДоверенностиВручнуюИзКарточкиДокумента(ЭтотОбъект,
		ОповещениеОЗавершении);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуЭлектронногоДокумента(Команда)
	ИнтерфейсДокументовЭДОКлиент.ОткрытьКарточкуЭлектронногоДокумента(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКонтейнерДокумента(Команда)
	
	Если Не ЗначениеЗаполнено(СообщениеЭДО) Тогда
		СообщениеЭДО = СообщениеЭДОИнформацииОтправителя(Объект.Ссылка);
	КонецЕсли;
	СинхронизацияЭДОКлиент.ОткрытьТранспортныйКонтейнерСообщения(СообщениеЭДО);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЖурналСобытийЭДО(Команда)
	ЭлектронныеДокументыЭДОКлиент.ОткрытьЖурналДействийПоЭДО(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкуЭДО(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьНастройкиОтраженияВУчетеИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеДокумент(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПометитьНаУдалениеДокументПослеОтветаНаВопрос", ЭтотОбъект);
	ОбщегоНазначенияБЭДКлиент.ЗадатьВопросНаУстановкуПометкиУдаления(Объект.ПометкаУдаления, ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормыЕще

#Область ОбработчикиКомандФормыОтражениеВУчете

&НаКлиенте
Процедура СоздатьДокументУчета(Команда)
	
	СтандартнаяОбработка = Истина;
	ОбменСКонтрагентамиКлиентПереопределяемый.ПриСозданииУчетногоДокумента(Объект.Ссылка, СтандартнаяОбработка);
	
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	ЛегкийИнтерфейсДокументовЭДОКлиент.ПоказатьВыборСпособаОбработкиСозданияОбъектовУчетаИзКарточкиДокумента(
		ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьДокументУчета1(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ПоказатьВыборСпособаОбработкиПодбораОбъектаУчетаИзКарточкиДокумента(
		ЭтотОбъект, 1);
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьДокументУчета2(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ПоказатьВыборСпособаОбработкиПодбораОбъектаУчетаИзКарточкиДокумента(
		ЭтотОбъект, 2);
КонецПроцедуры

&НаКлиенте
Процедура РазорватьСвязьДокументаУчета1(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ПоказатьВопросОРазрывеСвязиСОбъектомУчетаИзКарточкиДокумента(ЭтотОбъект, 1);
КонецПроцедуры

&НаКлиенте
Процедура РазорватьСвязьДокументаУчета2(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ПоказатьВопросОРазрывеСвязиСОбъектомУчетаИзКарточкиДокумента(ЭтотОбъект, 2);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокументУчета1(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьОбъектУчетаИзКарточкиДокумента(ЭтотОбъект, 1);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокументУчета2(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьОбъектУчетаИзКарточкиДокумента(ЭтотОбъект, 2);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеДокументыУчета(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ПоказатьРасширенныйПодборОбъектовУчетаИзКарточкиДокумента(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандОтраженияВУчете

#Область ОбработчикиКомандФормыСодержимое

&НаКлиенте
Процедура СостояниеОбработкаНажатия(Команда)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(КомментарийСостояния, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьФайлИнформацииОтправителяДокумента(
		Объект.Ссылка, Объект.ЭтоОблачныйЭДО, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВизуализацию(Команда)
	ПеречитатьКэшВизуализации = Истина;
	ОбновитьДанныеЭлектронногоДокумента();
	ПеречитатьКэшВизуализации = Ложь;
КонецПроцедуры
#КонецОбласти // ОбработчикиКомандФормыСодержимое

&НаКлиенте
Процедура ВключитьПолеПредпросмотраPDFФайлов(Команда)
	Элементы.ВключитьПолеПредпросмотраPDFФайлов.Пометка = Не Элементы.ВключитьПолеПредпросмотраPDFФайлов.Пометка;
	ВключитьПолеПредпросмотраPDFФайловНаСервере(Элементы.ВключитьПолеПредпросмотраPDFФайлов.Пометка);
	ОбновитьВизуализациюДокумента();
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеДанных

&НаСервереБезКонтекста
Функция СообщениеЭДОИнформацииОтправителя(Знач ЭлектронныйДокумент)
	Возврат ЭлектронныеДокументыЭДО.СообщениеОтправителя(ЭлектронныйДокумент);
КонецФункции

#КонецОбласти // ЗаполнениеДанных

#Область ОбновлениеДанных

&НаКлиенте
Процедура ОбновитьДанныеЭлектронногоДокумента()
	
	Прочитать();
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеДляПросмотраДокумента()
	
	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельКарточки, ПанельПометкиНаУдаление, ПанельКомандВходящегоДокумента, ПанельПакета, ПанельСостояния, 
		|ПанельИтогов, ПанельОтветственного, ПанельОтраженияВУчете, ПанельЗакладокВходящегоДокумента, 
		|ПанельВизуализации, ПанельКомандНастроекВизуализации, ПанельПредставленияФайла, ПанельПодписей, 
		|ПанельСопроводительнойЗапискиВходящегоДокумента, ПанельРеквизитов, ПанельРеквизитовПакета");
	
	ОбновитьДанныеКомпонентов(ДанныеКомпонентов);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеКомпонентов(ДанныеКомпонентов)

	Если ОбщегоНазначения.ЭтоВебКлиент() Тогда
		ИнтерфейсДокументовЭДО.УдалитьРеквизитыВыводаPDFДокумента(ЭтотОбъект, СведенияОPdfДокументе);
	КонецЕсли;

	ПараметрыЗаполнения = 
		ЛегкийИнтерфейсДокументовЭДО.ПараметрыЗаполненияДанныхКомпонентовПросмотраДокумента(ЭтотОбъект);
	ПараметрыЗаполнения.НастройкиФормы.ПеречитатьКэшВизуализации = ПеречитатьКэшВизуализации;
	ОперацияПолученияДанных = ЛегкийИнтерфейсДокументовЭДО.ЗаполнитьДанныеКомпонентовПросмотраДокумента(
		ДанныеКомпонентов, ПараметрыЗаполнения);
	
	ЛегкийИнтерфейсДокументовЭДО.НастроитьПанелиПросмотраКарточкиДокумента(ЭтотОбъект, ДанныеКомпонентов);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВизуализациюДокумента()

	ОбновитьВизуализациюДокументаНаСервере();

	ОжидатьПолученияАсинхронныхДанных();

КонецПроцедуры

&НаСервере
Процедура ОбновитьВизуализациюДокументаНаСервере()

	ЛегкийИнтерфейсДокументовЭДО.СохранитьНастройкиОтображенияВизуализацииКарточкиДокумента(ЭтотОбъект);

	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельКомандВходящегоДокумента, ПанельВизуализации, ПанельКомандНастроекВизуализации, 
		|ПанельПредставленияФайла");
	
	ОбновитьДанныеКомпонентов(ДанныеКомпонентов);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПослеВыполненияДействийПоЭДО()
	
	Прочитать();
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПослеИзмененияПодписейДокумента()
	
	ОбновитьДанныеПослеИзмененияПодписейДокументаНаСервере();
	
	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеИзмененияПодписейДокументаНаСервере()
	
	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельКомандВходящегоДокумента, ПанельСостояния, ПанельЗакладокВходящегоДокумента, ПанельВизуализации, 
		|ПанельКомандНастроекВизуализации, ПанельПредставленияФайла, ПанельПодписей");
	
	ОбновитьДанныеКомпонентов(ДанныеКомпонентов);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПослеИзмененияОбъектовУчета()

	ОбновитьДанныеПослеИзмененияОбъектовУчетаНаСервере();
	
	ОжидатьПолученияАсинхронныхДанных();

КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеИзмененияОбъектовУчетаНаСервере()

	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельСостояния, ПанельОтраженияВУчете");
	
	ОбновитьДанныеКомпонентов(ДанныеКомпонентов);

КонецПроцедуры

#Область АсинхронноеОбновлениеДанных

&НаКлиенте
Процедура ОжидатьПолученияАсинхронныхДанных()

	ДлительнаяОперация = ОперацияПолученияДанных; // См. ДлительныеОперации.ВыполнитьФункцию

	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаполнитьАсинхронныеДанныеПослеОжидания", ЭтотОбъект);

	Оповещение = Новый ОписаниеОповещения("ОжидатьПолученияДанныхКомпонентовПросмотраПослеДлительнойОперации",
		ЛегкийИнтерфейсДокументовЭДОКлиент, ОповещениеОЗавершении);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАсинхронныеДанныеПослеОжидания(Результат, ДополнительныеПараметры) Экспорт

	Если ОперацияПолученияДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Результат.Успех Тогда
		ОперацияПолученияДанных = Результат.ОперацияПолученияДанных;
		ОбновитьДанныеПослеЗаполненияАсинхронныхДанных();
		
		Если ЗначениеЗаполнено(АдресХранилищаPdf) Тогда
			ПредставлениеДокумента = ПолучитьИзВременногоХранилища(АдресХранилищаPdf);
			ИнтерфейсДокументовЭДОКлиент.ВывестиPDFДокументНаФорму(ЭтотОбъект, СведенияОPdfДокументе,
				ПредставлениеДокумента);
			УдалитьИзВременногоХранилища(АдресХранилищаPdf);
			АдресХранилищаPdf = "";
		КонецЕсли;
	Иначе
		ОперацияПолученияДанных = Неопределено;
		ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(Результат.КонтекстДиагностики);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеЗаполненияАсинхронныхДанных()

	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.РезультатЗаполненияДанныхКомпонентовПросмотраДокументаАсинхронноВФоне(
		ОперацияПолученияДанных);
	
	ОперацияПолученияДанных = Неопределено;
	
	Если ДанныеКомпонентов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЛегкийИнтерфейсДокументовЭДО.НастроитьПанелиПросмотраКарточкиДокумента(ЭтотОбъект, ДанныеКомпонентов);
	Если ЗначениеЗаполнено(ДанныеКомпонентов.ПанельПредставленияФайла) Тогда
		АдресХранилищаPdf = ПоместитьВоВременноеХранилище(ДанныеКомпонентов.ПанельПредставленияФайла.Данные);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // АсинхронноеОбновлениеДанных

#КонецОбласти // ОбновлениеДанных

#Область РаботаСПакетом

&НаКлиенте
Процедура СоставПакетаПослеАктивизацииСтроки()
	
	ТекущиеДанные = Элементы.СоставПакета.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
		Или Не ЗначениеЗаполнено(ТекущиеДанные.ЭлектронныйДокумент)
		Или ТекущиеДанные.ЭлектронныйДокумент = Объект.Ссылка Тогда
		Возврат;
	КонецЕсли;

	ТекущийДокументПакета = ТекущиеДанные.ЭлектронныйДокумент;

	Если ОповеститьОПрочтении Тогда
		Оповестить("ПрочтениеОбновитьСписок", ТекущийДокументПакета);
	КонецЕсли;
	
	ПоказатьДокументПакета();

	ОжидатьПолученияАсинхронныхДанных();
	
	Если ЭтоМобильныйКлиент Тогда
		МК_ОбновитьРеквизиты();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьДокументПакета()
	
	ЛегкийИнтерфейсДокументовЭДО.ПрекратитьОжиданиеПолученияДанныхКомпонентовПросмотраДокумента(ОперацияПолученияДанных);
	
	ЛегкийИнтерфейсДокументовЭДО.ПоказатьДокументПакетаКарточкиДокумента(ЭтотОбъект, ТекущийДокументПакета);

КонецПроцедуры

#КонецОбласти // РаботаСПакетом

#Область ОбработчикиОповещений

&НаКлиенте
Процедура ПослеСменыОтветственного(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Или Не Результат.Успех Тогда
		Возврат;
	КонецЕсли;

	ОбновитьДанныеЭлектронногоДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПроверкиПодписейДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) И Результат.Успех Тогда
		ОбновитьДанныеПослеИзмененияПодписейДокумента();
	КонецЕсли;
	
	ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(Результат.КонтекстДиагностики);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияРезультатаПроверкиПодписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьДанныеПослеИзмененияПодписейДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеДокументПослеОтветаНаВопрос(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено
		Или Ответ.Значение <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьПометкуУдаления();
	
	ОжидатьПолученияАсинхронныхДанных();
	
	Оповестить(ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияТекущихДелЭДО(),, ВладелецФормы);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиОповещений

#Область ОбработчикиКоманд

&НаСервере
Процедура ИзменитьПометкуУдаления()
	
	Объект.ПометкаУдаления = Не Объект.ПометкаУдаления;
	Записать();
	Прочитать();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВключитьПолеПредпросмотраPDFФайловНаСервере(Знач ВключитьПоле)
	НастройкиЭДО.УстановитьИспользованиеПросмотраPDFДокументовЭДО(ВключитьПоле);
КонецПроцедуры

#КонецОбласти // ОбработчикиКоманд

#Область СлужебныеПроцедурыИФункции_МобильныйКлиент
	
&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	МК_ЭлементыСтиля = ИнтерфейсМобильногоЭДОПовтИсп.ЭлементыСтиля();
	ИнтерфейсМобильногоЭДО.УстановитьОбщиеСвойстваФормы(ЭтотОбъект);
	
	ЦветФона = WebЦвета.Белый;
	
	СоставКоманднойПанелиНаМобильномУстройстве.Добавить(Элементы.ГруппаКомандыЕще);
	Элементы.Переместить(Элементы.ГруппаКомандыРаспечататьВыгрузить, Элементы.ГруппаКомандыЕще);
	Элементы.ГруппаКомандыРаспечататьВыгрузить.Видимость = Ложь;
	Элементы.ГруппаКомандыОсновные.Видимость = Ложь;
	Элементы.ГруппаПанельКоманд.ОтображатьОтступСлева = Ложь;
	
	Элементы.ГруппаЛево.ЦветФона = ЦветФона;
	Элементы.ГруппаЛево.ОтображатьОтступСлева = Ложь;
	
	Элементы.ГруппаПраво.ЦветФона = ЦветФона;
	Элементы.ГруппаПраво.ОтображатьОтступСлева = Ложь;
	Элементы.ГруппаПраво.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
	
	Элементы.ГруппаСодержание.ЦветФона = ЦветФона;
	Элементы.ГруппаСодержание.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаСодержание.ОтображатьОтступСлева = Ложь;
	
	Элементы.СтраницаНеДоступныеДляРедактированияРеквизиты.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.СтраницаНеДоступныеДляРедактированияРеквизиты.ЦветФона = ЦветФона;
	Элементы.СтраницаДоступныеДляРедактированияРеквизиты.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.СтраницаДоступныеДляРедактированияРеквизиты.ЦветФона = ЦветФона;

	Элементы.ГруппаНеДоступныеРеквизитыФайлЛево.ОтображатьОтступСлева = Ложь;
	Элементы.ГруппаНеДоступныеРеквизитыФайлПраво.ОтображатьОтступСлева = Ложь;
	Элементы.ГруппаНеДоступныеРеквизитыФайлПраво.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Одинарный;
	
	Элементы.ГруппаДоступныеРеквизитыФайлЛево.ОтображатьОтступСлева = Ложь;
	Элементы.ГруппаДоступныеРеквизитыФайлПраво.ОтображатьОтступСлева = Ложь;
	Элементы.ГруппаДоступныеРеквизитыФайлПраво.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Одинарный;
	
	Элементы.СтраницаФайл.ЦветФона = ЦветФона;
	Элементы.НадписьФайл.Видимость = Ложь;
	
	Элементы.СоставПакета.РастягиватьПоВертикали = Истина;
	Элементы.СоставПакета.РастягиватьПоГоризонтали = Истина;
	Элементы.СоставПакета.Шапка = Ложь;
	Элементы.СоставПакета.Ширина = 0;
	Элементы.СоставПакета.МаксимальнаяШирина = 0;
	
	Элементы.ГруппаПанельПакета.Поведение = ПоведениеОбычнойГруппы.Всплывающая;
	Элементы.ГруппаПанельПакета.ОтображатьЗаголовок = Истина;
	Элементы.ГруппаПанельПакета.Заголовок = НСтр("ru = 'Состав пакета'");
	Элементы.ГруппаПанельПакета.Скрыть();
	
	Элементы.ГруппаРеквизитыИОтражениеВУчете.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.КартинкаПанельИтоговРазделитель1.Видимость = Ложь;
	Элементы.КартинкаПанельОтветственногоРазделитель1.Видимость = Ложь;
	Элементы.ГруппаПанельОтраженияВУчете.Видимость = Ложь;
	Элементы.ГруппаРеквизитыСуммаНДС.ЦветФона = ЦветФона;
	Элементы.СтраницаЗагруженныеИтоги.ЦветФона = ЦветФона;
	
	Элементы.НадписьСуммаЗначение.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.НадписьНДСЗначение.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	
	Элементы.МК_НДССуммаЗаголовок.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Лево;
	
	Элементы.КнопкаСменитьОтветственного.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.НадписьОтветственный.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Лево;
	
	Элементы.ГруппаРеквизитыСуммаНДС.ГоризонтальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
	Элементы.ГруппаРеквизитыСуммаНДС.РастягиватьПоГоризонтали = Истина;
	
	Элементы.МК_СтраницаРеквизиты.Видимость = Истина;
	Элементы.Переместить(Элементы.ГруппаПанельСостояния, Элементы.МК_СтраницаРеквизиты);
	Элементы.Переместить(Элементы.ГруппаПанельПакета, Элементы.МК_СтраницаРеквизиты);
	Элементы.Переместить(Элементы.ГруппаРеквизитыИОтражениеВУчете, Элементы.МК_СтраницаРеквизиты);
	Элементы.Переместить(Элементы.СтраницаСопроводительнаяЗаписка, Элементы.МК_СтраницаРеквизиты);
	
	Элементы.МК_СтраницаРеквизиты.ЦветФона = ЦветФона;
	Элементы.СтраницаПодписи.ЦветФона = ЦветФона;
	Элементы.СтраницаДокумент.ЦветФона = ЦветФона;
	
	Элементы.Подписи.Шапка = Ложь;
	Элементы.МК_ПодписиГруппаКолонок.Видимость = Истина;
	Элементы.Переместить(Элементы.ПодписиСертификат, Элементы.МК_ПодписиГруппаКолонок);
	Элементы.Переместить(Элементы.ПодписиПодписьВернаПредставление, Элементы.МК_ПодписиГруппаКолонок);
	
	Элементы.ПодписиСертификат.Высота = 3;
	
	Элементы.ПодписиПодписьВернаПредставление.Шрифт = ШрифтыСтиля.МелкийШрифтТекста;
	Элементы.ПодписиПодписьВернаПредставление.ЦветТекста = WebЦвета.СветлоСерый;
	Элементы.ПодписиПодписьВернаПредставление.Ширина = 5;
	Элементы.ПодписиПодписьВернаПредставление.ФиксацияВТаблице = ФиксацияВТаблице.Право;
	
	Элементы.ПодписиДоверенностьПредставление.Видимость = Ложь;
	Элементы.ПодписиДатаПодписи.Видимость = Ложь;
	Элементы.СписокПодписейГруппаСтатусИКартинка.Видимость = Ложь;
	
	Элементы.ГруппаДанныеФайла.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;

	// Команды действий.
	Элементы.МК_ГруппаКоманд.Видимость = Истина;
	Элементы.Переместить(Элементы.КнопкаПодписатьОтправить, Элементы.МК_ГруппаКоманд);
	Элементы.Переместить(Элементы.КнопкаУтвердить, Элементы.МК_ГруппаКоманд);
	Элементы.Переместить(Элементы.КнопкаПодписать, Элементы.МК_ГруппаКоманд);
	Элементы.Переместить(Элементы.КнопкаОтправить, Элементы.МК_ГруппаКоманд);
	Элементы.Переместить(Элементы.КнопкаОтклонить, Элементы.МК_ГруппаКоманд);
	Элементы.Переместить(Элементы.КнопкаОтклонитьПодписание, Элементы.МК_ГруппаКоманд);
	Элементы.Переместить(Элементы.КнопкаОтправитьПовторно, Элементы.МК_ГруппаКоманд);
	
	ИнтерфейсМобильногоЭДОКлиентСервер.ОформитьАкцентнуюКнопку(МК_ЭлементыСтиля, Элементы.КнопкаПодписатьОтправить);
	ИнтерфейсМобильногоЭДОКлиентСервер.ОформитьАкцентнуюКнопку(МК_ЭлементыСтиля, Элементы.КнопкаУтвердить);
	ИнтерфейсМобильногоЭДОКлиентСервер.ОформитьАкцентнуюКнопку(МК_ЭлементыСтиля, Элементы.КнопкаПодписать);
	ИнтерфейсМобильногоЭДОКлиентСервер.ОформитьАкцентнуюКнопку(МК_ЭлементыСтиля, Элементы.КнопкаОтправить);
	ИнтерфейсМобильногоЭДОКлиентСервер.ОформитьАкцентнуюКнопку(МК_ЭлементыСтиля, Элементы.КнопкаОтклонить, Ложь);
	ИнтерфейсМобильногоЭДОКлиентСервер.ОформитьАкцентнуюКнопку(МК_ЭлементыСтиля, Элементы.КнопкаОтклонитьПодписание, Ложь);
	ИнтерфейсМобильногоЭДОКлиентСервер.ОформитьАкцентнуюКнопку(МК_ЭлементыСтиля, Элементы.КнопкаПодписатьОтправить);
	ИнтерфейсМобильногоЭДОКлиентСервер.ОформитьАкцентнуюКнопку(МК_ЭлементыСтиля, Элементы.КнопкаОтправитьПовторно);
	
	МК_ОформитьПереключателиСтраниц();
	
КонецПроцедуры

&НаКлиенте
Процедура МК_ОбновитьРеквизиты()
	
	Элементы.МК_НДССуммаЗаголовок.Видимость = Элементы.ГруппаРеквизитыСуммаНДС.Видимость;
	Элементы.МК_НДССуммаЗаголовок.Заголовок = НСтр("ru = 'Сумма'");
	
	Элементы.КартинкаПанельИтоговРазделитель1.Видимость = Ложь;
	Элементы.КартинкаПанельОтветственногоРазделитель1.Видимость = Ложь;
	Элементы.ГруппаПанельОтраженияВУчете.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура МК_ОформитьПереключателиСтраниц()
	
	Элементы.МК_ПереключателиСтраниц.Видимость = Истина;
	Элементы.ГруппаСтраницыПанельЗакладок.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;

	ИнтерфейсМобильногоЭДОКлиентСервер.ОформитьБазовуюКнопку(Элементы.МК_КнопкаСтраницаДокумент);
	ИнтерфейсМобильногоЭДОКлиентСервер.ОформитьБазовуюКнопку(Элементы.МК_КнопкаСтраницаПодписи);
	ИнтерфейсМобильногоЭДОКлиентСервер.ОформитьБазовуюКнопку(Элементы.МК_КнопкаСтраницаРеквизиты);
	
	МК_ПереключитьНаСтраницу(ЭтотОбъект, Элементы.ГруппаСтраницыПанельЗакладок, Элементы.СтраницаДокумент, Элементы.МК_КнопкаСтраницаДокумент);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура МК_ПереключитьНаСтраницу(Форма, РодительСтраниц, Страница, Кнопка)

	РодительСтраниц.ТекущаяСтраница = Страница;
	МК_СброситьОформлениеПереключателейСтраниц(Форма);
	ИнтерфейсМобильногоЭДОКлиентСервер.ОформитьПроизвольныйЭлементПоСостоянию(Форма.МК_ЭлементыСтиля, Кнопка, Истина);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура МК_СброситьОформлениеПереключателейСтраниц(Форма)

	ИнтерфейсМобильногоЭДОКлиентСервер.ОформитьПроизвольныйЭлементПоСостоянию(
		Форма.МК_ЭлементыСтиля, Форма.Элементы.МК_КнопкаСтраницаДокумент, Ложь);

	ИнтерфейсМобильногоЭДОКлиентСервер.ОформитьПроизвольныйЭлементПоСостоянию(
		Форма.МК_ЭлементыСтиля, Форма.Элементы.МК_КнопкаСтраницаПодписи, Ложь);

	ИнтерфейсМобильногоЭДОКлиентСервер.ОформитьПроизвольныйЭлементПоСостоянию(
		Форма.МК_ЭлементыСтиля, Форма.Элементы.МК_КнопкаСтраницаРеквизиты, Ложь);

КонецПроцедуры

#КонецОбласти

#КонецОбласти // СлужебныеПроцедурыИФункции