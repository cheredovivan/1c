
#Область ПрограммныйИнтерфейс

// См. ОбзорОбъектов.ОбзорОбъектаHTMLПоТипам
Функция ОбзорОбъектаHTMLПоТипам(ОбъектОбзора, УникальныйИдентификаторФормы, ТекстHTML) Экспорт
	
	// Локализация
	ТипОбъектаОбзора = ТипЗнч(ОбъектОбзора);
	
	Если ТипОбъектаОбзора = Тип("ДокументСсылка.ЭлектронныйДокументВходящийЭДО")
			Или ТипОбъектаОбзора = Тип("ДокументСсылка.ЭлектронныйДокументИсходящийЭДО") Тогда
		
		ТекстHTML = ОписаниеHTMLДокументЭДО(ОбъектОбзора);
		
		Возврат Истина;
		
	КонецЕсли;
	// Локализация Конец
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Локализация

// Формирует текст HTML обзора документа ЭДО.
//
// Параметры:
//   ДокументЭДО - ДокументСсылка.ЭлектронныйДокументВходящийЭДО, ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//
// Возвращаемое значение:
//   Строка - Текст HTML обзора документа ЭДО.
//
Функция ОписаниеHTMLДокументЭДО(ДокументЭДО) Экспорт
	
	ТекстHTML = "";
	
	ДанныеВизуализации = ОбменЭДОДокументооборотВызовСервера.ДанныеВизуализации(ДокументЭДО);
	
	Если ДанныеВизуализации.ТабличныйДокументСформирован Тогда
		
		Если ТипЗнч(ДанныеВизуализации.ТабличныйДокумент) = Тип("ТабличныйДокумент") Тогда
			
			ТабличныйДокументЭДО = ДанныеВизуализации.ТабличныйДокумент;
			
			ИмяФайлаСПутемHTML = ПолучитьИмяВременногоФайла("html");
			ТабличныйДокументЭДО.Записать(ИмяФайлаСПутемHTML, ТипФайлаТабличногоДокумента.HTML5);
			
			ТекстHTML = Новый ТекстовыйДокумент;
			ТекстHTML.Прочитать(ИмяФайлаСПутемHTML);
			ТекстИзфайла = ТекстHTML.ПолучитьТекст();
			
			ТекстHtmlНРег = НРег(ТекстИзфайла);
			
			Если СтрНайти(ТекстHtmlНРег, "charset=utf-8") <> 0 Или СтрНайти(ТекстHtmlНРег, "charset=""utf-8""") <> 0 Тогда
				ТекстHTML = Новый ТекстовыйДокумент();
				ТекстHTML.Прочитать(ИмяФайлаСПутемHTML, "UTF-8");
				ТекстИзфайла = ТекстHTML.ПолучитьТекст();
			КонецЕсли;
			
			ТекстИзфайла = ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(ТекстИзфайла);
			
			ФайлИмя = Новый Файл(ИмяФайлаСПутемHTML);
			ВременнаяПапка = ФайлИмя.Путь;
			ФайлИмя = Неопределено;
			
			ТекстHTMLСКартинками = ОбзорФайловКлиентСервер.HTMLСКартинками(
				ТекстИзфайла,
				"", 
				ВременнаяПапка);
			
			ТекстHTML = ТекстHTMLСКартинками;
			
			УдалитьФайлы(ИмяФайлаСПутемHTML);  
			
		Иначе // тут html	     
			
			ТекстHTML = ДанныеВизуализации.ТабличныйДокумент;
			
		КонецЕсли;
		
	Иначе
		
		Если ДанныеВизуализации.Свойство("ДвоичныеДанные")
				И ОбзорФайловКлиентСервер.ЭтоФайлСПодготовкойНаСервере(ДанныеВизуализации.ПрисоединенныйФайлРасширение) Тогда
			
			ПараметрыПолученияПредставления = ОбзорФайловКлиентСервер.ПараметрыПолученияПредставления();
			ПараметрыПолученияПредставления.ТребуетсяСоздатьВизуализациюЭП = Ложь;
			ПараметрыПолученияПредставления.МаксимальноеЧислоСтраницДляПредпросмотра = 10;
			ПараметрыПолученияПредставления.ПредпросмотрУрезан = Ложь;
			ПараметрыПолученияПредставления.Расширение = ДанныеВизуализации.ПрисоединенныйФайлРасширение;
			ПараметрыПолученияПредставления.ПоддерживаемыеТипы = ОбзорФайловКлиентСервер.ПоддерживаемыеТипыПредпросмотра();
			ПараметрыПолученияПредставления.ЭтоСлужебноеСообщение = Ложь;
			ПараметрыПолученияПредставления.ВсегдаПолучатьHtmlДляPdf = Истина;
			ПараметрыПолученияПредставления.Вставить("ДвоичныеДанные", ДанныеВизуализации.ДвоичныеДанные);
			
			РеквизитыФайла = Новый Структура(
				"ТекущаяВерсияРасширение, Зашифрован, ТекущаяВерсияРазмер, Наименование, ПолноеНаименование",
				ДанныеВизуализации.ПрисоединенныйФайлРасширение, Ложь, ДанныеВизуализации.ДвоичныеДанные.Размер(),
				ДанныеВизуализации.ПрисоединенныйФайлПредставление,
				ДанныеВизуализации.ПрисоединенныйФайлПредставление);
			
			ДанныеФайла = Новый Структура(
				"Расширение, ПолноеНаименование",
				ДанныеВизуализации.ПрисоединенныйФайлРасширение,
				ДанныеВизуализации.ПрисоединенныйФайлПредставление);
			
			ДанныеВизуализации = ОбзорФайловВызовСервера.РеквизитыФайлаИДанныеВизуализации(
				ПараметрыПолученияПредставления,
				РеквизитыФайла,
				ДанныеФайла);
			
			Если ТипЗнч(ДанныеВизуализации) = Тип("Строка") Тогда
				ТекстHTML = ДанныеВизуализации;
			КонецЕсли;
			
		Иначе
			
			// Сформируем HTML представление не поддерживаемого файла.
			ТекстHTML = ОбзорФайловКлиентСервер.HTMLПредставлениеНеПоддерживаемыйДокументЭДО();
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТекстHTML;
	
КонецФункции

// Локализация Конец
#КонецОбласти
