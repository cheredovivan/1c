
#Область СлужебныйПрограммныйИнтерфейс

// Составная часть процедуры
// См. ДокументооборотПраваДоступаПовтИсп.ОбъектыСЗависимымиПравами. Здесь можно дополнить массив объектов метаданных,
// чьи права можно рассчитывать многопоточно.
// 
// Параметры:
//  ОбъектыМетаданных - Массив из ОбъектМетаданных
Процедура ДополнитьОбъектыСЗависимымиПравами(ОбъектыМетаданных) Экспорт
	
	// Локализация
	ОбъектыМетаданных.Добавить(Метаданные.Документы.ВыгрузкаВССТУ);
	ОбъектыМетаданных.Добавить(Метаданные.Документы.ТранспортныйКонтейнерЭДО);
	ОбъектыМетаданных.Добавить(Метаданные.Документы.ЭлектронныйДокументВходящийЭДО);
	ОбъектыМетаданных.Добавить(Метаданные.Документы.ЭлектронныйДокументИсходящийЭДО);
	// Конец Локализация
	
КонецПроцедуры

// Возвращает Истина, если запрос прав переопределен в модуле ДокументооборотПраваДоступаЛокализация
// 
// Параметры:
//  ПервыйОбъектДоступа - СправочникОбъект, СправочникСсылка
// 
// Возвращаемое значение:
//  Булево - Запрос прав однотипных объектов подлежит локализации
Функция ЗапросПравОднотипныхОбъектовПодлежитЛокализации(ПервыйОбъектДоступа, ТипОбъекта) Экспорт
	
	// Локализация
	Если ЭтоОбъектДоступаБЭД(ПервыйОбъектДоступа) Тогда
		Возврат Истина;
	КонецЕсли;
	Если ТипОбъекта = Тип("ДокументСсылка.ВыгрузкаВССТУ") Тогда
		Возврат Истина;
	КонецЕсли;
	// Локализация Конец
	
	Возврат Ложь;
	
КонецФункции

Процедура ЗаполнитьЗапросПравОднотипныхОбъектов(ПервыйОбъектДоступа, Запрос,МетаданныеОбъекта, ТипОбъекта) Экспорт
	
	// Локализация
	Если ЭтоОбъектДоступаБЭД(ПервыйОбъектДоступа) Тогда
		
		// Для объектов БЭД дополнительно проверим и наличие полномочий, т.к. права на объекты БЭД
		// сейчас аналогичны правам их организаций. Это может привести к ситуации, когда есть права на организацию,
		// но нет доступа к документам БЭД, а функция при этом возвращает права на чтение.
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ДокументЭДО.Ссылка КАК ОбъектДоступа,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник КАК Сотрудник,
			|	ИСТИНА КАК Чтение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Добавление) КАК Добавление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Изменение) КАК Изменение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Удаление) КАК Удаление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами) КАК УправлениеПравами
			|ИЗ
			|	Документ.ЭлектронныйДокументВходящийЭДО КАК ДокументЭДО
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|		ПО (ДокументЭДО.Организация = ДескрипторыДляОбъектов.Объект)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|		ПО (ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
			|		ПО (ПраваПоДескрипторамДоступаОбъектов.Сотрудник = СотрудникиВКонтейнерах.Сотрудник)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПолномочияСотрудников КАК ПолномочияСотрудников
			|		ПО (СотрудникиВКонтейнерах.Контейнер = ПолномочияСотрудников.Владелец)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.Роли КАК Роли
			|		ПО (ПолномочияСотрудников.Полномочия = Роли.Ссылка И Роли.Роль = &РольЧтениеПоОрганизациям)
			|ГДЕ
			|	ДокументЭДО.Ссылка В (&ОбъектыДоступа)
			|	И &ПраваПоДескрипторам_ОтборПравообладателей
			|СГРУППИРОВАТЬ ПО
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|	ДокументЭДО.Ссылка
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДокументЭДО.Ссылка КАК ОбъектДоступа,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник КАК Сотрудник,
			|	ИСТИНА КАК Чтение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Добавление) КАК Добавление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Изменение) КАК Изменение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Удаление) КАК Удаление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами) КАК УправлениеПравами
			|ИЗ
			|	Документ.ЭлектронныйДокументВходящийЭДО КАК ДокументЭДО
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
			|		ПО (ДокументЭДО.Ссылка = ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|		ПО (ОбъектыУчетаДокументовЭДО.ОбъектУчета = ДескрипторыДляОбъектов.Объект)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|		ПО (ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
			|		ПО (ПраваПоДескрипторамДоступаОбъектов.Сотрудник = СотрудникиВКонтейнерах.Сотрудник)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПолномочияСотрудников КАК ПолномочияСотрудников
			|		ПО (СотрудникиВКонтейнерах.Контейнер = ПолномочияСотрудников.Владелец)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.Роли КАК Роли
			|		ПО (ПолномочияСотрудников.Полномочия = Роли.Ссылка И Роли.Роль = &РольЧтениеПоСвязаннымДокументам)
			|ГДЕ
			|	ДокументЭДО.Ссылка В (&ОбъектыДоступа)
			|	И &ПраваПоДескрипторам_ОтборПравообладателей
			|СГРУППИРОВАТЬ ПО
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|	ДокументЭДО.Ссылка";
			
		Запрос.УстановитьПараметр("РольЧтениеПоОрганизациям",
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
				Метаданные.Роли.ЧтениеЭлектронныхДокументовПоОрганизациямДокументооборот));
		Запрос.УстановитьПараметр("РольЧтениеПоСвязаннымДокументам",
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
				Метаданные.Роли.ЧтениеЭлектронныхДокументовПоДокументамДО));
				
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЭлектронныйДокументВходящийЭДО", МетаданныеОбъекта.Имя);
	
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ВыгрузкаВССТУ") Тогда
		
		#Область Запрос_ВыгрузкаВССТУ
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВыгрузкаВССТУ.Ссылка КАК ОбъектДоступа,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь,
			|	ИСТИНА КАК Чтение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Добавление) КАК Добавление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Изменение) КАК Изменение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Удаление) КАК Удаление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами) КАК УправлениеПравами
			|ИЗ
			|	Документ.ВыгрузкаВССТУ КАК ВыгрузкаВССТУ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|		ПО ВыгрузкаВССТУ.Организация = ДескрипторыДляОбъектов.Объект
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|		ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|		
			|ГДЕ
			|	ВыгрузкаВССТУ.Ссылка В (&ОбъектыДоступа)
			|	И &ПраваПоДескрипторам_ОтборПравообладателей
			|
			|СГРУППИРОВАТЬ ПО
			|	ВыгрузкаВССТУ.Ссылка,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь";
		#КонецОбласти
	
	КонецЕсли;
	
	// Локализация Конец
	
КонецПроцедуры

// Возвращает Истина, если получение описания объекта доступа переопределено в модуле ДокументооборотПраваДоступаЛокализация
// 
// Параметры:
//  ОбъектДоступа - СправочникОбъект, СправочникСсылка
// 
// Возвращаемое значение:
//  Булево - Описание объекта доступа подлежит локализации
Функция ОписаниеОбъектаДоступаПодлежитЛокализации(ОбъектДоступа) Экспорт

// Локализация
	Если ЭтоОбъектДоступаБЭД(ОбъектДоступа) Тогда
		Возврат Истина;
	КонецЕсли;
// Локализация Конец

	Возврат Ложь;

КонецФункции

// Возвращает описание объекта доступа.
// 
// Параметры:
// 	ОбъектДоступа - Объект, Ссылка
// 
// Возвращаемое значение:
// 	Объект, Структура - для объекта возвращает его самого, для ссылки возвращает структуру реквизитов.
// 
Функция ОписаниеОбъектаДоступа(ОбъектДоступа) Экспорт
	
	// Локализация
	Если ЭтоОбъектДоступаБЭД(ОбъектДоступа) Тогда
		Возврат ОписаниеОбъектаДоступаБЭД(ОбъектДоступа);
	КонецЕсли;
	// Локализация Конец
	
КонецФункции

// Возвращает Истина, если определение дескрипторов объекта переопределено в модуле ДокументооборотПраваДоступаЛокализация
// 
// Параметры:
//  ОбъектДоступа - СправочникОбъект, СправочникСсылка
// 
// Возвращаемое значение:
//  Булево - Определение дескрипторов объекта подлежит локализации
Функция ОпределениеДескрипторовОбъектаПодлежитЛокализации(ОбъектДоступа) Экспорт
	
	// Локализация
	Если ЭтоОбъектДоступаБЭД(ОбъектДоступа) Тогда
		Возврат Истина;
	КонецЕсли;
	// Локализация Конец
	
	Возврат Ложь;
	
КонецФункции

// Определяет дескрипторы доступа для указанного объекта доступа и записывает их в базу.
// 
// Параметры:
//  ОбъектДоступа - Ссылка, Объект - объект, дескрипторы которого надо определить или ссылка на него.
//  ТаблицаДескрипторов  - ТаблицаЗначений
//  ОпределятьЗависимыеПрава - Булево - признак необходимости определения зависимых прав.
//  ПротоколРасчетаПрав - Массив, Неопределено - протокол расчет прав для отображения в интерфейсе.
// 
// Возвращаемое значение:
//  Таблица значений - итоговая таблица дескрипторов объекта.
Процедура ОпределитьДескрипторыОбъекта(ОбъектДоступа, ТаблицаДескрипторов, ОпределятьЗависимыеПрава = Истина, ПротоколРасчетаПрав = Неопределено) Экспорт
// Локализация
	Если ЭтоОбъектДоступаБЭД(ОбъектДоступа) Тогда
		ЗаполнитьДескрипторыОбъектаБЭД(ОбъектДоступа, ТаблицаДескрипторов, ПротоколРасчетаПрав);
	КонецЕсли;
// Локализация Конец
КонецПроцедуры

#КонецОбласти

// Локализация
#Область СлужебныеПроцедурыИФункции

#Область ПраваНаОбъектыБЭД

// Возвращает описание типов со ссылками на документы БЭД, для которых нужно определять дескрипторы особым образом
// 
// Возвращаемое значение:
//  ОписаниеТипов - Типы ссылок БЭД, для которых нужно определять дескрипторы
Функция ТипыСсылокБЭДДляОпределенияДескрипторов()
	
	Типы = Новый Массив;
	Типы.Добавить(Тип("ДокументСсылка.ЭлектронныйДокументВходящийЭДО"));
	Типы.Добавить(Тип("ДокументСсылка.ЭлектронныйДокументИсходящийЭДО"));
	Типы.Добавить(Тип("ДокументСсылка.ТранспортныйКонтейнерЭДО"));

	Результат = Новый ОписаниеТипов(Типы);
	
	Возврат Результат;
	
КонецФункции

// Проверяет, относится ли объект доступа к БЭД. Если да, то для него нужно рассчитывать дескрипторы самостоятельно.
// 
// Параметры:
//  ОбъектДоступа - ДокументОбъект, ДокументСсылка
// 
// Возвращаемое значение:
//  Булево - Это объект доступа БЭД
Функция ЭтоОбъектДоступаБЭД(ОбъектДоступа)
	
	ТипСсылки = ТипЗнч(ОбъектДоступа.Ссылка);
	
	Возврат ТипыСсылокБЭДДляОпределенияДескрипторов().СодержитТип(ТипСсылки);
	
КонецФункции

// Возвращает описание объекта доступа.
// 
// Параметры:
// 	ОбъектДоступа - Объект, Ссылка
// 
// Возвращаемое значение:
// 	Объект, Структура - для объекта возвращает его самого, для ссылки возвращает структуру реквизитов.
Функция ОписаниеОбъектаДоступаБЭД(ОбъектДоступа)
	
	ОписаниеОбъекта = ОбъектДоступа;
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ОбъектДоступа)) Тогда
		
		// Для ссылки дескриптор заполняется на основании описания.
		СведенияОПоляхДоступа = "Ссылка, Организация";
		ОписаниеОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ОбъектДоступа, СведенияОПоляхДоступа);
		
		Если Не ЗначениеЗаполнено(ОписаниеОбъекта.Ссылка) Тогда
			ОписаниеОбъекта.Ссылка = ОбъектДоступа;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

// Заполняет дескрипторы доступа для документов БЭД.
// Для документов БЭД права доступа аналогичны правам на чтение организации документа.
// 
// Параметры:
//  ОписаниеОбъекта - См. ОписаниеОбъектаДоступаБЭД
//  ТаблицаДескрипторов - ТаблицаЗначений
//  ПротоколРасчетаПрав - Массив, Неопределено - протокол расчет прав для отображения в интерфейсе
//
Процедура ЗаполнитьДескрипторыОбъектаБЭД(ОписаниеОбъекта, ТаблицаДескрипторов, ПротоколРасчетаПрав)
	
	ДокументооборотПраваДоступа.ЗаполнитьДескрипторыОбъектаОтВладельца(ОписаниеОбъекта,
		ТаблицаДескрипторов, ОписаниеОбъекта.Организация);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
// Локализация Конец