///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает коллекцию общих модулей, для загрузки структуры метаданных в редактор.
//
// Возвращаемое значение:
//   Структура - коллекция общих модулей:
//     * Общие - Структура - общие модули.
//     * Глобальные - Структура - глобальные модули.
//
Функция КоллекцияОбщихМодулей() Экспорт
	
	КоллекцияОбщихМодулей = Новый Структура;
	КоллекцияОбщихМодулей.Вставить("Общие", Новый Структура);
	КоллекцияОбщихМодулей.Вставить("Глобальные", Новый Структура);
	
	Для Каждого ОбъектМетаданных Из Метаданные.ОбщиеМодули Цикл
		Если ОбъектМетаданных.Глобальный Тогда
			КоллекцияОбщихМодулей.Глобальные.Вставить(ОбъектМетаданных.Имя, Новый Структура);
		Иначе
			КоллекцияОбщихМодулей.Общие.Вставить(ОбъектМетаданных.Имя, Новый Структура);
		КонецЕсли;
	КонецЦикла;
	
	Возврат КоллекцияОбщихМодулей;
	
КонецФункции

// Возвращает путь к данным редактора встроенного языка.
//
// Параметры:
//   ВоВременномХранилище - Булево - если Истина, то возвращает адрес во временном хранилище, иначе возвращает
//     полный текст HTML документа.
//
// Возвращаемое значение:
//   Строка - данную строку нужно поместить в свойство ПутьКДанным поля HTML документа на форме.
//
Функция ПутьКДаннымРедактора(ВоВременномХранилище = Истина) Экспорт
	
	Если ВоВременномХранилище Тогда
		ДвоичныеДанныеРедактора = ПолучитьОбщийМакет("РедакторВстроенногоЯзыка");
		АдресХранилища = ПоместитьВоВременноеХранилище(ДвоичныеДанныеРедактора, Новый УникальныйИдентификатор);
		ПутьКДаннымРедактора = ПолучитьНавигационнуюСсылкуИнформационнойБазы() + "/" + АдресХранилища;
	Иначе
		ПутьКДаннымРедактора = ПолучитьОбщийМакет("РедакторВстроенногоЯзыкаHTML").ПолучитьТекст();
	КонецЕсли;
	
	Возврат ПутьКДаннымРедактора;
	
КонецФункции

#КонецОбласти