#Область ПрограммныйИнтерфейс

// Заменяет в тексте реквизиты сведений об участниках (в т.ч. участников ознакомлений) на соответствующие языку
//
// Параметры:
//  Текст - Строка - Текст запроса
// 
// Возвращаемое значение:
//  Строка
//
Функция ТекстЗапросаСведенийОбУчастникахСУчетомЯзыка(Текст) Экспорт
	
	Если МультиязычностьСервер.ИспользуетсяДополнительныйЯзык(1)
		Или МультиязычностьСервер.ИспользуетсяДополнительныйЯзык(2) Тогда
		
		СуффиксТекущегоЯзыка = МультиязычностьСервер.СуффиксТекущегоЯзыка();
		Текст = СтрЗаменить(Текст,
			"УчастникиОзнакомлений.ПредставлениеФактическогоИсполнителя", 
			"УчастникиОзнакомлений.ПредставлениеФактическогоИсполнителя" + СуффиксТекущегоЯзыка);
		Текст = СтрЗаменить(Текст,
			"УчастникиОзнакомлений.ПредставлениеСотрудникаДляПЭП", 
			"УчастникиОзнакомлений.ПредставлениеСотрудникаДляПЭП" + СуффиксТекущегоЯзыка);
		Текст = СтрЗаменить(Текст,
			"СведенияОбУчастникахДействий.ПредставлениеФактическогоИсполнителя", 
			"СведенияОбУчастникахДействий.ПредставлениеФактическогоИсполнителя" + СуффиксТекущегоЯзыка);
		Текст = СтрЗаменить(Текст,
			"СведенияОбУчастникахДействий.ПредставлениеСотрудникаДляПЭП", 
			"СведенияОбУчастникахДействий.ПредставлениеСотрудникаДляПЭП" + СуффиксТекущегоЯзыка);
		
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

// Локализация
// Возвращает суффикс русского языка
// 
// Возвращаемое значение:
//  Строка
//
Функция СуффиксРусскогоЯзыка() Экспорт
	
	КодРусскогоЯзыка = "ru";
	
	ДопЯзык1 = МультиязычностьСервер.КодПервогоДополнительногоЯзыкаИнформационнойБазы();
	ДопЯзык2 = МультиязычностьСервер.КодВторогоДополнительногоЯзыкаИнформационнойБазы();
	
	Если ДопЯзык1 = КодРусскогоЯзыка Тогда
		
		Возврат "Язык1";
		
	ИначеЕсли ДопЯзык2 = КодРусскогоЯзыка Тогда
		
		Возврат "Язык2";
		
	Иначе
		
		Возврат "";
		
	КонецЕсли;
	
КонецФункции
// Локализация Конец

// Проверяет, что в конфигурации используется только один язык.
// 
// Возвращаемое значение:
//  Булево - Используется только один язык
Функция ИспользуетсяТолькоОдинЯзык() Экспорт
	
	СведенияОЯзыках = МультиязычностьСервер.СведенияОЯзыках();
	
	Если СведенияОЯзыках.Используется.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из СведенияОЯзыках.Используется Цикл
		
		Если КлючИЗначение.Значение Тогда
			Возврат Ложь;
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти