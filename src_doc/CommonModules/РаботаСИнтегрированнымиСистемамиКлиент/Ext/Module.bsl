///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ОбщиеПроцедурыИФункции

// Открывает навигационную ссылку в программе, которая ассоциирована с протоколом навигационной ссылки.
// Допустимые протоколы: http, https, e1c, v8help, mailto, tel, skype.
//
// Параметры:
//   НавигационнаяСсылка - Строка - ссылка, которую требуется открыть.
//
Процедура ОткрытьНавигационнуюСсылку(Знач НавигационнаяСсылка) Экспорт
	
	ОбработатьНавигационнуюСсылку(НавигационнаяСсылка);
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(НавигационнаяСсылка);
	
КонецПроцедуры

// Запускает базу интегрированной системы и переходит по указанной навигационной ссылке.
//
// Параметры:
//   СсылкаНаОбъектИС - Строка - навигационная ссылка по которой требуется перейти в базе ИС.
//   ИнтегрированнаяСистема - ПланОбменаСсылка.ИнтегрированныеСистемы - узел интегрированной системы.
//   АдресБазыИС - Строка - адрес публикации базы ИС, которую требуется открыть.
//
Процедура ОткрытьОбъектИС(СсылкаНаОбъектИС, ИнтегрированнаяСистема = Неопределено, АдресБазыИС = "") Экспорт
	
	Если Не ЭтоНавигационнаяСсылка(СсылкаНаОбъектИС) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(АдресБазыИС) И ЗначениеЗаполнено(ИнтегрированнаяСистема) Тогда
		АдресБазыИС = РаботаСИнтегрированнымиСистемамиВызовСервера.НавигационнаяСсылкаБазыИС(ИнтегрированнаяСистема);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(АдресБазыИС) Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьНавигационнуюСсылку(СтрШаблон("%1#%2", АдресБазыИС, СсылкаНаОбъектИС));
	
КонецПроцедуры

// Проверяет, является ли переданная строка внешней или внутренней навигационной ссылкой.
//
// Параметры:
//   Строка - Строка - навигационная ссылка.
//
// Возвращаемое значение:
//   Булево - результат проверки.
//
Функция ЭтоНавигационнаяСсылка(Знач Строка) Экспорт
	
	ОбработатьНавигационнуюСсылку(Строка);
	Строка = НРег(Строка);
	
	Возврат СтрНачинаетсяС(Строка, "e1c:")
		Или СтрНачинаетсяС(Строка, "e1cib/")
		Или СтрНачинаетсяС(Строка, "e1ccs/")
		Или (СтрНачинаетсяС(Строка, "http") И (СтрНайти(Строка, "#e1c") > 0));
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьНавигационнуюСсылку(НавигационнаяСсылка)
	
	НавигационнаяСсылка = СокрЛП(СтрЗаменить(НавигационнаяСсылка, Символы.НПП, " "));
	
КонецПроцедуры

#КонецОбласти
