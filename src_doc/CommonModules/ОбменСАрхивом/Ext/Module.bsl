#Область ПрограммныйИнтерфейс

// Обработчик регламентного задания "Обмен с Архивом".
//
Процедура ВыполнитьОбмен() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ОбменСАрхивом);
	
	МиграцияДанныхИзВнешнихСистемСервер.ПриНачалеРаботыРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ОбменСАрхивом);
	
	ПараметрыТранспорта = ОбменСАрхивомТранспорт.ПараметрыТранспортаДляСеансаОбмена();
	Если ПараметрыТранспорта = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьДанные(ПараметрыТранспорта);
	ВыгрузитьДанные(ПараметрыТранспорта);
	
КонецПроцедуры

// Возвращает список доступных для текущей конфигурации версий формата обмена.
//
// Возвращаемое значение:
//  СписокЗначений из Строка - список форматов, отсортированный по убыванию.
//   Значение - пространство имен XDTO-пакета поддерживаемой версии,
//   Представление - пользовательское представление версии.
//
Функция ВерсииФорматаДоступные() Экспорт
	
	СписокВерсий = Новый СписокЗначений;
	
	НестандартныеПредставления = Новый Соответствие;
	НестандартныеПредставления[Метаданные.ПакетыXDTO.АДХ_1_0_3] = "1.0.6";
	
	ТаблицаВерсий = Новый ТаблицаЗначений;
	ТаблицаВерсий.Колонки.Добавить("ПространствоИмен");
	ТаблицаВерсий.Колонки.Добавить("Представление");
	ТаблицаВерсий.Колонки.Добавить("ЗначениеДляСортировки");
	
	Для Каждого Пакет Из Метаданные.ПакетыXDTO Цикл
		Если Не СтрНачинаетсяС(Пакет.Имя, "АДХ_") Тогда
			Продолжить;
		КонецЕсли;
		СтрокаВерсии = ТаблицаВерсий.Добавить();
		СтрокаВерсии.ПространствоИмен = Пакет.ПространствоИмен;
		СтрокаВерсии.Представление = НестандартныеПредставления[Пакет];
		Если СтрокаВерсии.Представление = Неопределено Тогда
			ЧастиПространстваИмен = СтрРазделить(СтрокаВерсии.ПространствоИмен, "/");
			СтрокаВерсии.Представление = ЧастиПространстваИмен[ЧастиПространстваИмен.Количество() - 1];
		КонецЕсли;
		СтрокаВерсии.ЗначениеДляСортировки = 0;
		ЧастиНомераВерсии = СтрРазделить(СтрокаВерсии.Представление, ".");
		КолСтрок = ЧастиНомераВерсии.Количество();
		Для Сч = 1 По КолСтрок Цикл
			ЧастьНомера = Число(ЧастиНомераВерсии[КолСтрок - Сч]);
			СтрокаВерсии.ЗначениеДляСортировки = СтрокаВерсии.ЗначениеДляСортировки + ЧастьНомера * Pow(1000, Сч-1);
		КонецЦикла;
	КонецЦикла;
	
	ТаблицаВерсий.Сортировать("ЗначениеДляСортировки Убыв");
	Для Каждого СтрокаВерсии Из ТаблицаВерсий Цикл
		СписокВерсий.Добавить(СтрокаВерсии.ПространствоИмен, СтрокаВерсии.Представление);
	КонецЦикла;
	
	Возврат СписокВерсий;
	
КонецФункции

// Возвращает пространство имен актуального пакета XDTO.
// 
// Возвращаемое значение:
//  Строка - версия формата (пространство имен XDTO-пакета).
// 
Функция ВерсияФорматаДляОтправкиСообщений() Экспорт
	
	ВерсияДляОтправки = ВерсияФорматаБазыАрхива();
	ВерсияМаксимальная = ВерсияФорматаМаксимальная();
	
	Если СравнитьВерсииФормата(ВерсияДляОтправки, ВерсияМаксимальная) > 0 Тогда
		ВерсияДляОтправки = ВерсияМаксимальная;
	КонецЕсли;
	
	Возврат ВерсияДляОтправки;
	
КонецФункции

// Возвращает версию формата, поддерживаемую 1С:Архивом. Если не заполнена, выдается исключение.
// 
// Параметры:
//  ВызыватьИсключение - Булево - Если Истина и константа не заполнена, то будет вызвано исключение.
// 
// Возвращаемое значение:
//  Строка - версия формата (пространство имен XDTO-пакета).
// 
Функция ВерсияФорматаБазыАрхива(ВызыватьИсключение = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Версия = Константы.ВерсияФорматаОбменаПоддерживаемая1САрхивом.Получить();
	Если ВызыватьИсключение И Не ЗначениеЗаполнено(Версия) Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнено значение настройки ""%1"". Обратитесь к администратору.
			|
			|Контроль версий осуществляется автоматически, но что-то пошло не так. Пожалуйста, обратитесь в поддержку. Для обхода проблемы можно указать версию, которую поддерживает ваша база 1С:Архива, вручную. См. ""Настройка программы - Интеграция с 1С:Архивом - Версия формата"".'"),
			Метаданные.Константы.ВерсияФорматаОбменаПоддерживаемая1САрхивом.Синоним);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Возврат Версия;
	
КонецФункции

// Возвращает максимальную версию формата, доступную в текущей конфигурации.
// 
// Параметры:
//  ВычислитьИзМетаданных - Булево - если Истина, то значение будет вычислено заново
//  	на основании метаданных конфигурации. Если Ложь, значение будет получено из константы.
//
// Возвращаемое значение:
//  Строка - версия формата, доступная для текущей конфигурации.
//
Функция ВерсияФорматаМаксимальная(ВычислитьИзМетаданных = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Версия = Неопределено;
	Если Не ВычислитьИзМетаданных Тогда
		Версия = Константы.ВерсияФорматаОбменаС1САрхивомМаксимальная.Получить();
	КонецЕсли;
	
	Если ВычислитьИзМетаданных Или Не ЗначениеЗаполнено(Версия) Тогда
		ВсеВерсииФормата = ВерсииФорматаДоступные();
		Версия = ВсеВерсииФормата[0].Значение;
	КонецЕсли;
	
	Возврат Версия;
	
КонецФункции

// Формирует сообщения для 1С:Архива о поддерживаемой версии формата файлов обмена.
//
Процедура СформироватьСообщениеОВерсииФормата() Экспорт
	
	ВерсияФормата = ВерсияФорматаМаксимальная();
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON(, Символы.Таб));
	ЗаписатьJSON(ЗаписьJSON, Новый Структура(ИмяСвойстваВерсияФормата(), ВерсияФормата));
	СтрокаJSON = ЗаписьJSON.Закрыть();
	
	НачатьТранзакцию();
	Попытка
		РегистрыСведений.СообщенияДляАрхива.Добавить(ИмяСвойстваВерсияФормата(), СтрокаJSON);
		Событие = СобытиеДляЛога();
		Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Информация;
		Событие.КраткоеОписание = СтрШаблон(
			НСтр("ru = 'Сформировано сообщение о версии формата (%1)'"), ВерсияФормата);
		ЗаписатьВЛог(Событие);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Не удалось сформировать сообщение с версией формата по причине:
			|
			|%1'"), ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ОписаниеОшибки;
	КонецПопытки;
	
КонецПроцедуры

// Сравнивает две строки версий формата.
//
// Параметры:
//  ВерсияФормата1 - Строка - формат в виде пространства имен (например, "http://v8.1c.ru/doc8/LTA/1.0.8").
//  ВерсияФормата2 - Строка - формат в виде пространства имен.
//
// Возвращаемое значение:
//   Число   - больше 0, если ВерсияФормата1 > ВерсияФормата2; 0, если версии равны.
//
Функция СравнитьВерсииФормата(ВерсияФормата1, ВерсияФормата2) Экспорт
	
	ПредставлениеВерсии1 = ПредставлениеВерсииФормата(ВерсияФормата1) + ".0";
	ПредставлениеВерсии2 = ПредставлениеВерсииФормата(ВерсияФормата2) + ".0";
	
	Возврат ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ПредставлениеВерсии1, ПредставлениеВерсии2);
	
КонецФункции

// Пользовательское представление версии формата.
//
// Параметры:
//  Версия - Строка - формат в виде пространства имен (например, "http://v8.1c.ru/doc8/LTA/1.0.8").
//
// Возвращаемое значение:
//   Строка - представление версии для пользовательского интерфейса.
//
Функция ПредставлениеВерсииФормата(Версия) Экспорт
	
	ПредставлениеВерсии = Версия;
	ЧастиПространстваИмен = СтрРазделить(Версия, "/");
	Если ЧастиПространстваИмен.Количество() > 1 Тогда
		ПредставлениеВерсии = ЧастиПространстваИмен[ЧастиПространстваИмен.Количество() - 1];
	КонецЕсли;
	
	Возврат ПредставлениеВерсии;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ТипыXDTOПоТипамЗначений() Экспорт
	
	ИменаТиповXDTO = Новый Соответствие;
	ИменаТиповXDTO[Тип("СправочникСсылка.ВидыДокументов")]             = "DocumentType";
	ИменаТиповXDTO[Тип("СправочникСсылка.Организации")]                = "Organization";
	ИменаТиповXDTO[Тип("СправочникСсылка.СтруктураПредприятия")]       = "Department";
	ИменаТиповXDTO[Тип("СправочникСсылка.Должности")]                  = "Title";
	ИменаТиповXDTO[Тип("СправочникСсылка.ФизическиеЛица")]             = "Individual";
	ИменаТиповXDTO[Тип("СправочникСсылка.Сотрудники")]                 = "Employee";
	ИменаТиповXDTO[Тип("СправочникСсылка.ГрифыДоступа")]               = "SecurityLevel";
	ИменаТиповXDTO[Тип("СправочникСсылка.ГруппыДоступаФизическихЛиц")] = "IndividualAccessGroup";
	ИменаТиповXDTO[Тип("СправочникСсылка.Контрагенты")]                = "Counterparty";
	ИменаТиповXDTO[Тип("СправочникСсылка.Проекты")]                    = "Project";
	ИменаТиповXDTO[Тип("СправочникСсылка.РазделыНоменклатурыДел")]     = "CaseFilesListSection";
	ИменаТиповXDTO[Тип("СправочникСсылка.НоменклатураДел")]            = "CaseFilesCatalog";
	ИменаТиповXDTO[Тип("СправочникСсылка.ДелаХраненияДокументов")]     = "Dossier";
	
	ИменаТиповXDTO[Тип("СправочникСсылка.ДокументыПредприятия")] = "Document";
	ИменаТиповXDTO[Тип("ДокументСсылка.ПередачаДелВАрхив")]      = "HandOverList";
	ИменаТиповXDTO[Тип("ДокументСсылка.УничтожениеДел")]         = "DossiersDestruction";
	ИменаТиповXDTO[Тип("СправочникСсылка.Валюты")]               = "Currency";
	
	ИменаТиповXDTO[Тип("Строка")] = "String";
	ИменаТиповXDTO[Тип("Число")]  = "Numeric";
	ИменаТиповXDTO[Тип("Булево")] = "Boolean";
	ИменаТиповXDTO[Тип("Дата")]   = "Date";
	
	Возврат ИменаТиповXDTO;
	
КонецФункции

Функция МетаданныеОбъектовПоТипамXDTO() Экспорт
	
	Типы = Новый Соответствие;
	ИменаТиповXDTO = ТипыXDTOПоТипамЗначений();
	Для Каждого КлючИЗначение Из ИменаТиповXDTO Цикл
		НайденныйОбъектМетаданных = Метаданные.НайтиПоТипу(КлючИЗначение.Ключ);
		Если НайденныйОбъектМетаданных <> Неопределено Тогда
			Типы[КлючИЗначение.Значение] = НайденныйОбъектМетаданных;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Типы;
	
КонецФункции

Функция СобытиеЖурналаРегистрацииВыгрузка() Экспорт
	
	Возврат НСтр("ru = 'Обмен с 1С:Архивом. Выгрузка данных.'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

Функция СобытиеЖурналаРегистрацииЗагрузка() Экспорт
	
	Возврат НСтр("ru = 'Обмен с 1С:Архивом. Загрузка данных.'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

Процедура УдалитьВременныеФайлы(ВременнаяПапка, ЭтоЗагрузкаДанных = Ложь) Экспорт
	
	СобытиеЖурналаРегистрации = ?(ЭтоЗагрузкаДанных,
		СобытиеЖурналаРегистрацииЗагрузка(), СобытиеЖурналаРегистрацииВыгрузка());
	
	Попытка
		УдалитьФайлы(ВременнаяПапка);
	Исключение
		Событие = СобытиеДляЛога("", ЭтоЗагрузкаДанных);
		Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка;
		Событие.КраткоеОписание = СтрШаблон(НСтр("ru = 'Ошибка при удалении временных файлов: %1'"),
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Событие.ДополнительныеСведения = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписатьВЛог(Событие);
	КонецПопытки;
	
КонецПроцедуры

// Проверяет заполнение свойств объекта XDTO разными методами в зависимости от версии формата обмена
// - с помощью строенного метода Проверить() при загрузке данных
// - все свойства объекта XDTO, включая подчиненные при выгрузке данных
// - только свойства объекта XDTO одного уровня, без подчиненных при выгрузке данных
// Используется, начиная с версии формата обмена АДХ 1.0.11
//
// Параметры:
//  ОбъектXDTO - ОбъектXDTO - ОбъектXDTO для проверки.
//  ЭтоЗагрузкаДанных - Булево - признак того, что загружаются данные.
//
// Возвращаемое значение:
//  Строка - ошибки при проверке, если ошибок нет, то пустая строка.
//
Функция ПроверитьЗаполнениеСвойствXDTO(ОбъектXDTO, ЭтоЗагрузкаДанных = Ложь) Экспорт
	
	СвойствоЭтоГруппа = ОбъектXDTO.Свойства().Получить("IsFolder");
	Если СвойствоЭтоГруппа <> Неопределено
		И ОбъектXDTO.IsFolder = Истина Тогда
		Возврат "";
	КонецЕсли;
	
	// Новая версия формата обмена, начиная с АДХ 1.0.11
	ЭтоНоваяВерсияФормата = СравнитьВерсииФормата(ОбъектXDTO.Тип().URIПространстваИмен,
		Метаданные.ПакетыXDTO.АДХ_1_0_10.ПространствоИмен) > 0;
	
	// Проверка объекта XDTO встроенным методом Проверить(),
	// для версий формата обмена до АДХ 1.0.10 включительно при загрузке данных.
	// Используется начиная с версии Документооборота 3.0.17
	Если ЭтоЗагрузкаДанных И Не ЭтоНоваяВерсияФормата Тогда
		ОписаниеОшибки = "";
		Попытка
			ОбъектXDTO.Проверить();
		Исключение
			ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		КонецПопытки;
		Возврат ОписаниеОшибки;
	КонецЕсли;
	
	// Проверка значений свойств объекта XDTO без подчиненных для старых версий формата обмена до АДХ 1.0.11 включительно
	// и с подчиненными для новых версий формата обмена.
	// Используется начиная с версии Документооборота 3.0.17
	МассивОшибок = Новый Массив;
	ОбъектыДляПроверки = Новый Массив;
	ОбъектыДляПроверки.Добавить(ОбъектXDTO);
	ПроверятьПодчиненные = ЭтоНоваяВерсияФормата;
	
	ДополнительныеСведенияПоОшибкам = Новый Соответствие;
	
	Пока ОбъектыДляПроверки.Количество() > 0 Цикл
		
		ОбъектДляПроверки = ОбъектыДляПроверки[0];
		ОбъектыДляПроверки.Удалить(0);
		
		Для Каждого СвойствоXDTO Из ОбъектДляПроверки.Свойства() Цикл
			
			Если ТипЗнч(ОбъектДляПроверки[СвойствоXDTO.Имя]) = Тип("ОбъектXDTO") И ПроверятьПодчиненные Тогда
				ОбъектыДляПроверки.Добавить(ОбъектДляПроверки.ПолучитьXDTO(СвойствоXDTO));
				Если ЗначениеЗаполнено(ОбъектДляПроверки.Владелец()) Тогда
					ИмяВладеющегоСвойства = ОбъектДляПроверки.Владелец().Тип().Имя;
				Иначе
					ИмяВладеющегоСвойства = ОбъектДляПроверки.Тип().Имя;
				КонецЕсли;
				ДополнительныеСведения =
					СтрШаблон(НСтр("ru = 'в свойстве ""%1""'"),
					ИмяВладеющегоСвойства);
				ДополнительныеСведенияПоОшибкам.Вставить(ОбъектДляПроверки.ПолучитьXDTO(СвойствоXDTO), ДополнительныеСведения);
			ИначеЕсли ТипЗнч(ОбъектДляПроверки[СвойствоXDTO.Имя]) = Тип("СписокXDTO") И ПроверятьПодчиненные Тогда
				СтрокиСпискаXDTO = ОбъектДляПроверки.ПолучитьСписок(СвойствоXDTO);
				Если СтрокиСпискаXDTO = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				НомерСтроки = 1;
				Для Каждого ЭлементСпискаXDTO Из СтрокиСпискаXDTO Цикл
					ОбъектыДляПроверки.Добавить(ЭлементСпискаXDTO);
					ДополнительныеСведения =
						СтрШаблон(НСтр("ru = 'в свойстве ""%1"" (строка %2)'"),
						СтрокиСпискаXDTO.ВладеющееСвойство.Имя, НомерСтроки);
					ДополнительныеСведенияПоОшибкам.Вставить(ЭлементСпискаXDTO, ДополнительныеСведения);
					НомерСтроки = НомерСтроки + 1;
				КонецЦикла;
			Иначе
				// Дополнительных действий не требуется.
			КонецЕсли;
			
			Если СвойствоXDTO.НижняяГраница <> 1
				Или СвойствоXDTO.ВозможноПустое = Истина Тогда
				Продолжить;
			КонецЕсли;
			Если ЗначениеЗаполнено(ОбъектДляПроверки[СвойствоXDTO.Имя]) Тогда
				Продолжить;
			КонецЕсли;
			
			ДополнительныеСведения = ДополнительныеСведенияПоОшибкам.Получить(ОбъектДляПроверки);
			Если ЗначениеЗаполнено(ДополнительныеСведения) Тогда
				ОписаниеОшибки = СтрШаблон(НСтр("ru = '%1 не заполнено поле ""%2""'"),
					ДополнительныеСведения,
					СвойствоXDTO.Имя);
			Иначе
				ОписаниеОшибки = СтрШаблон(НСтр("ru = 'не заполнено поле ""%1""'"),
					СвойствоXDTO.Имя);
			КонецЕсли;
			МассивОшибок.Добавить(ОписаниеОшибки);
		КонецЦикла;
		
	КонецЦикла;

	Возврат СтрСоединить(МассивОшибок, "; ");
		
КонецФункции

// Получает объекты для выгрузки в 1С:Архив.
// 
// Параметры:
//  ОбъектыМетаданных - Массив из ОбъектМетаданных - объекты метаданных
//  ОбъектыСОшибками - Неопределено, Массив из ЛюбаяСсылка - объекты с ошибками, их не нужно возвращать.
// 
// Возвращаемое значение:
//  Массив из ЛюбаяСсылка - Порция измененных данных для выгрузки
// 
Функция ПорцияИзмененныхДанныхДляВыгрузки(ОбъектыМетаданных, ОбъектыСОшибками = Неопределено) Экспорт
	
	ШаблонТекстаЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ТаблицаИзменений.Ссылка КАК Ссылка
		|ИЗ
		|	#ТаблицаИзменений КАК ТаблицаИзменений
		|ГДЕ
		|	ТаблицаИзменений.Узел = &УзелАрхив
		|	И НЕ ТаблицаИзменений.Ссылка В (&ОбъектыСОшибками)";
	
	Для Каждого ОбъектМетаданных Из ОбъектыМетаданных Цикл
		ИмяТаблицыИзменений = СтрШаблон("%1.Изменения", ОбъектМетаданных.ПолноеИмя());
		Запрос = Новый Запрос(СтрЗаменить(ШаблонТекстаЗапроса, "#ТаблицаИзменений", ИмяТаблицыИзменений));
		Запрос.УстановитьПараметр("УзелАрхив", ПланыОбмена.ОбменНСИСАрхивом.УзелАрхив());
		Запрос.УстановитьПараметр("ОбъектыСОшибками", ОбъектыСОшибками);
		//@skip-check query-in-loop
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			Возврат Результат.Выгрузить().ВыгрузитьКолонку(0);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый Массив;
	
КонецФункции

// Строка с датой с точностью до мс.
// 
// Возвращаемое значение:
//  Строка - текущая отметка времени.
// 
Функция ТекущаяОтметкаВремени() Экспорт
	
	ОтметкаЧислом = ТекущаяУниверсальнаяДатаВМиллисекундах();
	ОтметкаДатой = Дата(1, 1, 1) + Цел(ОтметкаЧислом / 1000);
	Миллисекунд = ОтметкаЧислом % 1000;
	
	Текущая = Формат(ОтметкаДатой, "ДФ=yyyyMMddHHmmss")
		+ Формат(Миллисекунд, "ЧЦ=3; ЧН=000; ЧВН=; ЧГ=0");
		
	Возврат Текущая;
	
КонецФункции

// Строка с датой в формате метки.
// 
// Возвращаемое значение:
//  Строка - текущая отметка времени.
// 
Функция ОтметкаВремениПоДате(Дата) Экспорт
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		ВызватьИсключение НСтр("ru = 'Не заполнена дата для получения отметки'");
	КонецЕсли;
	
	Отметка = Формат(Дата, "ДФ=yyyyMMddHHmmss")
		+ Формат(0, "ЧЦ=3; ЧН=000; ЧВН=; ЧГ=0");
	
	Возврат Отметка;
	
КонецФункции

Процедура ПриОбновленииИнформационнойБазы(Параметры = Неопределено) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИнтеграцияС1САрхивом") = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Параметры) = Тип("Структура")
		И Параметры.Свойство("МонопольныйРежим")
		И Параметры.МонопольныйРежим = Ложь Тогда
		Параметры.МонопольныйРежим = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Константы.ВерсияФорматаОбменаС1САрхивомМаксимальная.Получить()) Тогда
		// Отслеживание добавленных типов данных выполняется с версии формата 1.0.11.
		Константы.ВерсияФорматаОбменаС1САрхивомМаксимальная.Установить(
			Метаданные.ПакетыXDTO.АДХ_1_0_11.ПространствоИмен);
	КонецЕсли;
	
	МаксВерсияФорматаДоОбновления = ВерсияФорматаМаксимальная();
	МаксВерсияФорматаПослеОбновления = ВерсияФорматаМаксимальная(Истина);
	Если МаксВерсияФорматаДоОбновления = МаксВерсияФорматаПослеОбновления Тогда
		Возврат;
	КонецЕсли;
	
	// Отправка версии в 1С:Архив.
	ОбменСАрхивом.СформироватьСообщениеОВерсииФормата();
	
	// Регистрация новых данных в обмене.
	НачатьТранзакцию();
	Попытка
		ВерсияДляОтправкиДоОбновления = ВерсияФорматаДляОтправкиСообщений();
		Константы.ВерсияФорматаОбменаС1САрхивомМаксимальная.Установить(МаксВерсияФорматаПослеОбновления);
		ВерсияДляОтправкиПослеОбновления = ВерсияФорматаДляОтправкиСообщений();
		ЗарегистрироватьНовыеДанныеВОбмене(ВерсияДляОтправкиДоОбновления, ВерсияДляОтправкиПослеОбновления);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
	КонецПопытки;
	
КонецПроцедуры

// Регистрирует к обмену данные, возможность обмена которыми была добавлена в новых версиях формата.
// 
// Параметры:
//  СтараяВерсияФормата - Строка - версия, которая использовалась для обмена до внесения изменений.
//  АктуальнаяВерсияФормата - Строка - версия, ставшая актуальной после внесения изменений.
//
Процедура ЗарегистрироватьНовыеДанныеВОбмене(СтараяВерсияФормата, АктуальнаяВерсияФормата) Экспорт
	
	Если СравнитьВерсииФормата(АктуальнаяВерсияФормата, СтараяВерсияФормата) <= 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Если включена миграция и блокировка обмена с внешними ресурсами,
	// регистрация данных к обмену не выполняется.
	Если МиграцияДанныхИзВнешнихСистемСервер.БлокироватьОбменСВнешнимиРесурсами() Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоАктуальнаяВерсияИлиНиже = Ложь;
	Для Каждого Элемент Из ВерсииФорматаДоступные() Цикл
		Версия = Элемент.Значение;
		Если Версия = АктуальнаяВерсияФормата Тогда
			ЭтоАктуальнаяВерсияИлиНиже = Истина;
		КонецЕсли;
		Если Не ЭтоАктуальнаяВерсияИлиНиже Тогда
			Продолжить;
		КонецЕсли;
		Если Версия = СтараяВерсияФормата Тогда
			Прервать;
		КонецЕсли;
		ЗарегистрироватьНовыеДанныеВОбменеПоВерсии(Версия);
	КонецЦикла;
	
КонецПроцедуры

// Регистрирует к обмену данные, возможность обмена которыми была добавлена в новой версии формата.
// 
Процедура ЗарегистрироватьНовыеДанныеВОбменеПоВерсии(ВерсияФормата)
	
	Если ВерсияФормата = Метаданные.ПакетыXDTO.АДХ_1_0_12.ПространствоИмен Тогда
		
		// Все группы доступ ФЛ и все ФЛ с заполненными группами доступа.
		УзелАрхив = ПланыОбмена.ОбменНСИСАрхивом.УзелАрхив();
		ПланыОбмена.ЗарегистрироватьИзменения(УзелАрхив, Метаданные.Справочники.ГруппыДоступаФизическихЛиц);
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ФизическиеЛица.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ФизическиеЛица КАК ФизическиеЛица
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыДоступаФизическихЛиц КАК ГруппыДоступаФизическихЛиц
			|		ПО ФизическиеЛица.ГруппаДоступа = ГруппыДоступаФизическихЛиц.Ссылка");
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ПланыОбмена.ЗарегистрироватьИзменения(УзелАрхив, Выборка.Ссылка);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// см. РегламентныеЗаданияПереопределяемый.ПриОпределенииНастроекРегламентныхЗаданий()
Процедура ПриОпределенииНастроекРегламентныхЗаданий(Настройки) Экспорт
	
	Настройка = Настройки.Добавить();
	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ОбменСАрхивом;
	Настройка.ФункциональнаяОпция = Метаданные.ФункциональныеОпции.ИнтеграцияС1САрхивом;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыгрузитьДанные(ПараметрыТранспорта)
	
	ОтправитьСообщенияОВерсииФормата(ПараметрыТранспорта);
	
	Если Не ЗначениеЗаполнено(ВерсияФорматаБазыАрхива(Ложь)) Тогда
		Возврат; // Нет информации, в каком формате выгружать данные. Ожидается сообщение с форматом от 1С:Архива.
	КонецЕсли;
	
	СформироватьСообщенияНСИ();
	ОтправитьСообщенияНСИ(ПараметрыТранспорта);
	ОбменСАрхивомЛокализация.ВыгрузитьДанные(ПараметрыТранспорта);
	ОбменСАрхивомПереопределяемый.ПослеВыгрузкиДанных(ПараметрыТранспорта);
	
КонецПроцедуры

Процедура ЗагрузитьДанные(ПараметрыТранспорта)
	
	ТаблицаФайлов = ОбменСАрхивомТранспорт.ФайлыДляЗагрузки(ПараметрыТранспорта);
	Пока ТаблицаФайлов.Количество() > 0 Цикл
	
		Для Каждого СтрокаФайла Из ТаблицаФайлов Цикл
			
			ВходящийФайл = Новый Файл(СтрокаФайла.ПолноеИмя);
			ЕстьОшибки = Ложь;
			
			Попытка
				
				Событие = СобытиеДляЛога(ВходящийФайл.Имя, Истина);
				Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.НачалоОбмена;
				Событие.КраткоеОписание = НСтр("ru = 'Начало загрузки файла'");
				ЗаписатьВЛог(Событие);
                
				Если НРег(ВходящийФайл.Расширение) = ".zip" Тогда
					
					КаталогИзвлеченныхФайлов = ИзвлечьФайлыИзАрхива(
						ВходящийФайл.ПолноеИмя, СобытиеЖурналаРегистрацииЗагрузка());
					ФайлыОписанияОтчета = НайтиФайлы(КаталогИзвлеченныхФайлов, "Report.xml", Истина);
					ФайлыОписанияУничтоженияДел = НайтиФайлы(КаталогИзвлеченныхФайлов, "DossiersDestruction.xml", Истина);
					
					Если ФайлыОписанияОтчета.Количество() Тогда
						ЗагрузитьОтчет(ФайлыОписанияОтчета[0], КаталогИзвлеченныхФайлов, ВходящийФайл.Имя);
					ИначеЕсли ФайлыОписанияУничтоженияДел.Количество() Тогда
						//@skip-check query-in-loop
						ЗагрузитьУничтожениеДел(ФайлыОписанияУничтоженияДел[0], КаталогИзвлеченныхФайлов);
					КонецЕсли;
					УдалитьФайлы(КаталогИзвлеченныхФайлов);
					
				ИначеЕсли НРег(ВходящийФайл.Расширение) = ".json" Тогда
					
					СообщениеXDTO = ПрочитатьОбъектXDTOИзФайла(ВходящийФайл.ПолноеИмя);
					СвойстваXDTO = СообщениеXDTO.Свойства();
					Если СвойстваXDTO.Количество() = 1
							И СвойстваXDTO.Получить(ИмяСвойстваВерсияФормата()) <> Неопределено Тогда
						// Версия формата.
						НоваяВерсия = СообщениеXDTO.FormatVersion;
						СтараяВерсия = Константы.ВерсияФорматаОбменаПоддерживаемая1САрхивом.Получить();
						Если НоваяВерсия <> СтараяВерсия Тогда
							Константы.ВерсияФорматаОбменаПоддерживаемая1САрхивом.Установить(НоваяВерсия);
							Событие = СобытиеДляЛога(ВходящийФайл.Имя, Истина);
							Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.ОкончаниеОбмена;
							Событие.КраткоеОписание = СтрШаблон(
								НСтр("ru = 'Загружена информация о версии формата (%1)'"), НоваяВерсия);
							ЗаписатьВЛог(Событие);
							// Ответное сообщение о версии формата.
							СформироватьСообщениеОВерсииФормата();
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
				
				ЕстьОшибки = РегистрыСведений.ИсторияОбменаС1САрхивом.ЕстьОшибкиЗагрузкиФайла(ВходящийФайл.Имя);
				
			Исключение
				
				Если НРег(ВходящийФайл.Расширение) = ".zip" Тогда
					УдалитьФайлы(КаталогИзвлеченныхФайлов);
				КонецЕсли;
				
				Событие = СобытиеДляЛога(ВходящийФайл.Имя, Истина);
				Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка;
				Событие.КраткоеОписание = СтрШаблон(НСтр("ru = 'Ошибка загрузки файла: %1'"),
					ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				Событие.ДополнительныеСведения = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЗаписатьВЛог(Событие);
				
				ЕстьОшибки = Истина;
				
			КонецПопытки;
			
			ОбменСАрхивомТранспорт.ПереместитьЗагруженныйФайл(СтрокаФайла, ЕстьОшибки, ПараметрыТранспорта);
			
			Событие = СобытиеДляЛога(ВходящийФайл.Имя, Истина);
			Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.ОкончаниеОбмена;
			Событие.КраткоеОписание = НСтр("ru = 'Загрузка файла завершена'");
			ЗаписатьВЛог(Событие);
            
		КонецЦикла;
		
		ТаблицаФайлов = ОбменСАрхивомТранспорт.ФайлыДляЗагрузки(ПараметрыТранспорта);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьСообщенияНСИ()
	
	КодИсточника = СокрЛП(Константы.ИнтеграцияС1САрхивомКодБазы.Получить());
	ПространствоИменСхемы = ВерсияФорматаДляОтправкиСообщений();
	
	УзелАрхив = ПланыОбмена.ОбменНСИСАрхивом.УзелАрхив();
	ОбъектыДляВыгрузки = ПорцияНСИДляВыгрузки();
	ОбъектыСОшибками = Новый Массив;
	
	Пока ОбъектыДляВыгрузки.Количество() > 0 Цикл
		
		Для Каждого ОбъектДляВыгрузки Из ОбъектыДляВыгрузки Цикл
			
			НачатьТранзакцию();
			Попытка
				
				Если Не ОбщегоНазначения.СсылкаСуществует(ОбъектДляВыгрузки) Тогда
					ПланыОбмена.УдалитьРегистрациюИзменений(УзелАрхив, ОбъектДляВыгрузки);
					ЗафиксироватьТранзакцию();
					Продолжить;
				КонецЕсли;
				
				ОбъектНСИXDTO = ОбменСАрхивомПереопределяемый.ЗаполнитьXDTOОбъектНСИ(ОбъектДляВыгрузки);
				Если ОбъектНСИXDTO = Неопределено Тогда
					ПланыОбмена.УдалитьРегистрациюИзменений(УзелАрхив, ОбъектДляВыгрузки);
					ЗафиксироватьТранзакцию();
					Продолжить;
				КонецЕсли;
				
				СообщениеXDTO = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИменСхемы, "Message"));
				СообщениеXDTO.ID = Строка(Новый УникальныйИдентификатор);
				СообщениеXDTO.Date = ТекущаяДатаСеанса();
				СообщениеXDTO.SourceID = КодИсточника;
				СообщениеXDTO.Object.Добавить(ОбъектНСИXDTO);
				
				ЗаписьJSON = Новый ЗаписьJSON;
				ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON(, Символы.Таб));
				ФабрикаXDTO.ЗаписатьJSON(ЗаписьJSON, СообщениеXDTO, НазначениеТипаXML.Явное);
				СтрокаJSON = ЗаписьJSON.Закрыть();
				
				РегистрыСведений.СообщенияДляАрхива.Добавить(СообщениеXDTO.ID, СтрокаJSON);
				ПланыОбмена.УдалитьРегистрациюИзменений(УзелАрхив, ОбъектДляВыгрузки);
				ЗафиксироватьТранзакцию();
				
			Исключение
				ОтменитьТранзакцию();
				ОбъектыСОшибками.Добавить(ОбъектДляВыгрузки);
				
				Событие = СобытиеДляЛога();
				Событие.ОсновнойОбъект = ОбъектДляВыгрузки;
				Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка;
				Событие.КраткоеОписание = СтрШаблон(НСтр("ru = 'Не удалось выгрузить элемент НСИ: %1'"),
					ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				Событие.ДополнительныеСведения = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЗаписатьВЛог(Событие);
			КонецПопытки;
			
		КонецЦикла;
		
		//@skip-check query-in-loop
		ОбъектыДляВыгрузки = ПорцияНСИДляВыгрузки(ОбъектыСОшибками);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОтправитьСообщенияОВерсииФормата(ПараметрыТранспорта)
	
	ОтправитьСообщения(ПараметрыТранспорта, "ВерсияФормата");
	
КонецПроцедуры

Процедура ОтправитьСообщенияНСИ(ПараметрыТранспорта)
	
	ОтправитьСообщения(ПараметрыТранспорта);
	
КонецПроцедуры

Процедура ОтправитьСообщения(ПараметрыТранспорта, ТипСообщений = Неопределено)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 5000
		|	СообщенияДляАрхива.ИдентификаторСообщения КАК ИдентификаторСообщения,
		|	СообщенияДляАрхива.ТелоСообщения КАК ТелоСообщения,
		|	СообщенияДляАрхива.ДатаСоздания КАК ДатаСоздания
		|ИЗ
		|	РегистрСведений.СообщенияДляАрхива КАК СообщенияДляАрхива
		|ГДЕ
		|	СообщенияДляАрхива.ДатаОтправки = ДАТАВРЕМЯ(1, 1, 1)
		|	И СообщенияДляАрхива.КоличествоПопытокОбработки <= &ЛимитКолПопыток
		|	И ИСТИНА
		|УПОРЯДОЧИТЬ ПО
		|	ДатаСоздания");
	Запрос.УстановитьПараметр("ЛимитКолПопыток", 3);
	
	Если ТипСообщений = "ВерсияФормата" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"И ИСТИНА", "И СообщенияДляАрхива.ИдентификаторСообщения = &ИмяСвойстваВерсияФормата");
		Запрос.УстановитьПараметр("ИмяСвойстваВерсияФормата", ИмяСвойстваВерсияФормата());
	ИначеЕсли ТипСообщений <> Неопределено Тогда
		ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Неизвестный тип сообщений: %1'"), ТипСообщений);
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	ОтметкаВремениПредыдущая = "";
	ДополнениеКОтметке = 0;
	
	ВременнаяПапка = ПолучитьИмяВременногоФайла("") + ПолучитьРазделительПути();
	СоздатьКаталог(ВременнаяПапка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Попытка
			
			ОтметкаВремениТекущая = ТекущаяОтметкаВремени();
			ИмяФайлаБезРасширения = ОтметкаВремениТекущая;
			Если ОтметкаВремениТекущая = ОтметкаВремениПредыдущая Тогда
				ДополнениеКОтметке = ДополнениеКОтметке + 1;
				ИмяФайлаБезРасширения = ОтметкаВремениТекущая + "_" + ДополнениеКОтметке;
			Иначе
				ОтметкаВремениПредыдущая = ОтметкаВремениТекущая;
				ДополнениеКОтметке = 0;
			КонецЕсли;
			ИмяФайла = ИмяФайлаБезРасширения + ".json";
			ИмяФайлаПолное = ВременнаяПапка + ИмяФайла;
			
			ТекстСообщения = Выборка.ТелоСообщения.Получить();
			ЗаписьТекста = Новый ЗаписьТекста(ИмяФайлаПолное);
			ЗаписьТекста.Записать(ТекстСообщения);
			ЗаписьТекста.Закрыть();
			
			ОбменСАрхивомТранспорт.ОтправитьФайл(ИмяФайлаПолное, ПараметрыТранспорта);
			
			РегистрыСведений.СообщенияДляАрхива.ОтметитьОтправку(Выборка.ИдентификаторСообщения);
			
			Если Выборка.ИдентификаторСообщения = ИмяСвойстваВерсияФормата() Тогда
				Событие = СобытиеДляЛога(ИмяФайла);
				Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Информация;
				Событие.КраткоеОписание = НСтр("ru = 'Отправлено сообщение о версии формата'");
				ЗаписатьВЛог(Событие);
			КонецЕсли;
			
		Исключение
			КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			РегистрыСведений.СообщенияДляАрхива.ЗафиксироватьНеудачнуюПопытку(
				Выборка.ИдентификаторСообщения, КраткоеПредставлениеОшибки);
			
			Событие = СобытиеДляЛога();
			Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка;
			Событие.КраткоеОписание = СтрШаблон(НСтр("ru = 'Ошибка отправки сообщения %1 по причине: %2'"),
				Выборка.ИдентификаторСообщения, КраткоеПредставлениеОшибки);
			Событие.ДополнительныеСведения = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписатьВЛог(Событие);
		КонецПопытки;
	КонецЦикла;
	
	УдалитьВременныеФайлы(ВременнаяПапка);
	
КонецПроцедуры

Функция ПорцияНСИДляВыгрузки(ОбъектыСОшибками = Неопределено)
	
	МетаданныеНСИ = Новый Массив;
	ИсключаемыеМетаданные = ИсключаемыеМетаданныеВыгрузкиНСИ();
	Для Каждого ЭлементСостава Из Метаданные.ПланыОбмена.ОбменНСИСАрхивом.Состав Цикл
		Если ИсключаемыеМетаданные.Найти(ЭлементСостава.Метаданные) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		МетаданныеНСИ.Добавить(ЭлементСостава.Метаданные);
	КонецЦикла;
	
	Возврат ПорцияИзмененныхДанныхДляВыгрузки(МетаданныеНСИ, ОбъектыСОшибками);
	
КонецФункции

// Возвращает структуру для записи события в историю обмена и в ЖР.
//
// Параметры:
//  ИмяФайла - Строка - имя файла (локальное, не полное) для записи в историю.
//  ЭтоЗагрузкаДанных - Булево - для определения направления обмена. 
//
// Возвращаемое значение:
//  Структура - см. РегистрыСведений.ИсторияОбменаС1САрхивом.Событие:
//    *СобытиеЖР - Строка - доп. параметр, определяющий событие для записи в ЖР. 
// 
Функция СобытиеДляЛога(ИмяФайла = "", ЭтоЗагрузкаДанных = Ложь) Экспорт
	
	Событие = РегистрыСведений.ИсторияОбменаС1САрхивом.Событие(ИмяФайла, ЭтоЗагрузкаДанных);
	Событие.Вставить("СобытиеЖР", "");
	Событие.СобытиеЖР = ?(ЭтоЗагрузкаДанных,
		СобытиеЖурналаРегистрацииЗагрузка(), СобытиеЖурналаРегистрацииВыгрузка());
	
	Возврат Событие;
	
КонецФункции

// Записывает событие в ЖР и в регистр истории.
//
// Параметры:
//  Событие - см. СобытиеДляЛога.
// 
Процедура ЗаписатьВЛог(Событие) Экспорт
	
	УровеньЖР = УровеньЖурналаРегистрации.Информация;
	Если Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка Тогда
		УровеньЖР = УровеньЖурналаРегистрации.Ошибка;
	ИначеЕсли Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Предупреждение Тогда
		УровеньЖР = УровеньЖурналаРегистрации.Предупреждение;
	КонецЕсли;
	ЗаписьЖурналаРегистрации(Событие.СобытиеЖР, УровеньЖР,, Событие.Объект, Событие.КраткоеОписание);
	
	РегистрыСведений.ИсторияОбменаС1САрхивом.Добавить(Событие);
	
КонецПроцедуры

Процедура ЗагрузитьОтчет(ФайлОписанияОтчета, КаталогИзвлеченныхФайлов, ИмяВходящегоФайлаZip)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОтчетXDTO = ПрочитатьОбъектXDTOИзФайла(ФайлОписанияОтчета.ПолноеИмя);
	
	ИмяФайлаОписанияОбъекта = ОтчетXDTO.ObjectFileName;
	НайденныеФайлы = НайтиФайлы(КаталогИзвлеченныхФайлов, ИмяФайлаОписанияОбъекта, Ложь);
	Если НайденныеФайлы.Количество() = 0 Тогда
		ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Не найден файл описания выгруженного объекта %1'"),
			ИмяФайлаОписанияОбъекта);
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	ФайлОписанияОбъекта = НайденныеФайлы[0];
	ОбъектОтчетаXDTO = ПрочитатьОбъектXDTOИзФайла(ФайлОписанияОбъекта.ПолноеИмя);
	Если ОбъектОтчетаXDTO.Тип().Имя = "HandOverList" Тогда
		ЗагрузитьОтчетПоСдаточнойОписи(ОтчетXDTO, ОбъектОтчетаXDTO,
			КаталогИзвлеченныхФайлов, ИмяВходящегоФайлаZip);
	Иначе
		ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Неизвестный тип объекта отчета: ""%1""'"), ОбъектОтчетаXDTO.Тип());
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузитьОтчетПоСдаточнойОписи(ОтчетXDTO, ОбъектОтчетаXDTO,
			КаталогИзвлеченныхФайлов, ИмяВходящегоФайлаZip)
	
	СдаточнаяОписьСсылка = Документы.ПередачаДелВАрхив.ПолучитьСсылку(
		Новый УникальныйИдентификатор(ОбъектОтчетаXDTO.ID));
	СдаточнаяОписьОбъект = СдаточнаяОписьСсылка.ПолучитьОбъект();
	Если СдаточнаяОписьОбъект = Неопределено Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не найдена сдаточная опись ""%1"".'"), ОбъектОтчетаXDTO.ID);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	СтатусыОбмена = Перечисления.СтатусыОбменаСАрхивом;
	СвойстваОтчетаXDTO = ОтчетXDTO.Свойства();
	СвойстваОбъектаОтчетаXDTO = ОбъектОтчетаXDTO.Свойства();

	Если СвойстваОбъектаОтчетаXDTO.Получить("DeletionMark") <> Неопределено
			И ОбъектОтчетаXDTO.DeletionMark = Истина Тогда
		// Это отчет о приемке отзыва описи.
		Возврат;
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Документ.ПередачаДелВАрхив");
	ЭлементБлокировки.УстановитьЗначение("Ссылка", СдаточнаяОписьСсылка);

	НачатьТранзакцию();
	Попытка
		
		Блокировка.Заблокировать();
		СдаточнаяОписьОбъект.Заблокировать();
		
		Если ОтчетXDTO.Operation = "Upload" Тогда
			СдаточнаяОписьОбъект.СтатусВыгрузкиВАрхив = 
				?(ОтчетXDTO.Success = Истина, СтатусыОбмена.ЗагрузкаВАрхивВыполнена,
					СтатусыОбмена.ОбнаруженыОшибкиПриЗагрузкеВАрхив);
		ИначеЕсли ОтчетXDTO.Operation = "Verification" Тогда
			Если СдаточнаяОписьОбъект.СтатусВыгрузкиВАрхив <> СтатусыОбмена.ЗагрузкаВАрхивВыполнена Тогда
				// Опись редактируется для повторной отправки.
				ОтменитьТранзакцию();
				Событие = СобытиеДляЛога(ИмяВходящегоФайлаZip, Истина);
				Событие.ОсновнойОбъект = СдаточнаяОписьСсылка;
				Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Информация;
				Событие.КраткоеОписание = НСтр(
					"ru = 'Загрузка отчета о проверке отменена, т.к. опись редактируется для повторной отправки'");
				ЗаписатьВЛог(Событие);
				Возврат;
			КонецЕсли;
			СдаточнаяОписьОбъект.СтатусВыгрузкиВАрхив = 
				?(ОтчетXDTO.Success = Истина, СтатусыОбмена.ПринятВАрхив,
					СтатусыОбмена.ОбнаруженыОшибкиПриПроверкеВАрхиве);
		Иначе
			ОтменитьТранзакцию();
			ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Неожиданное значение в поле Operation: %1'"),
				ОтчетXDTO.Operation);
			Событие = СобытиеДляЛога(ИмяВходящегоФайлаZip, Истина);
			Событие.ОсновнойОбъект = СдаточнаяОписьСсылка;
			Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка;
			Событие.КраткоеОписание = ОписаниеОшибки;
			ЗаписатьВЛог(Событие);
			Возврат;
		КонецЕсли;
		
		ОшибкиСверкиДелМассив = Новый Массив;
		ДелаИзОтчета = Новый Массив;
		ИменаРеестровДокументов = Новый Соответствие;
		ДелаИзОписи = СдаточнаяОписьОбъект.ДелаХраненияДокументов.ВыгрузитьКолонку("ДелоХраненияДокументов");
		Для Каждого СтрокаДелаXDTO Из ОбъектОтчетаXDTO.StorageUnits Цикл
			Дело = Справочники.ДелаХраненияДокументов.ПолучитьСсылку(
				Новый УникальныйИдентификатор(СтрокаДелаXDTO.DossierID));
			ДелаИзОтчета.Добавить(Дело);
			ИменаРеестровДокументов[Дело] = СтрокаДелаXDTO.DocumentsList;
		КонецЦикла;
		ЛишниеДела = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ДелаИзОписи, ДелаИзОтчета);
		Для Каждого ЛишнееДело Из ЛишниеДела Цикл
			ОшибкиСверкиДелМассив.Добавить(СтрШаблон(
				НСтр("ru = 'В документе указано дело %1, отсутствующее в отчете. Ссылка: %2.'"), 
				ЛишнееДело, ПолучитьНавигационнуюСсылку(ЛишнееДело)));
		КонецЦикла;
		НедостающиеДела = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ДелаИзОтчета, ДелаИзОписи);
		Для Каждого НедостающееДело Из НедостающиеДела Цикл
			Если ОбщегоНазначения.СсылкаСуществует(НедостающееДело) Тогда
				ОшибкиСверкиДелМассив.Добавить(СтрШаблон(
					НСтр("ru = 'В документе отсутствует дело ""%1"", указанное в отчете. Ссылка: %2.'"),
					НедостающееДело, ПолучитьНавигационнуюСсылку(НедостающееДело)));
			Иначе
				ОшибкиСверкиДелМассив.Добавить(СтрШаблон(
					НСтр("ru = 'В документе отсутствует дело с ID ""%1"", указанное в отчете'"),
					НедостающееДело.УникальныйИдентификатор()));
			КонецЕсли;
		КонецЦикла;
		Если ОшибкиСверкиДелМассив.Количество() > 0 Тогда
			ОтменитьТранзакцию();
			ОшибкиСверкиДел = СтрСоединить(ОшибкиСверкиДелМассив, Символы.ПС + Символы.ПС);
			Событие = СобытиеДляЛога(ИмяВходящегоФайлаZip, Истина);
			Событие.ОсновнойОбъект = СдаточнаяОписьСсылка;
			Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка;
			Событие.КраткоеОписание = ОшибкиСверкиДел;
			ЗаписатьВЛог(Событие);
			Возврат;
		КонецЕсли;
				
		// Проверка реестров документов.
		Если СдаточнаяОписьОбъект.СтатусВыгрузкиВАрхив = СтатусыОбмена.ПринятВАрхив Тогда
			ОшибкиПроверкиСостава = ПроверитьСоставДелПоФайлуРеестра(
				ИменаРеестровДокументов, КаталогИзвлеченныхФайлов);
			Если ЗначениеЗаполнено(ОшибкиПроверкиСостава) Тогда
				ОтменитьТранзакцию();
				Событие = СобытиеДляЛога(ИмяВходящегоФайлаZip, Истина);
				Событие.ОсновнойОбъект = СдаточнаяОписьСсылка;
				Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка;
				Событие.КраткоеОписание = ОшибкиПроверкиСостава;
				ЗаписатьВЛог(Событие);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		ТекстовыйКомментарий = "";
		Если СвойстваОтчетаXDTO.Получить("ResultText") <> Неопределено Тогда
			ТекстовыйКомментарий = ОтчетXDTO.ResultText;
		ИначеЕсли СвойстваОтчетаXDTO.Получить("Comment") <> Неопределено Тогда
			ТекстовыйКомментарий = ОтчетXDTO.Comment;
		КонецЕсли;
		СдаточнаяОписьОбъект.РезультатПроверки = ТекстовыйКомментарий;
		
		Если ОтчетXDTO.Operation = "Upload" И СвойстваОтчетаXDTO.Получить("Errors") <> Неопределено Тогда
			МетаданныеОбъектовПоТипамXDTO = МетаданныеОбъектовПоТипамXDTO();
			Событие = СобытиеДляЛога(ИмяВходящегоФайлаZip, Истина);
			Событие.ОсновнойОбъект = СдаточнаяОписьСсылка;
			Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка;
			Для Каждого СтрокаОшибкиХДТО Из ОтчетXDTO.Errors Цикл
				Событие.Объект = Неопределено;
				Событие.Дата = СтрокаОшибкиХДТО.Date;
				Событие.КраткоеОписание = СтрокаОшибкиХДТО.Text;
				Событие.ДополнительныеСведения = СтрокаОшибкиХДТО.AdditionalInformation;
				Если Не ЗначениеЗаполнено(СтрокаОшибкиХДТО.ErrorObjectType)
						Или Не ЗначениеЗаполнено(СтрокаОшибкиХДТО.ErrorObjectID) Тогда
					Продолжить;
				КонецЕсли;
				МетеданныеОбъекта = МетаданныеОбъектовПоТипамXDTO[СтрокаОшибкиХДТО.ErrorObjectType];
				Если МетеданныеОбъекта <> Неопределено Тогда
					МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетеданныеОбъекта.ПолноеИмя());
					ОбъектСсылка = МенеджерОбъекта.ПолучитьСсылку(
						Новый УникальныйИдентификатор(СтрокаОшибкиХДТО.ErrorObjectID));
					Если ОбщегоНазначения.СсылкаСуществует(ОбъектСсылка) Тогда
						Событие.Объект = ОбъектСсылка;
					КонецЕсли;
				КонецЕсли;
				ЗаписатьВЛог(Событие);
			КонецЦикла;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОтчетXDTO.AccepterID) Тогда
			ПринялВАрхиве = Справочники.Сотрудники.ПолучитьСсылку(
				Новый УникальныйИдентификатор(ОтчетXDTO.AccepterID));
			Если ЗначениеЗаполнено(ПринялВАрхиве) Тогда
				СдаточнаяОписьОбъект.Принял = ПринялВАрхиве;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СдаточнаяОписьОбъект.Принял)
			И ЗначениеЗаполнено(ОтчетXDTO.AccepterName) Тогда
			СдаточнаяОписьОбъект.Принял = ОтчетXDTO.AccepterName;
		КонецЕсли;
		
		Если СвойстваОтчетаXDTO.Получить("AcceptanceDate") <> Неопределено
			И ЗначениеЗаполнено(ОтчетXDTO.AcceptanceDate) Тогда
			СдаточнаяОписьОбъект.ДатаПриемки = ОтчетXDTO.AcceptanceDate;
		КонецЕсли;
		
		СдаточнаяОписьОбъект.ДополнительныеСвойства.Вставить("ИнтеграцияДокументаСАрхивом", Истина);
		Если СдаточнаяОписьОбъект.СтатусВыгрузкиВАрхив = СтатусыОбмена.ПринятВАрхив Тогда
			СдаточнаяОписьОбъект.Записать(РежимЗаписиДокумента.Проведение);
			БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(СдаточнаяОписьОбъект.Ссылка,
				Справочники.ВидыБизнесСобытий.ПринятиеДокументовВАрхив);
		Иначе
			СдаточнаяОписьОбъект.Записать(РежимЗаписиДокумента.Запись);
			Если СдаточнаяОписьОбъект.СтатусВыгрузкиВАрхив = СтатусыОбмена.ОбнаруженыОшибкиПриЗагрузкеВАрхив
				Или СдаточнаяОписьОбъект.СтатусВыгрузкиВАрхив = СтатусыОбмена.ОбнаруженыОшибкиПриПроверкеВАрхиве Тогда
				БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(СдаточнаяОписьОбъект.Ссылка,
					Справочники.ВидыБизнесСобытий.ОшибкаВДокументахПереданныхВАрхив);
			КонецЕсли;
		КонецЕсли;
		
		Событие = СобытиеДляЛога(ИмяВходящегоФайлаZip, Истина);
		Событие.ОсновнойОбъект = СдаточнаяОписьСсылка;
		Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Информация;
		Событие.КраткоеОписание = СтрШаблон(НСтр("ru = 'Загружен отчет. Установлен статус ""%1"".'"),
			СдаточнаяОписьОбъект.СтатусВыгрузкиВАрхив);
		ЗаписатьВЛог(Событие);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		Событие = СобытиеДляЛога(ИмяВходящегоФайлаZip, Истина);
		Событие.ОсновнойОбъект = СдаточнаяОписьСсылка;
		Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка;
		Событие.КраткоеОписание = СтрШаблон(НСтр("ru = 'Не удалось загрузить отчет: %1'"),
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Событие.ДополнительныеСведения = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписатьВЛог(Событие);
		Возврат;
	КонецПопытки;
	
КонецПроцедуры

Процедура ЗагрузитьУничтожениеДел(ФайлОписанияУничтоженияДел, КаталогИзвлеченныхФайлов)

	УстановитьПривилегированныйРежим(Истина);
	
	ОбъектXDTOУничтожениеДел = ПрочитатьОбъектXDTOИзФайла(ФайлОписанияУничтоженияДел.ПолноеИмя);
	СвойстваXDTOУничтожениеДел = ОбъектXDTOУничтожениеДел.Свойства();
	ДокументСсылка = Документы.УничтожениеДел.ПолучитьСсылку(
		Новый УникальныйИдентификатор(ОбъектXDTOУничтожениеДел.ID));
	ДокументОбъект = ДокументСсылка.ПолучитьОбъект();

	Если ДокументОбъект = Неопределено Тогда
		ДокументОбъект = Документы.УничтожениеДел.СоздатьДокумент();
		ДокументОбъект.УстановитьСсылкуНового(ДокументСсылка);
	КонецЕсли;

	НачатьТранзакцию();

	Попытка
		
		ДокументОбъект.Заблокировать();
		
		ДокументОбъект.ЗагруженИз1САрхива = Истина;
		ДокументОбъект.Дата = ОбъектXDTOУничтожениеДел.Date;
		ДокументОбъект.Номер = ОбъектXDTOУничтожениеДел.Number;
		ДокументОбъект.Организация = Справочники.Организации.ПолучитьСсылку(
			Новый УникальныйИдентификатор(ОбъектXDTOУничтожениеДел.OrganizationID));
		Если ЗначениеЗаполнено(ОбъектXDTOУничтожениеДел.DepartmentID) Тогда
			ДокументОбъект.Подразделение = Справочники.СтруктураПредприятия.ПолучитьСсылку(
				Новый УникальныйИдентификатор(ОбъектXDTOУничтожениеДел.DepartmentID));
		КонецЕсли;
		Если СвойстваXDTOУничтожениеДел.Получить("SecurityLevelID") <> Неопределено
				И ЗначениеЗаполнено(ОбъектXDTOУничтожениеДел.SecurityLevelID) Тогда
			ДокументОбъект.ГрифДоступа = Справочники.ГрифыДоступа.ПолучитьСсылку(
				Новый УникальныйИдентификатор(ОбъектXDTOУничтожениеДел.SecurityLevelID));
		КонецЕсли;
		Если ОбъектXDTOУничтожениеДел.ElectronicDocuments = Истина Тогда
			ДокументОбъект.ФормаДокументов = Перечисления.ВариантыФормДокументов.Электронная;
		Иначе
			ДокументОбъект.ФормаДокументов = Перечисления.ВариантыФормДокументов.Бумажная;
		КонецЕсли;
		ДокументОбъект.Комментарий = ОбъектXDTOУничтожениеДел.Comment;
		ДокументОбъект.Основание = ОбъектXDTOУничтожениеДел.Basis;
		
		ДокументОбъект.ДелаХраненияДокументов.Очистить();
		
		ИменаРеестровДокументов = Новый Соответствие;
		Для Каждого СтрокаДелаXDTO Из ОбъектXDTOУничтожениеДел.StorageUnits Цикл
			Дело = Справочники.ДелаХраненияДокументов.ПолучитьСсылку(
				Новый УникальныйИдентификатор(СтрокаДелаXDTO.DossierID));
			Если Не ОбщегоНазначения.СсылкаСуществует(Дело) Тогда
				ВызватьИсключение СтрШаблон(
					НСтр("ru = 'В документе указано дело с ID %1, отсутствующее в базе'"), 
					СтрокаДелаXDTO.DossierID);
			КонецЕсли;
			ИменаРеестровДокументов[Дело] = СтрокаДелаXDTO.DocumentsList;
		КонецЦикла;
		
		ОшибкиПроверкиСостава = ПроверитьСоставДелПоФайлуРеестра(
			ИменаРеестровДокументов, КаталогИзвлеченныхФайлов);
		Если ЗначениеЗаполнено(ОшибкиПроверкиСостава) Тогда
			ВызватьИсключение ОшибкиПроверкиСостава;
		КонецЕсли;
		
		Для Каждого КлючИЗначение Из ИменаРеестровДокументов Цикл
			НовСтр = ДокументОбъект.ДелаХраненияДокументов.Добавить();
			НовСтр.ДелоХраненияДокументов = КлючИЗначение.Ключ;
		КонецЦикла;
		
		ДокументОбъект.ДополнительныеСвойства.Вставить("ИнтеграцияДокументаСАрхивом", Истина);
		ДокументОбъект.ПометкаУдаления = ОбъектXDTOУничтожениеДел.DeletionMark;
		Если ОбъектXDTOУничтожениеДел.Posted = Истина Тогда
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		ИначеЕсли ДокументОбъект.Проведен Тогда
			ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			ДокументОбъект.Записать();
		КонецЕсли;
		
		Событие = СобытиеДляЛога(, Истина);
		Событие.ОсновнойОбъект = ДокументОбъект.Ссылка;
		Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Информация;
		Событие.КраткоеОписание = НСтр("ru = 'Загружена информация об уничтожении дел'");
		ЗаписатьВЛог(Событие);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Функция ПроверитьСоставДелПоФайлуРеестра(ИменаРеестровДокументов, КаталогИзвлеченныхФайлов)
	
	ОшибкиПроверкиДокументовМассив = Новый Массив;
	
	Для Каждого КлючИЗначение Из ИменаРеестровДокументов Цикл
		
		Дело = КлючИЗначение.Ключ;
		ИмяФайлаРеестра = КлючИЗначение.Значение;
		
		НайденныеФайлы = НайтиФайлы(КаталогИзвлеченныхФайлов, ИмяФайлаРеестра, Истина);
		Если НайденныеФайлы.Количество() = 0 Тогда
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Не найден контейнер реестра документов %1'"),
				ИмяФайлаРеестра);
		КонецЕсли;
		
		ВременныйКаталогФайлаДляАвтоматическойПроверки = ИзвлечьФайлыИзАрхива(
			НайденныеФайлы[0].ПолноеИмя, СобытиеЖурналаРегистрацииЗагрузка());
		ФайлДляАвтоматическойПроверки = НайтиФайлы(ВременныйКаталогФайлаДляАвтоматическойПроверки, "machine.xml", Истина);
		Если ФайлДляАвтоматическойПроверки.Количество() = 0 Тогда
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Не найден файл реестра документов machine.xml в контейнере %1'"),
				ИмяФайлаРеестра);
		КонецЕсли;
		
		РеестрXDTO = ПрочитатьОбъектXDTOИзФайла(ФайлДляАвтоматическойПроверки[0].ПолноеИмя);
		Если РеестрXDTO.DossierID <> Строка(Дело.УникальныйИдентификатор()) Тогда
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Идентификатор дела в реестре %1 (%2) не совпадает с идентификатором в базе (%3)'"),
				ИмяФайлаРеестра, РеестрXDTO.DossierID, Строка(Дело.УникальныйИдентификатор()));
		КонецЕсли;
		
		ДокументыИзРеестра = Новый Массив;
		Для Каждого СтрокаДокумента Из РеестрXDTO.Documents Цикл
			ДокументСсылка = Справочники.ДокументыПредприятия.ПолучитьСсылку(
				Новый УникальныйИдентификатор(СтрокаДокумента.ID));
			ДокументыИзРеестра.Добавить(ДокументСсылка);
			Если Не ОбщегоНазначения.СсылкаСуществует(ДокументСсылка) Тогда
				ОшибкиПроверкиДокументовМассив.Добавить(СтрШаблон(
					НСтр("ru = 'Не найден документ с ID %1 из реестра'"), СтрокаДокумента.ID));
			КонецЕсли;
		КонецЦикла;
		
		Если ОшибкиПроверкиДокументовМассив.Количество() > 0 Тогда
			ОшибкиПроверкиДокументовМассив = СтрСоединить(ОшибкиПроверкиДокументовМассив, Символы.ПС);
				Возврат ОшибкиПроверкиДокументовМассив;
		КонецЕсли;
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	КритерийОтбораДокументыВДеле.Ссылка КАК Документ
			|ИЗ
			|	КритерийОтбора.ДокументыВДелеТоме(&Дело) КАК КритерийОтбораДокументыВДеле");
		Запрос.УстановитьПараметр("Дело", Дело);
		//@skip-check query-in-loop
		ДокументыВДеле = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Документ");
		
		ЛишниеДокументы = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ДокументыВДеле, ДокументыИзРеестра);
		Для Каждого ЛишнийДокумент Из ЛишниеДокументы Цикл
			ОшибкиПроверкиДокументовМассив.Добавить(СтрШаблон(
				НСтр("ru = 'В деле %1 указан документ %2, отсутствующий в реестре'"),
				ПолучитьНавигационнуюСсылку(Дело), ПолучитьНавигационнуюСсылку(ЛишнийДокумент)));
		КонецЦикла;
		НедостающиеДокументы = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ДокументыИзРеестра, ДокументыВДеле);
		Для Каждого НедостающийДокумент Из НедостающиеДокументы Цикл
			ОшибкиПроверкиДокументовМассив.Добавить(СтрШаблон(
				НСтр("ru = 'В деле %1 отсутствует документ %2, указанный в реестре'"),
				ПолучитьНавигационнуюСсылку(Дело),
				?(ОбщегоНазначения.СсылкаСуществует(НедостающийДокумент),
				ПолучитьНавигационнуюСсылку(НедостающийДокумент),
				НедостающийДокумент.УникальныйИдентификатор())));
		КонецЦикла;
		
		УдалитьФайлы(ВременныйКаталогФайлаДляАвтоматическойПроверки);
		
	КонецЦикла;
	
	Возврат СтрСоединить(ОшибкиПроверкиДокументовМассив, Символы.ПС);
	
КонецФункции

Функция ИзвлечьФайлыИзАрхива(ZipФайл, СобытиеЖурналаРегистрации)
	
	ИмяВременногоКаталога = ПолучитьИмяВременногоФайла("") + ПолучитьРазделительПути();
	СоздатьКаталог(ИмяВременногоКаталога);
	
	Попытка
		ЧтениеZipФайла = Новый ЧтениеZipФайла(ZipФайл);
		ПакетДокументовНеПустой = ЧтениеZipФайла.Элементы.Количество() > 0;
		Если ПакетДокументовНеПустой Тогда
			ЧтениеZipФайла.ИзвлечьВсе(ИмяВременногоКаталога);
			ЧтениеZipФайла.Закрыть();
		Иначе
			ЗаписьЖурналаРегистрации(
				СобытиеЖурналаРегистрации,
				УровеньЖурналаРегистрации.Ошибка, , ,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			УдалитьФайлы(ИмяВременногоКаталога);
		КонецЕсли;
	Исключение
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации,
			УровеньЖурналаРегистрации.Ошибка, , ,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		УдалитьФайлы(ИмяВременногоКаталога);
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат ИмяВременногоКаталога;
	
КонецФункции

// Читает файл обмена и возвращает соответствующий объект XDTO.
//
// Параметры:
//  ПолноеИмяФайла - Строка - имя файла.
//
// Возвращаемое значение:
//  ОбъектXDTO - объект.
//
Функция ПрочитатьОбъектXDTOИзФайла(ПолноеИмяФайла)
	
	Попытка
		Если СтрЗаканчиваетсяНа(ПолноеИмяФайла, ".xml") Тогда
			ЧтениеXML = Новый ЧтениеXML;
			ЧтениеXML.ОткрытьФайл(ПолноеИмяФайла);
			ЧтениеXML.ПерейтиКСодержимому();
			ПространствоИменОбъекта = ЧтениеXML.URIПространстваИмен;
			ИмяТипаОбъекта = ЧтениеXML.Имя;
			ТипОбъектаXDTO = ФабрикаXDTO.Тип(ПространствоИменОбъекта, ИмяТипаОбъекта);
			ОбъектXDTO = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML, ТипОбъектаXDTO);
			
			ОшибкиЗаполнения = ПроверитьЗаполнениеСвойствXDTO(ОбъектXDTO, Истина);
			Если ЗначениеЗаполнено(ОшибкиЗаполнения) Тогда
				ВызватьИсключение ОшибкиЗаполнения;
			КонецЕсли;
			
			ЧтениеXML.Закрыть();
		ИначеЕсли СтрЗаканчиваетсяНа(ПолноеИмяФайла, ".json") Тогда
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.ОткрытьФайл(ПолноеИмяФайла);
			ОбъектXDTO = ФабрикаXDTO.ПрочитатьJSON(ЧтениеJSON);
			ЧтениеJSON.Закрыть();
		Иначе
			ВызватьИсключение НСтр("ru = 'Неожиданный тип файла'");
		КонецЕсли;
	Исключение
		ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Ошибка при чтении файла %1:
			|%2'"), ПолноеИмяФайла, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ОписаниеОшибки;
	КонецПопытки;
	
	Возврат ОбъектXDTO;
	
КонецФункции

Функция ИмяСвойстваВерсияФормата()
	
	Возврат "FormatVersion";
	
КонецФункции

// Метаданные, исключаемые из выгрузки НСИ
// 
// Возвращаемое значение:
//  Массив из ОбъектМетаданных - Массив исключаемых из выгрузки НСИ метаданных
Функция ИсключаемыеМетаданныеВыгрузкиНСИ()
	
	Результат = Новый Массив;
	
	ОбменСАрхивомЛокализация.ДополнитьИсключаемыеМетаданныеВыгрузкиНСИ(Результат);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
