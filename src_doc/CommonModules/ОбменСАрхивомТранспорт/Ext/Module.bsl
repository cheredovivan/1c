//////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для операций получения и отправки файлов обмена.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Вычисляет параметры, которые необходимо закешировать для сеанса обмена.
//
// Возвращаемое значение:
//  Структура, Неопределено - для разных видов транспорта свойства структуры могут отличаться.
//    Если обмен с указанными параметрами невозможен, возвращается Неопределено.
//
Функция ПараметрыТранспортаДляСеансаОбмена() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыТранспорта = Новый Структура;
	
	КодДляОбменаСАрхивом = СокрЛП(Константы.ИнтеграцияС1САрхивомКодБазы.Получить());
	Если Не ЗначениеЗаполнено(КодДляОбменаСАрхивом) Тогда
		Событие = ОбменСАрхивом.СобытиеДляЛога();
		Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка;
		Событие.КраткоеОписание = СтрШаблон(НСтр("ru = 'Не указан код в константе ""%1""'"),
			Метаданные.Константы.ИнтеграцияС1САрхивомКодБазы.Синоним);
		ОбменСАрхивом.ЗаписатьВЛог(Событие);
		Возврат Неопределено;
	КонецЕсли;
	
	ВидТранспорта = Константы.ИнтеграцияС1САрхивомВидТранспорта.Получить();
	ПараметрыТранспорта.Вставить("ВидТранспорта", ВидТранспорта);
	
	Если ВидТранспорта = Перечисления.ВидыТранспортаДляОбменаС1САрхивом.ОбщийКаталог Тогда
		
		КаталогОбмена = КаталогОбмена();
		Если КаталогОбмена = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Попытка
			РазделительПути = ПолучитьРазделительПути();
			ПодпапкаФайловВАрхив = КаталогОбмена + КодДляОбменаСАрхивом + "_to_Archive" + РазделительПути;
			ПодпапкаФайловИзАрхива = КаталогОбмена + "Archive_to_" + КодДляОбменаСАрхивом + РазделительПути;
			
			СоздатьКаталог(ПодпапкаФайловВАрхив);	
			СоздатьКаталог(ПодпапкаФайловИзАрхива);
			
			ПараметрыТранспорта.Вставить("ПодпапкаФайловВАрхив", ПодпапкаФайловВАрхив);
			ПараметрыТранспорта.Вставить("ПодпапкаФайловИзАрхива", ПодпапкаФайловИзАрхива);
		Исключение
			Событие = ОбменСАрхивом.СобытиеДляЛога();
			Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка;
			Событие.КраткоеОписание = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			Событие.ДополнительныеСведения = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбменСАрхивом.ЗаписатьВЛог(Событие);
			Возврат Неопределено;
		КонецПопытки;
		
	ИначеЕсли ВидТранспорта = Перечисления.ВидыТранспортаДляОбменаС1САрхивом.FTPСервер Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ИнтеграцияС1САрхивомАдресFTP.Значение КАК АдресFTP,
			|	ИнтеграцияС1САрхивомПортFTP.Значение КАК ПортFTP,
			|	ИнтеграцияС1САрхивомПользовательFTP.Значение КАК ПользовательFTP,
			|	ИнтеграцияС1САрхивомПассивныйРежимFTP.Значение КАК ПассивныйРежимFTP
			|ИЗ
			|	Константа.ИнтеграцияС1САрхивомАдресFTP КАК ИнтеграцияС1САрхивомАдресFTP,
			|	Константа.ИнтеграцияС1САрхивомПортFTP КАК ИнтеграцияС1САрхивомПортFTP,
			|	Константа.ИнтеграцияС1САрхивомПользовательFTP КАК ИнтеграцияС1САрхивомПользовательFTP,
			|	Константа.ИнтеграцияС1САрхивомПассивныйРежимFTP КАК ИнтеграцияС1САрхивомПассивныйРежимFTP");
		
		НастройкиСоединения = Запрос.Выполнить().Выбрать();
		НастройкиСоединения.Следующий();
		
		Если Не ЗначениеЗаполнено(НастройкиСоединения.АдресFTP) Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Попытка
			ПараметрыПодключения = НовыеПараметрыПодключенияFTP();
			ПараметрыПодключения.КаталогFTP = НастройкиСоединения.АдресFTP;
			ПараметрыПодключения.Логин = НастройкиСоединения.ПользовательFTP;
			ПараметрыПодключения.Порт = НастройкиСоединения.ПортFTP;
			ПараметрыПодключения.ПассивноеСоединение = НастройкиСоединения.ПассивныйРежимFTP;
			ВладелецПароляFTPСервера = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
				Метаданные.Константы.ИнтеграцияС1САрхивомПользовательFTP);
			ПараметрыПодключения.Пароль = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
				ВладелецПароляFTPСервера);
			
			FTPСоединение = НовоеFTPСоединение(ПараметрыПодключения);
			СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(ПараметрыПодключения.КаталогFTP);
			
			РазделительПути = "/";
			КаталогОбмена = СтруктураURI.ПутьНаСервере;
			КаталогОбмена = КаталогОбмена + ?(СтрЗаканчиваетсяНа(КаталогОбмена, РазделительПути), "", РазделительПути);
			
			РазделительПути = "/";
			ПодпапкаФайловВАрхив = КаталогОбмена + КодДляОбменаСАрхивом + "_to_Archive" + РазделительПути;
			ПодпапкаФайловИзАрхива = КаталогОбмена + "Archive_to_" + КодДляОбменаСАрхивом + РазделительПути;
			
			МассивФайлов = FTPСоединение.НайтиФайлы(ПодпапкаФайловВАрхив);
			Если МассивФайлов.Количество() = 0 Тогда
				FTPСоединение.СоздатьКаталог(ПодпапкаФайловВАрхив);
			КонецЕсли;
			МассивФайлов = FTPСоединение.НайтиФайлы(ПодпапкаФайловИзАрхива);
			Если МассивФайлов.Количество() = 0 Тогда
				FTPСоединение.СоздатьКаталог(ПодпапкаФайловИзАрхива);
			КонецЕсли;
			
			ПараметрыТранспорта.Вставить("FTPСоединение", FTPСоединение);
			ПараметрыТранспорта.Вставить("ПодпапкаФайловВАрхив", ПодпапкаФайловВАрхив);
			ПараметрыТранспорта.Вставить("ПодпапкаФайловИзАрхива", ПодпапкаФайловИзАрхива);
			
		Исключение
			Событие = ОбменСАрхивом.СобытиеДляЛога();
			Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка;
			Событие.КраткоеОписание = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			Событие.ДополнительныеСведения = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбменСАрхивом.ЗаписатьВЛог(Событие);
			Возврат Неопределено;
		КонецПопытки;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Возврат ПараметрыТранспорта;
	
КонецФункции

// Возвращает таблицу файлов для загрузки из источника.
//
// Параметры:
//  ПараметрыТранспорта - Структура: см. ПараметрыТранспортаДляСеансаОбмена.
//
// Возвращаемое значение:
//  ТаблицаЗначений - см. НоваяТаблицаФайловДляЗагрузки.
//
Функция ФайлыДляЗагрузки(ПараметрыТранспорта) Экспорт

	Возврат НайтиФайлыОбмена(ПараметрыТранспорта, ПараметрыТранспорта.ПодпапкаФайловИзАрхива);
	
КонецФункции

// Описание.
//
// Параметры:
//  ПараметрыТранспорта - Структура: см. ПараметрыТранспортаДляСеансаОбмена.
//  ИмяФайла - Строка - имя файла.
//  ТипСобытия - ПеречислениеСсылка.СобытияОбмена - тип события, по которому выполняется поиск файла.
//  НаправлениеОбмена - ПеречислениеСсылка.НаправленияОбмена - направление обмена из события.
//
// Возвращаемое значение:
//  ТаблицаЗначений - см. ФайлыДляЗагрузки.
// 
Функция НайтиФайлИзИстории(ПараметрыТранспорта, ИмяФайла, ТипСобытия, НаправлениеОбмена) Экспорт
	
	ПапкаВыгрузкиЗагрузки = ?(НаправлениеОбмена = Перечисления.НаправленияОбмена.Отправка,
		ПараметрыТранспорта.ПодпапкаФайловВАрхив, ПараметрыТранспорта.ПодпапкаФайловИзАрхива);
	
	ПапкиДляПоиска = Новый Массив;
	ПапкиДляПоиска.Добавить(ПапкаВыгрузкиЗагрузки + ПостфиксОбработанныхУспешно());
	ПапкиДляПоиска.Вставить(?(ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка, 0, 1),
		ПапкаВыгрузкиЗагрузки + ПостфиксОбработанныхСОшибкой());
	ПапкиДляПоиска.Вставить(?(НаправлениеОбмена = Перечисления.НаправленияОбмена.Отправка, 0, 2),
		ПапкаВыгрузкиЗагрузки);
	
	Для Каждого ПапкаДляПоиска Из ПапкиДляПоиска Цикл
		ТаблицаФайлов = НайтиФайлыОбмена(ПараметрыТранспорта, ПапкаДляПоиска, ИмяФайла, Ложь);
		Если ТаблицаФайлов.Количество() > 0 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаФайлов;
	
КонецФункции

// Помещает во временное хранилище, возвращает адрес.
//
// Параметры:
//  ПараметрыТранспорта - Структура: см. ПараметрыТранспортаДляСеансаОбмена.
//  ПолноеИмяНаСервере - Строка - имя файла.
//  ИдентификаторФормы - Строка - идентификатор формы.
//
// Возвращаемое значение:
//  Строка - адрес.
// 
Функция ПоместитьФайлВоВременноеХранилище(ПараметрыТранспорта, ПолноеИмяНаСервере, ИдентификаторФормы) Экспорт
	
	ДвоичныеДанные = Неопределено;
	ИмяВременногоФайла = "";
	
	Если ПараметрыТранспорта.ВидТранспорта = Перечисления.ВидыТранспортаДляОбменаС1САрхивом.ОбщийКаталог Тогда
		ДвоичныеДанные = Новый ДвоичныеДанные(ПолноеИмяНаСервере);
	ИначеЕсли ПараметрыТранспорта.ВидТранспорта = Перечисления.ВидыТранспортаДляОбменаС1САрхивом.FTPСервер Тогда
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла("tmp");
		ПараметрыТранспорта.FTPСоединение.Получить(ПолноеИмяНаСервере, ИмяВременногоФайла);
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла);
		УдалитьФайлы(ИмяВременногоФайла);
	Иначе
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Неизвестный вид транспорта для обмена: ""%1""'"),
			ПараметрыТранспорта.ВидТранспорта);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Результат = ПоместитьВоВременноеХранилище(ДвоичныеДанные, ИдентификаторФормы);
	
	Возврат Результат;
	
КонецФункции

// Возвращает таблицу файлов для загрузки из источника.
//
// Параметры:
//  ПараметрыТранспорта - Структура - см. ПараметрыТранспортаДляСеансаОбмена().
//  ПодпапкаДляПоиска - Строка - имя подпапки.
//  Маска - Строка - маска.
//  СкачиватьНайденные - Булево - если Истина, найденные на FTP файлы будут сразу скачаны на сервер.
//
// Возвращаемое значение:
//  ТаблицаЗначений - см. НоваяТаблицаФайловДляЗагрузки.
//
Функция НайтиФайлыОбмена(ПараметрыТранспорта, ПодпапкаДляПоиска, Знач Маска = "", СкачиватьНайденные = Истина) Экспорт
	
	ТаблицаФайлов = НоваяТаблицаФайловДляЗагрузки();
	
	Если Маска = "" Тогда
		Маска = ПолучитьМаскуВсеФайлы();
	КонецЕсли;
	
	Если ПараметрыТранспорта.ВидТранспорта = Перечисления.ВидыТранспортаДляОбменаС1САрхивом.ОбщийКаталог Тогда
		
		НайденныеФайлы = НайтиФайлы(ПодпапкаДляПоиска, Маска);
		Для Каждого ВходящийФайл Из НайденныеФайлы Цикл
			Если ВходящийФайл.ЭтоКаталог() Тогда
				Продолжить;
			КонецЕсли;
			Стр = ТаблицаФайлов.Добавить();
			Стр.Имя = ВходящийФайл.Имя;
			Стр.ПолноеИмя = ВходящийФайл.ПолноеИмя;
			Стр.ПолноеИмяНаСервере = ВходящийФайл.ПолноеИмя;
		КонецЦикла;
		ТаблицаФайлов.Сортировать("Имя");
		
	ИначеЕсли ПараметрыТранспорта.ВидТранспорта = Перечисления.ВидыТранспортаДляОбменаС1САрхивом.FTPСервер Тогда
		
		НайденныеПапки = ПараметрыТранспорта.FTPСоединение.НайтиФайлы(ПодпапкаДляПоиска);
		Если НайденныеПапки.Количество() = 0 Тогда
			Возврат ТаблицаФайлов;
		КонецЕсли;
		
		НайденныеФайлы = ПараметрыТранспорта.FTPСоединение.НайтиФайлы(ПодпапкаДляПоиска, Маска);
		Для Каждого ВходящийФайл Из НайденныеФайлы Цикл
			Если ВходящийФайл.ЭтоКаталог() Тогда
				Продолжить;
			КонецЕсли;
			Стр = ТаблицаФайлов.Добавить();
			Стр.Имя = ВходящийФайл.Имя;
			Стр.ПолноеИмя = "";
			Стр.ПолноеИмяНаСервере = ВходящийФайл.ПолноеИмя;
		КонецЦикла;
		ТаблицаФайлов.Сортировать("Имя");
		
		ЛимитКоличестваФайлов = 100;
		КоличествоФайлов = ТаблицаФайлов.Количество();
		Для Сч = 1 По КоличествоФайлов - ЛимитКоличестваФайлов Цикл
			ТаблицаФайлов.Удалить(ТаблицаФайлов[ЛимитКоличестваФайлов]);
		КонецЦикла;
		
		Если СкачиватьНайденные Тогда
			РазделительПути = ПолучитьРазделительПути();
			ВременнаяПапка = ПолучитьИмяВременногоФайла("");
			ВременнаяПапка = ВременнаяПапка	+ ?(Прав(ВременнаяПапка, 1) = РазделительПути, "", РазделительПути);
			СоздатьКаталог(ВременнаяПапка);
			Для Каждого СтрокаФайла Из ТаблицаФайлов Цикл
				ИмяСкачанногоФайла = ВременнаяПапка + СтрокаФайла.Имя;
				ПараметрыТранспорта.FTPСоединение.Получить(СтрокаФайла.ПолноеИмяНаСервере, ИмяСкачанногоФайла);
				СтрокаФайла.ПолноеИмя = ИмяСкачанногоФайла;
			КонецЦикла;
		КонецЕсли;
		
	Иначе
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Неизвестный вид транспорта для обмена: ""%1""'"),
			ПараметрыТранспорта.ВидТранспорта);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Возврат ТаблицаФайлов;
	
КонецФункции

// Возвращает пустую таблицу файлов для загрузки.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - пустая таблица:
//   * Файл - Файл - полное имя файла на диске. Файл на диске.
//      При обмене через общий каталог - файл в сетевом каталоге.
//      При обмене через FTP - файл во временной папке.
//   * Имя - Строка - имя файла.
//   * ПолноеИмяНаСервере - Строка - полное имя файла на сервере
// 
Функция НоваяТаблицаФайловДляЗагрузки() Экспорт
	
	ТаблицаФайлов = Новый ТаблицаЗначений;
	ТаблицаФайлов.Колонки.Добавить("Имя");
	ТаблицаФайлов.Колонки.Добавить("ПолноеИмя");
	ТаблицаФайлов.Колонки.Добавить("ПолноеИмяНаСервере");
	
	Возврат ТаблицаФайлов;
	
КонецФункции

// Перемещает файл в каталог обработанных файлов, либо в каталог файлов с ошибками.
//
// Параметры:
//  СтрокаФайла - СтрокаТаблицыЗначений - см. ФайлыДляЗагрузки().
//  ЕстьОшибки - Булево - Истина, если при загрузке файла возникла ошибка и файл не загружен.
//  ПараметрыТранспорта - Структура - см. ПараметрыТранспортаДляСеансаОбмена().
//
Процедура ПереместитьЗагруженныйФайл(СтрокаФайла, ЕстьОшибки, ПараметрыТранспорта) Экспорт
	
	Если ПараметрыТранспорта.ВидТранспорта = Перечисления.ВидыТранспортаДляОбменаС1САрхивом.ОбщийКаталог Тогда
		
		РазделительПути = ПолучитьРазделительПути();
		ПодпапкаФайловИзАрхива = ПараметрыТранспорта.ПодпапкаФайловИзАрхива;
		ПодпапкаОбработанныхУспешно = ПодпапкаФайловИзАрхива + "Executed" + РазделительПути;
		ПодпапкаОбработанныхСОшибкой = ПодпапкаФайловИзАрхива + "Errors" + РазделительПути;
		СоздатьКаталог(ПодпапкаОбработанныхУспешно);	
		Если ЕстьОшибки Тогда
			СоздатьКаталог(ПодпапкаОбработанныхСОшибкой);
		КонецЕсли;
		ПереместитьФайл(СтрокаФайла.ПолноеИмяНаСервере,
			?(ЕстьОшибки, ПодпапкаОбработанныхСОшибкой, ПодпапкаОбработанныхУспешно) + СтрокаФайла.Имя);
		
	ИначеЕсли ПараметрыТранспорта.ВидТранспорта = Перечисления.ВидыТранспортаДляОбменаС1САрхивом.FTPСервер Тогда
		
		РазделительПути = "/";
		ПодпапкаФайловИзАрхива = ПараметрыТранспорта.ПодпапкаФайловИзАрхива;
		ПодпапкаОбработанныхУспешно = ПодпапкаФайловИзАрхива + "Executed" + РазделительПути;
		ПодпапкаОбработанныхСОшибкой = ПодпапкаФайловИзАрхива + "Errors" + РазделительПути;
		
		FTPСоединение = ПараметрыТранспорта.FTPСоединение;
		МассивФайлов = FTPСоединение.НайтиФайлы(ПодпапкаОбработанныхУспешно);
		Если МассивФайлов.Количество() = 0 Тогда
			FTPСоединение.СоздатьКаталог(ПодпапкаОбработанныхУспешно);
		КонецЕсли;
		МассивФайлов = FTPСоединение.НайтиФайлы(ПодпапкаОбработанныхСОшибкой);
		Если ЕстьОшибки И МассивФайлов.Количество() = 0 Тогда
			FTPСоединение.СоздатьКаталог(ПодпапкаОбработанныхСОшибкой);
		КонецЕсли;
		
		ПолноеИмяНовое = ?(ЕстьОшибки, ПодпапкаОбработанныхСОшибкой, ПодпапкаОбработанныхУспешно) + СтрокаФайла.Имя;
		МассивФайлов = FTPСоединение.НайтиФайлы(ПолноеИмяНовое);
		Если МассивФайлов.Количество() > 0 Тогда
			FTPСоединение.Удалить(ПолноеИмяНовое);
		КонецЕсли;
		
		FTPСоединение.Переместить(СтрокаФайла.ПолноеИмяНаСервере, ПолноеИмяНовое);
		УдалитьФайлы(СтрокаФайла.Файл.ПолноеИмя); // Удаляется файл из временной папки.
		
	Иначе
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Неизвестный вид транспорта для обмена: ""%1""'"),
			ПараметрыТранспорта.ВидТранспорта);
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;
	
КонецПроцедуры

// Отправляет указанный временный файл в 1С:Архив и удаляет его.
//
// Параметры:
//  ПолноеИмяФайла - Строка - полное имя файла (во временной папке), который необходимо отправить.
//  ПараметрыТранспорта - Структура - см. ПараметрыТранспортаДляСеансаОбмена().
//
Процедура ОтправитьФайл(ПолноеИмяФайла, ПараметрыТранспорта) Экспорт
	
	Файл = Новый Файл(ПолноеИмяФайла);
	
	Если ПараметрыТранспорта.ВидТранспорта = Перечисления.ВидыТранспортаДляОбменаС1САрхивом.ОбщийКаталог Тогда
		ПереместитьФайл(ПолноеИмяФайла, ПараметрыТранспорта.ПодпапкаФайловВАрхив + Файл.Имя);
	ИначеЕсли ПараметрыТранспорта.ВидТранспорта = Перечисления.ВидыТранспортаДляОбменаС1САрхивом.FTPСервер Тогда
		ПараметрыТранспорта.FTPСоединение.Записать(ПолноеИмяФайла, ПараметрыТранспорта.ПодпапкаФайловВАрхив + Файл.Имя);
		УдалитьФайлы(ПолноеИмяФайла);
	Иначе
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Неизвестный вид транспорта для обмена: ""%1""'"),
			ПараметрыТранспорта.ВидТранспорта);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает пустую структуру с параметрами подключения FTP.
//
// Возвращаемое значение:
//  Структура:
//   * КаталогFTP - Строка - полный путь к каталогу FTP.
//   * Логин - Строка - логин для подключения.
//   * Пароль - Строка - пароль для подключения.
//   * Порт - Число - номер порта. По умолчанию 21.
//   * ПассивноеСоединение - Булево - признак пассивного режима соединения. По умолчанию Истина.
//
Функция НовыеПараметрыПодключенияFTP() Экспорт
	
	ПараметрыFTP = Новый Структура;
	ПараметрыFTP.Вставить("КаталогFTP", "");
	ПараметрыFTP.Вставить("Логин", "");
	ПараметрыFTP.Вставить("Пароль", "");
	ПараметрыFTP.Вставить("Порт", 0);
	ПараметрыFTP.Вставить("ПассивноеСоединение", Истина);
	
	Возврат ПараметрыFTP;
	
КонецФункции

// Проверяет обмен через FTP для учетной записи прямого обмена.
// 
// Параметры:
// 	ПараметрыПодключенияFTP - Структура - см. НовыеПараметрыПодключенияFTP().
// 
// Возвращаемое значение:
// 	Строка - описание ошибки, если проверка прошла неудачно.
// 
Функция ПроверитьОбменЧерезFTP(ПараметрыПодключенияFTP) Экспорт
	
	ОписаниеОшибки = Неопределено;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("tmp");
	ЗаписьТекста = Новый ЗаписьТекста(ИмяВременногоФайла);
	ЗаписьТекста.ЗаписатьСтроку(ИмяВременногоФайла);
	ЗаписьТекста.Закрыть();
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(ПараметрыПодключенияFTP.КаталогFTP);
	ИмяФайлаНаСторонеПриемника = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ИмяВременногоФайла).Имя;
	
	Попытка
		FTPСоединение = НовоеFTPСоединение(ПараметрыПодключенияFTP, 7);
		FTPСоединение.УстановитьТекущийКаталог(СтруктураURI.ПутьНаСервере);
		FTPСоединение.Записать(ИмяВременногоФайла, ИмяФайлаНаСторонеПриемника);
		FTPСоединение.Удалить(ИмяФайлаНаСторонеПриемника);
	Исключение
		ОписаниеОшибки = НСтр("ru = 'При проверке подключения возникли ошибки:'")
			+ Символы.ПС + ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	ФайловаяСистема.УдалитьВременныйФайл(ИмяВременногоФайла);
	
	Возврат ОписаниеОшибки;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КаталогОбмена()
	
	УстановитьПривилегированныйРежим(Истина);
	
	КаталогОбмена = Константы.ИнтеграцияС1САрхивомКаталог.Получить();
	
	Если Не ЗначениеЗаполнено(КаталогОбмена) Тогда
		Событие = ОбменСАрхивом.СобытиеДляЛога();
		Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка;
		Событие.КраткоеОписание = СтрШаблон(НСтр("ru = 'Не указан каталог в константе ""%1""'"),
			Метаданные.Константы.ИнтеграцияС1САрхивомКаталог.Синоним);
		ОбменСАрхивом.ЗаписатьВЛог(Событие);
		Возврат Неопределено;
	КонецЕсли;
	
	ФайлКаталога = Новый Файл(КаталогОбмена);
	Если Не ФайлКаталога.Существует() Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Неверный путь к каталогу обмена: ""%1""'"), КаталогОбмена);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	КаталогОбмена = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогОбмена);
	
	Возврат КаталогОбмена;
	
КонецФункции

Функция НовоеFTPСоединение(ПараметрыПодключенияFTP, Таймаут = 30)
	
	НастройкиFTP = Новый Структура("Сервер, Порт, Логин, Пароль, Прокси,
		|ПассивноеСоединение, Таймаут, ЗащищенноеСоединение");
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(ПараметрыПодключенияFTP.КаталогFTP);
	НастройкиFTP.Сервер = СтруктураURI.ИмяСервера;
	Если ЗначениеЗаполнено(ПараметрыПодключенияFTP.Порт) Тогда
		НастройкиFTP.Порт = ПараметрыПодключенияFTP.Порт;
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыПодключенияFTP.Логин) Тогда
		НастройкиFTP.Логин = ПараметрыПодключенияFTP.Логин;
	Иначе
		НастройкиFTP.Логин = СтруктураURI.Логин;
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыПодключенияFTP.Пароль) Тогда
		НастройкиFTP.Пароль = ПараметрыПодключенияFTP.Пароль;
	Иначе
		НастройкиFTP.Пароль = СтруктураURI.Пароль;
	КонецЕсли;
	НастройкиFTP.Прокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси(СтруктураURI.Схема);
	НастройкиFTP.ПассивноеСоединение = ПараметрыПодключенияFTP.ПассивноеСоединение;
	НастройкиFTP.Таймаут = Таймаут;
	НастройкиFTP.ЗащищенноеСоединение = ?(СтруктураURI.Схема = "ftps",
		ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение(), Неопределено);
	
	Возврат Новый FTPСоединение(
		НастройкиFTP.Сервер,
		НастройкиFTP.Порт,
		НастройкиFTP.Логин,
		НастройкиFTP.Пароль,
		НастройкиFTP.Прокси,
		НастройкиFTP.ПассивноеСоединение,
		НастройкиFTP.Таймаут,
		НастройкиFTP.ЗащищенноеСоединение);
	
КонецФункции

Функция ПостфиксОбработанныхУспешно()
	
	Возврат "Executed";
	
КонецФункции

Функция ПостфиксОбработанныхСОшибкой()
	
	Возврат "Errors";
	
КонецФункции

#КонецОбласти
