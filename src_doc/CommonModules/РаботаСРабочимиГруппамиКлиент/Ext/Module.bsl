////////////////////////////////////////////////////////////////////////////////
// Работа с рабочими группами (клиент)
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// При попытке удаления последнего участника рабочей группы документа, для
// которого установлено правило обязательного заполнения рабочей группы
// выводится предупреждение и отменяется удаление.
//
Процедура РабочаяГруппаТаблицаПередУдалением(Форма, Отказ, ОписаниеОповещения) Экспорт
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	ВыделенныеСтроки = Форма.Элементы.РабочаяГруппаТаблица.ВыделенныеСтроки;
	КоличествоВыделенныхСтрок = ВыделенныеСтроки.Количество();
	КоличествоСтрокВсего = Форма.РабочаяГруппаТаблица.Количество();
	
	Если ДелопроизводствоКлиентСервер.ЭтоДокумент(Форма.Объект.Ссылка) Тогда
		Если КоличествоСтрокВсего = КоличествоВыделенныхСтрок Тогда
			Если РаботаСРабочимиГруппами.ОбязательноеЗаполнениеРабочихГруппДокументов(Форма.Объект.ВидДокумента) Тогда
				ПоказатьПредупреждение(,НСтр("ru = 'Для документов этого вида рабочая группа должна быть обязательно заполнена хотя бы одним участником!'"));
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ДелопроизводствоКлиентСервер.ЭтоМероприятие(Форма.Объект.Ссылка) Тогда
		Если КоличествоСтрокВсего = КоличествоВыделенныхСтрок Тогда
			Если РаботаСРабочимиГруппами.ОбязательноеЗаполнениеРабочихГруппДокументов(Форма.Объект.ВидМероприятия) Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Для мероприятий этого вида рабочая группа должна быть обязательно заполнена хотя бы одним участником!'"));
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ДелопроизводствоКлиентСервер.ЭтоПроект(Форма.Объект.Ссылка) Тогда
		Если КоличествоСтрокВсего = КоличествоВыделенныхСтрок Тогда
			Если РаботаСРабочимиГруппами.ОбязательноеЗаполнениеРабочихГруппДокументов(Форма.Объект.ВидПроекта) Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Для проектов этого вида рабочая группа должна быть обязательно заполнена хотя бы одним участником!'"));
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("ОписаниеОповещения", ОписаниеОповещения);
	ПараметрыОбработчика.Вставить("Форма", Форма);
	ОписаниеОповещенияВопроса = Новый ОписаниеОповещения(
		"РабочаяГруппаТаблицаПередУдалениемПродолжение",
		ЭтотОбъект,
		ПараметрыОбработчика);
		
	Если Форма.РабочаяГруппаТаблица.Количество() > ВыделенныеСтроки.Количество() Тогда
		Для Каждого Индекс Из ВыделенныеСтроки Цикл
			Если Форма.Элементы.РабочаяГруппаТаблица.ДанныеСтроки(Индекс).Участник = ТекущийПользователь Тогда
				ТекстВопроса = НСтр("ru = 'Вы пытаетесь удалить себя из рабочей группы. Вы можете потерять доступ к документу. Продолжить?'");
				ПоказатьВопрос(ОписаниеОповещенияВопроса, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0);		
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОписаниеОповещенияВопроса, КодВозвратаДиалога.Да);
	
КонецПроцедуры

// Обработчик табличной части формы шаблона документа РабочаяГруппаПриНачалеРедактирования
//
Процедура РабочаяГруппаПриНачалеРедактирования(РабочаяГруппаЭлемент, НоваяСтрока) Экспорт
	
	Если НоваяСтрока Тогда
		РабочаяГруппаЭлемент.ТекущиеДанные.Участник = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
		УстановитьРеквизитыУсловногоОформления(РабочаяГруппаЭлемент.ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик окончания редактирования таблицы РабочаяГруппаТаблица
//
Процедура РабочаяГруппаПриОкончанииРедактирования(Форма, Элемент, ОтменаРедактирования) Экспорт
	
	КоличествоУчастниковРабочейГруппы = Форма.РабочаяГруппаТаблица.Количество();
	Форма.КоличествоУчастниковРабочейГруппы = КоличествоУчастниковРабочейГруппы;
	
	Если Не ОтменаРедактирования Тогда
		Форма.Модифицированность = Истина;
		УстановитьРеквизитыУсловногоОформления(Элемент.ТекущиеДанные);
		Форма.Элементы.РабочаяГруппаТаблица.ЗакончитьРедактированиеСтроки(Ложь);
	КонецЕсли;
	
	УстановитьЗаголовокРабочейГруппы(Форма, КоличествоУчастниковРабочейГруппы);
	
КонецПроцедуры

Процедура ШаблонРабочаяГруппаПриОкончанииРедактирования(
	Форма,
	РабочаяГруппаТаблица,
	РабочаяГруппаЭлемент,
	ОтменаРедактирования) Экспорт
	
	Если Не ОтменаРедактирования Тогда
		Форма.Модифицированность = Истина;
		УстановитьРеквизитыУсловногоОформления(РабочаяГруппаЭлемент.ТекущиеДанные);
		Форма.Элементы.РабочаяГруппаДокумента.ЗакончитьРедактированиеСтроки(Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик автоподбора для реквизита Участник табличной части формы шаблона документа РабочаяГруппа
//
Процедура ШаблонРабочаяГруппаУчастникАвтоПодбор(
	Элемент,
	Текст,
	ДанныеВыбора,
	Ожидание,
	СтандартнаяОбработка,
	ТипОбъекта) Экспорт
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДополнениеТипа = Новый ОписаниеТипов(
			"СправочникСсылка.АвтоподстановкиДляОбъектов, СправочникСсылка.ПолныеРоли,
			|СправочникСсылка.РабочиеГруппы, СправочникСсылка.СтруктураПредприятия,
			|СправочникСсылка.Проекты");
		ДанныеВыбора = СотрудникиВызовСервера.СформироватьДанныеВыбора(
			Текст, ДополнениеТипа, ТипОбъекта);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик автоподбора для реквизита Участник табличной части формы шаблона документа РабочаяГруппа
//
Процедура ДокументРабочаяГруппаУчастникАвтоПодбор(
	Элемент,
	Текст,
	ДанныеВыбора,
	Ожидание,
	СтандартнаяОбработка) Экспорт
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДополнениеТипа = Новый ОписаниеТипов(
			"СправочникСсылка.ПолныеРоли, СправочникСсылка.РабочиеГруппы,
			|СправочникСсылка.СтруктураПредприятия, СправочникСсылка.Проекты");
		ДанныеВыбора = СотрудникиВызовСервера.СформироватьДанныеВыбора(
			Текст, ДополнениеТипа);
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает в строке реквизиты, необходимые для условного оформления
//
Процедура УстановитьРеквизитыУсловногоОформления(ТекущиеДанные) Экспорт
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ЭтоРоль = Ложь;
	Если ЗначениеЗаполнено(ТекущиеДанные.Участник) Тогда
		Если ТипЗнч(ТекущиеДанные.Участник) = Тип("СправочникСсылка.РолиИсполнителей") Тогда
			ТекущиеДанные.Иконка = 1;
			ТекущиеДанные.ЭтоРоль = Истина;
		ИначеЕсли ТипЗнч(ТекущиеДанные.Участник) = Тип("СправочникСсылка.РабочиеГруппы") Тогда
			ТекущиеДанные.Иконка = 2;
		ИначеЕсли ТипЗнч(ТекущиеДанные.Участник) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
			ТекущиеДанные.Иконка = 2;
		ИначеЕсли ТипЗнч(ТекущиеДанные.Участник) = Тип("СправочникСсылка.ПодразделенияКонтейнеры") Тогда
			ТекущиеДанные.Иконка = 2;
		ИначеЕсли ТипЗнч(ТекущиеДанные.Участник) = Тип("СправочникСсылка.ПроектыКонтейнеры") Тогда
			ТекущиеДанные.Иконка = 2;
		ИначеЕсли ТипЗнч(ТекущиеДанные.Участник) = Тип("СправочникСсылка.Пользователи")
			Или ТипЗнч(ТекущиеДанные.Участник) = Тип("СправочникСсылка.Сотрудники") Тогда
			ТекущиеДанные.Иконка = 3;
		ИначеЕсли ТипЗнч(ТекущиеДанные.Участник) = Тип("Строка") Тогда
			ТекущиеДанные.Иконка = 4;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Очищает рабочую группу в форме.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения.
//
Процедура ОчиститьРабочуюГруппу(Форма) Экспорт
	
	Форма.РабочаяГруппаТаблица.Очистить();
	Форма.КоличествоУчастниковРабочейГруппы = 0;
	
	УстановитьЗаголовокРабочейГруппы(Форма, Форма.КоличествоУчастниковРабочейГруппы);
	
КонецПроцедуры

// Текст соответствующего вопроса в формах про запрещенных участников.
// 
// Возвращаемое значение:
//  Строка
Функция ТекстВопроса_УвереныУдалитьЗапрещенныхУчастников() Экспорт
	
	Возврат НСтр("ru = 'Удалить запрещенных участников из рабочих групп всех объектов?'");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьЗаголовокРабочейГруппы(Форма, КоличествоУчастниковРабочейГруппы)
	
	Если ДелопроизводствоКлиентСервер.ЭтоФормаВидаДокумента(Форма.ИмяФормы) Или 
		ДелопроизводствоКлиентСервер.ЭтоФормаШаблонаДокумента(Форма.ИмяФормы) Тогда 
		Если КоличествоУчастниковРабочейГруппы > 0 Тогда
			Форма.ЗаголовокРабочейГруппы = СтрШаблон(НСтр("ru = 'Доступен (%1)'"), 
				КоличествоУчастниковРабочейГруппы);
			Форма.Элементы.ГруппаРабочаяГруппа.Поведение = ПоведениеОбычнойГруппы.Обычное;
		Иначе 
			Форма.ЗаголовокРабочейГруппы = НСтр("ru = 'Доступен всем'");
			Форма.Элементы.ГруппаРабочаяГруппа.Поведение = ПоведениеОбычнойГруппы.Свертываемая;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура РабочаяГруппаТаблицаПередУдалениемПродолжение(Результат, Параметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Форма = Параметры.Форма;
	Форма.КоличествоУчастниковРабочейГруппы = Форма.РабочаяГруппаТаблица.Количество() 
		- Форма.Элементы.РабочаяГруппаТаблица.ВыделенныеСтроки.Количество();
	Форма.Модифицированность = Истина;
	УстановитьЗаголовокРабочейГруппы(Форма, Форма.КоличествоУчастниковРабочейГруппы);
	
	ВыполнитьОбработкуОповещения(Параметры.ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти