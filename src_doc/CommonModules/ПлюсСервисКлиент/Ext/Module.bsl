#Область ПрограммныйИнтерфейс

// Возвращает признак использования сервиса 1С:Плюс в приложении
//
// Возвращаемое значение:
//  Булево
Функция Используется() Экспорт
	
	Возврат ПлюсСервисКлиентПовтИсп.Используется();

КонецФункции

// Открыть форму взаимодействия с сервисом 1С:Плюс.
Процедура ОткрытьФормуВзаимодействияССервисом() Экспорт
	
	ПлюсСервисСлужебныйКлиент.ОткрытьФормуВзаимодействияССервисом();
	
КонецПроцедуры

// Обработчик плана глобального поиска.
// 
// Параметры:
//  СтрокаПоиска - Строка
//  ПланПоиска - ПланГлобальногоПоиска
Процедура ПриГлобальномПоиске(СтрокаПоиска, ПланПоиска) Экспорт
	
	Если СтрНайти(СтрокаПоиска, ПлюсСервисКлиентСервер.АдресСервиса()) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПланПоиска.Очистить();
	ПланПоиска.Добавить("ОбработчикГлобальногоПоиска", "ПлюсСервисСлужебныйКлиент", Ложь);
	
КонецПроцедуры

// Обработчик выбора результата глобального поиска.
// 
// Параметры:
//  ЭлементРезультата - ЭлементРезультатаГлобальногоПоиска
//  СтандартнаяОбработка - Булево
//
Процедура ПриВыбореРезультатаГлобальногоПоиска(ЭлементРезультата, СтандартнаяОбработка) Экспорт
	
	Если Не ЭлементРезультата.Значение = ПлюсСервисСлужебныйКлиент.ИмяРезультатаПоиска1СПлюс() Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	Для Каждого Действие Из ЭлементРезультата.Действия Цикл
		ДействиеЗначение = Действие.Значение; // см. ПлюсСервисСлужебныйКлиент.ДействиеРезультатаГлобальногоПоиска
		Если ДействиеЗначение.Команда = ПлюсСервисКлиентСервер.КомандаПосмотретьОписание() Тогда
			ОткрытьИОповеститьФормуВзаимодействиеССервисом(Действие.Значение);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Обработчик выбора действия результата глобального поиска.
// 
// Параметры:
//  ЭлементРезультата - ЭлементРезультатаГлобальногоПоиска
//  Действие - см. ПлюсСервисСлужебныйКлиент.ДействиеРезультатаГлобальногоПоиска
Процедура ПриВыбореДействияРезультатаГлобальногоПоиска(ЭлементРезультата, Действие) Экспорт
	
	Если Не ЭлементРезультата.Значение = ПлюсСервисСлужебныйКлиент.ИмяРезультатаПоиска1СПлюс() Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьИОповеститьФормуВзаимодействиеССервисом(Действие);
	
КонецПроцедуры

// Обработчик перехода по навигационной ссылке.
// 
// Параметры:
//  ДанныеПерехода - ДанныеПереходаПоНавигационнойСсылке
//  СтандартнаяОбработка - Булево
Процедура ОбработкаПереходаПоНавигационнойСсылке(ДанныеПерехода, СтандартнаяОбработка) Экспорт
	
	АдресСсылки = ДанныеПерехода.БазоваяНавигационнаяСсылка + ДанныеПерехода.ОтносительнаяНавигационнаяСсылка;
	Если СтрНайти(АдресСсылки, ПлюсСервисКлиентСервер.АдресСервиса()) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Используется() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Сервис 1С:Плюс недоступен для использования в данной конфигурации'"));
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	СостояниеПерехода = ДанныеПерехода.ПараметрыНавигационнойСсылки.Получить("state");
	ИдентификаторСеанса = ДанныеПерехода.ПараметрыНавигационнойСсылки.Получить("id");
	Если Не ЗначениеЗаполнено(СостояниеПерехода) Или ЗначениеЗаполнено(ИдентификаторСеанса) Тогда
		Возврат;
	КонецЕсли;

	ЗначенияПараметров = ПлюсСервисСлужебныйКлиент.ЗначенияПараметровURL(СостояниеПерехода);
	ПубличныйИдентификатор = ЗначенияПараметров.Получить(ПлюсСервисКлиентСервер.ПолеПубличныйИдентификатор());
	ИдентификаторВерсии = ЗначенияПараметров.Получить(ПлюсСервисКлиентСервер.ПолеИдентификаторВерсии());
		
	Если Не ЗначениеЗаполнено(ПубличныйИдентификатор) И Не ЗначениеЗаполнено(ИдентификаторВерсии) Тогда
		Возврат;
	КонецЕсли;
	
	УстановкаРазрешена = ПлюсСервисСлужебныйКлиент.ЕстьПравоУстановкиРасширений();
	
	Если Не УстановкаРазрешена Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("ПубличныйИдентификатор", ПубличныйИдентификатор);
	ПараметрыПоиска.Вставить("ИдентификаторВерсии", ИдентификаторВерсии);
	
	ФормаОбработки = ПлюсСервисСлужебныйКлиент.ПолучитьФормуВзаимодействияССервисом();
	ИдентификаторСеанса = ФормаОбработки.ИдентификаторСеанса;

	Попытка
		ДанныеРасширения = ПлюсСервисВызовСервера.ПолучитьДанныеРасширения(ИдентификаторСеанса, ПараметрыПоиска);
	Исключение
		СтандартнаяОбработка = Ложь;
		ВызватьИсключение;
	КонецПопытки;
	
	Если ДанныеРасширения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	Действие = ПлюсСервисСлужебныйКлиент.ДействиеРезультатаГлобальногоПоиска(
		ПлюсСервисКлиентСервер.КомандаПосмотретьОписание(), , ДанныеРасширения);
	
	ОткрытьИОповеститьФормуВзаимодействиеССервисом(Действие);
	
КонецПроцедуры

// Снять монопольный режим
Процедура СнятьМонопольныйРежим() Экспорт
	
	ПлюсСервисВызовСервера.СнятьМонопольныйРежим();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Открыть и оповестить форму взаимодействие с сервисом.
// 
// Параметры:
//  Действие - см. ПлюсСервисСлужебныйКлиент.ДействиеРезультатаГлобальногоПоиска
Процедура ОткрытьИОповеститьФормуВзаимодействиеССервисом(Действие)
	
	ПараметрыФормы = Новый Структура("ОжидаетсяОповещение", Истина);
	ПлюсСервисСлужебныйКлиент.ОткрытьФормуВзаимодействияССервисом(ПараметрыФормы);
	Оповестить(ПлюсСервисСлужебныйКлиент.СобытиеОповещения(), Действие);
	
КонецПроцедуры

#КонецОбласти