#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытийБСП
// Обработка программных событий, возникающих в подсистемах БСП.

// Определяет события, на которые подписана эта библиотека.
//
// Параметры:
//  Подписки - Структура - Ключами свойств структуры являются имена событий, на которые
//           подписана эта библиотека.
//
Процедура ПриОпределенииПодписокНаСобытияБСП(Подписки) Экспорт
	
	// БазоваяФункциональность
	Подписки.ПередНачаломРаботыСистемы = Истина;
	Подписки.ПриНачалеРаботыСистемы = Истина;
	Подписки.ПередЗавершениемРаботыСистемы = Истина;
	
КонецПроцедуры

// Функция, вызываемая перед началом работы системы.
//
Процедура ПередНачаломРаботыСистемы() Экспорт
	
	Если ОбщегоНазначенияБПОКлиент.РазделениеВключено() 
		И Не ОбщегоНазначенияБПОКлиент.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРаботыКлиента = ПараметрыРаботыКлиентаПриЗапуске();
	ПодготовитьПараметрыПриложения(ПараметрыРаботыКлиента);
	
КонецПроцедуры

// Функция, вызываемая при начале работы системы.
//
Процедура ПриНачалеРаботыСистемы() Экспорт
	
	Если ОбщегоНазначенияБПОКлиент.РазделениеВключено() 
		И Не ОбщегоНазначенияБПОКлиент.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРаботыКлиента = ПараметрыРаботыКлиентаПриЗапуске();
	ПодготовитьПараметрыПриложения(ПараметрыРаботыКлиента);
	
#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда
	СоздаватьРабочееМесто = Истина;
	МенеджерОборудованияКлиент.ОпределитьРабочееМесто(СоздаватьРабочееМесто);
#Иначе
	// ++ НеМобильноеПриложение
	ПараметрыРабочегоМеста = ПараметрыРаботыКлиента.ПараметрыРабочегоМеста;
	ИспользуетсяРежимRDP = ПараметрыРабочегоМеста.ИспользуетсяRDP И Не ОбщегоНазначенияБПОКлиент.ЭтоВебКлиент();
	Если ИспользуетсяРежимRDP Тогда
		МенеджерОборудованияКлиент.ОпределитьРабочееМестоRDP();
	Иначе
		МенеджерОборудованияКлиент.ОпределитьРабочееМесто();
		Оповещение = Новый ОписаниеОповещения("ПолученMACАдресПриЗапуске", ЭтотОбъект);
		УстанавливатьРасширениеБраузера = Ложь;
		ЗапускПриложения = Истина;
		МенеджерОборудованияКлиент.НачатьПолучениеMACАдреса(Оповещение, УстанавливатьРасширениеБраузера, ЗапускПриложения);
	КонецЕсли;
	// -- НеМобильноеПриложение
#КонецЕсли
	
	// ++ НеМобильноеПриложение
#Если Не ВебКлиент Тогда
	ВнешниеКомпонентыБПОКлиент.ПереустановитьКомпоненты(ПараметрыРаботыКлиента.ОборудованиеДляПереустановки);
	Если ОбщегоНазначенияБПОКлиент.ИспользуетсяРаспределеннаяФискализация() Тогда
		МодульРаспределеннаяФискализацияКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("РаспределеннаяФискализацияКлиент");
		МодульРаспределеннаяФискализацияКлиент.ПодключениеСистемыВзаимодействия(ПараметрыРаботыКлиента.ИдентификаторОбсужденияРаспределеннойФискализации); 
	КонецЕсли;
#КонецЕсли
	// -- НеМобильноеПриложение
	
	
КонецПроцедуры

// Процедура, вызываемая при начале работы системы, выполняет подготовку данных механизма.
// 
// Параметры:
//  Отказ - Булево
//  Предупреждения - Строка 
//                 - Массив Из Структура
//
Процедура ПередЗавершениемРаботыСистемы(Отказ = Ложь, Предупреждения = "") Экспорт
	
	Если ОбщегоНазначенияБПОКлиент.ИспользуетсяПодсистемаЛогирования() 
		И ОбщегоНазначенияБПОКлиент.ДоступноИспользованиеРазделенныхДанных() Тогда
		
		ТекстПредупреждения = "";
		МодульЛогированиеОперацийБПОКлиент = 
			ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ЛогированиеОперацийБПОКлиент");
		МодульЛогированиеОперацийБПОКлиент.ЗаписатьЛогНаСервереПриЗавершении(
			Отказ, ТекстПредупреждения);
		
		Если ОбщегоНазначенияБПОКлиент.ИспользуетсяБСП() 
			И ТипЗнч(Предупреждения) = Тип("Массив") Тогда
			Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
				МодульСтандартныеПодсистемыКлиент = 
					ОбщегоНазначенияБПОКлиент.ОбщийМодуль("СтандартныеПодсистемыКлиент");
				Предупреждение = 
					МодульСтандартныеПодсистемыКлиент.ПредупреждениеПриЗавершенииРаботы();
				Предупреждение.ТекстПредупреждения = ТекстПредупреждения;
				Предупреждения.Добавить(Предупреждение);
			КонецЕсли;
		Иначе
			Предупреждения = ТекстПредупреждения;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Выполняет обработку внешнего события, вызывается из глобального модуля
//
// Параметры:
//  Источник - Строка.
//  Событие - Строка.
//  Данные - Строка.
Процедура ОбработкаВнешнегоСобытия(Источник, Событие, Данные) Экспорт

	ОписаниеОшибки  = "";
	// Подготовить данные
	ОписаниеСобытия = Новый Структура();
	ОписаниеСобытия.Вставить("Источник", Источник);
	ОписаниеСобытия.Вставить("Событие",  Событие);
	ОписаниеСобытия.Вставить("Данные",   Данные);
	// Передать на обработку данные.
	Результат = МенеджерОборудованияКлиент.ОбработатьСобытиеОтУстройства(ОписаниеСобытия, ОписаниеОшибки);
	Если Не Результат Тогда
		ОбщегоНазначенияБПОКлиент.СообщитьПользователю(НСтр("ru='При обработке внешнего события от устройства произошла ошибка.'") + Символы.ПС + ОписаниеОшибки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает значения параметров, необходимых для работы клиентского кода конфигурации
// без дополнительных серверных вызовов.
// 
// Возвращаемое значение:
//   Структура:
//     * ТекущаяДатаНаКлиенте - Дата
Функция ПараметрыРаботыКлиентаПриЗапуске() Экспорт
	
	Если ОбщегоНазначенияБПОКлиент.ИспользуетсяБСП() Тогда
		ПараметрыРаботыКлиента = ОбщегоНазначенияБПОКлиент.ПараметрыРаботыКлиентаПриЗапуске();
		Если ПараметрыРаботыКлиента.Свойство("ОборудованиеДляПереустановки") Тогда
			Возврат ПараметрыРаботыКлиента;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ИнтеграцияПодсистемБПОКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	
КонецФункции

Процедура ПолученMACАдресПриЗапуске(MACАдрес, Параметры) Экспорт
	
	СоздаватьРабочееМесто = Истина;
	МенеджерОборудованияКлиент.ОпределитьРабочееМесто(СоздаватьРабочееМесто);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьПараметрыПриложения(ПараметрыРаботыКлиента)
	
	Если ПараметрыПриложения = Неопределено Тогда
		ПараметрыПриложения = Новый Соответствие();
	КонецЕсли;
	ПодключаемоеОборудование = МенеджерОборудованияКлиент.ПодключаемоеОборудование();
	
	Если Не ОбщегоНазначенияБПОКлиент.ИспользуетсяБСП() Тогда
		ИмяПараметра = "БПО.ПодсистемыКонфигурации";
		Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
			ПараметрыПриложения.Вставить(ИмяПараметра, ПараметрыРаботыКлиента.ИменаПодсистем);
		КонецЕсли;
		
		ПодключаемоеОборудование.ПоправкаКВремениСеанса = ПараметрыРаботыКлиента.ПоправкаКВремениСеанса;
		
	КонецЕсли;
	
	ИмяПараметра = "БПО.ВерсияБСП";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПараметра, ПараметрыРаботыКлиента.ВерсияБСП);
	КонецЕсли;
	
	ИмяПараметра = "БПО.СоответствиеТиповОборудования";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПараметра, ПараметрыРаботыКлиента.СоответствиеТиповОборудования);
	КонецЕсли;
	
	ИмяПараметра = "БПО.ДоступныеТипыОборудования";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПараметра, ПараметрыРаботыКлиента.ДоступныеТипыОборудования);
	КонецЕсли;
	
	ПодключаемоеОборудование.ПараметрыРабочегоМеста = 
		ОбщегоНазначенияБПОКлиент.СкопироватьРекурсивно(ПараметрыРаботыКлиента.ПараметрыРабочегоМеста, Ложь);
		
	ПодключаемоеОборудование.ВерсияБиблиотеки = ПараметрыРаботыКлиента.ВерсияБиблиотеки;
		
	Если ОбщегоНазначенияБПОКлиент.ИспользуетсяПодсистемаЛогирования() Тогда
		Если ПодключаемоеОборудование.СистемаЛогирования = Неопределено Тогда
			МодульЛогированиеОперацийБПОКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ЛогированиеОперацийБПОКлиент");
			ПодключаемоеОборудование.СистемаЛогирования = МодульЛогированиеОперацийБПОКлиент.НоваяСистемаЛогирования(ПараметрыРаботыКлиента.ПараметрыЛогированияОперацийСОборудованием);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

