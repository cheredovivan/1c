#Область ПрограммныйИнтерфейс

// Возвращает массив всех возможных типов связанных объектов
//
Функция ПолучитьТипыСвязанныхОбъектов() Экспорт
	
	МетаданныеСвязанныхОбъектов = Новый Массив;
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Справочники.ВизыСогласования);
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Справочники.Резолюции);
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Справочники.УведомленияПрограммы);
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Справочники.АдреснаяКнига);
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Справочники.Контроль);
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Справочники.ОчередьЗаданийДокументооборота);
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Справочники.ВеткиОбработки);
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Справочники.Замечания);
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Справочники.РеестрыЗадач);
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Справочники.ГруппировкиЗадач);
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Справочники.КомандыВиджетов);
	
	Возврат МетаданныеСвязанныхОбъектов;
	
КонецФункции
	
// Находит объекты, которые согласно прикладной логике должны быть удалены 
// вместе с выбранными удаляемыми объектами
//
// Параметры:
// УдаляемыеОбъекты - Массив - массив удаляемых объектов
//
Функция ПолучитьТаблицуСвязанныхОбъектов(УдаляемыеОбъекты) Экспорт
	
	ТаблицаСвязанных = Новый ТаблицаЗначений;
	ТаблицаСвязанных.Колонки.Добавить("УдаляемыйОбъект");
	ТаблицаСвязанных.Колонки.Добавить("СвязанныйОбъект");
	ТаблицаСвязанных.Колонки.Добавить("ПометкаУдаленияСвязанного");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УдаляемыеОбъекты", УдаляемыеОбъекты);
	
	ДобавитьСвязанныеВизыСогласования(Запрос, ТаблицаСвязанных);
	ДобавитьСвязанныеРезолюции(Запрос, ТаблицаСвязанных);
	ДобавитьСвязанныеУведомленияПрограммы(Запрос, ТаблицаСвязанных);
	ДобавитьСвязанныеЗаписиАдреснойКниги(Запрос, ТаблицаСвязанных);
	ДобавитьСвязанныеКарточкиКонтроля(Запрос, ТаблицаСвязанных);
	ДобавитьСвязанныеЗаданияОчередиДокументооборота(Запрос, ТаблицаСвязанных);
	
	ДобавитьСвязанныеВеткиОбработки(Запрос, ТаблицаСвязанных);
	ДобавитьСвязанныеЗамечания(Запрос, ТаблицаСвязанных);
	ДобавитьСвязанныеРеестрыЗадач(Запрос, ТаблицаСвязанных);
	ДобавитьСвязанныеГруппировкиЗадач(Запрос, ТаблицаСвязанных);
	ДобавитьСвязанныеКомандыВиджетов(Запрос, ТаблицаСвязанных);
	
	ТаблицаСвязанных.Свернуть("УдаляемыйОбъект, СвязанныйОбъект, ПометкаУдаленияСвязанного");
	
	Возврат ТаблицаСвязанных;
	
КонецФункции

// Удаляет записи регистров, имеющие ссылки на удаляемые объекты
// Параметры:
//  УдаляемыеОбъекты - массив удаляемых объектов
//
Процедура УдалитьДанныеСвязанныхРегистров(УдаляемыеОбъекты) Экспорт
	
	// ФактическиеТрудозатраты
	ИмяРегистра = "ФактическиеТрудозатраты";
	ПоляПоиска = Новый Массив;
	ПоляПоиска.Добавить("ЕжедневныйОтчет");
	УдалениеОбъектов.УдалитьДанныеСвязанногоРегистра(ИмяРегистра, ПоляПоиска, УдаляемыеОбъекты);
	
	// ПроизошедшиеБизнесСобытия
	ИмяРегистра = "ПроизошедшиеБизнесСобытия";
	ПоляПоиска = Новый Массив;
	ПоляПоиска.Добавить("Источник");
	УдалениеОбъектов.УдалитьДанныеСвязанногоРегистра(ИмяРегистра, ПоляПоиска, УдаляемыеОбъекты);
	
	// ПроцессыДействий
	ИмяРегистра = "ПроцессыДействий";
	ПоляПоиска = Новый Массив;
	ПоляПоиска.Добавить("Действие");
	УдалениеОбъектов.УдалитьДанныеСвязанногоРегистра(ИмяРегистра, ПоляПоиска, УдаляемыеОбъекты);
	
	// ДействияОбработкиОбъектов
	ИмяРегистра = "ДействияОбработкиОбъектов";
	ПоляПоиска = Новый Массив;
	ПоляПоиска.Добавить("Обработка");
	УдалениеОбъектов.УдалитьДанныеСвязанногоРегистра(ИмяРегистра, ПоляПоиска, УдаляемыеОбъекты);
	
	// ПроцессыОбработокОбъектов
	ИмяРегистра = "ПроцессыОбработокОбъектов";
	ПоляПоиска = Новый Массив;
	ПоляПоиска.Добавить("Процесс");
	ПоляПоиска.Добавить("Обработка");
	УдалениеОбъектов.УдалитьДанныеСвязанногоРегистра(ИмяРегистра, ПоляПоиска, УдаляемыеОбъекты);
	
	// СведенияОбУчастникахДействий
	ИмяРегистра = "СведенияОбУчастникахДействий";
	ПоляПоиска = Новый Массив;
	ПоляПоиска.Добавить("ФактическийИсполнитель");
	УдалениеОбъектов.УдалитьДанныеСвязанногоРегистра(ИмяРегистра, ПоляПоиска, УдаляемыеОбъекты);
	
	// ПредпросмотрФайлов
	ИмяРегистра = "ПредпросмотрФайлов";
	ПоляПоиска = Новый Массив;
	ПоляПоиска.Добавить("ВерсияФайла");
	УдалениеОбъектов.УдалитьДанныеСвязанногоРегистра(ИмяРегистра, ПоляПоиска, УдаляемыеОбъекты);
	
	// НастройкиНумерации
	ИмяРегистра = "НастройкиНумерации";
	ПоляПоиска = Новый Массив;
	ПоляПоиска.Добавить("Нумератор");
	УдалениеОбъектов.УдалитьДанныеСвязанногоРегистра(ИмяРегистра, ПоляПоиска, УдаляемыеОбъекты);
	
	// НастройкиНумерации
	ИмяРегистра = "ОчередьОбновленияКэширующихДанных";
	ПоляПоиска = Новый Массив;
	ПоляПоиска.Добавить("КлючВлияющихДанных");
	УдалениеОбъектов.УдалитьДанныеСвязанногоРегистра(ИмяРегистра, ПоляПоиска, УдаляемыеОбъекты);

	// ЭскалированныеДействия
	ИмяРегистра = "ЭскалированныеДействия";
	ПоляПоиска = Новый Массив;
	ПоляПоиска.Добавить("ПравилоЭскалации");
	УдалениеОбъектов.УдалитьДанныеСвязанногоРегистра(ИмяРегистра, ПоляПоиска, УдаляемыеОбъекты);

	// ЭскалированныеЗадачи
	ИмяРегистра = "ЭскалированныеЗадачи";
	ПоляПоиска = Новый Массив;
	ПоляПоиска.Добавить("ПравилоЭскалации");
	УдалениеОбъектов.УдалитьДанныеСвязанногоРегистра(ИмяРегистра, ПоляПоиска, УдаляемыеОбъекты);
	
	// Сервис "1С:Share" Кэш видов документов
	ИмяРегистра = "Сервис1СShareКэшВидовДокументов";
	ПоляПоиска = Новый Массив;
	ПоляПоиска.Добавить("ТематикаДокумента");
	ПоляПоиска.Добавить("ШаблонДокумента");
	УдалениеОбъектов.УдалитьДанныеСвязанногоРегистра(ИмяРегистра, ПоляПоиска, УдаляемыеОбъекты);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Визы согласования по источнику и документу
Процедура ДобавитьСвязанныеВизыСогласования(Запрос, ТаблицаСвязанных)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВизыСогласования.Источник КАК УдаляемыйОбъект,
		|	ВизыСогласования.Ссылка КАК СвязанныйОбъект,
		|	ВизыСогласования.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.ВизыСогласования КАК ВизыСогласования
		|ГДЕ
		|	ВизыСогласования.Источник В(&УдаляемыеОбъекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВизыСогласования.Документ,
		|	ВизыСогласования.Ссылка,
		|	ВизыСогласования.ПометкаУдаления
		|ИЗ
		|	Справочник.ВизыСогласования КАК ВизыСогласования
		|ГДЕ
		|	ВизыСогласования.Документ В(&УдаляемыеОбъекты)";
	
	ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных);
	
КонецПроцедуры

// Резолюции по источнику и документу
Процедура ДобавитьСвязанныеРезолюции(Запрос, ТаблицаСвязанных)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Резолюции.Источник КАК УдаляемыйОбъект,
		|	Резолюции.Ссылка КАК СвязанныйОбъект,
		|	Резолюции.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.Резолюции КАК Резолюции
		|ГДЕ
		|	Резолюции.Источник В(&УдаляемыеОбъекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Резолюции.Документ,
		|	Резолюции.Ссылка,
		|	Резолюции.ПометкаУдаления
		|ИЗ
		|	Справочник.Резолюции КАК Резолюции
		|ГДЕ
		|	Резолюции.Документ В(&УдаляемыеОбъекты)";
	
	ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных);
	
КонецПроцедуры

// Ветки обработки по документу и действию
Процедура ДобавитьСвязанныеВеткиОбработки(Запрос, ТаблицаСвязанных)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВеткиОбработки.ВладелецВетки КАК УдаляемыйОбъект,
		|	ВеткиОбработки.Ссылка КАК СвязанныйОбъект,
		|	ВеткиОбработки.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.ВеткиОбработки КАК ВеткиОбработки
		|ГДЕ
		|	ВеткиОбработки.ВладелецВетки В (&УдаляемыеОбъекты)";
	
	ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных);
	
КонецПроцедуры

// Замечания по документу
Процедура ДобавитьСвязанныеЗамечания(Запрос, ТаблицаСвязанных)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Замечания.Документ КАК УдаляемыйОбъект,
		|	Замечания.Ссылка КАК СвязанныйОбъект,
		|	Замечания.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.Замечания КАК Замечания
		|ГДЕ
		|	(Замечания.Документ В (&УдаляемыеОбъекты)
		|			ИЛИ Замечания.ВладелецЗамечания В (&УдаляемыеОбъекты))";
	
	ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных);
	
КонецПроцедуры

// Реестры задач
Процедура ДобавитьСвязанныеРеестрыЗадач(Запрос, ТаблицаСвязанных)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеестрыЗадач.КлючРеестра КАК УдаляемыйОбъект,
		|	РеестрыЗадач.Ссылка КАК СвязанныйОбъект,
		|	РеестрыЗадач.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.РеестрыЗадач КАК РеестрыЗадач
		|ГДЕ
		|	РеестрыЗадач.КлючРеестра В (&УдаляемыеОбъекты)";
	
	ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных);
	
КонецПроцедуры

// Группировки задач
Процедура ДобавитьСвязанныеГруппировкиЗадач(Запрос, ТаблицаСвязанных)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГруппировкиЗадач.Автор КАК УдаляемыйОбъект,
		|	ГруппировкиЗадач.Ссылка КАК СвязанныйОбъект,
		|	ГруппировкиЗадач.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.ГруппировкиЗадач КАК ГруппировкиЗадач
		|ГДЕ
		|	ГруппировкиЗадач.Автор В (&УдаляемыеОбъекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ГруппировкиЗадач.ВидДействия КАК УдаляемыйОбъект,
		|	ГруппировкиЗадач.Ссылка КАК СвязанныйОбъект,
		|	ГруппировкиЗадач.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.ГруппировкиЗадач КАК ГруппировкиЗадач
		|ГДЕ
		|	ГруппировкиЗадач.ВидДействия В (&УдаляемыеОбъекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ГруппировкиЗадач.ВидЗадачи КАК УдаляемыйОбъект,
		|	ГруппировкиЗадач.Ссылка КАК СвязанныйОбъект,
		|	ГруппировкиЗадач.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.ГруппировкиЗадач КАК ГруппировкиЗадач
		|ГДЕ
		|	ГруппировкиЗадач.ВидЗадачи В (&УдаляемыеОбъекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ГруппировкиЗадач.ВидПриложения КАК УдаляемыйОбъект,
		|	ГруппировкиЗадач.Ссылка КАК СвязанныйОбъект,
		|	ГруппировкиЗадач.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.ГруппировкиЗадач КАК ГруппировкиЗадач
		|ГДЕ
		|	ГруппировкиЗадач.ВидПриложения В (&УдаляемыеОбъекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ГруппировкиЗадач.Исполнитель КАК УдаляемыйОбъект,
		|	ГруппировкиЗадач.Ссылка КАК СвязанныйОбъект,
		|	ГруппировкиЗадач.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.ГруппировкиЗадач КАК ГруппировкиЗадач
		|ГДЕ
		|	ГруппировкиЗадач.Исполнитель В (&УдаляемыеОбъекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ГруппировкиЗадач.Проект КАК УдаляемыйОбъект,
		|	ГруппировкиЗадач.Ссылка КАК СвязанныйОбъект,
		|	ГруппировкиЗадач.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.ГруппировкиЗадач КАК ГруппировкиЗадач
		|ГДЕ
		|	ГруппировкиЗадач.Проект В (&УдаляемыеОбъекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ГруппировкиЗадач.Состояние КАК УдаляемыйОбъект,
		|	ГруппировкиЗадач.Ссылка КАК СвязанныйОбъект,
		|	ГруппировкиЗадач.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.ГруппировкиЗадач КАК ГруппировкиЗадач
		|ГДЕ
		|	ГруппировкиЗадач.Состояние В (&УдаляемыеОбъекты)";
	
	ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных);
	
КонецПроцедуры

// Группировки задач
Процедура ДобавитьСвязанныеКомандыВиджетов(Запрос, ТаблицаСвязанных)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КомандыВиджетов.Источник КАК УдаляемыйОбъект,
		|	КомандыВиджетов.Ссылка КАК СвязанныйОбъект,
		|	КомандыВиджетов.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.КомандыВиджетов КАК КомандыВиджетов
		|ГДЕ
		|	КомандыВиджетов.Источник В (&УдаляемыеОбъекты)";
	
	ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных);
	
КонецПроцедуры

// Уведомления программы по объектам
Процедура ДобавитьСвязанныеУведомленияПрограммы(Запрос, ТаблицаСвязанных)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УведомленияПрограммы.Объект КАК УдаляемыйОбъект,
		|	УведомленияПрограммы.Ссылка КАК СвязанныйОбъект,
		|	УведомленияПрограммы.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.УведомленияПрограммы КАК УведомленияПрограммы
		|ГДЕ
		|	УведомленияПрограммы.Объект В(&УдаляемыеОбъекты)";
		
	ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных);
	
КонецПроцедуры

// Адресная книга
Процедура ДобавитьСвязанныеЗаписиАдреснойКниги(Запрос, ТаблицаСвязанных)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АдреснаяКнига.Объект КАК УдаляемыйОбъект,
		|	АдреснаяКнига.Ссылка КАК СвязанныйОбъект,
		|	АдреснаяКнига.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.АдреснаяКнига КАК АдреснаяКнига
		|ГДЕ
		|	АдреснаяКнига.Объект В(&УдаляемыеОбъекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	АдреснаяКнига.ОбъектДоступа,
		|	АдреснаяКнига.Ссылка,
		|	АдреснаяКнига.ПометкаУдаления
		|ИЗ
		|	Справочник.АдреснаяКнига КАК АдреснаяКнига
		|ГДЕ
		|	АдреснаяКнига.Объект В(&УдаляемыеОбъекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	АдреснаяКнига.РодительОбъекта,
		|	АдреснаяКнига.Ссылка,
		|	АдреснаяКнига.ПометкаУдаления
		|ИЗ
		|	Справочник.АдреснаяКнига КАК АдреснаяКнига
		|ГДЕ
		|	АдреснаяКнига.Объект В(&УдаляемыеОбъекты)";
		
	ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных);
	
КонецПроцедуры

// Добавляет строки результата запроса к переданной таблице связанных объектов
Процедура ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных)
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаСвязанных.Добавить(), Выборка);
	КонецЦикла;	
		
КонецПроцедуры

// Карточки контроля по удаляемым объектам
// 
// Параметры:
//  Запрос - Запрос
//  ТаблицаСвязанных - ТаблицаЗначений
Процедура ДобавитьСвязанныеКарточкиКонтроля(Запрос, ТаблицаСвязанных)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контроль.Ссылка КАК СвязанныйОбъект,
		|	Контроль.Предмет КАК УдаляемыйОбъект,
		|	Контроль.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.Контроль КАК Контроль
		|ГДЕ
		|	Контроль.Предмет В(&УдаляемыеОбъекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Контроль.Ссылка,
		|	Контроль.Источник,
		|	Контроль.ПометкаУдаления
		|ИЗ
		|	Справочник.Контроль КАК Контроль
		|ГДЕ
		|	Контроль.Источник В(&УдаляемыеОбъекты)";
	
	ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных);
	
КонецПроцедуры

Процедура ДобавитьСвязанныеЗаданияОчередиДокументооборота(Запрос, ТаблицаСвязанных)
	
	СтатусЗавершено = Перечисления.СтатусыЗаданий.Завершено;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОчередьЗаданийДокументооборота.Ссылка КАК Задание,
		|	ОчередьЗаданийДокументооборота.ПредметЗадания КАК УдаляемыйОбъект
		|ИЗ
		|	Справочник.ОчередьЗаданийДокументооборота КАК ОчередьЗаданийДокументооборота
		|ГДЕ
		|	ОчередьЗаданийДокументооборота.СтатусЗадания = &СтатусЗадания
		|	И ОчередьЗаданийДокументооборота.ПредметЗадания В (&УдаляемыеОбъекты)";
	Запрос.УстановитьПараметр("СтатусЗадания", СтатусЗавершено);
	
	ЗаданияПоОбъекту = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаЗадания Из ЗаданияПоОбъекту Цикл
		
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаЗадания.Задание,
			"Ссылка, Родитель, СтатусЗадания");
		Родитель = Реквизиты.Родитель;
		
		Пока Не Родитель.Пустая() Цикл
			
			Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Родитель,
				"Ссылка, Родитель, СтатусЗадания");
			Родитель = Реквизиты.Родитель;
			
		КонецЦикла;
		
		Если Реквизиты.СтатусЗадания = СтатусЗавершено Тогда
			
			ЗапросЗаданий = Новый Запрос;
			ЗапросЗаданий.Текст = "ВЫБРАТЬ
				|	ОчередьЗаданийДокументооборота.Ссылка,
				|	ОчередьЗаданийДокументооборота.ПометкаУдаления
				|ИЗ
				|	Справочник.ОчередьЗаданийДокументооборота КАК ОчередьЗаданийДокументооборота
				|ГДЕ
				|	ОчередьЗаданийДокументооборота.Ссылка В ИЕРАРХИИ (&КорневоеЗадание)";
			ЗапросЗаданий.УстановитьПараметр("КорневоеЗадание", Реквизиты.Ссылка);
			ВсеЗадания = ЗапросЗаданий.Выполнить().Выгрузить();
			
			Для Каждого Задание Из ВсеЗадания Цикл
				НоваяСтрока = ТаблицаСвязанных.Добавить();
				НоваяСтрока.СвязанныйОбъект = Задание.Ссылка;
				НоваяСтрока.ПометкаУдаленияСвязанного = Задание.ПометкаУдаления;
				НоваяСтрока.УдаляемыйОбъект = СтрокаЗадания.УдаляемыйОбъект;
			КонецЦикла;
			
		КонецЕсли;
		 
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
