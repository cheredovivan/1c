
#Область СлужебныйПрограммныйИнтерфейс

// Вызывается из Распознавание.ВыполнитьАвторизациюЧерезИТС()
// Выполняет авторизацию базы в сервисе распознавания, используя параметры ИТС.
// Возвращаемое значение:
//  Булево - Истина, если данные авторизации на ИТС заполнены
Функция ВыполненаАвторизацияЧерезИТС() Экспорт
	
	ВыполненаАвторизация = Ложь;
	
// Локализация
	ВыполненаАвторизация = ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
// Локализация Конец

	Возврат ВыполненаАвторизация;
	
КонецФункции

// Вызывается из Распознавание.ВыполнитьАвторизациюЧерезИТС()
// Возвращает тикет аутентификации пользователя на портале поддержки.
// Возвращенный тикет может быть проверен вызовом операции check
// Параметры:
//  ВладелецТикета - Строка - произвольное имя сервиса, для которого
//      выполняется аутентификация пользователя. Это же имя должно
//      использоваться при вызове операции checkTicket;
//      Не допускается незаполненное значение параметра.
// Возвращаемое значение:
//  Структура - результат получения тикета. Поля структуры:
//        * Тикет - Строка - полученный тикет аутентификации. Если при получении
//          тикета произошла ошибка (неверный логин или пароль или другая ошибка),
//          значение поля - Строка.
//        * КодОшибки - Строка - строковый код возникшей ошибки, который
//          может быть обработан вызывающим функционалом
//        * СообщениеОбОшибке - Строка - краткое описание ошибки, которое
//          может быть отображено пользователю;
//        * ИнформацияОбОшибке - Строка - подробное описание ошибки, которое
//          может быть записано в журнал регистрации.
Функция РезультатПолученияТикета(ВладелецТикета) Экспорт
	
	РезультатПолученияТикета = Новый Структура();
	
// Локализация
	РезультатПолученияТикета = ИнтернетПоддержкаПользователей.ТикетАутентификацииНаПорталеПоддержки(ВладелецТикета);
// Локализация Конец
	
	Возврат РезультатПолученияТикета;
	
КонецФункции

// Возвращает логин и пароль пользователя Интернет-поддержки, сохраненные в информационной базе.
// Перед вызовом вызывающий код должен устанавливать привилегированный режим.
//
// Возвращаемое значение:
//  Структура - Структура - логин и пароль пользователя Интернет-поддержки:
//    * Логин - Строка - логин пользователя Интернет-поддержки;
//    * Пароль - Строка - пароль пользователя Интернет-поддержки.
//  Неопределено - при отсутствии сохраненных данных аутентификации.
//
Функция ДанныеАутентификацииПользователяИнтернетПоддержки() Экспорт
	
	ПараметрыАутентификацииИТС = Новый Структура();
	
// Локализация
	ПараметрыАутентификацииИТС = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
// Локализация Конец
	
	Возврат ПараметрыАутентификацииИТС;

КонецФункции	

// Выполняет проверку возможности подключения тестового периода для пользователя.
// Перед запросом доступных тестовых периодов происходит проверка кэша. Если данные
// о доступных тестовых периодах были получены в течение 24 часов, используется информация
// из кэша. В противном случае производится запрос на получение обновленной информации
// из сервисов Портала 1С:ИТС. Функцию необходимо использовать для определения
// потребности в отображении элемента управления подключения тестового периода.
// Функцию следует использовать, если целевой сервис не подключен.
//
// Параметры:
//  Идентификаторы - Массив из Строка - содержит идентификаторы сервисов в системе Портал 1С:ИТС.
//
// Возвращаемое значение:
//  Структура - содержит информацию о доступных тестовых периодах сервисов сопровождения:
//    *Ошибка - Булево - Истина, если при обновлении кэша были ошибки;
Функция ДоступноПодключениеТестовогоПериода(Идентификаторы) Экспорт

	ОписательДоступности = Новый Структура();
	ОписательДоступности.Вставить("Ошибка",               Ложь);
	ОписательДоступности.Вставить("СообщениеОбОшибке",    "");
	ОписательДоступности.Вставить("СервисыСопровождения", Новый Соответствие);

	Для Каждого Идентификатор Из Идентификаторы Цикл
		ОписательДоступности.СервисыСопровождения.Вставить(Идентификатор, "Недоступно");
	КонецЦикла;

// Локализация
	ОписательДоступности = ПодключениеСервисовСопровождения.ДоступноПодключениеТестовогоПериода(Идентификаторы);
// Локализация Конец

	Возврат ОписательДоступности;

КонецФункции

// Проверяет возможность подключения тестового периода сервиса распознавания документов.
// 
// Возвращаемое значение:
//  Булево - Истина, если доступно.
//
Функция ПодключениеТестовогоПериодаДоступно() Экспорт
	
	// Локализация
	ИдентификаторСервиса = РаспознаваниеКлиентСервер.ИдентификаторСервиса();
	Идентификаторы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторСервиса);
	ОписательДоступности = ПодключениеСервисовСопровождения.ДоступноПодключениеТестовогоПериода(Идентификаторы);
	Если Не ОписательДоступности.Ошибка Тогда
		Возврат ОписательДоступности.СервисыСопровождения[ИдентификаторСервиса] = "Доступно";
	КонецЕсли;
	// Локализация Конец
	
	Возврат Ложь;
	
КонецФункции

// Регистрирует список сервисов, которые доступны для
// использования в конфигурации.
//
// Параметры:
//   СервисыСопровождения - Массив - массив структур, описывающий сервисы доступные для использовании в конфигурации;
//     Значение - Структура - см. ПодключениеСервисовСопровождения.НовыйОписательСервиса().
//
Процедура ПриДобавленииОписанийСервисовСопровождения(СервисыСопровождения) Экспорт
	
	// Локализация
	ОписательСервиса = ПодключениеСервисовСопровождения.НовыйОписательСервиса();
	ОписательСервиса.Идентификатор   = РаспознаваниеКлиентСервер.ИдентификаторСервиса();
	ОписательСервиса.Картинка        = Метаданные.ОбщиеКартинки.РаспознаваниеДокументов72;
	ОписательСервиса.Наименование    = НСтр("ru = '1С:Распознавание текста'");
	ОписательСервиса.Описание        = НСтр("ru = 'Извлечение текстов из файлов изображений.'");
	
	СервисыСопровождения.Добавить(ОписательСервиса);
	// Локализация Конец
	
КонецПроцедуры

#КонецОбласти