
#Область ПрограммныйИнтерфейс

// Параметры:
//  ИдентификаторЗадания - Строка - Идентификатор задания
// 
// Возвращаемое значение:
//  Структура 
//
Функция РезультатПоИдентификатору(ИдентификаторЗадания) Экспорт
	
	КомандаСервиса = НоваяКомандаСервиса();
	КомандаСервиса.URLЗапроса = БазовыйURL() + "/ocr/models/" + НомерМодели() + "/result/" + ИдентификаторЗадания;
	
	Возврат ВыполнитьКомандуСервиса(КомандаСервиса);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Команды

#Область Авторизация

// Выполнить авторизацию в сервисе распознавания используя тикет Портала 1С:ИСТ.
//
// Параметры:
//  Тикет - Строка
//  Область - Структура
//  ИдентификаторИБ - Строка
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
//  * ИдентификаторИБ - Строка - ["client_id"]
//  * ТокенДоступа - Строка - ["session_id"]
//  * Состояние - Строка - "Активирован" или "Ожидает" (["status"]="pending")
//
Функция ВыполнитьАвторизациюПоТикетуИТС(Тикет, Область, ИдентификаторИБ = Неопределено) Экспорт
	
	Данные = Новый Структура;
	Данные.Вставить("ticket", Тикет);
	Данные.Вставить("scope", РаспознаваниеДокументовHTTP.ОбъектВJson(Область));
	Данные.Вставить("client_id", ИдентификаторИБ);
	
	КомандаСервиса = НоваяКомандаСервиса();
	КомандаСервиса.URLЗапроса = БазовыйURL() + "/users_its";
	КомандаСервиса.Метод = "POST";
	КомандаСервиса.Заголовки.Вставить("Content-Type", "application/json");
	КомандаСервиса.Заголовки.Вставить("Charset", "utf-8");
	КомандаСервиса.Данные = РаспознаваниеДокументовHTTP.ОбъектВJson(Данные);
	
	Возврат ВыполнитьКомандуСервисаАвторизации(КомандаСервиса);
	
КонецФункции

// Выполнить авторизацию в сервисе распознавания используя логин и пароль Портала 1С:ИСТ.
//
// Параметры:
//  Логин - Строка
//  Пароль - Строка
//  Область - Структура
//  ИдентификаторИБ - Строка
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
//  * ИдентификаторИБ - Строка - ["client_id"]
//  * ТокенДоступа - Строка - ["session_id"]
//  * Состояние - Строка - "Активирован" или "Ожидает" (["status"]="pending")
//
Функция ВыполнитьАвторизациюПоЛогинуПаролюИТС(Логин, Пароль, Область, ИдентификаторИБ = Неопределено) Экспорт
	
	Данные = Новый Структура;
	Данные.Вставить("its_login", Логин);
	Данные.Вставить("its_password", Пароль);
	Данные.Вставить("scope", РаспознаваниеДокументовHTTP.ОбъектВJson(Область));
	Данные.Вставить("client_id", ИдентификаторИБ);
	
	КомандаСервиса = НоваяКомандаСервиса();
	КомандаСервиса.URLЗапроса = БазовыйURL() + "/users_its";
	КомандаСервиса.Метод = "POST";
	КомандаСервиса.Заголовки.Вставить("Content-Type", "application/json");
	КомандаСервиса.Заголовки.Вставить("Charset", "utf-8");
	КомандаСервиса.Данные = РаспознаваниеДокументовHTTP.ОбъектВJson(Данные);
	
	Возврат ВыполнитьКомандуСервисаАвторизации(КомандаСервиса);
	
КонецФункции

Функция ВыполнитьКомандуСервисаАвторизации(КомандаСервиса)
	
	РезультатКоманды = ВыполнитьКомандуСервиса(КомандаСервиса);
	
	Если РезультатКоманды = Неопределено Тогда
		ТекстОшибки = НСтр("ru = 'Ошибка при обращении к сервису распознавания.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Если РезультатКоманды.КодСостояния <> КодСостояния_200_ОК()
		И РезультатКоманды.КодСостояния <> КодСостояния_202_Accepted() Тогда
		
		Если РезультатКоманды.КодСостояния = КодСостояния_400_BadRequest() Тогда
			ТекстОшибки = НСтр("ru = 'Неверный логин или пароль.'");
		ИначеЕсли ТипЗнч(РезультатКоманды.ДесериализованноеЗначение) = Тип("Соответствие") Тогда
			ТекстОшибки = РезультатКоманды.ДесериализованноеЗначение.Получить("message");
		Иначе
			ТекстОшибки = НСтр("ru = 'Ошибка при авторизации в сервисе распознавания.'");
		КонецЕсли;
		
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Состояние = XMLСтрока(РезультатКоманды.ДесериализованноеЗначение.Получить("status"));
	Если Состояние = "pending" Тогда
		Состояние = "Ожидает";
	Иначе
		Состояние = "Активирован";
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторИБ", XMLСтрока(РезультатКоманды.ДесериализованноеЗначение.Получить("client_id")));
	Результат.Вставить("ТокенДоступа", XMLСтрока(РезультатКоманды.ДесериализованноеЗначение.Получить("session_id")));
	Результат.Вставить("Состояние", Состояние);
	
	Возврат Новый ФиксированнаяСтруктура(Результат);
	
КонецФункции

// Выполнить проверку состояния активации учетной записи связанной с Порталом 1С:ИТС.
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
//  * Состояние - Строка - "Активирован" или "Ожидает"
//
Функция СостояниеАктивацииУчетнойЗаписи() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Состояние", "Ожидает");
	
	КомандаСервиса = НоваяКомандаСервиса();
	КомандаСервиса.URLЗапроса = БазовыйURL() + "/user_status";
	КомандаСервиса.ОсталосьПопыток = 0;
	
	РезультатКоманды = ВыполнитьКомандуСервиса(КомандаСервиса);
	
	Если РезультатКоманды = Неопределено
		Или (РезультатКоманды.КодСостояния <> КодСостояния_200_ОК()
		И РезультатКоманды.КодСостояния <> КодСостояния_202_Accepted()) Тогда
		
		Возврат Новый ФиксированнаяСтруктура(Результат);
	КонецЕсли;
	
	Состояние = XMLСтрока(РезультатКоманды.ДесериализованноеЗначение.Получить("status"));
	Если Состояние = "pending" Тогда
		Состояние = "Ожидает";
	Иначе
		Состояние = "Активирован";
	КонецЕсли;
	
	Результат.Состояние = Состояние;
	
	Возврат Новый ФиксированнаяСтруктура(Результат);
	
КонецФункции

#КонецОбласти

#Область ЗадачиРаспознавания

Функция ПолучитьИдентификаторыЗаданий(ТокенАвторизации) Экспорт
	
	КомандаСервиса = НоваяКомандаСервиса();
	КомандаСервиса.URLЗапроса = БазовыйURL() + "/users/" + ТокенАвторизации + "/tasks";
	
	Возврат ВыполнитьКомандуСервиса(КомандаСервиса);
	
КонецФункции

#КонецОбласти

#Область ОбратнаяСвязь

Функция ПередатьОбратнуюСвязь(ИдентификаторРезультата, Данные) Экспорт
	
	Для Каждого КлючЗначение Из Данные Цикл
		КлючЗначение.Значение.Вставить("protocol_version", РаспознаваниеДокументов.ВерсияПротоколаОбмена());
	КонецЦикла;
	
	КомандаСервиса = НоваяКомандаСервиса();
	КомандаСервиса.URLЗапроса = БазовыйURL() + "/ocr/feedback/" + ИдентификаторРезультата;
	КомандаСервиса.Метод = "POST";
	КомандаСервиса.Заголовки.Вставить("Content-Type", "application/json");
	КомандаСервиса.Заголовки.Вставить("Charset", "utf-8");
	КомандаСервиса.Данные = РаспознаваниеДокументовHTTP.ОбъектВJson(Данные);
	
	Возврат ВыполнитьКомандуСервиса(КомандаСервиса);
	
КонецФункции

#КонецОбласти
#КонецОбласти

#Область Переопределения

Функция БазовыйURL() Экспорт
	
	Возврат РаспознаваниеДокументовSDK.АдресСервисаРаспознавания() + "/api/v1";
	
КонецФункции

Функция НомерМодели()
	
	Возврат РаспознаваниеДокументовSDK.НомерМодели();
	
КонецФункции

#КонецОбласти

#Область КодыСостояний

Функция КодСостояния_200_ОК()
	
	Возврат 200;
	
КонецФункции

Функция КодСостояния_202_Accepted()
	
	Возврат 202;
	
КонецФункции

Функция КодСостояния_300_Redirection()
	
	Возврат 300;
	
КонецФункции

Функция КодСостояния_400_BadRequest()
	
	Возврат 400;
	
КонецФункции

Функция КодСостояния_402_PaymentRequired()
	
	Возврат 402;
	
КонецФункции

Функция КодСостояния_500_InternalServerError()
	
	Возврат 500;
	
КонецФункции

#КонецОбласти

#Область Исполнение

Функция НоваяКомандаСервиса()

	Результат = Новый Структура;
	Результат.Вставить("URLЗапроса", "");
	Результат.Вставить("Метод", "GET");
	Результат.Вставить("Заголовки", Новый Соответствие);
	Результат.Вставить("Данные", Неопределено);
	Результат.Вставить("ОсталосьПопыток", 3);
	
	Возврат Результат;
	
КонецФункции

// Выполнение команды сервиса.
//
// Параметры:
//   КомандаСервиса - Структура - параметры вызова или имя команды.
// Возврат
//   Соответствие - возвращаемые данные сервиса.
//
Функция ВыполнитьКомандуСервиса(КомандаСервиса)
	
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыАвторизации = РаспознаваниеДокументов.ТекущиеПараметрыАвторизации();
	УстановитьПривилегированныйРежим(Ложь);
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(КомандаСервиса.URLЗапроса);
	
	ПортОбычногоСоединения = 80;
	ПортЗащищенногоСоединения = 443;
	
	Если СтруктураURI.Порт = Неопределено Тогда
		Если СтруктураURI.Схема = "https" Тогда
			СтруктураURI.Порт = ПортЗащищенногоСоединения;
		Иначе
			СтруктураURI.Порт = ПортОбычногоСоединения;
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураURI.Порт = ПортЗащищенногоСоединения Тогда
		ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(, Новый СертификатыУдостоверяющихЦентровОС);
	Иначе
		ЗащищенноеСоединение = Неопределено;
	КонецЕсли;
	
	ИнтернетПрокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси(СтруктураURI.Схема);
	КонецЕсли;
	
	HTTPСоединение = Новый HTTPСоединение(
		СтруктураURI.Хост,
		СтруктураURI.Порт, , ,
		ИнтернетПрокси,
		60,
		ЗащищенноеСоединение);
	HTTPЗапрос = Новый HTTPЗапрос(СтруктураURI.ПутьНаСервере);
	
	// Установка заголовков запроса.
	Если КомандаСервиса.Свойство("Заголовки") Тогда
		Для Каждого ЭлементКоллекции Из КомандаСервиса.Заголовки Цикл
			HTTPЗапрос.Заголовки.Вставить(ЭлементКоллекции.Ключ, ЭлементКоллекции.Значение);
		КонецЦикла;
	КонецЕсли;
	HTTPЗапрос.Заголовки.Вставить("X-Auth-Token", ПараметрыАвторизации.ТокенДоступа);
	
	Если КомандаСервиса.Свойство("Данные") Тогда
		Если ТипЗнч(КомандаСервиса.Данные) = Тип("ДвоичныеДанные") Тогда
			HTTPЗапрос.УстановитьТелоИзДвоичныхДанных(КомандаСервиса.Данные);
		Иначе
			HTTPЗапрос.УстановитьТелоИзСтроки(
				КомандаСервиса.Данные,
				КодировкаТекста.UTF8,
				ИспользованиеByteOrderMark.НеИспользовать);
		КонецЕсли;
	КонецЕсли;
	
	Попытка
		Результат = HTTPСоединение.ВызватьHTTPМетод(КомандаСервиса.Метод, HTTPЗапрос);
	Исключение
		// Запрос не дошел до HTTP-Сервера
		
		Если КомандаСервиса.ОсталосьПопыток > 0 Тогда
			РаспознаваниеДокументовHTTP.Приостановить(5);
			
			КомандаСервиса.ОсталосьПопыток = КомандаСервиса.ОсталосьПопыток - 1;
			Возврат ВыполнитьКомандуСервиса(КомандаСервиса);
		КонецЕсли;
		
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'По запросу URL: %1 произошла сетевая ошибка
			           |Описание ошибки:
			           |%2'"),
			КомандаСервиса.URLЗапроса,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			РаспознаваниеДокументов.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка, , ,
			ТекстОшибки);
		Возврат Неопределено;
	КонецПопытки;
	
	Если КомандаСервиса.ОсталосьПопыток > 0
		И Результат.КодСостояния >= КодСостояния_500_InternalServerError() Тогда
		
		РаспознаваниеДокументовHTTP.Приостановить(5);
		
		КомандаСервиса.ОсталосьПопыток = КомандаСервиса.ОсталосьПопыток - 1;
		Возврат ВыполнитьКомандуСервиса(КомандаСервиса);
	КонецЕсли;
	
	Если (Результат.КодСостояния <  КодСостояния_200_ОК()
	 Или Результат.КодСостояния >= КодСостояния_300_Redirection())
	   И Результат.КодСостояния <> КодСостояния_402_PaymentRequired() Тогда
		
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'По запросу URL: %1 получен код состояния %2.
			           |Метод: %3
			           |Тело запроса:
			           |%4
			           |Описание ошибки:
			           |%5'"),
			КомандаСервиса.URLЗапроса,
			Результат.КодСостояния,
			КомандаСервиса.Метод,
			МаскированныйРезультат(Лев(КомандаСервиса.Данные, 800)),
			МаскированныйРезультат(Лев(Результат.ПолучитьТелоКакСтроку(), 800)));
		
		ЗаписьЖурналаРегистрации(
			РаспознаваниеДокументов.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка, , ,
			ТекстОшибки);
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураРезультата = СтруктураОтветаСервиса(Результат);
	
	Возврат СтруктураРезультата;
	
КонецФункции

Функция СтруктураОтветаСервиса(Результат)
	
	ТелоОтветаНаЗапрос = Результат.ПолучитьТелоКакСтроку();
	ДесериализованноеЗначение = ПрочитатьТелоЗапросаJSON(ТелоОтветаНаЗапрос);
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Ответ", ТелоОтветаНаЗапрос);
	СтруктураРезультата.Вставить("КодСостояния", Результат.КодСостояния);
	СтруктураРезультата.Вставить("ДесериализованноеЗначение", ДесериализованноеЗначение);
	
	Возврат СтруктураРезультата;
	
КонецФункции

// Функция десериализует строку формата JSON, полученную от сервера, и возвращает результат этого действия
//
// Параметры:
//  СтрокаВФорматеJSON - Строка - Строка в формате JSON
//
// Возвращаемое значение:
//  Произвольный - десериализованное значение чтения JSON
//
Функция ПрочитатьТелоЗапросаJSON(СтрокаВФорматеJSON)
	
	ДесериализованноеЗначение = Неопределено;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	Попытка
		ЧтениеJSON.УстановитьСтроку(СтрокаВФорматеJSON);
		// Строка может быть не в формате JSON (например при ошибке)
		ДесериализованноеЗначение = ПрочитатьJSON(ЧтениеJSON, Истина, "create_time");
	Исключение
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Невозможно прочитать тело запроса JSON:
			           |Читаемый объект(первые 150 символов):
			           |%1
			           |Описание ошибки:
			           |%2""'"),
			Лев(Строка(СтрокаВФорматеJSON), 150),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			РаспознаваниеДокументов.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка, , ,
			ТекстОшибки);
	КонецПопытки;
	ЧтениеJSON.Закрыть();
	
	Возврат ДесериализованноеЗначение;
	
КонецФункции

Функция МаскированныйРезультат(Данные) Экспорт
	
	Данные = ВырезатьМаскированныйФрагмент(Данные, """login""");
	Данные = ВырезатьМаскированныйФрагмент(Данные, """password""");
	Данные = ВырезатьМаскированныйФрагмент(Данные, """client_id""");
	Данные = ВырезатьМаскированныйФрагмент(Данные, """ticket""");
	
	Возврат Данные;
	
КонецФункции

Функция ВырезатьМаскированныйФрагмент(Данные, Фрагмент)
	
	НачальнаяПозиция = СтрНайти(Данные, Фрагмент); // >>"<<key": "value"
	
	Если НачальнаяПозиция > 0 Тогда 
		
		НачальнаяПозиция = СтрНайти(Данные, """", , НачальнаяПозиция + 1); // "key>>"<<: "value"
		НачальнаяПозиция = СтрНайти(Данные, """", , НачальнаяПозиция + 1); // "key": >>"<<value"
		
		НовыеДанные = Лев(Данные, НачальнаяПозиция) + "********";
		
		КонечнаяПозиция = СтрНайти(Данные, """", , НачальнаяПозиция + 1); // "key": "value>>"<<
		Если КонечнаяПозиция > 0 Тогда
			НовыеДанные = НовыеДанные + Сред(Данные, КонечнаяПозиция);
		КонецЕсли;
		
		Данные = НовыеДанные;
		
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

Функция ИзвлечьДанныеКартинки(Знач ДанныеКартинки) Экспорт
	
	ДанныеКартинки = СтрЗаменить(ДанныеКартинки, "data:image/png;base64,", "");
	ДанныеКартинки = СтрЗаменить(ДанныеКартинки, "data:image/jpeg;base64,", "");
	
	Возврат Новый ХранилищеЗначения(Base64Значение(ДанныеКартинки));
	
КонецФункции

#КонецОбласти

#КонецОбласти
