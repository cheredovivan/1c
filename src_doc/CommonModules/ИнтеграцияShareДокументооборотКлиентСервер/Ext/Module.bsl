// Интеграция "1С:Share" Клиент-Сервер (Документооборот)

#Область ПрограммныйИнтерфейс

// Шаблоны для загрузки документа.
// 
// Возвращаемое значение:
//  Структура - Шаблоны для загрузки документа:
// 	 * ВидыДокументов - Массив из СправочникСсылка.ВидыДокументов 
// 	 * Шаблоны - Массив из СправочникСсылка.ШаблоныДокументов
// 	 * ШаблонДокумента - СправочникСсылка.ШаблоныДокументов
// 	 * ТематикаДокумента - СправочникСсылка.ТематикиДокументов
// 	 * ЗаписатьКэш - Булево - Истина, если нужно записать шаблон в РегистрыСведений.Сервис1СShareКэшВидовДокументов.
// 	 * НайденВКэше - Булево - Истина, если шаблон найден в РегистрСведений.Сервис1СShareКэшВидовДокументов.
// 	 * НаименованиеВидаДокумента - Строка - Наименование вида документа, по которому мы подбирали шаблон.
//
Функция ШаблоныДляЗагрузкиДокумента() Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("ВидыДокументов", Новый Массив);
	Структура.Вставить("Шаблоны", Новый Массив);
	Структура.Вставить("ШаблонДокумента", ПредопределенноеЗначение("Справочник.ШаблоныДокументов.ПустаяСсылка"));
	Структура.Вставить("ТематикаДокумента", ПредопределенноеЗначение("Справочник.ТематикиДокументов.ПустаяСсылка"));
	Структура.Вставить("ЗаписатьКэш", Истина);
	Структура.Вставить("НаименованиеВидаДокумента", "");
	Структура.Вставить("НайденВКэше", Ложь);
	
	Возврат Структура;
	
КонецФункции	

// Получает видимость команд сервиса 1С:Share.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ТекущийВидДокумента - Структура:
//   * ЯвляетсяДоговором - Булево
//   * ЯвляетсяИсходящейКорреспонденцией - Булево
//   * ВестиУчетСторон - Булево
//   * ВестиУчетПоОрганизациям - Булево
// 
// Возвращаемое значение:
//  Структура:
//   * ИспользоватьСервис1CShare - Булево - Истина, если есть права на использование сервиса
//   * Видимость - Булево - Истина, если команда видима
//
Функция ВидимостьКомандСервиса1CShare(Форма, ТекущийВидДокумента) Экспорт
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ИспользоватьСервис1CShare", Ложь);
	СтруктураРезультата.Вставить("Видимость", Ложь);
	
	Если Не (Форма.ИмяФормы = "Справочник.ДокументыПредприятия.Форма.ФормаСпискаСПапками" Или
		Форма.ИмяФормы = "Справочник.ДокументыПредприятия.Форма.ФормаЭлемента" Или
			Форма.ИмяФормы = "РегистрСведений.МоиДокументы.Форма.ФормаМоиДокументы") Тогда
			Возврат СтруктураРезультата;
	КонецЕсли;	
	
	СтруктураРезультата.ИспользоватьСервис1CShare = Форма.ИспользоватьСервис1CShare;
	СтруктураРезультата.Видимость = ТекущийВидДокумента.ВестиУчетПоОрганизациям = Истина
		Или ТекущийВидДокумента.ВестиУчетСторон = Истина;
	
	Возврат СтруктураРезультата;
	
КонецФункции

// Текст письма из "1С:Share".
// 
// Возвращаемое значение:
//  Строка - служебный текст, который означает, что письмо было сформировано функционалом библиотеки "1С:Share"
//
Функция ТекстПисьмаИз1СShare() Экспорт
	
	Возврат "1С:Share sent a link";
	
КонецФункции

// Ссылка на инструкцию сервиса на ИТС.
// 
// Возвращаемое значение:
//  Строка
//
Функция СсылкаНаИнструкциюСервисаВБЭД() Экспорт
	
	Возврат "https://its.1c.ru/db/updinfo#content:1910:hdoc:issogl2_3.5_%D0%B2%D0%BE%D0%B7%D0%BC%D0%BE%D0%B6%D0%BD%D0%BE%D1%81%D1%82%D1%8C_%D0%B4%D0%B5%D0%BB%D0%B8%D1%82%D1%8C%D1%81%D1%8F_%D0%B4%D0%BE%D0%BA%D1%83%D0%BC%D0%B5%D0%BD%D1%82%D0%B0%D0%BC%D0%B8_%D1%87%D0%B5%D1%80%D0%B5%D0%B7_%D1%81%D0%B5%D1%80%D0%B2%D0%B8%D1%81_1%D1%81_share";
	
КонецФункции

// Добавляет параметр в ссылку, согласно которому ссылка открывается в браузере
// 
// Параметры:
//  СсылкаДляСкачивания - Строка
// 
// Возвращаемое значение:
//  Строка
//
Функция СсылкаДляБраузера(Знач СсылкаДляСкачивания) Экспорт
	
	Если СтрНайти(СсылкаДляСкачивания, УсловиеОткрытияСсылкиВБраузере()) <> 0 Тогда
		Возврат СсылкаДляСкачивания;
	КонецЕсли;	 	
	
	Если СтрНайти(СсылкаДляСкачивания, "?") <> 0 Тогда
		Возврат СсылкаДляСкачивания + УсловиеОткрытияСсылкиВБраузере("&");
	КонецЕсли;	
	
	Возврат СсылкаДляСкачивания + УсловиеОткрытияСсылкиВБраузере("?");
	
КонецФункции 

// Строка с параметром, наличие которого означает, что к ссылке нет доступа
// 
// Возвращаемое значение:
//  Строка
//
Функция УсловиеОшибкиПравДоступа() Экспорт
	
	Возврат "&1сShareAccessError=True";
	 
КонецФункции

// Имя параметра, наличие которого означает, что к ссылке нет доступа
// 
// Возвращаемое значение:
//  Строка
//
Функция ПараметрОшибкиПравДоступа() Экспорт
	
	Возврат "1сShareAccessError";
	 
КонецФункции			

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Строка с параметром, наличие которой означает, что ссылка должна быть открыта не в базе 1с, а в интернет-браузере.
// 
// Параметры:
//  Префикс - Строка - "?" или "&" для строки параметров ссылки
// 
// Возвращаемое значение:
//  Строка
//
Функция УсловиеОткрытияСсылкиВБраузере(Префикс = "")
	
	Возврат Префикс + "1сShareWebLink=True";
	
КонецФункции

#КонецОбласти
