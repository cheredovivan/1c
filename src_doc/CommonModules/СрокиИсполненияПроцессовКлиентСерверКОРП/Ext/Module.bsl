
////////////////////////////////////////////////////////////////////////////////
// Сроки исполнения процессов клиент сервер КОРП: содержит процедуры и функции по работе
// со сроками процессов в редакциях КОРП/ДГУ.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает пути и строки, замыкающие цикл по таблице Предшественники.
//
// Параметры:
//  Предшественники - Массив - таблица предшественников.
//   * Структура
//      ** Предшественник - УникальныйИдентификатор - идентификатор предшественника
//      ** Последователь - УникальныйИдентификатор - идентификатор последователя
//
// Возвращаемое значение:
//  Пути - Массив - массив путей. Каждый путь представляет собой массив идентификаторов.
//  СтрокиПредшественниковЗамыкающиеЦикл - Массив - строки из таблицы Предшественники,
//                                         которые замыкают пути.
//
Функция ПараметрыПутей(Предшественники) Экспорт
	
	Последователи = Новый Соответствие;
	ВсеПредшественники = Новый Массив;
	Для Каждого СтрокаПредшественник Из Предшественники Цикл
		Если Последователи[СтрокаПредшественник.Предшественник] = Неопределено Тогда
			Последователи[СтрокаПредшественник.Предшественник] = Новый Массив;
			ВсеПредшественники.Добавить(СтрокаПредшественник.Предшественник);
		КонецЕсли;
		Последователи[СтрокаПредшественник.Предшественник].Добавить(СтрокаПредшественник.Последователь);
	КонецЦикла;
	ВсеПредшественники = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ВсеПредшественники);
	Для Каждого Предшественник Из ВсеПредшественники Цикл
		Последователи[Предшественник] = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Последователи[Предшественник]);
	КонецЦикла;
	
	Пути = Новый Массив;
	СтрокиПредшественниковЗамыкающиеЦикл = Новый Массив;
	
	ТаблицаПутей = Новый Массив;
	
	ТекущийЭтап = УникальныйИдентификаторПустой();
	
	Путь = Новый Массив;
	Путь.Добавить(ТекущийЭтап);
	
	ЭтапыПути = Новый Соответствие;
	ЭтапыПути[ТекущийЭтап] = Истина;
	
	ТаблицаПутей.Добавить(СтрокаТаблицыПутей(ТекущийЭтап, Путь, ЭтапыПути));
	
	ПутиВОбработке = Новый Массив;
	ПутиВОбработке.Добавить(0);
	
	Пока ПутиВОбработке.Количество() > 0 Цикл
		
		СтрокаТаблицы = ТаблицаПутей[ПутиВОбработке[0]];
		
		ПоследователиЭтапа = Последователи[СтрокаТаблицы.ТекущийЭтапПути];
		
		Если ПоследователиЭтапа = Неопределено
			Или ПоследователиЭтапа.Количество() = 0 Тогда
			
			ПутиВОбработке.Удалить(0);
			Продолжить;
		КонецЕсли;
		
		Индекс = 1;
		МаксимальныйИндекс = ПоследователиЭтапа.Количество() - 1;
		
		Пока Индекс <= МаксимальныйИндекс Цикл
			
			Последователь = ПоследователиЭтапа[Индекс];
			
			Индекс = Индекс + 1;
			
			Если СтрокаТаблицы.ЭтапыПути[Последователь] = Истина Тогда
				
				СтрокиПредшественниковЗамыкающиеЦикл.Добавить(
					СтрокаТаблицыПредшественниковЗамыкающихЦиклы(
					СтрокаТаблицы.ТекущийЭтапПути,
					Последователь));
				
				Продолжить;
			КонецЕсли;
			
			НоваяСтрокаТаблицы = СтрокаТаблицыПутей(
				Последователь,
				ОбщегоНазначенияДокументооборотКлиентСервер.ПростаяКопияМассива(СтрокаТаблицы.Путь),
				КопияПростогоСоответствия(СтрокаТаблицы.ЭтапыПути));
			
			НоваяСтрокаТаблицы.Путь.Добавить(Последователь);
			НоваяСтрокаТаблицы.ЭтапыПути[Последователь] = Истина;
			
			ТаблицаПутей.Добавить(НоваяСтрокаТаблицы);
			
			ПутиВОбработке.Вставить(1, ТаблицаПутей.Количество() - 1);
			
		КонецЦикла;
		
		Последователь = ПоследователиЭтапа[0];
		Если СтрокаТаблицы.ЭтапыПути[Последователь] = Истина Тогда
			
			СтрокиПредшественниковЗамыкающиеЦикл.Добавить(
				СтрокаТаблицыПредшественниковЗамыкающихЦиклы(
				СтрокаТаблицы.ТекущийЭтапПути,
				Последователь));
			
			ПутиВОбработке.Удалить(0);
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицы.ТекущийЭтапПути = Последователь;
		СтрокаТаблицы.Путь.Добавить(Последователь);
		СтрокаТаблицы.ЭтапыПути[Последователь] = Истина;
		
	КонецЦикла;
	
	Пути = Новый Массив;
	Для Каждого СтрТаблицы Из ТаблицаПутей Цикл
		Пути.Добавить(СтрТаблицы.Путь);
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("Пути", Пути);
	Результат.Вставить("СтрокиПредшественниковЗамыкающиеЦикл", СтрокиПредшественниковЗамыкающиеЦикл);
	
	Возврат Результат;
	
КонецФункции

// Возвращает параметры для определения длительности комплексного процесса.
// 
// Возвращаемое значение:
//  Структура:
//   * Ссылка - СправочникСсылка.ШаблоныКомплексныхБизнесПроцессов
//            - Неопределено
//            - БизнесПроцессСсылка.КомплексныйПроцесс - ссылка на процесс/шаблон. 
//   * ДанныеСхемы - см. СхемыПроцессовКлиентСервер.СтруктураДанныхСхемыПроцесса
//   * СрокиЭлементов - см. РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураСроковЭлементовСхемы
//  
Функция НовыеПараметрыДляОпределенияДлительностиКомплексногоПроцессаСоСхемой() Экспорт
	
	ПараметрыДляОпределенияДлительности = Новый Структура;
	ПараметрыДляОпределенияДлительности.Вставить("Ссылка", Неопределено);
	
	ПараметрыДляОпределенияДлительности.Вставить("ДанныеСхемы",
		СхемыПроцессовКлиентСервер.СтруктураДанныхСхемыПроцесса());
	
	ПараметрыДляОпределенияДлительности.Вставить("СрокиЭлементов",
		РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураСроковЭлементовСхемы());
	
	Возврат ПараметрыДляОпределенияДлительности;
	
КонецФункции

// Возвращает структуру параметров для расчета сроков процесса.
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессов.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессов.
//
// Возвращаемое значение:
//  Структура:
//   * ДатаОтсчета - Дата
//   * РеквизитТаблицаСИзмененнымСроком - Строка
//   * ИндексСтроки - Строка
//   * ТекущаяИтерация - Число
//   * ЗаполнятьСрокПроцессаТолькоПриПревышении - Булево
//   * Смещение - Число, Дата -
//
Функция НовыеПараметрыДляРасчетаСроков() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ДатаОтсчета", Дата(1,1,1));
	Параметры.Вставить("РеквизитТаблицаСИзмененнымСроком", "");
	Параметры.Вставить("ИндексСтроки", 0);
	Параметры.Вставить("ТекущаяИтерация", 1);
	Параметры.Вставить("ЗаполнятьСрокПроцессаТолькоПриПревышении", Ложь);
	Параметры.Вставить("Смещение", 0);
	
	Возврат Параметры;
	
КонецФункции

#Область КарточкиПроцессовИШаблонов

// Возвращает смещение даты отсчета определенное в карточке процесса/шаблона.
// Смещение определяется по настройке отложенного старта.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона.
//
// Возвращаемое значение:
//  Дата - возвращается для карточки процесса.
//  Число - смещение в секундах для шаблона процессов или комплексного процесса без даты отсчета.
//
Функция СмещенияДатыОтсчетаВКарточке(Форма) Экспорт
	
	Если ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(Форма.Объект.Ссылка) Тогда
		// Смещение в секундах
		Смещение = Форма.ОтложенныйСтартДни * 86400 + Форма.ОтложенныйСтартЧасы * 3600;
	Иначе
		
		Смещение = Дата(1,1,1);
		
		Если ЗначениеЗаполнено(Форма.НастройкаСтарта) Тогда
			Смещение = Форма.НастройкаСтарта.ДатаОтложенногоСтарта;
			Смещение = Смещение - Секунда(Смещение);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Форма.ДатаОтсчетаДляРасчетаСроков) Тогда
			
			#Если Сервер Тогда
				ТекущаяДата = ТекущаяДатаСеанса();
			#Иначе
				ТекущаяДата = ТекущаяДата();
			#КонецЕсли
			
			ТекущаяДата = ТекущаяДата - Секунда(ТекущаяДата);
			
			Если ТекущаяДата < Смещение Тогда
				Смещение = Смещение - ТекущаяДата;
			Иначе
				Смещение = 0;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Смещение;
	
КонецФункции

// Возвращает необходимость расчета относительного срока.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - текущая форма.
// 
// Возвращаемое значение:
//  Булево - Рассчитывать относительный срок
//
Функция РассчитыватьОтносительныйСрок(Форма) Экспорт
	
	Если ТипЗнч(Форма.Объект.Ссылка) = Тип("СправочникСсылка.ШаблоныКомплексныхБизнесПроцессов") Тогда
		Если Не ЗначениеЗаполнено(Форма.Объект.ВладелецШаблона) Или Форма.ЭтоДействиеШаблонаКомплексногоПроцесса
			Или (Форма.ЭтоДействиеКомплексногоПроцессаПоШаблону И Не Форма.КомплексныйПроцессСтартован) Тогда
			Возврат Истина;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Форма.Объект.Ссылка) = Тип("БизнесПроцессСсылка.КомплексныйПроцесс") Тогда
		Если Не Форма.Объект.Стартован И Не Форма.Объект.ПометкаУдаления И ЗначениеЗаполнено(Форма.Объект.Шаблон)
			И Не ЗначениеЗаполнено(Форма.Объект.ВедущаяЗадача) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;

	Возврат Ложь;
	
КонецФункции

// Возвращает дату отсчета для текущего действия по форме комплексного процесса/шаблона.
//
// Параметры:
//  Форма - УправляемаяФормы - форма комплексного процесса/шаблона.
//
// Возвращаемое значение:
//  Дата - дата отсчета для действия.
//
Функция ДатаОтсчетаДействияКомплексногоПроцессаВФорме(Форма) Экспорт
	
	ДатаОтсчета = Дата(1,1,1);
	
	Если РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.ЭтоФормаПроцессаСоСхемой(Форма) Тогда
		
		ИмяЭлемента = СхемыПроцессовКлиентСервер.ИмяТекущегоЭлементаСхемыПоЕеДаннымВФорме(Форма);
		Если Не ЗначениеЗаполнено(ИмяЭлемента) Тогда
			Возврат ДатаОтсчета;
		КонецЕсли;
		
		ТипыПредшествующихЭлементов = Новый Соответствие();
		
		ТипыПредшествующихЭлементов[
			ПредопределенноеЗначение("Перечисление.ТипыЭлементовСхемПроцессов.Действие")] = Истина;
			
		ТипыПредшествующихЭлементов[
			ПредопределенноеЗначение("Перечисление.ТипыЭлементовСхемПроцессов.ВложенныйПроцесс")] = Истина;
		
		ИменаПредшествующихЭлементов = 
			СхемыПроцессовКлиентСервер.ИменаПредшествующихЭлементовСхемыВФорме(
			ИмяЭлемента, ТипыПредшествующихЭлементов, Форма);
		
		СрокиЭлементов = 
			РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СрокиЭлементовСхемыВФорме(Форма);
		
		Для Каждого ИмяПредшественника Из ИменаПредшествующихЭлементов Цикл
			
			СрокИсполнения = 
				РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СрокИсполненияЭлемента(
				СрокиЭлементов, ИмяПредшественника);
			
			ДатаОтсчета = Макс(ДатаОтсчета, СрокИсполнения.СрокИсполненияПроцесса);
			
		КонецЦикла;
		
	Иначе
	
		Если Форма.Элементы.Этапы.ТекущиеДанные = Неопределено Тогда
			Возврат ДатаОтсчета;
		КонецЕсли;
		
		Этапы = Форма.Объект.Этапы;
		ПредшественникиЭтапов = Форма.Объект.ПредшественникиЭтапов;
		ИдентификаторЭтапа = Форма.Элементы.Этапы.ТекущиеДанные.ИдентификаторЭтапа;
		
		// Определим строки таблицы предшественников, замыкающие циклы.
		ТаблицаДляОпределенияПараметровПутей = Новый Массив;
		Для Каждого СтрПредшественникЭтапов Из ПредшественникиЭтапов Цикл
			СтрокаТаблицыПредшественниковЗамыкающихЦиклы = СтрокаТаблицыПредшественниковЗамыкающихЦиклы(
				СтрПредшественникЭтапов.ИдентификаторПредшественника,
				СтрПредшественникЭтапов.ИдентификаторПоследователя);
				
			ТаблицаДляОпределенияПараметровПутей.Добавить(СтрокаТаблицыПредшественниковЗамыкающихЦиклы);
		КонецЦикла;
		ПараметрыПутей = ПараметрыПутей(ТаблицаДляОпределенияПараметровПутей);
		СтрокиПредшественниковЗамыкающиеЦикл = ПараметрыПутей.СтрокиПредшественниковЗамыкающиеЦикл;
		
		Отбор = Новый Структура;
		Отбор.Вставить("ИдентификаторПоследователя", ИдентификаторЭтапа);
		
		НайденныеПредшественники = ПредшественникиЭтапов.НайтиСтроки(Отбор);
		
		// Определим дату отсчета по предшественниками
		Для Каждого СтрПредшественник Из НайденныеПредшественники Цикл
			
			// Если текущая строка замыкает цикл, то пропускаем ее.
			ЭтоСтрокаЗамыкающаяЦикл = Ложь;
			ИндексПоследнейСтроки = СтрокиПредшественниковЗамыкающиеЦикл.Количество() - 1;
			Для ИндексСтроки = 0 По ИндексПоследнейСтроки Цикл
				СтрокаПредшественниковЗамыкающиеЦикл = СтрокиПредшественниковЗамыкающиеЦикл[ИндексСтроки];
				Если СтрПредшественник.ИдентификаторПоследователя = 
						СтрокаПредшественниковЗамыкающиеЦикл.Последователь
					И СтрПредшественник.ИдентификаторПредшественника = 
						СтрокаПредшественниковЗамыкающиеЦикл.Предшественник Тогда
					
					ЭтоСтрокаЗамыкающаяЦикл = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ЭтоСтрокаЗамыкающаяЦикл Тогда
				СтрокиПредшественниковЗамыкающиеЦикл.Удалить(ИндексСтроки);
				Продолжить;
			КонецЕсли;
			
			Отбор = Новый Структура;
			Отбор.Вставить("ИдентификаторЭтапа", СтрПредшественник.ИдентификаторПредшественника);
			НайденныеСроки = Этапы.НайтиСтроки(Отбор);
			
			Для Каждого НайденныйСрок Из НайденныеСроки Цикл
				ДатаОтсчета = Макс(ДатаОтсчета, НайденныйСрок.СрокИсполненияПроцесса);
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Определим дату отсчета по карточке комплексного процесса,
	// если нет предшественников.
	Если Не ЗначениеЗаполнено(ДатаОтсчета) Тогда
	
		ДатаОтсчета = Форма.ДатаОтсчетаДляРасчетаСроков;
		
		Смещение = СмещенияДатыОтсчетаВКарточке(Форма);
		Если ЗначениеЗаполнено(Смещение) Тогда
			Если ТипЗнч(Смещение) = Тип("Число") Тогда
				ДатаОтсчета = ДатаОтсчета + Смещение;
			Иначе
				ДатаОтсчета = Макс(ДатаОтсчета, Смещение);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДатаОтсчета;
	
КонецФункции

// Возвращает дату отсчета для нового действия комплексного процесса/шаблона.
//
// Параметры:
//  Форма - УправляемаяФормы - форма комплексного процесса/шаблона.
//
// Возвращаемое значение:
//  Дата - дата отсчета для действия.
//
Функция ДатаОтсчетаНовогоДействияКомплексногоПроцессаВФорме(Форма) Экспорт
	
	ВариантыМаршуртизацииЗадач = РаботаСБизнесПроцессамиКлиентСервер.ВариантыМаршуртизацииЗадач();
	
	ДатаОтсчета = Дата(1,1,1);
	
	Если Форма.Объект.ВариантМаршрутизации = ВариантыМаршуртизацииЗадач.Последовательно Тогда
		ДатаОтсчета = Форма.Объект.СрокИсполненияПроцесса;
	ИначеЕсли Форма.Объект.ВариантМаршрутизации = ВариантыМаршуртизацииЗадач.Смешанно Тогда
		
		ИндексПоследнегоЭтапа = Форма.Объект.Этапы.Количество() - 1;
		
		Если ИндексПоследнегоЭтапа >= 0 Тогда
			ДатаОтсчета = Форма.Объект.Этапы[ИндексПоследнегоЭтапа].СрокИсполненияПроцесса;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаОтсчета) Тогда
		
		Смещение = СрокиИсполненияПроцессовКлиентСерверКОРП.СмещенияДатыОтсчетаВКарточке(Форма);
		
		Если ТипЗнч(Смещение) = Тип("Число") Тогда
			Смещение = Форма.ДатаОтсчетаДляРасчетаСроков + Смещение;
		КонецЕсли;
		
		ДатаОтсчета = Макс(Смещение, Форма.ДатаОтсчетаДляРасчетаСроков);
	КонецЕсли;
	
	Возврат ДатаОтсчета;
	
КонецФункции

// Заполняет представление сроков исполнения в форме комплексного процесса/шаблона.
//
// Параметры:
//  Форма - Управляемая Форма - формы комплексного процесса/шаблона.
//
Процедура ЗаполнитьПредставлениеСроковИсполненияВФормеКомплексногоПроцесса(Форма) Экспорт
	
	ПоказатьТочныеСроки = ПоказатьТочныеСрокиВКомплексномПроцессе(Форма);
	ПоказатьОтносительныеСроки = ПоказатьОтносительныеСрокиВКомплексномПроцессе(Форма);
	
	Если РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.ЭтоФормаПроцессаСоСхемой(Форма) Тогда
		
		РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.ОбновитьПредставлениеСхемыВФорме(Форма);
		
	Иначе
		Для Каждого Этап Из Форма.Объект.Этапы Цикл
			
			Дата = Дата(1,1,1);
			Дни = 0;
			Часы = 0;
			Минуты = 0;
			
			Если ПоказатьОтносительныеСроки Тогда
				Дни = Этап.СрокИсполненияПроцессаДни;
				Часы = Этап.СрокИсполненияПроцессаЧасы;
				Минуты = Этап.СрокИсполненияПроцессаМинуты;
			КонецЕсли;
			
			Если ПоказатьТочныеСроки Тогда
				Дата = Этап.СрокИсполненияПроцесса;
			КонецЕсли;
			
			СрокиИсполненияПроцессовКлиентСервер.ЗаполнитьПредставлениеСрокаИсполненияПроцесса(
				Этап.СрокИсполненияПроцессаПредставление,
				Дата, Дни, Часы, Минуты,
				Форма.ИспользоватьДатуИВремяВСрокахЗадач);
			
		КонецЦикла;
	КонецЕсли;
	
	Дата = Дата(1,1,1);
	Дни = 0;
	Часы = 0;
	Минуты = 0;
	
	Если ПоказатьОтносительныеСроки Тогда
		Дни = Форма.СрокИсполненияПроцессаДни;
		Часы = Форма.СрокИсполненияПроцессаЧасы;
		Минуты = Форма.СрокИсполненияПроцессаМинуты;
	КонецЕсли;
	
	Если ПоказатьТочныеСроки Тогда
		Дата = Форма.Объект.СрокИсполненияПроцесса;
	КонецЕсли;
	
	ПредставлениеСрокаИсполнения = "";
	СрокиИсполненияПроцессовКлиентСервер.ЗаполнитьПредставлениеСрокаИсполненияПроцесса(
		ПредставлениеСрокаИсполнения,
		Дата, Дни, Часы, Минуты,
		Форма.ИспользоватьДатуИВремяВСрокахЗадач);
		
	Если КомандаРасчетаСрокаДоступна(Форма) Тогда
		
		СтрокиПредставленияСрока = Новый Массив;
		СтрокиПредставленияСрока.Добавить(ПредставлениеСрокаИсполнения);
		СтрокиПредставленияСрока.Добавить(" (");
		
		СтрокиПредставленияСрока.Добавить(
			Новый ФорматированнаяСтрока(НСтр("ru = 'рассчитать'"),,,, "Рассчитать"));
		
		СтрокиПредставленияСрока.Добавить(")");
		
		ПредставлениеСрокаИсполнения = Новый ФорматированнаяСтрока(СтрокиПредставленияСрока);
		
	КонецЕсли;
	
	Форма.СрокИсполненияПроцессаПредставление = ПредставлениеСрокаИсполнения;
	
КонецПроцедуры

// Возвращает доступность команды расчета срока для карточки
// комплексного процесса/шаблона.
//
// Параметры:
//  форма - ФормаКлиентскогоПриложения - карточка комплексного процесса/шаблона.
//
// Возвращаемое значение:
//  Булево
//
Функция КомандаРасчетаСрокаДоступна(Форма) Экспорт
	
	Если ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(Форма.Объект.Ссылка) Тогда
		Доступность = Не ЗначениеЗаполнено(Форма.Объект.ВладелецШаблона)
			И Не ЗначениеЗаполнено(Форма.ДатаОтсчетаДляРасчетаСроков);
	Иначе
		Доступность = Не Форма.Объект.Стартован
			И Форма.Объект.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияБизнесПроцессов.Активен")
			И Не Форма.Объект.ПометкаУдаления
			И ЗначениеЗаполнено(Форма.Объект.Шаблон)
			И Не ЗначениеЗаполнено(Форма.Объект.ВедущаяЗадача)
			И Не ЗначениеЗаполнено(Форма.ДатаОтсчетаДляРасчетаСроков);
	КонецЕсли;
	
	Возврат Доступность;
	
КонецФункции

// Определяет необходимость отображения относительных сроков
// в карточке комплексного процесса.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Карточка процесса/служебного шаблона.
//
// Возвращаемое значение:
//  Булево - Возвращает Истину, если нужно показывать относительные сроки, иначе - Ложь.
//
Функция ПоказатьОтносительныеСрокиВКомплексномПроцессе(Форма) Экспорт
	
	ПоказатьОтносительныеСроки = Ложь;
	
	Если ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(Форма.Объект.Ссылка) Тогда
		ПоказатьОтносительныеСроки = Не ЗначениеЗаполнено(Форма.Объект.ВладелецШаблона)
			Или Форма.ЭтоДействиеШаблонаКомплексногоПроцесса
			Или (Форма.ЭтоДействиеКомплексногоПроцессаПоШаблону И Не Форма.КомплексныйПроцессСтартован);
	Иначе
		ПоказатьОтносительныеСроки = Не ЗначениеЗаполнено(Форма.Объект.ВедущаяЗадача)
			И ЗначениеЗаполнено(Форма.Объект.Шаблон)
			И Не Форма.Объект.ПометкаУдаления
			И Не Форма.Объект.Стартован
			И Форма.Объект.Состояние = 
				ПредопределенноеЗначение("Перечисление.СостоянияБизнесПроцессов.Активен");
	КонецЕсли;
	
	Возврат ПоказатьОтносительныеСроки;
	
КонецФункции

// Определяет необходимость отображения точных сроков
// в карточке комплексного процесса.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Карточка процесса/служебного шаблона.
//
// Возвращаемое значение:
//  Булево - Возвращает Истину, если нужно показывать точные сроки, иначе - Ложь.
//
Функция ПоказатьТочныеСрокиВКомплексномПроцессе(Форма) Экспорт
	
	Возврат ЗначениеЗаполнено(Форма.ДатаОтсчетаДляРасчетаСроков);
	
КонецФункции

// Обновляет признак истекшего срока для процесса
//
// Параметры:
//  СрокИсполнения - Дата - срок исполнения процесса.
//  ДатаЗаверешения - Дата - дата завершения процесса.
//  СрокИсполненияИстек - Булево - признак истекшего срока.
//
Процедура ОбновитьПризнакИстекшегоСрокаПроцесса(
	СрокИсполнения, ДатаЗаверешения, СрокИсполненияИстек) Экспорт
	
	СрокИсполненияИстек = Ложь;
	Если Не ЗначениеЗаполнено(СрокИсполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ДатаИсполнения = ТекущаяДата();
	
	Если ЗначениеЗаполнено(ДатаЗаверешения) Тогда
		ДатаИсполнения = ДатаЗаверешения;
	КонецЕсли;
	
	ДатаИсполнения = ДатаИсполнения - Секунда(ДатаИсполнения);
	
	Если СрокИсполнения < ДатаИсполнения Тогда
		СрокИсполненияИстек = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ФоновыйРасчетСроков

// Возвращает структуру параметров для фонового расчета срока.
// 
// Возвращаемое значение:
//  Структура:
//   * РассчитатьОтносительныйСрок - Булево
//   * РассчитатьТочныйСрок - Булево
//   * ДатаОтсчетаДляРасчетаСроков - Дата
//   * Смещение - Дата
//   * ПараметрыДляОпределенияДлительности - см. СрокиИсполненияПроцессовКлиентСерверКОРП.НовыеПараметрыДляОпределенияДлительностиКомплексногоПроцессаСоСхемой
//   * СтруктураДляРасчета - см. НоваяСтруктураДляРасчетаСрокаКомплексногоПроцессаПоСхеме
//   * ПараметрыДляРасчета - см. СрокиИсполненияПроцессовКлиентСерверКОРП.НовыеПараметрыДляРасчетаСроков
//   * ИдентификаторыИзмененныхДействий - Массив Из УникальныйИдентификатор
//   									- Неопределено							
//   * АдресХранилищаСРассчитаннымиСроками - Строка
//
Функция НовыйПараметрыФоновогоРасчетаСрокаПоСхеме() Экспорт

	Структура = Новый Структура;
	Структура.Вставить("РассчитатьОтносительныйСрок", Ложь);
	Структура.Вставить("РассчитатьТочныйСрок", Ложь);
	Структура.Вставить("ДатаОтсчетаДляРасчетаСроков", Дата(1, 1, 1));
	Структура.Вставить("Смещение", Дата(1, 1, 1));
	Структура.Вставить("ПараметрыДляОпределенияДлительности",
		НовыеПараметрыДляОпределенияДлительностиКомплексногоПроцессаСоСхемой());
	Структура.Вставить("СтруктураДляРасчета", НоваяСтруктураДляРасчетаСрокаКомплексногоПроцессаПоСхеме());
	Структура.Вставить("ПараметрыДляРасчета", НовыеПараметрыДляРасчетаСроков());
	Структура.Вставить("ИдентификаторыИзмененныхДействий", Неопределено);
	Структура.Вставить("АдресХранилищаСРассчитаннымиСроками", "");
	
	Возврат Структура;

КонецФункции

// Возвращает структуру для расчета срока комплексного процесса/шаблона.
// 
// Возвращаемое значение:
//  Структура - реквизиты и табличные части процесса/шаблона:
//   * Ссылка - БизнесПроцессСсылка.КомплексныйПроцесс
//            -  СправочникСсылка.ШаблоныКомплексныхБизнесПроцессов
//            -  Неопределено
//   * ИдентификаторСсылки - УникальныйИдентификатор
//   * Схема - СправочникСсылка.СхемыПроцессов
//   * ДанныеСхемы - см. СхемыПроцессовКлиентСервер.СтруктураДанныхСхемыПроцесса
//   * ДанныеПараметровСхемы - см. Справочники.ПараметрыСхемДляКомплексныхПроцессов.СтруктураДанныхПараметровСхемы
//   * ПроцессыЭлементов - см. РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураПроцессовЭлементов
//   * ТекущиеЭлементы - см. РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураТекущихЭлементовСхемы
//   * ПройденныеЭлементы - см. РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураПройденныхЭлементовСхемы
//   * КэшДанныхДействий - см. РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураКэшаДанныхДействий
//   * ПроцессыДляПрерывания - см. РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураПроцессовЭлементовДляПрерывания
//   * СрокиЭлементов - см. РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураСроковЭлементовСхемы
//   * СрокИсполненияПроцесса - Дата
//
Функция НоваяСтруктураДляРасчетаСрокаКомплексногоПроцессаПоСхеме() Экспорт
	
	РеквизитыДляРасчета = Новый Структура;
	
	РеквизитыДляРасчета.Вставить("Ссылка", Неопределено);
	РеквизитыДляРасчета.Вставить("ИдентификаторСсылки", УникальныйИдентификаторПустой());
	РеквизитыДляРасчета.Вставить("СрокИсполненияПроцесса", Дата(1,1,1));
	
	РеквизитыДляРасчета.Вставить("Схема", ПредопределенноеЗначение("Справочник.СхемыПроцессов.ПустаяСсылка"));
	РеквизитыДляРасчета.Вставить("ДанныеСхемы", СхемыПроцессовКлиентСервер.СтруктураДанныхСхемыПроцесса());
	
	РеквизитыДляРасчета.Вставить("ДанныеПараметровСхемы",
		РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураДанныхПараметровСхемы());
		
	РеквизитыДляРасчета.Вставить("ПроцессыЭлементов",
		РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураПроцессовЭлементов());
		
	РеквизитыДляРасчета.Вставить("ТекущиеЭлементы",
		РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураТекущихЭлементовСхемы());
		
	РеквизитыДляРасчета.Вставить("ПройденныеЭлементы", 
		РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураПройденныхЭлементовСхемы());
		
	РеквизитыДляРасчета.Вставить("КэшДанныхДействий",
		РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураКэшаДанныхДействий());	
	
	РеквизитыДляРасчета.Вставить("ПроцессыДляПрерывания",
		РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураПроцессовЭлементовДляПрерывания());
	
	РеквизитыДляРасчета.Вставить("СрокиЭлементов",
		РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураСроковЭлементовСхемы());
		
	Возврат РеквизитыДляРасчета;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает копию простого соответствия, не обходя его элементы рекурсивно, в отличие от ОбщегоНазначенияКлиентСервер.
// СкопироватьСоответствие().
//
// Параметры:
//   Массив - Массив - копируемый массив.
//
// Возвращаемое значение:
//   Массив - копия исходного массива.
//
Функция КопияПростогоСоответствия(Соответствие)
	
	Копия = Новый Соответствие;
	Для Каждого КлючЗначение Из Соответствие Цикл
		Копия[КлючЗначение.Ключ] = КлючЗначение.Значение;
	КонецЦикла;
	
	Возврат Копия;
	
КонецФункции

// Возвращает строку таблицы путей.
//
// Параметры:
//  ТекущийЭтапПути - УникальныйИдентификатор - идентификатор текущего этапа пути.
//  Путь - массив - путь из идентификаторов.
//  ЭтапыПути - соответствие - идентификаторы путей, используется для быстрого поиска пути.
//     * Ключ - идентификатор этапа пути; Значение - всегда Истина.
//
// Возвращаемое значение:
//  Структура
//   * ТекущийЭтапПути - УникальныйИдентификатор - идентификатор текущего этапа пути.
//   * Путь - Массив - путь из идентификаторов.
//   * ЭтапыПути - соответствие
//      ** Ключ - идентификатор этапа пути; Значение - всегда Истина.
//
Функция СтрокаТаблицыПутей(ТекущийЭтапПути, Путь, ЭтапыПути)
	
	СтрТаблицы = Новый Структура;
	СтрТаблицы.Вставить("ТекущийЭтапПути", ТекущийЭтапПути);
	СтрТаблицы.Вставить("ЭтапыПути", ЭтапыПути);
	СтрТаблицы.Вставить("Путь", Путь);
	
	Возврат СтрТаблицы;
	
КонецФункции

// Возвращает строку таблицы предшественников замыкающих цикл.
//
// Параметры:
//  Предшественник - УникальныйИдентификатор - идентификатор предшественника.
//  Последователь - УникальныйИдентификатор - идентификатор последователя.
//
// Возвращаемое значение:
//  Структура
//   * Предшественник - УникальныйИдентификатор - идентификатор предшественника.
//   * Последователь - УникальныйИдентификатор - идентификатор последователя.
//
Функция СтрокаТаблицыПредшественниковЗамыкающихЦиклы(Предшественник, Последователь)
	
	СтрТаблицы = Новый Структура;
	СтрТаблицы.Вставить("Предшественник", Предшественник);
	СтрТаблицы.Вставить("Последователь", Последователь);
	
	Возврат СтрТаблицы;
	
КонецФункции


#Область КарточкиПроцессовИШаблонов

// Возвращает структуру параметров для настройки доступности
// элемента управления сроком.
//
// Используется для переопределения одноименной функции в модуле СрокиИсполненияПроцессовКлиентСервер.
// Вместо текущей следует использовать функцию из модуля СрокиИсполненияПроцессовКлиентСервер.
//
// Возвращаемое значение:
//  Структура
//   * ДоступностьПоШаблону - Булево - настройка доступности реквизита по шаблону процесса.
//   * ВестиУчетПереносаСроков - Булево - значение одноименной настройки программы.
//   * ЗаявкаНаПереносСрока - БизнесПроцессСсылка.РешениеВопросовВыполненияЗадач - заявка на перенос срока.
//
Функция ПараметрыДоступностиЭлементаУправления() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ДоступностьПоШаблону");
	Параметры.Вставить("ВестиУчетПереносаСроков");
	Параметры.Вставить("ЗаявкаНаПереносСрока");
	
	Возврат Параметры;
	
КонецФункции

// Настраивает возможность изменения значения элемента управления сроком.
//
// Используется для переопределения одноименной процедуры в модуле СрокиИсполненияПроцессовКлиентСервер.
// Вместо текущей следует использовать процедуру из модуля СрокиИсполненияПроцессовКлиентСервер.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма процесса.
//  ЭлементУправленияСроком - ПолеФормы - поле формы управления сроком.
//  РеквизитПредставлениеСрока - Строка - реквизит содержащий представление срока.
//  ПараметрыДоступности - Структура - см. функцию ПараметрыДоступностиЭлементаУправления.
//
Процедура НастроитьЭлементУправленияСроком(Форма,
	ЭлементУправленияСроком,
	РеквизитПредставлениеСрока,
	ПараметрыДоступности) Экспорт
	
	ДоступностьПоШаблону = ПараметрыДоступности.ДоступностьПоШаблону;
	ВестиУчетПереносаСроков = ПараметрыДоступности.ВестиУчетПереносаСроков;
	ЗаявкаНаПереносСрока = ПараметрыДоступности.ЗаявкаНаПереносСрока;
	
	// Устанавливаем значения по умолчанию (включаем доступность всех полей)
	ЭлементУправленияСроком.ТолькоПросмотр = Ложь;
	
	// Устанвавливаем заначение в зависимости от параметров формы
	Если Форма.ТолькоПросмотр Тогда
		// Если форма процесса недоступна для изменения, тогда не даем менять сроки.
		ЭлементУправленияСроком.ТолькоПросмотр = Истина;
	ИначеЕсли ВестиУчетПереносаСроков = Истина И ЗначениеЗаполнено(ЗаявкаНаПереносСрока) Тогда
		// Если включен учет переносов сроков и есть заявка на перенос срока, тогда изменение разрешено.
	ИначеЕсли ДоступностьПоШаблону = Ложь И ЗначениеЗаполнено(РеквизитПредставлениеСрока) Тогда
		// Если изменение запрещено по шаблону и сроки заполнены, тогда запрещаем менять.
		ЭлементУправленияСроком.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти