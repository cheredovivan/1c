
#Область ПрограммныйИнтерфейс

// Получает мобильные номера телефонов контактных лиц, контрагента.
// Рекомендуется передавать номера мобильных телефонов контактного лица и контрагента, где они заполнены.
// Мобильный телефон используется для отправки сообщения через whatsapp на конкретный номер.
//
// Параметры:
//  Контрагент           - ОпределяемыйТип.КонтрагентБЭД - ссылка на элемент справочника контрагенты.
//  КонтактнаяИнформация - Массив из см. ИнтеграцияShareКлиентСервер.НовыйКонтактнаяИнформация - контактная информация.
//
Процедура КонтактныеЛицаСНомеромТелефона(Знач Контрагент, КонтактнаяИнформация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КонтактныеЛицаКонтактнаяИнформация.НомерТелефонаБезКодов,
		|	КонтактныеЛицаКонтактнаяИнформация.НомерТелефона,
		|	КонтактныеЛицаКонтактнаяИнформация.Ссылка КАК ВладелецКонтакта
		|ИЗ
		|	Справочник.КонтактныеЛица.КонтактнаяИнформация КАК КонтактныеЛицаКонтактнаяИнформация
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица КАК КонтактныеЛица
		|		ПО КонтактныеЛицаКонтактнаяИнформация.Ссылка = КонтактныеЛица.Ссылка
		|ГДЕ
		|	КонтактныеЛица.Владелец = &Владелец
		|	И КонтактныеЛицаКонтактнаяИнформация.НомерТелефона <> """"";
	
	Запрос.УстановитьПараметр("Владелец", Контрагент);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Контакт = ИнтеграцияShareКлиентСервер.НовыйКонтактнаяИнформация();
		ЗаполнитьЗначенияСвойств(Контакт, Выборка);
		Контакт.КодСтраны = СтрЗаменить(Выборка.НомерТелефона, Выборка.НомерТелефонаБезКодов, "");
		КонтактнаяИнформация.Добавить(Контакт);
	КонецЦикла;
	
КонецПроцедуры

// Определяет состав команд для использования сервиса 1С:Share.
//
// Параметры:
//  СоставКоманд - См. ИнтеграцияShare.НовыйСоставКомандShare
//
Процедура ПриОпределенииСоставаКомандShare(СоставКоманд) Экспорт
	
	СоставКоманд.Исходящие.Добавить("Справочник.ДокументыПредприятия");
	СоставКоманд.Входящие.Добавить("Справочник.ДокументыПредприятия");
	
КонецПроцедуры

// Переопределяет гиперссылку на инструкцию по работе с сервисом 1С:Share для прикладной базы.
// Например, url-страница сайта ИТС с описанием сервиса как в нем делиться документом и загружать.
//
// Параметры:
//  НавигационнаяСсылка - Строка - гиперссылка на инструкцию по работе с сервисом 1С:Share для прикладной базы.
//
Процедура ПриОпределенииГиперссылкиНаИнструкциюПоРаботеССервисом(НавигационнаяСсылка) Экспорт
	
	НавигационнаяСсылка = ИнтеграцияShareДокументооборотКлиентСервер.СсылкаНаИнструкциюСервисаВБЭД();
	
КонецПроцедуры

// Переопределяет файлы документа для выбора к публикации в сервис 1С:Share.
// При стандартной обработке файлы заполняются из присоединенных файлов к документу и не помеченные на удаление,
// используется метод БСП РаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту.
//
// Параметры:
//  ДанныеФайлов - Массив из см. ИнтеграцияShare.НовыеДанныеПрисоединенногоФайла - данные файлов для выбора к публикации. Выходной параметр.
//  СсылкаНаДокумент - ДокументСсылка, СправочникСсылка - ссылка на публикуемый документ.
//  СтандартнаяОбработка - Булево - признак стандартной обработки. По умолчанию Истина.
//
Процедура ПриОпределенииФайловДокументаДляВыбораКПубликации(ДанныеФайлов, Знач СсылкаНаДокумент, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Файлы.Ссылка КАК СсылкаНаФайл,
		|	Файлы.ТекущаяВерсияРасширение КАК Расширение,
		|	Файлы.Наименование КАК Наименование
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СлужебныеФайлыДокументов КАК СлужебныеФайлыДокументов
		|		ПО Файлы.Ссылка = СлужебныеФайлыДокументов.Файл
		|ГДЕ
		|	Файлы.ВладелецФайла = &ВладелецФайла
		|	И НЕ Файлы.ПометкаУдаления
		|	И СлужебныеФайлыДокументов.Файл ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ВладелецФайла", СсылкаНаДокумент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДанныеФайла = ИнтеграцияShare.НовыеДанныеПрисоединенногоФайла();
		ЗаполнитьЗначенияСвойств(ДанныеФайла, Выборка);
		ДанныеФайлов.Добавить(ДанныеФайла);
	КонецЦикла;
	
КонецПроцедуры

// Переопределяет данные публикуемого документа в сервис 1С:Share.
//
// Параметры:
//  ДанныеТранспортногоКонтейнера - ДеревоЗначений из см. ДеревоЭлектронногоДокументаБЭД.ДеревоЭлектронногоДокумента -
//                                  данные для заполнения транспортного контейнера. Строится по макету. 
//                                 см. Обработка.СервисShare.Макет.ТранспортныйКонтейнер. Выходной параметр.
//  СсылкаНаДокумент - ДокументСсылка, СправочникСсылка - электронный документ к публикации.
//
Процедура ПриОпределенииДанныхПубликуемогоДокумента(ДанныеТранспортногоКонтейнера, Знач СсылкаНаДокумент) Экспорт
	
КонецПроцедуры

// Переопределяет представление публикуемого документа.
// При необходимости можно переопределить параметры из свойства
// "ПараметрыПубликуемогоДокументаДляВизуализации.СвойстваПредставления" при использовании стандартной обработки.
//
// Параметры:
//  ОписаниеПредставленияДокумента - Структура из См. РаботаСФайламиБЭД.НовоеОписаниеФайла - представление документа
//      в поддерживаемом формате визуализации. См. ИнтеграцияShareКлиентСервер.ДоступныеРасширенияФайловВизуализации
//      Имя файла используется в качестве имени отправляемого документа, которое отображается пользователю при просмотре
//      файла визуализации. Выходной параметр.
//  ПараметрыПубликуемогоДокументаДляВизуализации - См. ИнтеграцияShare.НовыеПараметрыПубликуемогоДокументаДляВизуализации -
//                            параметры публикуемого документа. 
//  СтандартнаяОбработка    - Булево - признак стандартной обработки. По умолчанию Истина.
//
Процедура ПриОпределенииВизуализацииПубликуемогоДокумента(ОписаниеПредставленияДокумента,
	Знач ПараметрыПубликуемогоДокументаДляВизуализации, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// Заполняет настройки формирования электронного документа.
//
// Параметры:
//  СсылкаНаДокумент      - СправочникСсылка, ДокументСсылка, - документ или справочник объекта учета.
//  НастройкиФормирования - Структура - настройки источника для формирования обмена электронными документами.
//                                      Выходной параметр:
//    * ТипДокумента       - Произвольный - значение элемента структуры возвращаемой
//                           см. ОбменСКонтрагентами.ТипыДокументов.
//    * Формат             - Строка - формат значения элемента структуры возвращаемой см. ОбменСКонтрагентами.ФорматыДокументов().
//    * Направление        - Произвольный - значение элемента структуры возвращаемой.
//                           см. ОбменСКонтрагентами.НаправленияДокументов.
//    * Организация        - ОпределяемыйТип.Организация - организация документа.
//    * Контрагент         - ОпределяемыйТип.КонтрагентБЭД - контрагент документа.
//    * ДоговорКонтрагента - ОпределяемыйТип.ДоговорСКонтрагентомЭДО - договор контрагента.
//
Процедура ЗаполнитьНастройкиФормированияДокумента(СсылкаНаДокумент, НастройкиФормирования) Экспорт
	
	ИнтеграцияShareДокументооборот.ЗаполнитьНастройкиФормированияДокумента(СсылкаНаДокумент, НастройкиФормирования);
	
КонецПроцедуры

// Переопределяет настройки загрузки документа из сервиса 1С:Share.
//
// По умолчанию заполняются настройки документа в зависимости от состояния документа и указанных типов объектов из 
// метода ИнтеграцияЭДО.ТипыОбъектовУчетаПоТипуЭлектронногоДокумента. При отличии способов отражения учета от этого
// метода можно переопределить способы отражения и их отображения для пользователя.
// Так же в методе можно выполнять проверки на наличие доступа к учетным документам или данным у пользователя,
// и любые другие проверки перед загрузкой документа. В случае наличия ошибок необходимо установить Отказ = Истина,
// заполнить ТекстОшибки.
//
// 1)Пример для открытия документа, когда документу уже ранее был загружен:
//
//	ПараметрОтраженияВУчете = ПараметрыОтраженияВУчете.Добавить();
//	ПараметрОтраженияВУчете.СсылкаНаОбъектУчетаВБазе      = СсылкаНаОбъектУчета;
//	ПараметрОтраженияВУчете.ПредставлениеДокумента        = Строка(СсылкаНаОбъектУчета);
//	ПараметрОтраженияВУчете.ПредставлениеРезультатаПоиска = СтрШаблон(НСтр("ru = 'Открыть %1'",
//		ОбщегоНазначения.КодОсновногоЯзыка()), ПараметрыОтраженияВУчете.ПредставлениеДокумента);
//	ПараметрОтраженияВУчете.КартинкаРезультатаПоиска      = БиблиотекаКартинок.ПерейтиПоНавигационнойСсылке;
// 
// 2)Пример для загрузки документа, когда только один тип объекта:
//
//	ПараметрОтраженияВУчете = ПараметрыОтраженияВУчете.Добавить();
//	ПараметрОтраженияВУчете.СсылкаНаОбъектУчетаВБазе = Документы.ЗаказПоставщику.ПустаяСсылка();
//	ПараметрОтраженияВУчете.СпособОтраженияВУчете    = "ЗаказПоставщику";
//	ПараметрОтраженияВУчете.ПредставлениеДокумента   = СтрШаблон(НСтр("ru = '%1 от %2'",
//		ОбщегоНазначения.КодОсновногоЯзыка()), ОписаниеЭлектронногоДокумента.ПредставлениеДокументаОтправителя,
//		ОписаниеЭлектронногоДокумента.НаименованиеОтправителя);
//	ПараметрОтраженияВУчете.ПредставлениеРезультатаПоиска = СтрШаблон(НСтр("ru = 'Загрузить %1'",
//		ОбщегоНазначения.КодОсновногоЯзыка()), ПредставлениеДокумента);
//	ПараметрОтраженияВУчете.КартинкаРезультатаПоиска      = БиблиотекаКартинок.ЗагрузитьShare;
//
// 3)Пример для загрузки документа, когда несколько типов объекта:
//
//	ПараметрОтраженияВУчете = ПараметрыОтраженияВУчете.Добавить();
//	ПараметрОтраженияВУчете.СсылкаНаОбъектУчетаВБазе = Документы.ЗаказПоставщику.ПустаяСсылка();
//	ПараметрОтраженияВУчете.СпособОтраженияВУчете    = "ЗаказПоставщику";
//	ПараметрОтраженияВУчете.ПредставлениеДокумента   = НСтр("ru = 'Заказ поставщику'",
//		ОбщегоНазначения.КодОсновногоЯзыка());
//	ПараметрОтраженияВУчете.КартинкаРезультатаПоиска = БиблиотекаКартинок.ЗагрузитьShare;
//	ПараметрОтраженияВУчете.ПредставлениеВПодменюЗагрузить = СтрШаблон(НСтр("ru = 'Как %1'",
//		ОбщегоНазначения.КодОсновногоЯзыка()), ПредставлениеДокумента);
//	ПараметрОтраженияВУчете.ПредставлениеРезультатаПоиска = СтрШаблон(НСтр("ru = 'Загрузить как %1'",
//		ОбщегоНазначения.КодОсновногоЯзыка()), ПредставлениеДокумента).
//
// Параметры:
//  НастройкиЗагрузки - Структура Из см. ИнтеграцияShare.НовыеНастройкиЗагрузкиДокумента
//  ОписаниеЭлектронногоДокумента - см. ИнтеграцияShare.НовоеКраткоеОписаниеЭлектронногоДокумента.
//  Отказ - Булево - признак наличия ошибки или закрытия доступа к загрузке документа, например, при отсутствие прав.
//  ТекстОшибки - Строка - текст сообщения для отображения пользователю.
Процедура ПриОпределенииНастроекЗагрузкиДокумента(НастройкиЗагрузки, Знач ОписаниеЭлектронногоДокумента,
	Отказ = Ложь, ТекстОшибки = "") Экспорт
	
	ИнтеграцияShareДокументооборот.ПриОпределенииНастроекЗагрузкиДокумента(НастройкиЗагрузки, 
		ОписаниеЭлектронногоДокумента, Отказ, ТекстОшибки);

КонецПроцедуры

#КонецОбласти

