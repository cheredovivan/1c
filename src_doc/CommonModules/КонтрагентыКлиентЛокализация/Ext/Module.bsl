
#Область ПрограммныйИнтерфейс

#Область ФормаЭлемента

// Вызывается из "ПриОткрытии" формы элемента справочника Контрагенты
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
Процедура ПриОткрытии_ФормаЭлемента(Форма, Отказ) Экспорт

// Локализация
	Объект = Форма.Объект;
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыПриОткрытии = ОбменСКонтрагентамиКлиент.ПараметрыПриОткрытии();
	ПараметрыПриОткрытии.Форма                            = Форма;
	ПараметрыПриОткрытии.МестоРазмещенияКоманд            = Форма.Элементы.КомандыЭДО;
	ПараметрыПриОткрытии.ЕстьОбработчикОбновитьКомандыЭДО = Истина;
	
	ОбменСКонтрагентамиКлиент.ПриОткрытии(ПараметрыПриОткрытии);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОткрытииКонтрагент(Форма);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами

	// ИнтернетПоддержкаПользователей.СПАРКРиски
	СПАРКРискиКлиент.ПриОткрытии(Форма, Объект);
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
// Локализация Конец

КонецПроцедуры

// Вызывается из "ОбработкаОповещения" формы элемента справочника Контрагенты
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Объект, Неопределено - заполняется в том случае, если это форма элемента справочника, а не форма документа.
//  ИмяСобытия - Строка
//  Параметр - Произвольный
//  Источник - Произвольный
Процедура ОбработкаОповещения_ФормаЭлемента(Форма, Объект, ИмяСобытия, Параметр, Источник) Экспорт

// Локализация
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыОповещенияЭДО = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаСправочника();
	ПараметрыОповещенияЭДО.Форма                            = Форма;
	ПараметрыОповещенияЭДО.МестоРазмещенияКоманд            = Форма.Элементы.КомандыЭДО;
	ПараметрыОповещенияЭДО.ЕстьОбработчикОбновитьКомандыЭДО = Истина;
	
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаСправочника(ИмяСобытия, Параметр, Источник, ПараметрыОповещенияЭДО);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентаВСправочнике(Форма);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами

	// ИнтернетПоддержкаПользователей.СПАРКРиски
	СПАРКРискиКлиент.ОбработкаОповещения(Форма, Объект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
// Локализация Конец

КонецПроцедуры

// Вызывается из "ПередЗаписью" формы элемента справочника Контрагенты
// 
// Параметры:
//  Объект - ДанныеФормыСтруктура
//  ТекстСообщения - Строка
//  Отказ - Булево
//  ПараметрыЗаписи - Структура
Процедура ПередЗаписью_ФормаЭлемента(Объект, ТекстСообщения, Отказ, ПараметрыЗаписи) Экспорт

// Локализация
	ИНН = Объект.ИНН;
	КПП = Объект.КПП;
	КодПоОКПО = Объект.КодПоОКПО;
	ЮрФизЛицо = Объект.ЮрФизЛицо;
	
	Если ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо") Тогда 
		
		Если ЗначениеЗаполнено(ИНН) 
			И Не РегламентированныеДанныеКлиентСерверЛокализация.ИННСоответствуетТребованиям(
				ИНН, Истина, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "ИНН",, Отказ);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КПП) 
			И Не РегламентированныеДанныеКлиентСерверЛокализация.КППСоответствуетТребованиям(
				КПП, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "КПП",, Отказ);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КодПоОКПО) И Не 
			РегламентированныеДанныеКлиентСерверЛокализация.КодПоОКПОСоответствуетТребованиям(
				КодПоОКПО, Истина, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "КодПоОКПО",, Отказ);
		КонецЕсли;
		
	ИначеЕсли ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель") Тогда 
		
		Если ЗначениеЗаполнено(ИНН) 
			И Не РегламентированныеДанныеКлиентСерверЛокализация.ИННСоответствуетТребованиям(
				ИНН, Ложь, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "ИНН",, Отказ);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КодПоОКПО) 
			И Не РегламентированныеДанныеКлиентСерверЛокализация.КодПоОКПОСоответствуетТребованиям(
				КодПоОКПО, Ложь, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "КодПоОКПО",, Отказ);
		КонецЕсли;
		
	ИначеЕсли ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо") Тогда
		
		Если ЗначениеЗаполнено(ИНН) 
			И Не РегламентированныеДанныеКлиентСерверЛокализация.ИННСоответствуетТребованиям(
				ИНН, Ложь, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "ИНН",, Отказ);
		КонецЕсли;
		 
	КонецЕсли;
// Локализация Конец
	
КонецПроцедуры

// Вызывается из "ИННПриИзменении" формы элемента справочника Контрагенты
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Объект - СправочникОбъект.Контрагенты
Процедура ИННПриИзменении(Форма, Объект) Экспорт

// Локализация	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентаВСправочнике(Форма);
	//ЗаполнитьПоДаннымЕдиныхГосРеестровНаКлиенте(Объект.ИНН, Истина);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
	

	// ИнтернетПоддержкаПользователей.СПАРКРиски
	Форма.ИндексыСПАРКРиски = Неопределено;
	ОбновитьОтображениеИндексыСПАРК(Форма, Объект);
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
// Локализация Конец	

КонецПроцедуры	

// Вызывается из "КПППриИзменении" формы элемента справочника Контрагенты
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
Процедура КПППриИзменении(Форма) Экспорт
	
// Локализация	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентаВСправочнике(Форма);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
// Локализация Конец	
	
КонецПроцедуры	

// Вызывается из "ЮрФизЛицоПриИзменении" формы элемента справочника Контрагенты
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Объект - СправочникОбъект.Контрагенты
//  ЭтоЮридическоеЛицо - Булево
Процедура ЮрФизЛицоПриИзменении(Форма, Объект, ЭтоЮридическоеЛицо) Экспорт

// Локализация
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	Форма.РеквизитыПроверкиКонтрагентов.ЭтоЮридическоеЛицо = ЭтоЮридическоеЛицо;
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентаВСправочнике(Форма);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	Форма.ИндексыСПАРКРиски = Неопределено;
	ОбновитьОтображениеИндексыСПАРК(Форма, Объект);
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски	
// Локализация Конец

КонецПроцедуры	


#КонецОбласти

#Область ФормаСписка

// Вызывается из "ПриОткрытии" формы списка справочника Контрагенты
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
Процедура ПриОткрытии_ФормаСписка(Форма, Отказ) Экспорт

// Локализация

	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиКлиент.ПриОткрытии(Форма);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

// Локализация Конец

КонецПроцедуры

// Вызывается из "ОбработкаОповещения" формы списка справочника Контрагенты
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ИмяСобытия - Строка
//  Параметр - Произвольный
//  Источник - Произвольный
Процедура ОбработкаОповещения_ФормаСписка(Форма, ИмяСобытия, Параметр, Источник) Экспорт

// Локализация
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыОповещенияЭДО = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаСписка();
	ПараметрыОповещенияЭДО.Форма = Форма;
	ПараметрыОповещенияЭДО.ИмяДинамическогоСписка = "Список";
	ПараметрыОповещенияЭДО.МестоРазмещенияКоманд = Форма.Элементы.КомандыЭДО;
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаСписка(ИмяСобытия, Параметр, Источник, ПараметрыОповещенияЭДО);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
// Локализация Конец

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обновляет отображение индексов СПАРК
// 
// Параметры:
//  Форма - См. Справочник.Контрагенты.Форма.ФормаЭлемента
//  Объект - См. Справочник.Контрагенты.Форма.ФормаЭлемента.Объект
Процедура ОбновитьОтображениеИндексыСПАРК(Форма, Объект) Экспорт

// Локализация
	ПараметрыОтображения = Новый Структура("ВариантОтображения", "Многострочный");
	СПАРКРискиКлиент.ОтобразитьИндексыСПАРК(
		Форма.ИндексыСПАРКРиски,
		Объект,
		Объект.ИНН, // Искать по ИНН
		ОбщегоНазначенияДокументооборотКлиентСервер.ВидКонтрагентаСпаркПоВидуКонтрагента(Объект.ЮрФизЛицо),
		Форма,
		ПараметрыОтображения);

// Локализация Конец

КонецПроцедуры

#КонецОбласти