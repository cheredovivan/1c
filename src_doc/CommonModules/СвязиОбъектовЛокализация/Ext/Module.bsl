
#Область ПрограммныйИнтерфейс

// См. СвязиОбъектов.ЭтоПодходящийТипСвязи
//
// Параметры:
//   ОбъектСсылка - ЛюбаяСсылка - ссылка на документ основание связи, либо ссылка на вид документа.
//   СвязанныйОбъект - ЛюбаяСсылка - ссылка на документ с которым пытаемся создать связь, либо ссылка на вид документа.
//   ТипСвязи - СправочникСсылка.ТипыСвязей - проверяемый тип связи.
//
// Возвращаемое значение:
//    Булево - Истина, если между проверяемыми документами возможно установить проверяемый тип связи,
//      Ложь в противном случае.
//
Функция ЭтоПодходящийТипСвязи(ОбъектСсылка, СвязанныйОбъект, ТипСвязи) Экспорт
	
	// Локализация
	Если ТипСвязи = Справочники.ТипыСвязей.ПервичноеОбращение
			Или ТипСвязи = Справочники.ТипыСвязей.ПовторноеОбращение
			Или ТипСвязи = Справочники.ТипыСвязей.ОсновноеОбращение
			Или ТипСвязи = Справочники.ТипыСвязей.Дубликат Тогда
		
		ВестиУчетОбращенийГраждан = ПолучитьФункциональнуюОпцию("ВестиУчетОбращенийГраждан");
		
		Если Не ВестиУчетОбращенийГраждан Тогда
			
			Возврат Ложь;
			
		ИначеЕсли ЗначениеЗаполнено(СвязанныйОбъект)
				И ТипЗнч(СвязанныйОбъект) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда
			
			УчитыватьКакОбращениеГраждан = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СвязанныйОбъект,
				"ВидДокумента.ЯвляетсяОбращениемОтГраждан");
			
			Если УчитыватьКакОбращениеГраждан <> Истина Тогда // УчитыватьКакОбращениеГраждан может быть Неопределено.
				Возврат Ложь;
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(СвязанныйОбъект) = Тип("СправочникСсылка.ВидыДокументов") Тогда
			
			УчитыватьКакОбращениеГраждан = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СвязанныйОбъект,
				"ЯвляетсяОбращениемОтГраждан");
			
			Если УчитыватьКакОбращениеГраждан <> Истина Тогда // УчитыватьКакОбращениеГраждан может быть Неопределено.
				Возврат Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	// Локализация Конец
	
	Возврат Истина;
	
КонецФункции

// Обработка проверки заполнения на сервере форма записи.
// 
// Параметры:
//  Отказ - Булево
//  ПроверяемыеРеквизиты - Массив из Строка
//  Форма - ФормаКлиентскогоПриложения
Процедура ОбработкаПроверкиЗаполненияНаСервереФормаЗаписи(Отказ, ПроверяемыеРеквизиты, Форма) Экспорт

// Локализация

	Запись = Форма.Запись; // РегистрСведенийЗапись.СвязиОбъектов

	Если Запись.ТипСвязи = Справочники.ТипыСвязей.ПервичноеОбращение 
		Или Запись.ТипСвязи = Справочники.ТипыСвязей.ПовторноеОбращение
		Или Запись.ТипСвязи = Справочники.ТипыСвязей.ОсновноеОбращение 
		Или Запись.ТипСвязи = Справочники.ТипыСвязей.Дубликат Тогда
			
		Если Не Константы.ВестиУчетОбращенийГраждан.Получить() Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр(
				"ru = 'Указанный тип связи может быть использован только при включенном учете обращений граждан!'"), ,
				"Запись.ТипСвязи", , Отказ);

		ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоВходящийДокумент(Форма.СвязанныйОбъект) Тогда
			
			ВидСвязанногоДокумента = Форма.СвязанныйОбъект.ВидДокумента;
			Если Не ВидСвязанногоДокумента.ЯвляетсяОбращениемОтГраждан Тогда
				ОбщегоНазначения.СообщитьПользователю(
					НСтр("ru = 'Для текущего типа связи может быть указано только обращение гражданина!'"), ,
					"СвязанныйОбъект", , Отказ);
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЕсли;
	
// Локализация Конец

КонецПроцедуры

#КонецОбласти