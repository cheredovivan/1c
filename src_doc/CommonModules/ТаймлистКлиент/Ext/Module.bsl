
#Область ПрограммныйИнтерфейс

// Открывает форму для указания количества спикеров и последующего начала операции расшифровки.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма-владелец.
//  Файлы - СписокЗначений Из СправочникСсылка.Файлы
//        - СправочникСсылка.Файлы
//  Оповещение - ОписаниеОповещения
//
Процедура НачатьРасшифровку(Форма, Файлы, Оповещение) Экспорт
	
	ПараметрыФормы = Новый Структура("ФайлыНаРасшифровку, РежимПолученияРасшифровки", Файлы, Истина);
	
	ОткрытьФорму("ОбщаяФорма.ТаймлистСпикеры", ПараметрыФормы, Форма,,,,
		Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Открывает форму для указания имен спикеров и последующего начала операции получения автопротокола.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма-владелец.
//  Файл - СправочникСсылка.Файлы
//  Оповещение - ОписаниеОповещения
//  ОтборУчастников - СписокЗначений Из СправочникСсылка.Сотрудники, СправочникСсылка.КонтактныеЛица
//
Процедура НачатьПолучениеАвтопротокола(Форма, Файл, Оповещение, ОтборУчастников = Неопределено) Экспорт
	
	ПараметрыФормы = Новый Структура("ФайлНаПолучениеАвтопротокола, РежимПолученияАвтопротокола, ОтборУчастников",
		Файл, Истина, ОтборУчастников);
	
	ОткрытьФорму("ОбщаяФорма.ТаймлистСпикеры", ПараметрыФормы, Форма,,,,
		Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Открывает форму для редактирования расшифровки/автопротокола.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма-владелец.
//  Файл - СправочникСсылка.Файлы
//  Оповещение - ОписаниеОповещения
//  Расшифровка - Строка
//  Автопротокол - Строка
//  КлючИсточника - Строка
//
Процедура ОткрытьРедакторТекстовыхДанных(Форма, Файл, Оповещение, Расшифровка, Автопротокол, КлючИсточника = "") Экспорт
	
	РедактируетДанные = ТаймлистВызовСервера.РедактируетДанные(Файл);
	Если ЗначениеЗаполнено(РедактируетДанные) И РедактируетДанные <> ПользователиКлиент.ТекущийПользователь() Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(НСтр(
			"ru = 'В настоящий момент текст редактирует <%1>. Подождите, пока сотрудник сохранит изменения.'"),
			РедактируетДанные));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Файл", Файл);
	ПараметрыФормы.Вставить("Расшифровка", Расшифровка);
	ПараметрыФормы.Вставить("Автопротокол", Автопротокол);
	ПараметрыФормы.Вставить("КлючИсточника", ?(ЗначениеЗаполнено(КлючИсточника), КлючИсточника,
		ТаймлистКлиентСервер.КлючОтображенияРасшифровкиHTML()));
	
	Если ДелопроизводствоКлиентСервер.ЭтоФормаМероприятия(Форма.ИмяФормы) Тогда
		ОтборУчастников = Новый СписокЗначений;
		Для Каждого СтрокаУчастник Из Форма.Участники Цикл
			Если ЗначениеЗаполнено(СтрокаУчастник.Исполнитель)
				И (ТипЗнч(СтрокаУчастник.Исполнитель) = Тип("СправочникСсылка.Сотрудники")
				Или ТипЗнч(СтрокаУчастник.Исполнитель) = Тип("СправочникСсылка.КонтактныеЛица")) Тогда
				
				ОтборУчастников.Добавить(СтрокаУчастник.Исполнитель);
			КонецЕсли;
		КонецЦикла;
		ПараметрыФормы.Вставить("ОтборУчастников", ОтборУчастников);
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.ТаймлистРедактор", ПараметрыФормы, Форма,,,,
		Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Формирует и открывает документ в редакторе офисных документов.
// 
// Параметры:
//  Автопротокол - Строка - HTML представление.
//
Процедура ОткрытьАвтопротоколДокумент(Автопротокол) Экспорт
	
	АдресРезультата = ТаймлистВызовСервера.АвтопротоколДокументWord(Автопротокол);
	
	ПараметрыСохраненияФайла = ФайловаяСистемаКлиент.ПараметрыСохраненияФайла();
	ПараметрыСохраненияФайла.Диалог.Заголовок = НСтр("ru = 'Сохранить автопротокол в файл'");
	ПараметрыСохраненияФайла.Диалог.Фильтр = "Файл автопротокола (*.docx)|*.docx";
	
	ОбработчикПослеСохраненияФайла = Новый ОписаниеОповещения("ПослеСохраненияАвтопротоколаНаДиск", ЭтотОбъект);
	
	ФайловаяСистемаКлиент.СохранитьФайл(ОбработчикПослеСохраненияФайла, АдресРезультата,, ПараметрыСохраненияФайла);
	
КонецПроцедуры

// Проверяет возможность редактирования текстовых данных.
//
// Параметры:
//  Статус - ПеречислениеСсылка.ТаймлистСтатусы
// 
// Возвращаемое значение:
//  Булево - Истина, если в указанном статусе данные можно редактировать
// 
Функция РедактированиеДанныхРазрешено(Статус) Экспорт
	
	Возврат Статус = ПредопределенноеЗначение("Перечисление.ТаймлистСтатусы.Расшифрован")
		Или Статус = ПредопределенноеЗначение("Перечисление.ТаймлистСтатусы.ПолученАвтопротокол");
	
КонецФункции

#Область ПодборУчастников

// Реализация обработчика "НачалоВыбора" участника.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Элемент - ПолеВвода
//  ДанныеВыбора - СписокЗначений из СправочникСсылка.Сотрудники
//  СтандартнаяОбработка - Булево
//  ОписаниеТаблицыУчастников - см. ОписаниеТаблицыУчастников
//  ТекущийУчастник - СправочникСсылка.Сотрудники
//  ОтборУчастников - СписокЗначений из СправочникСсылка.Сотрудники, СправочникСсылка.КонтактныеЛица
//
Процедура УчастникНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка, ОписаниеТаблицыУчастников,
	ТекущийУчастник, ОтборУчастников) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если Форма.Элементы[ОписаниеТаблицыУчастников.ИмяТаблицыУчастников].ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтборУчастников.Количество() > 0 Тогда
		
		ДанныеВыбораУчастников = Новый СписокЗначений;
		
		Для Каждого Участник Из ОтборУчастников Цикл
			ДанныеВыбораУчастников.Добавить(Участник.Значение, Строка(Участник.Значение));
		КонецЦикла;
		
		ДанныеВыбораУчастников.СортироватьПоПредставлению();
		
		Если ДанныеВыбораУчастников.Количество() > 10 Тогда
			
			ДополнительныеПараметрыОповещения = Новый Структура("Форма, ОписаниеТаблицыУчастников", Форма,
				ОписаниеТаблицыУчастников);
			ОписаниеОповещения = Новый ОписаниеОповещения("УчастникНачалоВыбораЗавершение",
				ЭтотОбъект, ДополнительныеПараметрыОповещения);
			
			ДанныеВыбораУчастников.ПоказатьВыборЭлемента(ОписаниеОповещения, НСтр("ru = 'Выбор участника'"),
				ДанныеВыбораУчастников.НайтиПоЗначению(ТекущийУчастник));
		
		Иначе
			ДанныеВыбора = ДанныеВыбораУчастников;
		КонецЕсли;
		
	Иначе
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РежимРаботыФормы", 1);
		ПараметрыФормы.Вставить("УпрощенныйИнтерфейс", Истина);
		
		ПараметрыФормы.Вставить("ОтображатьСотрудников", Истина);
		ПараметрыФормы.Вставить("ПодменятьПользователейСотрудниками", Истина);
		
		ПараметрыФормы.Вставить("ОтображатьКонтрагентов", Истина);
		ПараметрыФормы.Вставить("ЗаголовокФормы", НСтр("ru = 'Выбор участника'"));
		
		Если ЗначениеЗаполнено(ТекущийУчастник) Тогда
			ПараметрыФормы.Вставить("ВыбранныеАдресаты", ТекущийУчастник);
		КонецЕсли;
		
		ОткрытьФорму("Справочник.АдреснаяКнига.Форма.ФормаСписка",
			ПараметрыФормы,
			Элемент,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

// Реализация обработчика "ОбработкаВыбора" участника.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ВыбранноеЗначение - Произвольный
//  СтандартнаяОбработка - Булево
//  ОписаниеТаблицыУчастников - см. ОписаниеТаблицыУчастников
//
Процедура УчастникОбработкаВыбора(Форма, ВыбранноеЗначение, СтандартнаяОбработка, ОписаниеТаблицыУчастников) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	УчастникЗначение = ПредопределенноеЗначение("Справочник.Сотрудники.ПустаяСсылка");
	ПредставлениеУчастника = "";
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Сотрудники")
		Или ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
		УчастникЗначение = ВыбранноеЗначение;
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("ЭлементСпискаЗначений") Тогда
		УчастникЗначение = ВыбранноеЗначение.Значение;
	Иначе
		ПредставлениеУчастника = ВыбранноеЗначение;
	КонецЕсли;
	
	ОбработатьВыборУчастника(УчастникЗначение, ПредставлениеУчастника, Форма,
		ОписаниеТаблицыУчастников.ИмяТаблицыУчастников,
		ОписаниеТаблицыУчастников.ИмяРеквизитаПредставленияУчастника,
		ОписаниеТаблицыУчастников.ИмяРеквизитаУчастника);
	
КонецПроцедуры

// Реализация обработчика "Автоподбор" участника.
// 
// Параметры:
//  Текст - Строка
//  ДанныеВыбора - СписокЗначений из СправочникСсылка.Сотрудники, СправочникСсылка.КонтактныеЛица
//  СтандартнаяОбработка - Булево
//  ОтборУчастников - СписокЗначений из СправочникСсылка.Сотрудники, СправочникСсылка.КонтактныеЛица
//
Процедура УчастникАвтоподбор(Текст, ДанныеВыбора, СтандартнаяОбработка, ОтборУчастников) Экспорт
	
	Если ЗначениеЗаполнено(Текст) Тогда
		
		ДанныеВыбора = Новый СписокЗначений;
		СтандартнаяОбработка = Ложь;
		
		Если ОтборУчастников.Количество() > 0 Тогда
			
			Для Каждого Сотрудник Из ОтборУчастников Цикл
				Если Текст <> "" И НРег(Лев(Сотрудник, СтрДлина(Текст))) <> НРег(Текст) Тогда
					Продолжить;
				КонецЕсли;
				ДанныеВыбора.Добавить(Сотрудник, Строка(Сотрудник));
			КонецЦикла;
			
		Иначе
			
			ТаймлистВызовСервера.ЗаполнитьДанныеВыбораУчастников(Текст, ДанныеВыбора, Истина);
			
		КонецЕсли;
		
		Если ДанныеВыбора.Количество() = 0 Тогда
			ДанныеВыбора.Добавить(Текст);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Формирует описание таблицы участников.
//
// Возвращаемое значение:
//  Структура:
//   * ИмяТаблицыУчастников - Строка
//   * ИмяРеквизитаПредставленияУчастника - Строка
//   * ИмяРеквизитаУчастника - Строка
//
Функция ОписаниеТаблицыУчастников() Экспорт
	
	ОписаниеТаблицыУчастников = Новый Структура;
	ОписаниеТаблицыУчастников.Вставить("ИмяТаблицыУчастников", "");
	ОписаниеТаблицыУчастников.Вставить("ИмяРеквизитаПредставленияУчастника", "");
	ОписаниеТаблицыУчастников.Вставить("ИмяРеквизитаУчастника", "");
	
	Возврат ОписаниеТаблицыУчастников;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УчастникНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
	ИмяТаблицыУчастников = ДополнительныеПараметры.ОписаниеТаблицыУчастников.ИмяТаблицыУчастников;
	ИмяРеквизитаПредставленияУчастника =
		ДополнительныеПараметры.ОписаниеТаблицыУчастников.ИмяРеквизитаПредставленияУчастника;
	ИмяРеквизитаУчастника = ДополнительныеПараметры.ОписаниеТаблицыУчастников.ИмяРеквизитаУчастника;
	
	ОбработатьВыборУчастника(Результат.Значение, Результат.Значение, Форма, ИмяТаблицыУчастников,
		ИмяРеквизитаПредставленияУчастника, ИмяРеквизитаУчастника);
	
КонецПроцедуры

Процедура ОбработатьВыборУчастника(Участник, ПредставлениеУчастника, Форма, ИмяТаблицыУчастников,
	ИмяРеквизитаПредставленияУчастника, ИмяРеквизитаУчастника)
	
	Если ЗначениеЗаполнено(Участник) Тогда
		ЧастиПредставленияУчастника = СтрРазделить(Строка(Участник), " ");
		Если ЧастиПредставленияУчастника.Количество() < 3 Тогда
			ПредставлениеУчастника = Строка(Участник);
		Иначе
			ПредставлениеУчастника = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(СтрШаблон("%1 %2 %3",
				ЧастиПредставленияУчастника[0],
				ЧастиПредставленияУчастника[1],
				ЧастиПредставленияУчастника[2]));
		КонецЕсли;
	КонецЕсли;
	
	Форма.Элементы[ИмяТаблицыУчастников].ТекущиеДанные[ИмяРеквизитаПредставленияУчастника] = ПредставлениеУчастника;
	Форма.Элементы[ИмяТаблицыУчастников].ТекущиеДанные[ИмяРеквизитаУчастника] = Участник;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияАвтопротоколаНаДиск(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Или Результат.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СохраненныйФайл = Результат[0];
	
	ФайловаяСистемаКлиент.ОткрытьФайл(СохраненныйФайл.ПолноеИмя);
	
КонецПроцедуры

#КонецОбласти
