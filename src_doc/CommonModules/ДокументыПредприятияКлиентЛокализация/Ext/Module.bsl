#Область ПрограммныйИнтерфейс

#Область ФормаЭлемента

// Вызывается из "ПриОткрытии" формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
Процедура ПриОткрытииФормаЭлемента(Форма) Экспорт

// Локализация	
	// ИнтернетПоддержкаПользователей.СПАРКРиски, ПриОткрытии.
	СПАРКРискиКлиент.ПриОткрытии(Форма, Неопределено);
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски, ПриОткрытии.

	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОткрытииДокумент(Форма);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
// Локализация Конец

КонецПроцедуры	

// Вызывается из "ОбработкаОповещения" формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - 
//  ИмяСобытия - Строка - 
//  Параметр - Строка, ЛюбаяСсылка - 
//  Источник - Произвольный - 
Процедура ОбработкаОповещенияФормаЭлемента(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
// Локализация
	Объект = Форма.Объект; // СправочникОбъект.ДокументыПредприятия
	
	Если ИмяСобытия = "Запись_Файл" И Параметр.Событие = "СозданФайл" И Параметр <> Неопределено
		И Параметр.Свойство("Владелец") И Параметр.Владелец = Объект.Ссылка Тогда
		
		Если Параметр.Свойство("ДокументЭДО") Тогда
			Форма.СвязанныеДокументыЭДО.Добавить(Параметр.ДокументЭДО);
	
			Если ЗначениеЗаполнено(Форма.ТекущийДокументЭДО) Тогда
	
				ЭлементСписка = Форма.СвязанныеДокументыЭДО.НайтиПоЗначению(
					Форма.ТекущийДокументЭДО);
				Если ЭлементСписка <> Неопределено Тогда
					Форма.СвязанныеДокументыЭДО.Удалить(ЭлементСписка);
				КонецЕсли;
	
			КонецЕсли;
	
			Форма.Элементы.СоздатьИсходящийДокументЭДО.Видимость = Ложь;
			Форма.Элементы.СоздатьИсходящийДокументЭДОМенюЭДО.Видимость = Ложь;
			Форма.Элементы.СоздатьИсходящийДокументЭДОПодменюСервис.Видимость = Ложь;
			Форма.Элементы.ПросмотретьЭлектронныйДокумент.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;

	Если ИмяСобытия = "Запись_УведомлениеМЭДО" Или ИмяСобытия = "Запись_КвитанцияМЭДО" 
		Или ИмяСобытия = "Запись_ДанныеДокументаМЭДО" Тогда
		
		Если Параметр.Документ = Объект.Ссылка Тогда
			ОповеститьОбИзменении(Форма.КлючЗаписи);
		КонецЕсли;
	КонецЕсли;

	// ИнтернетПоддержкаПользователей.СПАРКРиски, ОбработкаОповещения.
	СПАРКРискиКлиент.ОбработкаОповещения(Форма, Неопределено, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски, ОбработкаОповещения.
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами	
	
// Локализация Конец

КонецПроцедуры

// Вызывается из "ОбработкаОповещения" формы элемента справочника ДокументыПредприятия.
// Определяет, требуется ли обработать оповещение на сервере
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ИмяСобытия - Строка
//  Параметр - Произвольный
//  Источник - Произвольный
// 
// Возвращаемое значение:
//  Булево - Если Истина, то будет вызван ДокументыПредприятияКлиентЛокализация.ОбработкаОповещенияФормаЭлемента
Функция НеобходимСерверныйВызовДляОбработкиОповещенияФормаЭлемента(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	// Локализация
	Если ИмяСобытия = ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияСостоянияЭДО() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ИмяСобытия = "ЭлектронныйДокументВходящий_ПодборДокументаУчета" Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ИмяСобытия = ОбменЭДОДокументооборотКлиент.ИмяСобытияСозданияИсходящегоЭДО() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_УведомлениеМЭДО" Или ИмяСобытия = "Запись_КвитанцияМЭДО" Тогда
		Возврат Истина;
	КонецЕсли;
	// Локализация Конец
	
	Возврат Ложь;
	
КонецФункции

// Вызывается из ПоказатьОбзорТекущегоФайла формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
Процедура ПоказатьОбзорТекущегоФайлаФормаЭлемента(Форма) Экспорт
	
// Локализация

	Элементы = Форма.Элементы;
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	
	Если ТекущиеДанные.ЭтоДокументЭДО Тогда
		
		//@skip-check undefined-variable
		Элементы.ПредставлениеHTML.Вывод = ИспользованиеВывода.Авто;
		
		ДанныеВизуализации = ОбменЭДОДокументооборотВызовСервера.ДанныеВизуализации(ТекущиеДанные.ДокументЭДО);
		Форма.СостояниеЭДО = ДанныеВизуализации.Состояние;
		
		Если ДанныеВизуализации.ТабличныйДокументСформирован Тогда 
			
			Если ТипЗнч(ДанныеВизуализации.ТабличныйДокумент) = Тип("ТабличныйДокумент") Тогда
				
				Форма.ТабличныйДокументЭДО = ДанныеВизуализации.ТабличныйДокумент;
				Элементы.ГруппаЭДОСтраницы.ТекущаяСтраница = Элементы.ГруппаЭДОТабличный;
				Элементы.ГруппаПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ГруппаЭДО; // только если формализованный

				
			ИначеЕсли ТипЗнч(ДанныеВизуализации.ТабличныйДокумент) = Тип("Строка") Тогда // html
				
				Элементы.ГруппаПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ГруппаHtml;
				Форма.ПредставлениеHTML = ДанныеВизуализации.ТабличныйДокумент;
				
			КонецЕсли;
		
		Иначе
			
			Форма.ТабличныйДокументЭДО = Новый ТабличныйДокумент;
			
			Если ДанныеВизуализации.Свойство("ДвоичныеДанные") Тогда
				// тут предпросмотр
				
				Элементы.ГруппаПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ГруппаHtml;
				
				ДвоичныеДанныеФайла  = ДанныеВизуализации.ДвоичныеДанные;
				
				ПараметрыПолученияПредставления = ОбзорФайловКлиентСервер.ПараметрыПолученияПредставления();
				
				СистемнаяИнформация = Новый СистемнаяИнформация();
				ИдентификаторКлиента = СистемнаяИнформация.ИдентификаторКлиента;
				
				ПараметрыПолученияПредставления.ТребуетсяСоздатьВизуализациюЭП = Ложь;
				ПараметрыПолученияПредставления.МаксимальноеЧислоСтраницДляПредпросмотра = 10;
				ПараметрыПолученияПредставления.ПредпросмотрУрезан = Ложь;
				ПараметрыПолученияПредставления.УникальныйИдентификатор = Форма.УникальныйИдентификатор;
				ПараметрыПолученияПредставления.Расширение = ДанныеВизуализации.ПрисоединенныйФайлРасширение;
				ПараметрыПолученияПредставления.ПредпросмотрОфисныхФайлов = Форма.ПредпросмотрОфисныхФайлов;
				ПараметрыПолученияПредставления.ИдентификаторКлиента = ИдентификаторКлиента;
				ПараметрыПолученияПредставления.ПоддерживаемыеТипы = ОбзорФайловКлиентСервер.ПоддерживаемыеТипыПредпросмотра();
				ПараметрыПолученияПредставления.МасштабироватьPDFИКартинки = Форма.МасштабироватьPDFИКартинки;
				ПараметрыПолученияПредставления.ЭтоСлужебноеСообщение = Ложь;
				ПараметрыПолученияПредставления.ИспользоватьLibreOffice = Ложь;
				
				ПараметрыПолученияПредставления.Вставить("ДвоичныеДанные", ДвоичныеДанныеФайла);
				
				РеквизитыФайла = Новый Структура("ТекущаяВерсияРасширение, Зашифрован, ТекущаяВерсияРазмер, Наименование", 
					ДанныеВизуализации.ПрисоединенныйФайлРасширение, Ложь, ДвоичныеДанныеФайла.Размер(), 
					ДанныеВизуализации.ПрисоединенныйФайлПредставление);
				ДанныеФайла = Неопределено;
				ПредпросмотрУрезан = Ложь;
				ДокументPDF = Неопределено;
				
				ДанныеОбзора = ОбзорФайловКлиент.СформироватьHTMLПредставление(
					ПараметрыПолученияПредставления,
					РеквизитыФайла,
					ДанныеФайла,
					ПредпросмотрУрезан,
					ДокументPDF);
				
				ОписаниеРеквизитовОбзора = ОбзорФайловклиент.ОписаниеРеквизитовОбзора();
				ОбзорФайловКлиент.ЗаполнитьРеквизитыОбзора(Форма, ДанныеОбзора, ОписаниеРеквизитовОбзора);
				
				Если ДокументPDF = Неопределено Тогда
					Элементы.ГруппаПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ГруппаHtml;
				Иначе
					Элементы.ГруппаПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ГруппаPdf;
					Форма["ДокументPdfРеквизит"] = ДокументPDF;
					Элементы.ДокументPdfПоле.НомерТекущейСтраницы = 1;
				КонецЕсли;
					
			Иначе
				
				Форма.ПредставлениеHTML = ОбзорФайловКлиентСервер.HTMLПредставлениеНеПоддерживаемыйДокументЭДО();
				Элементы.ГруппаПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ГруппаHtml;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Форма.ТекущийДокументЭДО = ТекущиеДанные.ДокументЭДО;
		
		Возврат;
		
	КонецЕсли;

// Локализация Конец

КонецПроцедуры

// Вызывается из ФайлыВыбор формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Строка - СтрокаТаблицыЗначений - Строка списка файлов в форме документа
// 
// Возвращаемое значение:
//  Булево - Событие выбора обработано в этой функции
Функция ФайлыВыборФормаЭлемента(Строка) Экспорт
	
// Локализация
	Если Строка.ЭтоДокументЭДО Тогда
		Если ИнтерфейсЭДОДокументооборотВызовСервера.ДоступныДляЧтенияВсеДокументыПакета(Строка.ДокументЭДО) Тогда
			ПоказатьЗначение(,Строка.ДокументЭДО);
		Иначе
			ПоказатьПредупреждение(, НСтр("ru='Невозможно открыть документ ЭДО, так как он состоит в пакете, но не все документы пакета доступны для просмотра.'"));
		КонецЕсли;
		Возврат Истина;
	КонецЕсли;
// Локализация Конец
	Возврат Ложь;
	
КонецФункции

// Вызывается из ЗаполнитьСписокФайловКлиент формы элемента справочника ДокументыПредприятия
// 
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения
//  ЗаполнитьПризнакОригинал - Булево 
Процедура ПриЗаполненииСпискаФайловФормаЭлемента(Форма, ЗаполнитьПризнакОригинал = Ложь) Экспорт
	
// Локализация
	
	Форма.ФайлыЗаполненыСЭДО = (Форма.СвязанныеДокументыЭДО.Количество() <> 0);
	
	// добавим строку документа
	Если Форма.СвязанныеДокументыЭДО.Количество() <> 0 Тогда
		
		РольФайла = Неопределено;
		Если Форма.Файлы.ПолучитьЭлементы().Количество() <> 0 Тогда
			Строка1 = Форма.Файлы.ПолучитьЭлементы().Получить(0);
			РольФайла = Строка1.РольФайла;
		КонецЕсли;
		
		ДокументыПредприятияКлиентСервер.ДобавитьСтрокуДокументаВФайлыПриНеобходимости(Форма);
			
		Для Каждого СтрокаСписка Из Форма.СвязанныеДокументыЭДО Цикл
			
			ДокЭДО = СтрокаСписка.Значение;
			
			Если Форма.ИспользоватьПредпросмотрФайлов Тогда
				СтрЭДО = Форма.Файлы.ПолучитьЭлементы().Вставить(1);
			Иначе
				СтрЭДО = Форма.Файлы.ПолучитьЭлементы().Вставить(0);
			КонецЕсли;	
			
			СтрЭДО.ДокументЭДО = ДокЭДО;
			СтрЭДО.ЭтоДокументЭДО = Истина;
			
			СтрЭДО.Наименование = Строка(ДокЭДО) + НСтр("ru = ' (Документ ЭДО)'");
			
			СтрЭДО.ИндексКартинки = 30; // как xml  
			СтрЭДО.СтатусПроверкиЭП = -1;
			СтрЭДО.РольФайла = РольФайла;
			
		КонецЦикла;
		
	КонецЕсли;
	
// Локализация Конец

КонецПроцедуры

// Вызывается из ПослеЗаписи формы элемента справочника ДокументыПредприятия
// 
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения
//  ПараметрыЗаписи - Структура
Процедура ПослеЗаписиФормаЭлемента(Форма, ПараметрыЗаписи) Экспорт
	
// Локализация
	Если ТипЗнч(Форма.Параметры.Основание) = Тип("ДокументСсылка.ЭлектронныйДокументВходящийЭДО") Тогда
		Оповестить(ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияСостоянияЭДО());
	КонецЕсли;
// Локализация Конец

КонецПроцедуры

// Вызывается из ПолеHTMLПриНажатии формы элемента справочника ДокументыПредприятия
// 
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения
//  Элемент - ТаблицаФормы
//  ДанныеСобытия - ФиксированнаяСтруктура
//  СтандартнаяОбработка - Булево
// 
// Возвращаемое значение:
//  Булево - Если Истина, то нажатие будет обработано в ДокументыПредприятияКлиентЛокализация.ПолеHTMLПриНажатии
Функция ОбработатьНажатиеПоляHTMLФормаЭлемента(Форма, Элемент, ДанныеСобытия, СтандартнаяОбработка) Экспорт
	
	// Локализация
	Если СтрНайти(ДанныеСобытия.Href, "OpenEDI") <> 0 Тогда
		Возврат Истина;
	КонецЕсли;
	// Локализация Конец
	Возврат Ложь;
	
КонецФункции

// Вызывается из ПолеHTMLПриНажатии формы элемента справочника ДокументыПредприятия
// 
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения
//  Элемент - ТаблицаФормы
//  ДанныеСобытия - ФиксированнаяСтруктура
//  СтандартнаяОбработка - Булево
//
Процедура ПолеHTMLПриНажатииФормаЭлемента(Форма, Элемент, ДанныеСобытия, СтандартнаяОбработка) Экспорт
	
// Локализация
	Если ЗначениеЗаполнено(Форма.ТекущийДокументЭДО) Тогда
		ПоказатьЗначение(, Форма.ТекущийДокументЭДО);
	КонецЕсли;
// Локализация Конец

КонецПроцедуры

// Вызывается из УстановитьДоступностьКоманд формы элемента справочника ДокументыПредприятия
// 
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения
//  ТекущиеДанные - ДанныеФормыЭлементДерева
//  Префикс - Строка
Процедура УстановитьДоступностьКомандФормаЭлемента(Форма, ТекущиеДанные, Префикс) Экспорт
	
	// Локализация
	Элементы = Форма.Элементы;
	
	Если ТекущиеДанные = Неопределено
		Или ТипЗнч(Элементы["Файлы" + Префикс].ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Возврат;
	КонецЕсли;
		
	Редактирует = ТекущиеДанные.Редактирует;
	РазрешеноРедактирование = Не (Форма.ЗапретитьРедактироватьФайлы И ТекущиеДанные.СозданПоШаблонуДокумента);
	ЭтоОтправленныйЭДОФайл = (Форма.ДокументБлокированПоЭДОФайл = ТекущиеДанные.Ссылка); 
	РазрешеноПоЭДО = (Не Форма.ДокументБлокированПоЭДО) Или (Форма.ДокументБлокированПоЭДО И Не ЭтоОтправленныйЭДОФайл);
	ДокументВАрхивеИлиУничтожен = Форма.ДокументВАрхивеИлиУничтожен;
	
	ДокументыПредприятияКлиент.УстановитьДоступностьКоманды(Форма, Элементы["Редактировать"+Префикс], 
		НЕ ТекущиеДанные.ПодписанЭП И РазрешеноРедактирование И РазрешеноПоЭДО);
	ДокументыПредприятияКлиент.УстановитьДоступностьКоманды(Форма, Элементы["КонтекстноеМенюФайлыРедактировать"+Префикс], 
		Не ТекущиеДанные.ПодписанЭП И РазрешеноРедактирование И Не ДокументВАрхивеИлиУничтожен И РазрешеноПоЭДО);
	
	ДокументыПредприятияКлиент.УстановитьДоступностьКоманды(Форма, Элементы["Занять"+Префикс], 
		Не ЗначениеЗаполнено(Редактирует) И РазрешеноРедактирование И РазрешеноПоЭДО);
	ДокументыПредприятияКлиент.УстановитьДоступностьКоманды(Форма, Элементы["КонтекстноеМенюФайлыЗанять"+Префикс], 
		Не ЗначениеЗаполнено(Редактирует) И РазрешеноРедактирование И Не ДокументВАрхивеИлиУничтожен И РазрешеноПоЭДО);
	
	ДокументыПредприятияКлиент.УстановитьДоступностьКоманды(Форма, Элементы["ОбновитьИзФайлаНаДиске"+Префикс], 
		РазрешеноРедактирование И РазрешеноПоЭДО);
	ДокументыПредприятияКлиент.УстановитьДоступностьКоманды(Форма,
		Элементы["КонтекстноеМенюФайлыОбновитьИзФайлаНаДиске" + Префикс],
		РазрешеноРедактирование И Не ДокументВАрхивеИлиУничтожен И РазрешеноПоЭДО);
	// Локализация Конец
	
КонецПроцедуры

// Вызывается из ФайлыВыбор формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Строка - СтрокаТаблицыЗначений - Строка списка файлов в форме документа
//  Объект - Произвольный - Объект, наличие которого проверяется в строке файлов
// 
// Возвращаемое значение:
//  Булево - Файлы выбор форма элемента
Функция СтрокаФайловСодержитОбъектФормаЭлемента(Строка, Объект) Экспорт
	
// Локализация
	Возврат Строка.ДокументЭДО = Объект;
// Локализация Конец
	Возврат Ложь;
	
КонецФункции

// Вызывается из СтороныПередУдалением формы элемента справочника ДокументыПредприятия
// 
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения 
//  Элемент - ТаблицаФормы
//  Отказ - Булево
Процедура СтороныПередУдалениемФормаЭлемента(Форма, Элемент, Отказ) Экспорт
	
// Локализация
	Если Форма.ДокументБлокированПоЭДО Тогда
		Отказ = Истина;
	КонецЕсли;
// Локализация Конец

КонецПроцедуры

// Вызывается из СтороныПередНачаломДобавления формы элемента справочника ДокументыПредприятия
// 
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения 
//  Элемент - ТаблицаФормы
//  Отказ - Булево
//  Копирование - Булево
//  Родитель - Произвольный
//  Группа - Булево
//  Параметр - Произвольный
Процедура СтороныПередНачаломДобавленияФормаЭлемента(Форма, Элемент, Отказ, Копирование, Родитель, Группа, Параметр) Экспорт
	
// Локализация
	Если Форма.ДокументБлокированПоЭДО Тогда
		Отказ = Истина;
	КонецЕсли;
// Локализация Конец

КонецПроцедуры

// Вызывается из ОбработкаПолученияСпискаНавигационныхСсылок формы элемента справочника ДокументыПредприятия
// 
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения 
//  СписокНавигационыхСсылок - СписокВыбораНавигационнойСсылки
//  КлючПоУмолчанию - Произвольный
Процедура ОбработкаПолученияСпискаНавигационныхСсылокФормаЭлемента(Форма, СписокНавигационыхСсылок, КлючПоУмолчанию) Экспорт
	
// Локализация
	ВидимостьКомандСервиса1CShare = ИнтеграцияShareДокументооборотКлиентСервер.ВидимостьКомандСервиса1CShare(
		Форма, Форма.ВидДокументаКэш);
	
	Если ВидимостьКомандСервиса1CShare.ИспользоватьСервис1CShare
		И ВидимостьКомандСервиса1CShare.Видимость Тогда
			СписокНавигационыхСсылок.Добавить(ИнтеграцияShareДокументооборотКлиент.ИмяКлючаНавигационнойСсылки(), 
				ИнтеграцияShareДокументооборотКлиент.НазваниеНавигационнойСсылки());
	КонецЕсли;
// Локализация Конец

КонецПроцедуры

// Вызывается из ОбработкаПолученияНавигационнойСсылки формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Ключ - Произвольный 
//  Представление - Строка 
//  НавигационнаяСсылка - Строка 
//  СтандартнаяОбработка - Булево 
Процедура ОбработкаПолученияНавигационнойСсылкиФормаЭлемента(Форма, Ключ, Представление,
	НавигационнаяСсылка, СтандартнаяОбработка) Экспорт
	
// Локализация
	Если Ключ = ИнтеграцияShareДокументооборотКлиент.ИмяКлючаНавигационнойСсылки() Тогда
		НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(Форма.Объект.Ссылка);
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ПоделитьсяДокументомShare", 1, Истина);
	КонецЕсли;
// Локализация Конец

КонецПроцедуры

// Вызывается из ПередЗакрытием формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  ЗавершениеРаботы - Булево
//  ТекстПредупреждения - Строка
//  СтандартнаяОбработка - Булево
Процедура ПередЗакрытиемФормаЭлемента(Форма, Отказ, ЗавершениеРаботы, 
		ТекстПредупреждения, СтандартнаяОбработка) Экспорт
		
// Локализация
	Объект = Форма.Объект;
	
	// Для целей МЭДО, нужно предложить создать исходящее уведомление:
	Если Форма.ОтмененаРегистрацияДокумента Тогда
		МЭДОКлиент.НачатьСозданиеУведомленияОбОтказе(
			Объект.Ссылка,
			Форма,
			Форма.ЕстьДоступКМЭДО);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
// Локализация Конец

КонецПроцедуры

// Вызывается из СтороныСторонаПриИзменении формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Сторона - СправочникСсылка.Контрагенты
//  Стороны - ПолеФормы
Процедура СтороныСторонаПриИзмененииФормаЭлемента(Форма, Сторона, Стороны) Экспорт
// Локализация	
	// ИнтернетПоддержкаПользователей.СПАРКРиски, ОбновитьОтображениеИндексов.
	// Отображать не по ссылке, а по ИНН, НЕ сохраняя в кэше.
	Если Форма.ИспользоватьСервисСПАРКРиски Тогда
		Форма.ИндексыСПАРКРиски = Неопределено; // Сбросить полученные значения.
		ОбновитьОтображениеИндексыСПАРК(Форма, Сторона);
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски, ОбновитьОтображениеИндексов.

	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(Форма, Стороны);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами

// Локализация Конец	
КонецПроцедуры	

// Вызывается из СтороныВыбор формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Поле - ПолеФормы
//  Элементы - ВсеЭлементыФормы
//  ТекущиеДанные - ДанныеФормыЭлементКоллекции
Процедура СтороныВыборФормаЭлемента(Форма, Поле, Элементы, ТекущиеДанные) Экспорт 

// Локализация
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	Если Поле = Элементы.СтороныСводныйИндикатор Тогда
		СтандартнаяОбработка = Ложь;
		// Откроем подсказку
		Если ТекущиеДанные.ИнформацияСпаркРиски = "SPARK:NoInformation" Тогда 
			СПАРКРискиКлиент.ОбработкаНавигационнойСсылки(ЭтотОбъект,
				Поле, ТекущиеДанные.ИнформацияСпаркРиски, Истина);
		ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.СводныйИндикатор) 
			И ТекущиеДанные.СводныйИндикатор > 0 Тогда 
			СПАРКРискиКлиент.ПоказатьЗначенияИндексовКонтрагента(ТекущиеДанные.ИндексыСПАРКРиски, Форма);
		ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.ИнформацияСпаркРиски) Тогда 
			ПерейтиПоНавигационнойСсылке(ТекущиеДанные.ИнформацияСпаркРиски);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	// ИнтернетПоддержкаПользователей.СПАРКРиски
// Локализация Конец		

КонецПроцедуры

// Вызывается из СтороныСторонаОбработкаВыбора формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Сторона - СправочникСсылка.Контрагенты
//  Стороны - ТабличнаяЧасть
Процедура СтороныСторонаОбработкаВыбораФормаЭлемента(Форма, Сторона, Стороны) Экспорт

// Локализация		
	// ИнтернетПоддержкаПользователей.СПАРКРиски, ОбновитьОтображениеИндексов.
	// Отображать не по ссылке, а по ИНН, НЕ сохраняя в кэше.
	Если Форма.ИспользоватьСервисСПАРКРиски Тогда
		Форма.ИндексыСПАРКРиски = Неопределено; // Сбросить полученные значения.
		ОбновитьОтображениеИндексыСПАРК(Форма, Сторона);
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски, ОбновитьОтображениеИндексов.
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	//
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(Форма, Стороны);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
// Локализация Конец
	
КонецПроцедуры

// Вызывается из КонтрагентОбработкаВыбора формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Контрагент - ПолеФормы
Процедура КонтрагентОбработкаВыбораФормаЭлемента(Форма, Контрагент) Экспорт

// Локализация
	// ИнтернетПоддержкаПользователей.СПАРКРиски, ОбновитьОтображениеИндексов.
	// Отображать не по ссылке, а по ИНН, НЕ сохраняя в кэше.
	Если Форма.ИспользоватьСервисСПАРКРиски Тогда
		Форма.ИндексыСПАРКРиски = Неопределено; // Сбросить полученные значения.
		ОбновитьОтображениеИндексыСПАРК(Форма);
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски, ОбновитьОтображениеИндексов.
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(Форма, Контрагент);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами	
// Локализация Конец		

КонецПроцедуры

// Вызывается из КонтрагентыВыбор формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Поле - ПолеФормы
//  Элементы - ВсеЭлементыФормы
//  ТекущиеДанные - ДанныеФормыЭлементКоллекции
Процедура КонтрагентыВыборФормаЭлемента(Форма, Поле, Элементы, ТекущиеДанные) Экспорт

// Локализация	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	Если Поле = Элементы.КонтрагентыСводныйИндикатор Тогда
		// Откроем подсказку
		Если ТекущиеДанные.ИнформацияСпаркРиски = "SPARK:NoInformation" Тогда 
			СПАРКРискиКлиент.ОбработкаНавигационнойСсылки(Форма,
				Поле, ТекущиеДанные.ИнформацияСпаркРиски, Истина);
			
		ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.СводныйИндикатор) 
			И ТекущиеДанные.СводныйИндикатор > 0 Тогда 
			СПАРКРискиКлиент.ПоказатьЗначенияИндексовКонтрагента(ТекущиеДанные.ИндексыСПАРКРиски, Форма);
		ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.ИнформацияСпаркРиски) Тогда 
			ПерейтиПоНавигационнойСсылке(ТекущиеДанные.ИнформацияСпаркРиски);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	// ИнтернетПоддержкаПользователей.СПАРКРиски
// Локализация Конец

КонецПроцедуры	

// Вызывается из СтороныСторонаПриИзменении формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Контрагент - СправочникСсылка.Контрагенты
//  Контрагенты - ТабличнаяЧасть справочника Контрагенты
Процедура КонтрагентыКонтрагентПриИзмененииФормаЭлемента(Форма, Контрагент, Контрагенты) Экспорт

// Локализация
	// ИнтернетПоддержкаПользователей.СПАРКРиски, ОбновитьОтображениеИндексов.
	// Отображать не по ссылке, а по ИНН, НЕ сохраняя в кэше.
	Если Форма.ИспользоватьСервисСПАРКРиски Тогда
		Форма.ИндексыСПАРКРиски = Неопределено; // Сбросить полученные значения.
		ОбновитьОтображениеИндексыСПАРК(Форма, Контрагент);
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски, ОбновитьОтображениеИндексов.

	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(Форма, Контрагенты);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами	
	
// Локализация Конец	

КонецПроцедуры	

// Вызывается из СтороныСторонаПриИзменении формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
Процедура ОдинКонтрагентПродолжениеФормаЭлемента(Форма) Экспорт

// Локализация
	// ИнтернетПоддержкаПользователей.СПАРКРиски, ОбновитьОтображениеИндексов.
	// Отображать не по ссылке, а по ИНН, НЕ сохраняя в кэше.
	Если Форма.ИспользоватьСервисСПАРКРиски Тогда
		Форма.ИндексыСПАРКРиски = Неопределено; // Сбросить полученные значения.
		ОбновитьОтображениеИндексыСПАРК(Форма);
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски, ОбновитьОтображениеИндексов.
// Локализация Конец

КонецПроцедуры

// Вызывается из СтороныСторонаПриИзменении формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Контрагент - СправочникСсылка.Контрагенты
Процедура ОбработчикИзмененияКонтрагентаФормаЭлемента(Форма, Контрагент) Экспорт

// Локализация	
	// ИнтернетПоддержкаПользователей.СПАРКРиски, ОбновитьОтображениеИндексов.
	// Отображать не по ссылке, а по ИНН, НЕ сохраняя в кэше.
	Если Форма.ИспользоватьСервисСПАРКРиски Тогда
		Форма.ИндексыСПАРКРиски = Неопределено; // Сбросить полученные значения.
		ОбновитьОтображениеИндексыСПАРК(Форма);
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски, ОбновитьОтображениеИндексов.

	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(Форма, Контрагент);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
// Локализация Конец		

КонецПроцедуры	

// Вызывается из ДатаРегистрацииПриИзменении формы элемента справочника ДокументыПредприятия
// 
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения 
//  ДатаРегистрации - Дата
Процедура ДатаРегистрацииПриИзмененииФормаЭлемента(Форма, ДатаРегистрации) Экспорт

// Локализация
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(Форма, ДатаРегистрации);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
// Локализация Конец		

КонецПроцедуры	

#КонецОбласти

#Область ФормаСпискаСПапками

// Вызывается из ОбработкаОповещения формы элемента справочника ДокументыПредприятия
// 
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения 
//  ИмяСобытия - Строка
//  Параметр - Произвольный
//  Источник - Произвольный
Процедура ОбработкаОповещенияФормаСпискаСПапками(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
// Локализация
	Если ИмяСобытия = "ОбновленоСостояниеЭДДО" Тогда
		Форма.Элементы.Список.Обновить();
	КонецЕсли;

	Если ИмяСобытия = "ОтражениеВходящихЭДО" Тогда
		Форма.Элементы.Список.Обновить();
	КонецЕсли;
	
	Если ИмяСобытия = ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияСостоянияЭДО() Тогда
		Форма.Элементы.Список.Обновить();
	КонецЕсли;
// Локализация Конец

КонецПроцедуры

// Вызывается из ФайлыВыбор формы списка справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения 
//  Строка - ДанныеФормыЭлементДерева - Строка списка файлов в форме документа
//  Элемент - ТаблицаФормы
//  ВыбраннаяСтрока - Произвольный
//  Поле - ПолеФормы
//  СтандартнаяОбработка - Истина
// 
// Возвращаемое значение:
//  Булево - Событие выбора обработано в этой функции, дальнейшая обработка не требуется
Функция ФайлыВыборФормаСпискаСПапками(Форма, Строка, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка) Экспорт

// Локализация
	Если Строка.ЭтоДокументЭДО Тогда
		ПоказатьЗначение(,Строка.ДокументЭДО);
		Возврат Истина;
	КонецЕсли;
// Локализация Конец
	Возврат Ложь;

КонецФункции

// Проверяет, может ли текущая строка в дереве файлов формы элемента содержать подстроки
// 
// Параметры:
//  Строка - ДанныеФормыЭлементДерева
// 
// Возвращаемое значение:
//  Булево - могут ли быть подчиненные строки
Функция СтрокаФайловМожетСодержатьПодстрокиФормаСпискаСПапками(Строка) Экспорт

// Локализация
	Если Строка.ЭтоДокументЭДО Тогда
		Возврат Истина;
	КонецЕсли;
// Локализация Конец

	Возврат Ложь;

КонецФункции

// Обработка команды "Редактировать" в списке файлов формы списка документов предприятия
// 
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения 
//  Команда - КомандаФормы
// 
// Возвращаемое значение:
//  Булево - Истина, если команда обработана в этой функции и дальнейшая обработка не требуется
Функция РедактироватьФормаСпискаСПапками(Форма, Команда) Экспорт

// Локализация
	ТекущиеДанные = Форма.Элементы.ФайлыДокумента.ТекущиеДанные;
	Если ТекущиеДанные.ЭтоДокументЭДО Тогда
		Возврат Истина;
	КонецЕсли;
// Локализация Конец
	Возврат Ложь;

КонецФункции

// Обработка команды "ЗакончитьРедактирование" в списке файлов формы списка документов предприятия
// 
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения 
//  Команда - КомандаФормы
// 
// Возвращаемое значение:
//  Булево - Истина, если команда обработана в этой функции и дальнейшая обработка не требуется
Функция ЗакончитьРедактированиеФормаСпискаСПапками(Форма, Команда) Экспорт
// Локализация
	ТекущиеДанные = Форма.Элементы.ФайлыДокумента.ТекущиеДанные;
	Если ТекущиеДанные.ЭтоДокументЭДО Тогда
		Возврат Истина;
	КонецЕсли;
// Локализация Конец
	Возврат Ложь;
КонецФункции

// Обработка команды "СохранитьКак" в списке файлов формы списка документов предприятия
// 
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения 
//  Команда - КомандаФормы
// 
// Возвращаемое значение:
//  Булево - Истина, если команда обработана в этой функции и дальнейшая обработка не требуется
Функция СохранитьКакФормаСпискаСПапками(Форма, Команда) Экспорт
// Локализация
	ТекущиеДанные = Форма.Элементы.ФайлыДокумента.ТекущиеДанные;
	Если ТекущиеДанные.ЭтоДокументЭДО Тогда
		Возврат Истина;
	КонецЕсли;
// Локализация Конец
	Возврат Ложь;
КонецФункции

// Обработка команды "Напечатать" в списке файлов формы списка документов предприятия
// 
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения 
//  Команда - КомандаФормы
// 
// Возвращаемое значение:
//  Булево - Истина, если команда обработана в этой функции и дальнейшая обработка не требуется
Функция НапечататьФормаСпискаСПапками(Форма, Команда) Экспорт
// Локализация
	ТекущиеДанные = Форма.Элементы.ФайлыДокумента.ТекущиеДанные;
	Если ТекущиеДанные.ЭтоДокументЭДО Тогда
		Возврат Истина;
	КонецЕсли;
// Локализация Конец
	Возврат Ложь;
КонецФункции

// Обработка команды "ОбновитьИзФайлаНаДиске" в списке файлов формы списка документов предприятия
// 
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения 
//  Команда - КомандаФормы
// 
// Возвращаемое значение:
//  Булево - Истина, если команда обработана в этой функции и дальнейшая обработка не требуется
Функция ОбновитьИзФайлаНаДискеФормаСпискаСПапками(Форма, Команда) Экспорт
// Локализация
	ТекущиеДанные = Форма.Элементы.ФайлыДокумента.ТекущиеДанные;
	Если ТекущиеДанные.ЭтоДокументЭДО Тогда
		Возврат Истина;
	КонецЕсли;
// Локализация Конец
	Возврат Ложь;
КонецФункции

// Обработка получения списка навигационных ссылок в форме списка справочника ДокументыПредприятия
// 
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения 
//  СписокНавигационыхСсылок - СписокВыбораНавигационнойСсылки
//  КлючПоУмолчанию - Произвольный
Процедура ОбработкаПолученияСпискаНавигационныхСсылокФормаСпискаСПапками(Форма, СписокНавигационыхСсылок, КлючПоУмолчанию) Экспорт
// Локализация
	Если Форма.ИспользоватьСервис1CShare И Форма.Элементы.ОбщаяКомандаПоделитьсяДокументомShare.Видимость
		И ЗначениеЗаполнено(Форма.ТекущийДокумент) Тогда
		СписокНавигационыхСсылок.Добавить(ИнтеграцияShareДокументооборотКлиент.ИмяКлючаНавигационнойСсылки(), 
			ИнтеграцияShareДокументооборотКлиент.НазваниеНавигационнойСсылки());
	КонецЕсли;
// Локализация Конец
КонецПроцедуры

#КонецОбласти

#Область ФормаМоиДокументы

// Вызывается из "ФайлыВыбор" формы ФормаМоиДокументы РС МоиДокументы
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
// 
// Параметры:
//  Строка - ДанныеФормыЭлементДерева
// 
// Возвращаемое значение:
//  Булево - обработка выбора обработана, дальнейшей обработки не требуется
Функция ПриВыбореФайла_ФормаМоиДокументы(Строка) Экспорт

// Локализация
	Если Строка.ЭтоДокументЭДО Тогда
		Если ИнтерфейсЭДОДокументооборотВызовСервера.ДоступныДляЧтенияВсеДокументыПакета(Строка.ДокументЭДО) Тогда
			ПоказатьЗначение(, Строка.ДокументЭДО);
		Иначе
			ПоказатьПредупреждение(, НСтр("ru='Невозможно открыть документ ЭДО, так как он состоит в пакете, но не все документы пакета доступны для просмотра.'"));
		КонецЕсли;
		Возврат Истина;
	КонецЕсли;
// Локализация Конец

	Возврат Ложь;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Локализация

// ИнтернетПоддержкаПользователей.СПАРКРиски
// Параметры: 
//  Форма - ФормаКлиентскогоПриложения
//  КонтрагентСПАРК - СправочникСсылка.Контрагенты
Процедура ОбновитьОтображениеИндексыСПАРК(Форма, КонтрагентСПАРК = Неопределено) Экспорт

	Объект = Форма.Объект;
	
	Если КонтрагентСПАРК = Неопределено Тогда 
		КонтрагентСПАРК = Объект.Контрагент;
	КонецЕсли;
	
	ПараметрыОтображения = Новый Структура("ВариантОтображения", "Однострочный");
	СПАРКРискиКлиент.ОтобразитьИндексыСПАРК(
		Форма.ИндексыСПАРКРиски,
		КонтрагентСПАРК,
		КонтрагентСПАРК, // Искать по ссылке
		ОбщегоНазначенияДокументооборотКлиентСервер.ВидКонтрагентаСПАРК(КонтрагентСПАРК),
		Форма,
		ПараметрыОтображения);

КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.СПАРКРиски

#Область СлужебныеПроцедурыИФункции_РежимОбращенийГраждан

// Устанавливает доступность команд в форме документа
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
Процедура Переписка_УстановитьДоступностьКоманд(Форма) Экспорт

	Элементы = Форма.Элементы;

	Элементы.ДеревоПерепискиКонтекстноеМенюСкопироватьОтвет.Видимость = Форма.ЭтоРежимОбращенияГраждан;
	Если Не Форма.ЭтоРежимОбращенияГраждан Тогда
		Возврат;
	КонецЕсли;
	ДеревоТекущиеДанные = Элементы.ДеревоПереписки.ТекущиеДанные;
	СписокТекущиеДанные = Элементы.Список.ТекущиеДанные;

	Если ДеревоТекущиеДанные = Неопределено Тогда
		Элементы.СписокКонтекстноеМенюОтметитьКакПовторноеОбращение.Доступность = Ложь;
		Элементы.СписокКонтекстноеМенюОтметитьКакДубликатОбращения.Доступность = Ложь;
		Элементы.ДеревоПерепискиКонтекстноеМенюСкопироватьОтвет.Доступность = Ложь;
		Возврат;
	КонецЕсли;

	Элементы.ДеревоПерепискиКонтекстноеМенюСкопироватьОтвет.Доступность = (
		ДеревоТекущиеДанные.ВидСообщения = "Исходящее" 
		И ТипЗнч(ДеревоТекущиеДанные.Ссылка) = Тип("СправочникСсылка.ДокументыПредприятия"));

	Если СписокТекущиеДанные = Неопределено Тогда
		Элементы.СписокКонтекстноеМенюОтметитьКакПовторноеОбращение.Доступность = Ложь;
		Элементы.СписокКонтекстноеМенюОтметитьКакДубликатОбращения.Доступность = Ложь;
		Возврат;
	КонецЕсли;

	Если ДеревоТекущиеДанные.ВидСообщения = "Входящее" И ДеревоТекущиеДанные.Ссылка
		<> СписокТекущиеДанные.Ссылка Тогда
		Элементы.СписокКонтекстноеМенюОтметитьКакПовторноеОбращение.Доступность = Истина;
		Элементы.СписокКонтекстноеМенюОтметитьКакДубликатОбращения.Доступность = Истина;
	Иначе
		Элементы.СписокКонтекстноеМенюОтметитьКакПовторноеОбращение.Доступность = Ложь;
		Элементы.СписокКонтекстноеМенюОтметитьКакДубликатОбращения.Доступность = Ложь;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

// Локализация Конец

#КонецОбласти