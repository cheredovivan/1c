#Область ПрограммныйИнтерфейс

// Возвращает номер версии библиотеки.
//
// Возвращаемое значение:
//  Строка - номер версии библиотеки.
//
Функция ВерсияБиблиотеки() Экспорт

	Возврат "1.0.5.5";

КонецФункции

// см. ОчередьЗаданийПереопределяемый.ПриОпределенииПсевдонимовОбработчиков
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	СоответствиеИменПсевдонимам.Вставить(
		Метаданные.РегламентныеЗадания.ПлюсЗапускЗаданийРасширений.ИмяМетода);
	СоответствиеИменПсевдонимам.Вставить(
		Метаданные.РегламентныеЗадания.ПлюсСинхронизацияССервисом.ИмяМетода);
	СоответствиеИменПсевдонимам.Вставить(
		Метаданные.РегламентныеЗадания.ПлюсСинхронизацияРекомендаций.ИмяМетода);
КонецПроцедуры

// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииСерверныхОповещений
// 
// Параметры:
//  Оповещения - Структура
// @unavailable-in-safemode
Процедура ПриДобавленииСерверныхОповещений(Оповещения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТаблицаУстановленныхРасширений().Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = СерверныеОповещения.НовоеСерверноеОповещение(
		ПлюсСервисКлиентСервер.ИмяСерверногоОповещенияНеобходимаУстановкаМонопольногоРежима());
	Оповещение.ИмяМодуляПолучения = "ПлюсСервисСлужебныйКлиент";
	Оповещение.ПериодПроверки = 60;
	Оповещения.Вставить(Оповещение.Имя, Оповещение);
	
	Оповещение = СерверныеОповещения.НовоеСерверноеОповещение(
		ПлюсСервисКлиентСервер.ИмяСерверногоОповещенияОтключеноАвтообновление());
	Оповещение.ИмяМодуляПолучения = "ПлюсСервисСлужебныйКлиент";
	Оповещение.ПериодПроверки = 60;
	Оповещения.Вставить(Оповещение.Имя, Оповещение);
	
КонецПроцедуры

// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииПараметровРаботыКлиентаПриЗапуске.
// 
// Параметры:
//  Параметры - Структура
// @unavailable-in-safemode
Процедура ПриДобавленииПараметровРаботыКлиентаПриЗапуске(Параметры) Экспорт
	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПлюсУстановленныеРасширения.ИдРасширения
	|ИЗ
	|	РегистрСведений.ПлюсУстановленныеРасширения КАК ПлюсУстановленныеРасширения
	|ГДЕ
	|	ПлюсУстановленныеРасширения.АвтообновлениеОтключено";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	СерверныеОповещения.ОтправитьСерверноеОповещение(
		ПлюсСервисКлиентСервер.ИмяСерверногоОповещенияОтключеноАвтообновление(),
		Новый Структура("Расширения", Новый Массив()),
		Неопределено,
		Истина);
	
КонецПроцедуры

// Заполняет установленные расширения.
// 
// Параметры:
//  ПлюсРасширения - Соответствие из КлючИЗначение:
//  	* Ключ - УникальныйИдентификатор - из ИБ.
//		* Значение - Структура:
//			** ИдРасширения - Строка
//			** ИдВерсииРасширения - Строка
// @unavailable-in-safemode
Процедура ЗаполнитьУстановленныеРасширения(ПлюсРасширения) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
		|	ПлюсУстановленныеРасширения.ИдРасширения КАК ИдРасширения,
		|	ПлюсУстановленныеРасширения.ИдВерсииРасширения КАК ИдВерсииРасширения,
		|	ПлюсУстановленныеРасширения.ИдРасширенияВИБ КАК ИдРасширенияВИБ,
		|	ПлюсУстановленныеРасширения.ИмяРасширения КАК ИмяРасширения,
		|	ПлюсУстановленныеРасширения.ВерсияРасширения КАК ВерсияРасширения,
		|	ПлюсУстановленныеРасширения.ЗаданиеНаУстановку КАК ЗаданиеНаУстановку
		|ИЗ
		|	РегистрСведений.ПлюсУстановленныеРасширения КАК ПлюсУстановленныеРасширения");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеРасширения = Новый Структура();
		ДанныеРасширения.Вставить("ИдРасширения", Строка(Выборка.ИдРасширения));
		ДанныеРасширения.Вставить("ИдВерсииРасширения", Строка(Выборка.ИдВерсииРасширения));
		ДанныеРасширения.Вставить("ИмяРасширения", Выборка.ИмяРасширения);
		ДанныеРасширения.Вставить("ВерсияРасширения", Выборка.ВерсияРасширения);
		ДанныеРасширения.Вставить("ЗаданиеНаУстановку", Выборка.ЗаданиеНаУстановку);
		
		ПлюсРасширения.Вставить(Выборка.ИдРасширенияВИБ, ДанныеРасширения);
	КонецЦикла;
КонецПроцедуры

// Восстанавливает расширения при загрузке из datadump
// 
// Параметры:
//  ПлюсРасширения - Массив из Структура:
// * Name - Строка 
// * Version - Строка 
// * ExtId - Строка 
// * ExtVersionId - Строка 
Процедура ВосстановитьРасширения(ПлюсРасширения) Экспорт
	Для Каждого ПлюсРасширение Из ПлюсРасширения Цикл
		УстановитьРасширение(ПлюсРасширение.InstallTask, , ПлюсРасширение.Data);
	КонецЦикла;
КонецПроцедуры

//Восстанавливает расширения при работе в облаке, подготовленные Менеджером севриса
// Параметры:
//  ЗаданияНаУстановку - Массив из Структура:
//   * Name - Строка
//   * Version - Строка
//   * ExtId - Строка
//   * ExtVersionId - Строка 
//   * ModifiesDataStructure - Булево 
//   * PathToData - Строка
//   * ScheduledJobs - Массив из Структура:
//      ** Id - Строка
//      ** Schedule - Строка - расписание
Процедура ВосстановитьРасширенияИзМС(ЗаданияНаУстановку) Экспорт
	Для Каждого ЗаданиеНаУстановку Из ЗаданияНаУстановку Цикл
		УстановитьРасширение(ЗаданиеНаУстановку, Истина);
	КонецЦикла;
КонецПроцедуры

// Список измененных расширений.
// 
// Возвращаемое значение:
//  Массив из Структура:
//   * Имя - Строка
//   * Версия - Строка
//   * ИдВерсии - УникальныйИдентификатор
//   * ИдРасширения - УникальныйИдентификатор 
Функция СписокИзмененныхРасширений() Экспорт
	Результат = Новый Массив;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ПлюсУстановленныеРасширения.ИдРасширения КАК ИдРасширения,
	|	ПлюсУстановленныеРасширения.ИдВерсииРасширения КАК ИдВерсииРасширения,
	|	ПлюсУстановленныеРасширения.ИдРасширенияВИБ КАК ИдРасширенияВИБ,
	|	ПлюсУстановленныеРасширения.ИмяРасширения КАК ИмяРасширения,
	|	ПлюсУстановленныеРасширения.ВерсияРасширения КАК ВерсияРасширения
	|ИЗ
	|	РегистрСведений.ПлюсУстановленныеРасширения КАК ПлюсУстановленныеРасширения");
	Выборка = Запрос.Выполнить().Выбрать();

	Расширения = Новый Массив;
	Пока Выборка.Следующий() Цикл
		РасширенияБД = РасширенияКонфигурации.Получить(
			Новый Структура("УникальныйИдентификатор", Выборка.ИдРасширенияВИБ),
			ИсточникРасширенийКонфигурации.БазаДанных);
		Если РасширенияБД.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеРасширения = ИнформацияОРасширении(
			Выборка.ИдРасширения, Выборка.ИдВерсииРасширения, Выборка.ИмяРасширения, Выборка.ВерсияРасширения);
		ДанныеРасширения.Вставить("Hash", Base64Строка(РасширенияБД[0].ХешСумма));
		
		Расширения.Добавить(ДанныеРасширения);
	КонецЦикла;
	
	Если Расширения.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	ДанныеЗапроса = Новый Структура();	
	ДанныеЗапроса.Вставить("Extensions", Расширения);
	
	Попытка
		ПараметрыПодключения = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресAPI() + "/VerifyChecksums");
		Соединение = НовоеСоединениеСAPI(20, ПараметрыПодключения);
		Запрос = Новый HTTPЗапрос(ПараметрыПодключения.ПутьНаСервере);
		Запрос.УстановитьТелоИзСтроки(ОбщегоНазначения.ЗначениеВJSON(ДанныеЗапроса));
		
		Ответ = Соединение.ВызватьHTTPМетод("POST", Запрос);
		Если Ответ.КодСостояния = 204 Тогда
			Возврат Результат;
		ИначеЕсли Ответ.КодСостояния <> 200 Тогда		
			ВызватьИсключение Ответ.ПолучитьТелоКакСтроку();
		КонецЕсли;
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = '1С:Плюс . Проверка контрольных сумм расширений'"), 
			УровеньЖурналаРегистрации.Предупреждение, , , 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;	
	
	ДанныеОтвета = ОбщегоНазначения.JSONВЗначение(Ответ.ПолучитьТелоКакСтроку(), , Ложь);
	
	Для Каждого Расширение Из ДанныеОтвета.Extensions Цикл
		ОтветРасширение = Новый Структура();
		ОтветРасширение.Вставить("Имя", Расширение.Name);
		ОтветРасширение.Вставить("Версия", Расширение.Version);
		ОтветРасширение.Вставить("ИдВерсии", Новый УникальныйИдентификатор(Расширение.ExtVersionId));
		ОтветРасширение.Вставить("ИдРасширения", Новый УникальныйИдентификатор(Расширение.ExtId));
				
		Результат.Добавить(ОтветРасширение);		
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// см. ПодсистемыКонфигурацииПереопределяемый.ПриДобавленииПодсистем
Процедура ПриДобавленииПодсистем(МодулиПодсистем) Экспорт
	
	МодулиПодсистем.Добавить("ОбновлениеИнформационнойБазыПлюс");
	
КонецПроцедуры

// см. РаботаВМоделиСервисаПереопределяемый.ПриЗаполненииИсключенийОчисткиДанныхИБ
Процедура ПриЗаполненииИсключенийОчисткиДанныхИБ(ИсключенияОчистки) Экспорт
	
	ИсключенияОчистки.Добавить(Метаданные.РегистрыСведений.ПлюсУстановленныеРасширения.ПолноеИмя());
	
КонецПроцедуры

// см. ВыгрузкаЗагрузкаДанныхПереопределяемый.ПриЗаполненииТиповИсключаемыхИзВыгрузкиЗагрузки
Процедура ПриЗаполненииТиповИсключаемыхИзВыгрузкиЗагрузки(Типы) Экспорт

	Типы.Добавить(Метаданные.РегистрыСведений.ПлюсУстановленныеРасширения);
	Типы.Добавить(Метаданные.РегистрыСведений.ПлюсРасширенияКУстановке);
	Типы.Добавить(Метаданные.РегистрыСведений.ПлюсИсторияРекомендаций);
	Типы.Добавить(Метаданные.РегистрыСведений.ПлюсЖурналОшибок);
	Типы.Добавить(Метаданные.РегистрыСведений.ПлюсНастройки);
	
КонецПроцедуры

// Меняет значение использования 1С:Плюс в приложении
// 
// Параметры:
//  Использовать - Булево - требуемое значение использования 1С:Плюс в приложении
//
// Возвращаемое значение:
//  Булево - при успешном выполнении возвращает в качестве результата значение Истина
// @unavailable-in-safemode
Функция ИзменитьЗначениеИспользованияВПриложении(Использовать) Экспорт
	
	Если Не Пользователи.ЭтоПолноправныйПользователь(, Истина) Тогда
		ВызватьИсключение(НСтр("ru = 'Недостаточно прав для выполнения операции'"));
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Ложь;
	
	Если Не Константы.ПлюсСервисДоступенДляИспользования.Получить() Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Сервис 1С:Плюс недоступен для использования в данной конфигурации'"));
		Возврат Результат;
	КонецЕсли;
	
	Если Не РаботаВМоделиСервиса.РазделениеВключено() Тогда
		Если Использовать Тогда
			Результат = РегистрыСведений.ПлюсНастройки.ВключитьИспользование();
		Иначе
			Результат = РегистрыСведений.ПлюсНастройки.ВыключитьИспользование();
		КонецЕсли;
		
		Возврат Результат;
	КонецЕсли;
	
	Если Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Использование функции %1 доступно только для сеансов с установленным разделением данных.'"),
			"ПлюсСервис.ИзменитьЗначениеИспользованияВПриложении");
	КонецЕсли;
	
	КодПриложения = РаботаВМоделиСервиса.ЗначениеРазделителяСеанса();
	
	Результат = Ложь;
	
	Блокировка = Новый БлокировкаДанных;
	
	Блокировка.Добавить("Константа.ПлюсСервисДоступенДляИспользования");
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОбластиДанных");
	ЭлементБлокировки.УстановитьЗначение("ОбластьДанныхВспомогательныеДанные", КодПриложения);
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПлюсНастройки");
	ЭлементБлокировки.УстановитьЗначение("ОбластьДанныхВспомогательныеДанные", КодПриложения);

	НачатьТранзакцию();
	Попытка
		
		Блокировка.Заблокировать();
		
		Если Использовать Тогда
			Результат = РегистрыСведений.ПлюсНастройки.ВключитьИспользование();
		Иначе
			Результат = РегистрыСведений.ПлюсНастройки.ВыключитьИспользование();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;	
	
	Возврат Результат;
	
КонецФункции

// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииОбработчиковУстановкиПараметровСеанса
// Параметры:
//  ИмяПараметра - Строка
//  УстановленныеПараметры - Массив из Строка
//
Процедура УстановкаПараметровСеанса(Знач ИмяПараметра, УстановленныеПараметры) Экспорт
	
	Если ИмяПараметра = ПлюсСервисКлиентСервер.ИмяПараметраСеансаФормыСРекомендациями() Тогда
		МассивФормСРекомендациями = ПолучитьВсеФормыСРекомендациями();
		ПараметрыСеанса.ПлюсФормыСРекомендациями = Новый ФиксированныйМассив(МассивФормСРекомендациями);
		УстановленныеПараметры.Добавить(ИмяПараметра);
	КонецЕсли;
	
	Если ИмяПараметра = ПлюсСервисКлиентСервер.ИмяПараметраСеансаИсторияОткрытыхФорм() Тогда
		ПараметрыСеанса.ПлюсИсторияОткрытыхФорм = Новый ФиксированныйМассив(Новый Массив);
		УстановленныеПараметры.Добавить(ИмяПараметра);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает признак использования сервиса 1С:Плюс в приложении
//
// Возвращаемое значение:
//  Булево
Функция Используется() Экспорт
	
	Возврат ПлюсСервисВызовСервера.Используется();

КонецФункции

#КонецОбласти
	   
#Область СлужебныйПрограммныйИнтерфейс

Функция АдресAPI() Экспорт	
	Возврат "https://plus.1c.ru/api/v1/Extensions";
КонецФункции

Функция НавигационнаяСсылкаИнтерфейса(ИдентификаторСеанса) Экспорт
	
	Возврат СтрШаблон("%1?id=%2", ПлюсСервисКлиентСервер.АдресСервиса(), ИдентификаторСеанса);
	
КонецФункции

Процедура АктуализироватьРасписаниеСинхронизации() Экспорт
	ЧетыреЧаса = 4 * 60 * 60;
	ДесятьМинут = 600;
	Генератор = Новый ГенераторСлучайныхЧисел;
	РасписанияДня = Новый Массив;
	
	Для Индекс = 0 По 5 Цикл
		ПланируемыйЗапуск = Дата("00010101") + ЧетыреЧаса * Индекс + Генератор.СлучайноеЧисло(0, ЧетыреЧаса);
		Расписание = Новый РасписаниеРегламентногоЗадания;
		Расписание.ВремяНачала = ПланируемыйЗапуск;
		Расписание.ПериодПовтораДней = 1;
		РасписанияДня.Добавить(Расписание);
	КонецЦикла;
	
	ЗапускПослеОбновления = Дата("00010101") + (ТекущаяДатаСеанса() - НачалоДня(ТекущаяДатаСеанса()) + ДесятьМинут);
	Расписание = Новый РасписаниеРегламентногоЗадания;
	Расписание.ВремяНачала = ЗапускПослеОбновления;
	Расписание.ПериодПовтораДней = 1;
	РасписанияДня.Добавить(Расписание);
			
	Расписание = Новый РасписаниеРегламентногоЗадания();
	Расписание.ДетальныеРасписанияДня = РасписанияДня;
	Расписание.ПериодПовтораДней = 1;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Использование", Истина);
	ПараметрыЗадания.Вставить("Ключ", Метаданные.РегламентныеЗадания.ПлюсСинхронизацияССервисом.ИмяМетода);
	ПараметрыЗадания.Вставить("Расписание", Расписание);
	ПараметрыЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 
		Метаданные.РегламентныеЗадания.ПлюсСинхронизацияССервисом.ИнтервалПовтораПриАварийномЗавершении);
	ПараметрыЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 
		Метаданные.РегламентныеЗадания.ПлюсСинхронизацияССервисом.КоличествоПовторовПриАварийномЗавершении);
		
	Отбор = Новый Структура;
	Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ПлюсСинхронизацияССервисом);
	Задания = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	Если Задания.Количество() = 0 Тогда
		ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ПлюсСинхронизацияССервисом);
		Если ОбщегоНазначения.РазделениеВключено() Тогда
			ПараметрыЗадания.Вставить("ОбластьДанных", 0);
			ПараметрыЗадания.Вставить("ЗапланированныйМоментЗапуска", ТекущаяДатаСеанса() + ДесятьМинут);
		КонецЕсли;
		РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
	Иначе
		ПараметрыЗадания.Вставить(
			"ИмяМетода", Метаданные.РегламентныеЗадания.ПлюсСинхронизацияССервисом.ИмяМетода);
		РегламентныеЗаданияСервер.ИзменитьЗадание(Задания[0].УникальныйИдентификатор, ПараметрыЗадания);
	КонецЕсли;
КонецПроцедуры

Процедура АктуализироватьРасписаниеСинхронизацииРекомендаций() Экспорт
	
	Генератор = Новый ГенераторСлучайныхЧисел;
	РасписанияДня = Новый Массив;
	СлучайноеВремя = Генератор.СлучайноеЧисло(0, 86400);
	
	СлучайноеВремяНачала = Дата("00010101") + СлучайноеВремя;
	Расписание = Новый РасписаниеРегламентногоЗадания;
	Расписание.ВремяНачала = СлучайноеВремяНачала;
	РасписанияДня.Добавить(Расписание);
			
	Расписание = Новый РасписаниеРегламентногоЗадания();
	Расписание.ДетальныеРасписанияДня = РасписанияДня;
	Расписание.ПериодПовтораДней = 3;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Использование", Истина);
	ПараметрыЗадания.Вставить("Ключ", Метаданные.РегламентныеЗадания.ПлюсСинхронизацияРекомендаций.ИмяМетода);
	ПараметрыЗадания.Вставить("Расписание", Расписание);
	ПараметрыЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 
		Метаданные.РегламентныеЗадания.ПлюсСинхронизацияРекомендаций.ИнтервалПовтораПриАварийномЗавершении);
	ПараметрыЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 
		Метаданные.РегламентныеЗадания.ПлюсСинхронизацияРекомендаций.КоличествоПовторовПриАварийномЗавершении);
		
	Отбор = Новый Структура;
	Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ПлюсСинхронизацияРекомендаций);
	Задания = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	Если Задания.Количество() = 0 Тогда
		ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ПлюсСинхронизацияРекомендаций);
		ПараметрыЗадания.Вставить("ЗапланированныйМоментЗапуска", ТекущаяДатаСеанса() + СлучайноеВремя);
		РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
	Иначе
		ПараметрыЗадания.Вставить(
			"ИмяМетода", Метаданные.РегламентныеЗадания.ПлюсСинхронизацияРекомендаций.ИмяМетода);
		РегламентныеЗаданияСервер.ИзменитьЗадание(Задания[0].УникальныйИдентификатор, ПараметрыЗадания);
	КонецЕсли;
КонецПроцедуры

// @unavailable-in-safemode
Процедура ПроверкаДоступностиИспользованияСервиса() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПлюсСервисДоступенДляИспользования = Константы.ПлюсСервисДоступенДляИспользования.СоздатьМенеджерЗначения();
	ПлюсСервисДоступенДляИспользования.Значение = Не СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации();
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(ПлюсСервисДоступенДляИспользования);
	
КонецПроцедуры

// @unavailable-in-safemode
Процедура ЗаполнитьНастройкиИспользованияПоУмолчанию() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Событие = НСтр("ru = '1С:Плюс . Начальное заполнение настроек'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Информация, , ,
		НСтр("ru = 'Старт задания начального заполнения'"));
	
	ЗначениеПоУмолчанию = Константы.ПлюсСервисДоступенДляИспользования.Получить();
	Если ЗначениеПоУмолчанию Тогда
		РегистрыСведений.ПлюсНастройки.ВключитьИспользование();
	Иначе
		РегистрыСведений.ПлюсНастройки.ВыключитьИспользование();
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Информация, , ,
		НСтр("ru = 'Завершение задания начального заполнения'"));
		
КонецПроцедуры

Процедура ВыполнитьЗаданиеРасширения(ИдентификаторРасширения, Команда) Экспорт
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ПлюсЗапускЗаданийРасширений);
	
	ИдРасширения = Новый УникальныйИдентификатор(ИдентификаторРасширения);
	Событие = НСтр("ru = '1С:Плюс . Выполнение регламентного задания расширения'");
	
	МетаданныеМодуля = Метаданные.ОбщиеМодули.Найти(Команда);
	Если МетаданныеМодуля = Неопределено Тогда
		Ошибка = СтрШаблон(НСтр("ru = 'Общий модуль %1 не найден'", ОбщегоНазначения.КодОсновногоЯзыка()), Команда);
		ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка, , , Ошибка);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ПлюсУстановленныеРасширения.ИдРасширенияВИБ
	|ИЗ
	|	РегистрСведений.ПлюсУстановленныеРасширения КАК ПлюсУстановленныеРасширения
	|ГДЕ
	|	ПлюсУстановленныеРасширения.ИдРасширения = &ИдРасширения");
	Запрос.УстановитьПараметр("ИдРасширения", ИдРасширения);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И 
		МетаданныеМодуля.РасширениеКонфигурации().УникальныйИдентификатор = Выборка.ИдРасширенияВИБ Тогда
		
		МодульКоманды = ОбщегоНазначения.ОбщийМодуль(Команда);
		МодульКоманды.ВыполнитьКоманду();
	Иначе
		Ошибка = СтрШаблон(
			НСтр("ru = 'Общий модуль %1 не принадлежит расширению %2'", ОбщегоНазначения.КодОсновногоЯзыка()), 
			Команда, 
			ИдентификаторРасширения
		);
		ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка, , , Ошибка);
	КонецЕсли;
КонецПроцедуры

// Синхронизировать список расширений с сервисом.
// 
// Параметры:
//  УстановкаРазрешена - Булево
//  ЗаголовокПрограммы - Строка
// 
// Возвращаемое значение:
//  Строка
// @unavailable-in-safemode
Функция СинхронизироватьСписокРасширенийССервисом(УстановкаРазрешена, ЗаголовокПрограммы = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ИдБазы = ИдентификаторИнформационнойБазы();
		
	ТаблицаРасширений = ТаблицаУстановленныхРасширений();
	
	СинхронизироватьСписокРасширенийСБД(ТаблицаРасширений);
	
	МассивРасширений = Новый Массив;
	Для Каждого ТекСтрока Из ТаблицаРасширений Цикл
		
		ИнформацияОРасширении = ИнформацияОРасширении(
			ТекСтрока.ИдРасширения,
			ТекСтрока.ИдВерсииРасширения,
			ТекСтрока.ИмяРасширения,
			ТекСтрока.ВерсияРасширения);
		МассивРасширений.Добавить(ИнформацияОРасширении);
		
	КонецЦикла;
	ДанныеЗапроса = Новый Структура();
	ДанныеЗапроса.Вставить("InstallationAllowed", УстановкаРазрешена);
	ДанныеЗапроса.Вставить("Extensions", МассивРасширений);
	ДанныеЗапроса.Вставить("ConfigName", Метаданные.Имя);
	ДанныеЗапроса.Вставить("ConfigVersion", Метаданные.Версия);
	ДанныеЗапроса.Вставить("AppCaption", ЗаголовокПрограммы);
	ДанныеЗапроса.Вставить("UserFullName", Строка(Пользователи.АвторизованныйПользователь()));
	ДанныеЗапроса.Вставить("InfobaseId", ИдБазы);
	ДанныеЗапроса.Вставить("TypeOfSession", ТипСеансаПользователя());
	ДанныеЗапроса.Вставить("Vendor", Метаданные.Поставщик);
	
	ПараметрыПодключения = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресAPI() + "/CreateSession");
	Соединение = НовоеСоединениеСAPI(20, ПараметрыПодключения);
	Запрос = Новый HTTPЗапрос(ПараметрыПодключения.ПутьНаСервере);
	Запрос.УстановитьТелоИзСтроки(ОбщегоНазначения.ЗначениеВJSON(ДанныеЗапроса));
	
	Ответ = Соединение.ВызватьHTTPМетод("POST", Запрос);
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение Ответ.ПолучитьТелоКакСтроку();
	КонецЕсли;
	
	ДанныеОтвета = ОбщегоНазначения.JSONВЗначение(Ответ.ПолучитьТелоКакСтроку(), , Ложь);
	
	Возврат ДанныеОтвета.SessionId;

КонецФункции

// Данные расширения по публичному идентификатору.
// 
// Параметры:
//  ИдентификаторСеанса - Строка
//  ПубличныйИдентификатор - Строка
// 
// Возвращаемое значение:
//  см. ОписаниеРасширения 
Функция ДанныеРасширенияПоПубличномуИдентификатору(ИдентификаторСеанса, ПубличныйИдентификатор) Экспорт
	
	ПараметрыПодключения = ОбщегоНазначенияКлиентСервер.СтруктураURI(
		СтрШаблон("%1/%2/ExtensionInfoByPublicId/%3", АдресAPI(), ИдентификаторСеанса, ПубличныйИдентификатор));
	
	Возврат ВыполнитьЗапросДанныхРасширения(ПараметрыПодключения);
	
КонецФункции

// Данные расширения по идентификатору версии.
// 
// Параметры:
//  ИдентификаторСеанса - Строка
//  ИдентификаторВерсии - Строка
// 
// Возвращаемое значение:
//  см. ОписаниеРасширения 
Функция ДанныеРасширенияПоИдентификаторуВерсии(ИдентификаторСеанса, ИдентификаторВерсии) Экспорт
	
	ПараметрыПодключения = ОбщегоНазначенияКлиентСервер.СтруктураURI(
		СтрШаблон("%1/%2/ExtensionInfo/%3", АдресAPI(), ИдентификаторСеанса, ИдентификаторВерсии));
	
	Возврат ВыполнитьЗапросДанныхРасширения(ПараметрыПодключения);
	
КонецФункции

// Добавляет расширение в корзину для дальнейшей усатановки.
// 
// Параметры:
//  ИдентификаторСеанса - Строка
//  ИдентификаторВерсии - Строка
Процедура ДобавитьРасширениеКУстановке(ИдентификаторСеанса, ИдентификаторВерсии) Экспорт

	Метод = СтрШаблон("/%1/Cart/Purchase", Строка(ИдентификаторСеанса));
	ПараметрыПодключения = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресAPI() + Метод);
	Соединение = НовоеСоединениеСAPI(20, ПараметрыПодключения);
	
	Запрос = Новый HTTPЗапрос(ПараметрыПодключения.ПутьНаСервере);
	
	ДанныеРасширения = Новый Структура();
	ДанныеРасширения.Вставить("ExtVersionId", ИдентификаторВерсии);
	
	ТелоОтвета = Новый Структура();
	ТелоОтвета.Вставить("Extensions", Новый Массив());
	ТелоОтвета.Extensions.Добавить(ДанныеРасширения);
	
	Запрос.УстановитьТелоИзСтроки(ОбщегоНазначения.ЗначениеВJSON(ТелоОтвета));
	
	Ответ = Соединение.ВызватьHTTPМетод("POST", Запрос);
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение Ответ.ПолучитьТелоКакСтроку();
	КонецЕсли;

КонецПроцедуры

 
// Параметры:
//  ЭтоУстановкаОбновлений - Булево
// @unavailable-in-safemode
Процедура УстановитьРасширения(ЭтоУстановкаОбновлений) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ПлюсРасширенияКУстановке.ИмяРасширения КАК ИмяРасширения,
	|	ПлюсРасширенияКУстановке.ВерсияРасширения КАК ВерсияРасширения,
	|	ПлюсРасширенияКУстановке.ЗаданиеНаУстановку КАК ЗаданиеНаУстановку,
	|	ПлюсРасширенияКУстановке.ИдСеанса КАК ИдСеанса
	|ИЗ
	|	РегистрСведений.ПлюсРасширенияКУстановке КАК ПлюсРасширенияКУстановке");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаданиеНаУстановку = Выборка.ЗаданиеНаУстановку.Получить();
		
		Попытка
			УстановитьРасширение(ЗаданиеНаУстановку, , , ЭтоУстановкаОбновлений);
		Исключение
			УдалитьРасширениеИзКорзины(ЗаданиеНаУстановку, Выборка.ИдСеанса, ЭтоУстановкаОбновлений);
			ВызватьИсключение;
		КонецПопытки;
		
		ПодтвердитьУстановкуРасширения(
			ЗаданиеНаУстановку,
			Выборка.ИдСеанса,
			ЭтоУстановкаОбновлений);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СинхронизацияССервисом() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ПлюсСинхронизацияССервисом);

	Если Не Используется() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
						  |	ПлюсУстановленныеРасширения.ИдРасширения КАК ИдРасширения,
						  |	ПлюсУстановленныеРасширения.ИдВерсииРасширения КАК ИдВерсииРасширения,
						  |	ПлюсУстановленныеРасширения.ИмяРасширения КАК ИмяРасширения,
						  |	ПлюсУстановленныеРасширения.ВерсияРасширения КАК ВерсияРасширения
						  |ИЗ
						  |	РегистрСведений.ПлюсУстановленныеРасширения КАК ПлюсУстановленныеРасширения");
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	РасширенияНаПроверку = Новый Массив;
	Пока Выборка.Следующий() Цикл
		ИнформацияОРасширении = ИнформацияОРасширении(
			Выборка.ИдРасширения, 
			Выборка.ИдВерсииРасширения, 
			Выборка.ИмяРасширения, 
			Выборка.ВерсияРасширения);
		РасширенияНаПроверку.Добавить(ИнформацияОРасширении);
	КонецЦикла;

	ИдБазы = ИдентификаторИнформационнойБазы();

	ДанныеЗапроса = Новый Структура;
	ДанныеЗапроса.Вставить("ConfigVersion", Метаданные.Версия);
	ДанныеЗапроса.Вставить("ConfigName", Метаданные.Имя);
	ДанныеЗапроса.Вставить("InfobaseId", ИдБазы);
	ДанныеЗапроса.Вставить("Extensions", РасширенияНаПроверку);
	
	ПараметрыПодключения = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресAPI() + "/Sync");
	Соединение = НовоеСоединениеСAPI(60, ПараметрыПодключения);
	Запрос = Новый HTTPЗапрос(ПараметрыПодключения.ПутьНаСервере);
	Запрос.УстановитьТелоИзСтроки(ОбщегоНазначения.ЗначениеВJSON(ДанныеЗапроса));

	Ответ = Соединение.ВызватьHTTPМетод("POST", Запрос);
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение Ответ.ПолучитьТелоКакСтроку();
	КонецЕсли;

	ДанныеОтвета = ОбщегоНазначения.JSONВЗначение(Ответ.ПолучитьТелоКакСтроку(), , Ложь);

	Если ДанныеОтвета.Update.Количество() = 0 И ДанныеОтвета.Delete.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РасширенияКУдалению = Новый Массив;
	Для Каждого СтрокаОтвета Из ДанныеОтвета.Delete Цикл
		РасширенияКУдалению.Добавить(Новый УникальныйИдентификатор(СтрокаОтвета.ExtId));
	КонецЦикла;
	
	ЗаданияНаУстановку = Новый Соответствие();
	
	РасширенияКОбновлению = Новый ТаблицаЗначений();
	РасширенияКОбновлению.Колонки.Добавить("ИдРасширения", Новый ОписаниеТипов("УникальныйИдентификатор"));
	РасширенияКОбновлению.Колонки.Добавить("ИдВерсииРасширения", Новый ОписаниеТипов("УникальныйИдентификатор"));
	РасширенияКОбновлению.Колонки.Добавить("ИзменяетСтруктуруДанных", Новый ОписаниеТипов("Булево"));
	Для Каждого СтрокаОтвета Из ДанныеОтвета.Update Цикл
		ИдВерсии = Новый УникальныйИдентификатор(СтрокаОтвета.ExtVersionId);
		НовСтрока = РасширенияКОбновлению.Добавить();
		НовСтрока.ИдВерсииРасширения = ИдВерсии;
		НовСтрока.ИдРасширения = Новый УникальныйИдентификатор(СтрокаОтвета.ExtId);
		НовСтрока.ИзменяетСтруктуруДанных = СтрокаОтвета.ModifiesDataStructure;
		
		ЗаданияНаУстановку.Вставить(ИдВерсии, СтрокаОтвета);
	КонецЦикла;
	
	ЗапросОбновление = Новый Запрос("ВЫБРАТЬ
	|	КОбновлению.ИдРасширения КАК ИдРасширения,
	|	КОбновлению.ИдВерсииРасширения КАК ИдВерсииРасширения,
	|	КОбновлению.ИзменяетСтруктуруДанных КАК ИзменяетСтруктуруДанных
	|ПОМЕСТИТЬ втКОбновлению
	|ИЗ
	|	&РасширенияКОбновлению КАК КОбновлению
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлюсУстановленныеРасширения.ИдРасширения КАК ИдРасширения,
	|	КОбновлению.ИдВерсииРасширения КАК ИдВерсииРасширения,
	|	ПлюсУстановленныеРасширения.ОбластьДанныхВспомогательныеДанные КАК Область,
	|	Ложь КАК Удалить,
	|	КОбновлению.ИзменяетСтруктуруДанных КАК ИзменяетСтруктуруДанных,
	|	ПлюсУстановленныеРасширения.ИмяРасширения КАК ИмяРасширения,
	|	ПлюсУстановленныеРасширения.ВерсияРасширения КАК ВерсияРасширения
	|ИЗ
	|	РегистрСведений.ПлюсУстановленныеРасширения КАК ПлюсУстановленныеРасширения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКОбновлению КАК КОбновлению
	|		ПО КОбновлению.ИдРасширения = ПлюсУстановленныеРасширения.ИдРасширения
	|		И КОбновлению.ИдВерсииРасширения <> ПлюсУстановленныеРасширения.ИдВерсииРасширения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПлюсУстановленныеРасширения.ИдРасширения КАК ИдРасширения,
	|	ПлюсУстановленныеРасширения.ИдВерсииРасширения КАК ИдВерсииРасширения,
	|	ПлюсУстановленныеРасширения.ОбластьДанныхВспомогательныеДанные КАК Область,
	|	Истина КАК Удалить,
	|	Ложь,
	|	ПлюсУстановленныеРасширения.ИмяРасширения,
	|	ПлюсУстановленныеРасширения.ВерсияРасширения
	|ИЗ
	|	РегистрСведений.ПлюсУстановленныеРасширения КАК ПлюсУстановленныеРасширения
	|ГДЕ
	|	ПлюсУстановленныеРасширения.ИдРасширения В (&РасширенияКУдалению)
	|ИТОГИ
	|ПО
	|	Область");
	
	ЗапросОбновление.УстановитьПараметр("РасширенияКУдалению", РасширенияКУдалению);
	ЗапросОбновление.УстановитьПараметр("РасширенияКОбновлению", РасширенияКОбновлению);
	
	ВыборкаОбласть = ЗапросОбновление.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОбласть.Следующий() Цикл
		
		Если ВыборкаОбласть.Область <> 0 Тогда
			РаботаВМоделиСервиса.ВойтиВОбластьДанных(ВыборкаОбласть.Область);
		КонецЕсли;
		
		СинхронизироватьСписокРасширенийСБД(); //@skip-check query-in-loop
		ИзмененныеРасширения = ИдентификаторыИзмененныхРасширений(); //@skip-check query-in-loop
		
		ВыборкаРасширения = ВыборкаОбласть.Выбрать();
		ДляУстановкиВМонопольном = Новый Массив;
		Пока ВыборкаРасширения.Следующий() Цикл
			
			Если Не РасширениеМожноСинхронизовать(ВыборкаРасширения, ИзмененныеРасширения) Тогда
				Продолжить;
			КонецЕсли;
			
			Попытка
				//@skip-check unavailable-in-safemode
				СинхронизироватьРасширение(
					ВыборкаРасширения, ЗаданияНаУстановку, ВыборкаОбласть.Область, ДляУстановкиВМонопольном);
			Исключение
				ЗаписьЖурналаРегистрации(СобытиеСинхронизацияРасширения(),
					УровеньЖурналаРегистрации.Ошибка,,,
					СтрШаблон(НСтр("ru = 'Пропущена синхронизация расширения %1 версии %2 
						|%3'"), 
						ВыборкаРасширения.ИмяРасширения, 
						ВыборкаРасширения.ВерсияРасширения, 
						ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
			КонецПопытки;
		КонецЦикла;
		
		Если ДляУстановкиВМонопольном.Количество() > 0 Тогда
			СеансыПользователейИнформационнойБазы = СеансыПользователейИнформационнойБазы();
			Если СеансыПользователейИнформационнойБазы.Количество() = 0 Тогда
				ВключитьМонопольныйРежим();
				//@skip-check unavailable-in-safemode
				УстановитьРасширения(Истина); 
				СнятьМонопольныйРежим();	
			Иначе
				СерверныеОповещения.ОтправитьСерверноеОповещение(
					ПлюсСервисКлиентСервер.ИмяСерверногоОповещенияНеобходимаУстановкаМонопольногоРежима(),
					ДляУстановкиВМонопольном,
					Неопределено,
					Истина);
			КонецЕсли;
		КонецЕсли;
		
		Если ВыборкаОбласть.Область <> 0 Тогда
			РаботаВМоделиСервиса.ВыйтиИзОбластиДанных();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура СинхронизацияРекомендаций() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ПлюсСинхронизацияРекомендаций);

	Если Не РекомендацииДоступны() Тогда
		Возврат;
	КонецЕсли;
	
	ТекстОшибкиПодробно = "";

	СвойстваНаборЗаписей = РегистрыСведений.ПлюсЗначенияСвойствПриложения.СоздатьНаборЗаписей();
	СвойстваНаборЗаписей.Прочитать();

	Для Каждого СтрокаНабора Из СвойстваНаборЗаписей Цикл

		Запрос = Новый Запрос;
		Запрос.Текст = СтрокаНабора.ТекстЗапроса;
		Попытка
			Результат = Запрос.Выполнить(); //@skip-check query-in-loop
			СтрокаНабора.Значение = Не Результат.Пустой();
			СтрокаНабора.ДатаОбновления = ТекущаяДатаСеанса();
		Исключение
			ИнфоОбОшибке = ИнформацияОбОшибке();
			ТекстОшибкиКратко = СтрШаблон("Значение не обновлено.
										  | %1
										  |Подробно см. в журнале ошибок 1С:Плюс.",
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнфоОбОшибке));
			ТекстОшибкиПодробно = ТекстОшибкиПодробно + Символы.ПС + СтрШаблон("Свойство %1 не рассчитано.
																			   |%2", Строка(СтрокаНабора.Свойство),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнфоОбОшибке));
			СтрокаНабора.ТекстОшибки = ТекстОшибкиКратко;
		КонецПопытки;

		Попытка
			СвойстваНаборЗаписей.Записать();
		Исключение
			ИнфоОбОшибке = ИнформацияОбОшибке();
			ТекстОшибкиПодробно = ТекстОшибкиПодробно + Символы.ПС + СтрШаблон("Не удалось записать значения свойств приложения.
																			   |%1",
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнфоОбОшибке));
		КонецПопытки;

	КонецЦикла;

	Если ЗначениеЗаполнено(ТекстОшибкиПодробно) Тогда
		ЗарегистрироватьОшибку("Расчет свойств", УровеньЖурналаРегистрации.Ошибка, ТекстОшибкиПодробно);
	КонецЕсли;
	
	ТаблицаЗначенийСвойств = ВыгрузитьЗаписиИзРегистра("ПлюсЗначенияСвойствПриложения");

	ЗначенияСвойствПриложения = Новый Массив;
	Для Каждого СтрокаСвойство Из ТаблицаЗначенийСвойств Цикл
		ШаблонСвойства = ШаблонСвойствоПриложения();
		ШаблонСвойства.IndicatorId = Строка(СтрокаСвойство.Свойство);
		ШаблонСвойства.Date = XMLСтрока(СтрокаСвойство.ДатаОбновления);
		ШаблонСвойства.Value = Строка(СтрокаСвойство.Значение);
		ШаблонСвойства.ErrorInfo = Строка(СтрокаСвойство.ТекстОшибки);
		ЗначенияСвойствПриложения.Добавить(ШаблонСвойства);
	КонецЦикла;

	ТаблицаЖурналОшибок = ВыгрузитьЗаписиИзРегистра("ПлюсЖурналОшибок");
	
	ЖурналОшибок = Новый Массив;
	Для Каждого СтрокаОшибка Из ТаблицаЖурналОшибок Цикл
		ШаблонСвойства = ШаблонИнформацияОбОшибке();
		ШаблонСвойства.Date = XMLСтрока(СтрокаОшибка.Дата);
		ШаблонСвойства.Event = Строка(СтрокаОшибка.Событие);
		ШаблонСвойства.Level = Строка(СтрокаОшибка.Уровень);
		ШаблонСвойства.Comment = Строка(СтрокаОшибка.Комментарий);
		ЖурналОшибок.Добавить(ШаблонСвойства);
	КонецЦикла;

	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
						  |	ПлюсУстановленныеРасширения.ИдРасширения КАК ИдРасширения,
						  |	ПлюсУстановленныеРасширения.ИдВерсииРасширения КАК ИдВерсииРасширения,
						  |	ПлюсУстановленныеРасширения.ИмяРасширения КАК ИмяРасширения,
						  |	ПлюсУстановленныеРасширения.ВерсияРасширения КАК ВерсияРасширения
						  |ИЗ
						  |	РегистрСведений.ПлюсУстановленныеРасширения КАК ПлюсУстановленныеРасширения");
	Выборка = Запрос.Выполнить().Выбрать();

	УстановленныеРасширения = Новый Массив;
	Пока Выборка.Следующий() Цикл
		ИнформацияОРасширении = ИнформацияОРасширении(
			Выборка.ИдРасширения, 
			Выборка.ИдВерсииРасширения, 
			Выборка.ИмяРасширения, 
			Выборка.ВерсияРасширения);
		УстановленныеРасширения.Добавить(ИнформацияОРасширении);
	КонецЦикла;
	
	ИдБазы = ИдентификаторИнформационнойБазы();

	ДанныеЗапроса = Новый Структура;
	ДанныеЗапроса.Вставить("ConfigVersion", Метаданные.Версия);
	ДанныеЗапроса.Вставить("ConfigName", Метаданные.Имя);
	ДанныеЗапроса.Вставить("InfobaseId", ИдБазы);
	ДанныеЗапроса.Вставить("Indicators", ЗначенияСвойствПриложения);
	ДанныеЗапроса.Вставить("ErrorLog", ЖурналОшибок);
	ДанныеЗапроса.Вставить("Extensions", УстановленныеРасширения);
	
	ПараметрыПодключения = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресAPI() + "/SyncSuggestions");
	Соединение = НовоеСоединениеСAPI(60, ПараметрыПодключения);
	Запрос = Новый HTTPЗапрос(ПараметрыПодключения.ПутьНаСервере);
	Запрос.УстановитьТелоИзСтроки(ОбщегоНазначения.ЗначениеВJSON(ДанныеЗапроса));

	Ответ = Соединение.ВызватьHTTPМетод("POST", Запрос);
	Если Ответ.КодСостояния <> 200 Тогда

		НаборЗаписей = РегистрыСведений.ПлюсЗначенияСвойствПриложения.СоздатьНаборЗаписей();
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда
			НаборЗаписей.Загрузить(ТаблицаЗначенийСвойств);
			НаборЗаписей.Записать();
		КонецЕсли;

		НаборЗаписей = РегистрыСведений.ПлюсЖурналОшибок.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(ТаблицаЖурналОшибок);
		НаборЗаписей.Записать(Ложь);

		ВызватьИсключение Ответ.ПолучитьТелоКакСтроку();
	КонецЕсли;

	ДанныеОтвета = ОбщегоНазначения.JSONВЗначение(Ответ.ПолучитьТелоКакСтроку(), , Ложь);

	Если ДанныеОтвета.Свойство("MaxPerDay") Тогда
		ОбновитьНастройки("МаксимальноеКоличествоРекомендацийВДень", ДанныеОтвета.MaxPerDay);
	КонецЕсли;

	Если ДанныеОтвета.Свойство("Indicators") И ДанныеОтвета.Indicators.Количество() > 0 Тогда
		ОбновитьЗначенияСвойствПриложения(ДанныеОтвета.Indicators, ТаблицаЗначенийСвойств);
	КонецЕсли;

	УдалятьНеактуальнуюИсториюПоказов = Ложь;
	Если ДанныеОтвета.Свойство("Suggestions") Тогда
		НаборЗаписей = РегистрыСведений.ПлюсРасписаниеРекомендаций.СоздатьНаборЗаписей();
		НаборЗаписей.Прочитать();
		НаборЗаписей.Очистить();
		Для Каждого Элемент Из ДанныеОтвета.Suggestions Цикл
			СтрокаНабора = НаборЗаписей.Добавить();
			СтрокаНабора.ИдРасширения = Строка(Элемент.ExtPublicId);
			СтрокаНабора.ЦелевоеДействиеТип = Число(Элемент.TargetActionType);
			СтрокаНабора.ЦелевоеДействиеОбъект = Строка(Элемент.TargetActionObject);
			СтрокаНабора.ЦелевоеДействиеФорма = Строка(Элемент.TargetActionForm);
			СтрокаНабора.ДатаНачалаОтображения = XMLЗначение(Тип("Дата"), Элемент.StartDate);
			СтрокаНабора.ДатаОкончанияОтображения = XMLЗначение(Тип("Дата"), Элемент.EndDate);
			СтрокаНабора.КоличествоПоказовПлан = Число(Элемент.Count);
			СтрокаНабора.Заголовок = Строка(Элемент.Title);
			СтрокаНабора.Описание = Строка(Элемент.Text);
			Если ЗначениеЗаполнено(Элемент.Logo) Тогда
				СтрокаНабора.Логотип = Новый ХранилищеЗначения(Base64Значение(Элемент.Logo));
			КонецЕсли;
		КонецЦикла;
		Попытка
			НаборЗаписей.Записать();
			УдалятьНеактуальнуюИсториюПоказов = Истина;
		Исключение
			ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Комментарий = СтрШаблон(НСтр("ru = 'Ошибка при получении расписания рекомендаций.
									|%1'"), ПодробноеПредставлениеОшибки);
			ЗарегистрироватьОшибку("Синхронизация с сервисом", УровеньЖурналаРегистрации.Ошибка, Комментарий);
		КонецПопытки;
	КонецЕсли;
	
	Если УдалятьНеактуальнуюИсториюПоказов Тогда
		УдалитьНеактуальнуюИсториюПоказов();
	КонецЕсли;	
	
КонецПроцедуры

// @unavailable-in-safemode
// 
// Параметры:
//  ДанныеЗадания - Структура
//  ИдСеанса - УникальныйИдентификатор
//  НайденыРРД - Булево
Процедура ЗаписатьЗаданиеНаУстановку(Знач ДанныеЗадания, ИдСеанса, НайденыРРД) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		Для Каждого Расширение Из ДанныеЗадания.Extensions Цикл
			Запись = РегистрыСведений.ПлюсРасширенияКУстановке.СоздатьМенеджерЗаписи();
			Запись.ИдСеанса = Новый УникальныйИдентификатор(ИдСеанса);
			Запись.ИдРасширения = Новый УникальныйИдентификатор(Расширение.ExtId);
			Запись.ИдВерсииРасширения = Новый УникальныйИдентификатор(Расширение.ExtVersionId);
			Запись.ИмяРасширения = Расширение.Name;
			Запись.ВерсияРасширения = Расширение.Version;
			Запись.ЗаданиеНаУстановку = Новый ХранилищеЗначения(Расширение, Новый СжатиеДанных(9));
			Запись.Записать();
			
			Если Расширение.ModifiesDataStructure Тогда
				НайденыРРД = Истина;
			КонецЕсли;
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

// @unavailable-in-safemode
// 
// Параметры:
//  ДанныеРасширения - ВыборкаИзРезультатаЗапроса - Данные расширения
Процедура УдалитьРасширение(ДанныеРасширения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.ПлюсУстановленныеРасширения.СоздатьМенеджерЗаписи();
	Запись.ИдРасширения = ДанныеРасширения.ИдРасширения;
	Запись.Прочитать();
	
	Если Не Запись.Выбран() Тогда
		Возврат;
	КонецЕсли;
	
	РасширенияБД = РасширенияКонфигурации.Получить(
		Новый Структура("УникальныйИдентификатор", Запись.ИдРасширенияВИБ),
		ИсточникРасширенийКонфигурации.БазаДанных);
	
	Если РасширенияБД.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИзменяетСтруктуруДанных = РасширенияБД[0].ИзменяетСтруктуруДанных();
	
	Попытка
		Если ИзменяетСтруктуруДанных Тогда
			УдалитьРасширениеИзменяющееДанные(ДанныеРасширения, РасширенияБД, Запись);
		Иначе
			УдалитьРасширениеНеИзменяющееДанные(ДанныеРасширения, РасширенияБД, Запись);
		КонецЕсли;		
	Исключение
		
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		
		ТекстОшибки = 
			СтрШаблон(НСтр("ru = 'Ошибка удаления расширения %1 версии %2'"), 
				ДанныеРасширения.ИмяРасширения, ДанныеРасширения.ВерсияРасширения)
			+ Символы.ПС
			+ ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ИмяСобытия = НСтр("ru = 'Расширения 1С:Плюс.Ошибка удаления расширения'",
				ОбщегоНазначения.КодОсновногоЯзыка());
		ЗаписьЖурналаРегистрации(ИмяСобытия, 
			УровеньЖурналаРегистрации.Ошибка, , 
			Строка(ДанныеРасширения.ИдРасширения), 
			ТекстОшибки);
		ВызватьИсключение ТекстОшибки;
		
	КонецПопытки;
	
	ЗаписьЖурналаРегистрации(НСтр("ru = 'Расширения 1С:Плюс.Удаление расширения'"),
		УровеньЖурналаРегистрации.Информация, , ,
		СтрШаблон(НСтр("ru = 'Удалено расширение %1 версии %2'"),
			ДанныеРасширения.ИмяРасширения,
			ДанныеРасширения.ВерсияРасширения));
		
КонецПроцедуры

// @unavailable-in-safemode
// 
// Параметры:
//  ИдРасширения - УникальныйИдентификатор
// 
// Возвращаемое значение:
//  Булево - Расширение изменяет структуру данных
Функция РасширениеИзменяетСтруктуруДанных(ИдРасширения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.ПлюсУстановленныеРасширения.СоздатьМенеджерЗаписи();
	Запись.ИдРасширения = ИдРасширения;
	Запись.Прочитать();
	
	Если Не Запись.Выбран() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РасширенияБД = РасширенияКонфигурации.Получить(
		Новый Структура("УникальныйИдентификатор", Запись.ИдРасширенияВИБ),
		ИсточникРасширенийКонфигурации.БазаДанных);
	
	Если РасширенияБД.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат РасширенияБД[0].ИзменяетСтруктуруДанных();
	
КонецФункции

Процедура ОтправитьИнформациюОбУстановленныхРасширенияхВМС(Параметры) Экспорт
	ЭтоРазделенныйСеанс = ОбщегоНазначения.РазделениеВключено()
		И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных();
	Если Не ЭтоРазделенныйСеанс Тогда
		Возврат;		
	КонецЕсли;	
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ПлюсУстановленныеРасширения.ИмяРасширения КАК Имя,
	|	ПлюсУстановленныеРасширения.ВерсияРасширения КАК Версия,
	|	ПлюсУстановленныеРасширения.ИдВерсииРасширения КАК ИдВерсии,
	|	ПлюсУстановленныеРасширения.ИдРасширения КАК Ид,
	|	ПлюсУстановленныеРасширения.ДатаУстановкиОбновления КАК ДатаУстановкиОбновления
	|ИЗ
	|	РегистрСведений.ПлюсУстановленныеРасширения КАК ПлюсУстановленныеРасширения");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ИнформацияОбУстановке = Новый Структура;
		ИнформацияОбУстановке.Вставить("Name", Выборка.Имя);
		ИнформацияОбУстановке.Вставить("Version", Выборка.Версия);
		ИнформацияОбУстановке.Вставить("ExtVersionId", Строка(Выборка.ИдВерсии));
		ИнформацияОбУстановке.Вставить("ExtId", Строка(Выборка.Ид));
		
		НачатьТранзакцию();
		Попытка
			ОповеститьМенеджерСервисаОбИзмененииИнсталляции(ИнформацияОбУстановке, Выборка.ДатаУстановкиОбновления);
			ЗафиксироватьТранзакцию();
		Исключение
			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию();
			КонецЕсли;
			ВызватьИсключение;
		КонецПопытки;		
	КонецЦикла;
КонецПроцедуры

Процедура УстановитьРасширение(ЗаданиеНаУстановку, УстановкаИзВременныхФайлов = Ложь, ДанныеРасширения = Неопределено,
	ЭтоУстановкаОбновлений = Ложь) Экспорт
	
	ЭтоРазделенныйСеанс = ОбщегоНазначения.РазделениеВключено()
		И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных();

	Если ДанныеРасширения <> Неопределено Тогда
		ДвоичныеДанныеРасширения = ДанныеРасширения;
	Иначе
		ДвоичныеДанныеРасширения = ПолучитьДвоичныеДанныеРасширения(
			ЗаданиеНаУстановку.PathToData, УстановкаИзВременныхФайлов);
	КонецЕсли;		
	
	ИдРасширения = Новый УникальныйИдентификатор(ЗаданиеНаУстановку.ExtId);
	ИдВерсииРасширения = Новый УникальныйИдентификатор(ЗаданиеНаУстановку.ExtVersionId);
	
	Блокировка = Новый БлокировкаДанных();
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПлюсУстановленныеРасширения");
	ЭлементБлокировки.УстановитьЗначение("ИдРасширения", ИдРасширения);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ИзменяетСтруктуруДанных = ЗаданиеНаУстановку.ModifiesDataStructure; 
	
	Если Не ИзменяетСтруктуруДанных Тогда
		НачатьТранзакцию();
	КонецЕсли;
	Попытка
		Если Не ИзменяетСтруктуруДанных Тогда
			Блокировка.Заблокировать();
		КонецЕсли;
		
		УстановитьОтключениеБезопасногоРежима(Истина);
		УстановитьПривилегированныйРежим(Истина);
		ОбъектМетаданныхКонфигурация = Новый ОбъектМетаданныхКонфигурация(ДвоичныеДанныеРасширения);
		НайденныеРасширенияИБ = РасширенияКонфигурации.Получить(
			Новый Структура("Имя", ОбъектМетаданныхКонфигурация.Имя));
		
		Если НайденныеРасширенияИБ.Количество() = 0 Тогда
			РасширениеИБ = РасширенияКонфигурации.Создать();
		Иначе
			РасширениеИБ = НайденныеРасширенияИБ[0];
		КонецЕсли;
		
		Запись = РегистрыСведений.ПлюсУстановленныеРасширения.СоздатьМенеджерЗаписи();
		Запись.ИдРасширения = ИдРасширения;
		Запись.Прочитать();
		Если Запись.Выбран() Тогда
			РасширенияБД = РасширенияКонфигурации.Получить(
				Новый Структура("УникальныйИдентификатор", Запись.ИдРасширенияВИБ)
				, ИсточникРасширенийКонфигурации.БазаДанных);
			Если РасширенияБД.Количество() <> 0 Тогда 
				РасширениеИБ = РасширенияБД[0];
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоРазделенныйСеанс Тогда
			РасширениеИБ.БезопасныйРежим = ИмяПрофилияБезопасности();                         
			РасширениеИБ.ОбластьДействия = ОбластьДействияРасширенияКонфигурации.РазделениеДанных;
		Иначе
			РасширениеИБ.БезопасныйРежим = Ложь;                                              
			РасширениеИБ.ОбластьДействия = ОбластьДействияРасширенияКонфигурации.ИнформационнаяБаза;
		КонецЕсли;
		РасширениеИБ.ЗащитаОтОпасныхДействий = ОбщегоНазначения.ОписаниеЗащитыБезПредупреждений();
		
		ДанныеПроверки = РасширениеИБ.ПроверитьВозможностьПрименения(ДвоичныеДанныеРасширения);
		СтрокиПроверки = Новый Массив;
		Для Каждого ОписаниеПроблемы Из ДанныеПроверки Цикл
			
			Если ОписаниеПроблемы.Важность <> ВажностьПроблемыПримененияРасширенияКонфигурации.Критичная Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокиПроверки.Добавить(
				СтрШаблон("%1 %2", ОписаниеПроблемы.Важность, ОписаниеПроблемы.Описание));
		КонецЦикла;
		Если СтрокиПроверки.Количество() <> 0 Тогда
			ВызватьИсключение СтрСоединить(СтрокиПроверки, Символы.ПС);
		КонецЕсли;
		
		ТекстОшибки = ЗаписатьРасширениеВПопытке(РасширениеИБ, ДвоичныеДанныеРасширения);

		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			КодОшибки = "";
			Если ИзменяетСтруктуруДанных Тогда
				Попытка
					ВключитьМонопольныйРежим();
				Исключение
					// Записывается первая ошибка
					ЗаписьЖурналаРегистрации(ИмяСобытияОшибкаЗаписиРасширения(),
						УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
					
					КодОшибки = КодОшибкиМонопольногоДоступа();
					ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				КонецПопытки;

				Если МонопольныйРежим() Тогда
					ТекстОшибки = ЗаписатьРасширениеВПопытке(РасширениеИБ, ДвоичныеДанныеРасширения);
					СнятьМонопольныйРежим();
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТекстОшибки) Тогда
				ВызватьИсключение(ТекстОшибки, , КодОшибки);
			КонецЕсли;
		КонецЕсли;		
		
		АктуализироватьРегламентныеЗадания(ЗаданиеНаУстановку);
		
		Запись.ИдРасширения = ИдРасширения;
		Запись.ИдВерсииРасширения = ИдВерсииРасширения;
		Запись.ИдРасширенияВИБ = РасширениеИБ.УникальныйИдентификатор;
		Запись.ИмяРасширения = ЗаданиеНаУстановку.Name;
		Запись.ВерсияРасширения = ЗаданиеНаУстановку.Version;
		Запись.ДатаУстановкиОбновления = ТекущаяУниверсальнаяДата();
		Запись.ЗаданиеНаУстановку = Новый ХранилищеЗначения(ЗаданиеНаУстановку, Новый СжатиеДанных(9));
		Запись.Записать();
		
		Если ЭтоРазделенныйСеанс Тогда
			ОповеститьМенеджерСервисаОбИзмененииИнсталляции(ЗаданиеНаУстановку);
		КонецЕсли;
		
		Если Не ИзменяетСтруктуруДанных Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		
		Если ЭтоУстановкаОбновлений Тогда
			ТекстШаблона = НСтр("ru = 'Ошибка установки обновления расширения %1 - версии %2'");
		Иначе
			ТекстШаблона = НСтр("ru = 'Ошибка установки расширения %1 версии %2'");
		КонецЕсли;
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ТекстОшибки = 
			СтрШаблон(ТекстШаблона, ЗаданиеНаУстановку.Name, ЗаданиеНаУстановку.Version)
			+ Символы.ПС
			+ ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
			
		ЗаписьЖурналаРегистрации(ИмяСобытияОшибкаЗаписиРасширения(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ВызватьИсключение(ТекстОшибки,, ИнформацияОбОшибке.Код);
	КонецПопытки;	
	
	Если ЭтоУстановкаОбновлений Тогда
		ИмяСобытия = НСтр("ru = 'Расширения 1С:Плюс.Установка обновления'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		ТекстШаблона = НСтр("ru = 'Установлено обновление расширения %1 - версия %2'");
	Иначе
		ИмяСобытия = НСтр("ru = 'Расширения 1С:Плюс.Установка расширения'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		ТекстШаблона = НСтр("ru = 'Установлено расширение %1 версии %2'");
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(ИмяСобытия,
		УровеньЖурналаРегистрации.Информация, , ,
		СтрШаблон(ТекстШаблона, ЗаданиеНаУстановку.Name, ЗаданиеНаУстановку.Version));
			
КонецПроцедуры

Процедура УдалитьРасширениеИзКорзины(ЗаданиеНаУстановку, ИдСеанса, ЭтоУстановкаОбновлений = Ложь) Экспорт
	
	УдалитьРасширениеИзСпискаКУстановке(ЗаданиеНаУстановку.ExtId);
	
	Если ЭтоУстановкаОбновлений Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеРасширения = Новый Структура();
	ДанныеРасширения.Вставить("ExtVersionId", ЗаданиеНаУстановку.ExtVersionId);
	
	ДанныеДляУдаления = Новый Структура();
	ДанныеДляУдаления.Вставить("Extensions", Новый Массив());
	ДанныеДляУдаления.Extensions.Добавить(ДанныеРасширения);
	
	УдалитьРасширениеИзКорзиныНаСервере(ДанныеДляУдаления, ИдСеанса);
	
КонецПроцедуры

Процедура УдалитьРасширениеИзКорзиныНаСервере(ДанныеДляУдаления, ИдСеанса) Экспорт
	
	Метод = СтрШаблон("/%1/Cart/Delete", Строка(ИдСеанса));
	ПараметрыПодключения = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресAPI() + Метод);
	Соединение = НовоеСоединениеСAPI(20, ПараметрыПодключения);
	
	Запрос = Новый HTTPЗапрос(ПараметрыПодключения.ПутьНаСервере);
	Запрос.УстановитьТелоИзСтроки(ОбщегоНазначения.ЗначениеВJSON(ДанныеДляУдаления));
	
	Ответ = Соединение.ВызватьHTTPМетод("POST", Запрос);
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение Ответ.ПолучитьТелоКакСтроку();
	КонецЕсли;
	
КонецПроцедуры

Функция ВыбратьРекомендациюИзОчереди(Пользователь, ИсторияОткрытыхФорм) Экспорт

	ТЗИсторияОткрытыхФорм = СформироватьТаблицуИзМассива(ИсторияОткрытыхФорм);
	
	ТекущаяДата = НачалоДня(ТекущаяДатаСеанса());
	ЛимитПоказовКаждойРекомендацииВДень = 1;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ИсторияОткрытыхФорм.ТипИсточника КАК ТипИсточника,
	|	ИсторияОткрытыхФорм.ИмяОбъекта КАК ИмяОбъекта,
	|	ИсторияОткрытыхФорм.ИмяФормы КАК ИмяФормы
	|ПОМЕСТИТЬ ВТ_ИсторияОткрытыхФорм
	|ИЗ
	|	&ТЗИсторияОткрытыхФорм КАК ИсторияОткрытыхФорм
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлюсИсторияРекомендаций.Выполнено КАК Выполнено,
	|	КОЛИЧЕСТВО(ИСТИНА) КАК КоличествоПоказовФакт,
	|	СУММА(ВЫБОР
	|		КОГДА ДЕНЬ(ПлюсИсторияРекомендаций.ДатаВыполнения) = ДЕНЬ(&ТекущаяДата)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоПоказовСегодня,
	|	МАКСИМУМ(ПлюсИсторияРекомендаций.ДатаВыполнения) КАК ПоследнняяДатаВыполнения,
	|	ПлюсИсторияРекомендаций.ИдРасширения КАК ИдРасширения,
	|	ПлюсИсторияРекомендаций.ЦелевоеДействиеТип КАК ЦелевоеДействиеТип,
	|	ПлюсИсторияРекомендаций.ЦелевоеДействиеОбъект КАК ЦелевоеДействиеОбъект,
	|	ПлюсИсторияРекомендаций.ЦелевоеДействиеФорма КАК ЦелевоеДействиеФорма
	|ПОМЕСТИТЬ ВТ_ИсторияРекомендаций
	|ИЗ
	|	РегистрСведений.ПлюсИсторияРекомендаций КАК ПлюсИсторияРекомендаций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИсторияОткрытыхФорм КАК ИсторияОткрытыхФорм
	|		ПО ПлюсИсторияРекомендаций.Пользователь = &Пользователь
	|		И ПлюсИсторияРекомендаций.ЦелевоеДействиеТип = ИсторияОткрытыхФорм.ТипИсточника
	|		И ПлюсИсторияРекомендаций.ЦелевоеДействиеОбъект = ИсторияОткрытыхФорм.ИмяОбъекта
	|		И ПлюсИсторияРекомендаций.ЦелевоеДействиеФорма = ИсторияОткрытыхФорм.ИмяФормы
	|		И ПлюсИсторияРекомендаций.Выполнено
	|СГРУППИРОВАТЬ ПО
	|	ПлюсИсторияРекомендаций.Выполнено,
	|	ПлюсИсторияРекомендаций.ИдРасширения,
	|	ПлюсИсторияРекомендаций.ЦелевоеДействиеТип,
	|	ПлюсИсторияРекомендаций.ЦелевоеДействиеОбъект,
	|	ПлюсИсторияРекомендаций.ЦелевоеДействиеФорма
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПлюсРасписаниеРекомендаций.ИдРасширения КАК ИдРасширения,
	|	ПлюсРасписаниеРекомендаций.ЦелевоеДействиеТип КАК ЦелевоеДействиеТип,
	|	ПлюсРасписаниеРекомендаций.ЦелевоеДействиеОбъект КАК ЦелевоеДействиеОбъект,
	|	ПлюсРасписаниеРекомендаций.ЦелевоеДействиеФорма КАК ЦелевоеДействиеФорма,
	|	ПлюсРасписаниеРекомендаций.Заголовок КАК Заголовок,
	|	ПлюсРасписаниеРекомендаций.Описание КАК Описание,
	|	ПлюсРасписаниеРекомендаций.Логотип КАК Логотип,
	|	&Пользователь КАК Пользователь
	|ИЗ
	|	РегистрСведений.ПлюсРасписаниеРекомендаций КАК ПлюсРасписаниеРекомендаций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ИсторияОткрытыхФорм КАК ИсторияОткрытыхФорм
	|		ПО ПлюсРасписаниеРекомендаций.ЦелевоеДействиеТип = ИсторияОткрытыхФорм.ТипИсточника
	|		И ПлюсРасписаниеРекомендаций.ЦелевоеДействиеОбъект = ИсторияОткрытыхФорм.ИмяОбъекта
	|		И ПлюсРасписаниеРекомендаций.ЦелевоеДействиеФорма = ИсторияОткрытыхФорм.ИмяФормы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсторияРекомендаций КАК ИсторияРекомендаций
	|		ПО ПлюсРасписаниеРекомендаций.ИдРасширения = ИсторияРекомендаций.ИдРасширения
	|		И ПлюсРасписаниеРекомендаций.ЦелевоеДействиеТип = ИсторияРекомендаций.ЦелевоеДействиеТип
	|		И ПлюсРасписаниеРекомендаций.ЦелевоеДействиеОбъект = ИсторияРекомендаций.ЦелевоеДействиеОбъект
	|		И ПлюсРасписаниеРекомендаций.ЦелевоеДействиеФорма = ИсторияРекомендаций.ЦелевоеДействиеФорма
	|ГДЕ
	|	(ПлюсРасписаниеРекомендаций.ДатаНачалаОтображения = &ПустаяДата
	|	ИЛИ &ТекущаяДата >= ПлюсРасписаниеРекомендаций.ДатаНачалаОтображения)
	|	И (ПлюсРасписаниеРекомендаций.ДатаОкончанияОтображения = &ПустаяДата
	|	ИЛИ &ТекущаяДата <= ПлюсРасписаниеРекомендаций.ДатаОкончанияОтображения)
	|	И (ПлюсРасписаниеРекомендаций.КоличествоПоказовПлан - ЕСТЬNULL(ИсторияРекомендаций.КоличествоПоказовФакт, 0) > 0
	|	ИЛИ ПлюсРасписаниеРекомендаций.КоличествоПоказовПлан = 0)
	|	И ЕСТЬNULL(ИсторияРекомендаций.КоличествоПоказовСегодня, 0) < &ЛимитПоказовКаждойРекомендацииВДень
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИсторияРекомендаций.ПоследнняяДатаВыполнения";

	Запрос.УстановитьПараметр("ПустаяДата", Дата("00010101"));
	Запрос.УстановитьПараметр("ПустойПользователь", Справочники.Пользователи.ПустаяСсылка());
	Запрос.УстановитьПараметр("ЛимитПоказовКаждойРекомендацииВДень", ЛимитПоказовКаждойРекомендацииВДень);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("ТЗИсторияОткрытыхФорм", ТЗИсторияОткрытыхФорм);
	
	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() Тогда
		Рекомендация = ПлюсСервисКлиентСервер.ПараметрыРекомендацииДляОповещенияПользователя();
		ЗаполнитьЗначенияСвойств(Рекомендация, Выборка);
		Рекомендация.АдресЛоготип = ПоместитьВоВременноеХранилище(Выборка.Логотип.Получить());
		Возврат Рекомендация;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Процедура ЗарегистрироватьОшибку(ИмяСобытия, Уровень = Неопределено, Комментарий = Неопределено) Экспорт

	Если Уровень = Неопределено Тогда
		Уровень = УровеньЖурналаРегистрации.Информация;
	КонецЕсли;

	Если Комментарий = Неопределено Тогда
		Комментарий = "";
	КонецЕсли;

	Событие = СтрШаблон("1С:Плюс.%1", ИмяСобытия);

	ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Информация, , , Комментарий);

	Если Уровень = УровеньЖурналаРегистрации.Примечание Тогда
		УровеньДляЖурнала = 0;
	ИначеЕсли Уровень = УровеньЖурналаРегистрации.Информация Тогда
		УровеньДляЖурнала = 1;
	ИначеЕсли Уровень = УровеньЖурналаРегистрации.Предупреждение Тогда
		УровеньДляЖурнала = 2;
	ИначеЕсли Уровень = УровеньЖурналаРегистрации.Ошибка Тогда
		УровеньДляЖурнала = 3;
	КонецЕсли;

	Попытка
		ЖурналОшибокМенеджер = РегистрыСведений.ПлюсЖурналОшибок.СоздатьМенеджерЗаписи();
		ЖурналОшибокМенеджер.Дата = ТекущаяДатаСеанса();
		ЖурналОшибокМенеджер.Событие = Событие;
		ЖурналОшибокМенеджер.Уровень = УровеньДляЖурнала;
		ЖурналОшибокМенеджер.Комментарий = Комментарий;
		ЖурналОшибокМенеджер.Записать();
	Исключение
		ЗаписьЖурналаРегистрации("1С:Плюс.Журнал ошибок", 
			УровеньЖурналаРегистрации.Ошибка, , ,
			СтрШаблон("Не удалось создать запись журнала ошибок 1С:Плюс по причине:
				|%1",
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
	КонецПопытки;

КонецПроцедуры

// См. ОбщегоНазначенияПереопределяемый.ПриПериодическомПолученииДанныхКлиентаНаСервере.
Процедура ПриПериодическомПолученииДанныхКлиентаНаСервере(Параметры, Результаты) Экспорт

	Если Не РекомендацииДоступны() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьОтключениеБезопасногоРежима(Истина);
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ПлюсСервисПараметрыПриложения = Параметры.Получить("ПлюсСервисПараметрыПриложения");
	Если ПлюсСервисПараметрыПриложения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПараметра = ПлюсСервисКлиентСервер.ИмяПараметраПриложенияПлюсРекомендации();
	ПлюсРекомендацииПараметрыПриложения = ПлюсСервисПараметрыПриложения[ИмяПараметра];

	ПараметрИсторияПоказов = ПлюсСервисКлиентСервер.ИмяПараметраПриложенияИсторияПоказов();
	ПараметрЛимитИсчерпан = ПлюсСервисКлиентСервер.ИмяПараметраПриложенияЛимитИсчерпан();
	
	ИсторияПоказов = ПлюсРекомендацииПараметрыПриложения[ПараметрИсторияПоказов];
	Если ИсторияПоказов.Количество() > 0 Тогда
		ОбновитьИсториюРекомендаций(ТекущийПользователь, ИсторияПоказов);
	КонецЕсли;

	ЛимитИсчерпан = РекомендацииЛимитИсчерпан(ТекущийПользователь);
	ИсторияОткрытыхФорм = ВыгрузитьИсториюОткрытыхФорм();
	Если ЛимитИсчерпан Или ИсторияОткрытыхФорм.Количество() = 0 Тогда
		Рекомендация = Неопределено;
	Иначе
		Рекомендация = ВыбратьРекомендациюИзОчереди(ТекущийПользователь, ИсторияОткрытыхФорм);
	КонецЕсли;

	ПлюсРекомендацииПараметрыПриложения = Новый Соответствие;
	ПлюсРекомендацииПараметрыПриложения.Вставить(ПараметрЛимитИсчерпан, ЛимитИсчерпан);
	ПлюсРекомендацииПараметрыПриложения.Вставить(ПараметрИсторияПоказов, Новый Массив);
	
	ПлюсСервисПараметрыПриложения[ИмяПараметра] = ПлюсРекомендацииПараметрыПриложения;

	Результаты.Вставить("ПлюсСервисПараметрыПриложения", ПлюсСервисПараметрыПриложения);
	Результаты.Вставить("Рекомендация", Рекомендация);

КонецПроцедуры

Функция ВыгрузитьИсториюОткрытыхФорм() Экспорт

	ИсторияОткрытыхФорм = Новый Массив;
	
	Если ПараметрыСеанса.ПлюсИсторияОткрытыхФорм.Количество() = 0 Тогда
		Возврат ИсторияОткрытыхФорм;
	КонецЕсли;
	
	Попытка
		ИсторияОткрытыхФорм = ПараметрыСеанса.ПлюсИсторияОткрытыхФорм;
		ПараметрыСеанса.ПлюсИсторияОткрытыхФорм = Новый ФиксированныйМассив(Новый Массив);
	Исключение
		ТекстОшибкиПодробно = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗарегистрироватьОшибку("Выбор рекомендации", УровеньЖурналаРегистрации.Информация, ТекстОшибкиПодробно);
	КонецПопытки;
	
	Возврат ИсторияОткрытыхФорм;

КонецФункции

Функция ПустойИдентификаторСтрокой() Экспорт

	Возврат Строка(ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());

КонецФункции

Процедура УдалитьРасширениеИзСпискаКУстановке(ИдентификаторРасширения) Экспорт
	
	УдалениеЗадания = РегистрыСведений.ПлюсРасширенияКУстановке.СоздатьМенеджерЗаписи();
	УдалениеЗадания.ИдРасширения = Новый УникальныйИдентификатор(ИдентификаторРасширения);
	УдалениеЗадания.Удалить();
	
КонецПроцедуры

Процедура ОчиститьСписокРасширенийКУстановке(ИдентификаторСеанса) Экспорт
	
	Если ИдентификаторСеанса = Неопределено Тогда
		Возврат;
	КонецЕсли;

	//@skip-check unavailable-in-safemode
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеДляУдаления = Новый Структура();
	ДанныеДляУдаления.Вставить("Extensions", Новый Массив());
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ПлюсРасширенияКУстановке.ИмяРасширения КАК ИмяРасширения,
	|	ПлюсРасширенияКУстановке.ВерсияРасширения КАК ВерсияРасширения,
	|	ПлюсРасширенияКУстановке.ЗаданиеНаУстановку КАК ЗаданиеНаУстановку,
	|	ПлюсРасширенияКУстановке.ИдСеанса КАК ИдСеанса
	|ИЗ
	|	РегистрСведений.ПлюсРасширенияКУстановке КАК ПлюсРасширенияКУстановке");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаданиеНаУстановку = Выборка.ЗаданиеНаУстановку.Получить();
		
		ДанныеРасширения = Новый Структура();
		ДанныеРасширения.Вставить("ExtVersionId", ЗаданиеНаУстановку.ExtVersionId);
		ДанныеДляУдаления.Extensions.Добавить(ДанныеРасширения);
	КонецЦикла;
	
	УдалитьРасширениеИзКорзиныНаСервере(ДанныеДляУдаления, ИдентификаторСеанса);
	
	Набор = РегистрыСведений.ПлюсРасширенияКУстановке.СоздатьНаборЗаписей();
	Набор.Записать(Истина);
	
КонецПроцедуры

Процедура ДобавитьФормуВИсториюОткрытых(ТипИсточника = 0, ТипЗначенияИсточника = "", ИмяФормы = "") Экспорт

	Если Не РекомендацииДоступны() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьОтключениеБезопасногоРежима(Истина);
	УстановитьПривилегированныйРежим(Истина);
	
	ИмяОбъекта = Метаданные.НайтиПоТипу(ТипЗначенияИсточника).Имя;
	
	ФормыСРекомендациями = ПараметрыСеанса.ПлюсФормыСРекомендациями;
	Если ФормыСРекомендациями.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонСтроки = ПлюсСервисКлиентСервер.ШаблонСтрокиМассиваИсторияОткрытыхФорм();

	ТипОбъектаИмяФормы = СтрШаблон(ШаблонСтроки, ТипИсточника, ИмяОбъекта, ИмяФормы);
	Если ФормыСРекомендациями.Найти(ТипОбъектаИмяФормы) = Неопределено Тогда
		ТипОбъектаИмяФормы = СтрШаблон(ШаблонСтроки, ТипИсточника, ИмяОбъекта, "");
		Если ФормыСРекомендациями.Найти(ТипОбъектаИмяФормы) = Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	ИсторияОткрытыхФорм = Новый Массив(ПараметрыСеанса.ПлюсИсторияОткрытыхФорм);
	ИсторияОткрытыхФорм.Добавить(ТипОбъектаИмяФормы);

	ПараметрыСеанса.ПлюсИсторияОткрытыхФорм = Новый ФиксированныйМассив(ИсторияОткрытыхФорм);
	
КонецПроцедуры

// См. РаботаВМоделиСервиса.КонтрольНеразделенныхОбъектовПриЗаписи.
Процедура КонтрольНеразделенныхОбъектовПриЗаписи(Источник, Отказ) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ТехнологияСервиса") Тогда
		Возврат;
	КонецЕсли;
	
	МодульРаботаВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль("РаботаВМоделиСервиса");
	МодульРаботаВМоделиСервиса.КонтрольНеразделенныхОбъектовПриЗаписи(Источник, Отказ);
	
КонецПроцедуры

// Сеансы пользователей информационной базы.
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
// * ОбластьДанных - Число
// * ИмяПользователя - Строка
// * НомерСеанса - Число
Функция СеансыПользователейИнформационнойБазы() Экспорт
	
	Результат = ТаблицаСеансовПользователей();
	ИменаИнтерактивныхПриложений = ИменаИнтерактивныхПриложений();
	РазделительОсновныхДанных = РаботаВМоделиСервиса.РазделительОсновныхДанных();
	
	Сеансы = ПолучитьСеансыИнформационнойБазы();
	Для Каждого Сеанс Из Сеансы Цикл
		
		Если ИменаИнтерактивныхПриложений.Найти(Сеанс.ИмяПриложения) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПользовательСеанса = Сеанс.Пользователь;
		Если ПользовательСеанса = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяПользователяСеанса = ПользовательСеанса.Имя;
		Если Не ЗначениеЗаполнено(ИмяПользователяСеанса) Тогда
			ИмяПользователяСеанса = НСтр("ru = '<Не указан>'");
		КонецЕсли;
		
		РазделениеДанныхПользователяСеанса = ПользовательСеанса.РазделениеДанных;
		Если РазделениеДанныхПользователяСеанса.Свойство(РазделительОсновныхДанных) Тогда
			ОбластьДанных = РазделениеДанныхПользователяСеанса[РазделительОсновныхДанных];
		Иначе
			ОбластьДанных = "0";
		КонецЕсли;
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.ОбластьДанных = XMLЗначение(Тип("Число"), ОбластьДанных);
		НоваяСтрока.ИмяПользователя = ИмяПользователяСеанса;
		НоваяСтрока.НомерСеанса = Сеанс.НомерСеанса;
		
	КонецЦикла;
	
	Возврат Результат
	
КонецФункции

Процедура ВключитьМонопольныйРежим(Знач ЭтоРазделенныйСеанс = Неопределено) Экспорт

	Если ЭтоРазделенныйСеанс = Неопределено Тогда
		ЭтоРазделенныйСеанс = ОбщегоНазначения.РазделениеВключено() 
			И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных();
	КонецЕсли;
	
	Если ЭтоРазделенныйСеанс Тогда
		ПлюсСервисВМоделиСервиса.ВыполнитьУстановкуМонопольногоРежима(
			Пользователи.ТекущийПользователь(),
			Неопределено,
			Неопределено,
			Истина);
	ИначеЕсли Не МонопольныйРежим() Тогда 
		УстановитьМонопольныйРежим(Истина);
	КонецЕсли;	
	
КонецПроцедуры

Процедура СнятьМонопольныйРежим() Экспорт
	
	Если МонопольныйРежим() Тогда
		УстановитьМонопольныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТипСеансаПользователя()
	Попытка
		Если ОбщегоНазначения.ПодсистемаСуществует("ТехнологияСервиса") Тогда
			МодульРаботаВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль("РаботаВМоделиСервиса");
			ЗначениеРазделителя = МодульРаботаВМоделиСервиса.ЗначениеРазделителяСеанса();
		КонецЕсли;	
	Исключение
		ЗначениеРазделителя = Неопределено;
	КонецПопытки;
	
	Если ЗначениеРазделителя = Неопределено
		Или ЗначениеРазделителя = 0
		Или ЗначениеРазделителя = "0" 
		Или Не ЗначениеЗаполнено(ЗначениеРазделителя) Тогда
		Возврат "SelfHosted";
	КонецЕсли;
	
	Возврат "fresh";
	
КонецФункции

Функция РасширениеМожноСинхронизовать(Знач ДанныеРасширения, Знач ИзмененныеРасширения)
	
	Если ИзмененныеРасширения.Найти(ДанныеРасширения.ИдРасширения) = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ПлюсУстановленныеРасширения.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ИдРасширения = ДанныеРасширения.ИдРасширения;
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда
		
		МенеджерЗаписи.АвтообновлениеОтключено = Истина;
		МенеджерЗаписи.Записать();
		
	КонецЕсли;	
	
	ЗаписьЖурналаРегистрации(
		СобытиеСинхронизацияРасширения(),
		УровеньЖурналаРегистрации.Предупреждение,,,
		СтрШаблон(НСтр("ru = 'Пропущена синхронизация расширения %1 версии %2 
			|%3'"), 
			ДанныеРасширения.ИмяРасширения, 
			ДанныеРасширения.ВерсияРасширения, 
			НСтр("ru = 'Расширение отличается от исходного в сервисе.'")));
	
	Возврат Ложь;
	
КонецФункции

Функция СобытиеСинхронизацияРасширения()
	
	Возврат НСтр("ru = 'Расширения 1С:Плюс.Синхронизация расширения'");
	
КонецФункции

Функция ИдентификаторыИзмененныхРасширений()
	
	Идентификаторы = Новый Массив();
	
	ИзмененныеРасширения = СписокИзмененныхРасширений();
	Для Каждого ДанныеИзмененногоРасширения Из ИзмененныеРасширения Цикл
		Идентификаторы.Добавить(ДанныеИзмененногоРасширения.ИдРасширения);
	КонецЦикла; 
	
	Возврат Идентификаторы;
	
КонецФункции

Функция ВыполнитьЗапросДанныхРасширения(ПараметрыПодключения)
	
	Соединение = НовоеСоединениеСAPI(20, ПараметрыПодключения);
	Запрос = Новый HTTPЗапрос(ПараметрыПодключения.ПутьНаСервере);
	
	Ответ = Соединение.ВызватьHTTPМетод("GET", Запрос);
	ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
	Если Ответ.КодСостояния <> 200 Тогда
		Если Ответ.КодСостояния = 404 Тогда
			Данные = ОбщегоНазначения.JSONВЗначение(ТелоОтвета, , Ложь);
			ВызватьИсключение Данные.error_description;
		Иначе
			ВызватьИсключение ТелоОтвета;
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеРасширения = ОписаниеРасширения(ТелоОтвета);
	
	Возврат ОписаниеРасширения;
	
КонецФункции

// Возвращает описание расширения из JSON-строки данных расширения.
// 
// Параметры:
//  ДанныеРасширенияJSON - Строка
// 
// Возвращаемое значение:
//  Структура:
//   * ИдентификаторРасширения - Строка
//   * ИдентификаторВерсии - Строка
//   * Наименование - Строка
//   * НомерВерсии - Строка
//   * КраткаяИнформация - Строка
//   * ПодробнаяИнформация - Строка
//   * АвторскиеПрава - Строка
//   * АдресИнформацииОПоставщике - Строка
//   * АдресИнформацииОКонфигурации - Строка
//   * Разработчик - Строка
//   * АдресПочтыДляСвязиСРазработчиком - Строка
//   * ИзменяетСтруктуруДанных - Булево
//   * Установлено - Булево
//   * ДоступноОбновление - Булево
//   * ДанныеКартинки - ДвоичныеДанные 
//   * ПубличныеИдентификаторы - Массив из Строка
//   * ЭтоРасширение - Булево
//   * ИнтерфейсНастроек - Строка
Функция ОписаниеРасширения(ДанныеРасширенияJSON)
	
	Данные = ОбщегоНазначения.JSONВЗначение(ДанныеРасширенияJSON, , Ложь);
	
	ОписаниеРасширения = Новый Структура;
	ОписаниеРасширения.Вставить("ИдентификаторРасширения",  Данные.ExtId);
	ОписаниеРасширения.Вставить("ИдентификаторВерсии", Данные.ExtVersionId);
	ОписаниеРасширения.Вставить("Наименование", Данные.Name);
	ОписаниеРасширения.Вставить("НомерВерсии", Данные.Version);
	ОписаниеРасширения.Вставить("КраткаяИнформация", Данные.BriefInfo);
	ОписаниеРасширения.Вставить("ПодробнаяИнформация", Данные.DetailedInfo);
	ОписаниеРасширения.Вставить("АвторскиеПрава", Данные.Copyright);
	ОписаниеРасширения.Вставить("АдресИнформацииОПоставщике", Данные.VendorInfoURL);
	ОписаниеРасширения.Вставить("АдресИнформацииОКонфигурации", Данные.ConfigInfoURL);
	ОписаниеРасширения.Вставить("Разработчик", Данные.Developer);
	ОписаниеРасширения.Вставить("АдресПочтыДляСвязиСРазработчиком", Данные.DeveloperEmail);
	ОписаниеРасширения.Вставить("ИзменяетСтруктуруДанных", Данные.ModifiesDataStructure);
	ОписаниеРасширения.Вставить("Установлено", Данные.Installed);
	ОписаниеРасширения.Вставить("ДоступноОбновление", Данные.UpdateAvailable);
	ОписаниеРасширения.Вставить("ДанныеКартинки", ПолучитьДвоичныеДанныеИзBase64Строки(Данные.IconData));
	ОписаниеРасширения.Вставить("ПубличныеИдентификаторы", Данные.PublicIds);
	ОписаниеРасширения.Вставить("ЭтоРасширение", Данные.IsExtension);
	ОписаниеРасширения.Вставить("ИнтерфейсНастроек", Данные.SettingsInterface);
	
	Возврат ОписаниеРасширения; //@skip-check constructor-function-return-section

КонецФункции

Процедура УдалитьРасширениеИзменяющееДанные(ДанныеРасширения, РасширенияБД, Запись)
	
	УдалитьЗаписьИРасширение(ДанныеРасширения, РасширенияБД, Запись);
	Если ОбщегоНазначения.РазделениеВключено() И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		ПараметрыФоновогоЗадания = Новый Массив();
		ПараметрыФоновогоЗадания.Добавить(ДанныеРасширения);
		Задание = ФоновыеЗадания.Выполнить("ПлюсСервис.ОповеститьМенеджерСервисаОбУдалении", ПараметрыФоновогоЗадания);
		Задание.ОжидатьЗавершенияВыполнения();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОповеститьМенеджерСервисаОбУдалении(ДанныеРасширения) Экспорт
	ИнфоОРасширении = ИнформацияОРасширении(
		ДанныеРасширения.ИдРасширения, 
		ДанныеРасширения.ИдВерсииРасширения, 
		ДанныеРасширения.ИмяРасширения, 
		ДанныеРасширения.ВерсияРасширения);
	ОповеститьМенеджерСервисаОбИзмененииИнсталляции(ИнфоОРасширении, Неопределено, Истина);
КонецПроцедуры

Процедура УдалитьРасширениеНеИзменяющееДанные(ДанныеРасширения, РасширенияБД, Запись)
	
	Блокировка = Новый БлокировкаДанных();
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПлюсУстановленныеРасширения");
	ЭлементБлокировки.УстановитьЗначение("ИдРасширения", ДанныеРасширения.ИдРасширения);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	НачатьТранзакцию();
	Попытка
		Блокировка.Заблокировать();
		
		УдалитьЗаписьИРасширение(ДанныеРасширения, РасширенияБД, Запись);
		Если ОбщегоНазначения.РазделениеВключено() И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
			ОповеститьМенеджерСервисаОбУдалении(ДанныеРасширения);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Процедура УдалитьЗаписьИРасширение(ДанныеРасширения, РасширенияБД, Запись)
	
	РасширенияБД[0].Удалить();
	Запись.Удалить();
	УдалитьРасширениеИзСпискаКУстановке(ДанныеРасширения.ИдРасширения);
	УдалитьЗаданияРасширения(Строка(ДанныеРасширения.ИдРасширения));
	
КонецПроцедуры

Функция ИнформацияОРасширении(Идентификатор, ИдентификаторВерсии, Имя, Версия)
	
	ДанныеРасширения = Новый Структура();
	ДанныеРасширения.Вставить("ExtId", Строка(Идентификатор));
	ДанныеРасширения.Вставить("ExtVersionId", Строка(ИдентификаторВерсии));
	ДанныеРасширения.Вставить("Name", Строка(Имя));
	ДанныеРасширения.Вставить("Version", Строка(Версия));
	
	Возврат ДанныеРасширения;
	
КонецФункции

// @unavailable-in-safemode
Процедура СинхронизироватьРасширение(ВыборкаРасширения, ЗаданияНаУстановку, Область, ДляУстановкиВМонопольном)
	Задание = ЗаданияНаУстановку[ВыборкаРасширения.ИдВерсииРасширения];
	
	Если ВыборкаРасширения.Удалить Тогда
		УдалитьРасширение(ВыборкаРасширения);
	Иначе

		Попытка
			УстановитьРасширение(Задание);
			УдалитьРасширениеИзСпискаКУстановке(Задание.ExtId);
		Исключение

			ИнформацияОбОшибке = ИнформацияОбОшибке();

			Если ИнформацияОбОшибке.Код = КодОшибкиМонопольногоДоступа() И ВыборкаРасширения.ИзменяетСтруктуруДанных Тогда
				Запись = РегистрыСведений.ПлюсРасширенияКУстановке.СоздатьМенеджерЗаписи();
				Если Область <> 0 Тогда
					Запись.ОбластьДанныхВспомогательныеДанные = Область;
				КонецЕсли;
				Запись.ИдРасширения = Новый УникальныйИдентификатор(Задание.ExtId);
				Запись.ИдВерсииРасширения = Новый УникальныйИдентификатор(Задание.ExtVersionId);
				Запись.ИмяРасширения = Задание.Name;
				Запись.ВерсияРасширения = Задание.Version;
				Запись.ЗаданиеНаУстановку = Новый ХранилищеЗначения(Задание, Новый СжатиеДанных(9));
				Запись.Записать();

				ДляУстановкиВМонопольном.Добавить(Задание.Name);
			Иначе
				ВызватьИсключение;
			КонецЕсли;

		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

Функция ТаблицаУстановленныхРасширений()
	ЗапросКБД = Новый Запрос("ВЫБРАТЬ
	|	ПлюсУстановленныеРасширения.ИдВерсииРасширения КАК ИдВерсииРасширения,
	|	ПлюсУстановленныеРасширения.ИмяРасширения КАК ИмяРасширения,
	|	ПлюсУстановленныеРасширения.ВерсияРасширения КАК ВерсияРасширения,
	|	ПлюсУстановленныеРасширения.ИдРасширенияВИБ КАК ИдРасширенияВИБ,
	|	ПлюсУстановленныеРасширения.ИдРасширения КАК ИдРасширения
	|ИЗ
	|	РегистрСведений.ПлюсУстановленныеРасширения КАК ПлюсУстановленныеРасширения");
	
	ТаблицаРасширений = ЗапросКБД.Выполнить().Выгрузить();
	Возврат ТаблицаРасширений
КонецФункции

Функция ИмяПрофилияБезопасности() Экспорт
	Возврат "Int1cPlus";
КонецФункции

Процедура СинхронизироватьСписокРасширенийСБД(ТаблицаРасширений = Неопределено)
	Если ТаблицаРасширений = Неопределено Тогда
		ТаблицаРасширений = ТаблицаУстановленныхРасширений();
	КонецЕсли;
	
	УдаляемыеСтроки = Новый Массив;
	
	Для Каждого ТекСтрока Из ТаблицаРасширений Цикл
		РасширенияБД = РасширенияКонфигурации.Получить(
			Новый Структура("УникальныйИдентификатор", ТекСтрока.ИдРасширенияВИБ)
			, ИсточникРасширенийКонфигурации.БазаДанных);
		Если РасширенияБД.Количество() <> 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Запись = РегистрыСведений.ПлюсУстановленныеРасширения.СоздатьМенеджерЗаписи();
		Запись.ИдРасширения = ТекСтрока.ИдРасширения;
		Запись.Удалить();
		УдаляемыеСтроки.Добавить(ТекСтрока);
	КонецЦикла;
	
	Для Каждого ТекСтрока Из УдаляемыеСтроки Цикл
		УдалитьЗаданияРасширения(Строка(ТекСтрока.ИдРасширения));
		ТаблицаРасширений.Удалить(ТекСтрока);
	КонецЦикла;
КонецПроцедуры

Функция НовоеСоединениеСAPI(Таймаут, ПараметрыПодключения)
	Если ПараметрыПодключения.Схема = "http" Тогда
		ЗащищенноеСоединение = Неопределено;
	ИначеЕсли ПараметрыПодключения.Схема = "https" Тогда
		ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
	Иначе 
		
		ВызватьИсключение НСтр("ru = 'Не удалось определить схему запроса к МС.'");
		
	КонецЕсли;
	
	УстановитьОтключениеБезопасногоРежима(Истина);
	Возврат  Новый HTTPСоединение(ПараметрыПодключения.Хост, ПараметрыПодключения.Порт, , ,
		ПолучениеФайловИзИнтернета.ПолучитьПрокси(ПараметрыПодключения.Схема), Таймаут, ЗащищенноеСоединение, Ложь);
		
КонецФункции

Процедура ПодтвердитьУстановкуРасширения(ЗаданиеНаУстановку, ИдСеанса, ЭтоУстановкаОбновлений = Ложь)
	
	ПараметрыФоновогоЗадания = Новый Массив();
	ПараметрыФоновогоЗадания.Добавить(ЗаданиеНаУстановку);
	ПараметрыФоновогоЗадания.Добавить(ИдСеанса);
	ПараметрыФоновогоЗадания.Добавить(ЭтоУстановкаОбновлений);
	Задание = ФоновыеЗадания.Выполнить("ПлюсСервис.ПодтвердитьУстановкуРасширенияВФоне", ПараметрыФоновогоЗадания);
	Задание.ОжидатьЗавершенияВыполнения();
		
КонецПроцедуры

Процедура ПодтвердитьУстановкуРасширенияВФоне(ЗаданиеНаУстановку, ИдСеанса, ЭтоУстановкаОбновлений = Ложь) Экспорт
	
	УдалитьРасширениеИзСпискаКУстановке(ЗаданиеНаУстановку.ExtId);
	
	Если ЭтоУстановкаОбновлений Тогда
		Возврат;
	КонецЕсли;
	
	Метод = СтрШаблон("/%1/InstallComplete", Строка(ИдСеанса));
	ПараметрыПодключения = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресAPI() + Метод);
	Соединение = НовоеСоединениеСAPI(20, ПараметрыПодключения);
	
	Запрос = Новый HTTPЗапрос(ПараметрыПодключения.ПутьНаСервере);
	
	ИнформацияОРасширении = ИнформацияОРасширении(
		ЗаданиеНаУстановку.ExtId,
		ЗаданиеНаУстановку.ExtVersionId,
		ЗаданиеНаУстановку.Name,
		ЗаданиеНаУстановку.Version);
	Запрос.УстановитьТелоИзСтроки(ОбщегоНазначения.ЗначениеВJSON(ИнформацияОРасширении));
	
	Ответ = Соединение.ВызватьHTTPМетод("POST", Запрос);
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение Ответ.ПолучитьТелоКакСтроку();
	КонецЕсли;
		
КонецПроцедуры

Функция ПолучитьДвоичныеДанныеРасширения(ПутькДвоичнымДанным, УстановкаИзВременныхФайлов)
	Если УстановкаИзВременныхФайлов Тогда
		Возврат Новый ДвоичныеДанные(ПутькДвоичнымДанным);
	КонецЕсли;
	
	ПараметрыПодключения = ОбщегоНазначенияКлиентСервер.СтруктураURI(ПутькДвоичнымДанным);
	
	Соединение = НовоеСоединениеСAPI(120, ПараметрыПодключения);
		
	Запрос = Новый HTTPЗапрос(ПараметрыПодключения.ПутьНаСервере);
	Ответ = Соединение.Получить(Запрос);
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение Ответ.ПолучитьТелоКакСтроку();
	КонецЕсли;
	
	Возврат Ответ.ПолучитьТелоКакДвоичныеДанные();
КонецФункции

Процедура АктуализироватьРегламентныеЗадания(ЗаданиеНаУстановку)
	УдалитьЗаданияРасширения(ЗаданиеНаУстановку.ExtId);
	
	Для Каждого Команда Из ЗаданиеНаУстановку.ScheduledJobs Цикл
		ПараметрыМетода = Новый Массив;
		ПараметрыМетода.Добавить(ЗаданиеНаУстановку.ExtId);
		ПараметрыМетода.Добавить(Команда.Id);
		ПараметрыЗадания = Новый Структура;
		ПараметрыЗадания.Вставить("Использование", Истина);
		ПараметрыЗадания.Вставить("Ключ", Команда.Id);
		ПараметрыЗадания.Вставить("Параметры", ПараметрыМетода);
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Команда.Schedule);
		ПараметрыЗадания.Вставить("Расписание", 
			СериализаторXDTO.ПрочитатьJSON(ЧтениеJSON, Тип("РасписаниеРегламентногоЗадания")));
		
		ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ПлюсЗапускЗаданийРасширений);
		РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
	КонецЦикла;
КонецПроцедуры

Процедура УдалитьЗаданияРасширения(ИдРасширения)
	
	Отбор = Новый Структура;
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Отбор.Вставить("ИмяМетода", Метаданные.РегламентныеЗадания.ПлюсЗапускЗаданийРасширений.ИмяМетода);
	Иначе
		Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ПлюсЗапускЗаданийРасширений);
	КонецЕсли;
	
	Задания = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	
	Для Каждого Задание Из Задания Цикл
		Если Задание.Параметры.Количество() = 0 Или Задание.Параметры[0] <> ИдРасширения Тогда
			Продолжить;
		КонецЕсли;
		
		РегламентныеЗаданияСервер.УдалитьЗадание(Задание.УникальныйИдентификатор);
	КонецЦикла;
КонецПроцедуры

Процедура ОповеститьМенеджерСервисаОбИзмененииИнсталляции(ЗаданиеНаУстановку, ДатаУстановки = Неопределено, Удаление = Ложь)
	ИнформацияОбУстановке = Новый Структура();
	ИнформацияОбУстановке.Вставить("Name", ЗаданиеНаУстановку.Name);
	ИнформацияОбУстановке.Вставить("Version", ЗаданиеНаУстановку.Version);
	ИнформацияОбУстановке.Вставить("ExtVersionId", ЗаданиеНаУстановку.ExtVersionId);
	ИнформацияОбУстановке.Вставить("ExtId", ЗаданиеНаУстановку.ExtId);
	ИнформацияОбУстановке.Вставить("InstallTime", ТекущаяУниверсальнаяДата());
	ИнформацияОбУстановке.Вставить("Deleted", Удаление);
	ИнформацияОбУстановке.Вставить("Zone", РаботаВМоделиСервиса.ЗначениеРазделителяСеанса());
	Если ЗначениеЗаполнено(ДатаУстановки) Тогда
		ИнформацияОбУстановке.InstallTime = ДатаУстановки;
	КонецЕсли;			
	
	НачатьТранзакцию();
	Попытка
		ОбменСообщениями.ОтправитьСообщение(
			ИмяКаналаИнфоОбУстановленномРасширении(),
			ОбщегоНазначения.ЗначениеВJSON(ИнформацияОбУстановке),
			РаботаВМоделиСервиса.КонечнаяТочкаМенеджераСервиса());
		
		ЗафиксироватьТранзакцию();
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ВызватьИсключение;
	КонецПопытки;		
КонецПроцедуры

Функция ИмяКаналаИнфоОбУстановленномРасширении()
	Возврат "plus/InstalledExtensionInfo"
КонецФункции

Процедура ОбновитьИсториюРекомендаций(Пользователь, ИсторияПоказов)

	ИсторияМенеджер = РегистрыСведений.ПлюсИсторияРекомендаций.СоздатьМенеджерЗаписи();
	Для Каждого Рекомендация Из ИсторияПоказов Цикл
		ЗаполнитьЗначенияСвойств(ИсторияМенеджер, Рекомендация);
		ИсторияМенеджер.Пользователь = Пользователь;
		ИсторияМенеджер.Записать();
	КонецЦикла;
	
КонецПроцедуры

Функция РекомендацииЛимитИсчерпан(Пользователь)
	
	ТекущаяДата = НачалоДня(ТекущаяДатаСеанса());

	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
				   |	КОЛИЧЕСТВО(Истина) КАК ПланНаСегодня
				   |ИЗ
				   |	РегистрСведений.ПлюсИсторияРекомендаций КАК ПлюсИсторияРекомендаций
				   |ГДЕ
				   |	ПлюсИсторияРекомендаций.Пользователь = &Пользователь
				   |	И ПлюсИсторияРекомендаций.Выполнено
				   |	И ДЕНЬ(ПлюсИсторияРекомендаций.ДатаВыполнения) = ДЕНЬ(&ТекущаяДата)";

	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		НастройкиРекомендаций = НастройкиРекомендаций();
		Если Выборка.ПланНаСегодня >= НастройкиРекомендаций.МаксимальноеКоличествоРекомендацийВДень Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Процедура ОбновитьЗначенияСвойствПриложения(МассивДляОбновления, ИсходныеДанные)

	ТЗДляОбновления = Новый ТаблицаЗначений;
	ТЗДляОбновления.Колонки.Добавить("Свойство", Новый ОписаниеТипов("УникальныйИдентификатор"));
	ТЗДляОбновления.Колонки.Добавить("ТекстЗапроса", Новый ОписаниеТипов("Строка"));

	Для Каждого Элемент Из МассивДляОбновления Цикл
		Строка = ТЗДляОбновления.Добавить();
		Строка.Свойство = Новый УникальныйИдентификатор(Элемент.IndicatorId);
		Строка.ТекстЗапроса = Элемент.Query;
	КонецЦикла;

	Запрос = Новый Запрос("Выбрать
						  |	ТаблицаОбновление.Свойство как Свойство,
						  |	ТаблицаОбновление.ТекстЗапроса как ТекстЗапроса
						  |Поместить Обновление
						  |из
						  |	&ТаблицаОбновление как ТаблицаОбновление
						  |;
						  |
						  |////////////////////////////////////////////////////////////////////////////////
						  |Выбрать
						  |	ТаблицаИсходныеДанные.Свойство как Свойство,
						  |	ТаблицаИсходныеДанные.Значение как Значение,
						  |	ТаблицаИсходныеДанные.ДатаОбновления как ДатаОбновления,
						  |	ТаблицаИсходныеДанные.ТекстОшибки как ТекстОшибки,
						  |	ТаблицаИсходныеДанные.ТекстЗапроса как ТекстЗапроса
						  |Поместить ИсходныеДанные
						  |из
						  |	&ТаблицаИсходныеДанные как ТаблицаИсходныеДанные
						  |;
						  |
						  |////////////////////////////////////////////////////////////////////////////////
						  |ВЫБРАТЬ
						  |	ЕСТЬNULL(Обновление.Свойство, ИсходныеДанные.Свойство) КАК Свойство,
						  |	ИсходныеДанные.Значение,
						  |	ИсходныеДанные.ДатаОбновления,
						  |	ЕСТЬNULL(Обновление.ТекстЗапроса, ИсходныеДанные.ТекстЗапроса) КАК ТекстЗапроса,
						  |	ВЫБОР
						  |		КОГДА Обновление.Свойство ЕСТЬ NULL
						  |			ТОГДА ИсходныеДанные.ТекстОшибки
						  |		ИНАЧЕ """"
						  |	КОНЕЦ КАК ТекстОшибки
						  |ИЗ
						  |	Обновление КАК Обновление
						  |		ПОЛНОЕ СОЕДИНЕНИЕ ИсходныеДанные КАК ИсходныеДанные
						  |		ПО Обновление.Свойство = ИсходныеДанные.Свойство");

	Запрос.УстановитьПараметр("ТаблицаОбновление", ТЗДляОбновления);
	Запрос.УстановитьПараметр("ТаблицаИсходныеДанные", ИсходныеДанные);

	Попытка
		НаборЗаписей = РегистрыСведений.ПлюсЗначенияСвойствПриложения.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
		НаборЗаписей.Записать();
	Исключение
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Комментарий = СтрШаблон(НСтр("ru = 'Ошибка при обновлении свойств приложения.
								|%1'"), ПодробноеПредставлениеОшибки);
		ЗарегистрироватьОшибку("Синхронизация с сервисом", УровеньЖурналаРегистрации.Ошибка, Комментарий);
	КонецПопытки;

КонецПроцедуры

Функция РазрешеныСборИОтправкаСведений()

	РазрешеныСборИОтправкаСведений = Ложь;

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЦентрМониторинга") Тогда
		МодульЦентрМониторинга = ОбщегоНазначения.ОбщийМодуль("ЦентрМониторинга");
		РазрешеныСборИОтправкаСведений = МодульЦентрМониторинга.ЦентрМониторингаВключен();
	КонецЕсли;

	ЦентрМониторингаПлюсПереопределяемый.ЦентрМониторингаВключен(РазрешеныСборИОтправкаСведений);

	Возврат РазрешеныСборИОтправкаСведений;

КонецФункции

Функция ИдентификаторИнформационнойБазы()

	Если ОбщегоНазначения.РазделениеВключено() И Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		ИдБазы = Строка(Новый УникальныйИдентификатор);
	Иначе
		ИдБазы = СтандартныеПодсистемыСервер.ИдентификаторИнформационнойБазы();
	КонецЕсли;

	Возврат ИдБазы;

КонецФункции

Функция ШаблонСвойствоПриложения()

	Шаблон = Новый Структура;
	Шаблон.Вставить("IndicatorId", ПустойИдентификаторСтрокой());
	Шаблон.Вставить("Date", "");
	Шаблон.Вставить("Value", "");
	Шаблон.Вставить("ErrorInfo", "");

	Возврат Шаблон;

КонецФункции

Функция ШаблонИнформацияОбОшибке()

	Шаблон = Новый Структура;
	Шаблон.Вставить("Date", "");
	Шаблон.Вставить("Event", "");
	Шаблон.Вставить("Level", "");
	Шаблон.Вставить("Comment", "");

	Возврат Шаблон;

КонецФункции

Функция ШаблонНастройки()

	Настройки = Новый Структура;
	Настройки.Вставить("МаксимальноеКоличествоРекомендацийВДень", 0);

	Возврат Настройки;
КонецФункции

Процедура ОбновитьНастройки(ИмяНастройки, Значение)

	ТекущиеНастройки = НастройкиРекомендаций();
	Если Не ТекущиеНастройки.Свойство(ИмяНастройки) Тогда
		Комментарий = СтрШаблон(НСтр("ru = 'Ошибка при записи настроек.
									 |Некорректное имя настройки для 1С:Плюс: %1'"), ИмяНастройки);
		ЗарегистрироватьОшибку("Синхронизация с сервисом", УровеньЖурналаРегистрации.Предупреждение, Комментарий);
		Возврат;
	КонецЕсли;

	Если ТекущиеНастройки[ИмяНастройки] = Значение Тогда
		Возврат;
	КонецЕсли;

	РаботаВСервисе = Ложь;
	Если РаботаВМоделиСервиса.РазделениеВключено() И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		КодПриложения = РаботаВМоделиСервиса.ЗначениеРазделителяСеанса();
		РаботаВСервисе = Истина;
	КонецЕсли;

	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ПлюсНастройки");
	Если РаботаВСервисе Тогда
		ЭлементБлокировкиДанных.УстановитьЗначение("ОбластьДанныхВспомогательныеДанные", КодПриложения);
	КонецЕсли;

	НачатьТранзакцию();
	Попытка
		БлокировкаДанных.Заблокировать();

		НастройкиМенеджер = РегистрыСведений.ПлюсНастройки.СоздатьМенеджерЗаписи();
		НастройкиМенеджер.Прочитать();
		Если НастройкиМенеджер[ИмяНастройки] <> Значение Тогда
			НастройкиМенеджер[ИмяНастройки] = Значение;
			НастройкиМенеджер.Записать();
		КонецЕсли;

		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Комментарий = СтрШаблон(НСтр("ru = 'Ошибка при записи настроек.
									 |%1'"), ПодробноеПредставлениеОшибки);
		ЗарегистрироватьОшибку("Синхронизация с сервисом", УровеньЖурналаРегистрации.Ошибка, Комментарий);
	КонецПопытки;

КонецПроцедуры

Функция НастройкиРекомендаций()

	Настройки = ШаблонНастройки();

	Запрос = Новый Запрос("ВЫБРАТЬ
						  |	ПлюсНастройки.МаксимальноеКоличествоРекомендацийВДень
						  |ИЗ
						  |	РегистрСведений.ПлюсНастройки КАК ПлюсНастройки");

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Настройки, Выборка);
	КонецЕсли;

	Возврат Настройки;

КонецФункции

Функция ВыгрузитьЗаписиИзРегистра(ИмяРегистра)

	НаборЗаписей = РегистрыСведений[ИмяРегистра].СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	ТаблицаЗначений = НаборЗаписей.Выгрузить();
	НаборЗаписей.Очистить();
	НаборЗаписей.Записать();

	Возврат ТаблицаЗначений;

КонецФункции

Функция ПолучитьВсеФормыСРекомендациями()

	Массив = Новый Массив;

	Если Не РекомендацииДоступны() Тогда
		Возврат Массив;
	КонецЕсли;
	
	Запрос = Новый Запрос("
						  |ВЫБРАТЬ РАЗЛИЧНЫЕ
						  |	ПлюсРасписаниеРекомендаций.ЦелевоеДействиеТип КАК ТипИсточника,
						  |	ПлюсРасписаниеРекомендаций.ЦелевоеДействиеОбъект КАК ИмяОбъекта,
						  |	ПлюсРасписаниеРекомендаций.ЦелевоеДействиеФорма КАК ИмяФормы
						  |ИЗ
						  |	РегистрСведений.ПлюсРасписаниеРекомендаций КАК ПлюсРасписаниеРекомендаций
						  |ГДЕ
						  |	((ПлюсРасписаниеРекомендаций.ДатаНачалаОтображения = &ПустаяДата
						  |	ИЛИ &ТекущаяДата >= ПлюсРасписаниеРекомендаций.ДатаНачалаОтображения)
						  |	И (ПлюсРасписаниеРекомендаций.ДатаОкончанияОтображения = &ПустаяДата
						  |	ИЛИ &ТекущаяДата <= ПлюсРасписаниеРекомендаций.ДатаОкончанияОтображения))
						  |");

	Запрос.УстановитьПараметр("ПустаяДата", Дата("00010101"));
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));

	ШаблонСтроки = ПлюсСервисКлиентСервер.ШаблонСтрокиМассиваИсторияОткрытыхФорм();

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ТипОбъектаИмяФормы = СтрШаблон(ШаблонСтроки, Выборка.ТипИсточника, Выборка.ИмяОбъекта, Выборка.ИмяФормы);

		Массив.Добавить(ТипОбъектаИмяФормы);
	КонецЦикла;

	Возврат Массив;

КонецФункции

Функция СформироватьТаблицуИзМассива(МассивСтрок)

	КвалификаторСтроки = Новый КвалификаторыСтроки(250);
	КвалификаторЧисла = Новый КвалификаторыЧисла(1, 0);
	ТЗИсторияОткрытыхФорм = Новый ТаблицаЗначений;
	ТЗИсторияОткрытыхФорм.Колонки.Добавить("ТипИсточника", Новый ОписаниеТипов("Число", КвалификаторЧисла));
	ТЗИсторияОткрытыхФорм.Колонки.Добавить("ИмяОбъекта", Новый ОписаниеТипов("Строка", КвалификаторСтроки));
	ТЗИсторияОткрытыхФорм.Колонки.Добавить("ИмяФормы", Новый ОписаниеТипов("Строка", КвалификаторСтроки));
	Для Каждого Элемент Из МассивСтрок Цикл
		ТЗСтрока = ТЗИсторияОткрытыхФорм.Добавить();
		ДанныеСтроки = СтроковыеФункцииКлиентСервер.ПараметрыИзСтроки(Элемент);
		ЗаполнитьЗначенияСвойств(ТЗСтрока, ДанныеСтроки);
	КонецЦикла;

	Возврат ТЗИсторияОткрытыхФорм;

КонецФункции

Процедура УдалитьНеактуальнуюИсториюПоказов()

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	ПлюсИсторияРекомендаций.ИдРасширения,
				   |	ПлюсИсторияРекомендаций.ЦелевоеДействиеТип,
				   |	ПлюсИсторияРекомендаций.ЦелевоеДействиеОбъект,
				   |	ПлюсИсторияРекомендаций.ЦелевоеДействиеФорма
				   |ИЗ
				   |	РегистрСведений.ПлюсИсторияРекомендаций КАК ПлюсИсторияРекомендаций
				   |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлюсРасписаниеРекомендаций КАК ПлюсРасписаниеРекомендаций
				   |		ПО ПлюсРасписаниеРекомендаций.ИдРасширения = ПлюсИсторияРекомендаций.ИдРасширения
				   |		И ПлюсРасписаниеРекомендаций.ЦелевоеДействиеТип = ПлюсИсторияРекомендаций.ЦелевоеДействиеТип
				   |		И ПлюсРасписаниеРекомендаций.ЦелевоеДействиеОбъект = ПлюсИсторияРекомендаций.ЦелевоеДействиеОбъект
				   |		И ПлюсРасписаниеРекомендаций.ЦелевоеДействиеФорма = ПлюсИсторияРекомендаций.ЦелевоеДействиеФорма
				   |ГДЕ
				   |	ПлюсРасписаниеРекомендаций.ИдРасширения ЕСТЬ NULL
				   |	И ПлюсРасписаниеРекомендаций.ЦелевоеДействиеТип ЕСТЬ NULL
				   |	И ПлюсРасписаниеРекомендаций.ЦелевоеДействиеОбъект ЕСТЬ NULL
				   |	И ПлюсРасписаниеРекомендаций.ЦелевоеДействиеФорма ЕСТЬ NULL";

	НачатьТранзакцию();
	Попытка
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НаборЗаписейИсторияРекомендаций = РегистрыСведений.ПлюсИсторияРекомендаций.СоздатьНаборЗаписей();
			НаборЗаписейИсторияРекомендаций.Отбор.ИдРасширения.Установить(Выборка.ИдРасширения);
			НаборЗаписейИсторияРекомендаций.Отбор.ЦелевоеДействиеТип.Установить(Выборка.ЦелевоеДействиеТип);
			НаборЗаписейИсторияРекомендаций.Отбор.ЦелевоеДействиеОбъект.Установить(Выборка.ЦелевоеДействиеОбъект);
			НаборЗаписейИсторияРекомендаций.Отбор.ЦелевоеДействиеФорма.Установить(Выборка.ЦелевоеДействиеФорма);
			НаборЗаписейИсторияРекомендаций.Записать();
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Комментарий = СтрШаблон(НСтр("ru = 'Ошибка при удалении неактуальной истории показов.
									 |%1'"), ПодробноеПредставлениеОшибки);
		ЗарегистрироватьОшибку("Синхронизация с сервисом", УровеньЖурналаРегистрации.Ошибка, Комментарий);
	КонецПопытки;

КонецПроцедуры

Функция РекомендацииДоступны()
	
	Возврат ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() 
				И РазрешеныСборИОтправкаСведений()
				И Используется();
	
КонецФункции

// Таблица сеансов пользователей.
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
// * ОбластьДанных - Число
// * ИмяПользователя - Строка
// * НомерСеанса - Число
Функция ТаблицаСеансовПользователей() 
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ОбластьДанных", Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(7,0)));
	Таблица.Колонки.Добавить("ИмяПользователя", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(100)));
	Таблица.Колонки.Добавить("НомерСеанса", Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(25,0)));	
	
	Возврат Таблица;
	
КонецФункции

// Возвращаемое значение:
//  Массив из Строка -
//
Функция ИменаИнтерактивныхПриложений() 
	
	ИменаИнтерактивныхПриложений = Новый Массив();
	ИменаИнтерактивныхПриложений.Добавить("1CV8");
	ИменаИнтерактивныхПриложений.Добавить("1CV8C");
	ИменаИнтерактивныхПриложений.Добавить("WebClient");
	ИменаИнтерактивныхПриложений.Добавить("MobileClient");

	Возврат ИменаИнтерактивныхПриложений;
	
КонецФункции

Функция ЗаписатьРасширениеВПопытке(РасширениеИБ, ДвоичныеДанныеРасширения)

	ТекстОшибки = "";

	Попытка
		РасширениеИБ.Записать(ДвоичныеДанныеРасширения);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КонецПопытки;

	Возврат ТекстОшибки;

КонецФункции

Функция КодОшибкиМонопольногоДоступа()

	Возврат "ПлюсСервис.ОшибкаМонопольногоДоступа";

КонецФункции

Функция ИмяСобытияОшибкаЗаписиРасширения()
	
	Возврат НСтр("ru = 'Расширения 1С:Плюс.Ошибка записи расширения'", ОбщегоНазначения.КодОсновногоЯзыка());
			
КонецФункции

#КонецОбласти

