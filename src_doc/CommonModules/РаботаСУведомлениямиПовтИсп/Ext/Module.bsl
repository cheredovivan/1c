////////////////////////////////////////////////////////////////////////////////
// Работа с уведомлениями (сервер, повторное использование).
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает скорректированный адрес публикации на веб-сервере.
// 
// Возвращаемое значение:
//  Строка
// 
Функция СкорректированныйАдресПубликации() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СкорректированныйАдресПубликации = Константы.АдресПубликацииНаВебСервере.Получить();
	Если Не ЗначениеЗаполнено(СкорректированныйАдресПубликации) Тогда
		Возврат СкорректированныйАдресПубликации;
	КонецЕсли;
	
	Если СтрНачинаетсяС(НРег(СкорректированныйАдресПубликации), "e1c://") Тогда
		
		// Публикация на сервере 1С:Предприятия, например:
		// e1c://server/address/base#e1cib/data/Справочник.ДокументыПредприятия?ref=80d2cf9f495016cd11eee793f9e0f4fd
		Если СтрЗаканчиваетсяНа(СкорректированныйАдресПубликации, "/#") Тогда
			// Было e1c://server/address/base/#
			СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(СкорректированныйАдресПубликации, 2);
			// Теперь e1c://server/address/base
			СкорректированныйАдресПубликации = СкорректированныйАдресПубликации + "#";
			// Стало e1c://server/address/base#
		ИначеЕсли СтрЗаканчиваетсяНа(СкорректированныйАдресПубликации, "#") Тогда
			// Адрес уже корректный e1c://server/address/base#
		ИначеЕсли СтрЗаканчиваетсяНа(СкорректированныйАдресПубликации, "/") Тогда
			// Было e1c://server/address/base/
			СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(СкорректированныйАдресПубликации);
			// Теперь e1c://server/address/base
			СкорректированныйАдресПубликации = СкорректированныйАдресПубликации + "#";
			// Стало e1c://server/address/base#
		Иначе
			// Было e1c://server/address/base
			СкорректированныйАдресПубликации = СкорректированныйАдресПубликации + "#";
			// Стало e1c://server/address/base#
		КонецЕсли;
		
	Иначе
		
		// Публикация на веб-сервере, например:
		// http://address/base/#e1cib/data/Справочник.ДокументыПредприятия?ref=80d2cf9f495016cd11eee793f9e0f4fd
		Если СтрЗаканчиваетсяНа(СкорректированныйАдресПубликации, "/#") Тогда
			// Адрес уже корректный http://address/base/#
		ИначеЕсли СтрЗаканчиваетсяНа(СкорректированныйАдресПубликации, "#") Тогда
			// Адрес уже корректный http://address/base#
		ИначеЕсли СтрЗаканчиваетсяНа(СкорректированныйАдресПубликации, "/") Тогда
			// Было http://address/base/
			СкорректированныйАдресПубликации = СкорректированныйАдресПубликации + "#";
			// Стало http://address/base/#
		Иначе
			// Было http://address/base
			СкорректированныйАдресПубликации = СкорректированныйАдресПубликации + "/#";
			// Стало http://address/base/#
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СкорректированныйАдресПубликации;
	
КонецФункции

// Возвращает разрешение изменять настройки уведомлений.
// 
// Возвращаемое значение:
//  Булево
// 
Функция РазрешитьИзменятьНастройкиУведомлений() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	РазрешитьИзменятьНастройкиУведомлений = Константы.РазрешитьИзменятьНастройкиУведомлений.Получить();
	
	Возврат РазрешитьИзменятьНастройкиУведомлений;
	
КонецФункции

#КонецОбласти