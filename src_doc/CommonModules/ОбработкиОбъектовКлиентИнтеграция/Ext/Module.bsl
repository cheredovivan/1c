
#Область ПрограммныйИнтерфейс_НачалоОбработки

// Проверит что есть файлы, нужные для запуска в обработку.
//
// Параметры
//  Форма -УправляемаяФорма.
//  ПараметрыВидаОбъекта - Структура - Параметры вида объекта. См. ДействияКлиентСервер.НовыйПараметрыВидаОбъекта().
//
// Возвращаемое значение -Булево. Истина, если можно запускать.
Функция ПроверитьФайлы(Форма, ПараметрыВидаОбъекта) Экспорт
	
	// Документы
	Если ДелопроизводствоКлиентСервер.ЭтоДокументПредприятия(Форма.Объект.Ссылка) Тогда
		
		// Роли файлов
		ЧислоРолейОбязательная = 0;
		ЧислоРолейТолькоОдинФайл = 0;
		ЧислоФайлов = 0;
		ЧислоФайловБезРоли = 0;
		ФайлСсылка = Неопределено; // ссылка на файл без роли
		Для Каждого СтрРоль Из ПараметрыВидаОбъекта.РолиФайлов Цикл
			
			Если СтрРоль.Обязательная Тогда
				ЧислоРолейОбязательная = ЧислоРолейОбязательная + 1;
			КонецЕсли;	
			
			Если СтрРоль.ТолькоОдинФайл Тогда
				ЧислоРолейТолькоОдинФайл = ЧислоРолейТолькоОдинФайл + 1;
			КонецЕсли;	
			
		КонецЦикла;		
		
		Для Каждого Строка Из Форма.Файлы.ПолучитьЭлементы() Цикл
			Если Не Строка.ЭтоРольФайла И Не Строка.ЭтоДокумент И Не Строка.ЭтоДокументЭДО Тогда
				ЧислоФайлов = ЧислоФайлов + 1;
				Если Не ЗначениеЗаполнено(Строка.РольФайла) Тогда
					ЧислоФайловБезРоли = ЧислоФайловБезРоли + 1;
					ФайлСсылка = Строка.Ссылка;
				КонецЕсли;	
			Иначе
				
				Для Каждого Подстрока Из Строка.ПолучитьЭлементы() Цикл
					ЧислоФайлов = ЧислоФайлов + 1;
					Если Не ЗначениеЗаполнено(Подстрока.РольФайла) Тогда
						ЧислоФайловБезРоли = ЧислоФайловБезРоли + 1;
						ФайлСсылка = Подстрока.Ссылка;
					КонецЕсли;	
				КонецЦикла;	
			КонецЕсли;	
		КонецЦикла;   
		
		ЕстьОшибки = Ложь;  
		ТекстПредупреждения = "";
		
		// Обязательная
		Для Каждого СтрРоль Из ПараметрыВидаОбъекта.РолиФайлов Цикл
			
			Если СтрРоль.Обязательная Тогда
				
				Если Не ЕстьФайлыСРолью(Форма, СтрРоль.Роль) Тогда
					
					Если ЧислоРолейОбязательная = 1 И ЧислоФайлов = 1 И ЧислоФайловБезРоли = 1 Тогда
						
						// автоматом поставим роль файлу
						ОбработкиОбъектовВызовСервера.ЗаписатьРольФайла(ФайлСсылка, СтрРоль.Роль);
						
						// перечитаем в форме документа
						Форма.ЗаполнитьСписокФайловКлиент();
						
					Иначе	
						
						ЕстьОшибки = Истина;
						ТекстПредупреждения = ТекстПредупреждения + СтрШаблон(
							НСтр("ru = 'Для запуска в обработку требуется указать хотя бы один файл с ролью ""%1"".'"), СтрРоль.Роль)
							 + Символы.ПС;
						
					КонецЕсли;	
					
				КонецЕсли;	
				
			КонецЕсли;	
			
		КонецЦикла;	
		
		// ТолькоОдинФайл
		Для Каждого СтрРоль Из ПараметрыВидаОбъекта.РолиФайлов Цикл
			
			Если СтрРоль.ТолькоОдинФайл Тогда
				
				ЧислоФайловСРолью = ЧислоФайловСРолью(Форма, СтрРоль.Роль);
				
				Если ЧислоФайловСРолью <> 1 Тогда
					
					Если ЧислоРолейТолькоОдинФайл = 1 И ЧислоФайлов = 1 И ЧислоФайловБезРоли = 1 Тогда
						// автоматом поставим роль файлу
						
						// автоматом поставим роль файлу
						ОбработкиОбъектовВызовСервера.ЗаписатьРольФайла(ФайлСсылка, СтрРоль.Роль);
						
						// перечитаем в форме документа
						Форма.ЗаполнитьСписокФайловКлиент();
						
					ИначеЕсли ЧислоФайловСРолью >= 2 Тогда	 
						
						ЕстьОшибки = Истина;
						ТекстПредупреждения = ТекстПредупреждения + СтрШаблон(
							НСтр("ru = 'Для запуска в обработку должен быть указан только один файл с ролью ""%1"".'"), СтрРоль.Роль)
							+ Символы.ПС + Символы.ПС;  
							
					КонецЕсли;
					
				КонецЕсли;	
				
			КонецЕсли;	
			
		КонецЦикла;	   
		
		Если ЕстьОшибки Тогда
			ПоказатьПредупреждение(, ТекстПредупреждения);
			Возврат Ложь;
		КонецЕсли;	
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Получает занятые файлы
Процедура ДобавитьЗанятыеФайлыСамойКарточки(Форма, МассивЗахваченныхФайлов) Экспорт
	
	// Документы
	Если ДелопроизводствоКлиентСервер.ЭтоДокументПредприятия(Форма.Объект.Ссылка) Тогда
	
		ДелопроизводствоКлиент.ДобавитьЗанятыеФайлыСамойКарточки(Форма, МассивЗахваченныхФайлов);
	
	КонецЕсли;
	
КонецПроцедуры	

Функция ЕстьФайлыСРолью(Форма, Роль)
	
	НашлиРоль = Ложь;
	
	Для Каждого Строка Из Форма.Файлы.ПолучитьЭлементы() Цикл
		Если Не Строка.ЭтоРольФайла  И Не Строка.ЭтоДокумент И Не Строка.ПометкаУдаления И Не Строка.ЭтоДокументЭДО Тогда
			Если Строка.РольФайла = Роль Тогда
				НашлиРоль = Истина;
				Прервать;	
			КонецЕсли;	
		Иначе
			
			Для Каждого Подстрока Из Строка.ПолучитьЭлементы() Цикл
				Если Подстрока.РольФайла = Роль И Не Подстрока.ПометкаУдаления Тогда
					НашлиРоль = Истина;
					Прервать;	
				КонецЕсли;	
			КонецЦикла;	
			
		КонецЕсли;	
			
	КонецЦикла;
	
	Возврат НашлиРоль;
	
КонецФункции	

Функция ЧислоФайловСРолью(Форма, Роль)
	
	ЧислоФайловСРолью = 0;
	
	Для Каждого Строка Из Форма.Файлы.ПолучитьЭлементы() Цикл
		Если Не Строка.ЭтоРольФайла И Не Строка.ЭтоДокумент И Не Строка.ЭтоДокументЭДО Тогда
			Если Строка.РольФайла = Роль И Не Строка.ПометкаУдаления Тогда
				ЧислоФайловСРолью = ЧислоФайловСРолью + 1;
			КонецЕсли;	
		Иначе
			
			Для Каждого Подстрока Из Строка.ПолучитьЭлементы() Цикл
				Если Подстрока.РольФайла = Роль И Не Подстрока.ПометкаУдаления Тогда
					ЧислоФайловСРолью = ЧислоФайловСРолью + 1;
				КонецЕсли;	
			КонецЦикла;	
			
		КонецЕсли;	
			
	КонецЦикла;
	
	Возврат ЧислоФайловСРолью;
	
КонецФункции	

#КонецОбласти
