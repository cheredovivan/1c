
#Область ПрограммныйИнтерфейс

// Вызывается из ПриСозданииНаСервере формы СозданиеДокументаПоШаблону
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения 
//  Отказ - Булево 
//  СтандартнаяОбработка - Булево
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт

// Локализация
	Параметры = Форма.Параметры;
	Элементы = Форма.Элементы;
	
	Если Параметры.ПоказыватьТолькоДокументыПригодныеДляЭДО = Истина Тогда 
		Форма.ПоказыватьТолькоДокументыПригодныеДляЭДО = Истина;
	Иначе
		Форма.ПоказыватьТолькоДокументыПригодныеДляЭДО = Ложь;
	КонецЕсли;
	
	// Регламентированный учет обращений
	Если Форма.РегламентированныйУчетОбращений И Не Форма.ПоказыватьТолькоДокументыПригодныеДляЭДО Тогда 
		Элементы.Далее.Видимость = Истина;
		Элементы.Далее.КнопкаПоУмолчанию = Истина;
		Элементы.СоздатьПоШаблону.Видимость = Ложь;
	Иначе 
		Элементы.Далее.Видимость = Ложь;
		Элементы.СоздатьПоШаблону.КнопкаПоУмолчанию = Истина;
		Элементы.СоздатьПоШаблону.Видимость = Истина;
	КонецЕсли;
// Локализация Конец

КонецПроцедуры

// Вызывается из ПолучитьВсеШаблоныДокументов формы СозданиеДокументаПоШаблону
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - 
//  Запрос - Запрос
Процедура ДополнитьЗапросШаблоновДокументов(Форма, Запрос) Экспорт
	
// Локализация
	Если Форма.ПоказыватьТолькоДокументыПригодныеДляЭДО Тогда
		
		Запрос.Текст = Запрос.Текст + "
			|	И ШаблоныДокументов.ФормаДокумента <> Значение(Перечисление.ВариантыФормДокументов.Бумажная)
			|	И Не ВидыДокументов.ЯвляетсяОбращениемОтГраждан";
		
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ДополнительныеПараметры", 
		"ЕСТЬNULL(ВидыДокументов.ЯвляетсяОбращениемОтГраждан, ЛОЖЬ) КАК ЭтоОбращение");
	
// Локализация Конец
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		",
		|	&ДополнительныеПараметры",
		"");

КонецПроцедуры

// Вызывается из ПолучитьВсеШаблоныДокументов формы СозданиеДокументаПоШаблону
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - 
//  Запрос - Запрос - 
//  БезОтбораПоВидам - Булево
Процедура ДополнитьЗапросПоВсемТематикам(Форма, Запрос, БезОтбораПоВидам) Экспорт
	
// Локализация
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ДополнительныеПоля", 
		"ЕСТЬNULL(ВсеВидыДокументов.ЯвляетсяОбращениемОтГраждан, ЛОЖЬ) КАК ЭтоОбращение");
	
	Если Форма.СозданиеОбращенияГраждан Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И (ТематикиДокументов.ВидДокумента.ЯвляетсяОбращениемОтГраждан)";
	КонецЕсли;
	
// Локализация Конец
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ДополнительныеПоля,", "");

КонецПроцедуры

#КонецОбласти
