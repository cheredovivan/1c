
// @strict-types

#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытий

// Выполняет регистрацию отметки времени константы.
//
// Параметры:
//	Источник - ОпределяемыйТип.ОтметкиВремениКонстанты - Константа, отметку времени которой нужно зарегистрировать.
//	Отказ - Булево - Признак отказа от записи.
//
Процедура КонстантыПриЗаписи(Источник, Отказ) Экспорт
	
	Если Отказ
			Или Не ОтметкиВремениИспользуются(Источник)
			Или ПараметрыСеанса.ВыполняетсяУдалениеОбъектов Тогда
		
		Возврат;
	КонецЕсли;
	
	ЗафиксироватьОтметкуВремениОбъекта(Источник);
	
КонецПроцедуры

// Выполняет регистрацию отметки времени ссылочного объекта (при записи объекта).
//
// Параметры:
//	Источник - ОпределяемыйТип.ОтметкиВремениСсылочныеОбъекты - Объект, отметку времени которого нужно зарегистрировать.
//	Отказ - Булево - Признак отказа от записи.
//
Процедура СсылочныеОбъектыПриЗаписи(Источник, Отказ) Экспорт
	
	Если Отказ
			Или Не ОтметкиВремениИспользуются(Источник)
			Или ПараметрыСеанса.ВыполняетсяУдалениеОбъектов Тогда
		
		Возврат;
	КонецЕсли;
	
	ЗафиксироватьОтметкуВремениОбъекта(Источник);
	
КонецПроцедуры

// Выполняет регистрацию отметки времени ссылочного объекта (при удалении объекта).
//
// Параметры:
//	Источник - ОпределяемыйТип.ОтметкиВремениСсылочныеОбъекты - Объект, отметку времени которого нужно зарегистрировать.
//	Отказ - Булево - Признак отказа от удаления.
//
Процедура СсылочныеОбъектыПередУдалением(Источник, Отказ) Экспорт
	
	Если Отказ
			Или Не ОтметкиВремениИспользуются(Источник)
			Или ПараметрыСеанса.ВыполняетсяУдалениеОбъектов Тогда
		
		Возврат;
	КонецЕсли;
	
	ЗафиксироватьОтметкуВремениОбъекта(Источник, Истина);
	
КонецПроцедуры

// Выполняет регистрацию отметки времени набора записей.
//
// Параметры:
//	Источник - ОпределяемыйТип.ОтметкиВремениРегистры - Объект, отметку времени которого нужно зарегистрировать.
//	Отказ - Булево - Признак отказа от записи.
//	Замещение - Булево - Режим замещения записей набора.
//
Процедура РегистрыПередЗаписью(Источник, Отказ, Замещение) Экспорт
	
	Если Отказ
			Или Не ОтметкиВремениИспользуются(Источник)
			Или ПараметрыСеанса.ВыполняетсяУдалениеОбъектов
			Или (Не Источник.Количество()
					И Не ОбщегоНазначенияДокументооборот.ЭтоРежимЗамещениеНабораЗаписей(Замещение)) Тогда
		
		Возврат;
	КонецЕсли;
	
	ЗафиксироватьОтметкуВремениРегистра(Источник, Замещение);
	
КонецПроцедуры

// Фиксирует отметку времени для ссылочного объекта или константы.
//
// Параметры:
//	Источник - ОпределяемыйТип.ОтметкиВремениКонстанты -
//			 - ОпределяемыйТип.ОтметкиВремениСсылочныеОбъекты -
//			 - ЛюбаяСсылка - Объект, для которого необходимо зафиксировать отметку времени.
//	ЭтоУдалениеОбъекта - Булево - Признак удаления объекта.
//
Процедура ЗафиксироватьОтметкуВремениОбъекта(Источник, ЭтоУдалениеОбъекта = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторОбъекта = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Источник));
	ОписаниеОтметкиВремени = ОтметкиВремени.ОписаниеКлючаОтметкиВремени(Источник);
	
	ОтметкаВремени = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Источник.ДополнительныеСвойства, "ОтметкаВремени", ОтметкиВремени.Текущая()); // Число
	ИсточникОтметокВремени = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Источник.ДополнительныеСвойства, "ОтметкиВремениИсточник");
	СведенияОбОтметке = СведенияОбОтметке(ОтметкаВремени, ИсточникОтметокВремени);
	
	НаборЗаписей = МенеджерОчередиОтметокВремени().СоздатьНаборЗаписей();
	
	Запись = НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(Запись, ОписаниеОтметкиВремени);
	ЗаполнитьЗначенияСвойств(Запись, СведенияОбОтметке);
	Запись.Объект = ИдентификаторОбъекта;
	Запись.Удаление = ЭтоУдалениеОбъекта;
	
	ОтметкиВремени.ЗаписатьНабор(НаборЗаписей,, Истина);
	
КонецПроцедуры

// Фиксирует отметку времени для набора записей регистра.
//
// Параметры:
//	Источник - РегистрСведенийНаборЗаписей - Объект, для которого необходимо зафиксировать отметку времени.
//	Режим - Булево, РежимЗамещения - Режим замещения набора записей.
//
Процедура ЗафиксироватьОтметкуВремениРегистра(Источник, Режим) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторОбъекта = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Источник));
	ВидКлючаОтметокВремени = ОтметкиВремениПовтИсп.ВидКлючаОбъекта(ИдентификаторОбъекта);
	
	Если ВидКлючаОтметокВремени = Перечисления.ВидыКлючейОтметокВремени.КлючНабораИдентификаторНабораЗаписей Тогда
		
		ЗафиксироватьОтметкиВремениРегистраИдентификаторНабораЗаписей(Источник, Режим);
		
	ИначеЕсли ВидКлючаОтметокВремени = Перечисления.ВидыКлючейОтметокВремени.КлючНабораПервоеСсылочноеИзмерение
			Или ВидКлючаОтметокВремени = Перечисления.ВидыКлючейОтметокВремени.КлючНабораПодчинениеРегистратору Тогда
		
		ЗафиксироватьОтметкиВремениРегистраСсылочноеИзмерение(Источник, Режим);
		
	Иначе
		
		ВызватьИсключение СтрШаблон(
			Нстр("ru = 'Отметки времени объекта ""%1"" не могут быть зафиксированы.
						|Причина: Вид ключа ""%2"" не поддерживается.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			ИдентификаторОбъекта,
			ВидКлючаОтметокВремени);
		
	КонецЕсли;
	
КонецПроцедуры

// Фиксирует отметку времени для ссылки.
//
// Параметры:
//	Ссылка - ЛюбаяСсылка - Ссылка, для которой необходимо зафиксировать отметку времени.
//	ОтметкаВремени - Число - Отметка времени.
//	ЭтоУдаление - Булево - Признак удаления объекта.
//	Источник - ОпределяемыйТип.ИсточникиДляОтметокВремени - Источник отметки времени.
//
Процедура ЗафиксироватьОтметкуВремениСсылки(Ссылка, ОтметкаВремени = Неопределено, ЭтоУдаление = Ложь, Источник = Неопределено) Экспорт
	
	ИдентификаторОбъекта = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Ссылка));
	
	ОписаниеКлюча = ОтметкиВремени.ОписаниеКлючаОтметкиВремени(Ссылка);
	
	Если ОписаниеКлюча.ВидКлюча = Неопределено Тогда
		ВызватьИсключение СтрШаблон("%1 %2 ""%3""",
			НСтр("ru = 'Не удалось определить вид ключа'", ОбщегоНазначения.КодОсновногоЯзыка()),
			НСтр("ru = 'для'", ОбщегоНазначения.КодОсновногоЯзыка()),
			ИдентификаторОбъекта);
	КонецЕсли;
	
	Набор = РегистрыСведений["ОтметкиВремениОчередь" + ТекущийНомерОчереди()].СоздатьНаборЗаписей(); // РегистрСведенийНаборЗаписей.ОтметкиВремениОчередь1
	
	Запись = Набор.Добавить(); // РегистрСведенийЗапись.ОтметкиВремениОчередь1
	ЗаполнитьЗначенияСвойств(Запись, ОписаниеКлюча);
	Запись.Объект = ИдентификаторОбъекта;
	Запись.Удаление = ЭтоУдаление;
	Запись.Источник = Источник; 
	Запись.Отметка = ?(ОтметкаВремени = Неопределено, ОтметкиВремени.Текущая(), ОтметкаВремени);
	
	Набор.Отбор.Отметка.Установить(Запись.Отметка);
	Набор.Отбор.ИдентификаторКлюча.Установить(Запись.ИдентификаторКлюча);
	Набор.Отбор.ТипКлюча.Установить(Запись.ТипКлюча);
	Набор.Отбор.Объект.Установить(Запись.Объект);
	
	ОтметкиВремени.ОтключитьРегистрацию(Набор);
	
	Набор.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает менеджер очереди отметок времени.
//
// Возвращаемое значение:
//	РегистрСведенийМенеджер.ОтметкиВремениОчередь1,
//	РегистрСведенийМенеджер.ОтметкиВремениОчередь2,
//	РегистрСведенийМенеджер.ОтметкиВремениОчередь3
//
Функция МенеджерОчередиОтметокВремени()
	
	Возврат РегистрыСведений["ОтметкиВремениОчередь" + ТекущийНомерОчереди()];
	
КонецФункции

// Возвращает номер очереди, в которую будет записана отметка времени.
//
// Возвращаемое значение:
//  Число - Номер очереди.
//
Функция ТекущийНомерОчереди()
	
	Возврат (НомерСоединенияИнформационнойБазы() % 3) + 1;
	
КонецФункции

// Определяет возможность регистрации отметки времени для источника.
//
// Параметры:
//	Источник - ОпределяемыйТип.ОтметкиВремениКонстанты -
//			 - ОпределяемыйТип.ОтметкиВремениСсылочныеОбъекты -
//			 - ОпределяемыйТип.ОтметкиВремениРегистры - Объект, для которого необходимо проверить возможность.
//														регистрации отметки времени.
//
// Возвращаемое значение:
//	Булево - Истина - Отметка времени может быть зарегистрирована.
//
Функция ОтметкиВремениИспользуются(Источник)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОтметкиВремени")
			Или Источник.ДополнительныеСвойства.Свойство("ОтключитьОтметкиВремени") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Конструктор сведений об отметке времени.
//
// Параметры:
//	Отметка - Число - Отметка времени.
//	Источник - ОпределяемыйТип.ИсточникиДляОтметокВремени, Неопределено - Источник отметок.
//
// Возвращаемое значение:
//	Структура:
//		* Отметка - Число - Отметка времени.
//		* Источник - ОпределяемыйТип.ИсточникиДляОтметокВремени, Неопределено - Источник отметок.
//
Функция СведенияОбОтметке(Отметка, Источник)
	
	СведенияОбОтметке = Новый Структура;
	СведенияОбОтметке.Вставить("Отметка", Отметка);
	СведенияОбОтметке.Вставить("Источник", Источник);
	
	Возврат СведенияОбОтметке;
	
КонецФункции

// Выполняет запись в информационную базу набора записей отметок времени.
//
// Параметры:
//	НаборЗаписей - РегистрСведенийНаборЗаписей.ОтметкиВремениОчередь1 - Набор, в который нужно добавить запись.
//				 - РегистрСведенийНаборЗаписей.ОтметкиВремениОчередь2 -
//				 - РегистрСведенийНаборЗаписей.ОтметкиВремениОчередь3 -
//	ИдентификаторКлюча - УникальныйИдентификатор - Идентификатор ключа отметки времени.
//	ТипКлюча - СправочникСсылка.ИдентификаторыОбъектовМетаданных - Тип ключа отметки времени.
//			 - СправочникСсылка.ИдентификаторыОбъектовРасширений -
//			 - Неопределено -
//	Объект - СправочникСсылка.ИдентификаторыОбъектовМетаданных - Тип объекта-источника отметок времени.
//		   - СправочникСсылка.ИдентификаторыОбъектовРасширений -
//		   - Неопределено -
//	ВидКлюча - см. ОтметкиВремени.ВидКлючаОбъекта.
//
// Возвращаемое значение:
//	РегистрСведенийЗапись.ОтметкиВремениОчередь1,
//	РегистрСведенийЗапись.ОтметкиВремениОчередь2,
//	РегистрСведенийЗапись.ОтметкиВремениОчередь3
//
Функция ДобавитьЗаписьВНабор(НаборЗаписей, ИдентификаторКлюча, ТипКлюча, Объект, ВидКлюча)
	
	Запись = НаборЗаписей.Добавить();
	Запись.ИдентификаторКлюча = ИдентификаторКлюча;
	Запись.ТипКлюча = ТипКлюча;
	Запись.Объект = Объект;
	Запись.ВидКлюча = ВидКлюча;
	
	Возврат Запись;
	
КонецФункции

// Фиксирует отметки времени набора-источника (идентификатор набора записей).
//
// Параметры:
//	Источник - РегистрСведенийНаборЗаписей - Объект, для которого необходимо зафиксировать отметку времени.
//	Режим - Булево, РежимЗамещения - Режим замещения набора записей.
//
Процедура ЗафиксироватьОтметкиВремениРегистраИдентификаторНабораЗаписей(Источник, Режим)
	
	ИдентификаторОбъекта = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Источник));
	
	ОтметкаВремени = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Источник.ДополнительныеСвойства, "ОтметкаВремени", ОтметкиВремени.Текущая()); // Число
	ИсточникОтметокВремени = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Источник.ДополнительныеСвойства, "ОтметкиВремениИсточник");
	СведенияОбОтметке = СведенияОбОтметке(ОтметкаВремени, ИсточникОтметокВремени);
	
	ЭтоЗамещениеЗаписей = ОбщегоНазначенияДокументооборот.ЭтоРежимЗамещениеНабораЗаписей(Режим);
	ЭтоУдалениеЗаписей = ОбщегоНазначенияДокументооборот.ЭтоРежимУдалениеНабораЗаписей(Режим);
	ОтборНабораПолный = ОтборНабораПолный(Источник);
	
	ПолеКлючаОбъекта = ОтметкиВремени.ИмяПоляХраненияКлючейВидаИдентификаторНабораЗаписей();
	ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораИдентификаторНабораЗаписей;
	
	НаборЗаписей = МенеджерОчередиОтметокВремени().СоздатьНаборЗаписей();
	
	Если ЭтоЗамещениеЗаписей И Источник.Количество() = 0 Тогда
		КлючИсточника = ОтметкиВремени.ИдентификаторНабораЗаписей(Источник); // УникальныйИдентификатор
		
		Запись = ДобавитьЗаписьВНабор(
			НаборЗаписей,
			КлючИсточника,
			Неопределено,
			ИдентификаторОбъекта,
			ВидКлюча);
		
		Запись.ЗначенияКлюча = Новый ХранилищеЗначения(Источник.Отбор, Новый СжатиеДанных(9));
		Запись.Удаление = Истина;
		
		ЗаполнитьЗначенияСвойств(Запись, СведенияОбОтметке);
	Иначе
		Если ЭтоЗамещениеЗаписей И Не ОтборНабораПолный Тогда
			ДобавитьВНаборКлючиЗамещаемыхЗаписейИдентификаторНабораЗаписей(НаборЗаписей, Источник, СведенияОбОтметке);
		КонецЕсли;
		
		КлючевойНабор =
			ОтметкиВремениПовтИсп.МенеджерОбъектаПоИдентификатору(ИдентификаторОбъекта).СоздатьНаборЗаписей(); // РегистрСведенийНаборЗаписей
		
		Для Каждого СтрокаНабора Из Источник Цикл // РегистрСведенийЗапись
			Для Каждого Элемент Из КлючевойНабор.Отбор Цикл
				Элемент.Установить(Элемент.ТипЗначения.ПривестиЗначение(СтрокаНабора[Элемент.Имя]), Истина);
			КонецЦикла;
			КлючИсточника = ОтметкиВремени.ИдентификаторНабораЗаписей(КлючевойНабор); // УникальныйИдентификатор
			
			Запись = ДобавитьЗаписьВНабор(
				НаборЗаписей,
				КлючИсточника,
				Неопределено,
				ИдентификаторОбъекта,
				ВидКлюча);
			
			ЗаполнитьЗначенияСвойств(Запись, СведенияОбОтметке);
			
			Если ЭтоУдалениеЗаписей Тогда
				Запись.ЗначенияКлюча = Новый ХранилищеЗначения(КлючевойНабор.Отбор, Новый СжатиеДанных(9));
				Запись.Удаление = Истина;
			КонецЕсли;
			
			СтрокаНабора[ПолеКлючаОбъекта] = КлючИсточника;
			
			ОтметкиВремени.ЗаписатьНабор(НаборЗаписей);
		КонецЦикла;
	КонецЕсли;
	
	ОтметкиВремени.ЗаписатьНабор(НаборЗаписей,, Истина);
	
КонецПроцедуры

// Фиксирует отметки времени набора-источника (первое ссылочное измерение).
//
// Параметры:
//	Источник - РегистрСведенийНаборЗаписей - Объект, для которого необходимо зафиксировать отметку времени.
//	Режим - Булево, РежимЗамещения - Режим замещения набора записей.
//
Процедура ЗафиксироватьОтметкиВремениРегистраСсылочноеИзмерение(Источник, Режим)
	
	ИдентификаторОбъекта = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Источник));
	
	ОтметкаВремени = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Источник.ДополнительныеСвойства, "ОтметкаВремени", ОтметкиВремени.Текущая()); // Число
	ИсточникОтметокВремени = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Источник.ДополнительныеСвойства, "ОтметкиВремениИсточник");
	СведенияОбОтметке = СведенияОбОтметке(ОтметкаВремени, ИсточникОтметокВремени);
	
	ЭтоЗамещениеЗаписей = ОбщегоНазначенияДокументооборот.ЭтоРежимЗамещениеНабораЗаписей(Режим);
	ОтборНабораПолный = ОтборНабораПолный(Источник);
	
	ПолеКлючаОбъекта = ОтметкиВремениПовтИсп.ПолеКлючаОбъекта(ИдентификаторОбъекта);
	ВидКлюча = ОтметкиВремениПовтИсп.ВидКлючаОбъекта(ИдентификаторОбъекта);
	
	НаборЗаписей = МенеджерОчередиОтметокВремени().СоздатьНаборЗаписей();
	
	Если ОтборНабораПолный Тогда
		КлючИсточника = Источник.Отбор[ПолеКлючаОбъекта].Значение; // ЛюбаяСсылка
		
		ОтметкиВремени.ПроверитьЗначениеКлючаНабораЗаписей(
			КлючИсточника, ИдентификаторОбъекта, Источник.Отбор, ПолеКлючаОбъекта);
		
		Запись = ДобавитьЗаписьВНабор(
			НаборЗаписей,
			КлючИсточника.УникальныйИдентификатор(),
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(КлючИсточника)),
			ИдентификаторОбъекта,
			ВидКлюча);
			
		ЗаполнитьЗначенияСвойств(Запись, СведенияОбОтметке);
	Иначе
		Если ЭтоЗамещениеЗаписей
				И ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораПервоеСсылочноеИзмерение Тогда
			
			ДобавитьВНаборКлючиЗамещаемыхЗаписейПервоеСсылочноеИзмерение(НаборЗаписей, Источник, СведенияОбОтметке);
		КонецЕсли;

		ТаблицаКлючей = Источник.Выгрузить(, ПолеКлючаОбъекта); // ТаблицаЗначений
		ТаблицаКлючей.Свернуть(ПолеКлючаОбъекта);
		
		Для Каждого СтрокаТаблицы Из ТаблицаКлючей Цикл // СтрокаТаблицыЗначений
			КлючИсточника = СтрокаТаблицы[ПолеКлючаОбъекта]; // ЛюбаяСсылка
			
			ОтметкиВремени.ПроверитьЗначениеКлючаНабораЗаписей(
				КлючИсточника, ИдентификаторОбъекта, Источник.Отбор, ПолеКлючаОбъекта);
			
			Запись = ДобавитьЗаписьВНабор(
				НаборЗаписей,
				КлючИсточника.УникальныйИдентификатор(),
				ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(КлючИсточника)),
				ИдентификаторОбъекта,
				ВидКлюча);
			
			ЗаполнитьЗначенияСвойств(Запись, СведенияОбОтметке);
			
			ОтметкиВремени.ЗаписатьНабор(НаборЗаписей);
		КонецЦикла;
	КонецЕсли;
	
	ОтметкиВремени.ЗаписатьНабор(НаборЗаписей,, Истина);
	
КонецПроцедуры

// Дополняет набор отметок времени ключами замещаемых записей (первое ссылочное измерение).
//
// Параметры:
//	НаборЗаписей - РегистрСведенийНаборЗаписей.ОтметкиВремениОчередь1 - Набор записей отметок времени.
//				 - РегистрСведенийНаборЗаписей.ОтметкиВремениОчередь2 -
//				 - РегистрСведенийНаборЗаписей.ОтметкиВремениОчередь3 -
//	Источник - РегистрСведенийНаборЗаписей - Набор, который записывается  в информационную базу.
//	СведенияОбОтметке - см. СведенияОбОтметке.
//
Процедура ДобавитьВНаборКлючиЗамещаемыхЗаписейПервоеСсылочноеИзмерение(НаборЗаписей, Источник, СведенияОбОтметке)
	
	ИдентификаторОбъекта = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Источник));
	ПолеКлюча = ОтметкиВремениПовтИсп.ПолеКлючаОбъекта(ИдентификаторОбъекта);
	
	Запрос = Новый Запрос(ШаблонТекстаЗапросаЗамещаемыеЗаписиНабора());
	
	МассивУсловияОтбора = Новый Массив; // Массив Из Строка
	Для Каждого Элемент Из Источник.Отбор Цикл // ЭлементОтбора
		Если Элемент.Использование Тогда
			МассивУсловияОтбора.Добавить(СтрШаблон("Таблица.%1 = &Параметр%1", Элемент.Имя));
			Запрос.УстановитьПараметр(СтрШаблон("Параметр%1", Элемент.Имя), Элемент.Значение);
		КонецЕсли;
	КонецЦикла;
	
	СписокПолейТаблицы = СтрШаблон("Таблица.%1 КАК %1", ПолеКлюча);
	СписокПолейГруппировки = СтрШаблон("Таблица.%1", ПолеКлюча);
	УсловияОтбораТаблицы = СтрСоединить(МассивУсловияОтбора, Символы.ПС + Символы.Таб + " И ");
	
	УсловияОтбораТаблицы = ?(МассивУсловияОтбора.Количество() = 0,
		"ИСТИНА",
		СтрСоединить(МассивУсловияОтбора, Символы.ПС + Символы.Таб + " И "));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СписокПолейТаблицы", СписокПолейТаблицы);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СписокПолейГруппировки", СписокПолейГруппировки);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловияОтбораТаблицы", УсловияОтбораТаблицы);
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, "#ИмяТаблицы", ОтметкиВремениПовтИсп.ПолноеИмяОбъектаПоИдентификатору(ИдентификаторОбъекта));
		
	Запрос.УстановитьПараметр("ВнешнийИсточник", Источник.Выгрузить(, ПолеКлюча));
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		КлючЗаписиНабора = Выборка[ПолеКлюча]; // ЛюбаяСсылка, Неопределено
		
		ОтметкиВремени.ПроверитьЗначениеКлючаНабораЗаписей(
			КлючЗаписиНабора, ИдентификаторОбъекта, Источник.Отбор, ПолеКлюча);
		
		Запись = ДобавитьЗаписьВНабор(
			НаборЗаписей,
			КлючЗаписиНабора.УникальныйИдентификатор(),
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(КлючЗаписиНабора)),
			ИдентификаторОбъекта,
			Перечисления.ВидыКлючейОтметокВремени.КлючНабораПервоеСсылочноеИзмерение);
		ЗаполнитьЗначенияСвойств(Запись, СведенияОбОтметке);
		
		ОтметкиВремени.ЗаписатьНабор(НаборЗаписей);
	КонецЦикла;
	
КонецПроцедуры

// Дополняет набор отметок времени ключами замещаемых записей (идентификатор набора записей).
//
// Параметры:
//	НаборЗаписей - РегистрСведенийНаборЗаписей.ОтметкиВремениОчередь1 - Набор записей отметок времени.
//				 - РегистрСведенийНаборЗаписей.ОтметкиВремениОчередь2 -
//				 - РегистрСведенийНаборЗаписей.ОтметкиВремениОчередь3 -
//	Источник - РегистрСведенийНаборЗаписей - Набор, который записывается  в информационную базу.
//	СведенияОбОтметке - см. СведенияОбОтметке.
//
Процедура ДобавитьВНаборКлючиЗамещаемыхЗаписейИдентификаторНабораЗаписей(НаборЗаписей, Источник, СведенияОбОтметке)
	
	ИдентификаторОбъекта = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Источник));
	ПолеКлюча = ОтметкиВремениПовтИсп.ПолеКлючаОбъекта(ИдентификаторОбъекта);
	
	Запрос = Новый Запрос(ШаблонТекстаЗапросаЗамещаемыеЗаписиНабора());
	
	МассивПолейИсточника = Новый Массив; // Массив Из Строка
	МассивПолейТаблицы = Новый Массив; // Массив Из Строка
	МассивПолейГруппировки = Новый Массив; // Массив Из Строка
	МассивУсловияОтбора = Новый Массив; // Массив Из Строка
	
	Для Каждого Элемент Из Источник.Отбор Цикл // Отбор
		МассивПолейИсточника.Добавить(Элемент.Имя);
		МассивПолейТаблицы.Добавить(СтрШаблон("Таблица.%1 КАК %1", Элемент.Имя));
		МассивПолейГруппировки.Добавить(СтрШаблон("Таблица.%1", Элемент.Имя));
		Если Элемент.Использование Тогда
			МассивУсловияОтбора.Добавить(СтрШаблон("Таблица.%1 = &Параметр%1", Элемент.Имя));
			Запрос.УстановитьПараметр(СтрШаблон("Параметр%1", Элемент.Имя), Элемент.Значение);
		КонецЕсли;
	КонецЦикла;
	
	СписокПолейИсточника = СтрСоединить(МассивПолейИсточника, ",");
	СписокПолейТаблицы = СтрСоединить(МассивПолейТаблицы, "," + Символы.ПС + Символы.Таб);
	СписокПолейГруппировки = СтрСоединить(МассивПолейГруппировки, "," + Символы.ПС + Символы.Таб);
	УсловияОтбораТаблицы = ?(МассивУсловияОтбора.Количество() = 0,
		"ИСТИНА",
		СтрСоединить(МассивУсловияОтбора, Символы.ПС + Символы.Таб + " И "));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СписокПолейТаблицы", СписокПолейТаблицы);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СписокПолейГруппировки", СписокПолейГруппировки);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловияОтбораТаблицы", УсловияОтбораТаблицы);
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, "#ИмяТаблицы", ОтметкиВремениПовтИсп.ПолноеИмяОбъектаПоИдентификатору(ИдентификаторОбъекта));
		
	Запрос.УстановитьПараметр("ВнешнийИсточник", Источник.Выгрузить(, СписокПолейИсточника));
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерОбъекта = ОтметкиВремениПовтИсп.МенеджерОбъектаПоИдентификатору(ИдентификаторОбъекта); // РегистрСведенийМенеджер
	КлючевойНабор = МенеджерОбъекта.СоздатьНаборЗаписей(); // РегистрСведенийНаборЗаписей
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Для Каждого Элемент Из КлючевойНабор.Отбор Цикл
			Элемент.Установить(Элемент.ТипЗначения.ПривестиЗначение(Выборка[Элемент.Имя]));
		КонецЦикла;
		КлючЗаписиНабора = ОтметкиВремени.ИдентификаторНабораЗаписей(КлючевойНабор);
		
		Запись = ДобавитьЗаписьВНабор(
			НаборЗаписей,
			КлючЗаписиНабора,
			Неопределено,
			ИдентификаторОбъекта,
			Перечисления.ВидыКлючейОтметокВремени.КлючНабораИдентификаторНабораЗаписей);
		Запись.Удаление = Истина;
		Запись.ЗначенияКлюча = Новый ХранилищеЗначения(КлючевойНабор.Отбор, Новый СжатиеДанных(9));
		ЗаполнитьЗначенияСвойств(Запись, СведенияОбОтметке);
		
		ОтметкиВремени.ЗаписатьНабор(НаборЗаписей);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает шаблон текста запроса выборки замещаемых записей набора.
//
// Возвращаемое значение:
//	Строка.
//
Функция ШаблонТекстаЗапросаЗамещаемыеЗаписиНабора()
	
	Возврат
		"ВЫБРАТЬ
		|	&СписокПолейТаблицы
		|ПОМЕСТИТЬ ВнешнийИсточник
		|ИЗ
		|	&ВнешнийИсточник КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&СписокПолейТаблицы,
		|	СУММА(Таблица.Удаление) КАК Удаление
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		&СписокПолейТаблицы,
		|		1 КАК Удаление
		|	ИЗ
		|		#ИмяТаблицы КАК Таблица
		|	ГДЕ
		|		&УсловияОтбораТаблицы
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		&СписокПолейТаблицы,
		|		-1
		|	ИЗ
		|		ВнешнийИсточник КАК Таблица) КАК Таблица
		|
		|СГРУППИРОВАТЬ ПО
		|	&СписокПолейГруппировки
		|
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.Удаление) > 0";
	
КонецФункции

// Определяет, является ли отбор набора записей полным.
//
// Параметры:
//	Источник - РегистрСведенийНаборЗаписей - Набор записей, полноту отбора которого нужно определить.
//
// Возвращаемое значение:
//	Булево - Истина, если отбор набора записей является полным.
//
Функция ОтборНабораПолный(Источник)
	
	ИдентификаторОбъекта = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Источник));
	ВидКлючаОтметокВремени = ОтметкиВремениПовтИсп.ВидКлючаОбъекта(ИдентификаторОбъекта);
	
	Если ВидКлючаОтметокВремени = Перечисления.ВидыКлючейОтметокВремени.КлючНабораИдентификаторНабораЗаписей Тогда
		Для Каждого ЭлементОтбора Из Источник.Отбор Цикл
			Если Не ЭлементОтбора.Использование Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
	Иначе
		ПолеКлючаОбъекта = ОтметкиВремениПовтИсп.ПолеКлючаОбъекта(ИдентификаторОбъекта);
		Возврат Источник.Отбор[ПолеКлючаОбъекта].Использование;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти
