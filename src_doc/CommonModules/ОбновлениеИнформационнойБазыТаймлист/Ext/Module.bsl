////////////////////////////////////////////////////////////////////////////////
// ОбновлениеИнформационнойБазыТаймлист: Библиотека Таймлист (БТ).
// Процедуры и функции БТ.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииПодсистемы
// Параметры:
//  Описание - см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииПодсистемы.Описание
//
Процедура ПриДобавленииПодсистемы(Описание) Экспорт
	
	Описание.Имя = "БиблиотекаТаймлист";
	Описание.Версия = "1.0.1.3";
	Описание.РежимВыполненияОтложенныхОбработчиков = "Параллельно";
	Описание.ПараллельноеОтложенноеОбновлениеСВерсии = "1.0.1.1";
	Описание.ТребуемыеПодсистемы.Добавить("СтандартныеПодсистемы");
	
КонецПроцедуры

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
// @skip-warning ПустойМетод - особенность реализации.
//
// Параметры:
//  Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
// Пример:
//  // Добавление процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.0.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_0_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//  Обработчик.Опциональный        = Истина;
// 
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.0.1.1";
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.РежимВыполнения = "Оперативно";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыТаймлист.ПерейтиНаВерсию_1_0_1_1";
	Обработчик.Комментарий = НСтр("ru = 'Заполнение констант и добавление полномочия для работы с сервисом.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.0.1.2";
	Обработчик.РежимВыполнения = "Оперативно";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыТаймлист.ПерейтиНаВерсию_1_0_1_2";
	Обработчик.Комментарий = НСтр(
		"ru = 'Осуществляет перенос данных авторизации из константы УдалитьТаймлистДанныеАвторизации в РС БезопасноеХранилищеДанных.'");
	
#Область РегистрыСведений_ТаймлистДанныеРаботыСервиса_ОбработатьДанныеДляПереходаНаНовуюВерсию_1_0_1_3
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ТаймлистДанныеРаботыСервиса.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "1.0.1.3";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("1e1729ee-7ba9-4789-a3d5-0c486fb279b2");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления =
		"РегистрыСведений.ТаймлистДанныеРаботыСервиса.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Удаляет тег команды-гиперссылки ""TimelistGetAutoprotocol"" из расшифровки.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.ТаймлистДанныеРаботыСервиса.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ТаймлистДанныеРаботыСервиса.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
#КонецОбласти
	
КонецПроцедуры

// Вызывается перед процедурами-обработчиками обновления данных ИБ.
// @skip-warning ПустойМетод - особенность реализации.
//
Процедура ПередОбновлениемИнформационнойБазы() Экспорт
КонецПроцедуры

// Вызывается после завершения обновления данных ИБ.
// @skip-warning ПустойМетод - особенность реализации.
//
// Параметры:
//   ПредыдущаяВерсия - Строка - версия до обновления. "0.0.0.0" для "пустой" ИБ.
//   ТекущаяВерсия - Строка - версия после обновления.
//   ВыполненныеОбработчики - ДеревоЗначений - список выполненных процедур-обработчиков обновления,
//		сгруппированных по номеру версии.
//   ВыводитьОписаниеОбновлений - Булево - (возвращаемое значение) если установить Истина,
//		то будет выдана форма с описанием обновлений. По умолчанию, Истина.
//   МонопольныйРежим - Булево - Истина, если обновление выполнялось в монопольном режиме.
//   
// Пример:
// 
//	// Обход выполненных обработчиков обновления.
//	Для Каждого Версия Из ВыполненныеОбработчики.Строки Цикл
//		
//		Если Версия.Версия = "*" Тогда
//			// Обработчик, который может выполнятся при каждой смене версии.
//		Иначе
//			// Обработчик, который выполняется для определенной версии.
//		КонецЕсли;
//		
//		Для Каждого Обработчик Из Версия.Строки Цикл
//			...
//		КонецЦикла;
//		
//	КонецЦикла;
//
Процедура ПослеОбновленияИнформационнойБазы(Знач ПредыдущаяВерсия, Знач ТекущаяВерсия,
	Знач ВыполненныеОбработчики, ВыводитьОписаниеОбновлений, МонопольныйРежим) Экспорт
КонецПроцедуры

// Вызывается при подготовке табличного документа с описанием изменений в программе.
// @skip-warning ПустойМетод - особенность реализации.
//
// Параметры:
//   Макет - ТабличныйДокумент - описание обновления всех библиотек и конфигурации.
//           Макет можно дополнить или заменить.
//           См. также общий макет ОписаниеИзмененийСистемы.
//
Процедура ПриПодготовкеМакетаОписанияОбновлений(Знач Макет) Экспорт
КонецПроцедуры

// Добавляет в список процедуры-обработчики перехода с другой программы (с другим именем конфигурации).
// Например, для перехода между разными, но родственными конфигурациями: базовая -> проф -> корп.
// Вызывается перед началом обновления данных ИБ.
// @skip-warning ПустойМетод - особенность реализации.
// 
// Параметры:
//  Обработчики - ТаблицаЗначений - с колонками:
//    * ПредыдущееИмяКонфигурации - Строка - имя конфигурации, с которой выполняется переход;
//    * Процедура                 - Строка - полное имя процедуры-обработчика перехода с программы ПредыдущееИмяКонфигурации. 
//                                  Например, "ОбновлениеИнформационнойБазыУПП.ЗаполнитьУчетнуюПолитику"
//                                  Обязательно должна быть экспортной.
//                                  
// Пример:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.ПредыдущееИмяКонфигурации  = "УправлениеТорговлей";
//  Обработчик.Процедура                  = "ОбновлениеИнформационнойБазыУПП.ЗаполнитьУчетнуюПолитику";
//
Процедура ПриДобавленииОбработчиковПереходаСДругойПрограммы(Обработчики) Экспорт
КонецПроцедуры

// Позволяет переопределить режим обновления данных информационной базы.
// Для использования в редких (нештатных) случаях перехода, не предусмотренных в
// стандартной процедуре определения режима обновления.
// @skip-warning ПустойМетод - особенность реализации.
//
// Параметры:
//   РежимОбновленияДанных - Строка - в обработчике можно присвоить одно из значений:
//              "НачальноеЗаполнение"     - если это первый запуск пустой базы (области данных);
//              "ОбновлениеВерсии"        - если выполняется первый запуск после обновление конфигурации базы данных;
//              "ПереходСДругойПрограммы" - если выполняется первый запуск после обновление конфигурации базы данных, 
//                                          в которой изменилось имя основной конфигурации.
//
//   СтандартнаяОбработка  - Булево - если присвоить Ложь, то стандартная процедура
//                                    определения режима обновления не выполняется, 
//                                    а используется значение РежимОбновленияДанных.
//
Процедура ПриОпределенииРежимаОбновленияДанных(РежимОбновленияДанных, СтандартнаяОбработка) Экспорт
КонецПроцедуры

// Вызывается после выполнения всех процедур-обработчиков перехода с другой программы (с другим именем конфигурации),
// и до начала выполнения обновления данных ИБ.
// @skip-warning ПустойМетод - особенность реализации.
//
// Параметры:
//  ПредыдущееИмяКонфигурации    - Строка - имя конфигурации до перехода.
//  ПредыдущаяВерсияКонфигурации - Строка - имя предыдущей конфигурации (до перехода).
//  Параметры                    - Структура - со свойствами: 
//    * ВыполнитьОбновлениеСВерсии   - Булево - по умолчанию Истина. Если установить Ложь, 
//        то будут выполнена только обязательные обработчики обновления (с версией "*").
//    * ВерсияКонфигурации           - Строка - номер версии после перехода. 
//        По умолчанию, равен значению версии конфигурации в свойствах метаданных.
//        Для того чтобы выполнить, например, все обработчики обновления с версии ПредыдущаяВерсияКонфигурации, 
//        следует установить значение параметра в ПредыдущаяВерсияКонфигурации.
//        Для того чтобы выполнить вообще все обработчики обновления, установить значение "0.0.0.1".
//    * ОчиститьСведенияОПредыдущейКонфигурации - Булево - по умолчанию Истина. 
//        Для случаев когда предыдущая конфигурация совпадает по имени с подсистемой текущей конфигурации, следует
//        указать Ложь.
//
Процедура ПриЗавершенииПереходаСДругойПрограммы(Знач ПредыдущееИмяКонфигурации,
	 Знач ПредыдущаяВерсияКонфигурации, Параметры) Экспорт
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ПроцедурыОбновления

// Заполняет константы и добавляет полномочие для работы с сервисом.
//
Процедура ПерейтиНаВерсию_1_0_1_1() Экспорт
	
	Если ОбщегоНазначенияДокументооборот.ЭтоДокументооборотХолдинга() 
		И ОбменДаннымиДокументооборотПовтИсп.ЭтоПодчиненныйУзел() Тогда
		Возврат;
	КонецЕсли;
	
	Константы.ТаймлистПорт.Установить(443);
	Константы.ТаймлистЗащищенноеСоединение.Установить(Истина);
	
	АдресСервиса = Константы.ТаймлистАдресСервиса.Получить(); //константа могла быть установлена ранее
	Если Не ЗначениеЗаполнено(АдресСервиса) Тогда
		Константы.ТаймлистАдресСервиса.Установить("restapi.timelist.ru");
	КонецЕсли;
	
	ПоддерживаемыеРасширенияФайлов = Константы.ТаймлистАдресСервиса.Получить(); //константа могла быть установлена ранее
	Если Не ЗначениеЗаполнено(ПоддерживаемыеРасширенияФайлов) Тогда
		РасширенияФайловТаймлистПоУмолчанию = ТаймлистКлиентСервер.РасширенияФайловТаймлистПоУмолчанию();
		Константы.ТаймлистСписокРасширенийФайлов.Установить(РасширенияФайловТаймлистПоУмолчанию);
	КонецЕсли;
	
	// Полномочие "Пользователи сервиса Таймлист"
	ИдентификаторСтрока = "f4445076-62d5-43e6-9c9a-f5774bfa8940";
	ИдентификаторПрофиля = Новый УникальныйИдентификатор("f4445076-62d5-43e6-9c9a-f5774bfa8940");
	
	Полномочие = Справочники.ПрофилиГруппДоступа.ПоставляемыйПрофильПоИдентификатору(ИдентификаторСтрока); 
	Если Полномочие = Неопределено Тогда
		Полномочие = Справочники.ПрофилиГруппДоступа.СоздатьЭлемент();
		Полномочие.ИдентификаторПоставляемыхДанных = ИдентификаторПрофиля;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Полномочие);
		Справочники.ПрофилиГруппДоступа.ЗаполнитьПоставляемыйПрофиль(Полномочие.Ссылка, Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Осуществляет перенос данных авторизации из константы УдалитьТаймлистДанныеАвторизации в РС БезопасноеХранилищеДанных.
//
Процедура ПерейтиНаВерсию_1_0_1_2() Экспорт
	
	ДанныеАвторизации = Константы.УдалитьТаймлистДанныеАвторизации.Получить().Получить();
	
	Если ДанныеАвторизации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
		"Таймлист",
		ДанныеАвторизации.Логин,
		"login");
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
		"Таймлист",
		ДанныеАвторизации.Пароль,
		"password");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
