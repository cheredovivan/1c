////////////////////////////////////////////////////////////////////////////////
// Бронирование помещений
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Устанавливает пометку удаления брони и оповещает другие формы.
//
// Параметры:
//  Бронь			 - ДокументСсылка.Бронь	 - Бронь.
//  ПометкаУдаления	 - Булево				 - Новая пометка удаления.
// 
// Возвращаемое значение:
//  Булево - Признак того что новая пометка удаления была установлена.
//
Функция УстановитьПометкуУдаления(Бронь, ПометкаУдаления) Экспорт
	
	УстановленаПометкаУдаления = Ложь;
	
	Если ТипЗнч(Бронь) <> Тип("ДокументСсылка.Бронь") Тогда
		Возврат УстановленаПометкаУдаления;
	КонецЕсли;
	
	БроньОбъект = Бронь.ПолучитьОбъект();
	БроньОбъект.Заблокировать();
	Если БроньОбъект.ПометкаУдаления <> ПометкаУдаления Тогда
		
		БроньОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
		ДобавитьВИсториюРаботыПользователя(БроньОбъект);
		УстановленаПометкаУдаления = Истина;
		
		Если ПометкаУдаления Тогда
			
			РезультатБронирования = БронированиеПомещенийКлиентСервер.РезультатБронирования();
			РезультатБронирования.ОтмененныеБрони.Добавить(Бронь);
	
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат УстановленаПометкаУдаления;
	
КонецФункции

// Устанавливает пометки удаления броней и возвращает факт изменения пометки удаления.
//
// Параметры:
//  Брони				 - Массив	 - Помечаемые на удаление брони.
//  ПовторяющиесяБрони	 - Массив	 - Помечаемые на удаление повторяющиеся брони.
//  ПометкаУдаления		 - Булево	 - Новая пометка удаления.
// 
// Возвращаемое значение:
//  Булево - Признак того что новая пометка удаления была установлена.
//
Функция УстановитьПометкиУдаления(Брони, ПовторяющиесяБрони, ПометкаУдаления) Экспорт
	
	УстановленаПометкаУдаления = Ложь;
	МассоваяОперация = (Брони.Количество() + ПовторяющиесяБрони.Количество()) > 1;
	РезультатБронирования = БронированиеПомещенийКлиентСервер.РезультатБронирования();
	
	НачатьТранзакцию();
	Попытка
		
		Для Каждого Бронь Из Брони Цикл
			
			Если ТипЗнч(Бронь) <> Тип("ДокументСсылка.Бронь") Тогда
				Продолжить;
			КонецЕсли;
			
			БроньОбъект = Бронь.ПолучитьОбъект();
			БроньОбъект.Заблокировать();
			Если БроньОбъект.ПометкаУдаления <> ПометкаУдаления Тогда
				
				БроньОбъект.ПометкаУдаления = ПометкаУдаления;
				БроньОбъект.ДополнительныеСвойства.Вставить("МассоваяОперация", МассоваяОперация);
				БроньОбъект.Записать();
				
				ДобавитьВИсториюРаботыПользователя(БроньОбъект);
				
				УстановленаПометкаУдаления = Истина;
				
				Если ПометкаУдаления Тогда
					РезультатБронирования.ОтмененныеБрони.Добавить(БроньОбъект.Ссылка);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ПометкаУдаления и ПовторяющиесяБрони.Количество() > 0 Тогда
			
			Для Каждого ПовторяющаясяБронь Из ПовторяющиесяБрони Цикл
				
				БроньОбъект = ПовторяющаясяБронь.Бронь.ПолучитьОбъект();
				БроньОбъект.Заблокировать();
				БроньОбъект.ДобавитьИсключениеПовторения(ПовторяющаясяБронь.ДатаИсключения);
				БроньОбъект.ДополнительныеСвойства.Вставить("МассоваяОперация", МассоваяОперация);
				БроньОбъект.Записать();
				
				ДобавитьВИсториюРаботыПользователя(БроньОбъект);
				
				УстановленаПометкаУдаления = Истина;
				
				Если ПометкаУдаления Тогда
					РезультатБронирования.ОтмененныеБрони.Добавить(БроньОбъект.Ссылка);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ВызватьИсключение;
		
	КонецПопытки;
	

	Возврат УстановленаПометкаУдаления;
	
КонецФункции

// Создает бронь.
//
// Параметры:
//  Бронь  - Структура - Данные брони.
// 
// Возвращаемое значение:
//  Структура - Общий результат броней, которые дополняется:
//   * РезультатыБроней                - Массив Из Структура            - Результаты каждой брони:
//      ** Бронь                - ДокументСсылка.Бронь - Бронь.
//      ** ОжидаетПодтверждения - Булево               - Бронь ожидает подтверждения.
//   * ОтмененныеБрони                 - Массив из ДокументСсылка.Бронь - Брони, для которых была выполнена отмена.
//   * ДлительнаяОперацияПодтверждения - Структура                      - Длительная операция оперативного подтверждения брони.
//                                       Неопределено                   - Длительная операция не запущена.
//
Функция СоздатьБронь(Бронь) Экспорт
	
	БроньОбъект = Документы.Бронь.СоздатьДокумент();
	БроньОбъект.Заполнить(Бронь);
	БроньОбъект.Записать();
	
	ДобавитьВИсториюРаботыПользователя(БроньОбъект);
	
	РезультатБронирования = БронированиеПомещенийКлиентСервер.РезультатБронирования();
	РезультатБронирования.РезультатыБроней.Добавить(БроньОбъект.РезультатБрони());

	Возврат РезультатБронирования;
	
КонецФункции

// Изменяет брони.
//
// Параметры:
//  ИзмененныеБрони - Массив из Структура - Измененные структуры броней.
// 
// Возвращаемое значение:
//  Структура - Общий результат броней:
//   * РезультатыБроней                - Массив Из Структура            - Результаты каждой брони:
//      ** Бронь                - ДокументСсылка.Бронь - Бронь.
//      ** ОжидаетПодтверждения - Булево               - Бронь ожидает подтверждения.
//   * ОтмененныеБрони                 - Массив из ДокументСсылка.Бронь - Брони, для которых была выполнена отмена.
//   * ДлительнаяОперацияПодтверждения - Структура                      - Длительная операция оперативного подтверждения брони.
//                                       Неопределено                   - Длительная операция не запущена.
//
Функция ИзменитьБрони(ИзмененныеБрони) Экспорт
	
	РезультатБронирования = БронированиеПомещенийКлиентСервер.РезультатБронирования();
	
	НачатьТранзакцию();
	Попытка
		
		Для Каждого Бронь Из ИзмененныеБрони Цикл
			
			Если Бронь.ТипЗаписи = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие Тогда
				БроньОбъект = Документы.Бронь.СоздатьДокумент();
				БроньОбъект.ДополнительныеСвойства.Вставить("ПовторяющаясяБронь", Бронь.Ссылка);
				БроньОбъект.ДополнительныеСвойства.Вставить("ДатаИсключения", Бронь.ДатаНачалаИсходная);
				БроньОбъект.Заполнить(Бронь.Ссылка);
			Иначе
				БроньОбъект = Бронь.Ссылка.ПолучитьОбъект();
				БроньОбъект.Заблокировать();
			КонецЕсли;
			
			БроньОбъект.Помещение = Бронь.Помещение;
			БроньОбъект.ДатаНачала = РаботаСЧасовымиПоясами.ПривестиКВремениСеанса(Бронь.ДатаНачала);
			БроньОбъект.ДатаОкончания = РаботаСЧасовымиПоясами.ПривестиКВремениСеанса(Бронь.ДатаОкончания);
			БроньОбъект.СпособСозданияБрони = Перечисления.СпособыСозданияБрони.Вручную;
			БроньОбъект.Записать();
			
			ДобавитьВИсториюРаботыПользователя(БроньОбъект);
			РезультатБронирования.РезультатыБроней.Добавить(БроньОбъект.РезультатБрони());
			
			ПротоколированиеРаботыСотрудников.ЗаписатьИзменение(БроньОбъект.Ссылка);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат РезультатБронирования;
	
КонецФункции

// Сформировать данные выбора помещения.
//
// Параметры:
//  ПараметрыПолученияДанных - Структура - Параметры получения данных.
//
// Возвращаемое значение:
//  СписокЗначений - Данные выбора помещения.
//
Функция СформироватьДанныеВыбораПомещения(ПараметрыПолученияДанных) Экспорт
	
	ПараметрыПолученияДанных.Отбор.Вставить("ДляБронирования", Истина);
	ДанныеВыбора = Справочники.ТерриторииИПомещения.ПолучитьДанныеВыбора(ПараметрыПолученияДанных);
	
	Возврат ДанныеВыбора;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет ссылку в историю работы пользователя.
//
Процедура ДобавитьВИсториюРаботыПользователя(Бронь)
	
	ТипПараметра = ТипЗнч(Бронь);
	Если ТипПараметра = Тип("ДокументОбъект.Бронь") Тогда
		НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(Бронь.Ссылка);
	ИначеЕсли ТипПараметра = Тип("ДокументСсылка.Бронь") Тогда
		НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(Бронь);
	КонецЕсли;
	ИсторияРаботыПользователя.Добавить(НавигационнаяСсылка);
	
КонецПроцедуры

#КонецОбласти
