//  Содержит код, реализующий алгоритм выполнения сценариев.

#Область ПрограммныйИнтерфейс

// Разбирает строку запуска клиента, считывает сценарии и запускает их выполнение.
//
Процедура ЗапуститьВыполнениеСценариев(ПараметрЗапуска) Экспорт
	
	#Если ВебКлиент Или МобильныйКлиент Тогда
		Возврат; // В веб клиенте и мобильном не передать из командной строки файл со сценарием.
	#КонецЕсли
	
	Если ПустаяСтрока(ПараметрЗапуска) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыМассив = СтрРазделить(ВРег(ПараметрЗапуска), ";");
	Если ПараметрыМассив.Найти("RUNHLT") = Неопределено Тогда
		Возврат; // Остальные переменные не инициализируем.
	КонецЕсли;
	
	// Найден признак запуска нагрузочного теста, инициализируем и запускаем тестовые сценарии:
	
	#Область ИнициализацияПараметров
	Если ПараметрыПриложения = Неопределено Тогда
		ПараметрыПриложения = Новый Соответствие(); // Перестраховка, они уже есть.
	КонецЕсли;
	
	// Сценарии пользователя для проведения нагрузочного теста.
	// Тип - Массив Из Строка:
	ПараметрыПриложения[ТекущиеСценарии()] = Новый Массив();
	
	// Порядковый номер выполняемого сценария.
	// Тип - Число:
	ПараметрыПриложения[ИндексТекущегоСценария()] = 0;
	
	// Выполняемый сценарий.
	// Тип - Массив из См. НагрузочноеТестирование.НовыйШагСценария:
	ПараметрыПриложения[ТекущийСценарий()] = Новый Массив();
	
	// Порядковый номер выполняемого шага текущего сценария.
	// Тип - Число:
	ПараметрыПриложения[ИндексТекущегоШагаСценария()] = 0;
	
	НагрузочноеТестированиеВызовСервера.УстановитьПризнакНагрузочногоТестирования(Истина);
	#КонецОбласти // ИнициализацияПараметров
	
	// Чтение файла с последовательностью сценариев.
	#Если ВебКлиент Тогда
		// Сюда не придет, т.к. Возврат в начале.
	#Иначе
		Файл = Новый ЧтениеТекста(ПараметрыМассив[1]);
		ИмяСценария = Файл.ПрочитатьСтроку();
		Пока ИмяСценария <> Неопределено Цикл
			ПараметрыПриложения[ТекущиеСценарии()].Добавить(ИмяСценария);
			ИмяСценария = Файл.ПрочитатьСтроку();
		КонецЦикла;
	#КонецЕсли
	ЗапуститьВыполнениеТекущегоСценария();
	
КонецПроцедуры

// Признак, что текущий сеанс - это нагрузочное тестирование. Вызывается для обхода
// проверок, мешающих тестированию, чтобы подавить выдачу блокирующего сообщения в некоторых нестандартных случаях.
// Чаще всего, для подавления "ПоказатьВопрос" или "ПоказатьПредупреждение", эти окна нельзя отследить сценарием
// теста и программно закрыть.
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоНагрузочноеТестирование() Экспорт
	
	ТекущийСценарий = ПараметрыПриложения[ТекущийСценарий()];
	
	Возврат ТипЗнч(ТекущийСценарий) = Тип("Массив")
		И ТекущийСценарий.Количество() > 0
		И ПараметрыПриложения[ИндексТекущегоСценария()] <= ПараметрыПриложения[ТекущиеСценарии()].ВГраница();
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет переход к следующему шагу сценария.
// 
// Возвращаемое значение:
//  Булево
//
Функция ПерейтиКСледующемуШагуСценария() Экспорт
	
	ПараметрыПриложения[ИндексТекущегоШагаСценария()] = ПараметрыПриложения[ИндексТекущегоШагаСценария()] + 1;
	Возврат ВыполнитьТекущийШагСценария();
	
КонецФункции

// Случайное число сразу, обертка для быстрого получения случайных чисел.
// 
// Параметры:
//  От - Число - Нижняя граница от 0 до 2^32-1
//  До - Число - Верхняя граница от 0 до 2^32-1
// 
// Возвращаемое значение:
//  Число - Случайное число в заданном диапазоне
Функция СлучайноеЧислоСразу(От, До) Экспорт //@skip-check bsl-variable-name-invalid
	
	Если От = До Тогда
		Возврат От;
	КонецЕсли;
	
	#Если ВебКлиент Тогда
		Разность = До - От;
		Возврат От + ТекущаяУниверсальнаяДатаВМиллисекундах() % (Разность + 1); 
	#Иначе
		ГСЧ = Новый ГенераторСлучайныхЧисел(ТекущаяУниверсальнаяДатаВМиллисекундах());
		Возврат ГСЧ.СлучайноеЧисло(От, До);
	#КонецЕсли
	
КонецФункции

// Возвращает текущий шаг сценария.
// 
// Возвращаемое значение:
//  См. НагрузочноеТестирование.НовыйШагСценария
Функция ТекущийШагСценария() Экспорт
	
	ТекущийСценарий = ПараметрыПриложения[ТекущийСценарий()];
	ИндексТекущегоШагаСценария = ПараметрыПриложения[ИндексТекущегоШагаСценария()];
	Если ИндексТекущегоШагаСценария > ТекущийСценарий.ВГраница() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ТекущийСценарий[ИндексТекущегоШагаСценария];
	
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

#Область ПолучениеИзГлобальнойПеременной

Функция ТекущиеСценарии()
	
	Возврат "НагрузочноеТестирование.ТекущиеСценарии";
	
КонецФункции

Функция ИндексТекущегоСценария()
	
	Возврат "НагрузочноеТестирование.ИндексТекущегоСценария";
	
КонецФункции

Функция ТекущийСценарий()
	
	Возврат "НагрузочноеТестирование.ТекущийСценарий";
	
КонецФункции

Функция ИндексТекущегоШагаСценария()
	
	Возврат "НагрузочноеТестирование.ИндексТекущегоШагаСценария";
	
КонецФункции

#КонецОбласти // ПолучениеИзГлобальнойПеременной

// Выполняет текущий шаг сценария. Если шагов в сценарии нет, то запускает выполнение
// следующего сценария.
// 
// Возвращаемое значение:
//  Булево - Результат выполнения шага.
Функция ВыполнитьТекущийШагСценария()
	
	ТекущийШагСценария = ТекущийШагСценария();
	Если ТекущийШагСценария = Неопределено Тогда
		// Сценарий кончился, переходим к следующему в файле
		ПараметрыПриложения[ИндексТекущегоСценария()] = ПараметрыПриложения[ИндексТекущегоСценария()] + 1;
		ЗапуститьВыполнениеТекущегоСценария();
		Возврат Истина;
	КонецЕсли;
	
	ШагВыполнен = Вычислить("НагрузочноеТестированиеДействияКлиент.Действие_" + ТекущийШагСценария.Действие + "()");
	
	Если ШагВыполнен Тогда
		ПодключитьОбработчикОжидания("Обработчик_ПерейтиКСледующемуШагуСценария", ТекущийШагСценария.Пауза, Истина);
	Иначе
		НагрузочноеТестированиеВызовСервера.УстановитьПризнакНагрузочногоТестирования(Ложь);
		ВызватьИсключение НСтр("ru = 'Во время тестирования произошла ошибка, выполнение теста прервано. 
		|См. служебные сообщения. Возможно требуется сделать некоторые настройки в информационной базе.'");
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Запускает выполнение текущего сценария.
Процедура ЗапуститьВыполнениеТекущегоСценария()
	
	ТекущиеСценарии = ПараметрыПриложения[ТекущиеСценарии()];
	ИндексТекущегоСценария = ПараметрыПриложения[ИндексТекущегоСценария()];
	Если ИндексТекущегоСценария > ТекущиеСценарии.ВГраница() Тогда
		// Все сценарии кончились - завершение тестирования:
		НагрузочноеТестированиеВызовСервера.ЗаписьВЖурналОРезультате(Ложь);
		НагрузочноеТестированиеВызовСервера.УстановитьПризнакНагрузочногоТестирования(Ложь);
		Возврат;
	КонецЕсли;
	
	ИмяСценария = ТекущиеСценарии[ИндексТекущегоСценария]; // Строка
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Новый сценарий'"),
		Неопределено,
		СтрШаблон(НСтр("ru = 'Номер %1 из %2 ""%3""'"), 
			ИндексТекущегоСценария + 1, ТекущиеСценарии.Количество(), ИмяСценария),
		БиблиотекаКартинок.Информация);
	
	Сценарий = Вычислить("НагрузочноеТестированиеСценарииВызовСервера." + ИмяСценария + "()");
	
	Если Сценарий = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПриложения[ТекущийСценарий()] = Сценарий;
	ПараметрыПриложения[ИндексТекущегоШагаСценария()] = 0;
	ВыполнитьТекущийШагСценария();
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
