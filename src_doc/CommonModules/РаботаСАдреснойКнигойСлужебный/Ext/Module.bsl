
#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработкаОчередиОбновленияАдреснойКниги

// Обработчик регламентного задания.
//
Процедура ОбновлениеАдреснойКниги() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ОбновлениеАдреснойКниги);
	
	ЗапуститьПотокиОбновленияАдреснойКниги();
	
КонецПроцедуры

// Возвращает событие журнала регистрации.
//
// Возвращаемое значение:
//	Строка - Событие журнала регистрации.
//
Функция СобытиеЖурналаРегистрации() Экспорт
	
	Возврат
		НСтр("ru = 'Обновление адресной книги (очередь)'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

// Сбрасывает попытки обработки.
//
Процедура СброситьПопытки() Экспорт
	
	НомерЦикла = 0;
	ЕстьДанныеКОбработке = Истина;
	Пока ЕстьДанныеКОбработке Цикл
		
		ЕстьДанныеКОбработке = Ложь;
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1000
			|	ОчередьОбновленияАдреснойКниги.Объект КАК Объект,
			|	ОчередьОбновленияАдреснойКниги.Операция КАК Операция,
			|	ОчередьОбновленияАдреснойКниги.РодительОбъекта КАК РодительОбъекта,
			|	ОчередьОбновленияАдреснойКниги.Раздел КАК Раздел,
			|	ОчередьОбновленияАдреснойКниги.ОбъектДоступа КАК ОбъектДоступа
			|ИЗ
			|	РегистрСведений.ОчередьОбновленияАдреснойКниги КАК ОчередьОбновленияАдреснойКниги
			|ГДЕ
			|	ОчередьОбновленияАдреснойКниги.Попыток >= 3
			|
			|	УПОРЯДОЧИТЬ ПО
			|		ОтметкаВремени");
		
		//@skip-check query-in-loop
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ЕстьДанныеКОбработке = Истина;
			
			РегистрыСведений.ОчередьОбновленияАдреснойКниги.СброситьПопытки(
				Выборка.Объект,
				Выборка.Операция,
				Выборка.РодительОбъекта,
				Выборка.ОбъектДоступа,
				Выборка.Раздел);
			
		КонецЦикла;
		
		НомерЦикла = НомерЦикла + 1;
		Если НомерЦикла > 10000 Тогда
			ВызватьИсключение НСтр("ru = 'Обнаружено зацикливание'");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Обновляет данные адресной книги.
//
// Возвращаемое значение:
//	Булево - Есть обработанные данные.
//
Функция ОбработатьОчередьОбновленияАдреснойКниги() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЕстьОбработанныеДанные = Ложь;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 500
		|	ВложенныйЗапрос.ПриоритетОперации КАК ПриоритетОперации,
		|	ВложенныйЗапрос.Объект КАК Объект,
		|	ВложенныйЗапрос.Операция КАК Операция,
		|	ВложенныйЗапрос.РодительОбъекта КАК РодительОбъекта,
		|	ВложенныйЗапрос.Раздел КАК Раздел,
		|	ВложенныйЗапрос.ОбъектДоступа КАК ОбъектДоступа,
		|	ВложенныйЗапрос.ПараметрыОбновления КАК ПараметрыОбновления,
		|	ВложенныйЗапрос.ОтметкаВремени КАК ОтметкаВремени
		|ИЗ
		|	(ВЫБРАТЬ ПЕРВЫЕ 500
		|		0 КАК ПриоритетВыполнения,
		|		0 КАК ПриоритетОперации,
		|		ЗаданияУдалитьДанныеОбъекта.Объект КАК Объект,
		|		ЗаданияУдалитьДанныеОбъекта.Операция КАК Операция,
		|		ЗаданияУдалитьДанныеОбъекта.РодительОбъекта КАК РодительОбъекта,
		|		ЗаданияУдалитьДанныеОбъекта.Раздел КАК Раздел,
		|		ЗаданияУдалитьДанныеОбъекта.ОбъектДоступа КАК ОбъектДоступа,
		|		ЗаданияУдалитьДанныеОбъекта.ПараметрыОбновления КАК ПараметрыОбновления,
		|		ЗаданияУдалитьДанныеОбъекта.ОтметкаВремени КАК ОтметкаВремени
		|	ИЗ
		|		РегистрСведений.ОчередьОбновленияАдреснойКниги КАК ЗаданияУдалитьДанныеОбъекта
		|	ГДЕ
		|		ЗаданияУдалитьДанныеОбъекта.Попыток < &КоличествоПопытокОбработки
		|		И ЗаданияУдалитьДанныеОбъекта.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.УдалитьДанныеОбъекта)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ ПЕРВЫЕ 500
		|		1,
		|		ВЫБОР
		|			КОГДА ПрочиеЗаданияОбновленияАдреснойКниги.Операция В
		|				(ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.ОбновитьДанныеОбъекта))
		|				ТОГДА 1
		|			КОГДА ПрочиеЗаданияОбновленияАдреснойКниги.Операция В
		|				(ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.ОбновитьДанныеОтображения))
		|				ТОГДА 2
		|			КОГДА ПрочиеЗаданияОбновленияАдреснойКниги.Операция В
		|				(ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.ОбновитьСписокПодчиненныхОбъектов))
		|				ТОГДА 3
		|			ИНАЧЕ 999
		|		КОНЕЦ,
		|		ПрочиеЗаданияОбновленияАдреснойКниги.Объект,
		|		ПрочиеЗаданияОбновленияАдреснойКниги.Операция,
		|		ПрочиеЗаданияОбновленияАдреснойКниги.РодительОбъекта,
		|		ПрочиеЗаданияОбновленияАдреснойКниги.Раздел,
		|		ПрочиеЗаданияОбновленияАдреснойКниги.ОбъектДоступа,
		|		ПрочиеЗаданияОбновленияАдреснойКниги.ПараметрыОбновления,
		|		ПрочиеЗаданияОбновленияАдреснойКниги.ОтметкаВремени
		|	ИЗ
		|		РегистрСведений.ОчередьОбновленияАдреснойКниги КАК ПрочиеЗаданияОбновленияАдреснойКниги
		|	ГДЕ
		|		ПрочиеЗаданияОбновленияАдреснойКниги.Попыток < &КоличествоПопытокОбработки
		|		И ПрочиеЗаданияОбновленияАдреснойКниги.Операция <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.УдалитьДанныеОбъекта)
		|		И 0 В
		|			(ВЫБРАТЬ
		|				ЕСТЬNULL(КОЛИЧЕСТВО(Очередь.Объект), 0)
		|			ИЗ
		|				РегистрСведений.ОчередьОбновленияАдреснойКниги КАК Очередь
		|			ГДЕ
		|				Очередь.Объект = ПрочиеЗаданияОбновленияАдреснойКниги.РодительОбъекта
		|				И ПрочиеЗаданияОбновленияАдреснойКниги.Операция В
		|				(ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.ОбновитьДанныеОбъекта),
		|					ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.ОбновитьСписокПодчиненныхОбъектов))
		|				И Очередь.Операция В
		|				(ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.ОбновитьДанныеОбъекта),
		|					ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.ОбновитьСписокПодчиненныхОбъектов)))
		|		И 0 В
		|			(ВЫБРАТЬ
		|				ЕСТЬNULL(КОЛИЧЕСТВО(Очередь.Объект), 0)
		|			ИЗ
		|				РегистрСведений.ОчередьОбновленияАдреснойКниги КАК Очередь
		|			ГДЕ
		|				Очередь.Объект = ПрочиеЗаданияОбновленияАдреснойКниги.Объект
		|				И ПрочиеЗаданияОбновленияАдреснойКниги.Операция В
		|					(ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.ОбновитьСписокПодчиненныхОбъектов),
		|						ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.ОбновитьСловаПоиска),
		|						ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.ОбновитьСловаПоискаПоСотруднику),
		|						ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.ОбновитьСловаПоискаПоДолжностиСотрудника),
		|						ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.ОбновитьДоступностьВПоиске),
		|						ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.ОбновитьДанныеОтображения))
		|				И Очередь.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.ОбновитьДанныеОбъекта))
		|		И 0 В
		|			(ВЫБРАТЬ
		|				ЕСТЬNULL(КОЛИЧЕСТВО(Очередь.Объект), 0)
		|			ИЗ
		|				РегистрСведений.ОчередьОбновленияАдреснойКниги КАК Очередь
		|			ГДЕ
		|				Очередь.Объект = ПрочиеЗаданияОбновленияАдреснойКниги.Объект
		|				И Очередь.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.УдалитьДанныеОбъекта))
		|		И 0 В
		|			(ВЫБРАТЬ
		|				ЕСТЬNULL(КОЛИЧЕСТВО(Очередь.Объект), 0)
		|			ИЗ
		|				РегистрСведений.ОчередьОбновленияАдреснойКниги КАК Очередь
		|			ГДЕ
		|				Очередь.Объект = ПрочиеЗаданияОбновленияАдреснойКниги.РодительОбъекта
		|				И ПрочиеЗаданияОбновленияАдреснойКниги.Операция В
		|					(ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.ОбновитьДанныеОбъекта),
		|						ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.ОбновитьСписокПодчиненныхОбъектов))
		|				И Очередь.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОбновленияАдреснойКниги.УдалитьДанныеОбъекта))
		|
		|	УПОРЯДОЧИТЬ ПО
		|		ОтметкаВремени) КАК ВложенныйЗапрос
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВложенныйЗапрос.ПриоритетВыполнения,
		|	ВложенныйЗапрос.ОтметкаВремени,
		|	ВложенныйЗапрос.Объект,
		|	ВложенныйЗапрос.ПриоритетОперации");
	Запрос.УстановитьПараметр("КоличествоПопытокОбработки", КоличествоПопытокОбработки());
	
	НачатьТранзакцию();
	Попытка
		
		БлокировкаДанных = Новый БлокировкаДанных();
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОчередьОбновленияАдреснойКниги");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		БлокировкаДанных.Заблокировать();
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке =
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ИнформацияОбОшибке;
		
	КонецПопытки;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗначениеКлюча = Новый Структура;
			ЗначениеКлюча.Вставить("Объект", Выборка.Объект);
			ЗначениеКлюча.Вставить("Операция", Выборка.Операция);
			ЗначениеКлюча.Вставить("РодительОбъекта", Выборка.РодительОбъекта);
			ЗначениеКлюча.Вставить("Раздел", Выборка.Раздел);
			ЗначениеКлюча.Вставить("ОбъектДоступа", Выборка.ОбъектДоступа);
			КлючБлокировки =
				РегистрыСведений.ОчередьОбновленияАдреснойКниги.СоздатьКлючЗаписи(ЗначениеКлюча);
			ЗаблокироватьДанныеДляРедактирования(КлючБлокировки);
		Исключение
			// Блокировка используется для распределения по потокам и исключения обновления одних и тех же данных.
			Продолжить;
		КонецПопытки;
		
		// Проверим что запись ещё актуальна.
		ЕстьЗаписьСТакимиПолямиКлюча =
			РегистрыСведений.ОчередьОбновленияАдреснойКниги.ЕстьЗаписьСПолямиКлюча(
				Выборка.Объект, Выборка.Операция, Выборка.РодительОбъекта, Выборка.Раздел, Выборка.ОбъектДоступа);
				
		Если ЕстьЗаписьСТакимиПолямиКлюча Тогда
			
			НачатьТранзакцию();
			Попытка
				
				ЕстьОбработанныеДанные = Истина;
				
				Если Выборка.Операция = Перечисления.ВидыОперацийОбновленияАдреснойКниги.ОбновитьДанныеОбъекта Тогда
					
					Справочники.АдреснаяКнига.ОбновитьДанныеОбъекта(
						Выборка.Объект, Выборка.РодительОбъекта, Выборка.Раздел, Выборка.ОбъектДоступа);
				ИначеЕсли Выборка.Операция =
						Перечисления.ВидыОперацийОбновленияАдреснойКниги.ОбновитьСписокПодчиненныхОбъектов Тогда
					
					ПараметрыОбновления = Выборка.ПараметрыОбновления.Получить();
					
					Справочники.АдреснаяКнига.ОбновитьСписокПодчиненныхОбъектов(
						Выборка.Объект,
						Выборка.РодительОбъекта,
						ПараметрыОбновления.СписокОбъектов,
						Выборка.Раздел,
						Выборка.ОбъектДоступа,
						ПараметрыОбновления.ОбновитьТолькоСоставПодчиненных);
				ИначеЕсли Выборка.Операция = Перечисления.ВидыОперацийОбновленияАдреснойКниги.ОбновитьСловаПоиска Тогда
					
					ПараметрыОбновления = Выборка.ПараметрыОбновления.Получить();
					
					РегистрыСведений.ОбъектыПоискаВАдреснойКниге.ОбновитьСловаПоиска(
						Выборка.Объект, ПараметрыОбновления);
				ИначеЕсли Выборка.Операция =
						Перечисления.ВидыОперацийОбновленияАдреснойКниги.ОбновитьСловаПоискаПоСотруднику Тогда
					
					ПараметрыОбновления = Выборка.ПараметрыОбновления.Получить();
					
					РегистрыСведений.ОбъектыПоискаВАдреснойКниге.ОбновитьСловаПоискаПоСотруднику(ПараметрыОбновления);
				ИначеЕсли Выборка.Операция = 
						Перечисления.ВидыОперацийОбновленияАдреснойКниги.ОбновитьСловаПоискаПоПодразделениюСотрудника Тогда
					
					ПараметрыОбновления = Выборка.ПараметрыОбновления.Получить();
					
					РегистрыСведений.ОбъектыПоискаВАдреснойКниге.ОбновитьСловаПоискаПоПодразделениюСотрудника(
						Выборка.Объект, ПараметрыОбновления);
				ИначеЕсли Выборка.Операция =
						Перечисления.ВидыОперацийОбновленияАдреснойКниги.ОбновитьСловаПоискаПоДолжностиСотрудника Тогда
					
					ПараметрыОбновления = Выборка.ПараметрыОбновления.Получить();
					
					РегистрыСведений.ОбъектыПоискаВАдреснойКниге.ОбновитьСловаПоискаПоДолжностиСотрудника(
						ПараметрыОбновления);
				ИначеЕсли Выборка.Операция =
						Перечисления.ВидыОперацийОбновленияАдреснойКниги.ОбновитьДоступностьВПоиске Тогда
					
					ПараметрыОбновления = Выборка.ПараметрыОбновления.Получить();
					
					РегистрыСведений.ОбъектыПоискаВАдреснойКниге.ОбновитьДоступностьВПоиске(
						Выборка.Объект, ПараметрыОбновления);
				ИначеЕсли Выборка.Операция =
						Перечисления.ВидыОперацийОбновленияАдреснойКниги.ОбновитьДанныеОтображения Тогда
					
					Справочники.АдреснаяКнига.ОбновитьДанныеОтображенияПодчиненногоОбъекта(Выборка.Объект);
				ИначеЕсли Выборка.Операция =
						Перечисления.ВидыОперацийОбновленияАдреснойКниги.УдалитьДанныеОбъекта Тогда
						
					Справочники.АдреснаяКнига.УдалитьОбъектыАдреснойКниги(
						Выборка.Объект, Выборка.РодительОбъекта, Выборка.Раздел);
					РегистрыСведений.ОчередьОбновленияАдреснойКниги.УдалитьНеактуальныеЗаписиИзОчереди(
						Выборка.Объект, Выборка.ОтметкаВремени);
				КонецЕсли;
					
				РегистрыСведений.ОчередьОбновленияАдреснойКниги.ЗаписатьУспешнуюПопытку(
					Выборка.Объект, Выборка.Операция, Выборка.РодительОбъекта, Выборка.ОбъектДоступа, Выборка.Раздел);
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				
				ОтменитьТранзакцию();
				
				РегистрыСведений.ОчередьОбновленияАдреснойКниги.ЗаписатьНеуспешнуюПопытку(
					Выборка.Объект,
					Выборка.Операция,
					Выборка.РодительОбъекта,
					Выборка.ОбъектДоступа,
					Выборка.Раздел,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			КонецПопытки;
			
		КонецЕсли;
		
		РазблокироватьДанныеДляРедактирования(КлючБлокировки);
		
	КонецЦикла;
	
	Возврат ЕстьОбработанныеДанные;
	
КонецФункции

// Обрабатывает очередь обновления адресной книги в потоке.
//
Процедура ОбработатьОчередьОбновленияАдреснойКнигиВПотоке() Экспорт
	
	ЕстьОбработанныеДанные = Истина;
	Пока ЕстьОбработанныеДанные Цикл
		//@skip-check query-in-loop
		ЕстьОбработанныеДанные = ОбработатьОчередьОбновленияАдреснойКниги();
	КонецЦикла;
	
КонецПроцедуры

// Возвращает количество попыток обработки задания обновления адресной книги.
//
// Возвращаемое значение:
//	Число - Количество попыток обработки.
//
Функция КоличествоПопытокОбработки() Экспорт
	
	Возврат 3;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обновляет информацию об активных потоках обновления.
//
// Возвращаемое значение:
//	Число - Количество активных потоков.
//
Функция КоличествоАктивныхПотоков()
	
	АктивныеЗаданияОбновления =
		РегистрыСведений.АктивныеЗаданияОбновленияАдреснойКниги.АктивныеЗаданияОбновления();
	
	КоличествоЭлементов = АктивныеЗаданияОбновления.Количество();
	Для Индекс = 1 По КоличествоЭлементов Цикл
		ОбратныйИндекс = КоличествоЭлементов - Индекс;
		ИдентификаторЗадания = АктивныеЗаданияОбновления[ОбратныйИндекс];
		
		АктивноеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
		Если АктивноеЗадание <> Неопределено
				И АктивноеЗадание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
			Продолжить;
		КонецЕсли;
		
		АктивныеЗаданияОбновления.Удалить(ОбратныйИндекс);
		
		РегистрыСведений.АктивныеЗаданияОбновленияАдреснойКниги.Удалить(ИдентификаторЗадания);
	КонецЦикла;
	
	КоличествоАктивныхПотоков = АктивныеЗаданияОбновления.Количество();
	
	Возврат КоличествоАктивныхПотоков;
	
КонецФункции

// Выполняет запуск потоков обновления адресной книги.
//
Процедура ЗапуститьПотокиОбновленияАдреснойКниги()
	
	КоличествоПотоков = КоличествоПотоков();
	ИнформационнаяБазаФайловая = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	
	Если КоличествоПотоков < 2 Или ИнформационнаяБазаФайловая Тогда
		ОбработатьОчередьОбновленияАдреснойКнигиВПотоке();
		Возврат;
	КонецЕсли;
	
	КоличествоАктивныхПотоков = КоличествоАктивныхПотоков();
	Пока КоличествоАктивныхПотоков < КоличествоПотоков Цикл
		
		ФоновоеЗадание = ФоновыеЗадания.Выполнить(
			"РаботаСАдреснойКнигойСлужебный.ОбработатьОчередьОбновленияАдреснойКнигиВПотоке");
		
		РегистрыСведений.АктивныеЗаданияОбновленияАдреснойКниги.Добавить(
			ФоновоеЗадание.УникальныйИдентификатор);
		
		КоличествоАктивныхПотоков = КоличествоАктивныхПотоков + 1;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает количество потоков обновления адресной книги.
//
// Возвращаемое значение:
//	Число
//
Функция КоличествоПотоков()
	
	Возврат Константы.ПараметрыОбновленияАдреснойКниги.ЧислоПотоковОбновленияАдреснойКниги();
	
КонецФункции

#КонецОбласти
