////////////////////////////////////////////////////////////////////////////////
// Серверные процедуры и функции по работе со схемами процессов, для вызова с клиента.
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает доступность настройки скриптов в схемах процессов.
// 
// Возвращаемое значение:
// 	Булево
//
Функция ДоступнаНастройкаСкриптовВСхемахПроцессов() Экспорт
	
	Возврат СхемыПроцессовСервер.ДоступнаНастройкаСкриптовВСхемахПроцессов();
	
КонецФункции

// Проверяет, что пути схемы были изменены при редактировании формы
// 
// Параметры:
//  НачальныеПредшественники - см. СхемыПроцессовСервер.ПредшественникиЭлементовПоДаннымСхемы
//  ДанныеСхемы - см. СхемыПроцессовКлиентСервер.ДанныеСхемыПроцессаИзФормы
//  
// Возвращаемое значение:
//  Булево
//
Функция ПутиСхемыИзменены(Знач НачальныеПредшественники, Знач ДанныеСхемы) Экспорт
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат СхемыПроцессовСервер.ПутиСхемыИзменены(НачальныеПредшественники, ДанныеСхемы);
	
КонецФункции

#Область ФоноваяПроверкаСхемы

// Параметры фоновой проверки схемы.
// 
// Параметры:
//  ИдентификаторФормы - УникальныйИдентификатор
//  АдресХранилищаСхемы - Строка - Адрес временного хранилища графической схемы
// 
// Возвращаемое значение:
//  см. СхемыПроцессовСервер.ЗапускФоновойПроверкиСхемы
//
Функция ЗапускФоновойПроверкиСхемы(ИдентификаторФормы, АдресХранилищаСхемы) Экспорт
	
	Схема = ПолучитьИзВременногоХранилища(АдресХранилищаСхемы);
	Возврат СхемыПроцессовСервер.ЗапускФоновойПроверкиСхемы(ИдентификаторФормы, Схема);

КонецФункции

#КонецОбласти

#Область ФоновыйРасчетКэширующихДанныхСхемы
// Параметры фонового обновления кэширующих данных схемы(выполнения длительной операции).
// 
// Параметры:
//  ИдентификаторФормы - УникальныйИдентификатор
//  ДанныеСхемы - см. СхемыПроцессовКлиентСервер.СтруктураДанныхСхемыПроцесса
//  ДанныеПараметровСхемы 	- Неопределено
//  						- см. РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураДанныхПараметровСхемы
//  ОжидатьЗавершения - Булево
// 
// Возвращаемое значение:
//  см. СхемыПроцессовСервер.ЗапускФоновогоРасчетаКэширующихДанныхСхемы
//
Функция ЗапускФоновогоРасчетаКэширующихДанныхСхемы(ИдентификаторФормы, ДанныеСхемы, ДанныеПараметровСхемы,
		ОжидатьЗавершения = Истина) Экспорт
		
		Если ЭтоАдресВременногоХранилища(ДанныеСхемы.Схема) Тогда
			ДанныеСхемы.Схема = ПолучитьИзВременногоХранилища(ДанныеСхемы.Схема);
		КонецЕсли;
		
		Возврат СхемыПроцессовСервер.ЗапускФоновогоРасчетаКэширующихДанныхСхемы(ИдентификаторФормы, ДанныеСхемы,
			ДанныеПараметровСхемы, ОжидатьЗавершения);
		
КонецФункции

// Завершить расчет кэширующих данных схемы.
// 
// Параметры:
//  ПараметрыЗаписи - Структура:
//   * ДанныеСхемы - см. СхемыПроцессовКлиентСервер.СтруктураДанныхСхемыПроцесса
//   * ДанныеПараметровСхемы - см. РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураДанныхПараметровСхемы
//
Процедура ЗавершитьРасчетКэширующихДанныхСхемы(ПараметрыЗаписи) Экспорт
	
	ДанныеСхемы = ПараметрыЗаписи.ДанныеСхемы;
	Если ЭтоАдресВременногоХранилища(ДанныеСхемы.АдресХранилищаКэширующихДанных) Тогда

		Кэш = ПолучитьИзВременногоХранилища(ДанныеСхемы.АдресХранилищаКэширующихДанных); // см. СхемыПроцессовСервер.РезультатРасчетаКэширующихДанныхСхемы

		Если ДанныеСхемы.ПутиИзменены Тогда
			ДанныеПараметровСхемы = ПараметрыЗаписи.ДанныеПараметровСхемы;
			ДанныеПараметровСхемы.ПорядокСортировкиНастроекЭлементов = Кэш.ПорядокСортировкиНастроекЭлементов;
			РезультатЗаписи = Справочники.ПараметрыСхемДляКомплексныхПроцессов.ЗаписатьДанныеПараметровСхемы(
			ДанныеПараметровСхемы.Ссылка, ДанныеПараметровСхемы, Ложь);
			ДанныеПараметровСхемы.ВерсияДанных = РезультатЗаписи.ВерсияДанных;
		КонецЕсли;

		Если Кэш.Записывать Тогда
			ЗаполнитьЗначенияСвойств(ДанныеСхемы, Кэш);
			СхемыПроцессовСервер.ЗаписатьКэшСхемы(ДанныеСхемы, Кэш.ИменаДанныхДляЗаписи, Кэш.ТаблицаПредшественников);
		КонецЕсли;
		
		УдалитьИзВременногоХранилища(ДанныеСхемы.АдресХранилищаКэширующихДанных);
		
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#КонецОбласти