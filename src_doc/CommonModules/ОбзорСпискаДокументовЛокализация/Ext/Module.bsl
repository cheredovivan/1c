
#Область ПрограммныйИнтерфейс

// См. ОбзорСпискаДокументов.ПолучитьДанныеМиникарточки
// 
// Параметры:
//  ТекущийДокумент - СправочникСсылка.ДокументыПредприятия - Текущий документ
//  ДанныеМиникарточки - Структура - Данные миникарточки, которые нужно дополнить
//  ДопПараметры - Структура, Неопределено
Процедура ДополнитьДанныеМиникарточки(ТекущийДокумент, ДанныеМиникарточки, ДопПараметры = Неопределено) Экспорт
	
	// Локализация
	ДанныеМиникарточки.Вставить("СостояниеЭДОСсылка", Неопределено);
	ДанныеМиникарточки.Вставить("ДокументЭДО", Неопределено);
	
	Если ЗначениеЗаполнено(ТекущийДокумент) Тогда
		
		МассивЭД = ОбменЭДОДокументооборот.ЭлектронныеДокументыОбъектаУчета(ТекущийДокумент);
		Если МассивЭД.Количество() <> 0 Тогда
			ДанныеМиникарточки.ДокументЭДО = МассивЭД[0];
		КонецЕсли;
	
	КонецЕсли;
	// Локализация Конец
	
КонецПроцедуры

// Позволяет дополнить данные обзора документа для списка
// 
// Параметры:
//  HTMLТекст - Строка - HTML-страница
//  Выборка - ВыборкаИзРезультатаЗапроса - См. ОбзорСпискаДокументов.ПолучитьДанныеМиникарточки
//  КодЯзыка - Строка, Неопределено - Код языка
Процедура ДополнитьДанныеОбзора(HTMLТекст, Выборка, КодЯзыка = Неопределено) Экспорт

// Локализация
	ДобавитьПоляОбращенияГражданина(HTMLТекст, Выборка, КодЯзыка);
// Локализация Конец

КонецПроцедуры

// Позволяет добавить дополнительные поля в запрос документов для обзора в списке
// 
// Параметры:
//  ЗапросТекст - Строка - Текст запроса, в который нужно добавить поля
Процедура ДополнитьПоляЗапросаДляОбзораСпискаДокументов(ЗапросТекст) Экспорт
	
	// Локализация
	ЗапросТекст = СтрЗаменить(ЗапросТекст, 
		"&ДополнительныеПоля",
		"ЕСТЬNULL(ДокументыПредприятия.ВидДокумента.ЯвляетсяОбращениемОтГраждан, ЛОЖЬ) КАК
		|		ЯвляетсяОбращениемОтГраждан,
		|	ЕСТЬNULL(ДанныеДокументовПредприятия.ВидОбращения, ЗНАЧЕНИЕ(Перечисление.ВидыОбращенийГраждан.ПустаяСсылка))
		|		КАК ВидОбращения,
		|	ЕСТЬNULL(ДанныеДокументовПредприятия.Повторное, ЛОЖЬ) КАК Повторное,
		|	ЕСТЬNULL(ДанныеДокументовПредприятия.Дубликат, ЛОЖЬ) КАК Дубликат");
	// Локализация Конец
	
	ЗапросТекст = СтрЗаменить(ЗапросТекст, ",
		|	&ДополнительныеПоля", "");
	
КонецПроцедуры

#КонецОбласти

// Локализация
#Область СлужебныеПроцедурыИФункции

// Добавить поля обращения гражданина.
// 
// Параметры:
//  HTMLТекст - Строка -  HTMLТекст, куда нужно добавить поля
//  Выборка - ВыборкаИзРезультатаЗапроса -  Выборка, запрос см. ПолучитьЗапросДляДокумента
//  КодЯзыка - Строка
// 
Процедура ДобавитьПоляОбращенияГражданина(HTMLТекст, Выборка, КодЯзыка)
	
	Если Не Выборка.ЯвляетсяОбращениемОтГраждан Тогда
		Возврат;
	КонецЕсли;
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетОбращенийГраждан") Тогда
		Возврат;
	КонецЕсли;
	
	ОбзорСпискаДокументов.ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Вид документа:'", КодЯзыка), Выборка.ВидДокумента);
	ОбзорСпискаДокументов.ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Вид обращения:'", КодЯзыка), Выборка.ВидОбращения);
	ОбзорСпискаДокументов.ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Повторное:'", КодЯзыка), Выборка.Повторное);
	ОбзорСпискаДокументов.ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Дубликат:'", КодЯзыка), Выборка.Дубликат);
	ОбзорСпискаДокументов.ДобавитьРеквизит(
		HTMLТекст, НСтр("ru = 'Адресат(ы):'", КодЯзыка),
		?(ЗначениеЗаполнено(Выборка.АдресатыДляСписков), Выборка.АдресатыДляСписков, НСтр("ru = 'Не указан'", КодЯзыка)));
	
КонецПроцедуры

#КонецОбласти
// Локализация Конец