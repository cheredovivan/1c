#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗагрузитьКартинкуПоАдресу(ЭлементФормы, АдресКартинки) Экспорт
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресКартинки);
	
	Если ДвоичныеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	DataImage = Base64Строка(ДвоичныеДанные);
	DataImage = СтрЗаменить(DataImage, Символы.ВК, "");
	DataImage = СтрЗаменить(DataImage, Символы.ПС, "");
	
	ЭлементКартинкиПоляHTML = ЭлементФормы.Документ.getElementById("image_setter");
	Если ЭлементКартинкиПоляHTML <> Неопределено Тогда
		ЭлементКартинкиПоляHTML.setAttribute("data-img", DataImage);
		ЭлементКартинкиПоляHTML.click();
	КонецЕсли;
	
КонецПроцедуры

Функция ЗначенияСпискаВыбора(Текст, ТипЗначения, ВозможныеВарианты, ЕстьПравоСоздания = Истина) Экспорт
	
	ЭтоПримитивныйТип = РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(ТипЗначения);
	Если ЭтоПримитивныйТип Или Не ЕстьПравоСоздания Тогда
		КартинкаСоздание = 0;
	Иначе
		Если ЭтоПеречисление(ТипЗначения) Тогда
			КартинкаСоздание = 0;
		Иначе
			КартинкаСоздание = 1;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РаспознаваниеДокументовСлужебныйКлиентСервер.ПолучитьСписокДляВыбораПользователем(Текст, ВозможныеВарианты, 
		КартинкаСоздание);
	
КонецФункции

Процедура ПриИзмененииВерифицируемогоРеквизита(Форма, Знач ИмяРеквизита, Знач ЗначениеРеквизита, 
				Знач ИмяТЧ = Неопределено, Знач ИдентификаторСтроки = Неопределено) Экспорт
	
	ДанныеРеквизита = РаспознаваниеДокументовСлужебныйКлиентСервер.
							ДанныеРеквизита(Форма, ИмяРеквизита, ИмяТЧ, ИдентификаторСтроки);
	
	Если ДанныеРеквизита = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеРеквизита.Проверен = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеРеквизита.Проверен = Истина;
	ДанныеРеквизита.УстановленноеЗначение = ЗначениеРеквизита;
	
КонецПроцедуры

Процедура ПослеУдаленияСтрокиТЧ(Форма, ИдентификаторУдаленнойСтрокиТЧ, Элемент) Экспорт
	
	Если Не РаспознаваниеДокументовСлужебныйКлиентСервер.ЗаполненыДанныеВерификации(Форма) Тогда
		Возврат;
	КонецЕсли;
	
	Если ИдентификаторУдаленнойСтрокиТЧ = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ДанныеРеквизитовСтроки = РаспознаваниеДокументовСлужебныйКлиентСервер.
					ДанныеРеквизита(Форма, ,Элемент.Имя, ИдентификаторУдаленнойСтрокиТЧ);
	
	Если ДанныеРеквизитовСтроки = Неопределено Тогда
		Возврат;
	Конецесли;
	
	Для Каждого ДанныеРеквизита Из ДанныеРеквизитовСтроки Цикл
		ДанныеРеквизита.СтрокаУдалена = Истина;
	КонецЦикла;
	
	ИдентификаторУдаленнойСтрокиТЧ = Неопределено;
	
КонецПроцедуры

Процедура ОткрытьСообщениеПриОтправкеФайловНаРаспознавание(Параметры) Экспорт
	
	ОткрытьФорму("ОбщаяФорма.СообщениеПриОтправкеДокументаНаРаспознавание", Параметры);
	
КонецПроцедуры

Функция НеобходимаВерификацияЭлемента(Форма, Элемент) Экспорт
	
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ВерифицируемыеРеквизитыШапки") Тогда
		Возврат Ложь;
	Конецесли;
	
	Если Форма.ВерифицируемыеРеквизитыШапки.Количество() = 0 И Форма.ВерифицируемыеРеквизитыТЧ.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(Элемент) <> Тип("ПолеФормы") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекущийЭлемент = Форма.ТекущийЭлемент;
	
	ЭтоТаблица = ТипЗнч(ТекущийЭлемент) = Тип("ТаблицаФормы");
	
	ИмяТЧ = Неопределено;
	ИдентификаторСтроки = Неопределено;
	
	Если ЭтоТаблица Тогда
		
		ИмяРеквизита = СтрЗаменить(Элемент.Имя, ТекущийЭлемент.Имя, "");
		
		ВерифицируемыеТЧ = РаспознаваниеДокументовСлужебныйКлиентСервер.ВерифицируемыеТабличныеЧасти();
		
		ИмяТЧ = ТекущийЭлемент.Имя;
		
		Если ВерифицируемыеТЧ.Найти(ИмяТЧ) = Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ТекущиеДанные = ТекущийЭлемент.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ИдентификаторСтроки = ТекущиеДанные.Идентификатор;
		
	Иначе
		
		ИмяРеквизита = Элемент.Имя;
	
	КонецЕсли;
	
	Если ИмяРеквизита = "Сумма1" Тогда
		ИмяРеквизита = "Сумма";
	КонецЕсли;
	
	ДанныеРеквизита = 
		РаспознаваниеДокументовСлужебныйКлиентСервер.ДанныеРеквизита(Форма, ИмяРеквизита, ИмяТЧ, ИдентификаторСтроки);
		
	Если ДанныеРеквизита = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ДанныеРеквизита.Проверен = Истина ИЛИ ДанныеРеквизита.ТребуетПроверки = Ложь Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура РеквизитВерифицирован(Элемент, Форма, Знач ПриЗаполненномЗначении) Экспорт

	ИмяТЧ = Неопределено;

	ИдентификаторСтроки = Неопределено;
	
	ЗначениеРеквизита = Неопределено;
	
	Если ТипЗнч(Форма.ТекущийЭлемент) = Тип("ТаблицаФормы") Тогда
		
		ИмяТЧ = Форма.ТекущийЭлемент.Имя;
		
		ТекущиеДанные = Форма.ТекущийЭлемент.ТекущиеДанные;
		
		ИмяРеквизита = СтрЗаменить(Элемент.Имя, Форма.ТекущийЭлемент.Имя, "");
		
		ИдентификаторСтроки = ТекущиеДанные.Идентификатор;
		
		ЗначениеРеквизита = ТекущиеДанные[ИмяРеквизита];
		
		Если ПриЗаполненномЗначении = Ложь ИЛИ ПриЗаполненномЗначении = Истина 
			И ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
			Если ТекущиеДанные.Свойство(ИмяРеквизита + "ТребуетПроверки") Тогда
				ТекущиеДанные[ИмяРеквизита + "ТребуетПроверки"] = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		ИмяРеквизита = Элемент.Имя;
		
		Если Форма.Объект.Свойство(ИмяРеквизита) Тогда
			ЗначениеРеквизита = Форма.Объект[ИмяРеквизита];
		Иначе
			ЗначениеРеквизита = Форма[ИмяРеквизита];
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПриЗаполненномЗначении = Истина И Не ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	ПриИзмененииВерифицируемогоРеквизита(Форма, ИмяРеквизита, ЗначениеРеквизита, ИмяТЧ, ИдентификаторСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭтоПеречисление(Тип)
	
	ОписаниеТипов = РаспознаваниеДокументовСлужебныйКлиентПовтИсп.ПеречисленияТипВсеСсылки();
	Возврат ОписаниеТипов.СодержитТип(Тип);
	
КонецФункции

Функция НаборДанныхСозданияЭлемента(НаборДанных) Экспорт
	
	Возврат РаспознаваниеДокументовСлужебныйКлиентСервер.НаборДанныхСозданияЭлемента(НаборДанных);
	
КонецФункции

#КонецОбласти
