#Область ПрограммныйИнтерфейс

// Заполняет разрешения текущего пользователя в карточке действия.
//
// Параметры:
// 	 Действие - ОпределяемыйТип.Действия - ссылка на действие
//   Разрешения - ТаблицаЗначений - заполняемые разрешения, с колонками:
//     ИдентификаторЭтапа - УникальныйИдентификатор - этапа;
//     Разрешение - ПеречислениеСсылка.РазрезыПравилОбработкиДокументов.
//
Процедура ЗаполнитьРазрешенияПоДействию(Действие, Разрешения) Экспорт
	
	Разрешения.Очистить();
	КлючиИЗначения = РазрешенияСотрудникаПоДействию(Действие);
	Для Каждого КлючИЗначение Из КлючиИЗначения Цикл
		Разрешение = Разрешения.Добавить();
		Разрешение.ИдентификаторЭтапа = КлючИЗначение.Ключ;
		Разрешение.Разрешение = КлючИЗначение.Значение;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает разрешения сотрудника по действию.
//
// Параметры:
//   Действие - ОпределяемыйТип.Действия, ДанныеФормыСтруктура - ссылка на действие
//   КешДанных - см. ОбработкаЗапросовXDTOБизнесПроцессыИЗадачи.ДополнитьДанныеЗадачИСформироватьКешДанных
//
// Возвращаемое значение:
//   Соответствие - разрешения сотрудника по действию
//
Функция РазрешенияСотрудникаПоДействию(Действие, КешДанных = Неопределено) Экспорт
	
	РазрешенияСотрудника = Новый Соответствие;
	
	Если ДействияСервер.РолиПерекрываютНастройкиДоступностиДействий() Тогда
		РазрешенияСотрудника.Вставить(
			ДействияСервер.ПредопределенныйИдентификаторУчастника("Пустой"),
			Перечисления.ВариантыДоступностиИзмененияДействий.Разрешено);
		Возврат РазрешенияСотрудника;
	КонецЕсли;
	
	РеквизитыДействия = Неопределено;
	Если КешДанных <> Неопределено И КешДанных.Свойство("ДанныеДействий") Тогда
		РеквизитыДействия = КешДанных.ДанныеДействий[Действие];
	КонецЕсли;
	
	Если ТипЗнч(Действие) = Тип("ДанныеФормыСтруктура") Тогда
		НастройкаДействия = Действие.НастройкаДействия;
	ИначеЕсли РеквизитыДействия <> Неопределено Тогда
		НастройкаДействия = РеквизитыДействия.НастройкаДействия;
	Иначе
		КэшОбластиПросмотра = КэшиНаВремяВызоваПовтИсп.КэшЗадачаОбластьПросмотра();
		Если КэшОбластиПросмотра.Заполнен И КэшОбластиПросмотра.ДействиеОбработки = Действие Тогда
			НастройкаДействия = КэшОбластиПросмотра.РеквизитыДействияОбработки.НастройкаДействия;
		Иначе
			НастройкаДействия = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Действие, "НастройкаДействия");
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НастройкаДействия) Тогда
		РазрешенияСотрудника.Вставить(
			ДействияСервер.ПредопределенныйИдентификаторУчастника("Пустой"),
			Перечисления.ВариантыДоступностиИзмененияДействий.Разрешено);
		Возврат РазрешенияСотрудника;
	КонецЕсли;
	
	МенеджерНастройкиДействия = ОбщегоНазначения.МенеджерОбъектаПоСсылке(НастройкаДействия);
	РазрешенияСотрудника = МенеджерНастройкиДействия.РазрешенияИзмененияУчастников(
		НастройкаДействия);
	
	Возврат РазрешенияСотрудника;
	
КонецФункции

#КонецОбласти