
#Область ПрограммныйИнтерфейс

// Дополняет данные строки дерева приложений задачи
// См. РаботаСЗадачами.ДанныеДереваПриложений
//
// Параметры:
//  Задача - ДокументСсылка.Задача
//  ДанныеДействияЗадачи - ДокументСсылка.ДействиеЗадачи
//                       - СтрокаТаблицыЗначений - см. ОбработкаЗапросовXDTOБизнесПроцессыИЗадачи.ЗапросСОтборами
//  СтрокаПриложения - см. РаботаСЗадачами.НоваяСтрокаДереваПриложений
//  КешДанных - см. ОбработкаЗапросовXDTOБизнесПроцессыИЗадачи.ДополнитьДанныеЗадачИСформироватьКешДанных
//
Процедура ДополнитьСтрокуПриложенияДереваПриложений(Задача, ДанныеДействияЗадачи, СтрокаПриложения,
		КешДанных = Неопределено) Экспорт
	
	// Локализация
	ЭлектронныеДокументыОбъектаУчета = ОбменЭДОДокументооборот.ЭлектронныеДокументыОбъектаУчета(
		СтрокаПриложения.Приложение);
	
	// Документ - Документ ЭДО.
	Для Каждого ДокументЭДО Из ЭлектронныеДокументыОбъектаУчета Цикл
		
		СтрокаЭДО = РаботаСЗадачами.НоваяСтрокаДереваПриложений();
		СтрокаЭДО.КлючСтроки = ДокументЭДО;
		СтрокаЭДО.ТипСтроки = Перечисления.ТипыСтрокДереваПриложений.ДокументЭДО;
		СтрокаЭДО.ПредставлениеПриложения = Строка(ДокументЭДО) + НСтр("ru = ' (Документ ЭДО)'");
		СтрокаЭДО.ИндексКартинки = 30; // как xml
		СтрокаЭДО.ДокументЭДО = ДокументЭДО;
		СтрокаПриложения.Строки.Добавить(СтрокаЭДО);
		
	КонецЦикла;
	// Локализация Конец
	
КонецПроцедуры

// Дополняет пустую структуру данных строки дерева приложений.
// 
// Параметры:
//  СтрокаДереваПриложений - Структура - См. РаботаСЗадачами.НоваяСтрокаДереваПриложений
Процедура ДополнитьНовуюСтрокуДереваПриложений(СтрокаДереваПриложений) Экспорт
	
// Локализация	
	СтрокаДереваПриложений.Вставить("ДокументЭДО", Документы.ЭлектронныйДокументВходящийЭДО.ПустаяСсылка());
// Локализация Конец
КонецПроцедуры

// Вызывается из ВыполнитьДействиеЗадачи общего модуля РаботаСЗадачами
//
// Параметры:
//  ДействиеЗадачи - ДокументСсылка.ДействиеЗадачи - Действие задачи.
//  РезультатДействия - СправочникСсылка.РезультатыДействийЗадач - Результат действия.
//  КтоОтметил - СправочникСсылка.Сотрудники - Кто отметил в программе.
//  ПараметрыВыполнения - Структура - Параметры выполнения. См. РаботаСЗадачамиКлиентСервер.НовыйПараметрыВыполненияДействийЗадач
//
Процедура ВыполнитьДействиеЗадачи(ДействиеЗадачи, РезультатДействия, КтоОтметил, ПараметрыВыполнения) Экспорт

// Локализация
	РаботаСВнешнимПодписанием.ПриВыполненииДействияЗадачи(ДействиеЗадачи, РезультатДействия,
		КтоОтметил, ПараметрыВыполнения);
// Локализация Конец
	
КонецПроцедуры

// Дополняет строку приложения задачи
// См. РаботаСЗадачами.ДанныеДереваПриложений
// 
// Параметры:
//  СтрокаДляОбработки - См. РаботаСЗадачами.НоваяСтрокаДереваПриложений
// 
// Возвращаемое значение:
//  Булево - Строка приложения обработана в этом методе, дальнейшей обработки не требуется
Функция ОбработатьСтрокуПриложения(СтрокаДляОбработки) Экспорт

// Локализация
	Если ЗначениеЗаполнено(СтрокаДляОбработки.ДокументЭДО) Тогда
		СтрокаДляОбработки.ИндексКартинки = 30;
	КонецЕсли;
// Локализация Конец

	Возврат Ложь;

КонецФункции

#КонецОбласти