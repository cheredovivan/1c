////////////////////////////////////////////////////////////////////////////////
// Работа с встроенной почтой
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Регламентное задание ДиспетчерПотоковПроверкиНаличияПисем.
//  Запускает задания, проверяющие наличие писем в ящиках почтового сервера. 
//  Смотрит по регистру СтатусФоновыхЗаданийПроверкиПочты, какие задания уже запущены, не зависли ли они.
//
Процедура ДиспетчерПотоковПроверкиНаличияПисем() Экспорт

	Отказ = Ложь;
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ДиспетчерПотоковПроверкиНаличияПисем, Отказ);
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПотокиПроверкиПочты.НомерЗадания КАК НомерЗадания,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПотокиПроверкиПочты.УчетнаяЗапись) КАК ЧислоУчетныхЗаписей
		|ИЗ
		|	РегистрСведений.ПотокиПроверкиПочты КАК ПотокиПроверкиПочты
		|
		|СГРУППИРОВАТЬ ПО
		|	ПотокиПроверкиПочты.НомерЗадания
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерЗадания";
		
	ТаблицаНомеровЗаданий = Запрос.Выполнить().Выгрузить();
	МассивНомеровЗаданий = ТаблицаНомеровЗаданий.ВыгрузитьКолонку("НомерЗадания");	
	
	Для Каждого НомерПотока Из МассивНомеровЗаданий Цикл
		
		СтатусПотока = СтатусПроверкиПочты(НомерПотока);
		
		// Фоновое задание завершилось корректно и надо снова его пускать (с тем же номером потока).
		Если Не СтатусПотока.ЕстьЗапись Тогда
			
			ВыполнитьЗапускФоновогоЗаданияПроверки(НомерПотока);
			Продолжить;
			
		КонецЕсли;	
		
		КлючФоновогоЗадания = "ПриемкаВнешнейПочты" + Строка(НомерПотока);
		
		ОтборФоновыхЗаданий = Новый Структура;
		ОтборФоновыхЗаданий.Вставить("Ключ", КлючФоновогоЗадания);
		ОтборФоновыхЗаданий.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
		
		МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборФоновыхЗаданий);
		
		РегЗаданиеЗавершилосьСОшибкой = Ложь;
		ПричинаЗависания = "";
		СтрокаСтарыхУчетныхЗаписей = "";
		
		ДатаОбработкиСвежая = СтатусПотока.ДатаПоследнейАктивности >= ТекущаяДатаСеанса()
			- ПредельноеВремяОтсутствияРаботыПочты();
		
		// Был перезапуск сервера (или задание вылетело с ошибкой), и задания уже нет.
		Если МассивФоновыхЗаданий.Количество() = 0 Тогда
			
			ЕстьСтарыеУчетныеЗаписи = ЕстьСтарыеУчетныеЗаписиВЭтомПотокеПроверки(НомерПотока, СтрокаСтарыхУчетныхЗаписей);
			
			// Если есть запись, дата посл обр-ки свежая, но есть старые учетки
			// - идем по ветке кода для зависания - ищем последнюю запись = плохая учетка и исключаем ее.
			Если ДатаОбработкиСвежая И ЕстьСтарыеУчетныеЗаписи Тогда
				
				РегЗаданиеЗавершилосьСОшибкой = Истина;
				ПричинаЗависания = СтрШаблон(НСтр("ru = 'Есть запись в РС СтатусФоновыхЗаданий, 
					|дата последней обработки (%1) свежая, но есть старые учетные записи.
					|Вероятно рег задание падает при приемке плохого письма. '"), 
					СтатусПотока.ДатаПоследнейАктивности)
					+ СтрокаСтарыхУчетныхЗаписей;
				
			Иначе
				
				// Стираем запись в СтатусФоновыхЗаданийПроверкиПочты.
				УдалитьСтатусПроверкиПочты(НомерПотока);
				ВыполнитьЗапускФоновогоЗаданияПроверки(НомерПотока);
				Продолжить;
				
			КонецЕсли;	
			
		КонецЕсли;				
		
		// Фоновое задание еще не завершилось, работает нормально (последняя активность не более 40 минут назад).
		Если ДатаОбработкиСвежая И Не РегЗаданиеЗавершилосьСОшибкой Тогда
			
			Продолжить;
			
		Иначе // Поток завис (или задание вылетело с ошибкой, скажем из-за неверной кодировки).
			
			ПродолжитьЦикл = Ложь;
			ОбработатьЗависаниеПотокаПроверки(НомерПотока, СтатусПотока, ДатаОбработкиСвежая, ПродолжитьЦикл, 
				ТаблицаНомеровЗаданий, МассивНомеровЗаданий, ПричинаЗависания);
			Если ПродолжитьЦикл Тогда
				Продолжить;
			КонецЕсли;	
			
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры      

// Регламентное задание ДиспетчерПотоковДоставкиПочты.
//  Запускает задания, уже непосредственно принимающие письма. 
//  (по РС ПриемкаВнешнейПочты уже точно известно что в этом ящике есть письма)
//  Смотрит по регистру СтатусФоновыхЗаданийПолученияПочты, какие задания уже запущены, не зависли ли они.
//
Процедура ДиспетчерПотоковДоставкиПочты() Экспорт
	
	Отказ = Ложь;
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ДиспетчерПотоковДоставкиПочты, Отказ);
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;

	// Проверяем старые записи РС ПриемкаВнешнейПочты (ИдетОбработка Истина и ДатаПоследнейАктивности старая)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПриемкаВнешнейПочты.УчетнаяЗапись КАК УчетнаяЗапись,
		|	ПриемкаВнешнейПочты.ДатаПоявленияПисем КАК ДатаПоявленияПисем,
		|	ПриемкаВнешнейПочты.ДатаОбработки КАК ДатаОбработки,
		|	ПриемкаВнешнейПочты.ИдетОбработка КАК ИдетОбработка,
		|	ПриемкаВнешнейПочты.ЧислоПисем КАК ЧислоПисем,
		|	ПриемкаВнешнейПочты.НомерСеанса КАК НомерСеанса
		|ИЗ
		|	РегистрСведений.ПриемкаВнешнейПочты КАК ПриемкаВнешнейПочты
		|ГДЕ
		|	ПриемкаВнешнейПочты.ИдетОбработка = ИСТИНА
		|	И ПриемкаВнешнейПочты.ДатаОбработки < &ДатаОбработки";
		
	Запрос.УстановитьПараметр("ДатаОбработки", ТекущаяДатаСеанса() - ВремяПросроченнойАктивности());
	Выборка = Запрос.Выполнить().Выбрать();	
	
	ДатаПолностьюПросроченной = ТекущаяДатаСеанса() - ВремяПолностьюПросроченнойАктивности();
	
	НайденыСтарые = Ложь;
	Описание = "";
	
	Пока Выборка.Следующий() Цикл
		
		// Дата несколько просрочена, но еще не безнадежно.
		// Делаем проверку НомерСеансаЕстьСредиАктивныхСеансовИБ
		Если Выборка.ДатаОбработки > ДатаПолностьюПросроченной Тогда
			
			ЕстьСредиАктивных = НомерСеансаЕстьСредиАктивныхСеансовИБ(Выборка.НомерСеанса);
			Если ЕстьСредиАктивных Тогда // еще работает, все ок
				Продолжить;
			КонецЕсли;	
			
		КонецЕсли;	
		
		// В диспетчере получения, смотрим  если в РС НаличиеПисем  есть запись  с ИдетОбработка Истина, 
		// но ДатаОбработки старее 4 минут – считаем что фоновое упало или зависло, 
		// и перезаписываем с ИдетОбработка Ложь и ДатаОбработки = ТекущаяДатаСеанса().
		// – чтобы другие фоновые смогли взять в работу эту учетную запись.
		
		МенеджерЗаписи = РегистрыСведений.ПриемкаВнешнейПочты.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		
		НайденыСтарые = Истина;
		Описание = Описание + Символы.ВК 
			+ СтрШаблон(НСтр("ru = 'Фоновое задание получения почты перестало работать или зависло. 
			|ИдетОбработка = ИСТИНА и ДатаОбработки=%1  УчетнаяЗапись: %2. ДатаПоявленияПисем: %3. 
			|ЧислоПисем: %4. НомерСеанса: %5.'"),
				Выборка.ДатаОбработки, Выборка.УчетнаяЗапись, Выборка.ДатаПоявленияПисем,
				Выборка.ЧислоПисем, Выборка.НомерСеанса);
		
		МенеджерЗаписи.ИдетОбработка = Ложь;
		МенеджерЗаписи.ДатаОбработки = ТекущаяДатаСеанса();
		
		МенеджерЗаписи.Записать();
		
	КонецЦикла;	
	
	Если НайденыСтарые Тогда
		
		Наименование = НСтр("ru = 'Фоновое задание получения почты перестало работать или зависло.'");
		Описание = Описание;
		

		ОбщегоНазначенияДокументооборот.УведомитьОтветственныхОПроблеме(Наименование, Описание,
			НСтр("ru = 'ВыполнитьПолучение'"));
		
	КонецЕсли;	
	
	
	// Непосредственно приемка
	ПриемкаВнешнихПисемФоновымиЗаданиями();
	
КонецПроцедуры

// Обработчик регламентного задания "Получение и отправка писем".
//
Процедура ПолучениеИОтправкаПисем() Экспорт
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ПолучитьИОтправитьПисьма();
	Иначе
		Отбор = Новый Структура;
		Отбор.Вставить("ИмяМетода", "ВстроеннаяПочтаСервер.ПолучитьИОтправитьПисьма");
		Отбор.Вставить("Ключ", "ВстроеннаяПочтаСервер.ПолучитьИОтправитьПисьма");
		Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
		МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
		Если МассивФоновыхЗаданий.Количество() = 0 Тогда
			Попытка
				ФоновыеЗадания.Выполнить("ВстроеннаяПочтаСервер.ПолучитьИОтправитьПисьма",,
					"ВстроеннаяПочтаСервер.ПолучитьИОтправитьПисьма", // Ключ
					НСтр("ru = 'Получение и отправка писем'")); // Наименование
			Исключение
				// Иногда фоновое задание успевает запуститься после проверки.
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Выполняет процедуру отправки и получения писем.
//
Функция ПолучитьИОтправитьПисьма() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СообщенияОбОшибках = Новый Массив;
	
	ВремяНачала = ТекущаяДатаСеанса();
	
	ИдентификаторСеанса = Новый УникальныйИдентификатор;
	ПорядковыйНомерСобытия = 0;
	ПараметрыЛогирования = Новый Структура("ИдентификаторСеанса, ПорядковыйНомерСобытия, НомерЗадания",
		ИдентификаторСеанса, ПорядковыйНомерСобытия, 0);
		
	ОтправитьВсеПисьма(ТекущаяДатаСеанса(), СообщенияОбОшибках, ПараметрыЛогирования);
	
	ПолучитьВсеПисьма(СообщенияОбОшибках, ПараметрыЛогирования);
	
	ВремяОкончания = ТекущаяДатаСеанса();
	ТекстЛога = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Длительность сеанса: %1 сек.'"),
		ВремяОкончания - ВремяНачала);
	
	ЗаписатьПротоколДоставкиПочты(
		Неопределено,
		ТекстЛога,
		Ложь,
		Перечисления.ТипыСобытийДоставкиПочты.ЗавершениеСеансаПриемкиОтправкиПисем,
		Неопределено,
		ПараметрыЛогирования.ПорядковыйНомерСобытия,
		ПараметрыЛогирования.ИдентификаторСеанса);
	
	Возврат СообщенияОбОшибках;
	
КонецФункции

// Вычисляет размер почтового сообщения
Функция ВычислитьРазмерПочтовогоСообщения(ПараметрыОтправки) Экспорт
	
	Размер = 0;
	
	Размер = Размер + СтрДлина(ПараметрыОтправки.Тема);
	Для каждого Текст Из ПараметрыОтправки.Тексты Цикл
		Размер = Размер + СтрДлина(Текст.Текст);
	КонецЦикла;
	Для каждого Вложение Из ПараметрыОтправки.Вложения Цикл
		Если ТипЗнч(Вложение.Данные) <> Тип("ДвоичныеДанные") Тогда
			Продолжить;
		КонецЕсли;
		Размер = Размер + Вложение.Данные.Размер();
	КонецЦикла;
	
	Возврат Размер;
	
КонецФункции

// Возвращает значение поля из заголовка письма
Функция ПолучитьЗначениеПоляИзЗаголовкаПисьма(Знач ЗаголовокПисьма, Знач Поле) Экспорт
	
	СимволыПереводаСтроки = ВстроеннаяПочтаСерверПовтИсп.ПолучитьСоответствиеСимволовПереводаСтроки();

	КоличествоСтрок = СтрЧислоСтрок(ЗаголовокПисьма);
	Для Индекс = 1 По КоличествоСтрок Цикл
		Строка = СтрПолучитьСтроку(ЗаголовокПисьма, Индекс);
		Если СтрНайти(НРег(Строка), НРег(Поле) + ": ") = 1 Тогда
			Результат = Сред(Строка, СтрДлина(Поле) + 3);
			Для ВнутреннийИндекс = Индекс + 1 По КоличествоСтрок Цикл
				Строка = СтрПолучитьСтроку(ЗаголовокПисьма, ВнутреннийИндекс);
				СимволПеревода = СимволыПереводаСтроки.Получить(Лев(Строка, 1));
		
				Если СимволПеревода = Неопределено Тогда
					Прервать;
				КонецЕсли;
				
				Результат = Результат + " " + Сред(Строка, 2);
			КонецЦикла;
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	
	Возврат "";
	
КонецФункции

// Устанавливает текст входящего письма из структуры текста почтового сообщения.
//
Процедура УстановитьСодержаниеПисьмаИзСтруктурыТекстаПочтовогоСообщения(ПисьмоОбъект, СтруктураТекста) Экспорт
	
	ПисьмоОбъект.ТипТекста = СтруктураТекста.ТипТекста;
	
	Если СтруктураТекста.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		
		ПисьмоОбъект.УстановитьСодержаниеПисьма(
			СтруктураТекста.ТекстHTML,
			СтруктураТекста.ТипТекста,
			СтруктураТекста.Кодировка);
		
	Иначе
		
		ПисьмоОбъект.УстановитьСодержаниеПисьма(
			СтруктураТекста.Текст,
			СтруктураТекста.ТипТекста,
			СтруктураТекста.Кодировка);
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает ссылку на записанное письмо или Неопределено в случае ошибки.
//
Функция ЗаписатьВходящееПисьмо(Сообщение, УчетнаяЗапись, 
	СообщениеОбОшибке = Неопределено, НеЗаписыватьКакОшибку = Ложь) Экспорт
	
	СообщениеОбОшибке = "";
	ФайлПриглашение = Неопределено;
	
	ИдентификаторСообщения = РаботаСоСтроками.УдалитьНедопустимыеСимволыXML(Сообщение.ИдентификаторСообщения);
	Если ИдентификаторСообщения <> "$null" И ИдентификаторСообщения <> "null" Тогда
		РанееПринятоеПисьмо = НайтиВходящееПисьмоПоИдентификаторуИУчетнойЗаписи(ИдентификаторСообщения, УчетнаяЗапись);
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(РанееПринятоеПисьмо) Тогда
		
		ИдентификаторыСтрока = "";
		Для Каждого Ид Из Сообщение.Идентификатор Цикл
			ИдентификаторыСтрока = ИдентификаторыСтрока + Ид + " ";
		КонецЦикла;
		
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru= 'Ранее уже принято письмо (%1) с таким идентификатором (Message-Id: %2) - (Идентификатор: %3).'"),
			РанееПринятоеПисьмо, ИдентификаторСообщения, ИдентификаторыСтрока);
			
		// Запись идентификатора полученного сообщения - чтобы больше не принималось это письмо.
		Для каждого Идентификатор Из Сообщение.Идентификатор Цикл
			РегистрыСведений.ИдентификаторыПолученныхПисем.ДобавитьЗапись(
				УчетнаяЗапись,
				Идентификатор);
			КонецЦикла;
			
		НеЗаписыватьКакОшибку = Истина;
			
		Возврат Неопределено;
		
	КонецЕсли;      
	
	МассивПутейВТоме = Новый Массив;
	
	НачатьТранзакцию();
	
	Попытка
	
		ПисьмоОбъект = Документы.ВходящееПисьмо.СоздатьДокумент();
		ПисьмоОбъект.УчетнаяЗапись = УчетнаяЗапись;
		ПисьмоОбъект.ВидМаршрутизации = Перечисления.ВидыМаршрутизацииПисем.Внешняя;    
		ПисьмоОбъект.ДополнительныеСвойства.Вставить("НеЗаписыватьОперациюСПисьмом", Истина);
		
		ЗаполнитьВходящееПисьмоИзСтруктурыПочтовогоСообщения(ПисьмоОбъект, Сообщение, УчетнаяЗапись);
		
		ИдентификаторПроектаСтрока = ПолучитьЗначениеПоляИзЗаголовкаПисьма(Сообщение.Заголовок, "X-1C-Project-ID");
		Если ЗначениеЗаполнено(ИдентификаторПроектаСтрока) Тогда
			
			ИдентификаторПроекта = Неопределено;
			
			Попытка
				ИдентификаторПроекта = Новый УникальныйИдентификатор(ИдентификаторПроектаСтрока);
			Исключение
				// может придти некорректный Ид проекта
			КонецПопытки;
			
			Если ЗначениеЗаполнено(ИдентификаторПроекта) Тогда
				Проект = Справочники.Проекты.ПолучитьСсылку(ИдентификаторПроекта);
				Если ЗначениеЗаполнено(Проект) И ДоступенПоПравамОтветственнымУчетнойЗаписи(Проект, УчетнаяЗапись) Тогда 
					ПисьмоОбъект.Проект = Проект;
				КонецЕсли;
			КонецЕсли;
		
		КонецЕсли;	
		
		Основание = Неопределено;
		ИдентификаторПисьма = ПолучитьЗначениеПоляИзЗаголовкаПисьма(Сообщение.Заголовок, "In-Reply-To");
		Если ЗначениеЗаполнено(ИдентификаторПисьма) Тогда
			ИдентификаторОснования = РаботаСоСтроками.ВыделитьПодстрокуВСкобках(ИдентификаторПисьма, "<", ">");
			Основание = НайтиИсходящееПисьмоПоИдентификатору(ИдентификаторОснования, УчетнаяЗапись);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Основание) Тогда // не было поля "In-Reply-To" или не нашли по идентификатору
			СтрокаИдентификаторовПисем = ПолучитьЗначениеПоляИзЗаголовкаПисьма(Сообщение.Заголовок, "References");
			Если ЗначениеЗаполнено(СтрокаИдентификаторовПисем) Тогда
				МассивИдентификаторовПисем = ПолучитьМассивИдентификаторовПисем(СтрокаИдентификаторовПисем);
				Если МассивИдентификаторовПисем.Количество() > 0 Тогда
					МассивПисем = НайтиИсходящиеПисьмаПоИдентификаторам(МассивИдентификаторовПисем, УчетнаяЗапись);
					Если МассивПисем.Количество() > 0 Тогда
						Основание = МассивПисем[0];
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Основание) Тогда
			ПисьмоОбъект.ДополнительныеСвойства.Вставить("Основание", Основание);
			ЗаполнитьРеквизитыПисьмаИзОснования(ПисьмоОбъект, Основание);
		КонецЕсли;
		
		ПисьмоОбъект.Записать();
		
		РегистрыСведений.ТекстыПисем.ЗаписатьТекстПисьма(ПисьмоОбъект.Ссылка, 
			ПисьмоОбъект.УчетнаяЗапись, ПисьмоОбъект.ТекстПисьмаПростойТекстХранилище);
		
		Если ЗначениеЗаполнено(Основание) Тогда
			СвязиОбъектов.УстановитьСвязь(
				ПисьмоОбъект.Ссылка,
				Неопределено,
				Основание,
				Справочники.ТипыСвязей.ПолученоВОтветНаПисьмо);
		КонецЕсли;
		
		ОбновитьВеткуПереписки(ПисьмоОбъект.Ссылка);
		
		// Запись вложений
		СобственныеФайлыВозвращаемый = Новый Массив;
		ПорядковыйНомерВложения = 0;
		РасширенияICS = ВстроеннаяПочтаСерверПовтИсп.ПолучитьРасширенияФайловICalendar();	
		
		ДобавитьФайлыИзИнтернетПочтовогоСообщения(
			ПисьмоОбъект.Ссылка,
			Сообщение,
			ПисьмоОбъект.Дата, //ВремяИзменения
			"", // ПрефиксНомераПисьма
			СобственныеФайлыВозвращаемый,
			ПорядковыйНомерВложения,
			РасширенияICS,
			ФайлПриглашение,
			МассивПутейВТоме);
			
		ЭтоФайловаяБаза = ОбщегоНазначения.ИнформационнаяБазаФайловая();	
		ПроверятьУникальность = ЭтоФайловаяБаза;
		
		// Запись идентификатора полученного сообщения
		Для каждого Идентификатор Из Сообщение.Идентификатор Цикл
			РегистрыСведений.ИдентификаторыПолученныхПисем.ДобавитьЗапись(
				УчетнаяЗапись,
				Идентификатор,
				ПроверятьУникальность);
		КонецЦикла;
			
		БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(ПисьмоОбъект.Ссылка, 
			Справочники.ВидыБизнесСобытий.ПолучениеВходящегоПисьма);	
			
		РегистрыСведений.КоличествоПисемВПапкахИтоги.Добавить(ПисьмоОбъект.Ссылка, ПисьмоОбъект.Папка);
			
		ЗафиксироватьТранзакцию();
		
		Попытка
			
			ЗаблокироватьДанныеДляРедактирования(ПисьмоОбъект.Ссылка); 
			ПисьмоОбъект.ДополнительныеСвойства.Вставить("НеЗаписыватьОперациюСПисьмом", Ложь);
			
			Если ПрименитьПользовательскиеПравила(ПисьмоОбъект) Тогда
				ПисьмоОбъект.Записать();
			КонецЕсли;
			
			Если ПисьмоОбъект.ДополнительныеСвойства.Свойство("Правила_УстановитьПометкуУдаления")
				И ПисьмоОбъект.ДополнительныеСвойства.Правила_УстановитьПометкуУдаления Тогда
				ПисьмоОбъект.УстановитьПометкуУдаления(Истина);
			КонецЕсли;
			
			Если ТипЗнч(ПисьмоОбъект.Ссылка) = Тип("ДокументСсылка.ВходящееПисьмо") Тогда
				ПрименитьПравилаОповещенияДляВходящихПисем(ПисьмоОбъект);	
			КонецЕсли;
			
			РазблокироватьДанныеДляРедактирования(ПисьмоОбъект.Ссылка);
			
		Исключение
			РазблокироватьДанныеДляРедактирования(ПисьмоОбъект.Ссылка);
			Инфо = ИнформацияОбОшибке();
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при обработке письма правилами: 
					|%1'"),
				ПодробноеПредставлениеОшибки(Инфо));
			Почта.ЗаписатьОшибкуВЖурналРегистрации(СообщениеОбОшибке, ПисьмоОбъект.Ссылка);
		КонецПопытки;
		
		Если ФайлПриглашение <> Неопределено Тогда
			
			Попытка
				
				// ЕстьОшибкиПриемкиОтправкиПочты - используем для показа картинки "Приглашение" - для Входящих
				РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(
					ПисьмоОбъект.Ссылка, "ЕстьОшибкиПриемкиОтправкиПочты", Истина);
		
				СоздатьЗаписиКалендаряОтветственным(
					ПисьмоОбъект.Ссылка, ФайлПриглашение, ПисьмоОбъект.УчетнаяЗапись);
					
			Исключение
						
				Инфо = ИнформацияОбОшибке();
				СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка создания записей календаря по письму: 
						|%1'"),
					ПодробноеПредставлениеОшибки(Инфо));
				Почта.ЗаписатьОшибкуВЖурналРегистрации(СообщениеОбОшибке, ПисьмоОбъект.Ссылка);
			
			КонецПопытки;
						
		КонецЕсли;	
		
	Исключение
		
		ОтменитьТранзакцию();   
		
		ИнфоОшибки = ИнформацияОбОшибке();
		
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнфоОшибки);
		
		Если ИнфоОшибки.Код = РаботаСФайламиВызовСервера.КодОшибкиЗапретногоРасширения() Тогда
			
			// Запись идентификатора полученного сообщения - чтобы больше не принималось это письмо.
			Для Каждого Идентификатор Из Сообщение.Идентификатор Цикл
				РегистрыСведений.ИдентификаторыПолученныхПисем.ДобавитьЗапись(
					УчетнаяЗапись,
					Идентификатор);
			КонецЦикла;    
				
			Почта.ЗаписатьОшибкуВЖурналРегистрации(
				СтрШаблон(НСтр("ru = 'Письмо(%1 - %2) помечено как принятое, чтобы больше не принималось это письмо. '"), 
				ИдентификаторСообщения, Сообщение.Тема)
				+ СообщениеОбОшибке);	
			
		КонецЕсли;	
		
		// Чтобы ВстроеннаяПочтаСерверПовтИсп.ПолучитьПочтовогоАдресата сбросила закешированные значения.
		ОбновитьПовторноИспользуемыеЗначения();  
		
		Для Каждого ПутьФайла Из МассивПутейВТоме Цикл // при откате транзакции удалим файлы в томе
			Попытка
				ФайловыеФункции.УдалитьФайл(ПутьФайла);
			Исключение
				// не бросаем исключения
			КонецПопытки;
		КонецЦикла;	
		
		Возврат Неопределено;
		
	КонецПопытки;
	
	Возврат ПисьмоОбъект.Ссылка;
	
КонецФункции

// Возвращает массив исходящих писем, найденных по идентификатору основания.
//
Функция НайтиИсходящиеПисьмаПоИдентификаторуОснования(ИдентификаторОснования) Экспорт

	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИдентификаторыИмпортированныхПисем.Письмо.Ссылка КАК Ссылка
		|ИЗ
		|	РегистрСведений.ИдентификаторыИмпортированныхПисем КАК ИдентификаторыИмпортированныхПисем
		|ГДЕ
		|	ИдентификаторыИмпортированныхПисем.ИдентификаторОснования = &ИдентификаторОснования
		|	И ТИПЗНАЧЕНИЯ(ИдентификаторыИмпортированныхПисем.Письмо) = ТИП(Документ.ИсходящееПисьмо)
		|	И ИдентификаторыИмпортированныхПисем.Письмо.ПометкаУдаления = ЛОЖЬ");
		
	Запрос.УстановитьПараметр("ИдентификаторОснования", ИдентификаторОснования);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

// Возвращает массив входящих писем, найденных по идентификатору основания.
//
Функция НайтиВходящиеПисьмаПоИдентификаторуОснования(ИдентификаторОснования) Экспорт

	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИдентификаторыИмпортированныхПисем.Письмо.Ссылка КАК Ссылка
		|ИЗ
		|	РегистрСведений.ИдентификаторыИмпортированныхПисем КАК ИдентификаторыИмпортированныхПисем
		|ГДЕ
		|	ИдентификаторыИмпортированныхПисем.ИдентификаторОснования = &ИдентификаторОснования
		|	И ТИПЗНАЧЕНИЯ(ИдентификаторыИмпортированныхПисем.Письмо) = ТИП(Документ.ВходящееПисьмо)
		|	И ИдентификаторыИмпортированныхПисем.Письмо.ПометкаУдаления = ЛОЖЬ");
		
	Запрос.УстановитьПараметр("ИдентификаторОснования", ИдентификаторОснования);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции   

// Добавляет вложение письма.
//
Функция ДобавитьВложениеПисьмаИзДвоичныхДанных(
	Письмо, // ДокументСсылка.ВходящееПисьмо, ДокументСсылка.ИсходящееПисьмо
	ДвоичныеДанные, // ДвоичныеДанные
	ИмяФайла,
	ВремяИзменения, // ДатаВремя
	Идентификатор) Экспорт // Идентификатор почтового вложения
	
	Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	Размер = ДвоичныеДанные.Размер();
	
	ФайлСсылка = ДобавитьВложениеПисьмаИзВременногоХранилища(
		Письмо,
		Адрес,
		Неопределено,
		Размер,
		ИмяФайла,
		ВремяИзменения,
		Идентификатор);
	
	УдалитьИзВременногоХранилища(Адрес);
	
	Возврат ФайлСсылка;
	
КонецФункции

// Добавляет вложение письма.
//
Функция ДобавитьВложениеПисьмаИзВременногоХранилища(
	Письмо, // ДокументСсылка.ВходящееПисьмо, ДокументСсылка.ИсходящееПисьмо
	АдресВременногоХранилища,
	АдресВременногоХранилищаТекста,
	Размер,
	ИмяФайла,
	ВремяИзменения, // ДатаВремя
	Идентификатор) Экспорт // Идентификатор почтового вложения
	
	ИмяФайлаИнфо = РаботаСоСтроками.РазложитьИмяФайла(ИмяФайла);
	ВремяИзмененияУниверсальное = РаботаСФайламиКлиентСервер.ПолучитьУниверсальноеВремя(ВремяИзменения);
	
	// Создадим карточку Файла в БД
	СведенияОФайле = РаботаСФайламиКлиентСервер.СведенияОФайле("ФайлСВерсией");
	СведенияОФайле.АдресВременногоХранилищаФайла = АдресВременногоХранилища;
	СведенияОФайле.АдресВременногоХранилищаТекста = АдресВременногоХранилищаТекста;
	СведенияОФайле.ИмяБезРасширения = ИмяФайлаИнфо.Имя;
	СведенияОФайле.РасширениеБезТочки = ИмяФайлаИнфо.Расширение;
	СведенияОФайле.Размер = Размер;
	СведенияОФайле.ВремяИзменения = ВремяИзменения;
	СведенияОФайле.ВремяИзмененияУниверсальное = ВремяИзмененияУниверсальное;
	СведенияОФайле.ХранитьВерсии = Ложь;
	
	ВложениеПисьмаСсылка = РаботаСФайламиВызовСервера.СоздатьФайлСВерсией(Письмо, СведенияОФайле);
	
	Если Не ПустаяСтрока(Идентификатор) Тогда
		РегистрыСведений.ИдентификаторыПочтовыхВложений.УстановитьИдентификаторПочтовогоВложения(ВложениеПисьмаСсылка, Идентификатор);
	КонецЕсли;
	
	Возврат ВложениеПисьмаСсылка;
	
КонецФункции

Функция ПолучитьСтруктуруТекстаИзСтруктурыПочтовогоСообщения(Сообщение, ПрефиксНомера = "") Экспорт
	
	СтруктураТекста = Почта.ПолучитьСтруктуруТекстаИзСтруктурыПочтовогоСообщения(Сообщение);
	
	ИндексНомера = 1;
	
	Для каждого Вложение Из Сообщение.Вложения Цикл
		
		Если ТипЗнч(Вложение.Данные) = Тип("Структура") Тогда
			
			Если ПустаяСтрока(ПрефиксНомера) Тогда
				Номер = Строка(ИндексНомера);
			Иначе
				Номер = ПрефиксНомера + "." + ИндексНомера;
			КонецЕсли;
			ИндексНомера = ИндексНомера + 1;
			
			// рекурсия
			СтруктураТекстаВложения =
				ПолучитьСтруктуруТекстаИзСтруктурыПочтовогоСообщения(Вложение.Данные, Номер);
			
			ДобавитьСтруктуруТекстаВложения(СтруктураТекста, СтруктураТекстаВложения);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ПустаяСтрока(ПрефиксНомера) Тогда
		ВставитьШапкуСообщенияВСтруктуруТекста(СтруктураТекста, Сообщение, ПрефиксНомера);
	КонецЕсли;
	
	Возврат СтруктураТекста;
	
КонецФункции

// Заполняет реквизиты Предмет и Проект из письма-основания.
//
Процедура ЗаполнитьРеквизитыПисьмаИзОснования(ПисьмоОбъект, Основание) Экспорт
	
	ОснованиеИнфо = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание, "Предмет, Проект");
	ПисьмоОбъект.Предмет = ОснованиеИнфо.Предмет;
	ПисьмоОбъект.Проект = ОснованиеИнфо.Проект;
	
КонецПроцедуры

// Возвращает письмо по идентификатору.
// Если письмо не найдено, возвращает Неопределено.
//
Функция НайтиИсходящееПисьмоПоИдентификатору(Идентификатор, УчетнаяЗапись) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИсходящееПисьмо.Ссылка,
		|	ИсходящееПисьмо.Дата
		|ИЗ
		|	Документ.ИсходящееПисьмо КАК ИсходящееПисьмо
		|ГДЕ
		|	ИсходящееПисьмо.ИдентификаторСообщения = &Идентификатор
		|	И ИсходящееПисьмо.УчетнаяЗапись = &УчетнаяЗапись
		|	И ИсходящееПисьмо.ПометкаУдаления = ЛОЖЬ");
		
	ИдентификаторУрезанный = Лев(Идентификатор, 200);	
	Запрос.УстановитьПараметр("Идентификатор", ИдентификаторУрезанный);
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить()[0].Ссылка;
	
КонецФункции

// Возвращает входящее письмо по идентификатору.
// Если письмо не найдено, возвращает Неопределено.
//
Функция НайтиВходящееПисьмоПоИдентификатору(Идентификатор) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВходящееПисьмо.Ссылка,
		|	ВходящееПисьмо.Дата
		|ИЗ
		|	Документ.ВходящееПисьмо КАК ВходящееПисьмо
		|ГДЕ
		|	ВходящееПисьмо.ИдентификаторСообщения = &Идентификатор");
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить()[0].Ссылка;	
КонецФункции

// Возвращает письмо по идентификатору Outlook.
// Если письмо не найдено, возвращает Неопределено.
//
Функция НайтиПисьмоПоИдентификаторуOutlook(Идентификатор) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИдентификаторыИмпортированныхПисем.Письмо.Ссылка КАК Ссылка
		|ИЗ
		|	РегистрСведений.ИдентификаторыИмпортированныхПисем КАК ИдентификаторыИмпортированныхПисем
		|ГДЕ
		|	ИдентификаторыИмпортированныхПисем.ИдентификаторИсточника = &ИдентификаторИсточника");	
		
	Запрос.УстановитьПараметр("ИдентификаторИсточника", Идентификатор);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить()[0].Ссылка;	
КонецФункции

// Возвращает идентификаторы письма из заголовка
//
Функция ИдентификаторыПисьмаИзЗаголовка(Заголовок) Экспорт
		
	Результат = Новый Структура("ИдентификаторСообщения, ИдентификаторОснования");
	
	Результат.ИдентификаторОснования = ПолучитьЗначениеПоляИзЗаголовкаПисьма(
		Заголовок, 
		"In-Reply-To");
	Результат.ИдентификаторОснования = 
		РаботаСоСтроками.ВыделитьПодстрокуВСкобках(Результат.ИдентификаторОснования, "<", ">");
		
	Результат.ИдентификаторСообщения = ПолучитьЗначениеПоляИзЗаголовкаПисьма(
		Заголовок, 
		"Message-Id");
	Результат.ИдентификаторСообщения = 
		РаботаСоСтроками.ВыделитьПодстрокуВСкобках(Результат.ИдентификаторСообщения, "<", ">");
	
	Возврат Результат;	
КонецФункции

// Возвращает адресат для контакта - с минимальным кодом - аналогично функции ПолучитьПочтовогоАдресата
Функция ПолучитьАдресатПоКонтакту(Контакт) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СведенияОбАдресатах.АдресатСообщения КАК АдресатСообщения
		|ИЗ
		|	РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|ГДЕ
		|	СведенияОбАдресатах.Контакт = &Контакт
		|
		|УПОРЯДОЧИТЬ ПО
		|	СведенияОбАдресатах.АдресатСообщения.Код";
		
	Запрос.УстановитьПараметр("Контакт", Контакт);	
		
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.АдресатСообщения;
		
КонецФункции


/////////////////////////////////////////////////////////////////////////////////////////
// РАБОТА С ПРАВИЛАМИ

// Применяет к письму предопределенные правила.
// Вызывается из модулей объектов ВходящееПисьмо, ИсходящееПисьмо в конце процедуры ПередЗаписью.
//
Процедура ПрименитьПравила(ПисьмоОбъект) Экспорт
	
	ПрименитьПредопределенныеПравила(ПисьмоОбъект);
	
	Если ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(ПисьмоОбъект) Тогда
		Если ЗначениеЗаполнено(ПисьмоОбъект.ПодготовленоКОтправке)
			И ПисьмоОбъект.ДополнительныеСвойства.Свойство("ВыполняетсяОтправка")
			И ПисьмоОбъект.ДополнительныеСвойства.ВыполняетсяОтправка Тогда
			ПрименитьПравилоПриОтправке(ПисьмоОбъект, Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает письмо с учетом предопределенных правил.
//
Процедура ПрименитьПредопределенныеПравила(ПисьмоОбъект) Экспорт
	
	Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(ПисьмоОбъект) Тогда
		
		Если ПисьмоОбъект.ЭтоНовый() И Не ЗначениеЗаполнено(ПисьмоОбъект.Папка) Тогда
			// Новые письма без папки помещаются в папку Входящие.
			ПисьмоОбъект.Папка = РегистрыСведений.ПапкиУчетныхЗаписей.ПолучитьПапку(
				ПисьмоОбъект.УчетнаяЗапись,
				Перечисления.ВидыПапокПисем.Входящие);
		КонецЕсли;
		
	ИначеЕсли ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(ПисьмоОбъект) Тогда
		
		Если ЗначениеЗаполнено(ПисьмоОбъект.ПодготовленоКОтправке) Тогда
			// Письма, готовые к отправке помещаются в папку Исходящие.
			ПапкаДляИсходящих = РегистрыСведений.ПапкиУчетныхЗаписей.ПолучитьПапку(
				ПисьмоОбъект.УчетнаяЗапись,
				Перечисления.ВидыПапокПисем.Исходящие);
			Если Не ЗначениеЗаполнено(ПапкаДляИсходящих) Тогда
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Для учетной записи %1 не определена папка для исходящих писем.'"),
					ПисьмоОбъект.УчетнаяЗапись);
			КонецЕсли;
			ПисьмоОбъект.Папка = ПапкаДляИсходящих;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПисьмоОбъект.Папка) Тогда
			// Письма без папки помещаются в папку Черновики.
			ПапкаДляЧерновиков = РегистрыСведений.ПапкиУчетныхЗаписей.ПолучитьПапку(
				ПисьмоОбъект.УчетнаяЗапись,
				Перечисления.ВидыПапокПисем.Черновики);
			Если ЗначениеЗаполнено(ПапкаДляЧерновиков) Тогда
				ПисьмоОбъект.Папка = ПапкаДляЧерновиков;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		ВызватьИсключение НСтр("ru = 'В процедуру ""ПрименитьПредопределенныеПравила"" передан некорректный параметр.'");
		
	КонецЕсли;
	
	Возврат;
	
КонецПроцедуры

Функция ПрименитьПользовательскиеПравила(ПисьмоОбъект) Экспорт
	
	Если ТипЗнч(ПисьмоОбъект.Ссылка) = Тип("ДокументСсылка.ВходящееПисьмо") Тогда
		Возврат ПрименитьПравилаДляВходящихПисем(ПисьмоОбъект);
	ИначеЕсли ТипЗнч(ПисьмоОбъект.Ссылка) = Тип("ДокументСсылка.ИсходящееПисьмо") Тогда
		Возврат ПрименитьПравилаДляИсходящихПисем(ПисьмоОбъект);
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПроверитьУсловияПравилаДляВходящих(ПисьмоОбъектСсылка, Правило, ТаблицаПараметров, РеквизитыПисьма, Исключения) Экспорт
		
	Результат = Не Исключения;
	ПервоеСрабатываниеУсловия = Истина;
	
	Если Исключения = Ложь Тогда
		Связь = Перечисления.ТипыЛогическойСвязи.И;	
	Иначе
		Связь = Перечисления.ТипыЛогическойСвязи.Или;
	КонецЕсли;
	
	Если ТаблицаПараметров.Количество() > 0 Тогда
		Для Каждого ЗначениеПеречисления Из Перечисления.ВидыУсловийОтбораВходящихПисем Цикл

			Отбор = Новый Структура("Вид, ЭтоЗначенияИсключения", ЗначениеПеречисления, Исключения);
			МассивСтрок = ТаблицаПараметров.НайтиСтроки(Отбор);
			Если МассивСтрок.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			Если ПервоеСрабатываниеУсловия Тогда
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Истина;
				Иначе 
					Результат = Ложь;
				КонецЕсли;
				ПервоеСрабатываниеУсловия = Ложь;
			КонецЕсли;
			Если ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.ТемаСодержитУказанныеСлова Тогда
				
				РезультатПроверкиУсловия = Ложь;
				Для Каждого Строка Из МассивСтрок Цикл
					РезультатПроверкиУсловия = РезультатПроверкиУсловия 
						ИЛИ СтрНайти(НРег(РеквизитыПисьма.Тема), НРег(Строка.Параметр)) > 0;
				КонецЦикла;
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат Или РезультатПроверкиУсловия;
				КонецЕсли;
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.ТемаНачинаетсяС Тогда
				
				РезультатПроверкиУсловия = Ложь;
				Для Каждого Строка Из МассивСтрок Цикл
					РезультатПроверкиУсловия = РезультатПроверкиУсловия 
						ИЛИ СтрНайти(НРег(РеквизитыПисьма.Тема), НРег(Строка.Параметр)) = 1;
				КонецЦикла;
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат Или РезультатПроверкиУсловия;
				КонецЕсли;				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.АдресОтправителяСодержитУказанныеСлова Тогда
				
				РезультатПроверкиУсловия = Ложь;
				ПредставлениеОтправителя = ПолучитьПредставлениеИКонтактАдресата(РеквизитыПисьма.ОтправительАдресат).Представление;
				Для Каждого Строка Из МассивСтрок Цикл
					РезультатПроверкиУсловия = РезультатПроверкиУсловия 
						ИЛИ СтрНайти(НРег(РеквизитыПисьма.ОтправительАдресат.Адрес), НРег(Строка.Параметр)) > 0
						ИЛИ СтрНайти(НРег(РеквизитыПисьма.ОтправительАдресат.Наименование), НРег(Строка.Параметр)) > 0
						ИЛИ СтрНайти(НРег(ПредставлениеОтправителя), НРег(Строка.Параметр)) > 0;
				КонецЦикла;
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат Или РезультатПроверкиУсловия;
				КонецЕсли;	
				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.ОтправленоНаУказанныеАдреса Тогда
				
				РезультатПроверкиУсловия = Ложь;
				ТаблицаПолучателей = ПолучитьТаблицуПолучателейКомуКопияУВходящегоПисьма(РеквизитыПисьма.Ссылка);
				Для Каждого Строка Из МассивСтрок Цикл
					
					Для Каждого СтрокаПолучатель Из ТаблицаПолучателей Цикл
						РезультатПроверкиУсловия = РезультатПроверкиУсловия
							ИЛИ СтрНайти(НРег(СтрокаПолучатель.Адрес), НРег(Строка.Параметр)) > 0
							ИЛИ СтрНайти(НРег(СтрокаПолучатель.Наименование), НРег(Строка.Параметр)) > 0
							ИЛИ СтрНайти(НРег(СтрокаПолучатель.Представление), НРег(Строка.Параметр)) > 0;	
						Если РезультатПроверкиУсловия Тогда
							Прервать;
						КонецЕсли;	
					КонецЦикла;
					
					Если РезультатПроверкиУсловия Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				ИначеЕсли Связь = Перечисления.ТипыЛогическойСвязи.Или Тогда
					Результат = Результат Или РезультатПроверкиУсловия;
				КонецЕсли;
				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.ПолученоВТечениеУказанногоПериода Тогда
		
				РезультатПроверкиУсловия = Ложь;
				Дата1 = Дата(1,1,1);
				Дата2 = Дата(1,1,1);
				
				Если МассивСтрок.Количество() > 0 Тогда
					Дата1 = МассивСтрок[0].Параметр;
					Если ТипЗнч(Дата1) <> Тип("Дата") Тогда
						Дата1 = Дата(1,1,1);
					КонецЕсли;
				конецЕсли;                                                                                                            
				Если МассивСтрок.Количество() > 1 Тогда
					Дата2 = МассивСтрок[1].Параметр;
					Если ТипЗнч(Дата2) <> Тип("Дата") Тогда
						Дата2 = Дата(1,1,1);
					КонецЕсли;
				КонецЕсли;
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат 
						И РеквизитыПисьма.ДатаПолучения >= НачалоДня(Дата1)
						И (ЗначениеЗаполнено(Дата2) И РеквизитыПисьма.ДатаПолучения <= КонецДня(Дата2)
						ИЛИ Не ЗначениеЗаполнено(Дата2));
				Иначе
					Результат = Результат 
						ИЛИ РеквизитыПисьма.ДатаПолучения >= НачалоДня(Дата1)
						И (ЗначениеЗаполнено(Дата2) И РеквизитыПисьма.ДатаПолучения <= КонецДня(Дата2)
						ИЛИ Не ЗначениеЗаполнено(Дата2));
				КонецЕсли;
				
						
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.ИмеетВложения Тогда
				
				Для Каждого Строка Из МассивСтрок Цикл
					Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
						Результат = Результат 
							И (РеквизитыПисьма.ЕстьВложения И Строка.Параметр = Истина
							ИЛИ Не РеквизитыПисьма.ЕстьВложения И Строка.Параметр = Ложь);
					Иначе
						Результат = Результат 
							ИЛИ (РеквизитыПисьма.ЕстьВложения И Строка.Параметр = Истина
							ИЛИ Не РеквизитыПисьма.ЕстьВложения И Строка.Параметр = Ложь);
					КонецЕсли;
				КонецЦикла;
				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.АдресованоМне Тогда
				
				ТаблицаПолучателейПисьма = ПолучитьТаблицуПолучателейКомуКопияУВходящегоПисьма(РеквизитыПисьма.Ссылка);
				
				Для Каждого Строка Из МассивСтрок Цикл
					
					РезультатПроверкиУсловия = Ложь;
					Для Каждого СтрокаПолучатель Из ТаблицаПолучателейПисьма Цикл
						РезультатПроверкиУсловия = РезультатПроверкиУсловия 
							ИЛИ СтрокаПолучатель.Адрес = РеквизитыПисьма.УчетнаяЗапись.АдресЭлектроннойПочты;
						Если РезультатПроверкиУсловия Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;
		
					Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
						Результат = Результат 
							И (РезультатПроверкиУсловия И Строка.Параметр = Истина
							ИЛИ Не РезультатПроверкиУсловия И Строка.Параметр = Ложь);
					Иначе
						Результат = Результат 
							ИЛИ (РезультатПроверкиУсловия И Строка.Параметр = Истина
							ИЛИ Не РезультатПроверкиУсловия И Строка.Параметр = Ложь);
					КонецЕсли;
					
				КонецЦикла;
				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.АдресованоТолькоМне Тогда
				
				ТаблицаПолучателей = ПолучитьТаблицуПолучателейКомуКопияУВходящегоПисьма(РеквизитыПисьма.Ссылка);	
				Для Каждого Строка Из МассивСтрок Цикл
					Если ТаблицаПолучателей.Количество() = 1  Тогда
						СтрокаПолучатель = ТаблицаПолучателей[0];
						РезультатПроверкиУсловия = 
							СтрокаПолучатель.Адрес = РеквизитыПисьма.УчетнаяЗапись.АдресЭлектроннойПочты;
			
						Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
							Результат = Результат
								И (РезультатПроверкиУсловия И Строка.Параметр = Истина
								ИЛИ Не РезультатПроверкиУсловия И Строка.Параметр = Ложь);
						Иначе
							Результат = Результат
								ИЛИ (РезультатПроверкиУсловия И Строка.Параметр = Истина
								ИЛИ Не РезультатПроверкиУсловия И Строка.Параметр = Ложь);
						КонецЕсли;
					Иначе
						Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
							Результат = Результат И Строка.Параметр = Ложь;
						Иначе
							Результат = Результат ИЛИ Строка.Параметр = Ложь;
						КонецЕсли;	
					КонецЕсли;		
				КонецЦикла;
				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.ОтправительВходитВГруппы Тогда	
				
				РезультатПроверкиУсловия = Ложь;
				Запрос = Новый Запрос;
				Запрос.Текст = 
					"ВЫБРАТЬ РАЗРЕШЕННЫЕ
					|	СведенияОбАдресатах.Контакт
					|ИЗ
					|	РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
					|ГДЕ
					|	СведенияОбАдресатах.АдресатСообщения = &АдресатСообщения
					|	И ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Сотрудники)
					|	И СведенияОбАдресатах.Активна = ИСТИНА";
					
				Запрос.УстановитьПараметр("АдресатСообщения", РеквизитыПисьма.ОтправительАдресат);
				РезультатЗапроса = Запрос.Выполнить();
				Если Не РезультатЗапроса.Пустой() Тогда
					ВыборкаКонтакты = РезультатЗапроса.Выбрать();
					Пока ВыборкаКонтакты.Следующий() Цикл
						Для Каждого Строка Из МассивСтрок Цикл
							Группа = Строка.Параметр;
							СоставГруппы = ДокументооборотПраваДоступаПовтИсп.ПользователиРабочейГруппы(Группа);
							РезультатПроверкиУсловия = 
								РезультатПроверкиУсловия Или СоставГруппы.Найти(ВыборкаКонтакты.Контакт) <> Неопределено;
							Если РезультатПроверкиУсловия = Истина Тогда
								Прервать;
							КонецЕсли;
						КонецЦикла;
						Если РезультатПроверкиУсловия = Истина Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;		
				КонецЕсли;
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат ИЛИ РезультатПроверкиУсловия;
				КонецЕсли;
				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.ПолучательВходитВГруппы Тогда	
				
				РезультатПроверкиУсловия = Ложь;
	            ТаблицаПолучателей = ПолучитьТаблицуПолучателейКомуКопияУВходящегоПисьма(РеквизитыПисьма.Ссылка);
				Для Каждого СтрокаПолучатель Из ТаблицаПолучателей Цикл					
					Для Каждого Строка Из МассивСтрок Цикл
						Группа = Строка.Параметр;
						СоставГруппы = ДокументооборотПраваДоступаПовтИсп.ПользователиРабочейГруппы(Группа);
						РезультатПроверкиУсловия = 
							РезультатПроверкиУсловия Или СоставГруппы.Найти(СтрокаПолучатель.Контакт) <> Неопределено;
						Если РезультатПроверкиУсловия = Истина Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;
					Если РезультатПроверкиУсловия = Истина Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат ИЛИ РезультатПроверкиУсловия;
				КонецЕсли;
				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.АдресОтправителяВСписке Тогда	
				
				РезультатПроверкиУсловия = Ложь;
				
				СписокАдресовЭлектроннойПочты = Неопределено;
				Если МассивСтрок.Количество() = 1 Тогда
					СписокАдресовЭлектроннойПочты = МассивСтрок[0].Параметр;
				КонецЕсли;	
				
				Если ЗначениеЗаполнено(РеквизитыПисьма.ОтправительАдресат) Тогда
					АдресОтправителя = РеквизитыПисьма.ОтправительАдресат.Адрес;
				КонецЕсли;	
				
				Если ЗначениеЗаполнено(АдресОтправителя) И ЗначениеЗаполнено(СписокАдресовЭлектроннойПочты) Тогда
					
					Для Каждого Строка Из СписокАдресовЭлектроннойПочты.Адреса Цикл
						
						Если НРег(Строка.Адрес) = НРег(НСтр("ru='Все внутренние адресаты'")) Тогда
							
							Запрос = Новый Запрос;
							Запрос.Текст = 
								"ВЫБРАТЬ РАЗРЕШЕННЫЕ
								|	СведенияОбАдресатах.Контакт
								|ИЗ
								|	РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
								|ГДЕ
								|	СведенияОбАдресатах.АдресатСообщения = &АдресатСообщения
								|	И (ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.РолиИсполнителей)
								|	ИЛИ ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Сотрудники))
								|	И СведенияОбАдресатах.Активна = ИСТИНА";
								
							Запрос.УстановитьПараметр("АдресатСообщения", РеквизитыПисьма.ОтправительАдресат);
							РезультатЗапроса = Запрос.Выполнить();
							Если Не РезультатЗапроса.Пустой() Тогда
								РезультатПроверкиУсловия = Истина;
								Прервать;
							КонецЕсли;		
							
						Иначе
							
							Если СтрНайти(НРег(АдресОтправителя), НРег(Строка.Адрес)) <> 0 Тогда
								РезультатПроверкиУсловия = Истина;
								Прервать;
							КонецЕсли;	
							
						КонецЕсли;	
						
					КонецЦикла;	
					
				КонецЕсли;	
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат ИЛИ РезультатПроверкиУсловия;
				КонецЕсли;
				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.БылНаправленОтвет Тогда	
				
				БылНаправленОтвет = СвязиОбъектов.ПолучитьСвязанныйОбъект(
					РеквизитыПисьма.Ссылка, Справочники.ТипыСвязей.ОтправленоОтветноеПисьмо);
				Для Каждого Строка Из МассивСтрок Цикл
					Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
						Результат = Результат 
							И (БылНаправленОтвет <> Неопределено И Строка.Параметр = Истина
							ИЛИ Не БылНаправленОтвет <> Неопределено И Строка.Параметр = Ложь);
					Иначе
						Результат = Результат 
							ИЛИ (БылНаправленОтвет <> Неопределено И Строка.Параметр = Истина
							ИЛИ Не БылНаправленОтвет <> Неопределено И Строка.Параметр = Ложь);
					КонецЕсли;
				КонецЦикла;
				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.БылоПереслано Тогда	
				
				БылоПереслано = СвязиОбъектов.ПолучитьСвязанныйОбъект(
					РеквизитыПисьма.Ссылка, Справочники.ТипыСвязей.ПересланоПисьмом);
				Для Каждого Строка Из МассивСтрок Цикл
					Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
						Результат = Результат 
							И (БылоПереслано <> Неопределено И Строка.Параметр = Истина
							ИЛИ Не БылоПереслано <> Неопределено И Строка.Параметр = Ложь);
					Иначе
						Результат = Результат 
							ИЛИ (БылоПереслано <> Неопределено И Строка.Параметр = Истина
							ИЛИ Не БылоПереслано <> Неопределено И Строка.Параметр = Ложь);
					КонецЕсли;
				КонецЦикла;	
				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.НаходитсяВПапке Тогда	
				
				РезультатПроверкиУсловия = Ложь;
				ПапкаПисьма = РеквизитыПисьма.Папка;
				Для Каждого Строка Из МассивСтрок Цикл
					РезультатПроверкиУсловия = Строка.Параметр = ПапкаПисьма;
					Если РезультатПроверкиУсловия = Истина Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат ИЛИ РезультатПроверкиУсловия;
				КонецЕсли;
				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.Проект Тогда	
				
				РезультатПроверкиУсловия = Ложь;
				ПроектПисьма = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыПисьма.Ссылка, "Проект");
				Для Каждого Строка Из МассивСтрок Цикл
					РезультатПроверкиУсловия = Строка.Параметр = ПроектПисьма;
					Если РезультатПроверкиУсловия = Истина Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат ИЛИ РезультатПроверкиУсловия;
				КонецЕсли;
				
			КонецЕсли;
			
			Если Результат = Ложь И Связь = Перечисления.ТипыЛогическойСвязи.И
				ИЛИ Результат = Истина И Связь = Перечисления.ТипыЛогическойСвязи.Или Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ВыполнитьДействияПравилаДляВходящих(ПисьмоОбъект, Правило, ТаблицаПараметров, ДействияВыполнялись, НеобходимоЗаписатьПисьмо) Экспорт
	
	Если ТаблицаПараметров.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Для Каждого ЗначениеПеречисления Из Перечисления.ВидыДействийПриОбработкеВходящихПисем Цикл
		
		Отбор = Новый Структура("Вид", ЗначениеПеречисления);
		МассивСтрок = ТаблицаПараметров.НайтиСтроки(Отбор);
		Если МассивСтрок.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если ЗначениеПеречисления = Перечисления.ВидыДействийПриОбработкеВходящихПисем.ПереместитьВУказаннуюПапку Тогда
			
			Для Каждого Строка Из МассивСтрок Цикл
				Если Не Строка.Параметр.ПометкаУдаления И ПисьмоОбъект.Папка <> Строка.Параметр Тогда
					ПисьмоОбъект.Папка = Строка.Параметр;
					ДействияВыполнялись = Истина;
					НеобходимоЗаписатьПисьмо = Истина;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыДействийПриОбработкеВходящихПисем.УстановитьУказанныйФлаг Тогда			
			
			Для Каждого Строка Из МассивСтрок Цикл
				Для каждого Ответственный Из ПисьмоОбъект.УчетнаяЗапись.ОтветственныеЗаОбработкуПисем Цикл
					
					АвторФлага = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ответственный.Сотрудник, "Владелец");
					
					РегистрыСведений.ФлагиОбъектов.УстановитьФлаг(
						ПисьмоОбъект.Ссылка, 
						АвторФлага,
						Строка.Параметр);
					ДействияВыполнялись = Истина;	
				КонецЦикла;
			КонецЦикла;
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыДействийПриОбработкеВходящихПисем.УстановитьПометкуПрочтения Тогда
			
			Для Каждого Строка Из МассивСтрок Цикл
				Для каждого Ответственный Из ПисьмоОбъект.УчетнаяЗапись.ОтветственныеЗаОбработкуПисем Цикл
						
					Пользователь = Сотрудники.ЛюбойПользовательСотрудника(Ответственный.Сотрудник);
					Прочтен = Истина;
					РаботаСПрочтениями.УстановитьСвойствоПрочтенОбъект(ПисьмоОбъект.Ссылка, Прочтен, Ложь, Пользователь);
						
					ДействияВыполнялись = Истина;	
				КонецЦикла;
			КонецЦикла;
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыДействийПриОбработкеВходящихПисем.ОтветитьСУказаннымТекстом Тогда
			
			Значение = ПолучитьЗначениеПоляИзЗаголовкаПисьма(ПисьмоОбъект.ВнутреннийЗаголовок, "X-1C-AutoReply");
			Если Значение = "true" Тогда
				Продолжить;
			КонецЕсли;
			
			// на массовые рассылки не отвечаем.
			Если ВстроеннаяПочтаКлиентСервер.ЭтоРассылка(
				ПисьмоОбъект.ПолучателиПисьма.Количество() + ПисьмоОбъект.ПолучателиКопий.Количество()) Тогда
				Продолжить;
			КонецЕсли;
			
			// самому себе не отвечаем
			АдресОтправителя = НРег(ПисьмоОбъект.ОтправительАдресат.Адрес);
			Если АдресОтправителя = НРег(Правило.УчетнаяЗапись.АдресЭлектроннойПочты) Тогда
				Продолжить;
			КонецЕсли;
			
			АдресСистемнойУчетнойЗаписи = ПолучитьАдресСистемнойУчетнойЗаписи();
			Если АдресОтправителя = НРег(АдресСистемнойУчетнойЗаписи) Тогда
				Продолжить; // не отвечаем на письма уведомления от нашей системной учетной записи
			КонецЕсли;
			
			// на noreply***** не отвечаем
			Если СтрНайти(НРег(АдресОтправителя), НРег("noreply")) <> 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если РегистрыСведений.АвтоматическиеОтветыПоАдресам.ЕстьЗаписиПоПравилуИАдресу(
					Правило,
					АдресОтправителя) Тогда
				Продолжить;
			КонецЕсли;
			
			Для Каждого Строка Из МассивСтрок Цикл
				ИсходящееПисьмо = Документы.ИсходящееПисьмо.СоздатьДокумент();
				ЗначенияЗаполнения = Новый Структура;
				ЗначенияЗаполнения.Вставить("Команда", "Ответить");
				ЗначенияЗаполнения.Вставить("Письмо", ПисьмоОбъект.Ссылка);
				
				ИсходящееПисьмо.Заполнить(ЗначенияЗаполнения);

				ИсходящееПисьмо.УстановитьСодержаниеПисьма(Строка.ТекстАвтоответа);
				ИсходящееПисьмо.УчетнаяЗапись = ПисьмоОбъект.УчетнаяЗапись;
				
				ИсходящееПисьмо.ПодготовленоКОтправке = ТекущаяДатаСеанса();
				ИсходящееПисьмо.ДополнительныеСвойства.Вставить("ВыполняетсяОтправка", Истина);
				
				ИсходящееПисьмо.Записать();
									
				СвязиОбъектов.УстановитьСвязь(
					ИсходящееПисьмо.Ссылка,
					Неопределено,
					ПисьмоОбъект.Ссылка,
					Справочники.ТипыСвязей.ПисьмоОтправленоВОтветНа);
					
				РегистрыСведений.АвтоматическиеОтветыПоАдресам.ДобавитьЗапись(
					Правило,
					АдресОтправителя,
					ИсходящееПисьмо.Ссылка);						
				
				ДействияВыполнялись = Истина;
			КонецЦикла;
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыДействийПриОбработкеВходящихПисем.ПометитьНаУдаление Тогда
			
			ПисьмоОбъект.ДополнительныеСвойства.Вставить("Правила_УстановитьПометкуУдаления", Истина);
			ДействияВыполнялись = Истина;
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыДействийПриОбработкеВходящихПисем.УстановитьУказанныйПроект Тогда
			УстановитьПривилегированныйРежим(Истина);
			Для Каждого Строка Из МассивСтрок Цикл
				Если Не Строка.Параметр.ПометкаУдаления И ПисьмоОбъект.Проект <> Строка.Параметр Тогда
					ПисьмоОбъект.Проект = Строка.Параметр;
					ДействияВыполнялись = Истина;
					НеобходимоЗаписатьПисьмо = Истина;
				КонецЕсли;
			КонецЦикла;
			УстановитьПривилегированныйРежим(Ложь);
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыДействийПриОбработкеВходящихПисем.ОстановитьПроверкуДругихПравил Тогда
			
			Возврат Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция ПроверитьУсловияПравилаДляИсходящих(ПисьмоОбъектСсылка, Правило, ТаблицаПараметров, 
	РеквизитыПисьма, Исключения, ДополнительныеСвойства) Экспорт
			
	Результат = Не Исключения;
	ПервоеСрабатываниеУсловия = Истина;
	
	Если Исключения = Ложь Тогда
		Связь = Перечисления.ТипыЛогическойСвязи.И;	
	Иначе
		Связь = Перечисления.ТипыЛогическойСвязи.Или;
	КонецЕсли;
			
	Для Каждого ЗначениеПеречисления Из Перечисления.ВидыУсловийОтбораИсходящихПисем Цикл
		
		Отбор = Новый Структура("Вид, ЭтоЗначенияИсключения", ЗначениеПеречисления, Исключения);
		МассивСтрок = ТаблицаПараметров.НайтиСтроки(Отбор);
		Если МассивСтрок.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПервоеСрабатываниеУсловия Тогда
			Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
				Результат = Истина;
			Иначе 
				Результат = Ложь;
			КонецЕсли;
			ПервоеСрабатываниеУсловия = Ложь;
		КонецЕсли;

		Если ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораИсходящихПисем.ТемаСодержитУказанныеСлова Тогда
			
			РезультатПроверкиУсловия = Ложь;
			Для Каждого Строка Из МассивСтрок Цикл
				РезультатПроверкиУсловия = РезультатПроверкиУсловия 
					ИЛИ СтрНайти(НРег(РеквизитыПисьма.Тема), НРег(Строка.Параметр)) > 0;
			КонецЦикла;
				
			Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
				Результат = Результат И РезультатПроверкиУсловия;
			ИначеЕсли Связь = Перечисления.ТипыЛогическойСвязи.Или Тогда
				Результат = Результат Или РезультатПроверкиУсловия;
			КонецЕсли;
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораИсходящихПисем.ТемаНачинаетсяС Тогда
			
			РезультатПроверкиУсловия = Ложь;
			Для Каждого Строка Из МассивСтрок Цикл
				РезультатПроверкиУсловия = РезультатПроверкиУсловия 
					ИЛИ СтрНайти(НРег(РеквизитыПисьма.Тема), НРег(Строка.Параметр)) = 1;
			КонецЦикла;
			
			Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
				Результат = Результат И РезультатПроверкиУсловия;
			Иначе
				Результат = Результат Или РезультатПроверкиУсловия;
			КонецЕсли;	
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораИсходящихПисем.ОтправленоНаУказанныеАдреса Тогда
			
			РезультатПроверкиУсловия = Ложь;
			
			Если ЗначениеЗаполнено(РеквизитыПисьма.Ссылка) Тогда
				ТаблицаПолучатели = ПолучитьТаблицуПолучателейКомуКопияСкрытаяУИсходящегоПисьма(РеквизитыПисьма.Ссылка);
			Иначе
				МассивАдресатов = Новый Массив;
				Для Каждого СтрокаАдресат Из РеквизитыПисьма.ПолучателиПисьма Цикл
					Если МассивАдресатов.Найти(СтрокаАдресат.Адресат) = Неопределено Тогда
						МассивАдресатов.Добавить(СтрокаАдресат.Адресат);
					КонецЕсли;
				КонецЦикла;
				Для Каждого СтрокаАдресат Из РеквизитыПисьма.ПолучателиКопий Цикл
					Если МассивАдресатов.Найти(СтрокаАдресат.Адресат) = Неопределено Тогда
						МассивАдресатов.Добавить(СтрокаАдресат.Адресат);
					КонецЕсли;
				КонецЦикла;
				Для Каждого СтрокаАдресат Из РеквизитыПисьма.ПолучателиСкрытыхКопий Цикл
					Если МассивАдресатов.Найти(СтрокаАдресат.Адресат) = Неопределено Тогда
						МассивАдресатов.Добавить(СтрокаАдресат.Адресат);
					КонецЕсли;
				КонецЦикла;
				
				ДанныеАдресатов = ПолучитьСоответствиеСПредставлениямиИКонтактамиМассиваАдресатов(МассивАдресатов);
				ТаблицаПолучатели = Новый ТаблицаЗначений;
				ТаблицаПолучатели.Колонки.Добавить("Адрес");
				ТаблицаПолучатели.Колонки.Добавить("Наименование");
				ТаблицаПолучатели.Колонки.Добавить("Представление");
				
				Для Каждого КлючЗначение Из ДанныеАдресатов Цикл
					НоваяСтрока = ТаблицаПолучатели.Добавить();
					НоваяСтрока.Адрес = КлючЗначение.Значение.Адрес;
					НоваяСтрока.Наименование = КлючЗначение.Значение.Наименование;
					НоваяСтрока.Представление = КлючЗначение.Значение.Представление;
				КонецЦикла;
			КонецЕсли;
			
			Для Каждого Строка Из МассивСтрок Цикл
				Для Каждого СтрокаПолучатель Из ТаблицаПолучатели Цикл
					
					РезультатПроверкиУсловия = РезультатПроверкиУсловия
						ИЛИ СтрНайти(НРег(СтрокаПолучатель.Адрес), НРег(Строка.Параметр)) > 0
						ИЛИ СтрНайти(НРег(СтрокаПолучатель.Наименование), НРег(Строка.Параметр)) > 0
						ИЛИ СтрНайти(НРег(СтрокаПолучатель.Представление), НРег(Строка.Параметр)) > 0;
						
					Если РезультатПроверкиУсловия Тогда
						Прервать;
					КонецЕсли;	
				КонецЦикла;
				
				Если РезультатПроверкиУсловия Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
				Результат = Результат И РезультатПроверкиУсловия;
			ИначеЕсли Связь = Перечисления.ТипыЛогическойСвязи.Или Тогда
				Результат = Результат Или РезультатПроверкиУсловия;
			КонецЕсли;	
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораИсходящихПисем.ТолькоДляПользователей Тогда	
			
			РезультатПроверкиУсловия = Ложь;
			ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
			Для Каждого Строка Из МассивСтрок Цикл
				РезультатПроверкиУсловия = РезультатПроверкиУсловия 
					ИЛИ Строка.Параметр = ТекущийПользователь;
			КонецЦикла;
			Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
				Результат = Результат И РезультатПроверкиУсловия;
			ИначеЕсли Связь = Перечисления.ТипыЛогическойСвязи.Или Тогда
				Результат = Результат Или РезультатПроверкиУсловия;
			КонецЕсли;	
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораИсходящихПисем.ПересылаетВходящееПисьмо 
			ИЛИ ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораИсходящихПисем.ПересылаетИсходящееПисьмо Тогда
		
			Для Каждого Строка Из МассивСтрок Цикл
				СвязанныйДокумент = РеквизитыПисьма.ПисьмоОснование;
				Если РеквизитыПисьма.ТипОтвета <> Перечисления.ТипыОтвета.ПересылкаПисьма Тогда
					СвязанныйДокумент = Неопределено;
				КонецЕсли;
					
				РезультатПроверкиУсловия = ЗначениеЗаполнено(СвязанныйДокумент)
					И (ТипЗнч(СвязанныйДокумент) = Тип("ДокументСсылка.ВходящееПисьмо") 
						И ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораИсходящихПисем.ПересылаетВходящееПисьмо
					ИЛИ ТипЗнч(СвязанныйДокумент) = Тип("ДокументСсылка.ИсходящееПисьмо") 
						И ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораИсходящихПисем.ПересылаетИсходящееПисьмо);					
						
				РезультатПроверкиУсловия = 
					РезультатПроверкиУсловия И Строка.Параметр = Истина
					ИЛИ Не РезультатПроверкиУсловия И Строка.Параметр = Ложь;	
						
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				ИначеЕсли Связь = Перечисления.ТипыЛогическойСвязи.Или Тогда
					Результат = Результат Или РезультатПроверкиУсловия;
				КонецЕсли;
				
				Если Результат Тогда
					ДополнительныеСвойства.Вставить("Пересылает", Не Исключения);			
				КонецЕсли;
				
			КонецЦикла;		
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораИсходящихПисем.ЯвляетсяОтветомНаПисьмо Тогда
						
			Для Каждого Строка Из МассивСтрок Цикл
				СвязанныйДокумент = РеквизитыПисьма.ПисьмоОснование;
				Если РеквизитыПисьма.ТипОтвета <> Перечисления.ТипыОтвета.ОтветНаПисьмо Тогда
					СвязанныйДокумент = Неопределено;
				КонецЕсли;

				РезультатПроверкиУсловия = ЗначениеЗаполнено(СвязанныйДокумент);					
				
				РезультатПроверкиУсловия = 
					РезультатПроверкиУсловия И Строка.Параметр = Истина
					ИЛИ Не РезультатПроверкиУсловия И Строка.Параметр = Ложь;
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И 
						(РезультатПроверкиУсловия И Строка.Параметр = Истина
						ИЛИ Не РезультатПроверкиУсловия И Строка.Параметр = Ложь);
				ИначеЕсли Связь = Перечисления.ТипыЛогическойСвязи.Или Тогда
					Результат = Результат ИЛИ 
						(РезультатПроверкиУсловия И Строка.Параметр = Истина
						ИЛИ Не РезультатПроверкиУсловия И Строка.Параметр = Ложь);
				КонецЕсли;
					
				Если Результат Тогда
					ДополнительныеСвойства.Вставить("Ответ", Не Исключения);			
				КонецЕсли;
				
			КонецЦикла;
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораИсходящихПисем.ПолучательВходитВГруппы Тогда	
			
			РезультатПроверкиУсловия = Ложь;
			
			Если ЗначениеЗаполнено(РеквизитыПисьма.Ссылка) Тогда
				ТаблицаПолучатели = ПолучитьТаблицуПолучателейКомуКопияСкрытаяУИсходящегоПисьма(РеквизитыПисьма.Ссылка);
			Иначе
				МассивАдресатов = Новый Массив;
				Для Каждого СтрокаАдресат Из РеквизитыПисьма.ПолучателиПисьма Цикл
					Если МассивАдресатов.Найти(СтрокаАдресат.Адресат) = Неопределено Тогда
						МассивАдресатов.Добавить(СтрокаАдресат.Адресат);
					КонецЕсли;
				КонецЦикла;
				Для Каждого СтрокаАдресат Из РеквизитыПисьма.ПолучателиКопий Цикл
					Если МассивАдресатов.Найти(СтрокаАдресат.Адресат) = Неопределено Тогда
						МассивАдресатов.Добавить(СтрокаАдресат.Адресат);
					КонецЕсли;
				КонецЦикла;
				Для Каждого СтрокаАдресат Из РеквизитыПисьма.ПолучателиСкрытыхКопий Цикл
					Если МассивАдресатов.Найти(СтрокаАдресат.Адресат) = Неопределено Тогда
						МассивАдресатов.Добавить(СтрокаАдресат.Адресат);
					КонецЕсли;
				КонецЦикла;
				
				ДанныеАдресатов = ПолучитьСоответствиеСПредставлениямиИКонтактамиМассиваАдресатов(МассивАдресатов);
				ТаблицаПолучатели = Новый ТаблицаЗначений;
				ТаблицаПолучатели.Колонки.Добавить("Адрес");
				ТаблицаПолучатели.Колонки.Добавить("Наименование");
				ТаблицаПолучатели.Колонки.Добавить("Представление");
				ТаблицаПолучатели.Колонки.Добавить("Контакт");
				
				Для Каждого КлючЗначение Из ДанныеАдресатов Цикл
					НоваяСтрока = ТаблицаПолучатели.Добавить();
					НоваяСтрока.Адрес = КлючЗначение.Значение.Адрес;
					НоваяСтрока.Наименование = КлючЗначение.Значение.Наименование;
					НоваяСтрока.Представление = КлючЗначение.Значение.Представление;
					НоваяСтрока.Контакт = КлючЗначение.Значение.Контакт;
				КонецЦикла;
			КонецЕсли;
			
			Для Каждого СтрокаПолучатель Из ТаблицаПолучатели Цикл					
				Для Каждого Строка Из МассивСтрок Цикл
					Группа = Строка.Параметр;
					СоставГруппы = ДокументооборотПраваДоступаПовтИсп.ПользователиРабочейГруппы(Группа);
					РезультатПроверкиУсловия = 
						РезультатПроверкиУсловия Или СоставГруппы.Найти(СтрокаПолучатель.Контакт) <> Неопределено;
					Если РезультатПроверкиУсловия = Истина Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если РезультатПроверкиУсловия = Истина Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
				Результат = Результат И РезультатПроверкиУсловия;
			Иначе
				Результат = Результат ИЛИ РезультатПроверкиУсловия;
			КонецЕсли;	
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораИсходящихПисем.БылНаправленОтвет Тогда	
				
			БылНаправленОтвет = СвязиОбъектов.ПолучитьСвязанныйОбъект(
				РеквизитыПисьма.Ссылка, Справочники.ТипыСвязей.ПисьмоОтправленоВОтветНа);
			Для Каждого Строка Из МассивСтрок Цикл
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат 
						И (БылНаправленОтвет <> Неопределено И Строка.Параметр = Истина
						ИЛИ Не БылНаправленОтвет <> Неопределено И Строка.Параметр = Ложь);
				Иначе
					Результат = Результат 
						ИЛИ (БылНаправленОтвет <> Неопределено И Строка.Параметр = Истина
						ИЛИ Не БылНаправленОтвет <> Неопределено И Строка.Параметр = Ложь);
				КонецЕсли;
			КонецЦикла;
				
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораИсходящихПисем.БылоПереслано Тогда	
			
			БылоПереслано = СвязиОбъектов.ПолучитьСвязанныйОбъект(
				РеквизитыПисьма.Ссылка, Справочники.ТипыСвязей.ПересылкаПисьма);
			Для Каждого Строка Из МассивСтрок Цикл
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат 
						И (БылоПереслано <> Неопределено И Строка.Параметр = Истина
						ИЛИ Не БылоПереслано <> Неопределено И Строка.Параметр = Ложь);
				Иначе
					Результат = Результат 
						ИЛИ (БылоПереслано <> Неопределено И Строка.Параметр = Истина
						ИЛИ Не БылоПереслано <> Неопределено И Строка.Параметр = Ложь);
				КонецЕсли;
			КонецЦикла;	
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораИсходящихПисем.Проект Тогда	
				
				РезультатПроверкиУсловия = Ложь;
				ПроектПисьма = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыПисьма.Ссылка, "Проект");
				Для Каждого Строка Из МассивСтрок Цикл
					РезультатПроверкиУсловия = Строка.Параметр = ПроектПисьма;
					Если РезультатПроверкиУсловия = Истина Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат ИЛИ РезультатПроверкиУсловия;
				КонецЕсли;
			
		КонецЕсли;
		
		Если Результат = Ложь И Связь = Перечисления.ТипыЛогическойСвязи.И
			ИЛИ Результат = Истина И Связь = Перечисления.ТипыЛогическойСвязи.Или Тогда
			Прервать;
		КонецЕсли;
	
	КонецЦикла;

	
	Возврат Результат;
	
КонецФункции

Функция ВыполнитьДействияПравилаДляИсходящих(ПисьмоОбъект, Правило, ТаблицаПараметров, 
	ПрименятьДействияНаОтправляемоеПисьмо = Истина, ДействияВыполнялись, НеобходимоЗаписатьПисьмо) Экспорт
	
	Если ТаблицаПараметров.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Для Каждого ЗначениеПеречисления Из Перечисления.ВидыДействийПриОбработкеИсходящихПисем Цикл
		
		Отбор = Новый Структура("Вид", ЗначениеПеречисления);
		МассивСтрок = ТаблицаПараметров.НайтиСтроки(Отбор);
		Если МассивСтрок.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;		 
		
		Если ЗначениеПеречисления = Перечисления.ВидыДействийПриОбработкеИсходящихПисем.ПереместитьВУказаннуюПапку
			И ПрименятьДействияНаОтправляемоеПисьмо Тогда
			
			Для Каждого Строка Из МассивСтрок Цикл
				Если Не Строка.Параметр.ПометкаУдаления И ПисьмоОбъект.Папка <> Строка.Параметр Тогда
					ПисьмоОбъект.Папка = Строка.Параметр;
					ДействияВыполнялись = Истина;
					НеобходимоЗаписатьПисьмо = Истина;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыДействийПриОбработкеИсходящихПисем.УстановитьУказанныйФлаг 
			И ПрименятьДействияНаОтправляемоеПисьмо Тогда			
			
			Для Каждого Строка Из МассивСтрок Цикл
				Для каждого Ответственный Из ПисьмоОбъект.УчетнаяЗапись.ОтветственныеЗаОбработкуПисем Цикл
					
					АвторФлага = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ответственный.Сотрудник, "Владелец");
					
					РегистрыСведений.ФлагиОбъектов.УстановитьФлаг(
						ПисьмоОбъект.Ссылка, 
						АвторФлага, 
						Строка.Параметр);
					ДействияВыполнялись = Истина;
				КонецЦикла;
			КонецЦикла;
				
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыДействийПриОбработкеИсходящихПисем.ПоместитьИсходноеПисьмоВУказаннуюПапку Тогда
			
			Для Каждого Строка Из МассивСтрок Цикл 
				Если Строка.Параметр.ПометкаУдаления Тогда
					Продолжить;
				КонецЕсли;
				СвязанныйДокумент = Неопределено;
				СвязанныйДокументОтвет = Неопределено;
				СвязанныйДокументПересылка = Неопределено;
				Если Не ПисьмоОбъект.ДополнительныеСвойства.Свойство("Ответ") Тогда
					СвязанныйДокументОтвет = ПисьмоОбъект.ПисьмоОснование;
					Если ПисьмоОбъект.ТипОтвета <> Перечисления.ТипыОтвета.ОтветНаПисьмо Тогда
						СвязанныйДокументОтвет = Неопределено;
					КонецЕсли;					
				КонецЕсли;
						
				Если Не ПисьмоОбъект.ДополнительныеСвойства.Свойство("Пересылает") Тогда
					СвязанныйДокументПересылка = ПисьмоОбъект.ПисьмоОснование;
					Если ПисьмоОбъект.ТипОтвета <> Перечисления.ТипыОтвета.ПересылкаПисьма Тогда
						СвязанныйДокументПересылка = Неопределено;
					КонецЕсли;
				КонецЕсли;
					
				СвязанныйДокумент = 
					?(ЗначениеЗаполнено(СвязанныйДокументОтвет), СвязанныйДокументОтвет,
						?(ЗначениеЗаполнено(СвязанныйДокументПересылка), СвязанныйДокументПересылка, Неопределено));
						
				Если ПисьмоОбъект.ДополнительныеСвойства.Свойство("Пересылает")
					И ПисьмоОбъект.ДополнительныеСвойства.Пересылает Тогда
					
					СвязанныйДокумент = ПисьмоОбъект.ПисьмоОснование;
					Если ПисьмоОбъект.ТипОтвета <> Перечисления.ТипыОтвета.ПересылкаПисьма Тогда
						СвязанныйДокумент = Неопределено;
					КонецЕсли;
				КонецЕсли;
					
				Если ПисьмоОбъект.ДополнительныеСвойства.Свойство("Ответ")
					И ПисьмоОбъект.ДополнительныеСвойства.Ответ Тогда 						
					СвязанныйДокумент = ПисьмоОбъект.ПисьмоОснование;
					Если ПисьмоОбъект.ТипОтвета <> Перечисления.ТипыОтвета.ОтветНаПисьмо Тогда
						СвязанныйДокумент = Неопределено;
					КонецЕсли;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СвязанныйДокумент) Тогда
					ЗаблокироватьДанныеДляРедактирования(СвязанныйДокумент);	
					ИсходноеПисьмоОбъект = СвязанныйДокумент.ПолучитьОбъект();
					Если ИсходноеПисьмоОбъект.Папка <> Строка.Параметр Тогда
						ИсходноеПисьмоОбъект.Папка = Строка.Параметр;
						ИсходноеПисьмоОбъект.Записать();
						РазблокироватьДанныеДляРедактирования(СвязанныйДокумент);
						ДействияВыполнялись = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;	
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыДействийПриОбработкеИсходящихПисем.УстановитьУказанныйПроект Тогда
			УстановитьПривилегированныйРежим(Истина);
			Для Каждого Строка Из МассивСтрок Цикл
				Если Не Строка.Параметр.ПометкаУдаления И ПисьмоОбъект.Проект <> Строка.Параметр Тогда
					ПисьмоОбъект.Проект = Строка.Параметр;
					ДействияВыполнялись = Истина;
					НеобходимоЗаписатьПисьмо = Истина;
				КонецЕсли;
			КонецЦикла;
			УстановитьПривилегированныйРежим(Ложь);
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыДействийПриОбработкеИсходящихПисем.ПрименитьКИсходномуПисьмуПравила Тогда
			 
			СвязанныйДокумент = Неопределено;
			СвязанныйДокументОтвет = Неопределено;
			СвязанныйДокументПересылка = Неопределено;
			Если Не ПисьмоОбъект.ДополнительныеСвойства.Свойство("Ответ") Тогда
				СвязанныйДокументОтвет = ПисьмоОбъект.ПисьмоОснование;
				Если ПисьмоОбъект.ТипОтвета <> Перечисления.ТипыОтвета.ОтветНаПисьмо Тогда
					СвязанныйДокументОтвет = Неопределено;
				КонецЕсли;					
			КонецЕсли;
					
			Если Не ПисьмоОбъект.ДополнительныеСвойства.Свойство("Пересылает") Тогда
				СвязанныйДокументПересылка = ПисьмоОбъект.ПисьмоОснование;
				Если ПисьмоОбъект.ТипОтвета <> Перечисления.ТипыОтвета.ПересылкаПисьма Тогда
					СвязанныйДокументПересылка = Неопределено;
				КонецЕсли;
			КонецЕсли;
				
			СвязанныйДокумент = 
				?(ЗначениеЗаполнено(СвязанныйДокументОтвет), СвязанныйДокументОтвет,
					?(ЗначениеЗаполнено(СвязанныйДокументПересылка), СвязанныйДокументПересылка, Неопределено));
					
			Если ПисьмоОбъект.ДополнительныеСвойства.Свойство("Пересылает")
				И ПисьмоОбъект.ДополнительныеСвойства.Пересылает Тогда
				
				СвязанныйДокумент = ПисьмоОбъект.ПисьмоОснование;
				Если ПисьмоОбъект.ТипОтвета <> Перечисления.ТипыОтвета.ПересылкаПисьма Тогда
					СвязанныйДокумент = Неопределено;
				КонецЕсли;
			КонецЕсли;
				
			Если ПисьмоОбъект.ДополнительныеСвойства.Свойство("Ответ")
				И ПисьмоОбъект.ДополнительныеСвойства.Ответ Тогда 						
				СвязанныйДокумент = ПисьмоОбъект.ПисьмоОснование;
				Если ПисьмоОбъект.ТипОтвета <> Перечисления.ТипыОтвета.ОтветНаПисьмо Тогда
					СвязанныйДокумент = Неопределено;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СвязанныйДокумент) Тогда
					
				Для Каждого Строка Из МассивСтрок Цикл
					Если Строка.Параметр.ПометкаУдаления Тогда
						Продолжить;
					КонецЕсли;
					Если РаботаСПравиламиОбработкиПисем.ПрименитьПравилоКПисьму(Строка.Параметр, СвязанныйДокумент) Тогда					
						НаборЗаписей = РегистрыСведений.ЗначенияДействийПриОбработкеПисем.СоздатьНаборЗаписей();
						НаборЗаписей.Отбор.Правило.Установить(Строка.Параметр);
						НаборЗаписей.Отбор.ВидДействия.Установить(Перечисления.ВидыДействийПриОбработкеИсходящихПисем.ОстановитьПроверкуДругихПравил);
						НаборЗаписей.Прочитать();
						НеобходимоПрервать = Ложь;
						Для Каждого Запись Из НаборЗаписей Цикл
							НеобходимоПрервать = Запись.ПараметрДействия;								
							Если НеобходимоПрервать Тогда
								Прервать;
							КонецЕсли;
						КонецЦикла;
						Если НеобходимоПрервать Тогда
							Прервать;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;	
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыДействийПриОбработкеИсходящихПисем.ПереместитьВПапкуИсходногоПисьма 
			И ПрименятьДействияНаОтправляемоеПисьмо Тогда
			
			СвязанныйДокумент = Неопределено;
			СвязанныйДокументОтвет = Неопределено;
			СвязанныйДокументПересылка = Неопределено;
			Если Не ПисьмоОбъект.ДополнительныеСвойства.Свойство("Ответ") Тогда
				СвязанныйДокументОтвет = ПисьмоОбъект.ПисьмоОснование;
				Если ПисьмоОбъект.ТипОтвета <> Перечисления.ТипыОтвета.ОтветНаПисьмо Тогда
					СвязанныйДокументОтвет = Неопределено;
				КонецЕсли;					
			КонецЕсли;
					
			Если Не ПисьмоОбъект.ДополнительныеСвойства.Свойство("Пересылает") Тогда
				СвязанныйДокументПересылка = ПисьмоОбъект.ПисьмоОснование;
				Если ПисьмоОбъект.ТипОтвета <> Перечисления.ТипыОтвета.ПересылкаПисьма Тогда
					СвязанныйДокументПересылка = Неопределено;
				КонецЕсли;
			КонецЕсли;
				
			СвязанныйДокумент = 
				?(ЗначениеЗаполнено(СвязанныйДокументОтвет), СвязанныйДокументОтвет,
					?(ЗначениеЗаполнено(СвязанныйДокументПересылка), СвязанныйДокументПересылка, Неопределено));
					
			Если ПисьмоОбъект.ДополнительныеСвойства.Свойство("Пересылает")
				И ПисьмоОбъект.ДополнительныеСвойства.Пересылает Тогда
				
				СвязанныйДокумент = ПисьмоОбъект.ПисьмоОснование;
				Если ПисьмоОбъект.ТипОтвета <> Перечисления.ТипыОтвета.ПересылкаПисьма Тогда
					СвязанныйДокумент = Неопределено;
				КонецЕсли;
			КонецЕсли;
				
			Если ПисьмоОбъект.ДополнительныеСвойства.Свойство("Ответ")
				И ПисьмоОбъект.ДополнительныеСвойства.Ответ Тогда 						
				СвязанныйДокумент = ПисьмоОбъект.ПисьмоОснование;
				Если ПисьмоОбъект.ТипОтвета <> Перечисления.ТипыОтвета.ОтветНаПисьмо Тогда
					СвязанныйДокумент = Неопределено;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СвязанныйДокумент) Тогда
				Папка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СвязанныйДокумент, "Папка");
				Если Не Папка.ПометкаУдаления И ПисьмоОбъект.Папка <> Папка Тогда
					ПисьмоОбъект.Папка = Папка;
					ДействияВыполнялись = Истина;
					НеобходимоЗаписатьПисьмо = Истина;
				КонецЕсли;

			КонецЕсли;
			
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыДействийПриОбработкеИсходящихПисем.ОстановитьПроверкуДругихПравил Тогда
			
			Возврат Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

/////////////////////////////////////////////////////////////////////////////////////////
// РАБОТА С ПИСЬМАМИ

// Возвращает представление письма.
//
Функция ПолучитьПредставлениеПисьма(ПисьмоОбъект) Экспорт
	
	Если ПустаяСтрока(ПисьмоОбъект.Тема) Тогда
		Тема = НСтр("ru = 'без темы'");
	Иначе
		Тема = ПисьмоОбъект.Тема;
	КонецЕсли;
	
	Представление = "";
	Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(ПисьмоОбъект.Ссылка) Тогда
		ВидПисьма = НСтр("ru = 'Входящее письмо'");
	ИначеЕсли ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(ПисьмоОбъект.Ссылка) Тогда
		ВидПисьма = НСтр("ru = 'Исходящее письмо'");
	КонецЕсли;
	
	Если ПисьмоОбъект.Ссылка.Пустая() Тогда
		Представление = ВидПисьма;
		ДобавитьЗначениеКСтрокеЧерезРазделитель(
			Представление,
			" ",
			ВКавычках(НСтр("ru = 'создание'"),"(",")"))
	Иначе
		Представление = Тема;
		ДобавитьЗначениеКСтрокеЧерезРазделитель(
			Представление,
			" ",
			ВКавычках(ВидПисьма,"(",")"))
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

// Помещает письма в указанную папку писем.
// Возвращает результат переноса в виде структуры:
// - НеПеренесенныеПисьма (Массив)
//
Функция ПоместитьПисьмаВПапку(МассивПисем, Папка) Экспорт
	
	РезультатПереноса = Новый Структура("НеПеренесенныеПисьма, Ошибки", Новый Массив, Новый Массив);
	
	Для каждого Письмо Из МассивПисем Цикл
		СообщениеОбОшибке = "";
		Если Не ПоместитьПисьмоВПапку(Письмо, Папка, СообщениеОбОшибке) Тогда
			
			НеПеренесенноеПисьмо = Новый Структура("Письмо, СообщениеОбОшибке");
			НеПеренесенноеПисьмо.Письмо = Письмо;
			НеПеренесенноеПисьмо.СообщениеОбОшибке = СообщениеОбОшибке;
			РезультатПереноса.НеПеренесенныеПисьма.Добавить(НеПеренесенноеПисьмо);
			РезультатПереноса.Ошибки.Добавить(СообщениеОбОшибке);
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Перенос писем в папку.Ошибка'", Метаданные.ОсновнойЯзык.КодЯзыка),
				УровеньЖурналаРегистрации.Ошибка,, 
				Письмо,
				СообщениеОбОшибке);
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат РезультатПереноса;
	
КонецФункции

// Помещает письмо в указанную папку
Функция ПоместитьПисьмоВПапку(Письмо, Папка, СообщениеОбОшибке) Экспорт
	
	СообщениеОбОшибке = "";
	
	Попытка
		ПисьмоОбъект = Письмо.ПолучитьОбъект();
		ПисьмоОбъект.Заблокировать();
	Исключение
		СообщениеОбОшибке = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат Ложь;
	КонецПопытки;
	
	Если Не УстановитьПапкуПисьма(ПисьмоОбъект, Папка, СообщениеОбОшибке) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		ПисьмоОбъект.Записать();
	Исключение
		СообщениеОбОшибке = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

// Помещает письма в папку Корзина учетной записи.
// Возвращает результат переноса в виде структуры:
// - НеПеренесенныеПисьма (Массив)
//
Функция ПоместитьПисьмаВКорзину(МассивПисем) Экспорт
	
	РезультатПереноса = Новый Структура("НеПеренесенныеПисьма", Новый Массив);
	
	Для каждого Письмо Из МассивПисем Цикл
		СообщениеОбОшибке = "";
		Если Не ПоместитьПисьмоВКорзину(Письмо, СообщениеОбОшибке) Тогда
			РезультатПереноса.НеПеренесенныеПисьма.Добавить(Письмо);
		КонецЕсли;
	КонецЦикла;
	
	Возврат РезультатПереноса;
	
КонецФункции

// Помещает письмо в папку Корзина учетной записи.
//
Функция ПоместитьПисьмоВКорзину(Письмо, СообщениеОбОшибке) Экспорт
	
	СообщениеОбОшибке = "";
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(Письмо);
		ПисьмоОбъект = Письмо.ПолучитьОбъект();
	Исключение
		СообщениеОбОшибке = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат Ложь;
	КонецПопытки;
	
	УчетнаяЗапись = ПисьмоОбъект.УчетнаяЗапись;
	Корзина = РегистрыСведений.ПапкиУчетныхЗаписей.ПолучитьПапку(УчетнаяЗапись, Перечисления.ВидыПапокПисем.Корзина);
	Если ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(ПисьмоОбъект) Тогда
		Если ЗначениеЗаполнено(ПисьмоОбъект.ПодготовленоКОтправке) Тогда
			ПисьмоОбъект.ПодготовленоКОтправке = Дата(1, 1, 1);
		КонецЕсли;
	КонецЕсли;
	
	Если Не УстановитьПапкуПисьма(ПисьмоОбъект, Корзина, СообщениеОбОшибке) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		ПисьмоОбъект.Записать();
	Исключение
		СообщениеОбОшибке = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

// Возвращает признак доступности письма текущему пользователю.
//
Функция ПисьмоДоступно(Письмо) Экспорт
	
	Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(Письмо) Тогда
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Письма.Ссылка
			|ИЗ
			|	Документ.ВходящееПисьмо КАК Письма
			|ГДЕ
			|	Письма.Ссылка = &Ссылка");
	ИначеЕсли ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(Письмо) Тогда
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Письма.Ссылка
			|ИЗ
			|	Документ.ИсходящееПисьмо КАК Письма
			|ГДЕ
			|	Письма.Ссылка = &Ссылка");
	Иначе
		Возврат Ложь;
	КонецЕсли;
	Запрос.УстановитьПараметр("Ссылка", Письмо);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции


/////////////////////////////////////////////////////////////////////////////////////////
// РАБОТА С КОЛИЧЕСТВОМ ПИСЕМ В ПАПКАХ

// Возвращает данные о представлении папок писем, доступных пользователю,
// с учетом настроек отображения папок в списке.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - адрес которого нужно получить
//
// Возвращаемое значение:
//    Соответствие = Ключ - Папка,  
//    Значение - Структура (Представление ВариантОтображенияКоличестваПисем Всего Прочтено Количество Выделена ПолныйПутьСРодителями)
//
Функция ПолучитьСведенияОПапках(Знач Пользователь = Неопределено, Знач РежимМоиПапки = Ложь,
	МассивПапок = Неопределено) Экспорт
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ИСТИНА ИЗ &ВыборДоступныеПапки
		|
		|ГДЕ &ОтборМоиПапки
		|
		|;          
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДоступныеПапки.Папка,
		|	ДоступныеПапки.Наименование,
		|	ДоступныеПапки.ВариантОтображенияКоличестваПисем
		|ИЗ
		|	ДоступныеПапки КАК ДоступныеПапки
		|;          
		|
		|ВЫБРАТЬ
		|	Итоги.Папка КАК Папка,
		|	ВЫБОР КОГДА СУММА(Итоги.Всего) > 0 ТОГДА СУММА(Итоги.Всего) ИНАЧЕ 0 КОНЕЦ КАК Всего,
		|	ВЫБОР КОГДА СУММА(Итоги.Всего) > СУММА(Итоги.Прочтенных) ТОГДА СУММА(Итоги.Всего) - СУММА(Итоги.Прочтенных) ИНАЧЕ 0 КОНЕЦ КАК Непрочтенных
		|ИЗ
		|	(ВЫБРАТЬ
		|		Т.Папка КАК Папка,
		|		Т.Всего КАК Всего,
		|		ВЫБОР
		|			КОГДА Т.Пользователь = &Пользователь
		|				ТОГДА Т.Прочтенных
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК Прочтенных
		|	ИЗ
		|		РегистрСведений.КоличествоПисемВПапкахИтоги КАК Т
		|	ГДЕ
		|		Т.Папка В
		|				(ВЫБРАТЬ
		|					ДоступныеПапки.Папка
		|				ИЗ
		|					ДоступныеПапки)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Т.Папка,
		|		Т.Всего,
		|		ВЫБОР
		|			КОГДА Т.Пользователь = &Пользователь
		|				ТОГДА Т.Прочтенных
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	ИЗ
		|		РегистрСведений.КоличествоПисемВПапкахДвижения КАК Т
		|	ГДЕ
		|		Т.Папка В
		|				(ВЫБРАТЬ
		|					ДоступныеПапки.Папка
		|				ИЗ
		|					ДоступныеПапки)) КАК Итоги
		|
		|СГРУППИРОВАТЬ ПО
		|	Итоги.Папка");
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Если МассивПапок = Неопределено Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВЫБРАТЬ ИСТИНА ИЗ &ВыборДоступныеПапки",
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ПапкиПисем.Ссылка КАК Папка,
			|	ПапкиПисем.Представление КАК Наименование,
			|	ПапкиПисем.ВариантОтображенияКоличестваПисем
			|ПОМЕСТИТЬ ДоступныеПапки
			|ИЗ
			|	Справочник.ПапкиПисем КАК ПапкиПисем");
			
	Иначе		
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВЫБРАТЬ ИСТИНА ИЗ &ВыборДоступныеПапки",
			"ВЫБРАТЬ
			|	ТаблицаПапок.Ссылка КАК Ссылка
			|ПОМЕСТИТЬ ТаблицаПапокВременная
			|ИЗ
			|	&ТаблицаПапок КАК ТаблицаПапок
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ТаблицаПапокВременная.Ссылка КАК Папка,
			|	ПапкиПисем.Наименование КАК Наименование,
			|	ПапкиПисем.ВариантОтображенияКоличестваПисем КАК ВариантОтображенияКоличестваПисем
			|ПОМЕСТИТЬ ДоступныеПапки
			|ИЗ
			|	ТаблицаПапокВременная КАК ТаблицаПапокВременная
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПапкиПисем КАК ПапкиПисем
			|		ПО ТаблицаПапокВременная.Ссылка = ПапкиПисем.Ссылка
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Папка");
			
			ТаблицаПапок = Новый ТаблицаЗначений; 
			
			Массив = Новый Массив;
			Массив.Добавить(Тип("СправочникСсылка.ПапкиПисем")); 
			Массив.Добавить(Тип("Null")); 
			НоваяКолонка = ТаблицаПапок.Колонки.Добавить("Ссылка", Новый ОписаниеТипов(Массив));
			
			Для Каждого Папка Из МассивПапок Цикл
				
				НоваяСтрока = ТаблицаПапок.Добавить();
				НоваяСтрока.Ссылка = Папка;
				
			КонецЦикла;
			
			Запрос.УстановитьПараметр("ТаблицаПапок", ТаблицаПапок);
		
	КонецЕсли;	
	
	Если РежимМоиПапки И МассивПапок = Неопределено Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ГДЕ &ОтборМоиПапки",
			"ГДЕ ПапкиПисем.Ссылка В ИЕРАРХИИ
			|	(ВЫБРАТЬ
			|		ПапкиПисемБыстрогоДоступа.Папка
			|	ИЗ
			|		РегистрСведений.ПапкиПисемБыстрогоДоступа КАК ПапкиПисемБыстрогоДоступа
			|	ГДЕ
			|		ПапкиПисемБыстрогоДоступа.Пользователь = &Пользователь)");
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ГДЕ &ОтборМоиПапки", "");
		
	КонецЕсли;
	
	СтартовыйИндекс = 1;
	Если МассивПапок = Неопределено Тогда
		СтартовыйИндекс = 1;
	Иначе
		СтартовыйИндекс = 2;
	КонецЕсли;	
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	ДоступныеПапки = РезультатыЗапроса[СтартовыйИндекс].Выбрать();
	РассчитанныеПапки = РезультатыЗапроса[СтартовыйИндекс + 1].Выбрать();
	
	СведенияОПапках = Новый Соответствие;
	
	Пока ДоступныеПапки.Следующий() Цикл
		
		СведенияОПапке = Новый Структура;
		СведенияОПапке.Вставить("Представление", ДоступныеПапки.Наименование);
		
		СведенияОПапке.Вставить("ВариантОтображенияКоличестваПисем",
			ДоступныеПапки.ВариантОтображенияКоличестваПисем);
		
		СведенияОПапке.Вставить("Всего", 0);
		СведенияОПапке.Вставить("Прочтено", 0);
		СведенияОПапке.Вставить("Количество", 0);
		
		СведенияОПапке.Вставить("Выделена", Ложь);
		
		СведенияОПапках.Вставить(ДоступныеПапки.Папка, СведенияОПапке);
		
	КонецЦикла;
	
	Пока РассчитанныеПапки.Следующий() Цикл
		
		СведенияОПапке = СведенияОПапках.Получить(РассчитанныеПапки.Папка);
		СведенияОПапке.Всего = РассчитанныеПапки.Всего;
		
		СведенияОПапке.Прочтено = РассчитанныеПапки.Всего - РассчитанныеПапки.Непрочтенных;
		
	КонецЦикла;
	
	Для каждого Элемент Из СведенияОПапках Цикл
		
		СведенияОПапке = Элемент.Значение;
		
		Если СведенияОПапке.ВариантОтображенияКоличестваПисем
			= Перечисления.ВариантыОтображенияКоличестваПисемВПапке.Все Тогда
			
			СведенияОПапке.Количество = СведенияОПапке.Всего;
			
		ИначеЕсли СведенияОПапке.ВариантОтображенияКоличестваПисем
			= Перечисления.ВариантыОтображенияКоличестваПисемВПапке.Непрочтенные Тогда
			
			СведенияОПапке.Количество = СведенияОПапке.Всего - СведенияОПапке.Прочтено;
			
			СведенияОПапке.Выделена = (СведенияОПапке.Количество > 0);
			
		Иначе
			
			СведенияОПапке.Количество = 0;
			
		КонецЕсли;
		
		Если СведенияОПапке.Количество > 0 Тогда
			
			СведенияОПапке.Представление = СведенияОПапке.Представление
				+ " (" + Формат(СведенияОПапке.Количество, "ЧГ=0") + ")";
			
		КонецЕсли;
		
		СведенияОПапке.Удалить("ВариантОтображенияКоличестваПисем");
		СведенияОПапке.Удалить("Всего");
		СведенияОПапке.Удалить("Прочтено");
		
		
		Если МассивПапок <> Неопределено Тогда
			
			Папка = Элемент.Ключ;
			
			Родитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Папка, 
				"Родитель");
				
			Описание = "";	
				
			Если ЗначениеЗаполнено(Родитель) Тогда
				ОписаниеРодителей = Справочники.ПапкиПисем.ПолучитьПолныйПутьПапки(Родитель);
				Описание = Строка(Папка) + " (" + ОписаниеРодителей + ")";
			Иначе
				Описание = Строка(Папка);
			КонецЕсли;	
			
			СведенияОПапке.Вставить("ПолныйПутьСРодителями", Описание);
			
		КонецЕсли;
			
	КонецЦикла;
	
	Возврат СведенияОПапках;
	
КонецФункции

// Возвращает данные о количестве писем в папке писем.
//
Функция ПолучитьКоличествоПисемВПапке(Папка, ВариантОтображенияКоличестваПисем, Знач Пользователь = Неопределено) Экспорт
	
	КоличествоПисем = 0;
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Если ВариантОтображенияКоличестваПисем =
		Перечисления.ВариантыОтображенияКоличестваПисемВПапке.Все Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА СУММА(Итоги.Всего) > 0
			|			ТОГДА СУММА(Итоги.Всего)
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК Количество
			|ИЗ
			|	(ВЫБРАТЬ
			|		Т.Всего КАК Всего
			|	ИЗ
			|		РегистрСведений.КоличествоПисемВПапкахИтоги КАК Т
			|	ГДЕ
			|		Т.Папка = &Папка
			|		И Т.Всего <> 0
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Т.Всего
			|	ИЗ
			|		РегистрСведений.КоличествоПисемВПапкахДвижения КАК Т
			|	ГДЕ
			|		Т.Папка = &Папка
			|		И Т.Всего <> 0) КАК Итоги");
		
		Запрос.УстановитьПараметр("Папка", Папка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			КоличествоПисем = 0;
		Иначе
			КоличествоПисем = РезультатЗапроса.Выгрузить()[0].Количество;
		КонецЕсли;
		
	ИначеЕсли ВариантОтображенияКоличестваПисем =
		Перечисления.ВариантыОтображенияКоличестваПисемВПапке.Непрочтенные Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА СУММА(Итоги.Всего) > СУММА(Итоги.Прочтенных)
			|			ТОГДА СУММА(Итоги.Всего) - СУММА(Итоги.Прочтенных)
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК Количество
			|ИЗ
			|	(ВЫБРАТЬ
			|		Т.Всего КАК Всего,
			|		ВЫБОР
			|			КОГДА Т.Пользователь = &Пользователь
			|				ТОГДА Т.Прочтенных
			|			ИНАЧЕ 0
			|		КОНЕЦ КАК Прочтенных
			|	ИЗ
			|		РегистрСведений.КоличествоПисемВПапкахИтоги КАК Т
			|	ГДЕ
			|		Т.Папка = &Папка
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Т.Всего,
			|		ВЫБОР
			|			КОГДА Т.Пользователь = &Пользователь
			|				ТОГДА Т.Прочтенных
			|			ИНАЧЕ 0
			|		КОНЕЦ
			|	ИЗ
			|		РегистрСведений.КоличествоПисемВПапкахДвижения КАК Т
			|	ГДЕ
			|		Т.Папка = &Папка) КАК Итоги");
		
		Запрос.УстановитьПараметр("Папка", Папка);
		Запрос.УстановитьПараметр("Пользователь", Пользователь);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			КоличествоПисем = 0;
		Иначе
			КоличествоПисем = РезультатЗапроса.Выгрузить()[0].Количество;
		КонецЕсли;
			
	КонецЕсли;
	
	Возврат КоличествоПисем;
	
КонецФункции	

// Возвращает данные о представлении папки писем.
//
Функция ПолучитьСведенияОПапке(Папка, Знач Пользователь = Неопределено) Экспорт
	
	СведенияОПапке = Новый Структура(
		"Представление, Выделена",
		"", Ложь);
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	РеквизитыПапки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Папка,
		"Представление, ВариантОтображенияКоличестваПисем");
	
	СведенияОПапке.Представление = РеквизитыПапки.Представление;
	ВариантОтображенияКоличестваПисем = РеквизитыПапки.ВариантОтображенияКоличестваПисем;
	
	КоличествоПисем = ПолучитьКоличествоПисемВПапке(Папка, ВариантОтображенияКоличестваПисем, Пользователь);
	
	Если ВариантОтображенияКоличестваПисем =
		Перечисления.ВариантыОтображенияКоличестваПисемВПапке.Непрочтенные Тогда
		
		СведенияОПапке.Выделена = (КоличествоПисем > 0);
		
	КонецЕсли;
	
	Если КоличествоПисем > 0 Тогда
		
		СведенияОПапке.Представление = СведенияОПапке.Представление
			+ " (" + Формат(КоличествоПисем, "ЧГ=0") + ")";
		
	КонецЕсли;
	
	Возврат СведенияОПапке;
	
КонецФункции

/////////////////////////////////////////////////////////////////////////////////////////
// РАБОТА С ДЕРЕВОМ ПЕРЕПИСКИ

// Возвращает связанное письмо. Ищет по связям ПисьмоОтправленоВОтветНа, ПолученоВОтветНа, ПересылкаПисьма.
//
Функция ПолучитьПисьмоОснование(Письмо) Экспорт
	
	Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(Письмо) Тогда
		
		Основание = СвязиОбъектов.ПолучитьСвязанныйОбъект(Письмо, Справочники.ТипыСвязей.ПолученоВОтветНаДокумент);
		Если ЗначениеЗаполнено(Основание) Тогда
			ПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ПометкаУдаления");
			Если Не ПометкаУдаления Тогда
				Возврат Основание;
			КонецЕсли;
		КонецЕсли;
		Основание = СвязиОбъектов.ПолучитьСвязанныйОбъект(Письмо, Справочники.ТипыСвязей.ПолученоВОтветНаПисьмо);
		Если ЗначениеЗаполнено(Основание) Тогда
			ПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ПометкаУдаления");
			Если Не ПометкаУдаления Тогда
				Возврат Основание;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(Письмо) Тогда
		
		Основание = СвязиОбъектов.ПолучитьСвязанныйОбъект(Письмо, Справочники.ТипыСвязей.ПисьмоОтправленоВОтветНа);
		Если ЗначениеЗаполнено(Основание) Тогда
			ПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ПометкаУдаления");
			Если Не ПометкаУдаления Тогда
				Возврат Основание;
			КонецЕсли;
		КонецЕсли;
		Основание = СвязиОбъектов.ПолучитьСвязанныйОбъект(Письмо, Справочники.ТипыСвязей.ПересылкаПисьма);
		Если ЗначениеЗаполнено(Основание) Тогда
			ПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ПометкаУдаления");
			Если Не ПометкаУдаления Тогда
				Возврат Основание;
			КонецЕсли;
		КонецЕсли;
		Основание = СвязиОбъектов.ПолучитьСвязанныйОбъект(Письмо, Справочники.ТипыСвязей.ПеренаправлениеПисьма);
		Если ЗначениеЗаполнено(Основание) Тогда
			ПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ПометкаУдаления");
			Если Не ПометкаУдаления Тогда
				Возврат Основание;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоВходящийДокумент(Письмо) Тогда
		
		Основание = СвязиОбъектов.ПолучитьСвязанныйОбъект(Письмо, Справочники.ТипыСвязей.ПолученВОтветНа);
		Если ЗначениеЗаполнено(Основание) Тогда
			ПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ПометкаУдаления");
			Если Не ПометкаУдаления Тогда
				Возврат Основание;
			КонецЕсли;
		КонецЕсли;
		Основание = СвязиОбъектов.ПолучитьСвязанныйОбъект(Письмо, Справочники.ТипыСвязей.ПолученВОтветНаПисьмо);
		Если ЗначениеЗаполнено(Основание) Тогда
			ПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ПометкаУдаления");
			Если Не ПометкаУдаления Тогда
				Возврат Основание;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоИсходящийДокумент(Письмо) Тогда
		
		Основание = СвязиОбъектов.ПолучитьСвязанныйОбъект(Письмо, Справочники.ТипыСвязей.ОтправленВОтветНа);
		Если ЗначениеЗаполнено(Основание) Тогда
			ПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ПометкаУдаления");
			Если Не ПометкаУдаления Тогда
				Возврат Основание;
			КонецЕсли;
		КонецЕсли;
		Основание = СвязиОбъектов.ПолучитьСвязанныйОбъект(Письмо, Справочники.ТипыСвязей.ОтправленВОтветНаПисьмо);
		Если ЗначениеЗаполнено(Основание) Тогда
			ПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ПометкаУдаления");
			Если Не ПометкаУдаления Тогда
				Возврат Основание;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает массив писем, находящихся в дереве переписки.
//
Функция ПолучитьПисьмаПереписки(Письмо) Экспорт
	
	ТекКореньПереписки = ПолучитьКорневоеПисьмо(Письмо);
	Переписка = Новый Массив;
	Переписка.Добавить(ТекКореньПереписки);
	ДобавитьВПерепискуПодчиненныеПисьмаИерархически(Переписка, ТекКореньПереписки);
	
	Возврат Переписка;
	
КонецФункции

// Возвращает корневое письмо дерева переписки.
//
Функция ПолучитьКорневоеПисьмо(Знач Письмо) Экспорт
	
	КорневоеПисьмо = Письмо;
	Письмо = ПолучитьПисьмоОснование(Письмо);
	
	Счетчик = 0;
	
	Пока ЗначениеЗаполнено(Письмо) Цикл
		
		КорневоеПисьмо = Письмо;
		Письмо = ПолучитьПисьмоОснование(Письмо);
		
		Счетчик = Счетчик + 1;
		Если Счетчик > 1000 Тогда
			ВызватьИсключение НСтр("ru ='Обнаружено зацикливание в ПолучитьКорневоеПисьмо'");
		КонецЕсли;	
		
	КонецЦикла;
	
	Возврат КорневоеПисьмо;
	
КонецФункции

// Заполняет массив подчиненных писем.
//
Процедура ПолучитьПодчиненныеПисьма(Письмо, МассивПисем) Экспорт
	
	Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(Письмо) Тогда
		
		ПолучатьПомеченныеНаУдаление = Истина;
		ВсеСвязиДокумента = СвязиОбъектов.ПолучитьВсеСвязиОбъекта(Письмо, ПолучатьПомеченныеНаУдаление);
		
		Для каждого Связь Из ВсеСвязиДокумента Цикл
			
			СвязанноеПисьмо = Связь.СвязанныйОбъект;
			Если Не ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(СвязанноеПисьмо).Чтение Тогда
				Продолжить;
			КонецЕсли;	
			
			ТипСвязи = Связь.ТипСвязи;
			
			Если (ТипСвязи = Справочники.ТипыСвязей.ОтправленОтветныйДокумент
				Или ТипСвязи = Справочники.ТипыСвязей.ОтправленоОтветноеПисьмо
				Или ТипСвязи = Справочники.ТипыСвязей.ПересланоПисьмом
				Или ТипСвязи = Справочники.ТипыСвязей.ПеренаправленоПисьмом) Тогда
				
				МассивПисем.Добавить(СвязанноеПисьмо);
				
			КонецЕсли;	
			
		КонецЦикла;	
		
	ИначеЕсли ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(Письмо) Тогда
		
		ПолучатьПомеченныеНаУдаление = Истина;
		ВсеСвязиДокумента = СвязиОбъектов.ПолучитьВсеСвязиОбъекта(Письмо, ПолучатьПомеченныеНаУдаление);
		
		Для каждого Связь Из ВсеСвязиДокумента Цикл
			
			СвязанноеПисьмо = Связь.СвязанныйОбъект;
			Если Не ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(СвязанноеПисьмо).Чтение Тогда
				Продолжить;
			КонецЕсли;	
			
			ТипСвязи = Связь.ТипСвязи;
			
			Если (ТипСвязи = Справочники.ТипыСвязей.ПолученОтветныйДокумент
				Или ТипСвязи = Справочники.ТипыСвязей.ПолученоОтветноеПисьмо
				Или ТипСвязи = Справочники.ТипыСвязей.ПересланоПисьмом
				Или ТипСвязи = Справочники.ТипыСвязей.ОтправленОтвет
				Или ТипСвязи = Справочники.ТипыСвязей.ПеренаправленоПисьмом) Тогда
				
				МассивПисем.Добавить(СвязанноеПисьмо);
				
			КонецЕсли;	
			
		КонецЦикла;	
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоВходящийДокумент(Письмо) Тогда
		
		ПолучатьПомеченныеНаУдаление = Истина;
		ВсеСвязиДокумента = СвязиОбъектов.ПолучитьВсеСвязиОбъекта(Письмо, ПолучатьПомеченныеНаУдаление);
		
		Для каждого Связь Из ВсеСвязиДокумента Цикл
			
			СвязанноеПисьмо = Связь.СвязанныйОбъект;
			Если Не ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(СвязанноеПисьмо).Чтение Тогда
				Продолжить;
			КонецЕсли;	
			
			ТипСвязи = Связь.ТипСвязи;
			
			Если (ТипСвязи = Справочники.ТипыСвязей.ОтправленОтвет
				Или ТипСвязи = Справочники.ТипыСвязей.ОтправленоОтветноеПисьмо) Тогда
				
				МассивПисем.Добавить(СвязанноеПисьмо);
				
			КонецЕсли;	
			
		КонецЦикла;	
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоИсходящийДокумент(Письмо) Тогда
		
		ПолучатьПомеченныеНаУдаление = Истина;
		ВсеСвязиДокумента = СвязиОбъектов.ПолучитьВсеСвязиОбъекта(Письмо, ПолучатьПомеченныеНаУдаление);
		
		Для каждого Связь Из ВсеСвязиДокумента Цикл
			
			СвязанноеПисьмо = Связь.СвязанныйОбъект;
			Если Не ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(СвязанноеПисьмо).Чтение Тогда
				Продолжить;
			КонецЕсли;	
			
			ТипСвязи = Связь.ТипСвязи;
			
			Если (ТипСвязи = Справочники.ТипыСвязей.ПолученОтвет
				Или ТипСвязи = Справочники.ТипыСвязей.ПолученоОтветноеПисьмо) Тогда
				
				МассивПисем.Добавить(СвязанноеПисьмо);
				
			КонецЕсли;	
			
		КонецЦикла;	
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоДокументПредприятия(Письмо) Тогда
		
		ПолучатьПомеченныеНаУдаление = Истина;
		ВсеСвязиДокумента = СвязиОбъектов.ПолучитьВсеСвязиОбъекта(Письмо, ПолучатьПомеченныеНаУдаление);
		
		Для каждого Связь Из ВсеСвязиДокумента Цикл
			
			СвязанноеПисьмо = Связь.СвязанныйОбъект;
			Если Не ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(СвязанноеПисьмо).Чтение Тогда
				Продолжить;
			КонецЕсли;	
			
			ТипСвязи = Связь.ТипСвязи;
			
			Если ТипСвязи = Справочники.ТипыСвязей.ПерепискаПоПредмету Тогда
				
				МассивПисем.Добавить(СвязанноеПисьмо);
				
			КонецЕсли;	
			
		КонецЦикла;	
		
	ИначеЕсли ТипЗнч(Письмо) = Тип("БизнесПроцессСсылка.Исполнение") Тогда
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает количество писем в переписке, в которую входит текущее письмо.
//
Функция ПолучитьКоличествоПисемВПереписке(Письмо) Экспорт
	
	КорневоеПисьмо = ПолучитьКорневоеПисьмо(Письмо);
	
	ПисьмаВПереписке = Новый Массив;
	ПисьмаВПереписке.Добавить(КорневоеПисьмо);
	
	Счетчик = 0;
	Пока Счетчик < ПисьмаВПереписке.Количество() Цикл
		
		ПолучитьПодчиненныеПисьма(ПисьмаВПереписке[Счетчик], ПисьмаВПереписке);
		Счетчик = Счетчик + 1;
		
	КонецЦикла;

	Возврат ПисьмаВПереписке.Количество();

КонецФункции

/////////////////////////////////////////////////////////////////////////////////////////
// РАБОТА С ФАЙЛАМИ ПИСЬМА

// Возвращает таблицу значений с информацией о вложениях письма.
//
Функция ПолучитьФайлыПисьма(
	Письмо,
	ФормироватьПредставлениеРазмера = Ложь,
	ВключатьПомеченныеНаУдалениеПараметр = Ложь,
	ТолькоСИдентификаторами = Ложь,
	ТолькоБезИдентификаторов = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВключатьПомеченныеНаУдаление = ВключатьПомеченныеНаУдалениеПараметр;
	
	ПометкаУдаленияПисьма = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Письмо, "ПометкаУдаления");
	Если ПометкаУдаленияПисьма = Истина Тогда
		ВключатьПомеченныеНаУдаление = Истина;
	КонецЕсли;	
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Файлы.Ссылка КАК Ссылка,
		|	Файлы.Наименование + ""."" + Файлы.ТекущаяВерсияРасширение КАК Представление,
		|	Файлы.ИндексКартинки КАК ИндексКартинки,
		|	Файлы.ТекущаяВерсияРазмер КАК Размер,
		|	ЕСТЬNULL(ИдентификаторыПочтовыхВложений.Идентификатор, """") КАК ИДФайлаЭлектронногоПисьма,
		|	ВЫБОР
		|		КОГДА Файлы.ТекущаяВерсияРасширение = """"
		|			ТОГДА Файлы.Наименование
		|		ИНАЧЕ Файлы.Наименование + ""."" + Файлы.ТекущаяВерсияРасширение
		|	КОНЕЦ КАК ИмяФайла,
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(20)) КАК РазмерПредставление,
		|	Файлы.ПометкаУдаления КАК ПометкаУдаления,
		|	Файлы.Редактирует КАК Редактирует,
		|	ВЫБОР
		|		КОГДА Файлы.Редактирует В (&ПользовательИЕгоСотрудники)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РедактируетТекущийПользователь,
		|	Файлы.ТекущаяВерсия КАК ТекущаяВерсия,
		|	Файлы.ТекущаяВерсияРасширение КАК ТекущаяВерсияРасширение,
		|	Файлы.ТекущаяВерсияДатаМодификацииФайла КАК ТекущаяВерсияДатаМодификацииФайла,
		|	СведенияОФайлахДокументооборот.ПорядковыйНомерВложения КАК ПорядковыйНомерВложения,
		|	ЕСТЬNULL(СведенияОФайлахДокументооборот.ЭтоВложенноеПисьмо, ЛОЖЬ) КАК ЭтоВложенноеПисьмо
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИдентификаторыПочтовыхВложений КАК ИдентификаторыПочтовыхВложений
		|		ПО (ИдентификаторыПочтовыхВложений.Файл = Файлы.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОФайлахДокументооборот КАК СведенияОФайлахДокументооборот
		|		ПО (СведенияОФайлахДокументооборот.Файл = Файлы.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СлужебныеФайлыДокументов КАК СлужебныеФайлыДокументов
		|		ПО (СлужебныеФайлыДокументов.Файл = Файлы.Ссылка)
		|ГДЕ
		|	Файлы.ВладелецФайла = &Письмо
		|	И СлужебныеФайлыДокументов.Файл ЕСТЬ NULL
		|	И (&ВключатьПомеченныеНаУдаление
		|			ИЛИ НЕ Файлы.ПометкаУдаления)
		|	И НЕ(&ТолькоСИдентификаторами
		|				И ИдентификаторыПочтовыхВложений.Идентификатор ЕСТЬ NULL)
		|	И НЕ(&ТолькоБезИдентификаторов
		|				И НЕ ИдентификаторыПочтовыхВложений.Идентификатор ЕСТЬ NULL)");
	Запрос.УстановитьПараметр("Письмо", Письмо);
	Запрос.УстановитьПараметр("ВключатьПомеченныеНаУдаление", ВключатьПомеченныеНаУдаление);
	Запрос.УстановитьПараметр("ПользовательИЕгоСотрудники", 
		СотрудникиВызовСервера.ПользовательИЕгоСотрудники(Пользователи.ТекущийПользователь()));
	Запрос.УстановитьПараметр("ТолькоСИдентификаторами", ТолькоСИдентификаторами);
	Запрос.УстановитьПараметр("ТолькоБезИдентификаторов", ТолькоБезИдентификаторов);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Результат.Сортировать("ПорядковыйНомерВложения, Представление");
	
	Если ФормироватьПредставлениеРазмера Тогда
		Для каждого СтрокаТаблицы Из Результат Цикл
			СтрокаТаблицы.РазмерПредставление = РаботаСоСтроками.ПолучитьРазмерСтрокой(СтрокаТаблицы.Размер);
		КонецЦикла;
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из Результат Цикл
		
		Если Не СтрокаТаблицы.ЭтоВложенноеПисьмо Тогда
			
			Если НРег(СтрокаТаблицы.ТекущаяВерсияРасширение) = НРег("eml")
				Или НРег(СтрокаТаблицы.ТекущаяВерсияРасширение) = НРег("msg") Тогда
				СтрокаТаблицы.ЭтоВложенноеПисьмо = Истина;
			КонецЕсли;		
			
		КонецЕсли;	
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает признак наличия непомеченных на удаление вложений письма.
// Картинки в теле письма вложениями не считаются.
//
Функция ПисьмоИмеетВложения(Письмо) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Файлы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИдентификаторыПочтовыхВложений КАК ИдентификаторыПочтовыхВложений
		|		ПО (ИдентификаторыПочтовыхВложений.Файл = Файлы.Ссылка)
		|ГДЕ
		|	Файлы.ВладелецФайла = &Письмо
		|	И (НЕ Файлы.ПометкаУдаления)
		|	И ЕСТЬNULL(ИдентификаторыПочтовыхВложений.Идентификатор, """") = """"");
	Запрос.УстановитьПараметр("Письмо", Письмо);
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

// Возвращает Массив структур с информацией о вложениях письма
// Результат (Массив)
// - Элемент(Структура)
//   - Ссылка (Файл)
//   - Представление (Строка)
//   - ИмяФайла (Строка)
//   - ИндексКартинки (Число)
//   - Размер (Число)
//   - РазмерПредставление (Строка)
//   - ПометкаУдаления (Булево)
//   - Редактирует (Пользователь)
//   - РедактируетТекущийПользователь (Булево)
//
Функция ПолучитьИнформациюОВложениях(МассивФайлов) Экспорт
	
	Результат = Новый Массив;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Файлы.Ссылка КАК Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Файлы.Ссылка) КАК Представление,
		|	Файлы.ИндексКартинки КАК ИндексКартинки,
		|	Файлы.ТекущаяВерсияРазмер КАК Размер,
		|	ВЫБОР
		|		КОГДА Файлы.ТекущаяВерсияРасширение = """"
		|			ТОГДА Файлы.Наименование
		|		ИНАЧЕ Файлы.Наименование + ""."" + Файлы.ТекущаяВерсияРасширение
		|	КОНЕЦ КАК ИмяФайла,
		|	Файлы.ТекущаяВерсияРасширение КАК Расширение,
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(20)) КАК РазмерПредставление,
		|	Файлы.ПометкаУдаления КАК ПометкаУдаления,
		|	Файлы.Редактирует КАК Редактирует,
		|	ВЫБОР
		|		КОГДА Файлы.Редактирует В (&ПользовательИЕгоСотрудники)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РедактируетТекущийПользователь,
		|	Файлы.ПодписанЭП
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|ГДЕ
		|	Файлы.Ссылка В (&МассивФайлов)");
	Запрос.УстановитьПараметр("МассивФайлов", МассивФайлов);
	Запрос.УстановитьПараметр("ПользовательИЕгоСотрудники", 
		СотрудникиВызовСервера.ПользовательИЕгоСотрудники(Пользователи.ТекущийПользователь()));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ФайлСсылка = Выборка.Ссылка;
		АвтозаполнениеШаблоновФайловВызовСервера.ПерезаполнитьФайлПоДаннымВладельцаПриНеобходимости(ФайлСсылка);
		
		ИндексКартинки = Выборка.ИндексКартинки;
		Если Выборка.ПометкаУдаления Тогда
			ИндексКартинки = ИндексКартинки + 1;
		КонецЕсли;
		ФайлИнфо = Новый Структура;
		ФайлИнфо.Вставить("Ссылка", ФайлСсылка);
		ФайлИнфо.Вставить("Представление", Выборка.Представление);
		ФайлИнфо.Вставить("ИмяФайла", Выборка.ИмяФайла);
		ФайлИнфо.Вставить("ИндексКартинки", ИндексКартинки);
		ФайлИнфо.Вставить("Размер", Выборка.Размер);
		ФайлИнфо.Вставить("РазмерПредставление", РаботаСоСтроками.ПолучитьРазмерСтрокой(Выборка.Размер));
		ФайлИнфо.Вставить("ПометкаУдаления", Выборка.ПометкаУдаления);
		ФайлИнфо.Вставить("Редактирует", Выборка.Редактирует);
		ФайлИнфо.Вставить("Расширение", Выборка.Расширение);
		ФайлИнфо.Вставить("РедактируетТекущийПользователь", Выборка.РедактируетТекущийПользователь);
		ФайлИнфо.Вставить("ПодписанЭП", Выборка.ПодписанЭП);
		
		Результат.Добавить(ФайлИнфо);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Получает данные файла письма по строке уникального идентификатора файла.
//
Функция ПолучитьДанныеФайлаПоИдентификатору(ИдентификаторФайла) Экспорт
	
	Ссылка = Справочники.Файлы.ПолучитьСсылку(Новый УникальныйИдентификатор(ИдентификаторФайла));
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(Ссылка);
	
	Возврат ДанныеФайла;
	
КонецФункции

// Функция заполняет данные сохраняемых файлов, с учетом пути выбора для 
// сохранения файлов почты.
Функция ПолучитьДанныеФайловДляСохраненияФайлов(СписокФайлов, УникальныйИдентификатор) Экспорт
	
	СписокДанныхФайлов = Новый СписокЗначений();
	ОбщийРазмер = 0;
	
	Если ТипЗнч(СписокФайлов) = Тип("СписокЗначений") Тогда
		
		СписокФайловПисьма = СписокФайлов;
		
		Для Каждого Файл Из СписокФайловПисьма Цикл
			
			ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляСохранения(
				Файл.Значение,
				Неопределено,
				УникальныйИдентификатор);
				
			СписокДанныхФайлов.Добавить(ДанныеФайла);
			ОбщийРазмер = ОбщийРазмер + ДанныеФайла.Размер;
			
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(СписокФайлов) = Тип("ТаблицаЗначений") Тогда
		
		СписокФайловПисьма = Новый СписокЗначений();
		Для каждого ФайлыПисьмаСтрока Из СписокФайлов Цикл
			СписокФайловПисьма.Добавить(ФайлыПисьмаСтрока.Ссылка);
		КонецЦикла;
		
		Для Каждого Файл Из СписокФайлов Цикл
			
			Если Файл.Расположение = "Файл" ИЛИ Файл.Расположение = "СсылкаНаФайл" Тогда
				
				ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляСохранения(
					Файл.Ссылка,
					Неопределено,
					УникальныйИдентификатор);
				
			ИначеЕсли Файл.Расположение = "ВременноеХранилище" Тогда
				
				ДанныеФайла = Новый Структура;
				ДанныеФайла.Вставить("Ссылка", Справочники.Файлы.ПустаяСсылка());
				ДанныеФайла.Вставить("Версия", Справочники.ВерсииФайлов.ПустаяСсылка());
				ДанныеФайла.Вставить("ТекущаяВерсия", Справочники.ВерсииФайлов.ПустаяСсылка());
				ДанныеФайла.Вставить("НавигационнаяСсылкаТекущейВерсии", Файл.Адрес);
				ДанныеФайла.Вставить("Размер", Файл.Размер);
				ДанныеФайла.Вставить("ДатаМодификацииУниверсальная", ТекущаяДатаСеанса());
				Расширение = ФайловыеФункцииКлиентСервер.ПолучитьРасширениеИмениФайла(Файл.ИмяФайла);
				ДанныеФайла.Вставить("Расширение", Расширение);
				ДанныеФайла.Вставить("ПолноеНаименованиеВерсии", Файл.ИмяФайла);
				ДанныеФайла.Вставить("Зашифрован", Ложь);
				ДанныеФайла.Вставить("РедактируетТекущийПользователь", Ложь);
				
			КонецЕсли;
			
			СписокДанныхФайлов.Добавить(ДанныеФайла);
			ОбщийРазмер = ОбщийРазмер + ДанныеФайла.Размер;
			
		КонецЦикла;
		
	КонецЕсли;
	
	КоличествоФайлов = СписокФайловПисьма.Количество();
	ПутьВыбора = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиПрограммы", "ПапкаДляСохраненияПисем");
	
	ДанныеСохраняемыхФайлов = Новый Структура;
	ДанныеСохраняемыхФайлов.Вставить("СписокДанныхФайлов", СписокДанныхФайлов);
	ДанныеСохраняемыхФайлов.Вставить("СписокФайлов", СписокФайловПисьма);
	ДанныеСохраняемыхФайлов.Вставить("ПутьВыбора", ПутьВыбора);
	ДанныеСохраняемыхФайлов.Вставить("КоличествоФайлов", КоличествоФайлов);
	ДанныеСохраняемыхФайлов.Вставить("ОбщийРазмер", ОбщийРазмер);
	
	Возврат ДанныеСохраняемыхФайлов;
	
КонецФункции


/////////////////////////////////////////////////////////////////////////////////////////
// РАБОТА С ТАБЛИЦЕЙ ПОЛУЧАТЕЛЕЙ ПИСЬМА

// Возвращает строку получателей.
//
Функция ТаблицаПолучателейВСтроку(Получатели) Экспорт
	
	Если Получатели.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	Если ТипЗнч(Получатели) = Тип("ТаблицаЗначений") Тогда
		МассивАдресатов = Получатели.ВыгрузитьКолонку("Адресат");
	Иначе
		МассивАдресатов = Новый Массив;
		Для Каждого ЭлементПолучатель Из Получатели Цикл
			МассивАдресатов.Добавить(ЭлементПолучатель.Адресат);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПолучитьПредставлениеМассиваАдресатов(МассивАдресатов);
	
КонецФункции

// Возвращает строку получателей. Если получателей больше 10,
// то выводятся только первые 3 и общее количество.
//
Функция СформироватьСтрокуПолучателейДляОтчета(ТаблицаПолучателей, ТипАдреса) Экспорт
	
	КоличествоПолучателей = ТаблицаПолучателей.Количество();
	Если КоличествоПолучателей = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	МаксимальноеКоличествоОтображаемыхПолучателей = 10;
	КоличествоОтображаемыхПолучателейПриПревышении = 3;
	Если КоличествоПолучателей > МаксимальноеКоличествоОтображаемыхПолучателей Тогда
		КоличествоОтображаемыхПолучателей = КоличествоОтображаемыхПолучателейПриПревышении;
	Иначе
		КоличествоОтображаемыхПолучателей = КоличествоПолучателей;
	КонецЕсли;
	
	Результат = "";
	МассивАдресатов = Новый Массив;
	Для Индекс = 0 По КоличествоОтображаемыхПолучателей - 1 Цикл
		Получатель = ТаблицаПолучателей[Индекс];
		
		МассивАдресатов.Добавить(Получатель.Адресат);
		
	КонецЦикла;
	Результат = ПолучитьПредставлениеМассиваАдресатов(МассивАдресатов);
	
	Если КоличествоПолучателей > МаксимальноеКоличествоОтображаемыхПолучателей Тогда
		
		ПодписьПолучателей = ВстроеннаяПочтаКлиентСервер.ПодписьКЧислу(
			КоличествоПолучателей,
			НСтр("ru = 'получатель'"),
			НСтр("ru = 'получателя'"),
			НСтр("ru = 'получателей'"));
		
		Результат = Результат +
			СтрШаблон(
				НСтр("ru = ' и другие (всего %1 %2)'"),
				Формат(КоличествоПолучателей, "ЧГ=0"),
				ПодписьПолучателей);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


/////////////////////////////////////////////////////////////////////////////////////////
// РАБОТА С ТЕКСТОМ ПИСЬМА

// Возвращает HTML представление письма
//
Функция СформироватьHTMLПредставлениеПисьма(
	Письмо,
	УникальныйИдентификаторФормы,
	ВыводитьШапку,
	СокращенныйСписокАдресатов = Истина,
	ВключитьРежимРедактированияHTML = Ложь,
	ВыводитьИсториюПереписки = Истина,
	ДляКарточкиВходящегоПисьма = Ложь,
	ДляСохраненияПисьма = Ложь) Экспорт
	
	Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(Письмо) Тогда
		
		Возврат СформироватьHTMLПредставлениеВходящегоПисьма(
			Письмо,
			УникальныйИдентификаторФормы,
			ВыводитьШапку,
			СокращенныйСписокАдресатов,
			ВключитьРежимРедактированияHTML,
			ВыводитьИсториюПереписки,
			ДляКарточкиВходящегоПисьма,
			ДляСохраненияПисьма);
		
	ИначеЕсли ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(Письмо) Тогда
		
		Возврат СформироватьHTMLПредставлениеИсходящегоПисьма(
			Письмо,
			УникальныйИдентификаторФормы,
			ВыводитьШапку,
			СокращенныйСписокАдресатов,
			ВключитьРежимРедактированияHTML,
			ВыводитьИсториюПереписки,
			ДляКарточкиВходящегоПисьма,
			ДляСохраненияПисьма);
		
	Иначе
		
		ВызватьИсключение НСтр("ru = 'Некорректный вид письма.'");
		
	КонецЕсли;
	
КонецФункции

// В случае отсутствия у текста HTML тэгов <html></html>,  <body></body> добавляет их.
// Модифицирует параметр ТекстHTML
//
Процедура ДобавитьНеобходимыеТэгиHTML(ТекстHTML) Экспорт
	
	НРегТекстHTML = НРег(ТекстHTML);
	ПозицияТэгаHTML = СтрНайти(НРегТекстHTML, "<html");
	ПозицияТэгаBODY = СтрНайти(НРегТекстHTML, "<body");
	Если ПозицияТэгаHTML = 0 И ПозицияТэгаBODY = 0 Тогда
		ТекстHTML = "<html><body style=""margin-top:1px; padding-top:1px"">" + ТекстHTML + "</body></html>";
	ИначеЕсли ПозицияТэгаHTML = 0 И ПозицияТэгаBODY > 0 Тогда
		ТекстHTML = "<html>" + ТекстHTML + "</html>";
	ИначеЕсли ПозицияТэгаHTML > 0 И ПозицияТэгаBODY = 0 Тогда
		ПозицияОкончанияТэгаHTML = РаботаСоСтроками.НайтиПосле(НРегТекстHTML, ">", ПозицияТэгаHTML);
		Голова = Лев(ТекстHTML, ПозицияОкончанияТэгаHTML);
		ПозицияЗакрывающегосяТэгаHTML = СтрНайти(НРегТекстHTML, "</html>");
		Хвост = Сред(ТекстHTML, ПозицияЗакрывающегосяТэгаHTML);
		Середина = Сред(ТекстHTML, ПозицияОкончанияТэгаHTML + 1, ПозицияЗакрывающегосяТэгаHTML - ПозицияОкончанияТэгаHTML - 1);
		ТекстHTML = Голова + "<body style=""margin-top:1px; padding-top:1px"">" + Середина + "</body>" + Хвост;
	КонецЕсли;
	
КонецПроцедуры

Процедура Удалить_position_absolute(ТекстHTML) Экспорт
	
	ТекстHTML = СтрЗаменить(ТекстHTML, "position: absolute;", "");
	ТекстHTML = СтрЗаменить(ТекстHTML, "position: absolute", "");	
	
	ТекстHTML = СтрЗаменить(ТекстHTML, "position:absolute;", "");
	ТекстHTML = СтрЗаменить(ТекстHTML, "position:absolute", "");	
	
КонецПроцедуры	

// Вернет по письму (по связям) - один связанный документ или их число строкой (если более 1). Или просто "", если ничего нет
//
// Параметры:
//  Письмо        - ДокументССылка.ВходящееПисьмо, ДокументССылка.ИсходящееПисьмо
//
// Возвращаемое значение
//    Строка, СправочникССылка.ДокументыПредприятия
Функция ДокументыНаОсновании(Письмо) Экспорт
	
	МассивОбъектовПолный = СвязиОбъектов.ПолучитьСвязанныеОбъекты(Письмо, Справочники.ТипыСвязей.ПисьмоОснование);
	МассивОбъектов = Новый Массив;
	Для Каждого СсылкаСвязи Из МассивОбъектовПолный Цикл
		
		Если ДелопроизводствоКлиентСервер.ЭтоДокументПредприятия(СсылкаСвязи) Тогда
			МассивОбъектов.Добавить(СсылкаСвязи);
		КонецЕсли;	
		
	КонецЦикла;	
	
	Если МассивОбъектов.Количество() = 0 Тогда
		Возврат "";
	ИначеЕсли МассивОбъектов.Количество() = 1 Тогда	
		Возврат МассивОбъектов[0];
	ИначеЕсли МассивОбъектов.Количество() > 1 Тогда	
		Возврат Строка(МассивОбъектов.Количество());
	КонецЕсли;	   
	
	Возврат "";
	
КонецФункции

// При работе с веб сервером вставляет картинки письма в тело HTML,
// иначе помещает их во временное хранилище и заменяет ссылки на временное хранилище.
//
Процедура ВставитьКартинкиВТекстHTML(
	ТекстHTML,
	Письмо,
	УникальныйИдентификаторФормы,
	ИдентификаторыКартинокПисьма = Неопределено,
	ВызовИзШаблонаПисьма = Ложь) Экспорт
	
	ФайлыПисьма = ПолучитьФайлыПисьма(
		Письмо,
		Ложь, // ФормироватьПредставлениеРазмера
		Ложь, // ВключатьПомеченныеНаУдаление
		Истина); // ТолькоСИдентификаторами
	
	Для каждого ФайлПисьма Из ФайлыПисьма Цикл
		
		ИсточникФайла = ФайлПисьма.ИДФайлаЭлектронногоПисьма;
		
		ВставлятьКакBase64 = Истина;
		
		Если ИдентификаторыКартинокПисьма <> Неопределено Тогда // вызов из карточки исх письма
			ВставлятьКакBase64 = Ложь;
		КонецЕсли;	
		
		Если ВызовИзШаблонаПисьма Тогда
			ВставлятьКакBase64 = Истина;
		КонецЕсли;	
		
		Если НРег(ФайлПисьма.ТекущаяВерсияРасширение) = "emz" Тогда
			ВставлятьКакBase64 = Ложь;
		КонецЕсли;	
		
		Если СтрЧислоВхождений(ТекстHTML, ИсточникФайла) > 0 Тогда
			
			Если ВставлятьКакBase64 Тогда
			
				ДвоичныеДанныеФайла = РаботаСФайламиВызовСервера.ПолучитьДвоичныеДанныеФайла(ФайлПисьма.Ссылка);
				Расширение = ФайлПисьма.ТекущаяВерсияРасширение;
				
				Если ПустаяСтрока(Расширение) Тогда
					Картинка = Новый Картинка(ДвоичныеДанныеФайла);
					Расширение = Строка(Картинка.Формат());
				КонецЕсли;
				
				СтрокаИсточника = "data:image/" + Расширение + ";base64," + Base64Строка(ДвоичныеДанныеФайла);
				ТекстHTML = СтрЗаменить(ТекстHTML, "cid:" + ИсточникФайла, СтрокаИсточника);
				ТекстHTML = СтрЗаменить(ТекстHTML, "CID:" + ИсточникФайла, СтрокаИсточника);
				Если СтрДлина(ИсточникФайла) > 18 Тогда
					ТекстHTML = СтрЗаменить(ТекстHTML, ИсточникФайла, СтрокаИсточника);
				КонецЕсли;	
				
				Если ИдентификаторыКартинокПисьма = Неопределено И ВызовИзШаблонаПисьма = Ложь Тогда
					ДобавитьСсылкиККартинкам(ТекстHTML, СтрокаИсточника, ФайлПисьма.Ссылка);
				КонецЕсли;	
				
			Иначе // тонкий клиент
				
				НавигационнаяСсылкаИнформационнойБазы = ПолучитьНавигационнуюСсылкуИнформационнойБазы();
				ВырезатьПорт(НавигационнаяСсылкаИнформационнойБазы);
				ВырезатьПараметры(НавигационнаяСсылкаИнформационнойБазы);
				НавигационнаяСсылка = "";
				
				Попытка
					НавигационнаяСсылка = РаботаСФайламиВызовСервера.ПолучитьНавигационнуюСсылкуВоВременномХранилище(
						ФайлПисьма.ТекущаяВерсия, // ВерсияСсылка
						УникальныйИдентификаторФормы); // ИдентификаторФормы
				Исключение
					Инфо = ИнформацияОбОшибке();
					ЗаписьЖурналаРегистрации(
						НСтр("ru = 'Ошибка вставки картинок в HTML'", Метаданные.ОсновнойЯзык.КодЯзыка),
						УровеньЖурналаРегистрации.Ошибка,,
						,
						ПодробноеПредставлениеОшибки(Инфо));
					Продолжить;
				КонецПопытки;	
				
				АбсолютнаяСсылкаНаКартинку = НавигационнаяСсылкаИнформационнойБазы + "/" + НавигационнаяСсылка;
				
				ТекстHTML = СтрЗаменить(ТекстHTML, "http:cid:" + ИсточникФайла, АбсолютнаяСсылкаНаКартинку); // коррекция ошибочного html
				ТекстHTML = СтрЗаменить(ТекстHTML, "cid:" + ИсточникФайла, АбсолютнаяСсылкаНаКартинку);
				ТекстHTML = СтрЗаменить(ТекстHTML, "CID:" + ИсточникФайла, АбсолютнаяСсылкаНаКартинку);
				Если СтрДлина(ИсточникФайла) > 18 Тогда
					ТекстHTML = СтрЗаменить(ТекстHTML, ИсточникФайла, АбсолютнаяСсылкаНаКартинку);
				КонецЕсли;	
				
				Если ИдентификаторыКартинокПисьма = Неопределено Тогда
					ДобавитьСсылкиККартинкам(ТекстHTML, АбсолютнаяСсылкаНаКартинку, ФайлПисьма.Ссылка);
				КонецЕсли;	
				
				Если ИдентификаторыКартинокПисьма <> Неопределено Тогда
					ОписаниеИсточника = Новый Структура("ИсточникФайла, НавигационнаяСсылка", ИсточникФайла, АбсолютнаяСсылкаНаКартинку);
					УдалитьДублиВИдентификаторахКартинокПисьма(ИдентификаторыКартинокПисьма, ИсточникФайла);
					ИдентификаторыКартинокПисьма.Добавить(ОписаниеИсточника);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Выделяет строку с тегом img - от "<" до ">" включительно
Функция ВыделитьТегImg(ТекстHTML, АбсолютнаяСсылкаНаКартинку) Экспорт
	
	ПозицияНачалаСсылки = СтрНайти(ТекстHTML, АбсолютнаяСсылкаНаКартинку);
	Если ПозицияНачалаСсылки = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ПозицияНачалаПоиска = ПозицияНачалаСсылки - 1;
	ПозицияНачалаТега = 0;
	
	Пока ПозицияНачалаПоиска > 1 И ПозицияНачалаПоиска > ПозицияНачалаСсылки - 250 Цикл
		
		Символ = Сред(ТекстHTML, ПозицияНачалаПоиска, 1);
		Если Символ = "<" Тогда
			ПозицияНачалаТега = ПозицияНачалаПоиска;
			Прервать;
		КонецЕсли;	
		
		ПозицияНачалаПоиска = ПозицияНачалаПоиска - 1;
		
	КонецЦикла;	
	
	Если ПозицияНачалаТега = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ПозицияОкончанияТегаHTML = РаботаСоСтроками.НайтиПосле(ТекстHTML, ">", ПозицияНачалаСсылки + СтрДлина(АбсолютнаяСсылкаНаКартинку));
	Если ПозицияОкончанияТегаHTML = -1 Тогда
		Возврат "";
	КонецЕсли;
	
	Если РаботаС_HTML.ПозицияНаходитсяВТеге(ТекстHTML, "A", ПозицияНачалаТега) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстТега = Сред(ТекстHTML, ПозицияНачалаТега, ПозицияОкончанияТегаHTML - ПозицияНачалаТега + 1);
	Возврат ТекстТега;
	
КонецФункции	

Процедура ВырезатьПараметры(НавигационнаяСсылкаИнформационнойБазы) Экспорт
	
	ПозПарам = СтрНайти(НавигационнаяСсылкаИнформационнойБазы, "?");
	Если ПозПарам > 0 Тогда
		НавигационнаяСсылкаИнформационнойБазы = Лев(НавигационнаяСсылкаИнформационнойБазы, ПозПарам - 1);
	КонецЕсли;
	
КонецПроцедуры	

Процедура ВырезатьПорт(НавигационнаяСсылкаИнформационнойБазы) Экспорт
	
	Если СтрНайти(НавигационнаяСсылкаИнформационнойБазы, "https://") <> 0 
		Или СтрНайти(НавигационнаяСсылкаИнформационнойБазы, "http://") <> 0 Тогда
		
		ПозицияДвоеточия = СтрНайти(НавигационнаяСсылкаИнформационнойБазы, ":", НаправлениеПоиска.СНачала, СтрДлина("https://")+1);
		Если ПозицияДвоеточия <> 0 Тогда 
			
			ПозицияСлеша = СтрНайти(НавигационнаяСсылкаИнформационнойБазы, "/", НаправлениеПоиска.СНачала, СтрДлина("https://")+1);
			Если ПозицияСлеша <> 0 И ПозицияДвоеточия < ПозицияСлеша Тогда
				
				НавигационнаяСсылкаИнформационнойБазы = 
					Лев(НавигационнаяСсылкаИнформационнойБазы, ПозицияДвоеточия - 1)
					+ Сред(НавигационнаяСсылкаИнформационнойБазы, ПозицияСлеша);
				
			КонецЕсли;		
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры	

// Формирует текст исходящего письма.
//
Функция СформироватьТекстИсходящегоПисьма(Основание, ТипТекста, Кодировка, ТипОтвета, ИспользованныйШаблон = Неопределено) Экспорт
	
	ТекстПисьма = "";
	ЭтоШаблонПисьма = Ложь;
	
	Если ДелопроизводствоКлиентСервер.ЭтоДокумент(Основание) Тогда
		
		ТекстПисьма = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "Содержание");
		
	ИначеЕсли ТипЗнч(Основание) = Тип("Структура")
		И Основание.Свойство("Тема")
		И Основание.Свойство("ТекстПисьма") Тогда 
		ТекстПисьма = Основание.ТекстПисьма;
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоПроект(Основание) Тогда
		
		ТекстПисьма = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "Описание");
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоКонтрольОбъектов(Основание) Тогда
		
		РеквизитыКонтроля = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание, "Описание, Предмет");
		ТекстПисьма = РеквизитыКонтроля.Описание;
		
		Если ЗначениеЗаполнено(РеквизитыКонтроля.Предмет) Тогда 
			Если ТипЗнч(РеквизитыКонтроля.Предмет) = Тип("СправочникСсылка.ВеткиПереписки") Тогда
				КорневоеПисьмо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
					РеквизитыКонтроля.Предмет, "КорневоеПисьмо");
				НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(КорневоеПисьмо);
			Иначе 
				НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(РеквизитыКонтроля.Предмет);
			КонецЕсли;
			
			ДобавитьЗначениеКСтрокеЧерезРазделитель(
				ТекстПисьма,
				Символы.ПС,
				НавигационнаяСсылка);
		КонецЕсли;
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоПроектнаяЗадача(Основание) Тогда
		
		ТекстПисьма = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "Описание");
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоМероприятие(Основание) Тогда
		
		ТекстПисьма = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "Описание");
		
	ИначеЕсли ТипЗнч(Основание) = Тип("СправочникСсылка.ШаблоныПисем") Тогда 
		
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание, 
			"ТипТекста, Ссылка, ТекстПисьмаHTMLХранилище, ТекстХранилище, Кодировка");
		
		ТекстПисьма = Справочники.ШаблоныПисем.ПолучитьПредставлениеСодержанияШаблона(Реквизиты);
		ЭтоШаблонПисьма = Истина;
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоЗадачаИсполнителя(Основание) Тогда
		
		ТекстПисьма = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "Описание");
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоФайл(Основание) Тогда
		
		ТекстПисьма = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "Описание");
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоСообщение(Основание) Тогда
		
		ТекстСообщения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ТекстСообщения");
		ТекстПисьма = ТекстСообщения + Символы.ПС + Символы.ПС + ПолучитьНавигационнуюСсылку(Основание);
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоЗаметка(Основание) Тогда
		
		ТекстСообщения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "СодержаниеТекст");
		ТекстПисьма = ТекстСообщения;
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоОтсутствие(Основание) Тогда
		
		ТекстПисьма = Отсутствия.ПолучитьПредставлениеОтсутствияДляТекстаПисьма(Основание);
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоУведомлениеПрограммы(Основание) Тогда
		
		ТекстПисьма = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "Описание");
		ТекстПисьма = РаботаС_HTML.ПолучитьПростойТекстИзHTML(ТекстПисьма);
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоБронь(Основание) Тогда
		
		ТекстПисьма = БронированиеПомещений.ПолучитьОписаниеБрони(Основание);
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ДействиеЗадачи") Тогда
		
		ДействиеОбъект = Основание.ПолучитьОбъект();
		ТекстПисьма = ДействиеОбъект.ОписаниеПростойТекст();
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.Задача") Тогда
		
		ЗадачаОбъект = Основание.ПолучитьОбъект();
		ТекстПисьма = ЗадачаОбъект.ОписаниеПростойТекст();
		
	ИначеЕсли ТипЗнч(Основание) = Тип("Массив") И Основание.Количество() > 0 Тогда
		
		ТекстПисьма = "";
		
		Для каждого Файл Из Основание Цикл
			
			Если Не ДелопроизводствоКлиентСервер.ЭтоФайл(Файл) Тогда
				Продолжить;
			КонецЕсли;
			
			ПредставлениеФайла = Строка(Файл) + " (" + Файл.Метаданные().ПредставлениеОбъекта + ")";
			НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(Файл);
			
			ДобавитьЗначениеКСтрокеЧерезРазделитель(
				ТекстПисьма,
				Символы.ПС + Символы.ПС,
				ПредставлениеФайла);
			
			ДобавитьЗначениеКСтрокеЧерезРазделитель(
				ТекстПисьма,
				Символы.ПС,
				НавигационнаяСсылка);
			
		КонецЦикла;
		
		Если Основание.Количество() = 1 Тогда
			ТекстПисьма = НСтр("ru = 'Основание:'") + Символы.ПС + Символы.ПС + ТекстПисьма;
		Иначе
			ТекстПисьма = НСтр("ru = 'Основания:'") + Символы.ПС + Символы.ПС + ТекстПисьма;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("Строка") Тогда
		
		ТекстПисьма = Основание;
		
	КонецЕсли;
	
	Если ЭтоШаблонПисьма Тогда 
		
		ТекстШаблона = ТекстПисьма;
		// Дата
		ТекущаяДата = ТекущаяДатаСеанса();
		ТекстШаблона = СтрЗаменить(ТекстШаблона, "[День]", Формат(ТекущаяДата, "ДФ=дд"));
		ТекстШаблона = СтрЗаменить(ТекстШаблона, "[Месяц]", Формат(ТекущаяДата, "ДФ=ММ"));
		ТекстШаблона = СтрЗаменить(ТекстШаблона, "[Год]", Формат(ТекущаяДата, "ДФ=гггг"));
		
		// Время
		ТекстШаблона = СтрЗаменить(ТекстШаблона, "[Час]", Формат(ТекущаяДата, "ДФ=ЧЧ"));
		ТекстШаблона = СтрЗаменить(ТекстШаблона, "[Минута]", Формат(ТекущаяДата, "ДФ=мм"));
		
		ТекстПисьма = ТекстШаблона;
		
	Иначе 
	
		Если ЗначениеЗаполнено(Основание) Тогда
			
			ШаблонПисьма = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПерсональнуюНастройку("ПодписьПриОтветеИПересылке");
			
		Иначе
			
			ШаблонПисьма = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПерсональнуюНастройку("ПодписьДляНовыхПисем");
			
		КонецЕсли;
		
		Если ТипЗнч(ШаблонПисьма) = Тип("СправочникСсылка.ШаблоныТекстов")
			И ЗначениеЗаполнено(ШаблонПисьма) Тогда
			
			ИспользованныйШаблон = ШаблонПисьма;
			Подпись = Справочники.ШаблоныТекстов.ПолучитьТекстШаблона(ШаблонПисьма);
			
			ТипТекстаШаблона = ШаблонПисьма.ТипТекста;
			Если Не ЗначениеЗаполнено(ТипТекстаШаблона) Тогда
				ТипТекстаШаблона = Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст;
			КонецЕсли;	
			
			Если ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст 
				И ТипТекстаШаблона  = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
				Подпись = РаботаС_HTML.ПолучитьТекстИзHTML(Подпись);
			КонецЕсли;	
			
			Если ПустаяСтрока(ТекстПисьма) Тогда
				ТекстПисьма = Подпись;
			Иначе
				ТекстПисьма = ТекстПисьма + Символы.ПС + Подпись;
			КонецЕсли;
			
		Иначе	
			
			Если ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
				ТекстПисьма = РаботаС_HTML.ПолучитьHTMLИзТекста(ТекстПисьма, Кодировка);
			КонецЕсли;
		
		КонецЕсли;
		
		Если ПустаяСтрока(ТекстПисьма) Тогда
			ТекстПисьма = Символы.ПС;
		КонецЕсли;
		
		Если ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML И ТипТекстаШаблона  = Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст Тогда
			ТекстПисьма = РаботаС_HTML.ПолучитьHTMLИзТекста(ТекстПисьма, Кодировка);
		КонецЕсли;
	
	КонецЕсли;
	
	Если ВстроеннаяПочтаКлиентСервер.ЭтоПисьмо(Основание) Тогда
		
		ВставлятьТекстИсходногоПисьмаПриОтвете =
			ВстроеннаяПочтаСерверПовтИсп.ПолучитьПерсональнуюНастройку("ВставлятьТекстИсходногоПисьмаПриОтвете");
		
		Если ТипОтвета = Перечисления.ТипыОтвета.ПересылкаПисьма
			ИЛИ (ТипОтвета = Перечисления.ТипыОтвета.ОтветНаПисьмо
				И ВставлятьТекстИсходногоПисьмаПриОтвете) Тогда
			
			ДобавитьТекстИсходногоПисьма(
				ТекстПисьма,
				Основание,
				ТипТекста,
				ТипОтвета);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПараметрыСеанса.ЭтоМобильныйКлиент Тогда 
		ТекстПисьма = РаботаС_HTML.ПатчитьТекстИсходящегоПисьма(ТекстПисьма);
	КонецЕсли;
	
	Удалить_position_absolute(ТекстПисьма);
	
	Возврат ТекстПисьма;
	
КонецФункции


/////////////////////////////////////////////////////////////////////////////////////////
// РАБОТА С ПРЕДМЕТАМИ И ПРОЕКТАМИ ПИСЕМ

// Возвращает массив корневых писем в переписке по предмету.
// Корневыми считаются письма или документы (входящие, исходящие)
// которые либо первые в переписке либо их основание содержит переписку
// по другому предмету.
//
Функция ПолучитьКорневыеПисьмаПоПредмету(Предмет) Экспорт
	
	ТипыСвязи = Новый Массив;
	ТипыСвязи.Добавить(Справочники.ТипыСвязей.ОтправленВОтветНа);
	ТипыСвязи.Добавить(Справочники.ТипыСвязей.ОтправленВОтветНаПисьмо);
	ТипыСвязи.Добавить(Справочники.ТипыСвязей.ПисьмоОтправленоВОтветНа);
	ТипыСвязи.Добавить(Справочники.ТипыСвязей.ПолученВОтветНа);
	ТипыСвязи.Добавить(Справочники.ТипыСвязей.ПолученВОтветНаПисьмо);
	ТипыСвязи.Добавить(Справочники.ТипыСвязей.ПолученоВОтветНаДокумент);
	ТипыСвязи.Добавить(Справочники.ТипыСвязей.ПолученоВОтветНаПисьмо);
	ТипыСвязи.Добавить(Справочники.ТипыСвязей.ПересылкаПисьма);
	
	ПравоДоступаДокументыЧтение = ПравоДоступа("Чтение", Метаданные.Справочники.ДокументыПредприятия);
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЭлектроннаяПочта.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ТаблицаПисемПоПредмету
		|ИЗ
		|	Документ.ВходящееПисьмо КАК ЭлектроннаяПочта
		|ГДЕ
		|	ЭлектроннаяПочта.Предмет = &Предмет
		|	И НЕ ЭлектроннаяПочта.ПометкаУдаления
		|
		| ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЭлектроннаяПочта.Ссылка
		|ИЗ
		|	Документ.ИсходящееПисьмо КАК ЭлектроннаяПочта
		|ГДЕ
		|	ЭлектроннаяПочта.Предмет = &Предмет
		|	И НЕ ЭлектроннаяПочта.ПометкаУдаления
		|";
		
	Если ТипЗнч(Предмет) = Тип("СправочникСсылка.Проекты") Тогда 
		Запрос.Текст = Запрос.Текст + 
		
		"ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЭлектроннаяПочта.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ВходящееПисьмо КАК ЭлектроннаяПочта
		|ГДЕ
		|	ЭлектроннаяПочта.Проект = &Предмет
		|	И НЕ ЭлектроннаяПочта.ПометкаУдаления
		|
		| ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЭлектроннаяПочта.Ссылка
		|ИЗ
		|	Документ.ИсходящееПисьмо КАК ЭлектроннаяПочта
		|ГДЕ
		|	ЭлектроннаяПочта.Проект = &Предмет
		|	И НЕ ЭлектроннаяПочта.ПометкаУдаления
		|";
		
	КонецЕсли;		
		
	Если ПравоДоступаДокументыЧтение Тогда	
		
		Запрос.Текст = Запрос.Текст + 
	
		"ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВсеДокументы.Ссылка
		|ИЗ
		|	Справочник.ДокументыПредприятия КАК ВсеДокументы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СвязиОбъектов КАК СвязиОбъектов
		|		ПО ВсеДокументы.Ссылка = СвязиОбъектов.Объект
		|			И (СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ПредметПереписки))
		|			И (СвязиОбъектов.СвязанныйОбъект = &Предмет)
		|ГДЕ
		|	НЕ ВсеДокументы.ПометкаУдаления
		|	И ВсеДокументы.ВидДокумента.ЯвляетсяВходящейКорреспонденцией
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВсеДокументы.Ссылка
		|ИЗ
		|	Справочник.ДокументыПредприятия КАК ВсеДокументы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СвязиОбъектов КАК СвязиОбъектов
		|		ПО ВсеДокументы.Ссылка = СвязиОбъектов.Объект
		|			И (СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ПредметПереписки))
		|			И (СвязиОбъектов.СвязанныйОбъект = &Предмет)
		|ГДЕ
		|	НЕ ВсеДокументы.ПометкаУдаления
		|	И ВсеДокументы.ВидДокумента.ЯвляетсяИсходящейКорреспонденцией
		|";
		
		
		Если ТипЗнч(Предмет) = Тип("СправочникСсылка.Проекты") Тогда 
			
			Запрос.Текст = Запрос.Текст + 
	
			"ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ВсеДокументы.Ссылка
			|ИЗ
			|	Справочник.ДокументыПредприятия КАК ВсеДокументы
			|ГДЕ ВсеДокументы.Проект = &Предмет 
			|	И НЕ ВсеДокументы.ПометкаУдаления
			|	И ВсеДокументы.ВидДокумента.ЯвляетсяВходящейКорреспонденцией
			|	И ВсеДокументы.ВидДокумента.ЯвляетсяИсходящейКорреспонденцией";
			
		КонецЕсли;	
		
	КонецЕсли;	
		
	Запрос.Текст = Запрос.Текст + 
		";
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаПисемПоПредмету.Ссылка КАК Ссылка,
		|	СвязиОбъектов.СвязанныйОбъект
		|ПОМЕСТИТЬ СвязанныеПисьмаПоПредмету
		|ИЗ
		|	ТаблицаПисемПоПредмету КАК ТаблицаПисемПоПредмету
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвязиОбъектов КАК СвязиОбъектов
		|		ПО ТаблицаПисемПоПредмету.Ссылка = СвязиОбъектов.Объект
		|			И (СвязиОбъектов.ТипСвязи В (&ТипыСвязи))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СвязанныеПисьмаПоПредмету.Ссылка КАК Ссылка
		|ИЗ
		|	СвязанныеПисьмаПоПредмету КАК СвязанныеПисьмаПоПредмету
		|		ЛЕВОЕ СОЕДИНЕНИЕ СвязанныеПисьмаПоПредмету КАК СвязанныеДокументы
		|		ПО СвязанныеПисьмаПоПредмету.СвязанныйОбъект = СвязанныеДокументы.Ссылка
		|ГДЕ
		|	СвязанныеДокументы.Ссылка ЕСТЬ NULL ";
	
		
	Запрос.УстановитьПараметр("ТипыСвязи", ТипыСвязи);
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив корневых писем в переписке по предмету.
//
Функция ПолучитьИнформациюКорневыхПисемПоПредмету(Предмет) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЭлектроннаяПочта.Ссылка КАК Ссылка,
		|	ЭлектроннаяПочта.Дата,
		|	ЭлектроннаяПочта.Тема
		|ИЗ
		|	ЖурналДокументов.ЭлектроннаяПочта КАК ЭлектроннаяПочта
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвязиОбъектов КАК СвязиОбъектов
		|		ПО ЭлектроннаяПочта.Ссылка = СвязиОбъектов.Объект
		|			И (СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ПисьмоОтправленоВОтветНа)
		|				ИЛИ СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ПолученоВОтветНаДокумент)
		|				ИЛИ СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ПолученоВОтветНаПисьмо))
		|ГДЕ
		|	ЭлектроннаяПочта.Предмет = &Предмет
		|	И СвязиОбъектов.Объект ЕСТЬ NULL 
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭлектроннаяПочта.Дата";
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив писем в переписке по связям ОтправленОтвет ПолученОтвет ПересланоПисьмом НаОснованииПисьма
//
Функция ПолучитьИнформациюПисемПоСвязям(Предмет) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЭлектроннаяПочта.Ссылка КАК Ссылка,
		|	ЭлектроннаяПочта.Дата,
		|	ЭлектроннаяПочта.Тема
		|ИЗ
		|	ЖурналДокументов.ЭлектроннаяПочта КАК ЭлектроннаяПочта
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвязиОбъектов КАК СвязиОбъектов
		|		ПО ЭлектроннаяПочта.Ссылка = СвязиОбъектов.СвязанныйОбъект
		|			И (СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ОтправленОтвет)
		|				ИЛИ СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ПолученОтвет)
		|				ИЛИ СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ПересланоПисьмом)
		|				ИЛИ СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ПеренаправленоПисьмом)
		|				ИЛИ СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ПисьмоОснование)
		|				ИЛИ СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.НаОснованииПисьма))
		|ГДЕ
		|	СвязиОбъектов.Объект = &Предмет
		|	И ЭлектроннаяПочта.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭлектроннаяПочта.Дата";
		
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив подчиненных писем в переписке по предмету.
//
Функция ПолучитьПодчиненныеПисьмаПоПредмету(Письмо, Предмет) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СвязиОбъектов.СвязанныйОбъект КАК Ссылка
		|ИЗ
		|	РегистрСведений.СвязиОбъектов КАК СвязиОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЖурналДокументов.ЭлектроннаяПочта КАК ЭлектроннаяПочта
		|		ПО (ЭлектроннаяПочта.Ссылка = СвязиОбъектов.СвязанныйОбъект)
		|			И (СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ОтправленОтвет)
		|				ИЛИ СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ПолученОтвет)
		|				ИЛИ СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ПеренаправленоПисьмом)
		|				ИЛИ СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ПересланоПисьмом))
		|ГДЕ
		|	СвязиОбъектов.Объект = &Письмо
		|	И ЭлектроннаяПочта.Предмет = &Предмет
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭлектроннаяПочта.Дата");
	Запрос.УстановитьПараметр("Письмо", Письмо);
	Запрос.УстановитьПараметр("Предмет", Предмет);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции


/////////////////////////////////////////////////////////////////////////////////////////
// РАБОТА С УЧЕТНЫМИ ЗАПИСЯМИ

// Возвращает признак наличия писем по учетной записи.
//
Функция ЕстьПисьмаУчетнойЗаписи(УчетнаяЗапись) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЭлектроннаяПочта.Ссылка
		|ИЗ
		|	ЖурналДокументов.ЭлектроннаяПочта КАК ЭлектроннаяПочта
		|ГДЕ
		|	ЭлектроннаяПочта.УчетнаяЗапись = &УчетнаяЗапись");
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Возвращает учетную запись или Неопределено.
//
Функция ПолучитьУчетнуюЗаписьДляОтправки() Экспорт
	
	ОсновнаяУчетнаяЗапись =
		ВстроеннаяПочтаСерверПовтИсп.ПолучитьПерсональнуюНастройку("ОсновнаяУчетнаяЗапись");
	
	Если Почта.ЭтоУчетнаяЗапись(ОсновнаяУчетнаяЗапись)
		И ЗначениеЗаполнено(ОсновнаяУчетнаяЗапись) Тогда
		Возврат ОсновнаяУчетнаяЗапись;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ИспользоватьДляОтправки", Истина);
	ПараметрыОтбора.Вставить("ВариантИспользования", Перечисления.ВариантыИспользованияПочты.Встроенная);
	УчетныеЗаписи = Почта.ПолучитьУчетныеЗаписиЭлектроннойПочты(ПараметрыОтбора);
	Если УчетныеЗаписи.Количество() = 1 Тогда
		Возврат УчетныеЗаписи[0];
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

/////////////////////////////////////////////////////////////////////////////////////////
// РАБОТА С СОХРАНЕНИМ ПИСЬМА НА ДИСК

// Формирует данные письма для сохранения на диск
//
Функция ПолучитьДанныеПисьмаДляСохраненияТекста(Письмо, УникальныйИдентификаторФормы) Экспорт
	
	Расширение = "txt";
	ФорматСохраненияПисьма = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПерсональнуюНастройку("ФорматСохраненияПисьма");
	
	Если ФорматСохраненияПисьма = ПредопределенноеЗначение("Перечисление.ФорматыСохраненияПисьма.HTML") Тогда
		Расширение = "html";
		Тема = Письмо.Тема;
		ТекстПисьма = СформироватьHTMLПредставлениеПисьма(
			Письмо, УникальныйИдентификаторФормы, Истина,,,,,Истина);
	Иначе
	
		ПредставлениеОтправителя = "";
		ПредставлениеДатыСтрока = "";
		
		Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(Письмо)
			Или ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(Письмо) Тогда
			
			ПисьмоОбъект = Письмо.ПолучитьОбъект();
			
			Тема = Письмо.Тема;
			КодировкаПисьма = Письмо.Кодировка;
			
			ПростойТекстПисьма = ПисьмоОбъект.ПолучитьТекстовоеПредставлениеСодержанияПисьма();
			
			Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(Письмо) Тогда
				
				ПредставлениеОтправителя = ПолучитьПредставлениеИКонтактАдресата(
					Письмо.ОтправительАдресат).Представление;
				
				ПредставлениеДатыСтрока = "Получено: " + Формат(Письмо.ДатаПолучения, "ДФ='dd.MM.yyyy HH:mm'");
				
			ИначеЕсли ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(Письмо) Тогда
				
				ДанныеУчетнойЗаписи = Почта.ПолучитьДанныеУчетнойЗаписи(Письмо.УчетнаяЗапись);
				ПредставлениеОтправителя = ДанныеУчетнойЗаписи.ПредставлениеАдресаЭлектроннойПочты;
				ПредставлениеДатыСтрока = "Отправлено: " + Формат(Письмо.ДатаОтправки, "ДФ='dd.MM.yyyy HH:mm'");
				
			КонецЕсли;
			
			ПолучателиПисьма = Письмо.ПолучателиПисьма;
			ПолучателиКопий = Письмо.ПолучателиКопий;
			
		ИначеЕсли ТипЗнч(Письмо) = Тип("Структура") Тогда
			
			СтруктураПисьма = Письмо;
			Тема = СтруктураПисьма.ТемаПисьма;
			КодировкаПисьма = СтруктураПисьма.Кодировка;
			ПростойТекстПисьма = СтруктураПисьма.ТекстПисьма;
			
			ДанныеУчетнойЗаписи = Почта.ПолучитьДанныеУчетнойЗаписи(СтруктураПисьма.УчетнаяЗапись);
			ПредставлениеОтправителя = ДанныеУчетнойЗаписи.ПредставлениеАдресаЭлектроннойПочты;
			ПредставлениеДатыСтрока = "Отправлено: " + Формат(СтруктураПисьма.ДатаОтправки, "ДФ='dd.MM.yyyy HH:mm'");
			
			Получатели = СтруктураПисьма.Получатели;
			
			ПолучателиПисьма = Новый ТаблицаЗначений;
			ПолучателиПисьма.Колонки.Добавить("Адресат");
			
			ПолучателиКопий = Новый ТаблицаЗначений;
			ПолучателиКопий.Колонки.Добавить("Адресат");
			
			ПолучателиОтвета = Новый ТаблицаЗначений;
			ПолучателиОтвета.Колонки.Добавить("Адресат");
			
			Для каждого Строка Из Получатели Цикл
				
				Если Не ЗначениеЗаполнено(Строка.Представление) Тогда
					Продолжить;
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(Строка.Адресат) Тогда
					
					РазложенныйАдрес = РаботаСоСтроками.РазложитьПредставлениеАдресаЭлектроннойПочты(Строка.Представление);
					
					Если ПустаяСтрока(РазложенныйАдрес.Адрес) Тогда
						Продолжить;
					КонецЕсли;
					
					Если РаботаСоСтроками.ЭтоАдресЭлектроннойПочты(РазложенныйАдрес.Адрес) Тогда
						
						Строка.Адресат = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПочтовогоАдресата(
							РазложенныйАдрес.Адрес, Строка.Представление);
						
					Иначе
						
						Строка.Адресат = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПочтовогоАдресата(
							Строка.Представление, Строка.Представление);
						
					КонецЕсли;
					
				КонецЕсли;
				
				Если Строка.ТипАдреса = Перечисления.ТипыАдресатов.Кому Тогда
					НоваяСтрока = ПолучателиПисьма.Добавить();
				ИначеЕсли Строка.ТипАдреса = Перечисления.ТипыАдресатов.Копия Тогда
					НоваяСтрока = ПолучателиКопий.Добавить();
				ИначеЕсли Строка.ТипАдреса = Перечисления.ТипыАдресатов.ОбратныйАдрес Тогда
					НоваяСтрока = ПолучателиОтвета.Добавить();
				Иначе
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока.Адресат = Строка.Адресат;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ПредставлениеПолучателей = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Кому: %1'"),
			ТаблицаПолучателейВСтроку(ПолучателиПисьма));
		Если ЗначениеЗаполнено(ПолучателиКопий) Тогда
			ПредставлениеПолучателей = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1
	                  |Копии: %2'"),
				ПредставлениеПолучателей,
				ТаблицаПолучателейВСтроку(ПолучателиКопий));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолучателиОтвета) Тогда
			Если ПолучателиОтвета.Количество() = 1 Тогда
				ПредставлениеПолучателей = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%1
		                  |Обратный адрес: %2'"),
					ПредставлениеПолучателей,
					ТаблицаПолучателейВСтроку(ПолучателиОтвета));
			Иначе
				ПредставлениеПолучателей = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%1
		                  |Обратные адреса: %2'"),
					ПредставлениеПолучателей,
					ТаблицаПолучателейВСтроку(ПолучателиОтвета));
			КонецЕсли;
		КонецЕсли;
		Шапка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru =
				|'От: %1
				|%2
				|%3
				|Тема: %4
				|'"),
			ПредставлениеОтправителя,
			ПредставлениеДатыСтрока,
			ПредставлениеПолучателей,
			Тема);
		
		Кодировка = ПолучитьПерсональнуюНастройку("КодировкаПриСохраненииПисем");
		Если Не ЗначениеЗаполнено(Кодировка) Тогда
			Кодировка = КодировкаПисьма;
		КонецЕсли;
		
		Ширина = ПолучитьПерсональнуюНастройку("ШиринаСтрокиПриСохранении");
		
		ТекстПисьма = Шапка + Символы.ПС + ПростойТекстПисьма;
		
		Если ЗначениеЗаполнено(Ширина) Тогда
			СимволПереписки = ВстроеннаяПочтаСерверПовтИсп.ПолучитьСимволЦитированияВПереписке();
			ТекстПисьма = РаботаСоСтроками.ПолучитьСтрокуВФормате(ТекстПисьма, Ширина, СимволПереписки, Истина);
		КонецЕсли;
	
	КонецЕсли;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("txt");
	
	Попытка
		
		ЗаписьТекста = Новый ЗаписьТекста(ИмяВременногоФайла, Кодировка);
		ЗаписьТекста.Записать(ТекстПисьма);
		ЗаписьТекста.Закрыть();
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла);
		Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификаторФормы);
		
		УдалитьФайлы(ИмяВременногоФайла);
		
	Исключение
		
		УдалитьФайлы(ИмяВременногоФайла);
		
		ВызватьИсключение;
		
	КонецПопытки;
	
	ПутьВыбора = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиПрограммы", "ПапкаДляСохраненияПисем");
	
	ДанныеПисьма = Новый Структура;
	ДанныеПисьма.Вставить("Тема", Тема);
	ДанныеПисьма.Вставить("АдресФайла", Адрес);
	ДанныеПисьма.Вставить("ПутьВыбора", ПутьВыбора);
	ДанныеПисьма.Вставить("Кодировка", Кодировка);
	ДанныеПисьма.Вставить("ТекстПисьма", ТекстПисьма);
	ДанныеПисьма.Вставить("Расширение", Расширение);
	
	Возврат ДанныеПисьма;
	
КонецФункции


/////////////////////////////////////////////////////////////////////////////////////////
// ПРЕФИКСЫ ПРИ ОТВЕТАХ И ПЕРЕСЫЛКАХ

// Возвращает префикса для темы в ответном письме.
// Значение по умолчанию: "Re:"
//
Функция ПолучитьПрефиксДляОтветныхПисем() Экспорт
	
	ПрефиксДляОтветныхПисем = СокрЛП(Константы.ПрефиксДляОтветныхПисем.Получить());
	Если ПустаяСтрока(ПрефиксДляОтветныхПисем) Тогда
		ПрефиксДляОтветныхПисем = "Re:";
	КонецЕсли;
	
	Возврат ПрефиксДляОтветныхПисем;
	
КонецФункции

// Устанавливает настройку префикса для темы в ответном письме.
//
Процедура УстановитьПрефиксДляОтветныхПисем(Знач Префикс) Экспорт
	
	Если ПустаяСтрока(Префикс) Тогда
		Префикс = "Re:";
	КонецЕсли;
	Константы.ПрефиксДляОтветныхПисем.Установить(СокрЛп(Префикс));
	
КонецПроцедуры

// Возвращает префикс для темы в пересылаемом письме.
// Значение по умолчанию: "Fw:"
//
Функция ПолучитьПрефиксДляПересылаемыхПисем() Экспорт
	
	ПрефиксДляПересылаемыхПисем = СокрЛП(Константы.ПрефиксДляПересылаемыхПисем.Получить());
	Если ПустаяСтрока(ПрефиксДляПересылаемыхПисем) Тогда
		ПрефиксДляПересылаемыхПисем = "Fw:";
	КонецЕсли;
	
	Возврат ПрефиксДляПересылаемыхПисем;
	
КонецФункции

// Устанавливает настройку префикса для темы в пересылаемом письме.
//
Процедура УстановитьПрефиксДляПересылаемыхПисем(Знач Префикс) Экспорт
	
	Если ПустаяСтрока(Префикс) Тогда
		Префикс = "Fw:";
	КонецЕсли;
	Константы.ПрефиксДляПересылаемыхПисем.Установить(СокрЛП(Префикс));
	
КонецПроцедуры

// Получает данные для автоподбора в списке получателей для исх письма и формы пересылки.
Функция ПолучитьДанныеВыбораДляЭлектронногоПисьма(Текст, ТекущийПользователь, ЭтоВебКлиент,
	ИскатьПоГруппам = Ложь) Экспорт 
	
	ИспользоватьСвойСписокДляАвтоподбора = 
		ВстроеннаяПочтаСервер.ПолучитьПерсональнуюНастройку("ИспользоватьСвойСписокДляАвтоподбора");
	ТекстТаблицыАвтоподбораПользователя = ТекстЗапросаВычисленияАдресатовДляАвтоподбора();
	ТекстПоляВходитВСписокАвтоподбораПользователя = 
		"	ВЫБОР
		|		КОГДА НЕ СписокАвтоподбораПользователя.Адресат ЕСТЬ NULL 
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ВходитВСписокАвтоподбораПользователя";
				
	ТекстДополнительнойТаблицы = 
		"ЛЕВОЕ СОЕДИНЕНИЕ ("
		+ ТекстТаблицыАвтоподбораПользователя
		+ ") КАК СписокАвтоподбораПользователя
			|ПО СведенияОбАдресатах.Контакт = СписокАвтоподбораПользователя.Адресат";
			
	ВозвращаемыйСписок = Новый СписокЗначений;
	ВозвращаемыйСписокАвтоподбораПользователя = Новый СписокЗначений;
	
	МассивСлов = Новый Массив;
	МассивСлов.Добавить(Текст);
		
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	АдресатыПочтовыхСообщений.Адрес КАК Адрес,
		|	АдресатыПочтовыхСообщений.Ссылка КАК ПочтовыйАдресат,
		|	ЕСТЬNULL(СведенияОбАдресатах.Контакт, НЕОПРЕДЕЛЕНО) КАК Контакт,
		|	ЕСТЬNULL(СведенияОбАдресатах.Представление, АдресатыПочтовыхСообщений.Наименование) КАК Представление,
		|	ЛичныеАдресаты.Сотрудник,
		|	ЕСТЬNULL(ИспользованиеАдресатов.ДатаПоследнегоИспользования, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПоследнегоИспользования,
		|	ЛОЖЬ КАК ВходитВСписокАвтоподбораПользователя
		|ИЗ
		|	Справочник.АдресатыПочтовыхСообщений КАК АдресатыПочтовыхСообщений
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИспользованиеАдресатовПользователями КАК ИспользованиеАдресатов
		|		ПО АдресатыПочтовыхСообщений.Ссылка = ИспользованиеАдресатов.Адресат
		|			И (ИспользованиеАдресатов.Пользователь = &ТекущийПользователь)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЛичныеАдресаты КАК ЛичныеАдресаты
		|			ПО СведенияОбАдресатах.Контакт = ЛичныеАдресаты.Ссылка
		|		ПО (СведенияОбАдресатах.АдресатСообщения = АдресатыПочтовыхСообщений.Ссылка)
		|			И (СведенияОбАдресатах.Активна)
		|		ЛЕВОЕ СОЕДИНЕНИЕ &ТаблицаАвтоподбораПользователя КАК ТаблицаАвтоподбораПользователя ПО ИСТИНА
		|ГДЕ
		|	(СведенияОбАдресатах.Представление ПОДОБНО &Представление
		|			ИЛИ АдресатыПочтовыхСообщений.Адрес ПОДОБНО &Представление
		|			ИЛИ АдресатыПочтовыхСообщений.Наименование ПОДОБНО &Представление)
		|	И (ЛичныеАдресаты.Сотрудник ЕСТЬ NULL 
		|			ИЛИ ЛичныеАдресаты.Сотрудник В (&СотрудникиПользователя))
		|	И НЕ(ИспользованиеАдресатов.ДатаПоследнегоИспользования = ДАТАВРЕМЯ(1, 1, 1)
		|				И СведенияОбАдресатах.Представление ЕСТЬ NULL )
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаПоследнегоИспользования УБЫВ";
		
	Если ИспользоватьСвойСписокДляАвтоподбора Тогда
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса,
			"ЛОЖЬ КАК ВходитВСписокАвтоподбораПользователя",
			ТекстПоляВходитВСписокАвтоподбораПользователя);
			
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса,
			"ЛЕВОЕ СОЕДИНЕНИЕ &ТаблицаАвтоподбораПользователя КАК ТаблицаАвтоподбораПользователя ПО ИСТИНА",
			ТекстДополнительнойТаблицы);
	Иначе
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса,
			"ЛЕВОЕ СОЕДИНЕНИЕ &ТаблицаАвтоподбораПользователя КАК ТаблицаАвтоподбораПользователя ПО ИСТИНА",
			"");
	КонецЕсли;
		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Представление", Текст + "%");
	Запрос.УстановитьПараметр("ТекущийПользователь", ПользователиКлиентСервер.ТекущийПользователь());
	Запрос.УстановитьПараметр("СотрудникиПользователя",
		Сотрудники.ПользовательИЕгоСотрудники());

	Результат = Запрос.Выполнить();
	Если Результат.Пустой() И ВозвращаемыйСписок.Количество() = 0
		И ИскатьПоГруппам = Ложь Тогда
		Возврат ВозвращаемыйСписок;
	КонецЕсли;
	
	МаксимальноеКоличествоЭлементовВыбора = 22;
	КоличествоЭлементовВыбора = 0;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.Контакт) 
			И ТипЗнч(Выборка.Контакт) = Тип("СправочникСсылка.Пользователи") 
			И Выборка.Контакт.Недействителен Тогда
			Продолжить;
		КонецЕсли;
		
		Адрес = Выборка.Адрес;
		Если Не ЗначениеЗаполнено(Адрес) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не РаботаСоСтроками.ЭтоАдресЭлектроннойПочты(Адрес) Тогда
			Продолжить;
		КонецЕсли;
		
		ЕстьДубликат = Ложь;
		
		Для каждого Строка Из ВозвращаемыйСписок Цикл
			Если Строка.Значение.Адресат = Выборка.ПочтовыйАдресат 
				Тогда
				
				ЕстьДубликат = Истина;
				Если ТипЗнч(Выборка.Контакт ) = Тип("СправочникСсылка.Пользователи")
					Или ТипЗнч(Выборка.Контакт ) = Тип("СправочникСсылка.Сотрудники") Тогда
					Строка.Значение.Контакт = Выборка.Контакт;
				КонецЕсли;	
				
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьДубликат Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначениеВыбора = Новый Структура;
		ЗначениеВыбора.Вставить("Адресат", Выборка.ПочтовыйАдресат);
		ЗначениеВыбора.Вставить("Адрес", Выборка.ПочтовыйАдресат.Адрес);
		
		ЗначениеВыбора.Вставить("Контакт", Выборка.Контакт);
		Если ТипЗнч(ЗначениеВыбора.Контакт) = Тип("СправочникСсылка.Пользователи") Тогда
			ЗначениеВыбора.Контакт = СотрудникиВызовСервера.ОсновнойСотрудникПользователя(ЗначениеВыбора.Контакт);
		КонецЕсли;	
		
		Представление = Выборка.Представление;
		
		Если ЭтоВебКлиент Тогда
			Представление = СтрЗаменить(Представление, "<", "(");
			Представление = СтрЗаменить(Представление, ">", ")");
		КонецЕсли;	
		
		ЗначениеВыбора.Вставить("Представление", Представление);
		ЗначениеВыбора.Вставить("Внешний", ЭтоВнешнийАдресат(ЗначениеВыбора.Адресат));
		ЗначениеВыбора.Вставить("ВидМаршрутизации", ЗначениеВыбора.Адресат.ВидМаршрутизации);
		
		ФорматированноеПредставление = ПолучитьФорматированноеПредставление(Представление, МассивСлов);
		
		Если Выборка.ВходитВСписокАвтоподбораПользователя Тогда
			ВозвращаемыйСписокАвтоподбораПользователя.Добавить(ЗначениеВыбора, ФорматированноеПредставление);
		Иначе	
			ВозвращаемыйСписок.Добавить(ЗначениеВыбора, ФорматированноеПредставление);
		КонецЕсли;
		
		КоличествоЭлементовВыбора = КоличествоЭлементовВыбора + 1;
		
		Если КоличествоЭлементовВыбора >= МаксимальноеКоличествоЭлементовВыбора Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ВозвращаемыйСписокАвтоподбораПользователя.Количество() > 0 Тогда
		
		ДополнитьДанныеВыбораОписаниемОтсутствий(ВозвращаемыйСписокАвтоподбораПользователя);
		
		Возврат ВозвращаемыйСписокАвтоподбораПользователя;
		
	Иначе	
		
		ЧислоГрупп = МаксимальноеКоличествоЭлементовВыбора - ВозвращаемыйСписок.Количество();
		
		Если ИскатьПоГруппам = Истина И ЧислоГрупп > 0 Тогда
			
			МассивСловДляГрупп = Новый Массив;
			МассивСловДляГрупп.Добавить(Текст);
			
			УникальныеПредставления = Новый Соответствие;
			
			ЗапросГрупп = Новый Запрос;
			ЗапросГрупп.Текст = ПолучитьЗапросГрупп(); // Текст
			ЗапросГрупп.УстановитьПараметр("Наименование", "%" + Текст + "%");
			
			ЗапросГрупп.УстановитьПараметр("СотрудникиПользователя",
				Сотрудники.ПользовательИЕгоСотрудники());
			
			ЗапросГрупп.УстановитьПараметр("Родитель", 
				Справочники.ГруппыКонтактовПользователей.ПустаяСсылка());
			
			ВыборкаГрупп = ЗапросГрупп.Выполнить().Выбрать();
			Пока ВыборкаГрупп.Следующий() Цикл
				
				Если ТипЗнч(ВыборкаГрупп.Ссылка) = Тип("СправочникСсылка.РабочиеГруппы") Или
					ТипЗнч(ВыборкаГрупп.Ссылка) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
					
					Найдено = УникальныеПредставления.Получить(ВыборкаГрупп.Наименование);
					Если Найдено <> Неопределено Тогда
						Продолжить;
					Иначе
						УникальныеПредставления[ВыборкаГрупп.Наименование] = ВыборкаГрупп.Ссылка;
					КонецЕсли;	
					
				КонецЕсли;	
				
				ЗначениеВыбора = Новый Структура(
					"Адресат, Контакт, Адрес, Внешний, ВидМаршрутизации, ЭтоГруппа, Представление");
				ЗначениеВыбора.Внешний = Ложь;
				ЗначениеВыбора.ЭтоГруппа = Истина;
				ЗначениеВыбора.Адресат = ВыборкаГрупп.Ссылка;
				
				ИмяТипа = ПолучитьИмяТипа(ВыборкаГрупп.Ссылка);
				
				Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"%1 (%2)", ВыборкаГрупп.Наименование, ИмяТипа);
				
				Если ЭтоВебКлиент Тогда
					Представление = СтрЗаменить(Представление, "<", "(");
					Представление = СтрЗаменить(Представление, ">", ")");
				КонецЕсли;
				ЗначениеВыбора.Представление = Представление;
				
				ФорматированноеПредставление = ПолучитьФорматированноеПредставление(Представление, МассивСловДляГрупп);
				
				ВозвращаемыйСписок.Добавить(ЗначениеВыбора, ФорматированноеПредставление);
				
				КоличествоЭлементовВыбора = КоличествоЭлементовВыбора + 1;
				
				Если КоличествоЭлементовВыбора >= МаксимальноеКоличествоЭлементовВыбора Тогда
					Прервать;
				КонецЕсли;
					
			КонецЦикла;			
			
		КонецЕсли;
		
		ДополнитьДанныеВыбораОписаниемОтсутствий(ВозвращаемыйСписок);
		
		Возврат ВозвращаемыйСписок;
		
	КонецЕсли;	
	
КонецФункции

/////////////////////////////////////////////////////////////////////////////////////////
// РАБОТА С АДРЕСНЫМИ ДАННЫМИ КОНТАКТОВ

// Возвращает ссылку на почтового адресата
//
// Параметры:
//   Адрес        (Строка) Адрес email
//   ОтображаемоеИмя (Строка) Отображаемое имя для формирования представления адресата
//   Ид            (УникальныйИдентификатор)  Уникальный идентификатор адресата - заполнен
//	   при обмене с мобильным
//
// Возвращаемое значение: 
//   Адресат (СправочникСсылка.АдресатыПочтовыхСообщений)
Функция ПолучитьПочтовогоАдресата(Адрес, ОтображаемоеИмя = Неопределено, 
	Ид = Неопределено, КонтактАргумент = Неопределено) Экспорт
	
	Если Ид <> Неопределено Тогда
		
		Адресат = НайтиПочтовогоАдресатаПоСсылке(Ид);
		Если ЗначениеЗаполнено(Адресат) Тогда
			Возврат Адресат;
	  	КонецЕсли;
	  
	КонецЕсли; 
	
	// Адрес длиной более 100 не имеет смысла, 
	// т.к. реквизит Адрес в справочнике АдресатыПочтовыхСообщений имеет длину 100.
	Адрес = Лев(Адрес, 100); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АдресатыПочтовыхСообщений.Ссылка
		|ИЗ
		|	Справочник.АдресатыПочтовыхСообщений КАК АдресатыПочтовыхСообщений
		|ГДЕ
		|	АдресатыПочтовыхСообщений.Адрес = &Адрес";
	Запрос.УстановитьПараметр("Адрес", Адрес);
	РезультатЗапроса = Запрос.Выполнить();
	Адресат = Справочники.АдресатыПочтовыхСообщений.ПустаяСсылка();
	Если РезультатЗапроса.Пустой() Тогда
		Если Не ЗначениеЗаполнено(ОтображаемоеИмя) Тогда
			ОтображаемоеИмя = Адрес;
		КонецЕсли;
		АдресатОбъект = Справочники.АдресатыПочтовыхСообщений.СоздатьЭлемент();
		Если Ид <> Неопределено Тогда
			АдресатОбъект.УстановитьСсылкуНового(Справочники.АдресатыПочтовыхСообщений.ПолучитьСсылку(Ид));
		КонецЕсли;
		Если ЗначениеЗаполнено(ОтображаемоеИмя) И СтрНайти(НРег(ОтображаемоеИмя), НРег(Адрес)) = 0 Тогда
			АдресатОбъект.Наименование = ОтображаемоеИмя + " <" + Адрес + ">";
		ИначеЕсли ЗначениеЗаполнено(ОтображаемоеИмя) Тогда
			АдресатОбъект.Наименование = ОтображаемоеИмя;
		Иначе
			АдресатОбъект.Наименование = Адрес;
		КонецЕсли;	
		АдресатОбъект.Адрес = Адрес;  
		
		Внешний = Ложь;
		Контакт = Неопределено;
		ЕстьКонтактыСПравами = Ложь;
		НаименованиеАдресата = АдресатОбъект.Наименование;
		ПриоритетноеПредставление = ПриоритетноеПредставление(Адрес, Внешний, Контакт, ЕстьКонтактыСПравами);
		
		Если ЗначениеЗаполнено(ПриоритетноеПредставление) Тогда
			АдресатОбъект.Наименование = ПриоритетноеПредставление + " <" + АдресатОбъект.Адрес + ">";
			АдресатОбъект.ЕстьВКонтактах = Истина;
			АдресатОбъект.Контакт = Контакт;
		ИначеЕсли ЗначениеЗаполнено(КонтактАргумент) Тогда	
			АдресатОбъект.ЕстьВКонтактах = Истина;
			АдресатОбъект.Контакт = КонтактАргумент;
		Иначе
			АдресатОбъект.ЕстьВКонтактах = Ложь;
		КонецЕсли;	
		
		АдресатОбъект.Записать();
		Адресат = АдресатОбъект.Ссылка;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			Адресат = Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	Возврат Адресат;
	
КонецФункции

// Возвращает приоритетное представление для данного адреса email
//
// Параметры:
//  ТекущийАдрес - Строка - email
//  Внешний - Булево - возвращаемое значение
//  Контакт - СправочникСсылка.Сотрудники, СправочникСсылка.ФизическиеЛица, СправочникСсылка.Контрагенты, СправочникСсылка.ЛичныеАдресаты, СправочникСсылка.РолиИсполнителей, СправочникСсылка.Пользователи, СправочникСсылка.КонтактныеЛица -   - возвращаемое значение
//  ЕстьКонтактыСПравами - Булево - возвращаемое значение
//
// Возвращаемое значение:
//  Строка
//
Функция ПриоритетноеПредставление(ТекущийАдрес, Внешний, Контакт, ЕстьКонтактыСПравами) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОбАдресатах.Контакт КАК Контакт
		|ИЗ
		|	Справочник.АдресатыПочтовыхСообщений КАК АдресатыПочтовыхСообщений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|		ПО (СведенияОбАдресатах.АдресатСообщения = АдресатыПочтовыхСообщений.Ссылка)
		|ГДЕ
		|	АдресатыПочтовыхСообщений.Адрес = &Адрес";
	Запрос.УстановитьПараметр("Адрес", ТекущийАдрес);	
		
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат "";
	КонецЕсли;
	
	МассивКонтакты = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Контакт");
	
	ПриоритетноеПредставлениеСтруктура = ВычислитьПриоритетноеПредставление(МассивКонтакты);
	
	Внешний = ПриоритетноеПредставлениеСтруктура.Внешний;
	Контакт = ПриоритетноеПредставлениеСтруктура.Контакт;
	ЕстьКонтактыСПравами = ПриоритетноеПредставлениеСтруктура.ЕстьКонтактыСПравами;
	
	Возврат ПриоритетноеПредставлениеСтруктура.ПриоритетноеПредставление;
		
КонецФункции	

Функция НовыйКонтактБолееПриоритетен(СтарыйКонтакт, НовыйКонтакт)

	МассивКонтакты = Новый Массив;
	МассивКонтакты.Добавить(СтарыйКонтакт);
	МассивКонтакты.Добавить(НовыйКонтакт);
	
	Внешний = Ложь;
	Контакт = Неопределено;
	ЕстьКонтактыСПравами = Ложь;
	
	ПриоритетноеПредставлениеСтруктура = ВычислитьПриоритетноеПредставление(МассивКонтакты);
	Если ПриоритетноеПредставлениеСтруктура.Контакт = НовыйКонтакт Тогда
		Возврат Истина;
	КонецЕсли;	
	
	Возврат Ложь;
	
КонецФункции		

// Находит приоритетное представление для массива контактов, сортированного по приоритету
//
// Параметры:
//  МассивКонтакты - Массив из СправочникСсылка.Сотрудники, СправочникСсылка.ФизическиеЛица, СправочникСсылка.Контрагенты, СправочникСсылка.ЛичныеАдресаты, СправочникСсылка.РолиИсполнителей, СправочникСсылка.Пользователи, СправочникСсылка.КонтактныеЛица
//
// Возвращаемое значение:
//  Структура
//
Функция ВычислитьПриоритетноеПредставление(МассивКонтакты) Экспорт
	
	ПриоритетноеПредставление = "";
	ВнешнийВозврат = Истина;
	КонтактВозврат = Неопределено;
	ЕстьКонтактыСПравами = Ложь;
	
	Для Каждого Контакт Из МассивКонтакты Цикл
		
		Если ТипЗнч(Контакт) = Тип("СправочникСсылка.Сотрудники") Тогда
			РеквизитыКонтактаДействует = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контакт, "Действует");
			Если РеквизитыКонтактаДействует = Истина Тогда
				ПриоритетноеПредставление = Строка(Контакт);
				ВнешнийВозврат = Ложь;
				КонтактВозврат = Контакт;
				ЕстьКонтактыСПравами = Ложь;
			КонецЕсли;	
		ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.Пользователи") Тогда
			РеквизитыКонтакта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контакт, "ПредставлениеВПерепискеСРангом, Недействителен");
			Если РеквизитыКонтакта.Недействителен = Ложь Тогда
				ПриоритетноеПредставление = РеквизитыКонтакта.ПредставлениеВПерепискеСРангом;
				ВнешнийВозврат = Ложь;
				КонтактВозврат = Контакт;
				ЕстьКонтактыСПравами = Ложь;
			КонецЕсли;	
		ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.РолиИсполнителей") Тогда
			ПриоритетноеПредставление = Строка(Контакт);
			ВнешнийВозврат = Ложь;
			КонтактВозврат = Контакт;
			ЕстьКонтактыСПравами = Ложь;
		ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.Контрагенты") Тогда
			
			ГруппаДоступа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контакт, "ГруппаДоступа");
			Если Не ЗначениеЗаполнено(ГруппаДоступа) Тогда
				ПриоритетноеПредставление = Строка(Контакт);
				КонтактВозврат = Контакт;
				ЕстьКонтактыСПравами = Ложь;
			Иначе	
				ЕстьКонтактыСПравами = Истина;
			КонецЕсли;	
			
		ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
			
			ГруппаДоступа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контакт.Владелец, "ГруппаДоступа");
			Если Не ЗначениеЗаполнено(ГруппаДоступа) Тогда
				ПриоритетноеПредставление = Строка(Контакт);
				КонтактВозврат = Контакт;
				ЕстьКонтактыСПравами = Ложь;
			Иначе	
				ЕстьКонтактыСПравами = Истина;
			КонецЕсли;	
			
		ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			
			ГруппаДоступа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контакт, "ГруппаДоступа");
			Если Не ЗначениеЗаполнено(ГруппаДоступа) Тогда
				ПриоритетноеПредставление = Строка(Контакт);
				КонтактВозврат = Контакт;
				ЕстьКонтактыСПравами = Ложь;
			Иначе	
				ЕстьКонтактыСПравами = Истина;
			КонецЕсли;	
			
		ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.ЛичныеАдресаты") Тогда
			ЕстьКонтактыСПравами = Истина;
		КонецЕсли; 	
		
	КонецЦикла;
	
	Возврат Новый Структура("ПриоритетноеПредставление, Внешний, Контакт, ЕстьКонтактыСПравами", 
		ПриоритетноеПредставление,
		ВнешнийВозврат,
		КонтактВозврат,
		ЕстьКонтактыСПравами);
	
КонецФункции	

// Ищет контрагента по соответствию адреса электронной почты, в том числе и среди контактных лиц
// Результат (Структура, Неопределено)
// - Контрагент (СправочникСсылка.Контрагенты)
// - КонтактноеЛицо (СправочникСсылка.КонтактныеЛица, Неопределено)
//
// Параметры:
// - Отправитель (Строка) Строка вида "ХХХХХХ <AA@BB.cc>" или "AA@BB.cc"
//
Функция ПолучитьКонтрагентаИКонтактноеЛицоПоСтрокеАдреса(Отправитель) Экспорт
	
	АдресЭлектроннойПочты = РаботаСоСтроками.ВыделитьПодстрокуВСкобках(Отправитель, "<", ">");
	Если ПустаяСтрока(АдресЭлектроннойПочты) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТаблицаКонтрагентов = ПолучитьКонтрагентовПоАдресуЭлектроннойПочты(АдресЭлектроннойПочты);
	Если ТаблицаКонтрагентов.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Контрагент", ТаблицаКонтрагентов[0].Контрагент);
	Результат.Вставить("КонтактноеЛицо", ТаблицаКонтрагентов[0].КонтактноеЛицо);
	
	Возврат Результат;
	
КонецФункции

// Возвращает строку, содержащую представление адресата
Функция ПолучитьПочтовоеПредставлениеКонтакта(Контакт, Адрес, ПринудительноеДобавлениеАдреса = Истина) Экспорт
	
	СтрокаКВозврату = "";
	Если Не ЗначениеЗаполнено(Контакт) Тогда
		Возврат Адрес;
	КонецЕсли;
	// Если у контакта несколько адресов, то покажем его с адресом.
	// Если заполненный адрес только один, то покажем без адреса.
	КоличествоЗаполненныхАдресов = 0;
	ИмяТаблицы = Контакт.Метаданные().ПолноеИмя();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(Таблица.Ссылка) КАК Ссылка
	|ИЗ
	|	Справочник.ФизическиеЛица КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ТаблицаКонтактнаяИнформация
	|		ПО ТаблицаКонтактнаяИнформация.Ссылка = Таблица.Ссылка
	|		И ТаблицаКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
	|		И ТаблицаКонтактнаяИнформация.АдресЭП <> """"
	|		И Таблица.Ссылка = &Ссылка";

	Запрос.Текст = СтрЗаменить(Запрос.Текст, "Справочник.ФизическиеЛица", ИмяТаблицы);
	Запрос.УстановитьПараметр("Ссылка", Контакт);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			
			ПредставлениеКонтакт = Строка(Контакт);
			Если ТипЗнч(Контакт) = Тип("СправочникСсылка.Пользователи") Тогда
				ПредставлениеКонтакт = Контакт.ПредставлениеВПерепискеСРангом;
			КонецЕсли;
			Если ТипЗнч(Контакт) = Тип("СправочникСсылка.Сотрудники") Тогда
				ПредставлениеКонтакт = Контакт.ПредставлениеВПереписке;
			КонецЕсли;
		
			Если Выборка[0] = 1 И Не ПринудительноеДобавлениеАдреса Тогда
				ДобавитьЗначениеКСтрокеЧерезРазделитель(
					СтрокаКВозврату,
					", ",
					ПредставлениеКонтакт);
			Иначе
				ДобавитьЗначениеКСтрокеЧерезРазделитель(
					СтрокаКВозврату,
					", ",
					РаботаСоСтроками.ПолучитьПредставлениеАдресаЭлектроннойПочты(
						ПредставлениеКонтакт,
						Адрес));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат СтрокаКВозврату; 
	
КонецФункции

// Возвращает строку, содержащую представление адресата
Функция ПолучитьПредставлениеИКонтактАдресата(ПочтовыйАдресат, ПринудительноеДобавлениеАдреса = Ложь) Экспорт

	СтруктураВозврата = Новый Структура;
	Если ЗначениеЗаполнено(ПочтовыйАдресат.Наименование) Тогда
		Если СтрНайти(НРег(ПочтовыйАдресат.Наименование), НРег(ПочтовыйАдресат.Адрес)) > 0 Тогда
			ПредставлениеАдресата = ПочтовыйАдресат.Наименование;
		Иначе
			ПредставлениеАдресата = ПочтовыйАдресат.Наименование + " <" + НРег(ПочтовыйАдресат.Адрес) + ">";
		КонецЕсли;
	Иначе
		ПредставлениеАдресата = НРег(ПочтовыйАдресат.Адрес);
	КонецЕсли;
	СтруктураВозврата.Вставить("Представление", ПредставлениеАдресата);
	СтруктураВозврата.Вставить("Контакт", Неопределено);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СведенияОбАдресатах.АдресатСообщения КАК АдресатСообщения,
		|	ЕСТЬNULL(МИНИМУМ(СведенияОбАдресатах.Контакт), НЕОПРЕДЕЛЕНО) КАК Контакт,
		|	ПОДСТРОКА(МИНИМУМ(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Сотрудники)
		|			ТОГДА ""0""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ФизическиеЛица)
		|			ТОГДА ""1""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Контрагенты)
		|			ТОГДА ""2""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.КонтактныеЛица)
		|			ТОГДА ""3""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.РолиИсполнителей)
		|			ТОГДА ""4""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ЛичныеАдресаты)
		|			ТОГДА ""5""
		|		ИНАЧЕ ""6""
		|	КОНЕЦ + ЕСТЬNULL(СведенияОбАдресатах.Представление, Адресаты.Наименование)), 2, 1000) КАК Представление
		|ИЗ
		|	Справочник.АдресатыПочтовыхСообщений КАК Адресаты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|		ПО Адресаты.Ссылка = СведенияОбАдресатах.АдресатСообщения
		|		И (СведенияОбАдресатах.Активна)
		|ГДЕ
		|	Адресаты.Ссылка = &АдресатСообщения
		|СГРУППИРОВАТЬ ПО
		|	СведенияОбАдресатах.АдресатСообщения";
				
	Запрос.УстановитьПараметр("АдресатСообщения", ПочтовыйАдресат);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			СтруктураВозврата.Представление = Выборка.Представление;
			
			Если ПринудительноеДобавлениеАдреса
				И СтрНайти(НРег(Выборка.Представление), НРег(ПочтовыйАдресат.Адрес)) = 0 Тогда
				СтруктураВозврата.Представление = Выборка.Представление + " <" + НРег(ПочтовыйАдресат.Адрес) + ">";			
			КонецЕсли;
			
			СтруктураВозврата.Контакт = Выборка.Контакт;
			
		КонецЕсли;
	КонецЕсли;
	
	СтруктураВозврата.Вставить("Внешний", ЭтоВнешнийАдресат(ПочтовыйАдресат));
	СтруктураВозврата.Вставить("ВидМаршрутизации", ПочтовыйАдресат.ВидМаршрутизации);
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Возвращает строку, содержащую представление адресата
Функция ПолучитьПредставлениеИКонтактОтправителя(ПочтовыйАдресат, УникальныйИдентификаторФормы, 
	ПринудительноеДобавлениеАдреса = Ложь) Экспорт

	СтруктураВозврата = Новый Структура;
	Если ЗначениеЗаполнено(ПочтовыйАдресат.Наименование) Тогда
		Если СтрНайти(НРег(ПочтовыйАдресат.Наименование), НРег(ПочтовыйАдресат.Адрес)) > 0 Тогда
			ПредставлениеАдресата = ПочтовыйАдресат.Наименование;
		Иначе
			ПредставлениеАдресата = ПочтовыйАдресат.Наименование + " <" + НРег(ПочтовыйАдресат.Адрес) + ">";
		КонецЕсли;
	Иначе
		ПредставлениеАдресата = НРег(ПочтовыйАдресат.Адрес);
	КонецЕсли;
	
	СтруктураВозврата.Вставить("Контакт", Неопределено);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СведенияОбАдресатах.АдресатСообщения КАК АдресатСообщения,
		|	ЕСТЬNULL(МИНИМУМ(СведенияОбАдресатах.Контакт), НЕОПРЕДЕЛЕНО) КАК Контакт,
		|	ПОДСТРОКА(МИНИМУМ(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Сотрудники)
		|			ТОГДА ""0""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ФизическиеЛица)
		|			ТОГДА ""1""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Контрагенты)
		|			ТОГДА ""2""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.КонтактныеЛица)
		|			ТОГДА ""3""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.РолиИсполнителей)
		|			ТОГДА ""4""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ЛичныеАдресаты)
		|			ТОГДА ""5""
		|		ИНАЧЕ ""6""
		|	КОНЕЦ + ЕСТЬNULL(СведенияОбАдресатах.Представление, Адресаты.Наименование)), 2, 1000) КАК Представление
		|ИЗ
		|	Справочник.АдресатыПочтовыхСообщений КАК Адресаты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|		ПО Адресаты.Ссылка = СведенияОбАдресатах.АдресатСообщения
		|		И (СведенияОбАдресатах.Активна)
		|ГДЕ
		|	Адресаты.Ссылка = &АдресатСообщения
		|СГРУППИРОВАТЬ ПО
		|	СведенияОбАдресатах.АдресатСообщения";
				
	Запрос.УстановитьПараметр("АдресатСообщения", ПочтовыйАдресат);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			ПредставлениеАдресата = Выборка.Представление;
			
			Если ПринудительноеДобавлениеАдреса
				И СтрНайти(НРег(Выборка.Представление), НРег(ПочтовыйАдресат.Адрес)) = 0 Тогда
				ПредставлениеАдресата = Выборка.Представление + " <" + НРег(ПочтовыйАдресат.Адрес) + ">";			
			КонецЕсли;
			
			СтруктураВозврата.Контакт = Выборка.Контакт;
			
		КонецЕсли;
	КонецЕсли;
	
	ПредставлениеАдресата = РаботаС_HTML.ЗаменитьСпецСимволыHTML(ПредставлениеАдресата);
	
	ОтображатьФотографииОбщаяНастройка = ПолучитьФункциональнуюОпцию("ОтображатьФотографииОбщаяНастройка");
	ПриложениеЯвляетсяВебКлиентом = ОбщегоНазначенияДокументооборот.ПриложениеЯвляетсяВебКлиентом();
	
	ПолучатьФотографии = Истина;
	
	ОтображатьФотографииПерсональнаяНастройка =
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиПрограммы",
			"ОтображатьФотографииПерсональнаяНастройка",
			Истина);
	
	Если Не ОтображатьФотографииОбщаяНастройка
		Или Не ОтображатьФотографииПерсональнаяНастройка
		Или ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая
		Тогда
		ПолучатьФотографии = Ложь;
	КонецЕсли;
	
	УказатьГиперссылку = Не ПолучатьФотографии; // если нет фото, нужна гиперссылка
	
	Если ПолучатьФотографии Тогда
		
		Контакт = СтруктураВозврата.Контакт;
		
		Фотография = "";
		Если ЗначениеЗаполнено(Контакт) 
			И ТипЗнч(Контакт) <> Тип("СправочникСсылка.РолиИсполнителей") Тогда
			
			ЕстьКартинка = Ложь;
			Фотография = РаботаСФотографиями.ПолучитьАдресФото(Контакт, УникальныйИдентификаторФормы, ЕстьКартинка);
			
			Если Не ЗначениеЗаполнено(Фотография) Тогда
				УказатьГиперссылку = Истина;
			КонецЕсли;	
			
		КонецЕсли;
			
	КонецЕсли;	
	
	Если УказатьГиперссылку И ЗначениеЗаполнено(СтруктураВозврата.Контакт) Тогда
		ПредставлениеАдресата = 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"<a href=v8doc:%1> %2 </a>", 
					ПолучитьНавигационнуюСсылку(СтруктураВозврата.Контакт),
					ПредставлениеАдресата);
	КонецЕсли;	
	
	СтруктураВозврата.Вставить("Представление", ПредставлениеАдресата);
	
	СтруктураВозврата.Вставить("Внешний", ЭтоВнешнийАдресат(ПочтовыйАдресат));
	СтруктураВозврата.Вставить("ВидМаршрутизации", ПочтовыйАдресат.ВидМаршрутизации);
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Формирует представление массива адресатов  - как одну строку
Функция ПолучитьПредставлениеМассиваАдресатов(МассивАдресатов) Экспорт
	
	Если МассивАдресатов.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ТаблицаАдресатов = Новый ТаблицаЗначений; 
	Массив = Новый Массив;
	Массив.Добавить(Тип("СправочникСсылка.АдресатыПочтовыхСообщений"));	
	НоваяКолонка = ТаблицаАдресатов.Колонки.Добавить("Ссылка", Новый ОписаниеТипов(Массив));
	Для Каждого ЭлементМассива Из МассивАдресатов Цикл
		ТаблицаАдресатов.Добавить().Ссылка = ЭлементМассива;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Таблица.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВременнаяТаблица
		|ИЗ
		|	&Данные КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Адресаты.Ссылка,
		|	ПОДСТРОКА(МИНИМУМ(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Сотрудники)
		|			ТОГДА ""0""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ФизическиеЛица)
		|			ТОГДА ""1""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Контрагенты)
		|			ТОГДА ""2""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.КонтактныеЛица)
		|			ТОГДА ""3""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.РолиИсполнителей)
		|			ТОГДА ""4""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ЛичныеАдресаты)
		|			ТОГДА ""5""
		|		ИНАЧЕ ""6""
		|	КОНЕЦ + ЕСТЬNULL(СведенияОбАдресатах.Представление, Адресаты.Наименование)), 2, 1000) КАК Представление
		|ИЗ
		|	Справочник.АдресатыПочтовыхСообщений КАК Адресаты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблица КАК Таблица
		|		ПО Адресаты.Ссылка = Таблица.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|		ПО Адресаты.Ссылка = СведенияОбАдресатах.АдресатСообщения
		|		И (СведенияОбАдресатах.Активна)
		|СГРУППИРОВАТЬ ПО
		|	Адресаты.Ссылка";
	
	Запрос.УстановитьПараметр("Данные", ТаблицаАдресатов); 			
	РезультатЗапроса = Запрос.Выполнить();
	ИтоговаяСтрока = "";
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Не ПустаяСтрока(ИтоговаяСтрока) Тогда
				ИтоговаяСтрока = ИтоговаяСтрока + "; ";
			КонецЕсли;	
			ИтоговаяСтрока = ИтоговаяСтрока + Выборка.Представление;
		КонецЦикла;
	КонецЕсли;	
	
	Возврат ИтоговаяСтрока;
	
КонецФункции

// Формирует представление массива адресатов  - как одну html строку - с гиперссылками на контактах.
Функция ПолучитьПредставлениеМассиваАдресатовСГиперссылками(МассивАдресатов) Экспорт
	
	Если МассивАдресатов.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ТаблицаАдресатов = Новый ТаблицаЗначений; 
	Массив = Новый Массив;
	Массив.Добавить(Тип("СправочникСсылка.АдресатыПочтовыхСообщений"));	
	НоваяКолонка = ТаблицаАдресатов.Колонки.Добавить("Ссылка", Новый ОписаниеТипов(Массив));
	Для Каждого ЭлементМассива Из МассивАдресатов Цикл
		ТаблицаАдресатов.Добавить().Ссылка = ЭлементМассива;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Таблица.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВременнаяТаблица
		|ИЗ
		|	&Данные КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Адресаты.Ссылка КАК Ссылка,
		|	ПОДСТРОКА(МИНИМУМ(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Сотрудники)
		|			ТОГДА ""0""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ФизическиеЛица)
		|			ТОГДА ""1""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Контрагенты)
		|			ТОГДА ""2""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.КонтактныеЛица)
		|			ТОГДА ""3""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.РолиИсполнителей)
		|			ТОГДА ""4""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ЛичныеАдресаты)
		|			ТОГДА ""5""
		|		ИНАЧЕ ""6""
		|	КОНЕЦ + ЕСТЬNULL(СведенияОбАдресатах.Представление, Адресаты.Наименование)), 2, 1000) КАК Представление
		|ПОМЕСТИТЬ АдресатИПредставление
		|ИЗ
		|	Справочник.АдресатыПочтовыхСообщений КАК Адресаты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблица КАК Таблица
		|		ПО Адресаты.Ссылка = Таблица.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|		ПО Адресаты.Ссылка = СведенияОбАдресатах.АдресатСообщения
		|		И (СведенияОбАдресатах.Активна)
		|СГРУППИРОВАТЬ ПО
		|	Адресаты.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	АдресатИПредставление.Ссылка КАК Ссылка,
		|	АдресатИПредставление.Представление КАК Представление,
		|	ЕСТЬNULL(МИНИМУМ(СведенияОбАдресатах.Контакт), НЕОПРЕДЕЛЕНО) КАК Контакт
		|ИЗ
		|	АдресатИПредставление КАК АдресатИПредставление
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|		ПО АдресатИПредставление.Ссылка = СведенияОбАдресатах.АдресатСообщения
		|		И (СведенияОбАдресатах.Представление = АдресатИПредставление.Представление)
		|		И (СведенияОбАдресатах.Активна)
		|СГРУППИРОВАТЬ ПО
		|	АдресатИПредставление.Ссылка,
		|	АдресатИПредставление.Представление";
	
	Запрос.УстановитьПараметр("Данные", ТаблицаАдресатов); 			
	РезультатЗапроса = Запрос.Выполнить();
	ИтоговаяСтрока = "";
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Не ПустаяСтрока(ИтоговаяСтрока) Тогда
				ИтоговаяСтрока = ИтоговаяСтрока + "; ";
			КонецЕсли;
			
			ПредставлениеКорректное = Выборка.Представление;
			ПредставлениеКорректное = РаботаС_HTML.ЗаменитьСпецСимволыHTML(ПредставлениеКорректное);
			
			Если Не ЗначениеЗаполнено(Выборка.Контакт) Тогда
				ИтоговаяСтрока = ИтоговаяСтрока + ПредставлениеКорректное;
			Иначе
				ИтоговаяСтрока = ИтоговаяСтрока
					+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"<a href=v8doc:%1> %2 </a>", 
						ПолучитьНавигационнуюСсылку(Выборка.Контакт),
						ПредставлениеКорректное);
			КонецЕсли;	
			
		КонецЦикла;
	КонецЕсли;	
	
	Возврат ИтоговаяСтрока;
	
КонецФункции

// Заполняет массив получателей входящего письма
Функция ПолучитьТаблицуПолучателейКомуКопияУВходящегоПисьма(Письмо) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СведенияОбАдресатах.АдресатСообщения КАК АдресатСообщения,
		|	ПОДСТРОКА(МИНИМУМ(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Сотрудники)
		|			ТОГДА ""0""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ФизическиеЛица)
		|			ТОГДА ""1""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Контрагенты)
		|			ТОГДА ""2""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.КонтактныеЛица)
		|			ТОГДА ""3""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.РолиИсполнителей)
		|			ТОГДА ""4""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ЛичныеАдресаты)
		|			ТОГДА ""5""
		|		ИНАЧЕ ""6""
		|	КОНЕЦ + СведенияОбАдресатах.Представление), 2, 1000) КАК Представление
		|ПОМЕСТИТЬ СведенияПредставления
		|ИЗ
		|	РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|ГДЕ
		|	СведенияОбАдресатах.Активна
		|СГРУППИРОВАТЬ ПО
		|	СведенияОбАдресатах.АдресатСообщения
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АдресатСообщения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВходящееПисьмоПолучателиПисьма.Адресат.Адрес КАК Адрес,
		|	ВходящееПисьмоПолучателиПисьма.Адресат КАК ПочтовыйАдресат,
		|	ЕСТЬNULL(СведенияПредставления.Представление, ВходящееПисьмоПолучателиПисьма.Адресат.Наименование) КАК Представление,
		|	&СтрокаКому КАК ТипАдреса,
		|	1 КАК НомерСпособаАдресации
		|ПОМЕСТИТЬ Данные
		|ИЗ
		|	Документ.ВходящееПисьмо.ПолучателиПисьма КАК ВходящееПисьмоПолучателиПисьма
		|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПредставления КАК СведенияПредставления
		|		ПО (СведенияПредставления.АдресатСообщения = ВходящееПисьмоПолучателиПисьма.Адресат.Ссылка)
		|ГДЕ
		|	ВходящееПисьмоПолучателиПисьма.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВходящееПисьмоПолучателиКопий.Адресат.Адрес,
		|	ВходящееПисьмоПолучателиКопий.Адресат,
		|	ЕСТЬNULL(СведенияПредставления.Представление, ВходящееПисьмоПолучателиКопий.Адресат.Наименование),
		|	&СтрокаКопия,
		|	2
		|ИЗ
		|	Документ.ВходящееПисьмо.ПолучателиКопий КАК ВходящееПисьмоПолучателиКопий
		|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПредставления КАК СведенияПредставления
		|		ПО (СведенияПредставления.АдресатСообщения = ВходящееПисьмоПолучателиКопий.Адресат.Ссылка)
		|ГДЕ
		|	ВходящееПисьмоПолучателиКопий.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВходящееПисьмоПолучателиОтвета.Адресат.Адрес,
		|	ВходящееПисьмоПолучателиОтвета.Адресат,
		|	ЕСТЬNULL(СведенияПредставления.Представление, ВходящееПисьмоПолучателиОтвета.Адресат.Наименование),
		|	&СтрокаКопия,
		|	4
		|ИЗ
		|	Документ.ВходящееПисьмо.ПолучателиОтвета КАК ВходящееПисьмоПолучателиОтвета
		|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПредставления КАК СведенияПредставления
		|		ПО (СведенияПредставления.АдресатСообщения = ВходящееПисьмоПолучателиОтвета.Адресат.Ссылка)
		|ГДЕ
		|	ВходящееПисьмоПолучателиОтвета.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПочтовыйАдресат
		|
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Данные.Адрес,
		|	Данные.ПочтовыйАдресат.Наименование КАК Наименование,
		|	Данные.ПочтовыйАдресат КАК ПочтовыйАдресат,
		|	Данные.Представление КАК Представление,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(МИНИМУМ(СведенияОбАдресатах.Контакт)) = ТИП(Справочник.РолиИсполнителей)
		|		ИЛИ ТИПЗНАЧЕНИЯ(МИНИМУМ(СведенияОбАдресатах.Контакт)) = ТИП(Справочник.Сотрудники)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Внешний,
		|	Данные.ТипАдреса,
		|	ЕСТЬNULL(МИНИМУМ(СведенияОбАдресатах.Контакт), НЕОПРЕДЕЛЕНО) КАК Контакт
		|ИЗ
		|	Данные КАК Данные
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|		ПО Данные.ПочтовыйАдресат = СведенияОбАдресатах.АдресатСообщения
		|		И (СведенияОбАдресатах.Представление = Данные.Представление)
		|		И (СведенияОбАдресатах.Активна)
		|СГРУППИРОВАТЬ ПО
		|	Данные.Адрес,
		|	Данные.ПочтовыйАдресат,
		|	Данные.Представление,
		|	Данные.ТипАдреса,
		|	Данные.ПочтовыйАдресат.Наименование
		|УПОРЯДОЧИТЬ ПО
		|	Представление";
		
	Запрос.УстановитьПараметр("Ссылка", Письмо);
	Запрос.УстановитьПараметр("СтрокаКому", Перечисления.ТипыАдресатов.Кому);
	Запрос.УстановитьПараметр("СтрокаКопия", Перечисления.ТипыАдресатов.Копия);
	
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаЗначений;
	
КонецФункции

// Заполняет массив получателей исходящего письма
Функция ПолучитьТаблицуПолучателейКомуКопияСкрытаяУИсходящегоПисьма(Письмо,
	СортироватьПоПредставлению = Истина, ШаблоныПисем = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СведенияОбАдресатах.АдресатСообщения КАК АдресатСообщения,
		|	ПОДСТРОКА(МИНИМУМ(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Сотрудники)
		|			ТОГДА ""0""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ФизическиеЛица)
		|			ТОГДА ""1""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Контрагенты)
		|			ТОГДА ""2""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.КонтактныеЛица)
		|			ТОГДА ""3""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.РолиИсполнителей)
		|			ТОГДА ""4""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ЛичныеАдресаты)
		|			ТОГДА ""5""
		|		ИНАЧЕ ""6""
		|	КОНЕЦ + СведенияОбАдресатах.Представление), 2, 1000) КАК Представление
		|ПОМЕСТИТЬ СведенияПредставления
		|ИЗ
		|	РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|ГДЕ
		|	СведенияОбАдресатах.Активна
		|СГРУППИРОВАТЬ ПО
		|	СведенияОбАдресатах.АдресатСообщения
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АдресатСообщения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИсходящееПисьмоПолучателиПисьма.Адресат.Адрес КАК Адрес,
		|	ИсходящееПисьмоПолучателиПисьма.Адресат КАК ПочтовыйАдресат,
		|	ИсходящееПисьмоПолучателиПисьма.Адресат.ВидМаршрутизации КАК ВидМаршрутизации,
		|	ЕСТЬNULL(СведенияПредставления.Представление, ИсходящееПисьмоПолучателиПисьма.Адресат.Наименование) КАК
		|		Представление,
		|	&СтрокаКому КАК ТипАдреса,
		|	ИсходящееПисьмоПолучателиПисьма.ПорядковыйНомер КАК ПорядковыйНомер
		|ПОМЕСТИТЬ Данные
		|ИЗ
		|	Документ.ИсходящееПисьмо.ПолучателиПисьма КАК ИсходящееПисьмоПолучателиПисьма
		|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПредставления КАК СведенияПредставления
		|		ПО (СведенияПредставления.АдресатСообщения = ИсходящееПисьмоПолучателиПисьма.Адресат.Ссылка)
		|ГДЕ
		|	ИсходящееПисьмоПолучателиПисьма.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИсходящееПисьмоПолучателиКопий.Адресат.Адрес,
		|	ИсходящееПисьмоПолучателиКопий.Адресат,
		|	ИсходящееПисьмоПолучателиКопий.Адресат.ВидМаршрутизации,
		|	ЕСТЬNULL(СведенияПредставления.Представление, ИсходящееПисьмоПолучателиКопий.Адресат.Наименование),
		|	&СтрокаКопия,
		|	ИсходящееПисьмоПолучателиКопий.ПорядковыйНомер
		|ИЗ
		|	Документ.ИсходящееПисьмо.ПолучателиКопий КАК ИсходящееПисьмоПолучателиКопий
		|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПредставления КАК СведенияПредставления
		|		ПО (СведенияПредставления.АдресатСообщения = ИсходящееПисьмоПолучателиКопий.Адресат.Ссылка)
		|ГДЕ
		|	ИсходящееПисьмоПолучателиКопий.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИсходящееПисьмоПолучателиСкрытыхКопий.Адресат.Адрес,
		|	ИсходящееПисьмоПолучателиСкрытыхКопий.Адресат,
		|	ИсходящееПисьмоПолучателиСкрытыхКопий.Адресат.ВидМаршрутизации,
		|	ЕСТЬNULL(СведенияПредставления.Представление, ИсходящееПисьмоПолучателиСкрытыхКопий.Адресат.Наименование),
		|	&СтрокаСкрытаяКопия,
		|	ИсходящееПисьмоПолучателиСкрытыхКопий.ПорядковыйНомер
		|ИЗ
		|	Документ.ИсходящееПисьмо.ПолучателиСкрытыхКопий КАК ИсходящееПисьмоПолучателиСкрытыхКопий
		|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПредставления КАК СведенияПредставления
		|		ПО (СведенияПредставления.АдресатСообщения = ИсходящееПисьмоПолучателиСкрытыхКопий.Адресат.Ссылка)
		|ГДЕ
		|	ИсходящееПисьмоПолучателиСкрытыхКопий.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИсходящееПисьмоПолучателиОтвета.Адресат.Адрес,
		|	ИсходящееПисьмоПолучателиОтвета.Адресат,
		|	ИсходящееПисьмоПолучателиОтвета.Адресат.ВидМаршрутизации,
		|	ЕСТЬNULL(СведенияПредставления.Представление, ИсходящееПисьмоПолучателиОтвета.Адресат.Наименование),
		|	&СтрокаОбратныйАдрес,
		|	ИсходящееПисьмоПолучателиОтвета.ПорядковыйНомер
		|ИЗ
		|	Документ.ИсходящееПисьмо.ПолучателиОтвета КАК ИсходящееПисьмоПолучателиОтвета
		|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПредставления КАК СведенияПредставления
		|		ПО (СведенияПредставления.АдресатСообщения = ИсходящееПисьмоПолучателиОтвета.Адресат.Ссылка)
		|ГДЕ
		|	ИсходящееПисьмоПолучателиОтвета.Ссылка = &Ссылка
		|
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПочтовыйАдресат
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Данные.Адрес,
		|	Данные.ПочтовыйАдресат КАК Адресат,
		|	Данные.ПочтовыйАдресат.Наименование КАК Наименование,
		|	Данные.ВидМаршрутизации КАК ВидМаршрутизации,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(МИНИМУМ(СведенияОбАдресатах.Контакт)) = ТИП(Справочник.РолиИсполнителей)
		|		ИЛИ ТИПЗНАЧЕНИЯ(МИНИМУМ(СведенияОбАдресатах.Контакт)) = ТИП(Справочник.Сотрудники)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Внешний,
		|	Данные.Представление КАК Представление,
		|	Данные.ТипАдреса,
		|	ЕСТЬNULL(МИНИМУМ(СведенияОбАдресатах.Контакт), НЕОПРЕДЕЛЕНО) КАК Контакт,
		|	ИСТИНА КАК РедактированиеЗавершено,
		|	Данные.ПорядковыйНомер КАК ПорядковыйНомер
		|ИЗ
		|	Данные КАК Данные
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|		ПО Данные.ПочтовыйАдресат = СведенияОбАдресатах.АдресатСообщения
		|		И (СведенияОбАдресатах.Представление = Данные.Представление)
		|		И (СведенияОбАдресатах.Активна)
		|СГРУППИРОВАТЬ ПО
		|	Данные.Адрес,
		|	Данные.ПочтовыйАдресат,
		|	Данные.ВидМаршрутизации,
		|	Данные.Представление,
		|	Данные.ТипАдреса,
		|	Данные.ПочтовыйАдресат.Наименование,
		|	Данные.ПорядковыйНомер
		|УПОРЯДОЧИТЬ ПО
		|	Представление";
		
	Если ШаблоныПисем Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.ИсходящееПисьмо.", "Справочник.ШаблоныПисем.");
	КонецЕсли;
		
	Запрос.УстановитьПараметр("Ссылка", Письмо);	
	Запрос.УстановитьПараметр("СтрокаКому", Перечисления.ТипыАдресатов.Кому);
	Запрос.УстановитьПараметр("СтрокаКопия", Перечисления.ТипыАдресатов.Копия);
	Запрос.УстановитьПараметр("СтрокаСкрытаяКопия", Перечисления.ТипыАдресатов.СкрытаяКопия);
	Запрос.УстановитьПараметр("СтрокаОбратныйАдрес", Перечисления.ТипыАдресатов.ОбратныйАдрес);
	
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
	
	Если СортироватьПоПредставлению Тогда
		ТаблицаЗначений.Сортировать("Представление");
	Иначе
		ТаблицаЗначений.Сортировать("ПорядковыйНомер");
	КонецЕсли;
	
	Возврат ТаблицаЗначений;
	
КонецФункции

// Формирует соответствие структур-описаний массива адресатов
Функция ПолучитьСоответствиеСПредставлениямиИКонтактамиМассиваАдресатов(МассивАдресатов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СведенияОбАдресатах.АдресатСообщения КАК АдресатСообщения,
		|	ПОДСТРОКА(МИНИМУМ(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Сотрудники)
		|			ТОГДА ""0""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ФизическиеЛица)
		|			ТОГДА ""1""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Контрагенты)
		|			ТОГДА ""2""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.КонтактныеЛица)
		|			ТОГДА ""3""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.РолиИсполнителей)
		|			ТОГДА ""4""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ЛичныеАдресаты)
		|			ТОГДА ""5""
		|		ИНАЧЕ ""6""
		|	КОНЕЦ + СведенияОбАдресатах.Представление), 2, 1000) КАК Представление
		|ПОМЕСТИТЬ СведенияПредставления
		|ИЗ
		|	РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|ГДЕ
		|	СведенияОбАдресатах.Активна
		|СГРУППИРОВАТЬ ПО
		|	СведенияОбАдресатах.АдресатСообщения
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АдресатСообщения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Адресаты.Адрес КАК Адрес,
		|	Адресаты.Ссылка КАК ПочтовыйАдресат,
		|	ЕСТЬNULL(Сведения.Представление, Адресаты.Наименование) КАК Представление,
		|	Адресаты.ВидМаршрутизации КАК ВидМаршрутизации,
		|	&СтрокаКому КАК ТипАдреса
		|ПОМЕСТИТЬ Данные
		|ИЗ
		|	Справочник.АдресатыПочтовыхСообщений КАК Адресаты
		|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПредставления КАК Сведения
		|		ПО Адресаты.Ссылка = Сведения.АдресатСообщения
		|ГДЕ
		|	Адресаты.Ссылка В (&МассивАдресатов)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПочтовыйАдресат
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Данные.Адрес,
		|	Данные.ПочтовыйАдресат КАК ПочтовыйАдресат,
		|	Данные.Представление КАК Представление,
		|	ЕСТЬNULL(МИНИМУМ(СведенияОбАдресатах.Контакт), НЕОПРЕДЕЛЕНО) КАК Контакт,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(МИНИМУМ(СведенияОбАдресатах.Контакт)) = ТИП(Справочник.РолиИсполнителей)
		|		ИЛИ ТИПЗНАЧЕНИЯ(МИНИМУМ(СведенияОбАдресатах.Контакт)) = ТИП(Справочник.Сотрудники)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Внешний,
		|	Данные.ПочтовыйАдресат.Наименование,
		|	Данные.ВидМаршрутизации КАК ВидМаршрутизации
		|ИЗ
		|	Данные КАК Данные
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|		ПО Данные.ПочтовыйАдресат = СведенияОбАдресатах.АдресатСообщения
		|		И СведенияОбАдресатах.Представление = Данные.Представление
		|		И СведенияОбАдресатах.Активна
		|СГРУППИРОВАТЬ ПО
		|	Данные.Адрес,
		|	Данные.ПочтовыйАдресат,
		|	Данные.Представление,
		|	Данные.ТипАдреса,
		|	Данные.ВидМаршрутизации,
		|	Данные.ПочтовыйАдресат.Наименование
		|УПОРЯДОЧИТЬ ПО
		|	Представление";
		
	Запрос.УстановитьПараметр("МассивАдресатов", МассивАдресатов);	
	Запрос.УстановитьПараметр("СтрокаКому", Перечисления.ТипыАдресатов.Кому);
	
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
	
	Соответствие = Новый Соответствие;
	
	Для Каждого СтрокаДанных Из ТаблицаЗначений Цикл
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("Адрес", СтрокаДанных[0]);
		СтруктураДанных.Вставить("Представление", СтрокаДанных[2]);
		СтруктураДанных.Вставить("Контакт", СтрокаДанных[3]);
		СтруктураДанных.Вставить("Внешний", СтрокаДанных[4]);
		СтруктураДанных.Вставить("Наименование", СтрокаДанных[5]);
		СтруктураДанных.Вставить("ВидМаршрутизации", СтрокаДанных[6]);
		СтруктураДанных.Вставить("Ссылка", СтрокаДанных[1]);
		
		Соответствие.Вставить(СтрокаДанных[1], СтруктураДанных);
	КонецЦикла;
	
	Возврат Соответствие;	
	
КонецФункции

// Сотрудник другой организации
Функция ЭтоВнешнийАдресат(Адресат) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СведенияОбАдресатах.Контакт
	|ИЗ
	|	РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
	|ГДЕ
	|	СведенияОбАдресатах.АдресатСообщения = &АдресатСообщения
	|	И СведенияОбАдресатах.Активна";
	Запрос.УстановитьПараметр("АдресатСообщения", Адресат);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ТипЗнч(Выборка.Контакт) = Тип("СправочникСсылка.РолиИсполнителей")
		 Или ТипЗнч(Выборка.Контакт) = Тип("СправочникСсылка.Сотрудники")  Тогда 
			Возврат Ложь;
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат Истина;
	
КонецФункции	

// Получает вид маршрутизации (внутренняя или внешняя)
Функция ПолучитьВидМаршрутизацииПоАдресу(Адрес) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не Константы.ИспользоватьВстроеннуюПочту.Получить() Тогда 
		Возврат Перечисления.ВидыМаршрутизацииПисем.Внешняя;
	КонецЕсли;
	
	Если Не ВстроеннаяПочтаСерверПовтИсп.ИспользоватьВнутреннююМаршрутизацию() Тогда 
		Возврат Перечисления.ВидыМаршрутизацииПисем.Внешняя;
	КонецЕсли;
	
	УчетныеЗаписи = ПолучитьУчетныеЗаписиПоАдресу(Адрес, Истина);
	Если УчетныеЗаписи.Количество() = 0 Тогда  // для адресата нет учетных записей для внутренней маршрутизации
		Возврат Перечисления.ВидыМаршрутизацииПисем.Внешняя;
	КонецЕсли;
		
	Возврат Перечисления.ВидыМаршрутизацииПисем.Внутренняя;
		
КонецФункции	

// Получает таблицу с информацией о получателях переданного массива входящих писем
// Параметры:
//	МассивПисем - массив ссылок на письма. Из них будут обрабатываться только входящие письма.
// Возвращает
//	ТаблицаЗначений
//		Письмо - ссылка на письмо
//		Адрес - адрес получателя
//		Наименование - наименование адресата-получателя
//		ПочтовыйАдресат - ссылка на адресата-получателя
//		Представление - представление контакта, если он есть
//		Внешний - тип адресата
//		ТипАдреса - строка "Кому" или "Копия"
//		Контакт - ссылка на контакт из адресной книги
Функция ПолучитьПредставленияИКонтактыПолучателейВходящихПисем(МассивПисем) Экспорт
	
	Таблица = Новый ТаблицаЗначений; 
	Массив = Новый Массив;
	Массив.Добавить(Тип("ДокументСсылка.ВходящееПисьмо"));	
	НоваяКолонка = Таблица.Колонки.Добавить("Ссылка", Новый ОписаниеТипов(Массив));
	Для Каждого ЭлементМассива Из МассивПисем Цикл
		Если ТипЗнч(ЭлементМассива.Ссылка) = Тип("ДокументСсылка.ВходящееПисьмо") Тогда
			Таблица.Добавить().Ссылка = ЭлементМассива.Ссылка;
		КонецЕсли;
	КонецЦикла;
	
	Если Таблица.Количество() = 0 Тогда
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Таблица.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВходящиеПисьмаВременная
		|ИЗ
		|	&Данные КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СведенияОбАдресатах.АдресатСообщения КАК АдресатСообщения,
		|	ПОДСТРОКА(МИНИМУМ(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Сотрудники)
		|			ТОГДА ""0""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ФизическиеЛица)
		|			ТОГДА ""1""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Контрагенты)
		|			ТОГДА ""2""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.КонтактныеЛица)
		|			ТОГДА ""3""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.РолиИсполнителей)
		|			ТОГДА ""4""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ЛичныеАдресаты)
		|			ТОГДА ""5""
		|		ИНАЧЕ ""6""
		|	КОНЕЦ + СведенияОбАдресатах.Представление), 2, 1000) КАК Представление
		|ПОМЕСТИТЬ СведенияПредставления
		|ИЗ
		|	РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|СГРУППИРОВАТЬ ПО
		|	СведенияОбАдресатах.АдресатСообщения
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АдресатСообщения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВходящиеПисьмаВременная.Ссылка КАК Письмо,
		|	ВходящееПисьмоПолучателиПисьма.Адресат.Адрес КАК Адрес,
		|	ВходящееПисьмоПолучателиПисьма.Адресат КАК ПочтовыйАдресат,
		|	ЕСТЬNULL(СведенияПредставления.Представление, ВходящееПисьмоПолучателиПисьма.Адресат.Наименование) КАК Представление,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыАдресатов.Кому) КАК ТипАдреса
		|ПОМЕСТИТЬ Данные
		|ИЗ
		|	ВходящиеПисьмаВременная КАК ВходящиеПисьмаВременная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВходящееПисьмо.ПолучателиПисьма КАК ВходящееПисьмоПолучателиПисьма
		|		ПО ВходящееПисьмоПолучателиПисьма.Ссылка = ВходящиеПисьмаВременная.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПредставления КАК СведенияПредставления
		|		ПО (СведенияПредставления.АдресатСообщения = ВходящееПисьмоПолучателиПисьма.Адресат.Ссылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВходящиеПисьмаВременная.Ссылка,
		|	ВходящееПисьмоПолучателиКопий.Адресат.Адрес,
		|	ВходящееПисьмоПолучателиКопий.Адресат,
		|	ЕСТЬNULL(СведенияПредставления.Представление, ВходящееПисьмоПолучателиКопий.Адресат.Наименование),
		|	ЗНАЧЕНИЕ(Перечисление.ТипыАдресатов.Копия)
		|ИЗ
		|	ВходящиеПисьмаВременная КАК ВходящиеПисьмаВременная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВходящееПисьмо.ПолучателиКопий КАК ВходящееПисьмоПолучателиКопий
		|		ПО ВходящееПисьмоПолучателиКопий.Ссылка = ВходящиеПисьмаВременная.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПредставления КАК СведенияПредставления
		|		ПО (СведенияПредставления.АдресатСообщения = ВходящееПисьмоПолучателиКопий.Адресат.Ссылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВходящиеПисьмаВременная.Ссылка,
		|	ВходящееПисьмоПолучателиОтвета.Адресат.Адрес,
		|	ВходящееПисьмоПолучателиОтвета.Адресат,
		|	ЕСТЬNULL(СведенияПредставления.Представление, ВходящееПисьмоПолучателиОтвета.Адресат.Наименование),
		|	ЗНАЧЕНИЕ(Перечисление.ТипыАдресатов.ОбратныйАдрес)
		|ИЗ
		|	ВходящиеПисьмаВременная КАК ВходящиеПисьмаВременная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВходящееПисьмо.ПолучателиОтвета КАК ВходящееПисьмоПолучателиОтвета
		|		ПО ВходящееПисьмоПолучателиОтвета.Ссылка = ВходящиеПисьмаВременная.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПредставления КАК СведенияПредставления
		|		ПО (СведенияПредставления.АдресатСообщения = ВходящееПисьмоПолучателиОтвета.Адресат.Ссылка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПочтовыйАдресат
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Данные.Письмо,
		|	Данные.Адрес,
		|	Данные.ПочтовыйАдресат.Наименование КАК Наименование,
		|	Данные.ПочтовыйАдресат КАК ПочтовыйАдресат,
		|	Данные.Представление КАК Представление,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(МИНИМУМ(СведенияОбАдресатах.Контакт)) = ТИП(Справочник.РолиИсполнителей)
		|		ИЛИ ТИПЗНАЧЕНИЯ(МИНИМУМ(СведенияОбАдресатах.Контакт)) = ТИП(Справочник.Сотрудники)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Внешний,
		|	Данные.ТипАдреса,
		|	ЕСТЬNULL(МИНИМУМ(СведенияОбАдресатах.Контакт), НЕОПРЕДЕЛЕНО) КАК Контакт
		|ИЗ
		|	Данные КАК Данные
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|		ПО Данные.ПочтовыйАдресат = СведенияОбАдресатах.АдресатСообщения
		|		И (СведенияОбАдресатах.Представление = Данные.Представление)
		|СГРУППИРОВАТЬ ПО
		|	Данные.Письмо,
		|	Данные.Адрес,
		|	Данные.ПочтовыйАдресат,
		|	Данные.Представление,
		|	Данные.ТипАдреса,
		|	Данные.ПочтовыйАдресат.Наименование
		|УПОРЯДОЧИТЬ ПО
		|	Представление";
		
	Запрос.УстановитьПараметр("Данные", Таблица);
	
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаЗначений;
	
КонецФункции

// Получает таблицу с информацией о получателях переданного массива исходящих писем
// Параметры:
//	МассивПисем - массив ссылок на письма. Из них будут обрабатываться только исходящие письма.
// Возвращает
//	ТаблицаЗначений
//		Письмо - ссылка на письмо
//		Адрес - адрес получателя
//		ПочтовыйАдресат - ссылка на адресата-получателя
//		ВидМаршрутизации - вид марщрутизации адресата
//		Внешний - признак того, что адресат внешний (булево)
//		Представление - почтовое представление адресата
//		ТипАдреса - строка "Кому", "Копия" или "Скрытая"
//		Контакт - ссылка на конткат из адресной книги, если есть
Функция ПолучитьПредставленияИКонтактыПолучателейИсходящихПисем(МассивПисем) Экспорт
	
	Таблица = Новый ТаблицаЗначений; 
	Массив = Новый Массив;
	Массив.Добавить(Тип("ДокументСсылка.ИсходящееПисьмо"));	
	НоваяКолонка = Таблица.Колонки.Добавить("Ссылка", Новый ОписаниеТипов(Массив));
	Для Каждого ЭлементМассива Из МассивПисем Цикл
		Если ТипЗнч(ЭлементМассива.Ссылка) = Тип("ДокументСсылка.ИсходящееПисьмо") Тогда
			Таблица.Добавить().Ссылка = ЭлементМассива.Ссылка;
		КонецЕсли;
	КонецЦикла;
	
	Если Таблица.Количество() = 0 Тогда
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Таблица.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ИсходящиеПисьмаВременная
		|ИЗ
		|	&Данные КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СведенияОбАдресатах.АдресатСообщения КАК АдресатСообщения,
		|	ПОДСТРОКА(МИНИМУМ(ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Сотрудники)
		|			ТОГДА ""0""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ФизическиеЛица)
		|			ТОГДА ""1""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Контрагенты)
		|			ТОГДА ""2""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.КонтактныеЛица)
		|			ТОГДА ""3""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.РолиИсполнителей)
		|			ТОГДА ""4""
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.ЛичныеАдресаты)
		|			ТОГДА ""5""
		|		ИНАЧЕ ""6""
		|	КОНЕЦ + СведенияОбАдресатах.Представление), 2, 1000) КАК Представление
		|ПОМЕСТИТЬ СведенияПредставления
		|ИЗ
		|	РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|СГРУППИРОВАТЬ ПО
		|	СведенияОбАдресатах.АдресатСообщения
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АдресатСообщения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИсходящиеПисьмаВременная.Ссылка КАК Письмо,
		|	ИсходящееПисьмоПолучателиПисьма.Адресат.Адрес КАК Адрес,
		|	ИсходящееПисьмоПолучателиПисьма.Адресат КАК ПочтовыйАдресат,
		|	ИсходящееПисьмоПолучателиПисьма.Адресат.ВидМаршрутизации КАК ВидМаршрутизации,
		|	ЕСТЬNULL(СведенияПредставления.Представление, ИсходящееПисьмоПолучателиПисьма.Адресат.Наименование) КАК
		|		Представление,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыАдресатов.Кому) КАК ТипАдреса
		|ПОМЕСТИТЬ Данные
		|ИЗ
		|	ИсходящиеПисьмаВременная КАК ИсходящиеПисьмаВременная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИсходящееПисьмо.ПолучателиПисьма КАК ИсходящееПисьмоПолучателиПисьма
		|		ПО ИсходящееПисьмоПолучателиПисьма.Ссылка = ИсходящиеПисьмаВременная.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПредставления КАК СведенияПредставления
		|		ПО (СведенияПредставления.АдресатСообщения = ИсходящееПисьмоПолучателиПисьма.Адресат.Ссылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИсходящиеПисьмаВременная.Ссылка,
		|	ИсходящееПисьмоПолучателиКопий.Адресат.Адрес,
		|	ИсходящееПисьмоПолучателиКопий.Адресат,
		|	ИсходящееПисьмоПолучателиКопий.Адресат.ВидМаршрутизации,
		|	ЕСТЬNULL(СведенияПредставления.Представление, ИсходящееПисьмоПолучателиКопий.Адресат.Наименование),
		|	ЗНАЧЕНИЕ(Перечисление.ТипыАдресатов.Копия)
		|ИЗ
		|	ИсходящиеПисьмаВременная КАК ИсходящиеПисьмаВременная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИсходящееПисьмо.ПолучателиКопий КАК ИсходящееПисьмоПолучателиКопий
		|		ПО ИсходящееПисьмоПолучателиКопий.Ссылка = ИсходящиеПисьмаВременная.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПредставления КАК СведенияПредставления
		|		ПО (СведенияПредставления.АдресатСообщения = ИсходящееПисьмоПолучателиКопий.Адресат.Ссылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИсходящиеПисьмаВременная.Ссылка,
		|	ИсходящееПисьмоПолучателиСкрытыхКопий.Адресат.Адрес,
		|	ИсходящееПисьмоПолучателиСкрытыхКопий.Адресат,
		|	ИсходящееПисьмоПолучателиСкрытыхКопий.Адресат.ВидМаршрутизации,
		|	ЕСТЬNULL(СведенияПредставления.Представление, ИсходящееПисьмоПолучателиСкрытыхКопий.Адресат.Наименование),
		|	ЗНАЧЕНИЕ(Перечисление.ТипыАдресатов.СкрытаяКопия)
		|ИЗ
		|	ИсходящиеПисьмаВременная КАК ИсходящиеПисьмаВременная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИсходящееПисьмо.ПолучателиСкрытыхКопий КАК ИсходящееПисьмоПолучателиСкрытыхКопий
		|		ПО ИсходящееПисьмоПолучателиСкрытыхКопий.Ссылка = ИсходящиеПисьмаВременная.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПредставления КАК СведенияПредставления
		|		ПО (СведенияПредставления.АдресатСообщения = ИсходящееПисьмоПолучателиСкрытыхКопий.Адресат.Ссылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИсходящиеПисьмаВременная.Ссылка,
		|	ИсходящееПисьмоПолучателиОтвета.Адресат.Адрес,
		|	ИсходящееПисьмоПолучателиОтвета.Адресат,
		|	ИсходящееПисьмоПолучателиОтвета.Адресат.ВидМаршрутизации,
		|	ЕСТЬNULL(СведенияПредставления.Представление, ИсходящееПисьмоПолучателиОтвета.Адресат.Наименование),
		|	ЗНАЧЕНИЕ(Перечисление.ТипыАдресатов.ОбратныйАдрес)
		|ИЗ
		|	ИсходящиеПисьмаВременная КАК ИсходящиеПисьмаВременная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИсходящееПисьмо.ПолучателиОтвета КАК ИсходящееПисьмоПолучателиОтвета
		|		ПО ИсходящееПисьмоПолучателиОтвета.Ссылка = ИсходящиеПисьмаВременная.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПредставления КАК СведенияПредставления
		|		ПО (СведенияПредставления.АдресатСообщения = ИсходящееПисьмоПолучателиОтвета.Адресат.Ссылка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПочтовыйАдресат
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Данные.Письмо,
		|	Данные.Адрес,
		|	Данные.ПочтовыйАдресат КАК Адресат,
		|	Данные.ПочтовыйАдресат.Наименование КАК Наименование,
		|	Данные.ВидМаршрутизации КАК ВидМаршрутизации,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(МИНИМУМ(СведенияОбАдресатах.Контакт)) = ТИП(Справочник.РолиИсполнителей)
		|		ИЛИ ТИПЗНАЧЕНИЯ(МИНИМУМ(СведенияОбАдресатах.Контакт)) = ТИП(Справочник.Сотрудники)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Внешний,
		|	Данные.Представление КАК Представление,
		|	Данные.ТипАдреса,
		|	ЕСТЬNULL(МИНИМУМ(СведенияОбАдресатах.Контакт), НЕОПРЕДЕЛЕНО) КАК Контакт
		|ИЗ
		|	Данные КАК Данные
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|		ПО Данные.ПочтовыйАдресат = СведенияОбАдресатах.АдресатСообщения
		|		И (СведенияОбАдресатах.Представление = Данные.Представление)
		|СГРУППИРОВАТЬ ПО
		|	Данные.Письмо,
		|	Данные.Адрес,
		|	Данные.ПочтовыйАдресат,
		|	Данные.ВидМаршрутизации,
		|	Данные.Представление,
		|	Данные.ТипАдреса,
		|	Данные.ПочтовыйАдресат.Наименование
		|УПОРЯДОЧИТЬ ПО
		|	Представление";
		
	Запрос.УстановитьПараметр("Данные", Таблица);
	
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаЗначений;
	
КонецФункции


/////////////////////////////////////////////////////////////////////////////////////////
// ИЗМЕНЕНИЕ АДРЕСНЫХ ДАННЫХ ПРИ РЕДАКТИРОВАНИИ КОНТАКТОВ

Процедура ПочтовыйКонтактПередЗаписьюПередЗаписью(Источник, Отказ) Экспорт
	
	Если ОбщегоНазначенияДокументооборот.ЭтоЗагрузкаИзУзлаРИБ(Источник) Тогда
    	Возврат;
	КонецЕсли;
	
	Если МиграцияПриложенийПереопределяемый.ЭтоЗагрузка(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущееПредставлениеОбъекта = "";
	Если Не Источник.Ссылка.Пустая() Тогда
		ПредыдущееПредставлениеОбъекта = Строка(Источник.Ссылка);
		Если ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.Пользователи") Тогда
			ПредыдущееПредставлениеОбъекта = Источник.Ссылка.ПредставлениеВПерепискеСРангом;
		КонецЕсли;
		Если ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.Сотрудники") Тогда
			ПредыдущееПредставлениеОбъекта = Источник.Ссылка.ПредставлениеВПереписке;
		КонецЕсли;
		
	КонецЕсли;
	Источник.ДополнительныеСвойства.Вставить(
		"ПредыдущееПредставлениеОбъекта", 
		ПредыдущееПредставлениеОбъекта); 		
	МассивПочтовыеАдреса = Новый Массив;
	Для каждого Строка Из Источник.Ссылка.КонтактнаяИнформация Цикл
		Если Строка.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты 
			И ЗначениеЗаполнено(Строка.АдресЭП) Тогда
			СтруктураПочтовыеАдреса = Новый Структура("Вид, Адрес, Обработан");
			СтруктураПочтовыеАдреса.Вид = Строка.Вид;
			СтруктураПочтовыеАдреса.Обработан = Ложь;
			СтруктураПочтовыеАдреса.Адрес = Строка.АдресЭП;
			МассивПочтовыеАдреса.Добавить(СтруктураПочтовыеАдреса);
		КонецЕсли;	
	КонецЦикла;
	Источник.ДополнительныеСвойства.Вставить(
		"ПредыдущееКоличествоПочтовыхАдресов", 
		МассивПочтовыеАдреса.Количество());
	Источник.ДополнительныеСвойства.Вставить("ПредыдущиеАдреса", МассивПочтовыеАдреса);
	
	Источник.ДополнительныеСвойства.Вставить(
		"ПредыдущаяПометкаУдаления", 
		Источник.Ссылка.ПометкаУдаления);

	Если ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.Сотрудники") Тогда
		Источник.ДополнительныеСвойства.Вставить(
			"ПредыдущаяПометкаДействует", 
			Источник.Ссылка.Действует);		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПочтовыйКонтактПриЗаписиПриЗаписи(Источник, Отказ) Экспорт
	
	Если ОбщегоНазначенияДокументооборот.ЭтоЗагрузкаИзУзлаРИБ(Источник) Тогда
    	Возврат;
	КонецЕсли;
	
	Если МиграцияПриложенийПереопределяемый.ЭтоЗагрузка(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ПометкаУдаления 
		И Источник.ДополнительныеСвойства.ПредыдущаяПометкаУдаления = Источник.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	ТекущееПредставлениеОбъекта = "";
	
	ТекущееПредставлениеОбъекта = Строка(Источник.Ссылка);
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.Пользователи") Тогда
		ТекущееПредставлениеОбъекта = Источник.ПредставлениеВПерепискеСРангом;
	КонецЕсли;
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.Сотрудники") Тогда
		ТекущееПредставлениеОбъекта = Источник.ПредставлениеВПереписке;
	КонецЕсли;
	
	// Получение текущих контактных данных Источника.
	МассивТекущиеПочтовыеАдреса = Новый Массив;
	Для каждого Строка Из Источник.Ссылка.КонтактнаяИнформация Цикл
		Если Строка.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты 
			И ЗначениеЗаполнено(Строка.АдресЭП) Тогда
			СтруктураПочтовыеАдреса = Новый Структура("Вид, Адрес, НайденСредиСтарых");
			СтруктураПочтовыеАдреса.Вид = Строка.Вид;
			СтруктураПочтовыеАдреса.НайденСредиСтарых = Ложь;
			СтруктураПочтовыеАдреса.Адрес = Строка.АдресЭП;
			МассивТекущиеПочтовыеАдреса.Добавить(СтруктураПочтовыеАдреса);
		КонецЕсли;	
	КонецЦикла;
			
	Если ТекущееПредставлениеОбъекта <> Источник.ДополнительныеСвойства.ПредыдущееПредставлениеОбъекта Тогда
		// Необходимо обновить все представления в регистре СведенияОбадресатах, ссылающиеся на этот Источник.
		УстановитьПривилегированныйРежим(Истина);
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СведенияОбАдресатах.АдресатСообщения,
			|	СведенияОбАдресатах.Представление
			|ИЗ
			|	РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
			|ГДЕ
			|	СведенияОбАдресатах.Контакт = &Контакт";
		Запрос.УстановитьПараметр("Контакт", Источник.Ссылка);
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				НаборРегистра = РегистрыСведений.СведенияОбАдресатах.СоздатьНаборЗаписей();
				НаборРегистра.Отбор.АдресатСообщения.Установить(Выборка.АдресатСообщения);
				НаборРегистра.Прочитать();
				Для Каждого Запись Из НаборРегистра Цикл
					Запись.Представление = ПолучитьПочтовоеПредставлениеКонтакта(
						Запись.Контакт,
						Запись.АдресатСообщения.Адрес);
				КонецЦикла;
				НаборРегистра.Записать();				
			КонецЦикла;
		КонецЕсли;  
		
		// для всех адресов обновляем АдресатПочтовыхСообщений.	
		Для Каждого ТекущийАдресСтруктура Из МассивТекущиеПочтовыеАдреса Цикл
			
			ТекущийАдрес = ТекущийАдресСтруктура.Адрес;
			
			ПредыдущееПредставление = Источник.ДополнительныеСвойства.ПредыдущееПредставлениеОбъекта + " <" + ТекущийАдрес + ">";

			Если Не ПустаяСтрока(ПредыдущееПредставление) Тогда
				
				Адресат = НайтиПочтовогоАдресата(ТекущийАдрес);
				Если ЗначениеЗаполнено(Адресат) Тогда
					
					Внешний = Ложь;
					Контакт = Неопределено;
					ЕстьКонтактыСПравами = Ложь;
					ПриоритетноеПредставление = ПриоритетноеПредставление(
						ТекущийАдрес, Внешний, Контакт, ЕстьКонтактыСПравами);
						
					// если новый контакт - более приоритетен
					Если Не ЗначениеЗаполнено(ПриоритетноеПредставление)
						Или НовыйКонтактБолееПриоритетен(Контакт, Источник.Ссылка) Тогда
						
						НовоеНаименование = ТекущееПредставлениеОбъекта + " <" + ТекущийАдрес + ">";
						
						АдресатОбъект = Адресат.ПолучитьОбъект();
						АдресатОбъект.Наименование = НовоеНаименование; 
						АдресатОбъект.Контакт = Источник.Ссылка;
						АдресатОбъект.ЕстьВКонтактах = Истина;
						АдресатОбъект.Записать();
						
						МенеджерЗаписиРегистра = РегистрыСведений.СведенияОбАдресатах.СоздатьМенеджерЗаписи();
						МенеджерЗаписиРегистра.АдресатСообщения = Адресат;
						МенеджерЗаписиРегистра.Контакт = Источник.Ссылка;
						МенеджерЗаписиРегистра.Представление = НовоеНаименование;
						МенеджерЗаписиРегистра.Записать();
						
					КонецЕсли;	
					
				КонецЕсли;	

			КонецЕсли;	
			
		КонецЦикла;
		
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Для Каждого ТекущийАдрес Из МассивТекущиеПочтовыеАдреса Цикл
		Для Каждого СтарыйАдрес Из Источник.ДополнительныеСвойства.ПредыдущиеАдреса Цикл
			Если ТекущийАдрес.Вид = СтарыйАдрес.Вид Тогда
				ТекущийАдрес.НайденСредиСтарых = Истина;
				СтарыйАдрес.Обработан = Истина;
				Если ТекущийАдрес.Адрес <> СтарыйАдрес.Адрес Тогда // адрес был изменен
					// Если адрес был изменен, необходимо перезаписать ссылку на адресата в регистре СведенияОбАдресатах и 
					//	перевычислить представление.                    
					НовыйПочтовыйАдресат = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПочтовогоАдресата(
						ТекущийАдрес.Адрес,	ТекущееПредставлениеОбъекта, Источник.Ссылка);
					ПредыдущийПочтовыйАдресат = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПочтовогоАдресата(
						СтарыйАдрес.Адрес, Источник.Ссылка);
					МенеджерЗаписиРегистра = РегистрыСведений.СведенияОбАдресатах.СоздатьМенеджерЗаписи();
					МенеджерЗаписиРегистра.АдресатСообщения = ПредыдущийПочтовыйАдресат;
					МенеджерЗаписиРегистра.Контакт = Источник.Ссылка;
					МенеджерЗаписиРегистра.Прочитать();
					МенеджерЗаписиРегистра.АдресатСообщения = НовыйПочтовыйАдресат;
					МенеджерЗаписиРегистра.Контакт = Источник.Ссылка;
					МенеджерЗаписиРегистра.Представление = ПолучитьПочтовоеПредставлениеКонтакта(
						Источник.Ссылка,
						НовыйПочтовыйАдресат.Адрес);
					МенеджерЗаписиРегистра.Активна = 
						НЕ Источник.ПометкаУдаления
						И НЕ(ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.Сотрудники") И Не Источник.Действует);
					МенеджерЗаписиРегистра.Записать();
				КонецЕсли;
			КонецЕсли;	
		КонецЦикла;
	КонецЦикла;
	
	НеобходимоПерезаписатьПредставления = Ложь;
	Для Каждого ТекущийАдрес Из МассивТекущиеПочтовыеАдреса Цикл
		Если НЕ ТекущийАдрес.НайденСредиСтарых Тогда
			// Был введен новый адрес, необходимо записать об этом информацию.
			НовыйПочтовыйАдресат = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПочтовогоАдресата(
				ТекущийАдрес.Адрес,	ТекущееПредставлениеОбъекта);
			МенеджерЗаписиРегистра = РегистрыСведений.СведенияОбАдресатах.СоздатьМенеджерЗаписи();
			МенеджерЗаписиРегистра.АдресатСообщения = НовыйПочтовыйАдресат;
			МенеджерЗаписиРегистра.Контакт = Источник.Ссылка;
			МенеджерЗаписиРегистра.Прочитать();
			Если Не МенеджерЗаписиРегистра.Выбран() Тогда	
				МенеджерЗаписиРегистра.АдресатСообщения = НовыйПочтовыйАдресат;
				МенеджерЗаписиРегистра.Контакт = Источник.Ссылка;
			КонецЕсли;
			МенеджерЗаписиРегистра.Представление = ПолучитьПочтовоеПредставлениеКонтакта(
				Источник.Ссылка,
				НовыйПочтовыйАдресат.Адрес);
			МенеджерЗаписиРегистра.Активна = 
						НЕ Источник.ПометкаУдаления
						И НЕ(ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.Сотрудники") И Не Источник.Действует);	
			МенеджерЗаписиРегистра.Записать();
			
			// Т.к. был добавлен новый адрес, необходимо перевычислить старые представления контакта.
			НеобходимоПерезаписатьПредставления = Истина;
		КонецЕсли;
	КонецЦикла;
		
	Для Каждого СтарыйАдрес Из Источник.ДополнительныеСвойства.ПредыдущиеАдреса Цикл
		Если Не СтарыйАдрес.Обработан Тогда
			// Адрес был удален, необходимо очистить соответствующие структуры.
			ПредыдущийПочтовыйАдресат = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПочтовогоАдресата(
				СтарыйАдрес.Адрес);
			МенеджерЗаписиРегистра = РегистрыСведений.СведенияОбАдресатах.СоздатьМенеджерЗаписи();
			МенеджерЗаписиРегистра.АдресатСообщения = ПредыдущийПочтовыйАдресат;
			МенеджерЗаписиРегистра.Контакт = Источник.Ссылка;
			МенеджерЗаписиРегистра.Удалить();
			
			// Т.к. был удален адрес, необходимо перевычислить старые представления контакта.
			НеобходимоПерезаписатьПредставления = Истина;
		КонецЕсли;
	КонецЦикла; 
	
	Если Источник.ДополнительныеСвойства.ПредыдущаяПометкаУдаления <> Источник.ПометкаУдаления 
		ИЛИ ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.Сотрудники")
		И Источник.ДополнительныеСвойства.ПредыдущаяПометкаДействует <> Источник.Действует Тогда
		НеобходимоПерезаписатьПредставления = Истина;
	КонецЕсли;
	
	// Если у контакта есть адреса, но для них нет записей в регистре СведенияОбАдресатах, 
	//	или необходимо перевычислить представления, необходимо добавить записи в этот регистр.
	Для Каждого Запись Из МассивТекущиеПочтовыеАдреса Цикл
		Адресат = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПочтовогоАдресата(
			Запись.Адрес, 
			ТекущееПредставлениеОбъекта);
		МенеджерЗаписиРегистра = РегистрыСведений.СведенияОбАдресатах.СоздатьМенеджерЗаписи();
		МенеджерЗаписиРегистра.АдресатСообщения = Адресат;
		МенеджерЗаписиРегистра.Контакт = Источник.Ссылка;
		МенеджерЗаписиРегистра.Прочитать();
		Если Не МенеджерЗаписиРегистра.Выбран() Тогда
			МенеджерЗаписиРегистра.АдресатСообщения = Адресат;
			МенеджерЗаписиРегистра.Контакт = Источник.Ссылка;
			МенеджерЗаписиРегистра.Представление = 
				ПолучитьПочтовоеПредставлениеКонтакта(Источник.Ссылка, Запись.Адрес);
			МенеджерЗаписиРегистра.Активна = 
						НЕ Источник.ПометкаУдаления
						И НЕ(ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.Сотрудники") И Не Источник.Действует);	
			МенеджерЗаписиРегистра.Записать();
		ИначеЕсли НеобходимоПерезаписатьПредставления
			И МенеджерЗаписиРегистра.Контакт = Источник.Ссылка Тогда
			
			МенеджерЗаписиРегистра.Представление = 
				ПолучитьПочтовоеПредставлениеКонтакта(Источник.Ссылка, Запись.Адрес);
			МенеджерЗаписиРегистра.Активна = 
						НЕ Источник.ПометкаУдаления
						И НЕ(ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.Сотрудники") И Не Источник.Действует);	
			МенеджерЗаписиРегистра.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновитьСведенияОбАдресатах = Ложь;
	ОбновитьПредставлениеАдресатовПоКонтакту(Источник.Ссылка, ОбновитьСведенияОбАдресатах);

	ПредыдущиеАдресаМассив = Новый Массив;
	Для каждого Строка Из Источник.ДополнительныеСвойства.ПредыдущиеАдреса Цикл
		
		ПредыдущиеАдресаМассив.Добавить(Строка.Адрес);
		
	КонецЦикла;	
	
	ОбновитьПредставлениеАдресатовДляМассиваАдресов(
		ПредыдущиеАдресаМассив, 
		ОбновитьСведенияОбАдресатах, 
		Неопределено // Контакт - пуст
		);
	
КонецПроцедуры

// Вычисляет для указанных адресов приоритетное представление адресатов почтовых сообщений
//
// Параметры:
//  МассивТекущиеПочтовыеАдреса - Массив Из Строка - массив email
//  ОбновитьСведенияОбАдресатах - Булево
//  КонтактСсылка - СправочникСсылка.Сотрудники, СправочникСсылка.ФизическиеЛица, СправочникСсылка.Контрагенты, СправочникСсылка.ЛичныеАдресаты, СправочникСсылка.РолиИсполнителей, СправочникСсылка.Пользователи, СправочникСсылка.КонтактныеЛица
//  ОбработатьДанныеДляПереходаНаНовуюВерсию - Булево - если Истина,  используем ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные
//
Процедура ОбновитьПредставлениеАдресатовДляМассиваАдресов(
	МассивТекущиеПочтовыеАдреса,
	ОбновитьСведенияОбАдресатах, 
	КонтактСсылка,
	ОбработатьДанныеДляПереходаНаНовуюВерсию = Ложь) Экспорт
	
	Для Каждого ТекущийАдрес Из МассивТекущиеПочтовыеАдреса Цикл	
		
		Внешний = Ложь;
		Контакт = Неопределено;
		
		ЕстьКонтактыСПравами = Ложь;
		ПриоритетноеПредставление = ПриоритетноеПредставление(ТекущийАдрес, Внешний, Контакт, ЕстьКонтактыСПравами);
		Если ЗначениеЗаполнено(ПриоритетноеПредставление) Тогда
			ОбновитьАдресатыПочтовыхСообщений(ТекущийАдрес, ПриоритетноеПредставление, Истина, Внешний, Контакт, ЕстьКонтактыСПравами,
				, ОбработатьДанныеДляПереходаНаНовуюВерсию);
		Иначе	
			ОбновитьАдресатыПочтовыхСообщений(ТекущийАдрес, "", Ложь, Внешний, Контакт, ЕстьКонтактыСПравами,
				, ОбработатьДанныеДляПереходаНаНовуюВерсию);
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////////////
// РАБОТА С ПРОТОКОЛОМ ДОСТАВКИ ПОЧТЫ

// Записать событие в регистр ПротоколДоставкиПочты 
Процедура ЗаписатьПротоколДоставкиПочты(Письмо, Текст, ЭтоОшибка, ТипСобытия,
	УчетнаяЗапись, ПорядковыйНомерСобытия, ИдентификаторСеанса,
	НомерЗадания = 0) Экспорт
	
	Если Не ВстроеннаяПочтаСерверПовтИсп.ПолучитьВестиПротоколДоставкиПочты() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.ПротоколДоставкиПочты.СоздатьМенеджерЗаписи();
	
	ПорядковыйНомерСобытия = ПорядковыйНомерСобытия + 1;
	
	Запись.Письмо = Письмо;
	Запись.Дата = ТекущаяДатаСеанса();
	Запись.ИдентификаторСеанса = ИдентификаторСеанса;
	Запись.ПорядковыйНомерСобытия = ПорядковыйНомерСобытия;
	
	Запись.Сотрудник = Сотрудники.ОсновнойСотрудник();
	Запись.УчетнаяЗапись = УчетнаяЗапись;
	Запись.ЭтоОшибка = ЭтоОшибка;
	Запись.ТипСобытия = ТипСобытия;
	Запись.Текст = Текст;
	Запись.НомерЗадания = НомерЗадания;
	
	Запись.Записать();
	
КонецПроцедуры

// Записывает ошибку и в журнал регистрации и в регистр ПротоколДоставкиПочты
Процедура ЗаписатьОшибкуДоставки(СообщениеОбОшибке, Письмо, УчетнаяЗапись, ПараметрыЛогирования,
	ОсталосьПопытокОтправки = Неопределено, 
	ТипСобытияДоставки = Неопределено) Экспорт
	
	Почта.ЗаписатьОшибкуВЖурналРегистрации(СообщениеОбОшибке, Письмо);
	
	СообщениеОбОшибкеЛокальное = СообщениеОбОшибке;
	
	Если ОсталосьПопытокОтправки <> Неопределено Тогда
		СообщениеОбОшибкеЛокальное = СообщениеОбОшибкеЛокальное + Символы.ПС +
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Осталось попыток отправки: ""%1""'"), 
			ОсталосьПопытокОтправки);
		КонецЕсли;
		
	Если ТипСобытияДоставки = Неопределено Тогда
		ТипСобытияДоставки = Перечисления.ТипыСобытийДоставкиПочты.ОшибкаОтправкиПисьма;
	КонецЕсли;
	
	ЗаписатьПротоколДоставкиПочты(Письмо, СообщениеОбОшибкеЛокальное, 
		Истина,
		ТипСобытияДоставки,
		УчетнаяЗапись, ПараметрыЛогирования.ПорядковыйНомерСобытия, 
		ПараметрыЛогирования.ИдентификаторСеанса,
		ПараметрыЛогирования.НомерЗадания);
		
	// Запись уведомлений программы
	Если ТипСобытияДоставки = Перечисления.ТипыСобытийДоставкиПочты.ОшибкаОтправкиПисьма
		И Письмо <> Неопределено Тогда
		
		АвторПисьма = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Письмо, "Автор");
		
		АвторПисьма = Сотрудники.ЛюбойПользовательСотрудника(АвторПисьма);
		
		ТекстУведомления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Во время отправки этого письма возникли ошибки:
				|%1'"),
				СообщениеОбОшибке);
				
		РаботаСУведомлениями.ОбработатьУведомлениеПрограммы(
				ТекстУведомления,
				АвторПисьма,
				Письмо);
				
	КонецЕсли;
	
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////////////////
// РАБОТА С НАСТРОЙКАМИ ВСТРОЕННОЙ ПОЧТЫ

// Возвращает персональную настройку встроенной почты текущего пользователя по ключу.
//
Функция ПолучитьПерсональнуюНастройку(Настройка, Пользователь = Неопределено, ДопОписаниеНастройки = "") Экспорт
	
	ИмяПользователяИБ = ?(Пользователь <> Неопределено,
		ОбщегоНазначенияДокументооборот.ИмяПользователяИБ(Пользователь),
		Неопределено);
	
	Если Настройка = "АвтовыборКодировкиИсходящихПисем" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"АвтовыборКодировкиИсходящихПисем",
			Ложь,,
			ИмяПользователяИБ);
		
	ИначеЕсли Настройка = "ВставлятьТекстИсходногоПисьмаПриОтвете" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ВставлятьТекстИсходногоПисьмаПриОтвете",
			Истина,,
			ИмяПользователяИБ);
		
	ИначеЕсли Настройка = "ДействиеПриВыбореВходящегоПисьма" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ДействиеПриВыбореВходящегоПисьма",
			Перечисления.ДействияПриВыбореВходящегоПисьма.ОткрытьКарточкуПисьма,,
			ИмяПользователяИБ);
		
	ИначеЕсли Настройка = "ДобавлятьАвтораИсходногоПисьмаПриПересылке" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ДобавлятьАвтораИсходногоПисьмаПриПересылке",
			Ложь,,
			ИмяПользователяИБ);
		
	ИначеЕсли Настройка = "ДобавлятьПолучателейИсходногоПисьмаПриПересылке" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ДобавлятьПолучателейИсходногоПисьмаПриПересылке",
			Ложь,,
			ИмяПользователяИБ);
		
	ИначеЕсли Настройка = "ДобавлятьПолучателейКопийИсходногоПисьмаПриПересылке" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ДобавлятьПолучателейКопийИсходногоПисьмаПриПересылке",
			Ложь,,
			ИмяПользователяИБ);
		
	ИначеЕсли Настройка = "КодировкаИсходящихПисем" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"КодировкаИсходящихПисем",
			"utf-8",,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "КодировкаПриСохраненииПисем" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"КодировкаПриСохраненииПисем",
			"",,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "ОсновнаяУчетнаяЗапись" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ОсновнаяУчетнаяЗапись",
			Неопределено,,
			ИмяПользователяИБ);
		
	ИначеЕсли Настройка = "ОсновнойФлаг" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ФлагиОбъектов",
			"ФлагПоУмолчаниюДляПисем",
			Перечисления.ФлагиОбъектов.Красный,,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "ОткрыватьФайлыИсходящегоПисьмаНаРедактирование" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ОткрыватьФайлыИсходящегоПисьмаНаРедактирование",
			Ложь,,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "ОтвечатьСФайлами" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ОтвечатьСФайлами",
			Ложь,,
			ИмяПользователяИБ);
		
	ИначеЕсли Настройка = "ОтображатьУдаленныеПисьмаИПапки" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ОтображатьУдаленныеПисьмаИПапки",
			Ложь,,
			ИмяПользователяИБ);
		
	ИначеЕсли Настройка = "ОтображениеОбластиЧтения" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ОтображениеОбластиЧтения",
			Перечисления.ВариантыОтображенияОбластиЧтения.Внизу,,
			ИмяПользователяИБ);
		
	ИначеЕсли Настройка = "ПериодАвтоматическогоСохраненияНеотправленныхПисем" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ПериодАвтоматическогоСохраненияНеотправленныхПисем",
			0,,
			ИмяПользователяИБ);
		
	ИначеЕсли Настройка = "ПодписьДляНовыхПисем" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ПодписьДляНовыхПисем",
			Неопределено,,
			ИмяПользователяИБ);
		
	ИначеЕсли Настройка = "ПодписьПриОтветеИПересылке" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ПодписьПриОтветеИПересылке",
			Неопределено,,
			ИмяПользователяИБ);
		
	ИначеЕсли Настройка = "РежимМоиПапки" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"РежимМоиПапки",
			Ложь,,
			ИмяПользователяИБ);
		
	ИначеЕсли Настройка = "ШиринаСтрокиПриСохранении" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ШиринаСтрокиПриСохранении",
			0,,
			ИмяПользователяИБ);
		
	ИначеЕсли Настройка = "ШиринаСтрокиПриФорматировании" Тогда
			
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ШиринаСтрокиПриФорматировании",
			0,,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "ПоказыватьПредупреждениеПриОтправкеПисьмаБольшомуЧислуАдресатов" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ПоказыватьПредупреждениеПриОтправкеПисьмаБольшомуЧислуАдресатов",
			Истина,,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "ПредупреждатьПриПересылкеВнутреннихПисемВнешнимПолучателям" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ПредупреждатьПриПересылкеВнутреннихПисемВнешнимПолучателям",
			Истина,,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "НеПредлагатьСоздатьПравилоПриПеретаскивании" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"НеПредлагатьСоздатьПравилоПриПеретаскивании",
			Ложь,,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "СпособРедактированияТекстаПисьма" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"СпособРедактированияТекстаПисьма",
			Перечисления.СпособРедактированияТекстаПисьма.Авто,,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "ЧислоСимволовПисьмаДляСпособаРедактирования" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ЧислоСимволовПисьмаДляСпособаРедактирования",
			5000,,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "ФорматНовыхПисем" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ФорматНовыхПисем",
			Перечисления.ФорматыНовыхПисем.HTML,,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "ФорматОтветныхИПересылаемыхПисем" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ФорматОтветныхИПересылаемыхПисем",
			Неопределено,,
			ИмяПользователяИБ);
			
		Если Не ЗначениеЗаполнено(Значение) Тогда
			
			Значение = ПредопределенноеЗначение("Перечисление.ФорматыОтветныхИПересылаемыхПисем.ФорматИсходногоПисьма");
			
			ИспользоватьHTMLФорматПисем = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
				"ВстроеннаяПочта",
				"ИспользоватьHTMLФорматПисем",
				Неопределено,,
				ИмяПользователяИБ);
		
			Если ИспользоватьHTMLФорматПисем <> Неопределено Тогда
				Если Не ИспользоватьHTMLФорматПисем Тогда
					Значение = ПредопределенноеЗначение("Перечисление.ФорматыОтветныхИПересылаемыхПисем.ПростойТекст");
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли Настройка = "ФорматСохраненияПисьма" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ФорматСохраненияПисьма",
			Неопределено,,
			ИмяПользователяИБ);
		
		Если Не ЗначениеЗаполнено(Значение) Тогда
			
			ФорматОтветныхИПересылаемыхПисем = ПолучитьПерсональнуюНастройку("ФорматОтветныхИПересылаемыхПисем", Пользователь);
			Если ФорматОтветныхИПересылаемыхПисем = Перечисления.ФорматыОтветныхИПересылаемыхПисем.ПростойТекст Тогда
				Значение = Перечисления.ФорматыСохраненияПисьма.ПростойТекст;
			Иначе
				Значение = Перечисления.ФорматыСохраненияПисьма.HTML;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли Настройка = "ПомечатьВходящиеПисьмаПрочтеннымиПриПросмотреВОбластиЧтения" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ПомечатьВходящиеПисьмаПрочтеннымиПриПросмотреВОбластиЧтения",
			Ложь,,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "ПоказыватьПредупреждениеПриОтправкеОтветаНаICalendar" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ПоказыватьПредупреждениеПриОтправкеОтветаНаICalendar",
			Истина,,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "АвтоматическиСохранятьВерсииНеотправленногоПисьма" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"АвтоматическиСохранятьВерсииНеотправленногоПисьма",
			Истина);
			
	ИначеЕсли Настройка = "ПомечатьВходящиеПисьмаПрочтеннымиТолькоВручную" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ПомечатьВходящиеПисьмаПрочтеннымиТолькоВручную",
			Ложь,,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "ВремяПроверкиНовыхПисем" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ВремяПроверкиНовыхПисем",
			5,,
			ИмяПользователяИБ); //  5 минут
			
	ИначеЕсли Настройка = "ПроверятьНаличиеНовыхПисем" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ПроверятьНаличиеНовыхПисем",
			Истина,,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "ВидСписка" Тогда
			
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ВидСписка",
			"Обычный",,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "ШрифтОтображения" Тогда
		
		ШрифтОтображенияПоУмолчанию = ВстроеннаяПочтаКлиентСервер.ПолучитьШрифтПочтыПоУмолчанию();
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ШрифтОтображения",
			ШрифтОтображенияПоУмолчанию,,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "ОтображатьДругиеШрифтыВПисьмах" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ОтображатьДругиеШрифтыВПисьмах",
			Истина,,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "ИспользоватьСвойСписокДляАвтоподбора" Тогда
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ИспользоватьСвойСписокДляАвтоподбора",
			Ложь,,
			ИмяПользователяИБ);
			
	ИначеЕсли  Настройка = "ОтправлятьPushУведомленияОНовыхПисьмах" Тогда
		
		КлючНастройки = СтрШаблон("%1%2",
			Настройка, ?(ЗначениеЗаполнено(ДопОписаниеНастройки), "_" + ДопОписаниеНастройки, ""));
		
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ВстроеннаяПочта",
			КлючНастройки,
			Истина,,
			ИмяПользователяИБ);
			
	ИначеЕсли Настройка = "ОткрыватьПриглашенияВнутриДО" Тогда
			
		Значение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВстроеннаяПочта",
			"ОткрыватьПриглашенияВнутриДО",
			Истина); //  по умолчанию вКлючено
			
	Иначе
		
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Некорректная настройка встроенной почты: ""%1""'"),
			Настройка);
		
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

// Сохраняет персональную настройку встроенной почты текущего пользователя.
//
Процедура УстановитьПерсональнуюНастройку(Настройка, Значение, ДопОписаниеНастройки = "") Экспорт
	
	Если Настройка = "АвтовыборКодировкиИсходящихПисем" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"АвтовыборКодировкиИсходящихПисем",
			Значение);
		
	ИначеЕсли Настройка = "ВставлятьТекстИсходногоПисьмаПриОтвете" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ВставлятьТекстИсходногоПисьмаПриОтвете",
			Значение);
		
	ИначеЕсли Настройка = "ДействиеПриВыбореВходящегоПисьма" Тогда
		
		Если ТипЗнч(Значение) <> Тип("ПеречислениеСсылка.ДействияПриВыбореВходящегоПисьма") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ДействиеПриВыбореВходящегоПисьма",
			Значение);
		
	ИначеЕсли Настройка = "ДобавлятьАвтораИсходногоПисьмаПриПересылке" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ДобавлятьАвтораИсходногоПисьмаПриПересылке",
			Значение);
		
	ИначеЕсли Настройка = "ДобавлятьПолучателейИсходногоПисьмаПриПересылке" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ДобавлятьПолучателейИсходногоПисьмаПриПересылке",
			Значение);
		
	ИначеЕсли Настройка = "ДобавлятьПолучателейКопийИсходногоПисьмаПриПересылке" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ДобавлятьПолучателейКопийИсходногоПисьмаПриПересылке",
			Значение);
		
	ИначеЕсли Настройка = "КодировкаИсходящихПисем" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Строка") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"КодировкаИсходящихПисем",
			Значение);
			
	ИначеЕсли Настройка = "КодировкаПриСохраненииПисем" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Строка") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"КодировкаПриСохраненииПисем",
			Значение);
			
	ИначеЕсли Настройка = "ОсновнаяУчетнаяЗапись" Тогда
		
		Если ТипЗнч(Значение) <> Тип("СправочникСсылка.УчетныеЗаписиЭлектроннойПочты")
			И Значение <> Неопределено Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ОсновнаяУчетнаяЗапись",
			Значение);
		
	ИначеЕсли Настройка = "ОсновнойФлаг" Тогда
		
		Если ТипЗнч(Значение) <> Тип("ПеречислениеСсылка.ФлагиОбъектов") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ФлагиОбъектов",
			"ФлагПоУмолчаниюДляПисем",
			Значение);
			
	ИначеЕсли Настройка = "ОткрыватьФайлыИсходящегоПисьмаНаРедактирование" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ОткрыватьФайлыИсходящегоПисьмаНаРедактирование",
			Значение);
		
	ИначеЕсли Настройка = "ОтвечатьСФайлами" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ОтвечатьСФайлами",
			Значение);
		
	ИначеЕсли Настройка = "ОтображатьУдаленныеПисьмаИПапки" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ОтображатьУдаленныеПисьмаИПапки",
			Значение);
		
	ИначеЕсли Настройка = "ОтображениеОбластиЧтения" Тогда
		
		Если ТипЗнч(Значение) <> Тип("ПеречислениеСсылка.ВариантыОтображенияОбластиЧтения") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ОтображениеОбластиЧтения",
			Значение);
		
	ИначеЕсли Настройка = "ПериодАвтоматическогоСохраненияНеотправленныхПисем" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Число") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ПериодАвтоматическогоСохраненияНеотправленныхПисем",
			Значение);
		
	ИначеЕсли Настройка = "ПодписьДляНовыхПисем" Тогда
		
		Если ТипЗнч(Значение) <> Тип("СправочникСсылка.ШаблоныТекстов")
			И Значение <> Неопределено Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ПодписьДляНовыхПисем",
			Значение);
		
	ИначеЕсли Настройка = "ПодписьПриОтветеИПересылке" Тогда
		
		Если ТипЗнч(Значение) <> Тип("СправочникСсылка.ШаблоныТекстов")
			И Значение <> Неопределено Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ПодписьПриОтветеИПересылке",
			Значение);
			
	ИначеЕсли Настройка = "РежимМоиПапки" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"РежимМоиПапки",
			Значение);
		
	ИначеЕсли Настройка = "ШиринаСтрокиПриСохранении" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Число") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ШиринаСтрокиПриСохранении",
			Значение);
		
	ИначеЕсли Настройка = "ШиринаСтрокиПриФорматировании" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Число") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ШиринаСтрокиПриФорматировании",
			Значение);
			
	ИначеЕсли Настройка = "ПоказыватьПредупреждениеПриОтправкеПисьмаБольшомуЧислуАдресатов" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ПоказыватьПредупреждениеПриОтправкеПисьмаБольшомуЧислуАдресатов",
			Значение);
			
	ИначеЕсли Настройка = "ПредупреждатьПриПересылкеВнутреннихПисемВнешнимПолучателям" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ПредупреждатьПриПересылкеВнутреннихПисемВнешнимПолучателям",
			Значение);		
			
	ИначеЕсли Настройка = "НеПредлагатьСоздатьПравилоПриПеретаскивании" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"НеПредлагатьСоздатьПравилоПриПеретаскивании",
			Значение);		
			
	ИначеЕсли Настройка = "СпособРедактированияТекстаПисьма" Тогда
		
		Если ТипЗнч(Значение) <> Тип("ПеречислениеСсылка.СпособРедактированияТекстаПисьма") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"СпособРедактированияТекстаПисьма",
			Значение);
			
	ИначеЕсли Настройка = "ЧислоСимволовПисьмаДляСпособаРедактирования" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Число") Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ЧислоСимволовПисьмаДляСпособаРедактирования",
			Значение);
			
	ИначеЕсли Настройка = "ФорматНовыхПисем" Тогда
		
		Если ТипЗнч(Значение) <> Тип("ПеречислениеСсылка.ФорматыНовыхПисем") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ФорматНовыхПисем",
			Значение);
			
	ИначеЕсли Настройка = "ФорматОтветныхИПересылаемыхПисем" Тогда
		
		Если ТипЗнч(Значение) <> Тип("ПеречислениеСсылка.ФорматыОтветныхИПересылаемыхПисем") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ФорматОтветныхИПересылаемыхПисем",
			Значение);
			
	ИначеЕсли Настройка = "ФорматСохраненияПисьма" Тогда
		
		Если ТипЗнч(Значение) <> Тип("ПеречислениеСсылка.ФорматыСохраненияПисьма") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ФорматСохраненияПисьма",
			Значение);
			
	ИначеЕсли Настройка = "ПомечатьВходящиеПисьмаПрочтеннымиПриПросмотреВОбластиЧтения" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ПомечатьВходящиеПисьмаПрочтеннымиПриПросмотреВОбластиЧтения",
			Значение);
			
	ИначеЕсли Настройка = "ПоказыватьПредупреждениеПриОтправкеОтветаНаICalendar" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ПоказыватьПредупреждениеПриОтправкеОтветаНаICalendar",
			Значение);
			
	ИначеЕсли Настройка = "АвтоматическиСохранятьВерсииНеотправленногоПисьма" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"АвтоматическиСохранятьВерсииНеотправленногоПисьма",
			Значение);
			
	ИначеЕсли Настройка = "ПомечатьВходящиеПисьмаПрочтеннымиТолькоВручную" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ПомечатьВходящиеПисьмаПрочтеннымиТолькоВручную",
			Значение);
			
	ИначеЕсли Настройка = "ВремяПроверкиНовыхПисем" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Число") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ВремяПроверкиНовыхПисем",
			Значение);
			
	ИначеЕсли Настройка = "ПроверятьНаличиеНовыхПисем" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ПроверятьНаличиеНовыхПисем",
			Значение);
			
	ИначеЕсли Настройка = "ВидСписка" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Строка") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ВидСписка",
			Значение);
			
	ИначеЕсли Настройка = "ШрифтОтображения" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Шрифт") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ШрифтОтображения",
			Значение);
			
	ИначеЕсли Настройка = "ОтображатьДругиеШрифтыВПисьмах" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ОтображатьДругиеШрифтыВПисьмах",
			Значение);
			
	ИначеЕсли Настройка = "ИспользоватьСвойСписокДляАвтоподбора" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ИспользоватьСвойСписокДляАвтоподбора",
			Значение);
			
	ИначеЕсли Настройка = "ОтправлятьPushУведомленияОНовыхПисьмах" Тогда
		
		КлючНастройки = СтрШаблон("%1%2",
			Настройка, ?(ЗначениеЗаполнено(ДопОписаниеНастройки), "_" + ДопОписаниеНастройки, ""));
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
				"ВстроеннаяПочта",
				КлючНастройки,
				Значение);
				
	ИначеЕсли Настройка = "ОткрыватьПриглашенияВнутриДО" Тогда
		
		Если ТипЗнч(Значение) <> Тип("Булево") Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректное значение настройки встроенной почты: ""%1""'"),
				Настройка);
			
		КонецЕсли;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ВстроеннаяПочта",
			"ОткрыватьПриглашенияВнутриДО",
			Значение);
				
	Иначе
		
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Некорректная настройка встроенной почты: ""%1""'"),
			Настройка);
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает настройку встроенной почты
//
Функция ПолучитьНастройкуПрограммы(Настройка) Экспорт
	
	Если Настройка = "ПериодХраненияHTMLПредставленияСодержанияПисем" Тогда
		
		Значение = Константы.ПериодХраненияHTMLПредставленияСодержанияПисем.Получить();
		Если Значение = 0 Тогда
			Значение = 30; // значение по умолчанию
		КонецЕсли;
		
	Иначе
		
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Некорректная настройка встроенной почты: ""%1""'"),
			Настройка);
		
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

// Устанавливает настройку встроенной почты
//
Процедура УстановитьНастройкуПрограммы(Настройка, Значение) Экспорт
	
	Если Настройка = "ПериодХраненияHTMLПредставленияСодержанияПисем" Тогда
		
		Константы.ПериодХраненияHTMLПредставленияСодержанияПисем.Установить(Значение);
		
	Иначе
		
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Некорректная настройка встроенной почты: ""%1""'"),
			Настройка);
		
	КонецЕсли;
	
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////////////////
// ЗАМЕНА ПРЕДМЕТА И ПРОЕКТА ПЕРЕПИСКИ

// Заменяет предмет переписки письма.
//
// Параметры:
//  Письмо - ДокументСсылка.ВходящееПисьмо, ДокументСсылка.ИсходящееПисьмо - Письмо.
//  НовыйПредмет - СправочникСсылка - Новый предмет, который будет установлен для переписки
//  ЗаменяемыйПредмет - СправочникСсылка - Старый предмет, который будет заменен в переписке.
//  СообщенияОбОшибках - Массив - Сообщения об ошибках при замене предмета.
//
// Возвращаемое значение:
//  Булево - Признак того что замена прошла успешно.
//
Функция ЗаменитьПредметПереписки(ОсновноеПисьмо, НовыйПредмет, ЗаменяемыйПредмет,
	СообщенияОбОшибках = Неопределено, ЗаменятьПредметОсновогоПисьма = Ложь) Экспорт
	
	Если СообщенияОбОшибках = Неопределено Тогда
		СообщенияОбОшибках = Новый Массив;
	КонецЕсли;
	
	ПисьмаПереписки = ВстроеннаяПочтаСервер.ПолучитьПисьмаПереписки(ОсновноеПисьмо);
	ПисьмаДляЗаменыПредмета = Новый Массив;
	Если ЗаменятьПредметОсновогоПисьма Тогда
		ПисьмаДляЗаменыПредмета.Добавить(ОсновноеПисьмо);
	КонецЕсли;
	
	Для каждого Письмо Из ПисьмаПереписки Цикл
		
		Если ОсновноеПисьмо <> Письмо
			И ВстроеннаяПочтаКлиентСервер.ЭтоПисьмо(Письмо) Тогда
			
			Предмет = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Письмо, "Предмет");
			
			Если Предмет = ЗаменяемыйПредмет Или Не ЗначениеЗаполнено(Предмет) Тогда
				ПисьмаДляЗаменыПредмета.Добавить(Письмо);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПисьмаДляЗаменыПредмета.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат ВстроеннаяПочтаСервер.ЗаменитьПредметВПисьмах(
		ПисьмаДляЗаменыПредмета,
		НовыйПредмет,
		СообщенияОбОшибках);
	
КонецФункции

// Заменяет предмет в массиве переданных писем, при неудаче возвращает Ложь.
// При успешной замене возвращает Истина.
//
Функция ЗаменитьПредметВПисьмах(Письма, Предмет, СообщенияОбОшибках) Экспорт
	
	СообщенияОбОшибках = Новый Массив;
	
	Для каждого Письмо Из Письма Цикл
		
		Попытка
			
			ЗаблокироватьДанныеДляРедактирования(Письмо);
			
			ПисьмоОбъект = Письмо.ПолучитьОбъект();
			ПисьмоОбъект.Предмет = Предмет;
			ПисьмоОбъект.Записать();
			РазблокироватьДанныеДляРедактирования(Письмо);
			
		Исключение
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru ='Не удалось заменить предмет в письме: ""%1""
					|%2'"),
				ПолучитьПредставлениеПисьма(ПисьмоОбъект),
				КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			СообщенияОбОшибках.Добавить(ТекстСообщения);
			
			Возврат Ложь;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

// Заменяет проект в массиве переданных писем, при неудаче возвращает Ложь.
// При успешной замене возвращает Истина.
//
Функция ЗаменитьПроектВПисьмах(Письма, Проект, СообщенияОбОшибках) Экспорт
	
	СообщенияОбОшибках = Новый Массив;
	
	Для каждого Письмо Из Письма Цикл
		
		Попытка
			
			ЗаблокироватьДанныеДляРедактирования(Письмо);
			
			ПисьмоОбъект = Письмо.ПолучитьОбъект();
			ПисьмоОбъект.Проект = Проект;
			ПисьмоОбъект.Записать();
			РазблокироватьДанныеДляРедактирования(Письмо);
			
		Исключение
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru ='Не удалось заменить проект в письме: ""%1""
					|%2'"),
				ПолучитьПредставлениеПисьма(ПисьмоОбъект),
				КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			СообщенияОбОшибках.Добавить(ТекстСообщения);
			
			Возврат Ложь;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

/////////////////////////////////////////////////////////////////////////////////////////
// ПОДГОТОВЛЕННЫЕ К ОТПРАВКЕ ПИСЬМА

Процедура ДобавитьПисьмоВПодготовленныеКОтправке(Письмо) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Письмо) Тогда 
		Возврат;
	КонецЕсли;
	
	СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Письмо,
		"ПодготовленоКОтправке, УчетнаяЗапись, Автор, ОтправкаОтменена, ВидМаршрутизации");
	
	МенеджерЗаписи = РегистрыСведений.ПодготовленныеКОтправкеПисьма.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтруктураРеквизитов);
	
	МенеджерЗаписи.Письмо = Письмо;
	
	МенеджерЗаписи.Автор = Сотрудники.ЛюбойПользовательСотрудника(
		СтруктураРеквизитов.Автор);
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры	

Процедура УдалитьПисьмоИзПодготовленныхКОтправке(Письмо) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Письмо) Тогда 
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ПодготовленныеКОтправкеПисьма.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Письмо = Письмо;
	МенеджерЗаписи.Удалить();
	
КонецПроцедуры	

/////////////////////////////////////////////////////////////////////////////////////////
// ВНУТРЕННЯЯ МАРШРУТИЗАЦИЯ

Функция ПолучитьИОтправитьПисьмаПоВнутреннейМаршрутизации(СообщенияОбОшибках) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВремяНачала = ТекущаяДатаСеанса();
	
	ИдентификаторСеанса = Новый УникальныйИдентификатор;
	ПорядковыйНомерСобытия = 0;
	ПараметрыЛогирования = Новый Структура("ИдентификаторСеанса, ПорядковыйНомерСобытия, НомерЗадания",
		ИдентификаторСеанса, ПорядковыйНомерСобытия, 0);
		
	ОтправитьПисьмаПоВнутреннейМаршрутизации(СообщенияОбОшибках, ПараметрыЛогирования);
	
	ПолучитьПисьмаПоВнутреннейМаршрутизации(СообщенияОбОшибках, ПараметрыЛогирования);
	
	ВремяОкончания = ТекущаяДатаСеанса();
	
	ТекстЛога = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Длительность сеанса: %1 сек.'"),
		ВремяОкончания - ВремяНачала);
	
	Возврат СообщенияОбОшибках;

КонецФункции

Процедура ОтправкаПисемПоВнутреннейМаршрутизации() Экспорт 
	
	Отказ = Ложь;
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОтправкаПисемПоВнутреннейМаршрутизации, Отказ);
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	СообщенияОбОшибках = Новый Массив;
	
	ИдентификаторСеанса = Новый УникальныйИдентификатор;
	ПорядковыйНомерСобытия = 0;
	ПараметрыЛогирования = Новый Структура("ИдентификаторСеанса, ПорядковыйНомерСобытия, НомерЗадания",
		ИдентификаторСеанса, ПорядковыйНомерСобытия, 0);
	
	ОтправитьПисьмаПоВнутреннейМаршрутизации(СообщенияОбОшибках, ПараметрыЛогирования);
	
КонецПроцедуры

Процедура ЗаполнитьВходящееПисьмоПриВнутреннейМаршрутизации(ПисьмоОбъект, Сообщение, УчетнаяЗапись) Экспорт
	
	ПисьмоОбъект.УчетнаяЗапись = УчетнаяЗапись;
	ПисьмоОбъект.ВидМаршрутизации = Перечисления.ВидыМаршрутизацииПисем.Внутренняя; 
	
	ЗаполнитьВходящееПисьмоИзСтруктурыПочтовогоСообщения(ПисьмоОбъект, Сообщение, УчетнаяЗапись);
	
	Основание = Неопределено;
	ИдентификаторПисьма = ПолучитьЗначениеПоляИзЗаголовкаПисьма(Сообщение.Заголовок, "In-Reply-To");
	Если ЗначениеЗаполнено(ИдентификаторПисьма) Тогда
		ИдентификаторОснования = РаботаСоСтроками.ВыделитьПодстрокуВСкобках(ИдентификаторПисьма, "<", ">");
		Если ИдентификаторОснования <> "NULL" Тогда 
			Основание = НайтиИсходящееПисьмоПоИдентификатору(ИдентификаторОснования, УчетнаяЗапись); // поиск основания по идентификатору
		КонецЕсли;	
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Основание) Тогда // не было поля "In-Reply-To" или не нашли по идентификатору
		
		СтрокаИдентификаторовПисем = ПолучитьЗначениеПоляИзЗаголовкаПисьма(Сообщение.Заголовок, "References");
		Если ЗначениеЗаполнено(СтрокаИдентификаторовПисем) Тогда
			МассивИдентификаторовПисем = ПолучитьМассивИдентификаторовПисем(СтрокаИдентификаторовПисем);
			Если МассивИдентификаторовПисем.Количество() > 0 Тогда
				МассивПисем = НайтиИсходящиеПисьмаПоИдентификаторам(МассивИдентификаторовПисем, УчетнаяЗапись);
				Если МассивПисем.Количество() > 0 Тогда
					Основание = МассивПисем[0];
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Основание) Тогда
		ПисьмоОбъект.ДополнительныеСвойства.Вставить("Основание", Основание);
	КонецЕсли;       
	
	ПисьмоОбъект.Записать();
	
	РегистрыСведений.ТекстыПисем.ЗаписатьТекстПисьма(ПисьмоОбъект.Ссылка, 
		ПисьмоОбъект.УчетнаяЗапись, ПисьмоОбъект.ТекстПисьмаПростойТекстХранилище);
	
	БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(ПисьмоОбъект.Ссылка, 
		Справочники.ВидыБизнесСобытий.ПолучениеВходящегоПисьма);	
		
	РегистрыСведений.КоличествоПисемВПапкахИтоги.Добавить(ПисьмоОбъект.Ссылка, ПисьмоОбъект.Папка);
	
	Если ЗначениеЗаполнено(Основание) Тогда
		СвязиОбъектов.УстановитьСвязь(
			ПисьмоОбъект.Ссылка,
			Неопределено,
			Основание,
			Справочники.ТипыСвязей.ПолученоВОтветНаПисьмо);
	КонецЕсли;
	
	ОбновитьВеткуПереписки(ПисьмоОбъект.Ссылка);
	
	// Запись вложений
	ДатаИзменения = Сообщение.ДатаПолучения;
	Если Не ЗначениеЗаполнено(ДатаИзменения) Тогда
		ДатаИзменения = ТекущаяДатаСеанса();
	КонецЕсли;	
	
	СобственныеФайлыВозвращаемый = Новый Массив;
	ПорядковыйНомерВложения = 0;
	
	ДобавитьФайлыИзИнтернетПочтовогоСообщения(
		ПисьмоОбъект.Ссылка,
		Сообщение,
		ДатаИзменения, //ВремяИзменения
		"", // ПрефиксНомераПисьма
		СобственныеФайлыВозвращаемый,
		ПорядковыйНомерВложения);
	
	Попытка
			
		ЗаблокироватьДанныеДляРедактирования(ПисьмоОбъект.Ссылка);  
		
		ПисьмоОбъект.ДополнительныеСвойства.Вставить("НеЗаписыватьОперациюСПисьмом", Ложь);
		
		Если ПрименитьПользовательскиеПравила(ПисьмоОбъект) Тогда
			ПисьмоОбъект.Записать();
		КонецЕсли;
		
		Если ПисьмоОбъект.ДополнительныеСвойства.Свойство("Правила_УстановитьПометкуУдаления")
			И ПисьмоОбъект.ДополнительныеСвойства.Правила_УстановитьПометкуУдаления Тогда
			ПисьмоОбъект.УстановитьПометкуУдаления(Истина);
		КонецЕсли;
		
		Если ТипЗнч(ПисьмоОбъект.Ссылка) = Тип("ДокументСсылка.ВходящееПисьмо") Тогда
			ПрименитьПравилаОповещенияДляВходящихПисем(ПисьмоОбъект);	
		КонецЕсли;
		
		РазблокироватьДанныеДляРедактирования(ПисьмоОбъект.Ссылка);
		
	Исключение
		РазблокироватьДанныеДляРедактирования(ПисьмоОбъект.Ссылка);
		Инфо = ИнформацияОбОшибке();
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка при обработке письма правилами: 
				|%1'"),
			ПодробноеПредставлениеОшибки(Инфо));
		Почта.ЗаписатьОшибкуВЖурналРегистрации(СообщениеОбОшибке, ПисьмоОбъект.Ссылка);
	КонецПопытки;
	
КонецПроцедуры

Функция ПолучитьУчетныеЗаписиПоАдресу(Адрес, ТолькоПодходящиеДляВнутреннейМаршрутизации = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	УчетныеЗаписиЭлектроннойПочты.Ссылка
	|ИЗ
	|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
	|ГДЕ
	|	УчетныеЗаписиЭлектроннойПочты.АдресЭлектроннойПочты = &Адрес
	|	И НЕ УчетныеЗаписиЭлектроннойПочты.ПометкаУдаления";
	
	Если ТолькоПодходящиеДляВнутреннейМаршрутизации Тогда 
		ТекстЗапроса = ТекстЗапроса + "
		|	И УчетныеЗаписиЭлектроннойПочты.ВариантИспользования = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияПочты.Встроенная)
		|	И УчетныеЗаписиЭлектроннойПочты.ИспользоватьДляПолучения
		|	И УчетныеЗаписиЭлектроннойПочты.НеИспользоватьВнутреннююМаршрутизациюДляВходящихПисем = ЛОЖЬ";
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Адрес", Адрес);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции	

Функция ПолучитьВидМаршрутизацииПисьма(ПисьмоОбъект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ВстроеннаяПочтаСерверПовтИсп.ИспользоватьВнутреннююМаршрутизацию() Тогда 
		Возврат Перечисления.ВидыМаршрутизацииПисем.Внешняя;
	КонецЕсли;	
	
	ВсеАдресатыВнутренние = Истина;
	
	МассивАдресатов = Новый Массив;
	Для каждого Получатель Из ПисьмоОбъект.ПолучателиПисьма Цикл
		МассивАдресатов.Добавить(Получатель.Адресат);
	КонецЦикла;
	Для каждого Получатель Из ПисьмоОбъект.ПолучателиКопий Цикл
		МассивАдресатов.Добавить(Получатель.Адресат);
	КонецЦикла;
	Для каждого Получатель Из ПисьмоОбъект.ПолучателиСкрытыхКопий Цикл
		МассивАдресатов.Добавить(Получатель.Адресат);
	КонецЦикла;
	
	Для Каждого Адресат Из МассивАдресатов Цикл
		
		УчетныеЗаписи = ПолучитьУчетныеЗаписиПоАдресу(Адресат.Адрес, Истина);
		Если УчетныеЗаписи.Количество() = 0 Тогда  // для адресата нет учетных записей для внутренней маршрутизации
			ВсеАдресатыВнутренние = Ложь;
			Прервать;
		КонецЕсли;	
			
	КонецЦикла;	
	
	Если ВсеАдресатыВнутренние Тогда 
		Возврат Перечисления.ВидыМаршрутизацииПисем.Внутренняя;
	Иначе
		Возврат Перечисления.ВидыМаршрутизацииПисем.Внешняя;
	КонецЕсли;	
	
КонецФункции	

// Доступен ли объект (проект или предмет) ответственным учетной записи
Функция ДоступенПоПравамОтветственнымУчетнойЗаписи(Объект, УчетнаяЗапись) Экспорт
	
	МассивСотрудников = Новый Массив;
	
	Для Каждого Строка Из УчетнаяЗапись.ОтветственныеЗаОбработкуПисем Цикл
		МассивСотрудников.Добавить(Строка.Сотрудник);
	КонецЦикла;	
	
	ТаблицаПрав = ДокументооборотПраваДоступа.ПраваСотрудниковПоОбъекту(Объект, Истина, МассивСотрудников);
	
	ЧислоСотрудников = 0;
	
	Для Каждого Строка Из ТаблицаПрав Цикл
		
		Если Не Строка.Чтение Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ЧислоСотрудников = ЧислоСотрудников + 1;
		
	КонецЦикла;

	Возврат (ЧислоСотрудников = МассивСотрудников.Количество());
	
КонецФункции	

// Добавляет вложение письма.
//
Функция ДобавитьВложениеПисьмаИзИнтернетПочтовогоВложения(
	Письмо, // ДокументСсылка.ВходящееПисьмо, ДокументСсылка.ИсходящееПисьмо
	Вложение, // ИнтернетПочтовоеВложение
	ВремяИзменения) Экспорт // ДатаВремя
	
	Если ТипЗнч(Вложение.Данные) <> Тип("ДвоичныеДанные") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(Вложение.Имя, "_");
	
	ФайлСсылка = ДобавитьВложениеПисьмаИзДвоичныхДанных(
		Письмо,
		Вложение.Данные, // ДвоичныеДанные
		ИмяФайла, // ИмяФайла
		ВремяИзменения,
		Вложение.Идентификатор);
		
	Возврат ФайлСсылка;
	
КонецФункции

// Возвращает список подписей пользователя.
//
Функция ПолучитьСписокПодписейПользователя(Знач Пользователь = Неопределено) Экспорт
	
	Если Пользователь = Неопределено Тогда
		Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	
	МассивГруппПользователей = ПользователиСерверПовтИсп.ПолучитьМассивГруппПользователя(Пользователь);
	МассивГруппПользователей.Добавить(Пользователь);
	Результат = Новый СписокЗначений;
	
	Запрос = Новый Запрос(
	    "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ШаблоныТекстов.Ссылка КАК Ссылка,
		|	ШаблоныТекстов.Наименование КАК Наименование,
		|	ШаблоныТекстов.Шаблон КАК Шаблон
		|ИЗ
		|	Справочник.ШаблоныТекстов КАК ШаблоныТекстов
		|ГДЕ
		|	(ШаблоныТекстов.Автор = &Пользователь
		|			ИЛИ ШаблоныТекстов.ОбщийШаблон
		|				И ШаблоныТекстов.Пользователи.ПользовательИлиГруппа.Ссылка В (&МассивГруппПользователей))");

	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("МассивГруппПользователей", МассивГруппПользователей);

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ТекстШаблона = Справочники.ШаблоныТекстов.ПолучитьТекстШаблона(Выборка.Ссылка);
		Результат.Добавить(ТекстШаблона, Выборка.Наименование);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Распараллеливание приемки email

// Перезаполняет регистр ПотокиПроверкиПочты
Процедура ПерезаполнитьРегистрОбрабатываемыеРегламентнымиЗаданиямиУчетныеЗаписи(ЧислоЗаданий) Экспорт
	
	Если ЧислоЗаданий = 0 Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	// очистим весь регистр
	НаборЗаписей = РегистрыСведений.ПотокиПроверкиПочты.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
	// Получаем все учетки УчетныеЗаписиЭлектроннойПочты - кроме помеченных на удаление - и только те, где заполнены поля 1С:Почты
	//  - считаем их количество.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетныеЗаписиЭлектроннойПочты.Ссылка,
		|	УчетныеЗаписиЭлектроннойПочты.Наименование КАК Наименование
		|ИЗ
		|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
		|ГДЕ
		|	УчетныеЗаписиЭлектроннойПочты.ПометкаУдаления = ЛОЖЬ
		|	И (УчетныеЗаписиЭлектроннойПочты.ИспользоватьДляОтправки = ИСТИНА
		|			ИЛИ УчетныеЗаписиЭлектроннойПочты.ИспользоватьДляПолучения = ИСТИНА)
		|	И УчетныеЗаписиЭлектроннойПочты.ВариантИспользования = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияПочты.Встроенная)

		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";

	ТаблицаУчетныхЗаписей = Запрос.Выполнить().Выгрузить();	
	Если ТаблицаУчетныхЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// равномерно делим все учетки УчетныеЗаписиЭлектроннойПочты
	Для Индекс = 0 По ТаблицаУчетныхЗаписей.Количество()-1 Цикл
		
		УчетнаяЗапись = ТаблицаУчетныхЗаписей[Индекс].Ссылка;
		
		НомерРобота = Индекс % ЧислоЗаданий;
		
		НомерЗадания = НомерРобота + 1;
		
		МенеджерЗаписи = РегистрыСведений.ПотокиПроверкиПочты.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
		МенеджерЗаписи.Прочитать();
		
		МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
		МенеджерЗаписи.НомерЗадания = НомерЗадания;
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	
	
КонецПроцедуры

// добавим в наименее загруженного робота
Процедура ДобавитьУчетнуюЗаписьВРегистрОбрабатываемыеРегламентнымиЗаданиямиУчетныеЗаписи(УчетнаяЗапись) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПотокиПроверкиПочты.НомерЗадания,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПотокиПроверкиПочты.УчетнаяЗапись) КАК КоличествоУчетныхЗаписей
		|ИЗ
		|	РегистрСведений.ПотокиПроверкиПочты КАК ПотокиПроверкиПочты
		|
		|СГРУППИРОВАТЬ ПО
		|	ПотокиПроверкиПочты.НомерЗадания
		|
		|УПОРЯДОЧИТЬ ПО
		|	КоличествоУчетныхЗаписей";
		
	НомерЗадания = 1;
	
	Таблица = Запрос.Выполнить().Выгрузить();	
	Если Таблица.Количество() = 1 Тогда
		НомерЗадания = Таблица[0].НомерЗадания;
	КонецЕсли;	
		
	МенеджерЗаписи = РегистрыСведений.ПотокиПроверкиПочты.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
	МенеджерЗаписи.НомерЗадания = НомерЗадания;
	МенеджерЗаписи.Записать();
		
КонецПроцедуры

// удалим запись у робота
Процедура УдалитьУчетнуюЗаписьИзРегистраОбрабатываемыеРегламентнымиЗаданиямиУчетныеЗаписи(УчетнаяЗапись) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.ПотокиПроверкиПочты.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.УчетнаяЗапись.Установить(УчетнаяЗапись);
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Принять и отправить почту для указанного задания
Процедура ВыполнитьПриемкуОтправкуПочты(НомерЗадания, УчетнаяЗапись = Неопределено, КонтекстДоставки) Экспорт      
	
	МассивУчетныхЗаписейПолучения = Новый Массив;
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		МассивУчетныхЗаписейПолучения.Добавить(УчетнаяЗапись);
		
	Иначе	
	
		// Получаем учетки именно ДЛЯ ПОЛУЧЕНИЯ - для отправки другой запрос
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПотокиПроверкиПочты.УчетнаяЗапись КАК УчетнаяЗапись
			|ИЗ
			|	РегистрСведений.ПотокиПроверкиПочты КАК ПотокиПроверкиПочты
			|ГДЕ
			|	ПотокиПроверкиПочты.НомерЗадания = &НомерЗадания
			|	И ПотокиПроверкиПочты.УчетнаяЗапись.ВариантИспользования = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияПочты.Встроенная)
			|	И ПотокиПроверкиПочты.УчетнаяЗапись.ИспользоватьДляПолучения = ИСТИНА
			|	И ПотокиПроверкиПочты.УчетнаяЗапись.ПометкаУдаления = ЛОЖЬ
			|
			|УПОРЯДОЧИТЬ ПО
			|	УчетнаяЗапись";
		Запрос.УстановитьПараметр("НомерЗадания", НомерЗадания);	
			
		МассивУчетныхЗаписейПолучения = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("УчетнаяЗапись");	
		
	КонецЕсли;	
	
	Если МассивУчетныхЗаписейПолучения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	СообщенияОбОшибках = Новый Массив;
	
	ВремяНачала = ТекущаяДатаСеанса();
	
	ИдентификаторСеанса = Новый УникальныйИдентификатор;
	ПорядковыйНомерСобытия = 0;
	ПараметрыЛогирования = Новый Структура("ИдентификаторСеанса, ПорядковыйНомерСобытия, НомерЗадания",
		ИдентификаторСеанса, ПорядковыйНомерСобытия, НомерЗадания);
		
	ПолучитьВсеПисьма(СообщенияОбОшибках, ПараметрыЛогирования, МассивУчетныхЗаписейПолучения, КонтекстДоставки);
	
	ВремяОкончания = ТекущаяДатаСеанса();
	ТекстЛога = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Длительность сеанса: %1 сек.'"),
		ВремяОкончания - ВремяНачала);
	
	ЗаписатьПротоколДоставкиПочты(
		Неопределено,
		ТекстЛога,
		Ложь,
		Перечисления.ТипыСобытийДоставкиПочты.ЗавершениеСеансаПриемкиОтправкиПисем,
		Неопределено,
		ПараметрыЛогирования.ПорядковыйНомерСобытия,
		ПараметрыЛогирования.ИдентификаторСеанса,
		ПараметрыЛогирования.НомерЗадания);
	
КонецПроцедуры

// Выполняет отправку внешних писем - по email
Процедура ОтправкаПисем() Экспорт
	
	Отказ = Ложь;
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОтправкаПисем, Отказ);
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	СообщенияОбОшибках = Новый Массив;
	
	ВремяНачала = ТекущаяДатаСеанса();
	
	ИдентификаторСеанса = Новый УникальныйИдентификатор;
	ПорядковыйНомерСобытия = 0;
	ПараметрыЛогирования = Новый Структура("ИдентификаторСеанса, ПорядковыйНомерСобытия, НомерЗадания",
		ИдентификаторСеанса, ПорядковыйНомерСобытия, 0);
		
	// только отправка
	ОтправитьВсеПисьма(ТекущаяДатаСеанса(), СообщенияОбОшибках, ПараметрыЛогирования);
	
	ВремяОкончания = ТекущаяДатаСеанса();
	ТекстЛога = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Длительность сеанса: %1 сек.'"),
		ВремяОкончания - ВремяНачала);
	
	ЗаписатьПротоколДоставкиПочты(
		Неопределено,
		ТекстЛога,
		Ложь,
		Перечисления.ТипыСобытийДоставкиПочты.ЗавершениеСеансаОтправкиПисем,
		Неопределено,
		ПараметрыЛогирования.ПорядковыйНомерСобытия,
		ПараметрыЛогирования.ИдентификаторСеанса);
	
КонецПроцедуры

// Подписка на событие ПередЗаписью учетной записи
Процедура УчетнаяЗаписьПередЗаписьюПередЗаписью(Источник, Отказ) Экспорт
	
	Если МиграцияПриложенийПереопределяемый.ЭтоЗагрузка(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыСтарые = "ПометкаУдаления, ИспользоватьДляПолучения, ИспользоватьДляОтправки";

	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Ссылка, 
		РеквизитыСтарые);
	
	// учетная запись помечена на удаление
	Если ЗначенияРеквизитов.ПометкаУдаления = Ложь И Источник.ПометкаУдаления = Истина
		И Источник.ВариантИспользования = Перечисления.ВариантыИспользованияПочты.Встроенная Тогда
		
		УдалитьУчетнуюЗаписьИзРегистраОбрабатываемыеРегламентнымиЗаданиямиУчетныеЗаписи(Источник.Ссылка);
		УдалитьЗаписьИзРегистраОбработанныеУчетныеЗаписи(Источник.Ссылка);
		
	КонецЕсли;	
	
	// с учетной записи снята пометка удаления
	Если ЗначенияРеквизитов.ПометкаУдаления = Истина И Источник.ПометкаУдаления = Ложь
		И Источник.ВариантИспользования = Перечисления.ВариантыИспользованияПочты.Встроенная Тогда
		
		ДобавитьУчетнуюЗаписьВРегистрОбрабатываемыеРегламентнымиЗаданиямиУчетныеЗаписи(Источник.Ссылка);
		УдалитьЗаписьИзРегистраОбработанныеУчетныеЗаписи(Источник.Ссылка);
		
	КонецЕсли;	
	
	// У учетной записи не были заполнены поля ИспользоватьДляОтправки ИспользоватьДляПолучения - теперь заполнены.
	Если (ЗначенияРеквизитов.ИспользоватьДляОтправки = Ложь
		И ЗначенияРеквизитов.ИспользоватьДляПолучения = Ложь)
		И (Источник.ИспользоватьДляОтправки = Истина Или Источник.ИспользоватьДляПолучения = Истина)
		И Источник.ВариантИспользования = Перечисления.ВариантыИспользованияПочты.Встроенная Тогда
		
		УдалитьЗаписьИзРегистраОбработанныеУчетныеЗаписи(Источник.Ссылка);
		
		Если ЗначениеЗаполнено(Источник.Ссылка) Тогда
			ДобавитьУчетнуюЗаписьВРегистрОбрабатываемыеРегламентнымиЗаданиямиУчетныеЗаписи(Источник.Ссылка);
		Иначе
			Источник.ДополнительныеСвойства.Вставить("ДобавитьУчетнуюЗаписьВРегистрОбрабатываемыеРегламентнымиЗаданиямиУчетныеЗаписи", 
				Истина);
		КонецЕсли;	
		
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(Источник.Ссылка)
		И Источник.ВариантИспользования = Перечисления.ВариантыИспользованияПочты.Встроенная Тогда
		
		Если (Источник.ИспользоватьДляОтправки = Истина Или Источник.ИспользоватьДляПолучения = Истина) Тогда
			Источник.ДополнительныеСвойства.Вставить("ДобавитьУчетнуюЗаписьВРегистрОбрабатываемыеРегламентнымиЗаданиямиУчетныеЗаписи", 
				Истина);
		КонецЕсли;	
		
	КонецЕсли;		
	
	// У учетной записи БЫЛИ заполнены поля ИспользоватьДляОтправки ИспользоватьДляПолучения - теперь НЕ заполнены.
	Если (Источник.ИспользоватьДляОтправки = Ложь И Источник.ИспользоватьДляПолучения = Ложь)
		И (ЗначенияРеквизитов.ИспользоватьДляОтправки = Истина Или ЗначенияРеквизитов.ИспользоватьДляПолучения = Истина)
		И Источник.ВариантИспользования = Перечисления.ВариантыИспользованияПочты.Встроенная Тогда
		
		УдалитьУчетнуюЗаписьИзРегистраОбрабатываемыеРегламентнымиЗаданиямиУчетныеЗаписи(Источник.Ссылка);
		УдалитьЗаписьИзРегистраОбработанныеУчетныеЗаписи(Источник.Ссылка);
		
	КонецЕсли;	          
	
	ЭтоПодходящаяУчетка = Истина;

	// У учетки сейчас заполнены поля ИспользоватьДляОтправки ИспользоватьДляПолучения и ПометкаУдаления = Ложь
	//  - добавим ее в регистр ОбрабатываемыеРоботамиУчетныеЗаписи
	//  - неважно, какое раньше было состояние.
	Если (Источник.ИспользоватьДляОтправки = Истина Или Источник.ИспользоватьДляПолучения = Истина)
		И Источник.ВариантИспользования = Перечисления.ВариантыИспользованияПочты.Встроенная
		 И Источник.ПометкаУдаления = Ложь 
		 И ЭтоПодходящаяУчетка
		 Тогда
		
		Если ЗначениеЗаполнено(Источник.Ссылка) Тогда
			ДобавитьУчетнуюЗаписьВРегистрОбрабатываемыеРегламентнымиЗаданиямиУчетныеЗаписи(Источник.Ссылка);
		Иначе
			Источник.ДополнительныеСвойства.Вставить("ДобавитьУчетнуюЗаписьВРегистрОбрабатываемыеРегламентнымиЗаданиямиУчетныеЗаписи", 
				Истина);
		КонецЕсли;	
		
	КонецЕсли;
	

КонецПроцедуры

// Подписка на событие ПриЗаписи учетной записи
Процедура УчетнаяЗаписьПриЗаписиПриЗаписи(Источник, Отказ) Экспорт
	
	Если МиграцияПриложенийПереопределяемый.ЭтоЗагрузка(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ДобавитьУчетнуюЗаписьВРегистрОбрабатываемыеРегламентнымиЗаданиямиУчетныеЗаписи") Тогда
		ДобавитьУчетнуюЗаписьВРегистрОбрабатываемыеРегламентнымиЗаданиямиУчетныеЗаписи(Источник.Ссылка);
	КонецЕсли;	
	
КонецПроцедуры

// Модифицирует параметр ТекстПисьма: добавляет в него текст исходного письма
//
Процедура ДобавитьТекстИсходногоПисьма(
	ТекстПисьма,
	ИсходноеПисьмо,
	ТипТекста,
	ТипОтвета) Экспорт
	
	Если ТипЗнч(ИсходноеПисьмо) = Тип("СправочникСсылка.ШаблоныПисем") Тогда
		ЭтоШаблон = Истина;
	Иначе 
		ЭтоШаблон = Ложь;
	КонецЕсли;
	
	Если ТипОтвета = Перечисления.ТипыОтвета.ОтветНаПисьмо Тогда
		
		ПомечатьКаждуюСтрокуИсходногоПисьма = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПомечатьКаждуюСтрокуИсходногоПисьмаПриОтвете();
		СокращатьИнформациюОПисьмеПриОтвете = ВстроеннаяПочтаСерверПовтИсп.ПолучитьСокращатьИнформациюОПисьмеПриОтвете();
		
	ИначеЕсли ТипОтвета = Перечисления.ТипыОтвета.ПересылкаПисьма Тогда
		
		ПомечатьКаждуюСтрокуИсходногоПисьма = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПомечатьКаждуюСтрокуИсходногоПисьмаПриПересылке();
		СокращатьИнформациюОПисьмеПриОтвете = ВстроеннаяПочтаСерверПовтИсп.ПолучитьСокращатьИнформациюОПисьмеПриПересылке();
		
	ИначеЕсли ЭтоШаблон Тогда 
		
		ПомечатьКаждуюСтрокуИсходногоПисьма = Ложь;
		СокращатьИнформациюОПисьмеПриОтвете = ВстроеннаяПочтаСерверПовтИсп.ПолучитьСокращатьИнформациюОПисьмеПриОтвете();
		
	Иначе
		
		ПомечатьКаждуюСтрокуИсходногоПисьма = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПомечатьКаждуюСтрокуИсходногоПисьмаПриОтвете();
		СокращатьИнформациюОПисьмеПриОтвете = ВстроеннаяПочтаСерверПовтИсп.ПолучитьСокращатьИнформациюОПисьмеПриОтвете();
		
	КонецЕсли;
	
	СимволЦитирования = ВстроеннаяПочтаСерверПовтИсп.ПолучитьСимволЦитированияВПереписке();
	
	ИсходноеПисьмоОбъект = ИсходноеПисьмо.ПолучитьОбъект();
	
	Если ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		
		ТекстHTML = "";
		Если ЭтоШаблон Тогда 
			ТекстHTML = Справочники.ШаблоныПисем.ПолучитьПредставлениеСодержанияШаблона(ИсходноеПисьмоОбъект);
		Иначе 
		
			HTMLПредставлениеШапкиИсходногоПисьма = СформироватьШапкуПисьма(
				ИсходноеПисьмоОбъект,
				ТипТекста,
				СокращатьИнформациюОПисьмеПриОтвете);
			
			ТекстHTML = "";
			Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(ИсходноеПисьмо) Тогда 
				ТекстHTML = Документы.ВходящееПисьмо.ПолучитьHTMLПредставлениеСодержанияПисьма(ИсходноеПисьмоОбъект);
			Иначе	
				ТекстHTML = Документы.ИсходящееПисьмо.ПолучитьHTMLПредставлениеСодержанияПисьма(ИсходноеПисьмоОбъект);
			КонецЕсли;	
			
		КонецЕсли;
		
		ИсходныйЗаголовок = "";
		ИсходныйОкончание = "";

		РазложенныйТекстHTML = РаботаС_HTML.РазложитьТекстHTML(ТекстHTML);
		ИсходныйЗаголовок = РазложенныйТекстHTML.Заголовок;
		ИсходныйОкончание = РазложенныйТекстHTML.Окончание;
		HTMLПредставлениеИсходногоПисьма = РазложенныйТекстHTML.Тело;
		
		Если ПомечатьКаждуюСтрокуИсходногоПисьма Тогда
			
			ВидЦитированияПриОтвете = ВстроеннаяПочтаСерверПовтИсп.ПолучитьВидЦитированияПриОтвете();
			
			ТекстСтиляBlockquote = РаботаС_HTML.ПолучитьСтильЦитированияПриОтвете(ВидЦитированияПриОтвете);
			
			Если Не СокращатьИнформациюОПисьмеПриОтвете Тогда
				
				ТекстИсходногоПисьма =
					"<div>&nbsp;</div>
					|<blockquote " + ТекстСтиляBlockquote + ">
					|" + HTMLПредставлениеШапкиИсходногоПисьма + "
					|<div>&nbsp;</div>
					|" + HTMLПредставлениеИсходногоПисьма + "
					|</blockquote>";
				
			Иначе
				
				ТекстИсходногоПисьма =
					"<div>&nbsp;</div>
					|" + HTMLПредставлениеШапкиИсходногоПисьма + "
					|<blockquote " + ТекстСтиляBlockquote + ">
					|" + HTMLПредставлениеИсходногоПисьма + "
					|</blockquote>";
				
			КонецЕсли;
			
		Иначе
			
			ТекстСтиляBlockquote = "style = 'border:none;padding:0cm;margin:0cm'";
			
			ТекстИсходногоПисьма =
				"<div>&nbsp;</div>
				|<hr>
				|<blockquote " + ТекстСтиляBlockquote + ">
				|" + HTMLПредставлениеШапкиИсходногоПисьма + "
				|<div>&nbsp;</div>
				|" + HTMLПредставлениеИсходногоПисьма + "
				|</blockquote>";
			
		КонецЕсли;
		
		ТекстИсходногоПисьма = 
			  ИсходныйЗаголовок
			+ ТекстИсходногоПисьма
			+ ИсходныйОкончание;
		
		РаботаС_HTML.ДобавитьТекстHTML(ТекстПисьма, ТекстИсходногоПисьма);
		РаботаС_HTML.ПрименитьНастройкиОтображениеПисьма(ТекстПисьма);
		
	ИначеЕсли ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст Тогда
		
		
		Если ЭтоШаблон Тогда 
			ПредставлениеШапкаИсходногоПисьма = "";
			ПредставлениеИсходногоПисьма = ИсходноеПисьмоОбъект.ПолучитьТекстовоеПредставлениеСодержанияШаблона();
		Иначе 
		
			ПредставлениеШапкаИсходногоПисьма = СформироватьШапкуПисьма(
				ИсходноеПисьмоОбъект,
				ТипТекста,
				СокращатьИнформациюОПисьмеПриОтвете);
			
			ПредставлениеИсходногоПисьма = ИсходноеПисьмоОбъект.ПолучитьТекстовоеПредставлениеСодержанияПисьма();
			
		КонецЕсли;
		
		Если ПомечатьКаждуюСтрокуИсходногоПисьма Тогда
			
			Если Не СокращатьИнформациюОПисьмеПриОтвете Тогда
				
				ТекстИсходногоПисьма = ПредставлениеШапкаИсходногоПисьма
					+ Символы.ПС
					+ Символы.ПС
					+ ПредставлениеИсходногоПисьма;
				
				Если ЗначениеЗаполнено(СимволЦитирования) Тогда
					Почта.ДобавитьКвотирование(ТекстИсходногоПисьма, СимволЦитирования);
				КонецЕсли;
				
			Иначе
				
				Если ЗначениеЗаполнено(СимволЦитирования) Тогда
					Почта.ДобавитьКвотирование(ПредставлениеИсходногоПисьма, СимволЦитирования);
				КонецЕсли;
				
				ТекстИсходногоПисьма = ПредставлениеШапкаИсходногоПисьма
					+ Символы.ПС
					+ ПредставлениеИсходногоПисьма;
				
			КонецЕсли;
			
		ИначеЕсли ЭтоШаблон Тогда 
			
			ТекстИсходногоПисьма = ПредставлениеШапкаИсходногоПисьма
				+ Символы.ПС
				+ ПредставлениеИсходногоПисьма;
			
		Иначе
			
			ТекстИсходногоПисьма = НСтр("ru = '---------- Исходное сообщение ----------'") 
				+ Символы.ПС 
				+ ПредставлениеШапкаИсходногоПисьма
				+ Символы.ПС
				+ Символы.ПС
				+ ПредставлениеИсходногоПисьма;
			
		КонецЕсли;
		
		// Добавление текста исходного письма с отбивкой пустой строкой от
		// основного текста письма
		Если ПустаяСтрока(ТекстПисьма) Тогда
			
			ТекстПисьма = Символы.ПС + Символы.ПС + ТекстИсходногоПисьма;
			
		Иначе
			
			Если Прав(ТекстПисьма, 1) = Символы.ПС Тогда
				ТекстПисьма = ТекстПисьма + Символы.ПС + ТекстИсходногоПисьма;
			Иначе
				ТекстПисьма = ТекстПисьма + Символы.ПС + Символы.ПС + ТекстИсходногоПисьма;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоШаблон Тогда
		ТекстШаблона = ТекстПисьма;
		// Дата
		ТекущаяДата = ТекущаяДатаСеанса();
		ТекстШаблона = СтрЗаменить(ТекстШаблона, "[День]", Формат(ТекущаяДата, "ДФ=дд"));
		ТекстШаблона = СтрЗаменить(ТекстШаблона, "[Месяц]", Формат(ТекущаяДата, "ДФ=ММ"));
		ТекстШаблона = СтрЗаменить(ТекстШаблона, "[Год]", Формат(ТекущаяДата, "ДФ=гггг"));
		
		// Время
		ТекстШаблона = СтрЗаменить(ТекстШаблона, "[Час]", Формат(ТекущаяДата, "ДФ=ЧЧ"));
		ТекстШаблона = СтрЗаменить(ТекстШаблона, "[Минута]", Формат(ТекущаяДата, "ДФ=мм"));
		
		ТекстПисьма = ТекстШаблона;
	КонецЕсли;
	
КонецПроцедуры

// обработчик регламентного задания ПроверкаРаботыПриемкиПочты
Процедура ПроверкаРаботыПриемкиПочты() Экспорт
	
	Отказ = Ложь;
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ПроверкаРаботыПриемкиПочты, Отказ);
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОбработанныеУчетныеЗаписи.УчетнаяЗапись КАК УчетнаяЗапись
		|ИЗ
		|	РегистрСведений.ОбработанныеУчетныеЗаписи КАК ОбработанныеУчетныеЗаписи
		|ГДЕ
		|	ОбработанныеУчетныеЗаписи.ДатаПоследнейОбработки < &ДатаОтсечения
		|	И ОбработанныеУчетныеЗаписи.УчетнаяЗапись.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	УчетнаяЗапись";
	
	ДатаОтсечения = ТекущаяДатаСеанса() - 30 * 60; // если 30 минут ящик не обрабатывался - считаем ошибкой
	Запрос.УстановитьПараметр("ДатаОтсечения", ДатаОтсечения);
	
	МассивУчетныхЗаписей = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("УчетнаяЗапись");
	
	Если МассивУчетныхЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивСотрудников = РаботаСУведомлениями.СписокПолучателейУведомленийОПроблемах(
			Перечисления.РазделыУведомленийОПроблемах.Почта);
	Если МассивСотрудников.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиОписания = Новый Массив;
	
	СтрокиОписания.Добавить(
		СтрШаблон(
			НСтр("ru ='Учетные записи электронной почты перестали обрабатываться (%1 шт)'"),
			МассивУчетныхЗаписей.Количество()));
	
	СтрокиОписания.Добавить(Символы.ПС);
	СтрокиОписания.Добавить(Символы.ПС);
	
	СтрокиОписания.Добавить(
		НСтр("ru='Перечисленные ниже учетные записи не обрабатывались более 30 минут.
			|Возможно не работает автоматическая приемка почты.'", Метаданные.ОсновнойЯзык.КодЯзыка));
	
	СтрокиОписания.Добавить(Символы.ПС);
	СтрокиОписания.Добавить(Символы.ПС);
	
	Для Каждого УчетнаяЗапись Из МассивУчетныхЗаписей Цикл
		СтрокиОписания.Добавить(Символы.ПС);
		СтрокиОписания.Добавить(
			РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(УчетнаяЗапись));
	КонецЦикла;
	
	Описание = СтрСоединить(СтрокиОписания);
	
	Для Каждого Сотрудник Из МассивСотрудников Цикл
		РаботаСУведомлениями.ОбработатьУведомлениеПрограммы(
			Описание,
			Сотрудник);
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Приемка электронной почты.Учетные записи электронной почты перестали обрабатываться'", 
			Метаданные.ОсновнойЯзык.КодЯзыка),
		УровеньЖурналаРегистрации.Ошибка,,,
		Описание);
	
КонецПроцедуры

// Удаляет запись из регистра ОбработанныеУчетныеЗаписи
Процедура УдалитьЗаписьИзРегистраОбработанныеУчетныеЗаписи(УчетнаяЗапись) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		НаборЗаписей = РегистрыСведений.ОбработанныеУчетныеЗаписи.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.УчетнаяЗапись.Установить(УчетнаяЗапись);
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры	

// Возвращает вид цитирования для HTML
Функция ПолучитьВидЦитированияПриОтвете() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ВидЦитирования = Константы.ВидЦитированияПриОтветеВФорматеHTML.Получить();
	
	Если Не ЗначениеЗаполнено(ВидЦитирования) Тогда
		ВидЦитирования = Перечисления.ВидыЦитированияПриОтвете.ВертикальнаяПолоса;
	КонецЕсли;	
	
	Возврат ВидЦитирования;
	
КонецФункции	

/////////////////////////////////////////////////////////////////////////////////////////
// ОЧИСТКА УСТАРЕВШИХ ЗАПИСЕЙ HTML ПРЕДСТАВЛЕНИЯ СОДЕРЖАНИЯ ПИСЕМ

Функция ПолучитьСпискиАдресовДляАвтоподбора(Текст, УчетнаяЗапись) Экспорт
	
	ВозвращаемыйСписок = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СпискиАдресовЭлектроннойПочты.Ссылка КАК Ссылка,
		|	СпискиАдресовЭлектроннойПочты.Наименование КАК Наименование
		|ИЗ
		|	Справочник.СпискиАдресовЭлектроннойПочты КАК СпискиАдресовЭлектроннойПочты
		|ГДЕ
		|	СпискиАдресовЭлектроннойПочты.Наименование ПОДОБНО &Наименование
		|	И СпискиАдресовЭлектроннойПочты.УчетнаяЗапись = &УчетнаяЗапись
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
	
	Запрос.УстановитьПараметр("Наименование", Текст + "%");
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);

	Результат = Запрос.Выполнить();
	Если Результат.Пустой() И ВозвращаемыйСписок.Количество() = 0 Тогда
		Возврат ВозвращаемыйСписок;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		ВозвращаемыйСписок.Добавить(Выборка.Ссылка, Выборка.Наименование);
	КонецЦикла;
	
	Возврат ВозвращаемыйСписок;
	
КонецФункции	

// получить значение константы СворачиватьПрефиксОтветаИПересылкиВТемеПисьма
Функция ПолучитьСворачиватьПрефиксОтветаИПересылкиВТемеПисьма() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Возврат Константы.СворачиватьПрефиксОтветаИПересылкиВТемеПисьма.Получить();
КонецФункции	

/////////////////////////////////////////////////////////////////////////////////////////
// ССЫЛКИ MAILTO

// Возвращает структуру, содержащую информацию из ссылки mailto
Функция РазобратьСсылкуMailto(СсылкаMailto) Экспорт
	
	СтруктураMailto = Новый Структура;
	СтрокаПоискаВСсылке = НРег(СсылкаMailto);
	
	Если СтрНайти(СтрокаПоискаВСсылке, "mailto:") = 1 Тогда
		
		СсылкаMailto = Сред(СсылкаMailto, СтрДлина("mailto:") + 1);
		АнализПараметров = Ложь;
		
		Пока Не ПустаяСтрока(СсылкаMailto) Цикл
			
			ВыделенныйПараметр = ВыделитьПараметр(СсылкаMailto, АнализПараметров);
			ВнестиПараметрВСтруктуру(СтруктураMailto, ВыделенныйПараметр.НаименованиеПараметра, ВыделенныйПараметр.ЗначениеПараметра);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СтруктураMailto;
	
КонецФункции

/////////////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ФУНКЦИИ

// Получает все папки писем пользователя ("Мои папки")
Функция ПолучитьВсеПапкиПисемПользователя(Пользователь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ЗапросВсеПапки = Новый Запрос;
	ЗапросВсеПапки.Текст = 
		"ВЫБРАТЬ
		|	ПапкиПисем.Ссылка
		|ИЗ
		|	Справочник.ПапкиПисем КАК ПапкиПисем
		|ГДЕ
		|	ПапкиПисем.Ссылка В ИЕРАРХИИ
		|			(ВЫБРАТЬ
		|				ПапкиПисемБыстрогоДоступа.Папка
		|			ИЗ
		|				РегистрСведений.ПапкиПисемБыстрогоДоступа КАК ПапкиПисемБыстрогоДоступа
		|			ГДЕ
		|				ПапкиПисемБыстрогоДоступа.Пользователь = &Пользователь)
		|	И ПапкиПисем.ПометкаУдаления = ЛОЖЬ";
	ЗапросВсеПапки.УстановитьПараметр("Пользователь", Пользователь);
	ПапкиПользователя = ЗапросВсеПапки.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	Возврат ПапкиПользователя;
	
КонецФункции

// Удаляет теги из тела письма
Процедура УдалитьТеги(ТекстПисьмаHTML, ИмяТега) Экспорт
	
	НРегТекстHTML = НРег(ТекстПисьмаHTML);
	
	Пока СтрНайти(НРегТекстHTML, "<" + ИмяТега) <> 0 Цикл
		
		ПозицияНачалаТегаHTML = СтрНайти(НРегТекстHTML, "<" + ИмяТега);
		ПозицияОкончанияТегаHTML = 0;
		Если ПозицияНачалаТегаHTML > 0 Тогда
			ПозицияОкончанияТегаHTML = РаботаСоСтроками.НайтиПосле(НРегТекстHTML, ">", ПозицияНачалаТегаHTML);
		КонецЕсли;
		
		Если ПозицияНачалаТегаHTML <> 0 И ПозицияОкончанияТегаHTML <> 0 Тогда
			
			ПозицияНачалаЗакрывающегоТегаHTML = РаботаСоСтроками.НайтиПосле(НРегТекстHTML, "</" + ИмяТега, ПозицияОкончанияТегаHTML);
			ПозицияОкончанияЗакрывающегоТегаHTML = 0;
			Если ПозицияНачалаЗакрывающегоТегаHTML > 0 Тогда
				ПозицияОкончанияЗакрывающегоТегаHTML = РаботаСоСтроками.НайтиПосле(НРегТекстHTML, ">", ПозицияНачалаЗакрывающегоТегаHTML);
			КонецЕсли;
			
			Если ПозицияНачалаЗакрывающегоТегаHTML <> 0 И ПозицияОкончанияЗакрывающегоТегаHTML <> 0 Тогда
				
				ТекстПисьмаHTML = Лев(ТекстПисьмаHTML, ПозицияНачалаТегаHTML - 1) 
					+ Сред(ТекстПисьмаHTML, ПозицияОкончанияЗакрывающегоТегаHTML + 1);
				НРегТекстHTML = НРег(ТекстПисьмаHTML);
				
			Иначе
				Возврат;	
			КонецЕсли;		
				
		Иначе
			
			Возврат;	
			
		КонецЕсли;		
		
	КонецЦикла;	
	
КонецПроцедуры	

// Удаляет onmouseover onmouseout из тега A
//
// Параметры:
//  ТекстПисьмаHTML - Строка с html текстом письма
//  ИмяСвойства - Строка, например "onmouseover"
//
Процедура УдалитьСвойствоТегаА(ТекстПисьмаHTML, ИмяСвойства) Экспорт
	
	УдалитьСвойствоТегаАСУказаниемКавычки(ТекстПисьмаHTML, ИмяСвойства, """");
	УдалитьСвойствоТегаАСУказаниемКавычки(ТекстПисьмаHTML, ИмяСвойства, "'");
	
КонецПроцедуры	
	
// Удаляет onmouseover onmouseout из тега A
//
// Параметры:
//  ТекстПисьмаHTML - Строка с html текстом письма
//  ИмяСвойства - Строка, например "onmouseover"
//  Кавычка - Строка   " или '
//
Процедура УдалитьСвойствоТегаАСУказаниемКавычки(ТекстПисьмаHTML, ИмяСвойства, Кавычка) Экспорт
	
	НРегТекстHTML = НРег(ТекстПисьмаHTML);
	
	ИмяСвойстваСКавычкой = ИмяСвойства + "=" + Кавычка;
	ЧислоВхождений = 0;
	
	Пока Найти(НРегТекстHTML, ИмяСвойстваСКавычкой) <> 0 Цикл
		
		ПозицияНачалаСвойства = Найти(НРегТекстHTML, ИмяСвойстваСКавычкой);
		ЧислоВхождений = ЧислоВхождений + 1;
		Если ЧислоВхождений > 1000 Тогда
			Возврат; // чтобы не было зацикливания
		КонецЕсли;	
		
		Если ПозицияНачалаСвойства <> 0 Тогда
			
			СвойствоВнутриТегаА = Ложь;
		
			Позиция = 0;
			Для Позиция = 0 По 100 Цикл
				
				Если ПозицияНачалаСвойства - Позиция > 0 Тогда
					
					Фрагмент = Сред(НРегТекстHTML, ПозицияНачалаСвойства - Позиция, 2);
					Если Фрагмент = "<a" Тогда
						СвойствоВнутриТегаА = Истина;
						Прервать;
					КонецЕсли;		
					
				КонецЕсли;	
				
			КонецЦикла;	
			
			Если СвойствоВнутриТегаА Тогда
				
				ПозицияОкончанияСвойства = РаботаСоСтроками.НайтиПосле(
					НРегТекстHTML, Кавычка, ПозицияНачалаСвойства + СтрДлина(ИмяСвойстваСКавычкой));
					
				Если ПозицияОкончанияСвойства <> 0 Тогда
						
					ТекстПисьмаHTML = Лев(ТекстПисьмаHTML, ПозицияНачалаСвойства - 1) 
						+ Сред(ТекстПисьмаHTML, ПозицияОкончанияСвойства + 1);
					НРегТекстHTML = НРег(ТекстПисьмаHTML);
					
				Иначе
					Возврат; // чтобы не было зацикливания
				КонецЕсли;	
				
			Иначе
				Возврат; // чтобы не было зацикливания
			КонецЕсли;	
			
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры	

// Возвращает константу МаксимальныйРазмерВнешнегоИсходящегоПисьма - размер в Мб (по умолчанию 25).
Функция ПолучитьМаксимальныйРазмерВнешнегоИсходящегоПисьма() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МаксимальныйРазмерВнешнегоИсходящегоПисьма = Константы.МаксимальныйРазмерВнешнегоИсходящегоПисьма.Получить();
	Возврат МаксимальныйРазмерВнешнегоИсходящегоПисьма;
	
КонецФункции

// Функция разбивает переданную строку на отдельные слова
Функция ТекстПисьмаВСлова(Знач ТекстПисьма, РазделителиСлов = Неопределено) Экспорт 
	
	Если ТипЗнч(РазделителиСлов) <> Тип("Массив") Тогда
		РазделителиСлов = ПолучитьРазделителиСловТекстаПисьма();
	КонецЕсли;
	
	ТекстПисьма = СтрЗаменить(ТекстПисьма, "ё", "е");
	ТекстПисьма = СтрЗаменить(ТекстПисьма, "Ё", "Е");
	
	СловаПисьма = Новый Соответствие;
	
	ДлинаСтр = СтрДлина(ТекстПисьма);
	НачалоСлова = 0;
	ДлинаСлова = 0;
	Для Счетчик = 1 По ДлинаСтр Цикл
		
		ТекущийСимвол = Сред(ТекстПисьма, Счетчик, 1);
		
		Если РазделителиСлов.Найти(ТекущийСимвол) <> Неопределено Тогда
			
			Если НачалоСлова > 0 и ДлинаСлова > 0 Тогда
				
				Слово = Сред(ТекстПисьма, НачалоСлова, ДлинаСлова);
				
				Если СтрНайти(Слово, "@") = 0 И СтрНайти(Слово, ".") <> 0 Тогда
					МассивСловРазделенныхТочкой = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Слово, ".");
					Для Каждого СловоМассива Из МассивСловРазделенныхТочкой Цикл
						Если Не ПустаяСтрока(СловоМассива) Тогда
							СловаПисьма.Вставить(СловоМассива, 1);
						КонецЕсли;	
					КонецЦикла;	
				Иначе	
					СловаПисьма.Вставить(Слово, 1);
				КонецЕсли;	
				
			КонецЕсли;
			
			ДлинаСлова = 0;
			НачалоСлова = 0;
			
		Иначе
			
			Если НачалоСлова = 0 Тогда
				НачалоСлова = Счетчик;
			КонецЕсли;
			
			ДлинаСлова = ДлинаСлова + 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НачалоСлова > 0 и ДлинаСлова > 0 Тогда
		
		Слово = Сред(ТекстПисьма, НачалоСлова, Счетчик - 1);
		
		Если СтрНайти(Слово, "@") = 0 И СтрНайти(Слово, ".") <> 0 Тогда
			МассивСловРазделенныхТочкой = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Слово, ".");
			Для Каждого СловоМассива Из МассивСловРазделенныхТочкой Цикл
				Если Не ПустаяСтрока(СловоМассива) Тогда
					СловаПисьма.Вставить(СловоМассива, 1);
				КонецЕсли;	
			КонецЦикла;	
		Иначе	
			СловаПисьма.Вставить(Слово, 1);
		КонецЕсли;	
		
	КонецЕсли;
	
	Возврат СловаПисьма;
	
КонецФункции

// Проверяем наличие новых писем для пользователя
Процедура ПроверитьНаличиеНовыхПисем(ВозвращаемыеПараметры) Экспорт

	ВозвращаемыеПараметры = Новый Структура(
		"ЧислоНовыхПисем, ЧислоНовыхПисемСписка, ПоследнееПисьмо, Описание, УчетныеЗаписи, НужноОбновитьСписок, СписокОповещений", 
		0, 0, Неопределено, "", Новый Массив, Ложь, Новый Массив);

	Если Не ВстроеннаяПочтаСерверПовтИсп.ВстроеннаяПочтаИспользуется() Тогда
		Возврат;
	КонецЕсли;	

	Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	СотрудникиПользователя = 
		Сотрудники.ПользовательИЕгоСотрудники(
		Пользователь);
	
	УстановитьПривилегированныйРежим(Истина); 

	ЗапросУчетныеЗаписи = Новый Запрос;
	ЗапросУчетныеЗаписи.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	УчетныеЗаписиЭлектроннойПочтыОтветственныеЗаОбработкуПисем.Ссылка КАК УчетнаяЗапись
		|ПОМЕСТИТЬ УчетныеЗаписи
		|ИЗ
		|	Справочник.УчетныеЗаписиЭлектроннойПочты.ОтветственныеЗаОбработкуПисем КАК УчетныеЗаписиЭлектроннойПочтыОтветственныеЗаОбработкуПисем
		|ГДЕ
		|	УчетныеЗаписиЭлектроннойПочтыОтветственныеЗаОбработкуПисем.Сотрудник В(&СотрудникиПользователя)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УчетныеЗаписи.УчетнаяЗапись КАК УчетнаяЗапись
		|ПОМЕСТИТЬ УчетныеЗаписиПроверенные
		|ИЗ
		|	УчетныеЗаписи КАК УчетныеЗаписи
		|ГДЕ
		|	УчетныеЗаписи.УчетнаяЗапись.ВариантИспользования = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияПочты.Встроенная)
		|	И НЕ УчетныеЗаписи.УчетнаяЗапись.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(НастройкиОповещенийОПисьмах.ПоказыватьСписокОповещений, ЛОЖЬ) КАК ПоказыватьСписокОповещений,
		|	ЕСТЬNULL(НастройкиОповещенийОПисьмах.ПоказыватьВсплывающиеОповещения, ЛОЖЬ) КАК ПоказыватьВсплывающиеОповещения,
		|	УчетныеЗаписиПроверенные.УчетнаяЗапись КАК УчетнаяЗапись
		|ИЗ
		|	УчетныеЗаписиПроверенные КАК УчетныеЗаписиПроверенные
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОповещенийОПисьмах КАК НастройкиОповещенийОПисьмах
		|		ПО (НастройкиОповещенийОПисьмах.Пользователь = &ТекущийПользователь)
		|			И УчетныеЗаписиПроверенные.УчетнаяЗапись = НастройкиОповещенийОПисьмах.УчетнаяЗапись";
	
	ЗапросУчетныеЗаписи.УстановитьПараметр("СотрудникиПользователя", СотрудникиПользователя);
	ЗапросУчетныеЗаписи.УстановитьПараметр("ТекущийПользователь", Пользователь);
	
	Выборка = ЗапросУчетныеЗаписи.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
	
	МассивУчетныеЗаписиПоказыватьОповещение = Новый Массив;
	МассивУчетныеЗаписиПоОтветственному = Новый Массив;
	МассивОповещений = Новый Массив;
	ПоказыватьСписокОповещений = Ложь;
	Пока Выборка.Следующий() Цикл
		МассивУчетныеЗаписиПоОтветственному.Добавить(Выборка.УчетнаяЗапись);
		
		Если Выборка.ПоказыватьВсплывающиеОповещения Тогда
			МассивУчетныеЗаписиПоказыватьОповещение.Добавить(Выборка.УчетнаяЗапись);
		КонецЕсли;	
		
		Если Не ПоказыватьСписокОповещений И Выборка.ПоказыватьСписокОповещений Тогда
			ПоказыватьСписокОповещений = Истина;
		КонецЕсли;
		
	КонецЦикла;	
	
	// Формирование списка оповещений
	Если ПоказыватьСписокОповещений Тогда 
		ЗапросОповещения = Новый Запрос;
		ЗапросОповещения.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ОповещенияОПисьмах.Письмо КАК Письмо
			|ИЗ
			|	РегистрСведений.ОповещенияОПисьмах КАК ОповещенияОПисьмах
			|ГДЕ
			|	ОповещенияОПисьмах.Пользователь = &Пользователь
			|
			|УПОРЯДОЧИТЬ ПО
			|	Письмо УБЫВ";
			
		ЗапросОповещения.УстановитьПараметр("Пользователь", Пользователь);		
		Выборка = ЗапросОповещения.Выполнить().Выбрать();
				
		Пока Выборка.Следующий() Цикл 
			Если ЗначениеЗаполнено(Выборка.Письмо) Тогда 
				МассивОповещений.Добавить(Выборка.Письмо);	
			КонецЕсли;	
		КонецЦикла;
	
		ВозвращаемыеПараметры.СписокОповещений = МассивОповещений;
	КонецЕсли;	
	
	// тут читаем дату последней проверки
	УстановитьПривилегированныйРежим(Истина);
	ЗапросДатаПроверки = Новый Запрос;
	ЗапросДатаПроверки.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПроверкаПоступленияНовыхПисем.Дата
		|ИЗ
		|	РегистрСведений.ПроверкаПоступленияНовыхПисем КАК ПроверкаПоступленияНовыхПисем
		|ГДЕ
		|	ПроверкаПоступленияНовыхПисем.Пользователь = &Пользователь";
	ЗапросДатаПроверки.УстановитьПараметр("Пользователь", Пользователь);
	МассивДаты = ЗапросДатаПроверки.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
	УстановитьПривилегированныйРежим(Ложь);
	
	ДатаПоследнейПроверки = ТекущаяДатаСеанса();
	
	Если МассивДаты.Количество() = 1 Тогда
		
		ДатаПоследнейПроверки = МассивДаты[0];
		
		МинимальнаяДата = ТекущаяДатаСеанса() - 86400;
		Если ДатаПоследнейПроверки < МинимальнаяДата Тогда
			ДатаПоследнейПроверки = МинимальнаяДата;
		КонецЕсли;	
		
	КонецЕсли;
	
	КодОсновногоЯзыка = МультиязычностьСервер.СведенияОЯзыках().ОсновнойЯзык;
	
	ИмяСобытия  = НСтр("ru = 'ПроверитьНаличиеНовыхПисем.ДатаПоследнейПроверки'", КодОсновногоЯзыка);
	Комментарий = НСтр("ru = 'ДатаПоследнейПроверки = '", КодОсновногоЯзыка) + Строка(ДатаПоследнейПроверки);

	ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Информация, , , Комментарий);
	
	// проверка входящих писем
	ЗапросПисьма = Новый Запрос;
	ЗапросПисьма.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВходящееПисьмо.Ссылка КАК Ссылка,
		|	ВходящееПисьмо.Дата КАК Дата,
		|	ВходящееПисьмо.УчетнаяЗапись КАК УчетнаяЗапись,
		|	ВходящееПисьмо.Тема КАК Тема,
		|	ВходящееПисьмо.ОтправительАдресат КАК ОтправительАдресат,
		|	ВходящееПисьмо.ПометкаУдаления КАК ПометкаУдаления
		|ИЗ
		|	Документ.ВходящееПисьмо КАК ВходящееПисьмо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПрочтении КАК СведенияОПрочтении
		|		ПО (СведенияОПрочтении.Объект = ВходящееПисьмо.Ссылка)
		|			И (СведенияОПрочтении.Пользователь = &Пользователь)
		|ГДЕ
		|	ВходящееПисьмо.Дата > &Дата
		|	И ЕСТЬNULL(СведенияОПрочтении.Прочтен, ЛОЖЬ) = ЛОЖЬ
		|	И ВходящееПисьмо.УчетнаяЗапись В(&МассивУчетныеЗаписи)
		|	И ВходящееПисьмо.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата УБЫВ";
		
	ЗапросПисьма.УстановитьПараметр("Дата", ДатаПоследнейПроверки);
	ЗапросПисьма.УстановитьПараметр("Пользователь", Пользователь);
	ЗапросПисьма.УстановитьПараметр("МассивУчетныеЗаписи", МассивУчетныеЗаписиПоОтветственному);
	Выборка = ЗапросПисьма.Выполнить();
	
	ТаблицаПисем = Выборка.Выгрузить();
	
	ЗаписатьДатуВРегистрПроверкаПоступленияНовыхПисем(Пользователь, ТекущаяДатаСеанса());
	
	Если ТаблицаПисем.Количество() = 0 Тогда
		
		// проверка исходящих писем
		ЗапросПисьма = Новый Запрос;
		ЗапросПисьма.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ИсходящееПисьмо.Ссылка КАК Ссылка,
			|	ИсходящееПисьмо.ОтправительКонтакт КАК ОтправительКонтакт
			|ИЗ
			|	Документ.ИсходящееПисьмо КАК ИсходящееПисьмо
			|ГДЕ
			|	ИсходящееПисьмо.Дата > &Дата
			|	И ИсходящееПисьмо.УчетнаяЗапись В(&МассивУчетныеЗаписи)";
			
		ЗапросПисьма.УстановитьПараметр("Дата", ДатаПоследнейПроверки);
		ЗапросПисьма.УстановитьПараметр("МассивУчетныеЗаписи", МассивУчетныеЗаписиПоОтветственному);
		Выборка = ЗапросПисьма.Выполнить();
		
		Если Выборка.Пустой() Тогда
			Возврат;
		Иначе
			
			ТаблицаИсхПисем = Выборка.Выгрузить();
			Для Каждого Строка Из ТаблицаИсхПисем Цикл
				Если СотрудникиПользователя.Найти(Строка.ОтправительКонтакт) <> Неопределено Тогда
					ВозвращаемыеПараметры.НужноОбновитьСписок = Истина; // измененные исх письма есть
					Прервать;
				КонецЕсли;	
			КонецЦикла;	
			
			Возврат;
			
		КонецЕсли;	
		
	КонецЕсли;	
	
	ВозвращаемыеПараметры.НужноОбновитьСписок = Истина; // новые письма есть, даже если они не в учетных записях с настроенным оповещением
	
	ЧислоПисемВУчетныхЗаписяхСОповещением = 0;
	УчетныеЗаписиСоответствие = Новый Соответствие;
	ПоследнееПисьмоСтрока = Неопределено;
	Для Каждого Строка Из ТаблицаПисем Цикл
		
		Если МассивУчетныеЗаписиПоказыватьОповещение.Найти(Строка.УчетнаяЗапись) <> Неопределено Тогда
			УчетныеЗаписиСоответствие.Вставить(Строка.УчетнаяЗапись, 0);
			ЧислоПисемВУчетныхЗаписяхСОповещением = ЧислоПисемВУчетныхЗаписяхСОповещением + 1;
			Если ПоследнееПисьмоСтрока = Неопределено Тогда
				ПоследнееПисьмоСтрока = Строка;
			КонецЕсли;			
		КонецЕсли;	
		
	КонецЦикла;	
	
	Если ЧислоПисемВУчетныхЗаписяхСОповещением = 0
		И МассивОповещений.Количество() > 0 Тогда 
		ВозвращаемыеПараметры.ЧислоНовыхПисемСписка = МассивОповещений.Количество();
	КонецЕсли;
	
	ВозвращаемыеПараметры.ЧислоНовыхПисем = ЧислоПисемВУчетныхЗаписяхСОповещением;
	Если ПоследнееПисьмоСтрока <> Неопределено Тогда
		ВозвращаемыеПараметры.ПоследнееПисьмо = ПоследнееПисьмоСтрока.Ссылка;
	КонецЕсли;	
	
	Для Каждого КлючИЗначение Из УчетныеЗаписиСоответствие Цикл
		ВозвращаемыеПараметры.УчетныеЗаписи.Добавить(КлючИЗначение.Ключ);
	КонецЦикла;	
	
	Если ЧислоПисемВУчетныхЗаписяхСОповещением = 1 Тогда
		
		МаксимальнаяДлинаСтроки = 18;
		
		Тема = Лев(ПоследнееПисьмоСтрока.Тема, МаксимальнаяДлинаСтроки);
		Если СтрДлина(ПоследнееПисьмоСтрока.Тема) > МаксимальнаяДлинаСтроки Тогда
			Тема = Тема + "...";
		КонецЕсли;	
		ПредставлениеАдресата = ПолучитьПредставлениеИКонтактАдресата(ПоследнееПисьмоСтрока.ОтправительАдресат);
		ОтКого = Лев(ПредставлениеАдресата.Представление, МаксимальнаяДлинаСтроки);
		Если СтрДлина(ПредставлениеАдресата.Представление) > МаксимальнаяДлинаСтроки Тогда
			ОтКого = ОтКого + "...";
		КонецЕсли;	
		
		ВозвращаемыеПараметры.Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='От: ""%1""
			|Тема: ""%2""'"), 
			ОтКого, Тема);
			
		Если МассивУчетныеЗаписиПоказыватьОповещение.Количество() > 1 Тогда
			
			УчетнаяЗаписьПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСТр("ru = 'Учет. запись: ""%1""'"), Строка(ПоследнееПисьмоСтрока.УчетнаяЗапись));
				
			ВозвращаемыеПараметры.Описание = ВозвращаемыеПараметры.Описание + Символы.ПС 
				+ УчетнаяЗаписьПредставление;
				
		КонецЕсли;	
		
	ИначеЕсли ЧислоПисемВУчетныхЗаписяхСОповещением > 1 Тогда
		
		ВозвращаемыеПараметры.Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Новые письма: %1 шт.'"), Строка(ТаблицаПисем.Количество()));
			
		Если МассивУчетныеЗаписиПоказыватьОповещение.Количество() > 1 Тогда
				
			СтрокаВсехУчетныхЗаписей = "";
			Для Каждого УчетнаяЗапись Из ВозвращаемыеПараметры.УчетныеЗаписи Цикл
				Если Не ПустаяСтрока(СтрокаВсехУчетныхЗаписей) Тогда
					СтрокаВсехУчетныхЗаписей = СтрокаВсехУчетныхЗаписей + ", ";
				КонецЕсли;	
				СтрокаВсехУчетныхЗаписей = СтрокаВсехУчетныхЗаписей + Строка(УчетнаяЗапись);
			КонецЦикла;	
			
			УчетнаяЗаписьПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСТр("ru = 'Учет. записи: ""%1""'"), СтрокаВсехУчетныхЗаписей);
				
			ВозвращаемыеПараметры.Описание = ВозвращаемыеПараметры.Описание + Символы.ПС 
				+ УчетнаяЗаписьПредставление;
				
		КонецЕсли;	
			
	КонецЕсли;	       
	
	ВозвращаемыеПараметры.Описание = ОбработкаСтрокиXML.УдалитьНедопустимыеСимволыXML(ВозвращаемыеПараметры.Описание);	
	
КонецПроцедуры	

// Записывает дату в регистр ПроверкаПоступленияНовыхПисем для указанного пользователя.
Процедура ЗаписатьДатуВРегистрПроверкаПоступленияНовыхПисем(Пользователь, Дата) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	МенеджерЗаписи = РегистрыСведений.ПроверкаПоступленияНовыхПисем.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Пользователь = Пользователь;
	МенеджерЗаписи.Дата = Дата;
	МенеджерЗаписи.Записать();

КонецПроцедуры	

// Возвращает структуру  ВключенаПроверкаПриходаПисем, ИнтервалПроверкиВМинутах
Функция ПолучитьНастройкиОповещенийОНовыхПисьмах() Экспорт
	
	ИнтервалПроверкиВМинутах = ПолучитьПерсональнуюНастройку("ВремяПроверкиНовыхПисем");
	ПроверятьНаличиеНовыхПисем = ПолучитьПерсональнуюНастройку("ПроверятьНаличиеНовыхПисем");
	
	СтруктураВозврата = Новый Структура("ПроверятьНаличиеНовыхПисем, ИнтервалПроверкиВМинутах", 
		ПроверятьНаличиеНовыхПисем, ИнтервалПроверкиВМинутах);
		
	Возврат СтруктураВозврата;
	
КонецФункции

// Получает учетные записи, где текущий пользователь - Ответственный
Функция ПолучитьУчетныеЗаписиТекущегоПользователя() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	УчетныеЗаписиЭлектроннойПочтыОтветственныеЗаОбработкуПисем.Ссылка
		|ИЗ
		|	Справочник.УчетныеЗаписиЭлектроннойПочты.ОтветственныеЗаОбработкуПисем КАК УчетныеЗаписиЭлектроннойПочтыОтветственныеЗаОбработкуПисем
		|ГДЕ
		|	УчетныеЗаписиЭлектроннойПочтыОтветственныеЗаОбработкуПисем.Сотрудник В (&СотрудникиПользователя)
		|	И УчетныеЗаписиЭлектроннойПочтыОтветственныеЗаОбработкуПисем.Ссылка.ВариантИспользования = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияПочты.Встроенная)
		|	И УчетныеЗаписиЭлектроннойПочтыОтветственныеЗаОбработкуПисем.Ссылка.ПометкаУдаления = ЛОЖЬ";
	
	СотрудникиПользователя = Сотрудники.СотрудникиПользователя();
	Запрос.УстановитьПараметр("СотрудникиПользователя", СотрудникиПользователя);
	
	МассивУчетныхЗаписей = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
	
	Возврат МассивУчетныхЗаписей;
	
КонецФункции

// Устанавливает связь между письмом и веткой переписки. Если ветка не существует, тогда создает
// новую.
// Выполняет в привилегированном режиме.
//
// Параметры:
//  Письмо - ссылка на документы ВходящееПисьмо и ИсходящееПисьмо
//
Процедура ОбновитьВеткуПереписки(Письмо) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПисьмаБезВеток = Новый Соответствие;
	ПредельноеКоличествоИтераций = 1000;
	КорневоеПисьмо = Письмо.Ссылка;
	
	ВеткаПереписки = Неопределено;
	КоличествоНеучтенныхПисемВПереписке = 0;
	
	Пока Истина Цикл
		
		ВеткаПереписки = РегистрыСведений.ПисьмаВеток.ПолучитьВетку(КорневоеПисьмо, Ложь);
		// Корневое письмо найдено
		Если ВеткаПереписки <> Неопределено Тогда
			КорневоеПисьмо = ВеткаПереписки.КорневоеПисьмо;
			Прервать;
		КонецЕсли;
		
		// У текущего письма нет связи с веткой переписки
		Если ВстроеннаяПочтаКлиентСервер.ЭтоПисьмо(КорневоеПисьмо.Ссылка) Тогда
			ПисьмаБезВеток.Вставить(КорневоеПисьмо.Ссылка);
		КонецЕсли;
		КоличествоНеучтенныхПисемВПереписке = КоличествоНеучтенныхПисемВПереписке + 1;
		
		ПисьмоОснование = ПолучитьПисьмоОснование(КорневоеПисьмо);
		// Корневое письмо найдено
		Если ПисьмоОснование = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		КорневоеПисьмо = ПисьмоОснование;
		
		// Чтобы не висело, в случае коллапса
		ПредельноеКоличествоИтераций = ПредельноеКоличествоИтераций - 1;
		Если ПредельноеКоличествоИтераций = 0 Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	// Получаем ветку для корневого письма, когда не дождались завершения итераций.
	Если ВеткаПереписки = Неопределено И ПредельноеКоличествоИтераций = 0 Тогда
		ВеткаПереписки = РегистрыСведений.ПисьмаВеток.ПолучитьВетку(КорневоеПисьмо, Ложь);
	КонецЕсли;
	
	// Если ветки нет, тогда создаем новую.
	Если ВеткаПереписки = Неопределено Тогда
		
		НоваяВетка = Справочники.ВеткиПереписки.СоздатьЭлемент();
		НоваяВетка.КорневоеПисьмо = КорневоеПисьмо.Ссылка;
		НоваяВетка.КоличествоПисемВПереписке = КоличествоНеучтенныхПисемВПереписке;
		НоваяВетка.Записать();
		
		ВеткаПереписки = НоваяВетка.Ссылка;
		
	ИначеЕсли КоличествоНеучтенныхПисемВПереписке > 0 Тогда
		
		СуществующаяВетка = ВеткаПереписки.Ссылка.ПолучитьОбъект();
		СуществующаяВетка.КоличествоПисемВПереписке = СуществующаяВетка.КоличествоПисемВПереписке + КоличествоНеучтенныхПисемВПереписке;
		СуществующаяВетка.Записать();
		
		ВеткаПереписки = СуществующаяВетка.Ссылка;
		
	КонецЕсли;
	
	// Связываем письма без веток
	Для каждого ПисьмоБезВетки Из ПисьмаБезВеток Цикл
		
		МенеджерЗаписи = РегистрыСведений.ПисьмаВеток.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Письмо = ПисьмоБезВетки.Ключ;
		МенеджерЗаписи.ВеткаПереписки = ВеткаПереписки.Ссылка;
		МенеджерЗаписи.Записать();
		
		Контроль.ОтметитьПоследнееПисьмоВКонтрольнойКарточке(ВеткаПереписки.Ссылка, ПисьмоБезВетки.Ключ);
		
	КонецЦикла;
	
КонецПроцедуры

// Обработчик подписки ОбновлениеКоличестваПисемВПереписке.
//
Процедура ОбновлениеВеткиПерепискиПриЗаписи(Источник, Отказ, Замещение) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для каждого Запись Из Источник.ЭтотОбъект Цикл
		
		Если ВстроеннаяПочтаКлиентСервер.ЭтоПисьмо(Запись.Объект) Тогда
			ОбновитьВеткуПереписки(Запись.Объект);
		КонецЕсли;
		
		Если ВстроеннаяПочтаКлиентСервер.ЭтоПисьмо(Запись.СвязанныйОбъект) Тогда
			ОбновитьВеткуПереписки(Запись.СвязанныйОбъект);
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

// Формирует таблицу быстрого отбора для списка писем
Процедура СформироватьТаблицуБыстрогоОтбора(ОтборВПапке, ИмяЭтойФормы) Экспорт
	
	// Настройка отбора в текущей папке
	НоваяСтрока = ОтборВПапке.Добавить();
	НоваяСтрока.Параметр = "Адресаты";
	НоваяСтрока.ПредставлениеПараметра = НСтр("ru = 'От кого/Кому:'");
	НоваяСтрока.Тип = "Строка";
	НоваяСтрока.Значение = "";
	
	НоваяСтрока = ОтборВПапке.Добавить();
	НоваяСтрока.Параметр = "Тема";
	НоваяСтрока.ПредставлениеПараметра = НСтр("ru = 'Тема:'");
	НоваяСтрока.Тип = "Строка";
	НоваяСтрока.Значение = "";

	Если ПолучитьФункциональнуюОпцию("ИспользоватьПолнотекстовыйПоиск") Тогда
		НоваяСтрока = ОтборВПапке.Добавить();
		НоваяСтрока.Параметр = "Текст";
		НоваяСтрока.ПредставлениеПараметра = НСтр("ru = 'Текст:'");
		НоваяСтрока.Тип = "Строка";
		НоваяСтрока.Значение = "";
	КонецЕсли;
	
	НоваяСтрока = ОтборВПапке.Добавить();
	НоваяСтрока.Параметр = "Дата_С";
	НоваяСтрока.ПредставлениеПараметра = НСтр("ru = 'Дата с:'");
	НоваяСтрока.Тип = "Дата";
	НоваяСтрока.Значение = Дата(1,1,1);
	
	НоваяСтрока = ОтборВПапке.Добавить();
	НоваяСтрока.Параметр = "Дата_По";
	НоваяСтрока.ПредставлениеПараметра = НСтр("ru = 'Дата по:'");
	НоваяСтрока.Тип = "Дата";
	НоваяСтрока.Значение = Дата(1,1,1);
	
	НоваяСтрока = ОтборВПапке.Добавить();
	НоваяСтрока.Параметр = "Флаг";
	НоваяСтрока.ПредставлениеПараметра = НСтр("ru = 'Флаг:'");
	НоваяСтрока.Тип = "ПеречислениеСсылка.ФлагиОбъектов";
	НоваяСтрока.Значение = Перечисления.ФлагиОбъектов.ПустаяСсылка();
	
	НоваяСтрока = ОтборВПапке.Добавить();
	НоваяСтрока.Параметр = "Прочтено";
	НоваяСтрока.ПредставлениеПараметра = НСтр("ru = 'Прочтенные:'");
	НоваяСтрока.Тип = "Булево";
	НоваяСтрока.Значение = Неопределено;
	
	НоваяСтрока = ОтборВПапке.Добавить();
	НоваяСтрока.Параметр = "Комментарий";
	НоваяСтрока.ПредставлениеПараметра = НСтр("ru = 'Комментарий:'");
	НоваяСтрока.Тип = "Строка";
	НоваяСтрока.Значение = "";
	
	НастройкиОтбора = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяЭтойФормы,
		"ОтборВПапке",
		Новый Массив);

	Если НастройкиОтбора.Количество() <> ОтборВПапке.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Индекс = 0;
	Для каждого Элемент Из НастройкиОтбора Цикл
		
		Если Тип(ОтборВПапке[Индекс].Тип) = ТипЗнч(Элемент) Тогда
			ОтборВПапке[Индекс].Значение = Элемент;
		КонецЕсли;
		
		Индекс = Индекс + 1;
		
	КонецЦикла;
	
КонецПроцедуры

// Меняет настройку рабочего стола
//
// Параметры:
//  ИмяПрежнее - Строка - прежнее имя настройки
//  ИмяНовое - Строка - новое имя настройки
Процедура ПоменятьНастройкуРабочегоСтола(ИмяПрежнее, ИмяНовое) Экспорт
	
	НастройкиНачальнойСтраницы = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиНачальнойСтраницы");
	СоставФорм = НастройкиНачальнойСтраницы.ПолучитьСоставФорм();
	
	Для Индекс = 0 По СоставФорм.ЛеваяКолонка.Количество()-1  Цикл
		Если СоставФорм.ЛеваяКолонка[Индекс] = ИмяПрежнее Тогда
			СоставФорм.ЛеваяКолонка[Индекс] = ИмяНовое;
		КонецЕсли;	
	КонецЦикла;	
	
	Для Индекс = 0 По СоставФорм.ПраваяКолонка.Количество()-1  Цикл
		Если СоставФорм.ПраваяКолонка[Индекс] = ИмяПрежнее Тогда
			СоставФорм.ПраваяКолонка[Индекс] = ИмяНовое;
		КонецЕсли;	
	КонецЦикла;	
	
	НастройкиНачальнойСтраницы.УстановитьСоставФорм(СоставФорм);
	
	ХранилищеСистемныхНастроек.Сохранить("Общее/НастройкиНачальнойСтраницы", , НастройкиНачальнойСтраницы);
	
КонецПроцедуры	

// Заполняет дерево папок в форме списка
Процедура ЗаполнитьДеревоПапок(ДеревоПапок, РежимМоиПапки, ОтображатьУдаленныеПисьмаИПапки) Экспорт
	
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПапкиПисем.Ссылка КАК Папка,
		|	ПапкиПисем.ПометкаУдаления КАК ПапкаПометкаУдаления,
		|	ПапкиПисем.Представление КАК ПредставлениеПапки,
		|	ПапкиПисем.ВидПапки КАК ВидПапки,
		|	ВЫБОР
		|		КОГДА ПапкиПисем.ПометкаУдаления
		|			ТОГДА 1
		|		КОГДА ПапкиПисем.ВидПапки = ЗНАЧЕНИЕ(Перечисление.ВидыПапокПисем.Черновики)
		|			ТОГДА 2
		|		КОГДА ПапкиПисем.ВидПапки = ЗНАЧЕНИЕ(Перечисление.ВидыПапокПисем.Корзина)
		|			ТОГДА 4
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК НомерКартинки,
		|	ВЫБОР
		|		КОГДА ПапкиПисем.ВидПапки = ЗНАЧЕНИЕ(Перечисление.ВидыПапокПисем.Входящие)
		|			ТОГДА 1
		|		КОГДА ПапкиПисем.ВидПапки = ЗНАЧЕНИЕ(Перечисление.ВидыПапокПисем.Исходящие)
		|			ТОГДА 2
		|		КОГДА ПапкиПисем.ВидПапки = ЗНАЧЕНИЕ(Перечисление.ВидыПапокПисем.Отправленные)
		|			ТОГДА 3
		|		КОГДА ПапкиПисем.ВидПапки = ЗНАЧЕНИЕ(Перечисление.ВидыПапокПисем.Корзина)
		|			ТОГДА 5
		|		КОГДА ПапкиПисем.ВидПапки = ЗНАЧЕНИЕ(Перечисление.ВидыПапокПисем.Черновики)
		|			ТОГДА 6
		|		ИНАЧЕ 7
		|	КОНЕЦ КАК Порядок,
		|
		|	&ПапкаБыстрогоДоступа
		|
		|ИЗ
		|	Справочник.ПапкиПисем КАК ПапкиПисем
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПапкиПисемБыстрогоДоступа КАК ПапкиПисемБыстрогоДоступа
		|	ПО (ПапкиПисемБыстрогоДоступа.Папка = ПапкиПисем.Ссылка И ПапкиПисемБыстрогоДоступа.Пользователь = &ТекущийПользователь)
		|ГДЕ
		|	((НЕ ПапкиПисем.ПометкаУдаления)
		|			ИЛИ &ОтображатьУдаленные)
		|
		| И &ОтборМоиПапки
		|
		|УПОРЯДОЧИТЬ ПО
		|	Папка ИЕРАРХИЯ";
	
	// Добавление отметки папки быстрого доступа, которая используется для условного
	// оформления в режиме просмотра "Все папки"
	Если РежимМоиПапки Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПапкаБыстрогоДоступа",
			"	ЛОЖЬ КАК ПапкаБыстрогоДоступа");
		
		// Добавление отбора только папок, которые находятся в иерархии папок быстрого
		// доступа
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ОтборМоиПапки",
			"	И ПапкиПисем.Ссылка В ИЕРАРХИИ
			|		(ВЫБРАТЬ
			|			ПапкиПисемБыстрогоДоступа.Папка
			|		ИЗ
			|			РегистрСведений.ПапкиПисемБыстрогоДоступа КАК ПапкиПисемБыстрогоДоступа
			|		ГДЕ
			|			ПапкиПисемБыстрогоДоступа.Пользователь = &ТекущийПользователь)");
		
	Иначе
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПапкаБыстрогоДоступа",
			"	ВЫБОР
			|		КОГДА ПапкиПисемБыстрогоДоступа.Папка ЕСТЬ NULL 
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ КАК ПапкаБыстрогоДоступа");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ОтборМоиПапки", "");
		
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ОтображатьУдаленные", ОтображатьУдаленныеПисьмаИПапки);
	Запрос.УстановитьПараметр("ТекущийПользователь", ПользователиКлиентСервер.ТекущийПользователь());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Дерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ДеревоПапок.Строки.Очистить();
	
	СведенияОПапках = ВстроеннаяПочтаСервер.ПолучитьСведенияОПапках(
		Пользователи.ТекущийПользователь(),
		РежимМоиПапки);
	
	ДобавитьПапкиВДерево(ДеревоПапок.Строки, Дерево.Строки, СведенияОПапках);
	
	СортироватьИерархически(ДеревоПапок.Строки, "Порядок, Представление");
	
	// Добавление папок поиска в общее дерево папок
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПапкиПоиска.Ссылка,
		|	ПапкиПоиска.КоличествоОбъектов,
		|	ПапкиПоиска.Наименование КАК Наименование
		|ИЗ
		|	Справочник.ПапкиПоиска КАК ПапкиПоиска
		|ГДЕ
		|	ПапкиПоиска.Ответственный В (&ПользователиИСотрудники)
		|	И НЕ ПапкиПоиска.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
	
	Запрос.УстановитьПараметр("ПользователиИСотрудники", Сотрудники.ПользовательИЕгоСотрудники());
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		ПапкаПоиска = ДеревоПапок.Строки.Добавить();
		ПапкаПоиска.Ссылка = Неопределено;
		ПапкаПоиска.Представление = НСтр("ru = 'Папки поиска'");
		ПапкаПоиска.НомерКартинки = 5;
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = ПапкаПоиска.Строки.Добавить();
			НоваяСтрока.Ссылка = Выборка.Ссылка;
			
			НоваяСтрока.Представление = Строка(Выборка.Ссылка);
			
			Если Выборка.КоличествоОбъектов > 0 Тогда
				
				НоваяСтрока.Представление = НоваяСтрока.Представление
					+ " (" + Строка(Выборка.КоличествоОбъектов) + ")";
				
			КонецЕсли;
			
			НоваяСтрока.НомерКартинки = 5;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает текст запроса поиска по папке поиска для формы списка
Функция ПолучитьТекстЗапросаПоПапкеПоиска() Экспорт
	
	Возврат "ЖурналДокументов.ЭлектроннаяПочта КАК ЭлектроннаяПочта
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПромежуточныеРезультатыПоискаПисем КАК ПромежуточныеРезультатыПоискаПисем
	|ПО (ПромежуточныеРезультатыПоискаПисем.Ссылка = ЭлектроннаяПочта.Ссылка)
	|	И (ПромежуточныеРезультатыПоискаПисем.ИдентификаторПоиска = &ИдентификаторПоиска)
	| ";

КонецФункции	

// Возвращает текст запроса поиска по папке поиска для формы списка
//
// Возвращаемое значение:
//  Строка
Функция ТекстЗапросаДо3_0_17() Экспорт
	
	Возврат 
	"ВЫБРАТЬ
	|ВЫБОР
	|	КОГДА ЕСТЬNULL(СведенияОПрочтении.Прочтен, ЛОЖЬ) = ЛОЖЬ
	|			И НЕ ЭлектроннаяПочта.ПометкаУдаления
	|		ТОГДА 0
	|	КОГДА ЕСТЬNULL(СведенияОПрочтении.Прочтен, ЛОЖЬ) = ИСТИНА
	|			И НЕ ЭлектроннаяПочта.ПометкаУдаления
	|		ТОГДА 1
	|	КОГДА ЕСТЬNULL(СведенияОПрочтении.Прочтен, ЛОЖЬ) = ИСТИНА
	|			И ЭлектроннаяПочта.ПометкаУдаления
	|		ТОГДА 2
	|	КОГДА ЕСТЬNULL(СведенияОПрочтении.Прочтен, ЛОЖЬ) = ЛОЖЬ
	|			И ЭлектроннаяПочта.ПометкаУдаления
	|		ТОГДА 3
	|КОНЕЦ КАК НомерКартинки,
	|ЭлектроннаяПочта.Ссылка КАК Ссылка,
	|ЭлектроннаяПочта.ПометкаУдаления КАК ПометкаУдаления,
	|ЭлектроннаяПочта.Тема КАК Тема,
	|ЭлектроннаяПочта.Получатели КАК Получатели,
	|ЭлектроннаяПочта.Тип КАК Тип,
	|ЭлектроннаяПочта.ЕстьВложения КАК ЕстьВложения,
	|ЭлектроннаяПочта.УчетнаяЗапись КАК УчетнаяЗапись,
	|ЭлектроннаяПочта.Получено КАК Получено,
	|ЭлектроннаяПочта.Отправлено КАК Отправлено,
	|ЭлектроннаяПочта.Дата КАК Дата,
	|ЭлектроннаяПочта.ДатаОтправкиПолучения КАК ДатаОтправкиПолучения,
	|ЭлектроннаяПочта.Предмет КАК Предмет,
	|ЭлектроннаяПочта.Папка КАК Папка,
	|ЭлектроннаяПочта.Отправитель КАК Отправитель,
	|ЭлектроннаяПочта.Отправитель.Наименование КАК От_кого,
	|ВЫРАЗИТЬ(ЭлектроннаяПочта.Размер / 1024 КАК ЧИСЛО(10, 1)) КАК РазмерКб,
	|ВЫБОР
	|	КОГДА ТИПЗНАЧЕНИЯ(ЭлектроннаяПочта.Ссылка) = ТИП(Документ.ВходящееПисьмо)
	|		ТОГДА ЕСТЬNULL(СведенияОбАдресатах.Представление, ЭлектроннаяПочта.Отправитель.Наименование)
	|	ИНАЧЕ ЭлектроннаяПочта.Адресаты
	|КОНЕЦ КАК Адресаты,
	|ВЫБОР
	|	КОГДА ТИПЗНАЧЕНИЯ(ЭлектроннаяПочта.Ссылка) = ТИП(Документ.ВходящееПисьмо)
	|		ТОГДА ВЫБОР
	|				КОГДА СведенияОбАдресатах.Контакт ЕСТЬ NULL
	|					ТОГДА ЛОЖЬ
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	ИНАЧЕ ИСТИНА
	|КОНЕЦ КАК НайденВКонтактах,
	|ЕСТЬNULL(СведенияОПрочтении.Прочтен, ЛОЖЬ) КАК Прочтено,
	|ВЫБОР
	|	КОГДА ЭлектроннаяПочта.Важность = ЗНАЧЕНИЕ(Перечисление.ВажностьПисем.Высокая)
	|		ТОГДА 2
	|	КОГДА ЭлектроннаяПочта.Важность = ЗНАЧЕНИЕ(Перечисление.ВажностьПисем.Низкая)
	|		ТОГДА 0
	|	ИНАЧЕ 1
	|КОНЕЦ КАК ВажностьНомерКартинки,
	|ВЫБОР
	|	КОГДА ФлагиПисем.Флаг = ЗНАЧЕНИЕ(Перечисление.ФлагиОбъектов.Красный)
	|		ТОГДА 1
	|	КОГДА ФлагиПисем.Флаг = ЗНАЧЕНИЕ(Перечисление.ФлагиОбъектов.Синий)
	|		ТОГДА 2
	|	КОГДА ФлагиПисем.Флаг = ЗНАЧЕНИЕ(Перечисление.ФлагиОбъектов.Желтый)
	|		ТОГДА 3
	|	КОГДА ФлагиПисем.Флаг = ЗНАЧЕНИЕ(Перечисление.ФлагиОбъектов.Зеленый)
	|		ТОГДА 4
	|	КОГДА ФлагиПисем.Флаг = ЗНАЧЕНИЕ(Перечисление.ФлагиОбъектов.Оранжевый)
	|		ТОГДА 5
	|	КОГДА ФлагиПисем.Флаг = ЗНАЧЕНИЕ(Перечисление.ФлагиОбъектов.Лиловый)
	|		ТОГДА 6
	|	ИНАЧЕ 0
	|КОНЕЦ КАК НомерФлага,
	|ЕСТЬNULL(ФлагиПисем.Флаг, ЛОЖЬ) КАК Флаг,
	|ФлагиПисем.Флаг КАК ФлагПоиск,
	|КешИнформацииОбОбъектах.ПолученОтвет КАК ПолученОтвет,
	|КешИнформацииОбОбъектах.ОтправленОтвет КАК ОтправленОтвет,
	|КешИнформацииОбОбъектах.Переслан КАК Переслан,
	|КешИнформацииОбОбъектах.ЕстьДокументы КАК ЕстьДокументы,
	|СведенияОПрочтении.Прочтен КАК Прочтен,
	|ВЫБОР
	|	КОГДА КешИнформацииОбОбъектах.Объект ЕСТЬ NULL
	|		ТОГДА 0
	|	КОГДА КешИнформацииОбОбъектах.ЕстьОшибкиПриемкиОтправкиПочты
	|			И ЭлектроннаяПочта.Отправлено = ДАТАВРЕМЯ(1, 1, 1)
	|		ТОГДА 6
	|	КОГДА КешИнформацииОбОбъектах.ЕстьОшибкиПриемкиОтправкиПочты
	|			И ТИПЗНАЧЕНИЯ(ЭлектроннаяПочта.Ссылка) = ТИП(Документ.ВходящееПисьмо)
	|		ТОГДА 13
	|	КОГДА КешИнформацииОбОбъектах.Отозван
	|		ТОГДА 9
	|	КОГДА ЭлектроннаяПочта.ОтправкаОтменена
	|		ТОГДА 5
	|	КОГДА КешИнформацииОбОбъектах.ПолученОтвет
	|		ТОГДА 4
	|	КОГДА КешИнформацииОбОбъектах.ОтправленОтвет
	|			И КешИнформацииОбОбъектах.Перенаправлен
	|		ТОГДА 8
	|	КОГДА НЕ КешИнформацииОбОбъектах.ОтправленОтвет
	|			И КешИнформацииОбОбъектах.Перенаправлен
	|		ТОГДА 7
	|	КОГДА КешИнформацииОбОбъектах.ОтправленОтвет
	|			И КешИнформацииОбОбъектах.Переслан
	|		ТОГДА 3
	|	КОГДА КешИнформацииОбОбъектах.ОтправленОтвет
	|			И НЕ КешИнформацииОбОбъектах.Переслан
	|		ТОГДА 1
	|	КОГДА НЕ КешИнформацииОбОбъектах.ОтправленОтвет
	|			И КешИнформацииОбОбъектах.Переслан
	|		ТОГДА 2
	|	ИНАЧЕ 0
	|КОНЕЦ КАК НомерСостояния,
	|ВЫБОР
	|	КОГДА ЭлектроннаяПочта.Тип = ТИП(Документ.ВходящееПисьмо)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыПисем.Входящие)
	|	ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыПисем.Исходящие)
	|КОНЕЦ КАК ТипПисьма,
	|ЭлектроннаяПочта.Важность КАК Важность,
	|ВЫБОР
	|	КОГДА КешИнформацииОбОбъектах.ЕстьОшибкиПриемкиОтправкиПочты
	|			И (ЭлектроннаяПочта.Отправлено = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ ТИПЗНАЧЕНИЯ(ЭлектроннаяПочта.Ссылка) = ТИП(Документ.ВходящееПисьмо))
	|		ТОГДА ИСТИНА
	|	ИНАЧЕ ЛОЖЬ
	|КОНЕЦ КАК ЕстьОшибкиПриемкиОтправкиПочты,
	|ЭлектроннаяПочта.ОтправкаОтменена КАК ОтправкаОтменена,
	|ЭлектроннаяПочта.ВидМаршрутизации КАК ВидМаршрутизации,
	|ВЫБОР
	|	КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.НаКонтроле)
	|		ТОГДА ВЫБОР
	|				КОГДА КешИнформацииОбОбъектах.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
	|						ИЛИ КешИнформацииОбОбъектах.СрокИсполнения >= &ТекущаяДата
	|					ТОГДА 1
	|				ИНАЧЕ 3
	|			КОНЕЦ
	|	КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.СнятсКонтроля)
	|		ТОГДА 4
	|	КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.НаКонтролеНесколько)
	|		ТОГДА ВЫБОР
	|				КОГДА КешИнформацииОбОбъектах.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
	|						ИЛИ КешИнформацииОбОбъектах.СрокИсполнения >= &ТекущаяДата
	|					ТОГДА 5
	|				КОГДА КешИнформацииОбОбъектах.СрокИсполненияОбщий = ДАТАВРЕМЯ(1, 1, 1)
	|						ИЛИ КешИнформацииОбОбъектах.СрокИсполненияОбщий >= &ТекущаяДата
	|					ТОГДА 8
	|				ИНАЧЕ 6
	|			КОНЕЦ
	|	КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.СнятсКонтроляНесколько)
	|		ТОГДА 7
	|	КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.СмешанноНесколько)
	|		ТОГДА ВЫБОР
	|				КОГДА КешИнформацииОбОбъектах.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
	|						ИЛИ КешИнформацииОбОбъектах.СрокИсполнения >= &ТекущаяДата
	|					ТОГДА 9
	|				ИНАЧЕ 10
	|			КОНЕЦ
	|	ИНАЧЕ 0
	|КОНЕЦ КАК СостояниеКонтроля,
	|ЭлектроннаяПочта.Комментарий КАК Комментарий,
	|ЭлектроннаяПочта.НомерСпособаАдресации КАК НомерСпособаАдресации
	|ИЗ
	|ЖурналДокументов.ЭлектроннаяПочта КАК ЭлектроннаяПочта
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФлагиОбъектов КАК ФлагиПисем
	|	ПО (ФлагиПисем.Объект = ЭлектроннаяПочта.Ссылка)
	|		И (ФлагиПисем.ФизическоеЛицо = &ФизическоеЛицо)
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПрочтении КАК СведенияОПрочтении
	|	ПО ЭлектроннаяПочта.Ссылка = СведенияОПрочтении.Объект
	|		И (СведенияОПрочтении.Пользователь = &Пользователь)
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КешИнформацииОбОбъектах КАК КешИнформацииОбОбъектах
	|	ПО ЭлектроннаяПочта.Ссылка = КешИнформацииОбОбъектах.Объект
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
	|	ПО ЭлектроннаяПочта.Отправитель = СведенияОбАдресатах.АдресатСообщения
	|		И (СведенияОбАдресатах.Активна)
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах1
	|	ПО ЭлектроннаяПочта.Отправитель = СведенияОбАдресатах1.АдресатСообщения
	|		И (СведенияОбАдресатах1.Активна)
	|		И (СведенияОбАдресатах1.Контакт < СведенияОбАдресатах.Контакт)
	|ГДЕ
	|СведенияОбАдресатах1.Контакт ЕСТЬ NULL
	|И (&ОтображатьУдаленные
	|		ИЛИ НЕ ЭлектроннаяПочта.ПометкаУдаления)
	|{ГДЕ
	|	(&Папка = НЕОПРЕДЕЛЕНО
	|			ИЛИ ЭлектроннаяПочта.Папка = &Папка)}";

КонецФункции	

// Устанавливает отбор быстрого поиска в форме списка писем
Процедура УстановитьОтборСписка(ПоискВключен, ЭтаФорма) Экспорт
	
	Для каждого строка Из ЭтаФорма.ОтборВПапке Цикл
		ИмяПараметра = СтрЗаменить(СтрЗаменить(Строка.Параметр, "_С", ""), "_По", "");
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ЭтаФорма.Список.Отбор, ИмяПараметра);
	КонецЦикла;
	
	УстановитьДоступностьКомандыСбросаОтбора(Ложь, ЭтаФорма);
	
	Для каждого строка Из ЭтаФорма.ОтборВПапке Цикл
		
		ИмяПараметра = СтрЗаменить(СтрЗаменить(Строка.Параметр, "_С", ""), "_По", "");
		
		Если ЗначениеЗаполнено(Строка.Значение)
			И Строка.Значение <> Неопределено Тогда
			УстановитьДоступностьКомандыСбросаОтбора(Истина, ЭтаФорма);
		КонецЕсли;	
		
		Если Не ПоискВключен Тогда
			Продолжить;
		КонецЕсли;
		
		Если Строка.Тип = "Строка"
			И ЗначениеЗаполнено(Строка.Значение)
			И Строка.Параметр <> "Текст"  Тогда
			
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
				ЭтаФорма.Список.Отбор,
				ИмяПараметра,
				ВидСравненияКомпоновкиДанных.Содержит,
				Строка.Значение);
						
		ИначеЕсли Строка.Тип = "Булево"
			И ЗначениеЗаполнено(Строка.Значение) Тогда
			
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
				ЭтаФорма.Список.Отбор,
				ИмяПараметра,
				ВидСравненияКомпоновкиДанных.Равно,
				Строка.Значение);
					
		ИначеЕсли Строка.Тип = "Дата"
			И ЗначениеЗаполнено(Строка.Значение)Тогда
			
			Если СтрНайти(Строка.Параметр, "_С") > 0 Тогда
				
				ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
					ЭтаФорма.Список.Отбор,
					ИмяПараметра,
					ВидСравненияКомпоновкиДанных.БольшеИлиРавно,
					НачалоДня(Строка.Значение));
				
			КонецЕсли;
			
			Если СтрНайти(Строка.Параметр, "_По") > 0 Тогда
				
				ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
					ЭтаФорма.Список.Отбор,
					ИмяПараметра,
					ВидСравненияКомпоновкиДанных.МеньшеИлиРавно,
					КонецДня(Строка.Значение));
				
			КонецЕсли;
							
		ИначеЕсли (Строка.Значение = Неопределено
			Или ЗначениеЗаполнено(Строка.Значение))
			И Строка.Тип = "ПеречислениеСсылка.ФлагиОбъектов" Тогда
			
			Если Строка.Значение = НСтр("ru = 'Любой'") Тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
					ЭтаФорма.Список.Отбор,
					ИмяПараметра,
					ВидСравненияКомпоновкиДанных.НеРавно,
					Ложь);			
			Иначе
				ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
					ЭтаФорма.Список.Отбор,
					ИмяПараметра,
					ВидСравненияКомпоновкиДанных.Равно,
					Строка.Значение);
			КонецЕсли;
						
		ИначеЕсли (Строка.Значение = Неопределено
			Или ЗначениеЗаполнено(Строка.Значение))
			И Строка.Тип <> "Булево" 
			И Строка.Параметр <> "Текст" Тогда
			
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
				ЭтаФорма.Список.Отбор,
				ИмяПараметра,
				ВидСравненияКомпоновкиДанных.Равно,
				Строка.Значение);
				
		КонецЕсли;
		
	КонецЦикла;
	
	// Запись истории ввода
	ЭтаФорма.СписокСписковВыбора.Очистить();
	Для каждого Строка Из ЭтаФорма.ОтборВПапке Цикл
		
		СписокПоследнихЗначений = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			ЭтаФорма.ИмяЭтойФормы,
			Строка.Параметр,
			Новый СписокЗначений());
		
		ОбновленныйСписокПоследнихЗначений = Новый СписокЗначений;
		Если (Строка.Тип = "Строка"
			Или Строка.Тип = "Дата")
			И ЗначениеЗаполнено(Строка.Значение) Тогда
			
			Представление = Строка(Строка.Значение);
			Если ТипЗнч(Строка.Значение) = Тип("Дата") Тогда
				Представление = Формат(Строка.Значение, "ДФ=dd.MM.yyyy");
			КонецЕсли;
			
			ОбновленныйСписокПоследнихЗначений.Добавить(
				Строка.Значение,
				Представление);
				
		ИначеЕсли Строка.Тип = "Булево" Тогда				
			
			ОбновленныйСписокПоследнихЗначений.Добавить(Истина);
			ОбновленныйСписокПоследнихЗначений.Добавить(Ложь);
			
		ИначеЕсли Строка.Тип = "ПеречислениеСсылка.ФлагиОбъектов" Тогда
			
			// Подготовка списка выбора для Флага
			Для каждого Флаг Из Метаданные.Перечисления.ФлагиОбъектов.ЗначенияПеречисления Цикл
				
				ОбновленныйСписокПоследнихЗначений.Добавить(
					Перечисления.ФлагиОбъектов[Флаг.Имя],,,
					БиблиотекаКартинок[Флаг.Имя + "Флаг"]);
				
			КонецЦикла;
			
			ОбновленныйСписокПоследнихЗначений.Добавить(
				НСтр("ru = 'Любой'"), НСтр("ru = 'Любой флаг'"),,
				БиблиотекаКартинок.ПустойФлаг);
				
			ОбновленныйСписокПоследнихЗначений.Добавить(
				Ложь,,,
				БиблиотекаКартинок.ПустойФлаг);
			
		КонецЕсли;
		
		Для каждого Элемент Из СписокПоследнихЗначений Цикл
			
			Если ОбновленныйСписокПоследнихЗначений.НайтиПоЗначению(Элемент.Значение) = Неопределено
				И ОбновленныйСписокПоследнихЗначений.Количество() < 8 Тогда
				
				ОбновленныйСписокПоследнихЗначений.Добавить(
					Элемент.Значение,
					Элемент.Представление,
					Элемент.Пометка,
					Элемент.Картинка);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ЭтаФорма.ИмяЭтойФормы, Строка.Параметр, ОбновленныйСписокПоследнихЗначений);
		
		// Обновление коллекции списков выбора на форме
		ЭтаФорма.СписокСписковВыбора.Добавить(ОбновленныйСписокПоследнихЗначений);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьДоступностьКомандыСбросаОтбора(Доступность, ЭтаФорма) Экспорт
	
	ЭтаФорма.Элементы.СброситьОтбор.Доступность = Доступность;	
	
	Если Не ЭтаФорма.ПоказыватьДеревоПапок Тогда
		ЭтаФорма.Элементы.ГруппаОтбор.Видимость = Доступность;	
	КонецЕсли;	
	
КонецПроцедуры

// Читает статус письма
Процедура ПрочитатьСтатусПисьма(Письмо, НомерСостояния, ПодсказкаСтатуса) Экспорт
	
	ОтправкаОтменена = Неопределено;
	
	Если ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(Письмо) Тогда
		ОтправкаОтменена = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Письмо, "ОтправкаОтменена");
	КонецЕсли;	
	
	Отправлено = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Письмо, "ДатаОтправки");
	
	КешИнформации = РегистрыСведений.КешИнформацииОбОбъектах.СоздатьМенеджерЗаписи();
	КешИнформации.Объект = Письмо;
	КешИнформации.Прочитать();
	
	НомерСостояния = 0;
	
	Если ОтправкаОтменена = Истина Тогда
		НомерСостояния = 5;
		ПодсказкаСтатуса = НСтр("ru='Отправка отменена'");
	ИначеЕсли Не КешИнформации.Выбран() Тогда
		НомерСостояния = 0;
	ИначеЕсли КешИнформации.ЕстьОшибкиПриемкиОтправкиПочты И Отправлено = Дата(1, 1, 1) Тогда
		НомерСостояния = 6;
		ПодсказкаСтатуса = НСтр("ru='Есть ошибки отправки почты'");
	ИначеЕсли КешИнформации.ПолученОтвет Тогда	
		НомерСостояния = 4;
		ПодсказкаСтатуса = НСтр("ru='Получен ответ'");
	ИначеЕсли КешИнформации.ОтправленОтвет И КешИнформации.Переслан Тогда	
		НомерСостояния = 3;
		ПодсказкаСтатуса = НСтр("ru='Отвечено, переслано'");
	ИначеЕсли КешИнформации.ОтправленОтвет
				И Не КешИнформации.Переслан Тогда	
		НомерСостояния = 1;
		ПодсказкаСтатуса = НСтр("ru='Отправлен ответ'");
	ИначеЕсли НЕ КешИнформации.ОтправленОтвет
				И КешИнформации.Переслан Тогда	
		НомерСостояния = 2;
		ПодсказкаСтатуса = НСтр("ru='Переслано'"); 
	ИначеЕсли КешИнформации.Отозван Тогда
		
		НомерСостояния = 9;
		ПодсказкаСтатуса = НСтр("ru='Письмо отозвано'");
		
	КонецЕсли;	
	
КонецПроцедуры

// Получает представление папки для режима отображения по одной папке
//
// Параметры:
//  Папка - СправочникСсылка.ПапкиПисем - папка письма
//
// Возвращаемое значение:
//  Структура:
//   *Описание - Строка - полное описание выбранной папки
//   *КоличествоПисем - Число - количество писем в папке
//   *ОписаниеРодителей - Строка - описание всех родителей через /
//
Функция ПолучитьПредставлениеПапки(Папка) Экспорт
	
	Описание = "";
	
	РеквизитыПапки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Папка, 
		"Родитель, ВариантОтображенияКоличестваПисем");
		
	Родитель = РеквизитыПапки.Родитель;
	ВариантОтображенияКоличестваПисем = РеквизитыПапки.ВариантОтображенияКоличестваПисем;
	
	Если ЗначениеЗаполнено(Родитель) Тогда
		ОписаниеРодителей = Справочники.ПапкиПисем.ПолучитьПолныйПутьПапки(Родитель);
		Описание = СтрШаблон("%1 (%2)", Строка(Папка), ОписаниеРодителей);
	Иначе
		Описание = Строка(Папка);
	КонецЕсли;	
	
	КоличествоПисем = ПолучитьКоличествоПисемВПапке(Папка, ВариантОтображенияКоличестваПисем);
	
	Возврат Новый Структура("Описание, КоличествоПисем, ОписаниеРодителей",
		Описание,
		КоличествоПисем,
		ОписаниеРодителей);
	
КонецФункции

// Вызывается непосредственно перед удалением письма из ИБ.
// Удаляет связи письма с веткой переписки из РС ПисьмаВеток и саму ветку, при необходимости.
//
// Параметры:
//  Письмо - ДокументСсылка.ВходящееПисьмо
//         - ДокументСсылка.ИсходящееПисьмо
//
Процедура ПередУдалениемПисьма(Письмо) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Определение ветки переписки.
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВеткиПереписки.Ссылка,
		|	ВеткиПереписки.КорневоеПисьмо
		|ИЗ
		|	РегистрСведений.ПисьмаВеток КАК ПисьмаВеток
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВеткиПереписки КАК ВеткиПереписки
		|		ПО ПисьмаВеток.ВеткаПереписки = ВеткиПереписки.Ссылка
		|ГДЕ
		|	ПисьмаВеток.Письмо = &Письмо";
	Запрос.УстановитьПараметр("Письмо", Письмо);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		// Если письмо является корневым
		Если Выборка.КорневоеПисьмо = Письмо Тогда
			
			ПисьмаБезВеток = Новый Соответствие;
			
			// Удаление записей из РС, соответствующих удаляемой ветке переписки.
			Запрос2 = Новый Запрос;
			Запрос2.Текст =
				"ВЫБРАТЬ
				|	ПисьмаВеток.Письмо
				|ИЗ
				|	РегистрСведений.ПисьмаВеток КАК ПисьмаВеток
				|ГДЕ
				|	ПисьмаВеток.ВеткаПереписки = &ВеткаПереписки";
			Запрос2.УстановитьПараметр("ВеткаПереписки", Выборка.Ссылка);
			Выборка2 = Запрос2.Выполнить().Выбрать();
			
			Если Выборка2.Количество() > 0 Тогда
				Пока Выборка2.Следующий() Цикл
					
					МенеджерЗаписи = РегистрыСведений.ПисьмаВеток.СоздатьМенеджерЗаписи();
					МенеджерЗаписи.Письмо = Выборка2.Письмо;
					МенеджерЗаписи.Прочитать();
					МенеджерЗаписи.Удалить();
					
					Если Выборка2.Письмо <> Письмо Тогда
						ПисьмаБезВеток.Вставить(Выборка2.Письмо, Истина);
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
			
			// Удаление ветки.
			ОбъектВеткаПереписки = Выборка.Ссылка.ПолучитьОбъект();
			ОбъектВеткаПереписки.Удалить();
			
			// Создание новых веток переписок и связей с другими ветками для писем без веток.
			Для Каждого ПисьмоБезВетки Из ПисьмаБезВеток Цикл
				ОбновитьВеткуПереписки(ПисьмоБезВетки.Ключ);
			КонецЦикла;
			
		// Если письмо не является корневым.
		Иначе
			
			// Удаление записи из РС.
			МенеджерЗаписи = РегистрыСведений.ПисьмаВеток.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Письмо = Письмо;
			МенеджерЗаписи.Прочитать();
			МенеджерЗаписи.Удалить();
			
			// Обновление количества писем в переписке.
			ОбъектВеткаПереписки = Выборка.Ссылка.ПолучитьОбъект();
			ОбъектВеткаПереписки.КоличествоПисемВПереписке = ОбъектВеткаПереписки.КоличествоПисемВПереписке - 1;
			ОбъектВеткаПереписки.Записать();
			
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Возвращает адрес электронной почты пользователя.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - адрес которого нужно получить
//
// Возвращаемое значение:
//    Адресат - Структура - найденные данные.
//    ""      - Строка - при отсутствии данных.
//
Функция ПолучитьАдресЭлектроннойПочты(Пользователь) Экспорт 
	
	ОбъектКИ = Новый Массив;
	ОбъектКИ.Добавить(Пользователь);
	ТаблицаКонтактов = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(
		ОбъектКИ, Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты,, ТекущаяДатаСеанса());
	
	Если ТаблицаКонтактов.Количество() > 0 Тогда
		Адрес = ТаблицаКонтактов[0].Представление;
		Представление = Строка(Пользователь);
		Адресат = Новый Структура("Контакт, Адрес, ОтображаемоеИмя",
			Пользователь, Адрес, Представление);
		
		Возврат Адресат;
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

// Получает имя значения перечисления как объекта метаданных.
//
// Параметры:
//  Значение - значение перечисления для которого необходимо получить имя перечисления.
//
// Возвращаемое значение:
//  Строка - имя значения перечисления как объекта метаданных.
//
Функция ИмяЗначенияПеречисления(Значение) Экспорт
	
	ОбъектМетаданных = Значение.Метаданные();
	
	ИндексЗначения = Перечисления[ОбъектМетаданных.Имя].Индекс(Значение);
	
	Возврат ОбъектМетаданных.ЗначенияПеречисления[ИндексЗначения].Имя;
	
КонецФункции 

// Выполняет отзыв (удаление) писем в ящиках получателей
Процедура ОтозватьПисьмо(Письмо, ТекстСообщения) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	РеквизитыПисьма = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Письмо, 
		"ВидМаршрутизации, 
		|ИдентификаторСообщения, 
		|ДатаОтправки");
	
	ПисьмоОтправлено = ЗначениеЗаполнено(РеквизитыПисьма.ДатаОтправки);
	Если Не ПисьмоОтправлено Тогда 
		ТекстСообщения = НСтр("ru = 'Отзыв писем возможен только для отправленных писем.'");
		Возврат;
	КонецЕсли;	
	
	Если ПисьмоОтозвано(Письмо) Тогда 
		ТекстСообщения = НСтр("ru = 'Письмо уже было отозвано ранее.'");
		Возврат;
	КонецЕсли;
	
	СведенияОбОтзыве = Новый ТаблицаЗначений;
	СведенияОбОтзыве.Колонки.Добавить("Адрес");
	СведенияОбОтзыве.Колонки.Добавить("Адресат");
	СведенияОбОтзыве.Колонки.Добавить("ВходящееПисьмо");
	СведенияОбОтзыве.Колонки.Добавить("Отозвано");
	СведенияОбОтзыве.Колонки.Добавить("Сообщение");
	СведенияОбОтзыве.Колонки.Добавить("ТипАдреса");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсходящееПисьмоПолучателиПисьма.Адресат,
	|	ИсходящееПисьмоПолучателиПисьма.Адресат.Адрес КАК Адрес,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыАдресатов.Кому) КАК ТипАдреса
	|ИЗ
	|	Документ.ИсходящееПисьмо.ПолучателиПисьма КАК ИсходящееПисьмоПолучателиПисьма
	|ГДЕ
	|	ИсходящееПисьмоПолучателиПисьма.Ссылка = &Письмо
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИсходящееПисьмоПолучателиКопий.Адресат,
	|	ИсходящееПисьмоПолучателиКопий.Адресат.Адрес,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыАдресатов.Копия)
	|ИЗ
	|	Документ.ИсходящееПисьмо.ПолучателиКопий КАК ИсходящееПисьмоПолучателиКопий
	|ГДЕ
	|	ИсходящееПисьмоПолучателиКопий.Ссылка = &Письмо
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИсходящееПисьмоПолучателиСкрытыхКопий.Адресат,
	|	ИсходящееПисьмоПолучателиСкрытыхКопий.Адресат.Адрес,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыАдресатов.СкрытаяКопия)
	|ИЗ
	|	Документ.ИсходящееПисьмо.ПолучателиСкрытыхКопий КАК ИсходящееПисьмоПолучателиСкрытыхКопий
	|ГДЕ
	|	ИсходящееПисьмоПолучателиСкрытыхКопий.Ссылка = &Письмо";
	
	Запрос.УстановитьПараметр("Письмо", Письмо);
	ВыборкаПолучателиПисьма = Запрос.Выполнить().Выбрать();
	Пока ВыборкаПолучателиПисьма.Следующий() Цикл
		НоваяСтрока = СведенияОбОтзыве.Добавить();
		НоваяСтрока.Адресат = ВыборкаПолучателиПисьма.Адресат;
		НоваяСтрока.Адрес = ВыборкаПолучателиПисьма.Адрес;
		НоваяСтрока.ТипАдреса = ВыборкаПолучателиПисьма.ТипАдреса;
		НоваяСтрока.Отозвано = Ложь;
	КонецЦикла;	
	
	Если РеквизитыПисьма.ВидМаршрутизации = Перечисления.ВидыМаршрутизацииПисем.Внутренняя Тогда 
		
		ВсеАдресатыВОчереди = РегистрыСведений.ОчередьВнутреннейПриемкиПисем.АдресатыПоПисьму(Письмо);
		
		// удаление из регистра ОчередьВнутреннейПриемкиПисем
		НачатьТранзакцию();
		Попытка
			
			Для Каждого СтрокаАдресат Из ВсеАдресатыВОчереди Цикл
				
				Адресат = СтрокаАдресат.Адресат;
				
				// Проверки возможности конкурентной записи регистра.
				// Если запись уже занята - значит конкурирующий поток ей занимается, не ждём когда станет доступна.
				КлючЗаписи = РегистрыСведений.ОчередьВнутреннейПриемкиПисем.СоздатьКлючЗаписи(
					Новый Структура("Письмо, Адресат", Письмо, Адресат));
				Попытка
					ЗаблокироватьДанныеДляРедактирования(КлючЗаписи);
					
				Исключение
					Продолжить; // не смогли заблокировать, потом будем удалять уже по входящим письмам
				КонецПопытки;
				
				РегистрыСведений.ОчередьВнутреннейПриемкиПисем.ПисьмоАдресатаБылоУдалено(
					Письмо,
					Адресат);
					
				НайденнаяСтрока = СведенияОбОтзыве.Найти(СтрокаАдресат.Адрес, "Адрес");
				Если НайденнаяСтрока <> Неопределено Тогда 
					НайденнаяСтрока.Отозвано = Истина;
					НайденнаяСтрока.Сообщение = НСтр("ru = 'письмо удалено из очереди отправки'");
				КонецЕсли;
					
			КонецЦикла;	
			
			ЗафиксироватьТранзакцию();	
		Исключение
			ОтменитьТранзакцию();
			ВызватьИсключение;
		КонецПопытки;
		
		Если СведенияОбОтзыве.Найти(Ложь, "Отозвано") <> Неопределено Тогда // есть неотозванные письма
			
			// поиск в ящике получателя
			Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ВходящееПисьмо.Ссылка КАК ВходящееПисьмо,
			|	ВходящееПисьмо.УчетнаяЗапись КАК УчетнаяЗапись,
			|	ВходящееПисьмо.УчетнаяЗапись.АдресЭлектроннойПочты КАК Адрес
			|ИЗ
			|	Документ.ВходящееПисьмо КАК ВходящееПисьмо
			|ГДЕ
			|	ВходящееПисьмо.ИдентификаторСообщения = &ИдентификаторСообщения");
			Запрос.УстановитьПараметр("ИдентификаторСообщения", РеквизитыПисьма.ИдентификаторСообщения);
			
			ВыборкаПисьма = Запрос.Выполнить().Выбрать();
			Пока ВыборкаПисьма.Следующий() Цикл
				ВходящееПисьмоСсылка = ВыборкаПисьма.ВходящееПисьмо;
				ВходящееПисьмоАдрес = ВыборкаПисьма.Адрес;
				
				Просмотрено = РегистрыСведений.СведенияОПросмотреПисем.ПолучитьПризнакПросмотрено(ВходящееПисьмоСсылка);
				Если Просмотрено Тогда 
					Продолжить;
				КонецЕсли;	
				
				Попытка
					ЗаблокироватьДанныеДляРедактирования(ВходящееПисьмоСсылка);
					ВходящееПисьмоОбъект = ВходящееПисьмоСсылка.ПолучитьОбъект();
					ВходящееПисьмоОбъект.Тема = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Письмо отозвано: %1'"),
						ВходящееПисьмоОбъект.Тема);
					
					ВходящееПисьмоОбъект.ТекстПисьмаHTMLХранилище = Неопределено;
					ВходящееПисьмоОбъект.ТекстПисьмаПростойТекстХранилище = Неопределено;
					ВходящееПисьмоОбъект.ТекстПисьмаРазмеченныйТекстХранилище = Неопределено;
					ВходящееПисьмоОбъект.Записать();
					
					РегистрыСведений.HTMLПредставленияСодержанияПисем.Удалить(ВходящееПисьмоСсылка);
					
					Запрос = Новый Запрос;
					Запрос.Текст = 
					"ВЫБРАТЬ
					|	Справочник.Файлы.Ссылка КАК Ссылка
					|ГДЕ
					|	ВладелецФайла = &ВладелецФайла";
					Запрос.УстановитьПараметр("ВладелецФайла", ВходящееПисьмоСсылка);
					ВыборкаФайлы = Запрос.Выполнить().Выбрать();
					Пока ВыборкаФайлы.Следующий() Цикл
						ЗаблокироватьДанныеДляРедактирования(ВыборкаФайлы.Ссылка);
						ФайлОбъект = ВыборкаФайлы.Ссылка.ПолучитьОбъект();
						ФайлОбъект.ПолноеНаименование = НСтр("ru = 'Отозван'");
						ФайлОбъект.Записать();
						ФайлОбъект.УстановитьПометкуУдаления(Истина);
						
						РегистрыСведений.ТекстыФайлов.УдалитьЗапись(ВыборкаФайлы.Ссылка);
					КонецЦикла;
					
					Запрос = Новый Запрос;
					Запрос.Текст = 
					"ВЫБРАТЬ
					|	Версиифайлов.Ссылка КАК Ссылка,
					|	Версиифайлов.ТипХраненияФайла
					|ИЗ
					|	Справочник.ВерсииФайлов КАК Версиифайлов
					|ГДЕ
					|	Версиифайлов.Владелец.ВладелецФайла = &ВладелецФайла";
					Запрос.УстановитьПараметр("ВладелецФайла", ВходящееПисьмоСсылка);
					ВыборкаВерсии = Запрос.Выполнить().Выбрать();
					Пока ВыборкаВерсии.Следующий() Цикл
						ЗаблокироватьДанныеДляРедактирования(ВыборкаВерсии.Ссылка);
						ВерсияОбъект = ВыборкаВерсии.Ссылка.ПолучитьОбъект();
						ВерсияОбъект.Расширение = "txt";
						ВерсияОбъект.Размер = 0;
						
						РегистрыСведений.ТекстыВерсийФайлов.УдалитьЗапись(ВыборкаВерсии.Ссылка);
						
						ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
						ТекстовыйДокумент = Новый ТекстовыйДокумент;
						ТекстовыйДокумент.Записать(ИмяВременногоФайла);
						ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла);
						
						Если ВыборкаВерсии.ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда 
							
							ПолныйПуть = ФайловыеФункцииСлужебный.ПолныйПутьТома(ВерсияОбъект.Том) + ВерсияОбъект.ПутьКФайлу; 
							Файл = Новый Файл(ПолныйПуть);
							Файл.УстановитьТолькоЧтение(Ложь);
							ДвоичныеДанные.Записать(ПолныйПуть);
							
						Иначе
							
							МенеджерЗаписи = РегистрыСведений.ДвоичныеДанныеФайлов.СоздатьМенеджерЗаписи();
							МенеджерЗаписи.Файл = ВерсияОбъект.Ссылка;
							МенеджерЗаписи.Прочитать();
							
							Если МенеджерЗаписи.Выбран() Тогда 
								МенеджерЗаписи.ДвоичныеДанныеФайла = Новый ХранилищеЗначения(ДвоичныеДанные);
								МенеджерЗаписи.Записать();
							КонецЕсли;	
							
						КонецЕсли;
						
						УдалитьФайлы(ИмяВременногоФайла);
						ВерсияОбъект.Записать();
						
					КонецЦикла;	
					
					НайденнаяСтрока = СведенияОбОтзыве.Найти(ВходящееПисьмоАдрес, "Адрес");
					Если НайденнаяСтрока <> Неопределено Тогда 
						НайденнаяСтрока.Отозвано = Истина;
						НайденнаяСтрока.Сообщение = НСтр("ru = 'очищено входящее письмо'");
					КонецЕсли;	
					
				Исключение
					
					// отзовем уведомлением
					Инфо = ИнформацияОбОшибке();
					ОписаниеОшибки = ПодробноеПредставлениеОшибки(Инфо);
					
					ЗаписьЖурналаРегистрации(
						НСтр("ru = 'Отзыв писем. Ошибка при очистке входящего письма.'"),
						УровеньЖурналаРегистрации.Ошибка,,
						ВходящееПисьмоСсылка,
						ОписаниеОшибки);
					
				КонецПопытки;	
				
			КонецЦикла;	
			
		КонецЕсли;
		
	КонецЕсли;	
		 	
	//Формирование уведомлений
	Адресаты = Новый Массив;
	АдресатыДляУведомления = Новый Массив;
	Для Каждого Строка Из СведенияОбОтзыве Цикл
		
		Если Строка.Отозвано Тогда 
			Продолжить;
		КонецЕсли;	
		
		Адресаты.Добавить(Строка.Адресат);
		
		Если Строка.ТипАдреса <> Перечисления.ТипыАдресатов.СкрытаяКопия Тогда
			АдресатыДляУведомления.Добавить(Строка.Адресат);
		КонецЕсли;		
		
	КонецЦикла;
	
	Если АдресатыДляУведомления.Количество() > 0 Тогда 
		ОтправитьУведомлениеОбОтзыве(АдресатыДляУведомления, Письмо);
	КонецЕсли;	
	
	Для Каждого Адресат Из АдресатыДляУведомления Цикл
		НайденнаяСтрока = СведенияОбОтзыве.Найти(Адресат.Адрес, "Адрес");
		Если НайденнаяСтрока <> Неопределено Тогда 
			НайденнаяСтрока.Отозвано = Истина;
			НайденнаяСтрока.Сообщение = НСтр("ru = 'отправлено уведомление об отзыве'");
		КонецЕсли;
	КонецЦикла;	
	
	ТекстСообщения = НСтр("ru = 'Письмо было успешно отозвано у получателей:'");
	Для Каждого Строка Из СведенияОбОтзыве Цикл
		СтруктураАдресата = ВстроеннаяПочтаСервер.ПолучитьПредставлениеИКонтактАдресата(Строка.Адресат);
		ТекстСообщения = ТекстСообщения + Символы.ПС + СтруктураАдресата.Представление + " - " + Строка.Сообщение;
	КонецЦикла;	
	
	// признак отозвано
	МенеджерЗаписи = РегистрыСведений.КешИнформацииОбОбъектах.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Объект = Письмо;
	МенеджерЗаписи.Прочитать();
		
	МенеджерЗаписи.Объект = Письмо;
	МенеджерЗаписи.Отозван = Истина;
	МенеджерЗаписи.Записать();
				
КонецПроцедуры			
				
// Проверяет, что переданное письмо было отозвано
Функция ПисьмоОтозвано(ПисьмоСсылка) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ПисьмоСсылка) Тогда 
		Возврат Ложь;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КешИнформацииОбОбъектах.Отозван
	|ИЗ
	|	РегистрСведений.КешИнформацииОбОбъектах КАК КешИнформацииОбОбъектах
	|ГДЕ
	|	КешИнформацииОбОбъектах.Объект = &Объект";
	
	Запрос.УстановитьПараметр("Объект", ПисьмоСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.Отозван;
	КонецЕсли;
		
	Возврат Ложь;
	
КонецФункции	

// Возвращает массив структур - данные о файлах письма
Функция ПолучитьСведенияОФайлахПисьма(Письмо) Экспорт
	
	СведенияОФайлахДокументооборот = Новый Массив;
	
	ФайлыПисьма = ВстроеннаяПочтаСервер.ПолучитьФайлыПисьма(
		Письмо, // Письмо
		Истина, // ФормироватьРазмерПредставление
		Ложь, // ВключатьПомеченныеНаУдаление
		Ложь,    // ТолькоСИдентификаторами
		Истина); // ТолькоБезИдентификаторов  - чтобы картинки в HTML не показывать
	
	Для Каждого Файл Из ФайлыПисьма Цикл
		
		СведенияОФайле = Новый Структура(
			"ИмяФайла,
			|ИндексКартинки,
			|ПометкаУдаления,
			|Представление,
			|Размер,
			|РазмерПредставление,
			|Редактирует,
			|РедактируетТекущийПользователь,
			|ЭтоВложенноеПисьмо,
			|Ссылка,
			|Письмо");
		
		ЗаполнитьЗначенияСвойств(СведенияОФайле, Файл);
		СведенияОФайлахДокументооборот.Добавить(СведенияОФайле);
		
	КонецЦикла;
	
	ВложенныеПисьма = РегистрыСведений.ВложенныеПисьма.ПолучитьВложенныеПисьма(Письмо);
	Для Каждого Файл Из ВложенныеПисьма Цикл
		
		СведенияОФайле = Новый Структура(
			"ИмяФайла,
			|ИндексКартинки,
			|ПометкаУдаления,
			|Представление,
			|Размер,
			|РазмерПредставление,
			|Редактирует,
			|РедактируетТекущийПользователь,
			|ЭтоВложенноеПисьмо,
			|Ссылка,
			|Письмо");
		
		ЗаполнитьЗначенияСвойств(СведенияОФайле, Файл);
		СведенияОФайлахДокументооборот.Добавить(СведенияОФайле);
		
	КонецЦикла;
	
	Возврат СведенияОФайлахДокументооборот;
	
КонецФункции

// Преобразует массив ссылок на письма в массив структур с информацией в формате вложений - для поля Вложения
//
// Параметры:
// - МассивПисем - массив, содержащий письма 
//
//  Возвращаемое значение:
//   Массив структур - массив 
// с полями писем (Письмо Представление ИмяФайла ИндексКартинки Размер РазмерПредставление ПометкаУдаления Редактирует РедактируетТекущийПользователь)
//  - для заполнения реквизита Вложения в карточке письма или форме списка писем.
Функция ПолучитьИнформациюОПисьмахВложениях(МассивПисем) Экспорт
	
	Результат = Новый Массив;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	КраткиеОписанияПисем.Ссылка КАК Письмо,
		|	КраткиеОписанияПисем.Тема,
		|	КраткиеОписанияПисем.Дата,
		|	КраткиеОписанияПисем.Размер,
		|	КраткиеОписанияПисем.ПометкаУдаления
		|ИЗ
		|	ЖурналДокументов.ЭлектроннаяПочта КАК КраткиеОписанияПисем
		|ГДЕ
		|	КраткиеОписанияПисем.Ссылка В(&МассивПисем)");
		
	Запрос.УстановитьПараметр("МассивПисем", МассивПисем);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ИндексКартинки = 32;
		
		ФайлИнфо = Новый Структура;
		ФайлИнфо.Вставить("Письмо", Выборка.Письмо);
		ФайлИнфо.Вставить("Представление", Выборка.Тема);
		ФайлИнфо.Вставить("ИмяФайла", Выборка.Тема);
		ФайлИнфо.Вставить("ИндексКартинки", ИндексКартинки);
		ФайлИнфо.Вставить("Размер", Выборка.Размер);
		ФайлИнфо.Вставить("РазмерПредставление", РаботаСоСтроками.ПолучитьРазмерСтрокой(Выборка.Размер));
		ФайлИнфо.Вставить("ПометкаУдаления", Выборка.ПометкаУдаления);
		ФайлИнфо.Вставить("Редактирует", Неопределено);
		ФайлИнфо.Вставить("РедактируетТекущийПользователь", Ложь);
		
		Результат.Добавить(ФайлИнфо);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСоответствиеАдресатовПоАдресуИКонтакту(МассивСтруктур) Экспорт
	
	Таблица = Новый ТаблицаЗначений; 
	
	Массив = Новый Массив;
	Массив.Добавить(Тип("Строка")); 
	НоваяКолонка = Таблица.Колонки.Добавить("Адрес", Новый ОписаниеТипов(Массив));
	
	Массив = Новый Массив;
	Массив.Добавить(Тип("СправочникСсылка.Пользователи")); 
	Массив.Добавить(Тип("СправочникСсылка.ФизическиеЛица")); 
	Массив.Добавить(Тип("СправочникСсылка.РолиИсполнителей")); 
	Массив.Добавить(Тип("СправочникСсылка.Контрагенты")); 
	Массив.Добавить(Тип("СправочникСсылка.КонтактныеЛица")); 
	Массив.Добавить(Тип("СправочникСсылка.ЛичныеАдресаты")); 
	Массив.Добавить(Тип("СправочникСсылка.Сотрудники"));
	НоваяКолонка = Таблица.Колонки.Добавить("Контакт", Новый ОписаниеТипов(Массив));
	
	Для Каждого Элемент Из МассивСтруктур Цикл
		НоваяСтрока = Таблица.Добавить();
		НоваяСтрока.Адрес = Элемент.Адрес;
		НоваяСтрока.Контакт = Элемент.Контакт;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Таблица.Контакт КАК Контакт,
		|	ВЫРАЗИТЬ(Таблица.Адрес КАК СТРОКА(100)) КАК Адрес
		|ПОМЕСТИТЬ ВременнаяТаблица
		|ИЗ
		|	&Данные КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СведенияОбАдресатах.АдресатСообщения КАК АдресатСообщения,
		|	СведенияОбАдресатах.Контакт КАК Контакт,
		|	СведенияОбАдресатах.АдресатСообщения.Адрес КАК Адрес,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.РолиИсполнителей)
		|				ИЛИ ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Сотрудники)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Внешний,
		|	СведенияОбАдресатах.АдресатСообщения.ВидМаршрутизации КАК ВидМаршрутизации
		|ИЗ
		|	ВременнаяТаблица КАК ВременнаяТаблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|		ПО (СведенияОбАдресатах.Контакт = ВременнаяТаблица.Контакт)
		|			И (СведенияОбАдресатах.АдресатСообщения.Адрес = ВременнаяТаблица.Адрес)";
		
	Запрос.УстановитьПараметр("Данные", Таблица);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	СоответствиеАдресатов = Новый Соответствие;
	Для Каждого Строка Из РезультатЗапроса Цикл
		СоответствиеАдресатов[НРег(Строка.Адрес)] 
			= Новый Структура("Адресат, Внешний, ВидМаршрутизации", 
				Строка.АдресатСообщения, Строка.Внешний, Строка.ВидМаршрутизации);
	КонецЦикла;	
	
	Возврат СоответствиеАдресатов;
		
КонецФункции	

// Получает элементы справочника "Адресаты почтовых сообщений"
//	по переданной Фамилии и ФИО.
// Если адресата с таким адресом и представлением не найдено, 
//	то создастся новый адресат и привяжется к контактам с таким же адресом
//
// Параметры:
//   ФИО - Строка - ФИО искомого адресата
//
// Возвращаемое значение: 
//   МассивАдресатов - Маасив адрестов удовлетворяющих условию
//
Функция ПолучитьПочтовогоАдресатаПоФИО(ФИО) Экспорт
	
	ПозицияПробела = Найти(ФИО, " ");
	Если ПозицияПробела > 0 Тогда 
		Фамилия = Лев(ФИО, ПозицияПробела - 1);
	Иначе 
		Фамилия = ФИО;
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Сотрудники.Ссылка,
		|	ЕСТЬNULL(СведенияОбАдресатах.АдресатСообщения, НЕОПРЕДЕЛЕНО) КАК АдресатСообщения
		|ПОМЕСТИТЬ Адресаты
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСотрудники КАК ОсновныеСотрудники
		|		ПО (ОсновныеСотрудники.ФизическоеЛицо = Сотрудники.Владелец)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|		ПО (СведенияОбАдресатах.Контакт = Сотрудники.Ссылка)
		|ГДЕ
		|	(Сотрудники.Владелец.Наименование ПОДОБНО &Фамилия
		|	ИЛИ Сотрудники.ПредставлениеВДокументах ПОДОБНО &Фамилия
		|	ИЛИ Сотрудники.ПредставлениеВПереписке ПОДОБНО &Фамилия)
		|	И Сотрудники.Действует
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Сотрудники.Ссылка,
		|	Сотрудники.ПредставлениеВДокументах,
		|	Сотрудники.ПредставлениеВПереписке,
		|	Адресаты.АдресатСообщения
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСотрудники КАК ОсновныеСотрудники
		|		ПО (ОсновныеСотрудники.ФизическоеЛицо = Сотрудники.Владелец)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Адресаты КАК Адресаты
		|		ПО Сотрудники.Ссылка = Адресаты.Ссылка
		|ГДЕ
		|	(Сотрудники.Владелец.Наименование ПОДОБНО &Фамилия
		|	ИЛИ Сотрудники.ПредставлениеВДокументах ПОДОБНО &Фамилия
		|	ИЛИ Сотрудники.ПредставлениеВПереписке ПОДОБНО &Фамилия)
		|	И Сотрудники.Действует";
		
		
	Запрос.УстановитьПараметр("Фамилия", "%" + Фамилия + "%");
	РезультатЗапроса = Запрос.Выполнить();
	МассивАдресатов = Новый Массив;
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат МассивАдресатов;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл 
			Если ЗначениеЗаполнено(Выборка.АдресатСообщения) Тогда 
				
				Если Строка(Выборка.Ссылка) = ФИО 
					Или Выборка.ПредставлениеВДокументах = ФИО 
					Или Выборка.ПредставлениеВПереписке = ФИО Тогда 
					МассивАдресатов = Новый Массив;
					МассивАдресатов.Добавить(Выборка.АдресатСообщения);
					Прервать;
				Иначе 
					МассивАдресатов.Добавить(Выборка.АдресатСообщения);
				КонецЕсли;
			Иначе 
				Адрес = ПолучитьОсновнойАдрес(Выборка.Ссылка);
				Если Не ЗначениеЗаполнено(Адрес) Тогда 
					Продолжить;
				КонецЕсли;
				
				Адресат = ПолучитьПочтовогоАдресата(Адрес, Строка(Выборка.Ссылка));
				
				Если Строка(Выборка.Ссылка) = ФИО 
					Или Выборка.ПредставлениеВДокументах = ФИО 
					Или Выборка.ПредставлениеВПереписке = ФИО Тогда 
					МассивАдресатов = Новый Массив;
					МассивАдресатов.Добавить(Адресат);
					Прервать;
				Иначе 
					МассивАдресатов.Добавить(Адресат);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат МассивАдресатов;
	
КонецФункции

// Возвращает основной адрес контакта
Функция ПолучитьОсновнойАдрес(Контакт) Экспорт  
	
	Если ТипЗнч(Контакт) = Тип("СправочникСсылка.Пользователи")
		Или ТипЗнч(Контакт) = Тип("СправочникСсылка.РолиИсполнителей") 
		Или ТипЗнч(Контакт) = Тип("СправочникСсылка.ФизическиеЛица") 
		Или ТипЗнч(Контакт) = Тип("СправочникСсылка.Контрагенты") 
		Или ТипЗнч(Контакт) = Тип("СправочникСсылка.КонтактныеЛица") 
		Или ТипЗнч(Контакт) = Тип("СправочникСсылка.ЛичныеАдресаты")
		Или ТипЗнч(Контакт) = Тип("СправочникСсылка.Сотрудники")  Тогда
	
		ТаблицаКонтактовEmail = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Контакт), 
			Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты,,
			ТекущаяДатаСеанса());
			
		Если ТаблицаКонтактовEmail.Количество() <> 0 Тогда
			Возврат ТаблицаКонтактовEmail[0].Представление;
		КонецЕсли;	
		
	КонецЕсли;
	
	Возврат "";
	
КонецФункции	

// Находит адресатов в тексте письма в строках вида From: От: От кого:
Функция НайтиАдресатовВТекстеПисьма(Письмо) Экспорт 
	
	АдресаПолучателей = Новый Массив;
	
	ПисьмоОбъект = Письмо.ПолучитьОбъект();
	ТекстПисьма = ПисьмоОбъект.ПолучитьТекстовоеПредставлениеСодержанияПисьма();
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(ТекстПисьма);
	
	КоличествоСтрок = ТекстовыйДокумент.КоличествоСтрок(); 
	Для Инд = 1 По КоличествоСтрок Цикл
		Строка = ТекстовыйДокумент.ПолучитьСтроку(КоличествоСтрок-Инд+1);
		
		Если Найти(Строка, "From:") > 0 Тогда 
			Поз = Найти(Строка, "From:");
			СтрокаАдресаПроверка = Сред(Строка, Поз + СтрДлина("From:"));
		ИначеЕсли Найти(Строка, "От:") > 0 Тогда 	
			Поз = Найти(Строка, "От:");
			СтрокаАдресаПроверка = Сред(Строка, Поз + СтрДлина("От:"));
		ИначеЕсли Найти(Строка, "От кого:") > 0 Тогда 	
			Поз = Найти(Строка, "От кого:");
			СтрокаАдресаПроверка = Сред(Строка, Поз + СтрДлина("От кого:"));
		Иначе	
			Продолжить;
		КонецЕсли;	
		
		СтрокаАдресаПроверка = СтрЗаменить(СтрокаАдресаПроверка, "mailto:", "");
		СтрокаАдресаПроверка = СтрЗаменить(СтрокаАдресаПроверка, "mailto", "");
		
		АдресИнфо = РаботаСоСтроками.РазложитьПредставлениеАдресаЭлектроннойПочты(СтрокаАдресаПроверка);
		Если ПустаяСтрока(АдресИнфо.Адрес) Тогда
			Продолжить;
		КонецЕсли;	
			
		ИмяАдресата = ?(ЗначениеЗаполнено(АдресИнфо.ОтображаемоеИмя), АдресИнфо.ОтображаемоеИмя, АдресИнфо.Адрес);
		Адресат = ВстроеннаяПочтаСервер.ПолучитьПочтовогоАдресата(АдресИнфо.Адрес, ИмяАдресата);
		
		Если АдресаПолучателей.Найти(Адресат) = Неопределено Тогда 
			АдресаПолучателей.Добавить(Адресат);
		КонецЕсли;	
		
	КонецЦикла;	
	
	УчетнаяЗапись = ПисьмоОбъект.УчетнаяЗапись;
	Если ТипЗнч(Письмо.Ссылка) = Тип("ДокументСсылка.ВходящееПисьмо") Тогда 
		ОтправительАдресат = ПисьмоОбъект.ОтправительАдресат;
		
		Если АдресаПолучателей.Найти(ОтправительАдресат) = Неопределено Тогда 
			АдресаПолучателей.Добавить(ОтправительАдресат);
		КонецЕсли;	
	КонецЕсли;	
		
	АдресатыВТекстеПисьма = Новый СписокЗначений;
	Для Каждого Адресат Из АдресаПолучателей Цикл
		Если Адресат.Адрес = УчетнаяЗапись.АдресЭлектроннойПочты Тогда 
			Продолжить;
		КонецЕсли;	
		
		ПредставлениеАдресата = ВстроеннаяПочтаСервер.ПолучитьПредставлениеИКонтактАдресата(Адресат).Представление;
		АдресатыВТекстеПисьма.Вставить(0, Адресат, ПредставлениеАдресата); 
	КонецЦикла;	
		
	Возврат АдресатыВТекстеПисьма;	
	
КонецФункции

// Групповая отправка писем

Функция ПолучитьДанныеПисемДляОтправкиИзСписка(Письма) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеПисем = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсходящееПисьмо.Ссылка КАК Ссылка,
	|	ИсходящееПисьмо.Тема КАК Тема,
	|	ИсходящееПисьмо.ТипОтвета КАК ТипОтвета,
	|	ИсходящееПисьмо.УчетнаяЗапись КАК УчетнаяЗапись,
	|	ИсходящееПисьмо.УчетнаяЗапись.ИспользоватьДляОтправки КАК УчетнаяЗаписьИспользоватьДляОтправки,
	|	ИсходящееПисьмо.УчетнаяЗапись.ВариантИспользования КАК УчетнаяЗаписьВариантИспользования,
	|	ИсходящееПисьмо.УчетнаяЗапись.ПометкаУдаления КАК УчетнаяЗаписьПометкаУдаления,
	|	ИсходящееПисьмо.ВидМаршрутизации КАК ВидМаршрутизации,
	|	ИсходящееПисьмо.ПисьмоОснование КАК ПисьмоОснование,
	|	ИсходящееПисьмо.Проект КАК Проект,
	|	ИсходящееПисьмо.ПолучателиПисьма.(
	|		Адресат КАК Адресат,
	|		Адресат.Наименование КАК Наименование,
	|		Адресат.Адрес КАК Адрес
	|	) КАК ПолучателиПисьма,
	|	ИсходящееПисьмо.ПолучателиСкрытыхКопий.(
	|		Адресат КАК Адресат,
	|		Адресат.Наименование КАК Наименование,
	|		Адресат.Адрес КАК Адрес
	|	) КАК ПолучателиСкрытыхКопий,
	|	ИсходящееПисьмо.ПолучателиКопий.(
	|		Адресат КАК Адресат,
	|		Адресат.Наименование КАК Наименование,
	|		Адресат.Адрес КАК Адрес
	|	) КАК ПолучателиКопий,
	|	ВЫБОР
	|		КОГДА ИСТИНА В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					ИСТИНА
	|				ИЗ
	|					Справочник.Файлы КАК Файлы
	|				ГДЕ
	|					Файлы.ВладелецФайла = ИсходящееПисьмо.Ссылка
	|					И НЕ Файлы.ПометкаУдаления
	|					И Файлы.Редактирует <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|					И Файлы.Редактирует <> ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
	|					И Файлы.Редактирует <> НЕОПРЕДЕЛЕНО)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПисьмоСодержитЗанятыеФайлы
	|ИЗ
	|	Документ.ИсходящееПисьмо КАК ИсходящееПисьмо
	|ГДЕ
	|	ИсходящееПисьмо.Ссылка В(&Письма)";
	
	Запрос.УстановитьПараметр("Письма", Письма);
	
	ПроверятьОтсутствие = Отсутствия.ПредупреждатьОбОтсутствии();
	АдресаДляПроверки = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДанныеПисьма = Новый Структура;
		ДанныеПисьма.Вставить("Ссылка", Выборка.Ссылка);
	    ДанныеПисьма.Вставить("Тема", Выборка.Тема);
		ДанныеПисьма.Вставить("ТипОтвета", Выборка.ТипОтвета);
		ДанныеПисьма.Вставить("УчетнаяЗапись", Выборка.УчетнаяЗапись);
		ДанныеПисьма.Вставить("УчетнаяЗаписьИспользоватьДляОтправки", Выборка.УчетнаяЗаписьИспользоватьДляОтправки);
		ДанныеПисьма.Вставить("УчетнаяЗаписьВариантИспользования", Выборка.УчетнаяЗаписьВариантИспользования);
		ДанныеПисьма.Вставить("УчетнаяЗаписьПометкаУдаления", Выборка.УчетнаяЗаписьПометкаУдаления);
		ДанныеПисьма.Вставить("ПисьмоОснование", Выборка.ПисьмоОснование);
		ДанныеПисьма.Вставить("Проект", Выборка.Проект);
		ДанныеПисьма.Вставить("ПроверятьОтсутствие", ПроверятьОтсутствие);
		ДанныеПисьма.Вставить("Получатели", Новый Массив);
		ДанныеПисьма.Вставить("ПисьмоСодержитЗанятыеФайлы", Выборка.ПисьмоСодержитЗанятыеФайлы);
			
		ВыборкаПолучатели = Выборка.ПолучателиПисьма.Выбрать();
		Пока ВыборкаПолучатели.Следующий() Цикл
			НоваяСтрока = Новый Структура;
			НоваяСтрока.Вставить("Адресат", ВыборкаПолучатели.Адресат);
			НоваяСтрока.Вставить("Представление", ВыборкаПолучатели.Наименование);
			НоваяСтрока.Вставить("Адрес", ВыборкаПолучатели.Адрес);
			НоваяСтрока.Вставить("ТипАдреса", Перечисления.ТипыАдресатов.Кому);
			НоваяСтрока.Вставить("Внутренний", Не ЭтоВнешнийАдресат(ВыборкаПолучатели.Адресат));
			НоваяСтрока.Вставить("Внешний", Не НоваяСтрока.Внутренний);
			НоваяСтрока.Вставить("Контакт", ПолучитьПользователяПоАдресату(ВыборкаПолучатели.Адресат));
			НоваяСтрока.Вставить("ЭтоАдресЭлектроннойПочты", 
				РаботаСоСтроками.ЭтоАдресЭлектроннойПочты(ВыборкаПолучатели.Адрес));
			
			ДанныеПисьма.Получатели.Добавить(НоваяСтрока);
			АдресаДляПроверки.Добавить(НоваяСтрока.Адрес);
		КонецЦикла;	
		
		ВыборкаПолучатели = Выборка.ПолучателиКопий.Выбрать();
		Пока ВыборкаПолучатели.Следующий() Цикл
			НоваяСтрока = Новый Структура;
			НоваяСтрока.Вставить("Адресат", ВыборкаПолучатели.Адресат);
			НоваяСтрока.Вставить("Представление", ВыборкаПолучатели.Наименование);
			НоваяСтрока.Вставить("Адрес", ВыборкаПолучатели.Адрес);
			НоваяСтрока.Вставить("ТипАдреса", Перечисления.ТипыАдресатов.Копия);
			НоваяСтрока.Вставить("Внутренний", Не ЭтоВнешнийАдресат(ВыборкаПолучатели.Адресат));
			НоваяСтрока.Вставить("Внешний", Не НоваяСтрока.Внутренний);
			НоваяСтрока.Вставить("Контакт", ПолучитьПользователяПоАдресату(ВыборкаПолучатели.Адресат));
			НоваяСтрока.Вставить("ЭтоАдресЭлектроннойПочты", 
				РаботаСоСтроками.ЭтоАдресЭлектроннойПочты(ВыборкаПолучатели.Адрес));
			
			ДанныеПисьма.Получатели.Добавить(НоваяСтрока);
			АдресаДляПроверки.Добавить(НоваяСтрока.Адрес);
		КонецЦикла;
		
		ВыборкаПолучатели = Выборка.ПолучателиСкрытыхКопий.Выбрать();
		Пока ВыборкаПолучатели.Следующий() Цикл
			НоваяСтрока = Новый Структура;
			НоваяСтрока.Вставить("Адресат", ВыборкаПолучатели.Адресат);
			НоваяСтрока.Вставить("Представление", ВыборкаПолучатели.Наименование);
			НоваяСтрока.Вставить("Адрес", ВыборкаПолучатели.Адрес);
			НоваяСтрока.Вставить("ТипАдреса", Перечисления.ТипыАдресатов.СкрытаяКопия);
			НоваяСтрока.Вставить("Внутренний", Не ЭтоВнешнийАдресат(ВыборкаПолучатели.Адресат));
			НоваяСтрока.Вставить("Внешний", Не НоваяСтрока.Внутренний);
			НоваяСтрока.Вставить("Контакт", ПолучитьПользователяПоАдресату(ВыборкаПолучатели.Адресат));
			НоваяСтрока.Вставить("ЭтоАдресЭлектроннойПочты", 
				РаботаСоСтроками.ЭтоАдресЭлектроннойПочты(ВыборкаПолучатели.Адрес));
			
			ДанныеПисьма.Получатели.Добавить(НоваяСтрока);
			АдресаДляПроверки.Добавить(НоваяСтрока.Адрес);
		КонецЦикла;
		
		ОригинальноеПисьмоВФорматеICalendar = Ложь;	
		ПисьмоОснование = Выборка.ПисьмоОснование;
		Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(ПисьмоОснование) Тогда
			ContentType = ВстроеннаяПочтаСервер.ПолучитьЗначениеПоляИзЗаголовкаПисьма(ПисьмоОснование.ВнутреннийЗаголовок, "Content-Type");
			Если Найти(НРег(ContentType), "text/calendar") > 0 Тогда 
				СодержаниеПисьма = ПисьмоОснование.ПолучитьОбъект().ПолучитьСодержаниеПисьма();
				ТекстHTML = СодержаниеПисьма.Текст;
				Если Найти(ТекстHTML, "BEGIN:VCALENDAR") <> 0 Тогда
					ОригинальноеПисьмоВФорматеICalendar = Истина;
				КонецЕсли;	
			КонецЕсли;	
		КонецЕсли;
		ДанныеПисьма.Вставить("ОригинальноеПисьмоВФорматеICalendar", ОригинальноеПисьмоВФорматеICalendar);
		
		ТекстОшибкиПредельныйРазмерПисьма = "";
		ПроверитьПредельныйРазмерПисьма = ВстроеннаяПочтаСервер.ПроверитьПредельныйРазмерПисьма(
			Выборка.Ссылка, ТекстОшибкиПредельныйРазмерПисьма);
		ДанныеПисьма.Вставить("ПроверитьПредельныйРазмерПисьма", ПроверитьПредельныйРазмерПисьма);
		ДанныеПисьма.Вставить("ТекстОшибкиПредельныйРазмерПисьма", ТекстОшибкиПредельныйРазмерПисьма);
		
		ДанныеПисем.Добавить(ДанныеПисьма);
	КонецЦикла;	
	
	Возврат ДанныеПисем;
	
КонецФункции	

Функция ПроверитьПредельныйРазмерПисьма(Письмо, ТекстОшибки = "") Экспорт 
	
	РеквизитыПисьма = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Письмо, 
		"ВидМаршрутизации, 
		|ТипТекста, 
		|ТекстХранилище, 
		|ТекстПисьмаHTMLХранилище");
	
	Если РеквизитыПисьма.ВидМаршрутизации = Перечисления.ВидыМаршрутизацииПисем.Внутренняя Тогда
		Возврат Истина;
	КонецЕсли;	
			
	РазмерТекстаБайт = 0;
	Если РеквизитыПисьма.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		РазмерТекстаБайт = СтрДлина(РеквизитыПисьма.ТекстПисьмаHTMLХранилище.Получить());
	Иначе	
		РазмерТекстаБайт = СтрДлина(РеквизитыПисьма.ТекстХранилище.Получить());
	КонецЕсли;	
	РазмерТекстаМБ = РазмерТекстаБайт / (1024*1024);
	
	МаксимальныйРазмерТекста = 3; // 3 МБ
	Если РазмерТекстаМБ > МаксимальныйРазмерТекста Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Размер содержимого письма (%1 Мб) превышает максимально допустимый размер (%2 Мб). 
			|Удалите лишний текст письма.'"),
			ФайловыеФункцииКлиентСервер.ПолучитьСтрокуСРазмеромФайла(РазмерТекстаМБ), 
			ФайловыеФункцииКлиентСервер.ПолучитьСтрокуСРазмеромФайла(МаксимальныйРазмерТекста));
		Возврат Ложь;
	КонецЕсли;	
	
	МаксимальныйРазмерПисьма = ВстроеннаяПочтаСервер.ПолучитьМаксимальныйРазмерВнешнегоИсходящегоПисьма();
	Если МаксимальныйРазмерПисьма = 0 Тогда 
		Возврат Истина;
	КонецЕсли;	
	
	РазмерПисьмаБайт = 0;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(СУММА(Файлы.ТекущаяВерсияРазмер), 0) КАК ТекущаяВерсияРазмер
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|ГДЕ
		|	Файлы.ВладелецФайла = &Письмо
		|	И НЕ Файлы.ПометкаУдаления";
	Запрос.УстановитьПараметр("Письмо", Письмо);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		РазмерПисьмаБайт = Выборка.ТекущаяВерсияРазмер;
	КонецЕсли;	
	РазмерПисьмаБайт = РазмерПисьмаБайт + РазмерТекстаБайт;
	РазмерПисьмаМБ = РазмерПисьмаБайт / (1024*1024);
	
	Если РазмерПисьмаМБ > МаксимальныйРазмерПисьма Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Размер письма (%1 Мб) превышает максимально допустимый размер (%2 Мб). 
			|Удалите слишком большие вложения.'"),
			ФайловыеФункцииКлиентСервер.ПолучитьСтрокуСРазмеромФайла(РазмерПисьмаМБ), 
			ФайловыеФункцииКлиентСервер.ПолучитьСтрокуСРазмеромФайла(МаксимальныйРазмерПисьма));
		Возврат Ложь;
	КонецЕсли;	
		
	Возврат Истина;
	
КонецФункции	

Функция ПересылкаВнутреннегоПисьма(Письмо) Экспорт 
	
	ПересылкаВнутреннегоПисьма = Ложь;
	Если ЗначениеЗаполнено(Письмо.ПисьмоОснование) И 
		ВстроеннаяПочтаКлиентСервер.ЭтоПисьмо(Письмо.ПисьмоОснование) И
		(Письмо.ТипОтвета = Перечисления.ТипыОтвета.ОтветНаПисьмо Или 
		 Письмо.ТипОтвета = Перечисления.ТипыОтвета.ПересылкаПисьма Или 
		 Письмо.ТипОтвета = Перечисления.ТипыОтвета.ПеренаправлениеПисьма)
		 И ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(Письмо.ПисьмоОснование).Чтение Тогда
		 
		ВидМаршрутизацииОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Письмо.ПисьмоОснование, "ВидМаршрутизации");
		
		ПересылкаВнутреннегоПисьма = Истина;
		
		Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(Письмо.ПисьмоОснование) Тогда 
			ПересылкаВнутреннегоПисьма = Не ЕстьВнешнийАдресатУВходящегоПисьма(Письмо.ПисьмоОснование);
		ИначеЕсли ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(Письмо.ПисьмоОснование) Тогда 	
			ПересылкаВнутреннегоПисьма = Не ЕстьВнешнийАдресатУИсходящегоПисьма(Письмо.ПисьмоОснование);
		КонецЕсли;	
		
	КонецЕсли;
	
	Возврат ПересылкаВнутреннегоПисьма;
	
КонецФункции

Функция ЕстьВнешнийАдресатУВходящегоПисьма(ПисьмоОснование) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВходящееПисьмо.ОтправительАдресат КАК Адресат
		|ИЗ
		|	Документ.ВходящееПисьмо КАК ВходящееПисьмо
		|ГДЕ
		|	ВходящееПисьмо.Ссылка = &Письмо
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВходящееПисьмоПолучателиПисьма.Адресат
		|ИЗ
		|	Документ.ВходящееПисьмо.ПолучателиПисьма КАК ВходящееПисьмоПолучателиПисьма
		|ГДЕ
		|	ВходящееПисьмоПолучателиПисьма.Ссылка = &Письмо
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВходящееПисьмоПолучателиКопий.Адресат
		|ИЗ
		|	Документ.ВходящееПисьмо.ПолучателиКопий КАК ВходящееПисьмоПолучателиКопий
		|ГДЕ
		|	ВходящееПисьмоПолучателиКопий.Ссылка = &Письмо";
		
	Запрос.УстановитьПараметр("Письмо", ПисьмоОснование);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ЭтоВнешнийАдресат(Выборка.Адресат) Тогда
			Возврат Истина;
		КонецЕсли;	
		
	КонецЦикла;	
	
	Возврат Ложь;
	
КонецФункции	

Функция ЕстьВнешнийАдресатУИсходящегоПисьма(ПисьмоОснование) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсходящееПисьмоПолучателиПисьма.Адресат КАК Адресат
		|ИЗ
		|	Документ.ИсходящееПисьмо.ПолучателиПисьма КАК ИсходящееПисьмоПолучателиПисьма
		|ГДЕ
		|	ИсходящееПисьмоПолучателиПисьма.Ссылка = &Письмо
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИсходящееПисьмоПолучателиКопий.Адресат
		|ИЗ
		|	Документ.ИсходящееПисьмо.ПолучателиКопий КАК ИсходящееПисьмоПолучателиКопий
		|ГДЕ
		|	ИсходящееПисьмоПолучателиКопий.Ссылка = &Письмо";
		
		
	Запрос.УстановитьПараметр("Письмо", ПисьмоОснование);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ЭтоВнешнийАдресат(Выборка.Адресат) Тогда
			Возврат Истина;
		КонецЕсли;	
		
	КонецЦикла;	
	
	Возврат Ложь;
	
КонецФункции	

Процедура ОтправитьПисьмаПриГрупповойОтправке(Письма, ОтправленныеПисьма, СписокОшибок) Экспорт 
	
	ИдентификаторСеанса = Новый УникальныйИдентификатор;
	ПорядковыйНомерСобытия = 0;
	
	Для Каждого Письмо Из Письма Цикл
		
		НачатьТранзакцию();
		Попытка
		
		ЗаблокироватьДанныеДляРедактирования(Письмо);
		
		ПисьмоОбъект = Письмо.ПолучитьОбъект();
		ПисьмоОбъект.ПодготовленоКОтправке = ТекущаяДатаСеанса();
		ПисьмоОбъект.Дата = ТекущаяДатаСеанса();
		ПисьмоОбъект.ОтправкаОтменена = Ложь;
		ПисьмоОбъект.ДополнительныеСвойства.Вставить("ВыполняетсяОтправка", Истина);
		ПисьмоОбъект.Записать();
		
		Если ЗначениеЗаполнено(ПисьмоОбъект.ПисьмоОснование) Тогда
			ПредЗначениеПривилегированныйРежим = ПривилегированныйРежим();
			УстановитьПривилегированныйРежим(Истина); // При отправке письма связи пишем в привилег режиме
		
			Если ПисьмоОбъект.ТипОтвета = Перечисления.ТипыОтвета.ОтветНаПисьмо Тогда
			
				СвязиОбъектов.УстановитьСвязь(
					ПисьмоОбъект.Ссылка,
					Неопределено,
					ПисьмоОбъект.ПисьмоОснование,
					Справочники.ТипыСвязей.ПисьмоОтправленоВОтветНа);
				
			ИначеЕсли ПисьмоОбъект.ТипОтвета = Перечисления.ТипыОтвета.ПересылкаПисьма Тогда
				
				СвязиОбъектов.УстановитьСвязь(
					ПисьмоОбъект.Ссылка,
					Неопределено,
					ПисьмоОбъект.ПисьмоОснование,
					Справочники.ТипыСвязей.ПересылкаПисьма);	
				
			ИначеЕсли ПисьмоОбъект.ТипОтвета = Перечисления.ТипыОтвета.ПеренаправлениеПисьма Тогда
				
				СвязиОбъектов.УстановитьСвязь(
					ПисьмоОбъект.Ссылка,
					Неопределено,
					ПисьмоОбъект.ПисьмоОснование,
					Справочники.ТипыСвязей.ПеренаправлениеПисьма);
				
			КонецЕсли;
			
			УстановитьПривилегированныйРежим(ПредЗначениеПривилегированныйРежим);
		КонецЕсли;
		
		ВстроеннаяПочтаСервер.ЗаписатьПротоколДоставкиПочты(
			ПисьмоОбъект.Ссылка,
			"",
			Ложь,
			Перечисления.ТипыСобытийДоставкиПочты.ИнтерактивнаяОтправкаПисьма,
			ПисьмоОбъект.УчетнаяЗапись,
			ПорядковыйНомерСобытия,
			ИдентификаторСеанса);
			
			ВстроеннаяПочтаСервер.ПрименитьПравила(ПисьмоОбъект);
		
			РазблокироватьДанныеДляРедактирования(Письмо);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			РазблокироватьДанныеДляРедактирования(Письмо);
			
			ПараметрыЛогирования = Новый Структура("ИдентификаторСеанса, ПорядковыйНомерСобытия, НомерЗадания",
				ИдентификаторСеанса, ПорядковыйНомерСобытия, 0);
			
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Произошла ошибка при групповой отправке письма:
				|%1'"),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
			ЗаписатьОшибкуДоставки(
				СообщениеОбОшибке,
				Письмо,
				Письмо.УчетнаяЗапись,
				ПараметрыЛогирования,
				Перечисления.ТипыСобытийДоставкиПочты.ИнтерактивнаяОтправкаПисьма);

			СписокОшибок.Добавить(Письмо, СообщениеОбОшибке);	
				
			Продолжить;
				
		КонецПопытки;
		
		ОтправленныеПисьма.Добавить(Письмо);
		
	КонецЦикла;
	
КонецПроцедуры	

// Групповая отправка писем - конец

// По переданной группе пользователей, контрагентов, физлиц, личных адресатов, группе Мои контакты, Отделу
// заполняет массив структур (Адрес  Группа Контакт	Представление)
//  Работает рекурсивно
//
// Параметры
// Группа - входной параметр - группа пользователей, контрагентов, физлиц, 
//		личных адресатов, групп Мои контакты, Отдел
// АдресатыГруппы - выходной параметр, массив структур(Адрес  Группа Контакт Представление)
// ИспользованныеАдреса - Соответствие - для проверки уникальности адресов
Процедура РазвернутьГруппуВАдресаты(Группа, АдресатыГруппы, ИспользованныеАдреса) Экспорт
	
	ТипАдресаКому = Перечисления.ТипыАдресатов.Кому;
	
	Если ТипЗнч(Группа) = Тип("СправочникСсылка.РабочиеГруппы") Тогда
		
		Запрос = Новый Запрос;
		
		Если Группа = ПредопределенноеЗначение("Справочник.РабочиеГруппы.ВсеПользователи") Тогда
		
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	Сотрудники.Ссылка КАК Ссылка,
				|	Сотрудники.Наименование КАК Наименование
				|ИЗ
				|	Справочник.Сотрудники КАК Сотрудники
				|ГДЕ
				|	Сотрудники.ПометкаУдаления = ЛОЖЬ
				|	И Сотрудники.Действует = ИСТИНА
				|
				|УПОРЯДОЧИТЬ ПО
				|	Наименование";
		Иначе
				
			Запрос.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	РабочиеГруппы.Ссылка КАК Ссылка
				|ПОМЕСТИТЬ РабочиеГруппыВременная
				|ИЗ
				|	Справочник.РабочиеГруппы КАК РабочиеГруппы
				|ГДЕ
				|	РабочиеГруппы.Ссылка В ИЕРАРХИИ (&Родитель)
				|
				|ИНДЕКСИРОВАТЬ ПО
				|	Ссылка
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	РабочиеГруппыСостав.Участник КАК Ссылка,
				|	РабочиеГруппыСостав.Участник.Наименование КАК Наименование
				|ИЗ
				|	Справочник.РабочиеГруппы.Состав КАК РабочиеГруппыСостав
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РабочиеГруппыВременная КАК РабочиеГруппыВременная
				|		ПО РабочиеГруппыСостав.Ссылка = РабочиеГруппыВременная.Ссылка
				|ГДЕ
				|	РабочиеГруппыСостав.Участник.Действует
				|	И РабочиеГруппыСостав.Участник.ПометкаУдаления = ЛОЖЬ
				|
				|УПОРЯДОЧИТЬ ПО
				|	Наименование";
				
			Запрос.УстановитьПараметр("Родитель", Группа);		
			
		КонецЕсли;		
		
		Если Группа = ПредопределенноеЗначение("Справочник.РабочиеГруппы.ВсеПользователи") Тогда
			УстановитьПривилегированныйРежим(Истина);
		КонецЕсли;	
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Адрес = ПолучитьОсновнойАдрес(Выборка.Ссылка);
			Если ЗначениеЗаполнено(Адрес) И ИспользованныеАдреса.Получить(Адрес) = Неопределено Тогда
				
				ГруппаТипАдреса = ТипАдресаКому;
				
				Наименование = Выборка.Наименование + " <" + Адрес + ">";
				
				Контакт = Новый Структура("Адрес, Представление, Контакт, Группа", 
					Адрес, Наименование, Выборка.Ссылка, ГруппаТипАдреса);
				АдресатыГруппы.Добавить(Контакт);
				ИспользованныеАдреса.Вставить(Адрес, 1);
				
			КонецЕсли;		
				
		КонецЦикла;
		
		Если Группа = ПредопределенноеЗначение("Справочник.РабочиеГруппы.ВсеПользователи") Тогда
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли;	
		
	ИначеЕсли ТипЗнч(Группа) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Сотрудники.Ссылка КАК Ссылка,
			|	ВЫРАЗИТЬ(Сотрудники.ПредставлениеВПереписке КАК Строка(105)) КАК Наименование
			|ИЗ
			|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
			|		ПО СтруктураПредприятия.Ссылка = Сотрудники.Подразделение
			|ГДЕ
			|	СтруктураПредприятия.Ссылка В ИЕРАРХИИ (&Родитель)
			|		И НЕ Сотрудники.ПометкаУдаления
			|		И Сотрудники.Действует
			|УПОРЯДОЧИТЬ ПО
			|	Наименование";
			
		Запрос.УстановитьПараметр("Родитель", Группа);		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Адрес = ПолучитьОсновнойАдрес(Выборка.Ссылка);
			Если ЗначениеЗаполнено(Адрес) И ИспользованныеАдреса.Получить(Адрес) = Неопределено Тогда
				
				ГруппаТипАдреса = ТипАдресаКому;
				
				Наименование = Выборка.Наименование + " <" + Адрес + ">";
				
				Контакт = Новый Структура("Адрес, Представление, Контакт, Группа", 
					Адрес, Наименование, Выборка.Ссылка, ГруппаТипАдреса);
				АдресатыГруппы.Добавить(Контакт);
				ИспользованныеАдреса.Вставить(Адрес, 1);
				
			КонецЕсли;		
				
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Группа) = Тип("СправочникСсылка.ГруппыЛичныхАдресатов") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ЛичныеАдресаты.Ссылка,
			|	ЛичныеАдресаты.Наименование КАК Наименование
			|ИЗ
			|	Справочник.ЛичныеАдресаты КАК ЛичныеАдресаты
			|ГДЕ
			|	ЛичныеАдресаты.Группа = &Родитель
			|	И ЛичныеАдресаты.ПометкаУдаления = ЛОЖЬ
			|
			|УПОРЯДОЧИТЬ ПО
			|	Наименование";
			
		Запрос.УстановитьПараметр("Родитель", Группа);		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Адрес = ПолучитьОсновнойАдрес(Выборка.Ссылка);
			Если ЗначениеЗаполнено(Адрес) И ИспользованныеАдреса.Получить(Адрес) = Неопределено Тогда
				
				ГруппаТипАдреса = ТипАдресаКому;
				
				Контакт = Новый Структура("Адрес, Представление, Контакт, Группа", 
					Адрес, Выборка.Наименование, Выборка.Ссылка, ГруппаТипАдреса);
				АдресатыГруппы.Добавить(Контакт);
				ИспользованныеАдреса.Вставить(Адрес, 1);
				
			КонецЕсли;		
			
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Группа) = Тип("СправочникСсылка.ГруппыКонтактовПользователей") Тогда
		
		Для Каждого Строка Из Группа.Контакты Цикл
			
			Адрес = Строка.КонтактнаяИнформация;
			Если ЗначениеЗаполнено(Адрес) И ИспользованныеАдреса.Получить(Адрес) = Неопределено Тогда
				
				ГруппаТипАдреса = ТипАдресаКому;
				
				Контакт = Новый Структура("Адрес, Представление, Контакт, Группа", 
					Адрес, Строка.Представление, Строка.Контакт, ГруппаТипАдреса);
				АдресатыГруппы.Добавить(Контакт);
				ИспользованныеАдреса.Вставить(Адрес, 1);
				
			ИначеЕсли ЗначениеЗаполнено(Строка.Контакт)	Тогда
				
				Если 	ТипЗнч(Строка.Контакт) = Тип("СправочникСсылка.РабочиеГруппы")
					Или ТипЗнч(Строка.Контакт) = Тип("СправочникСсылка.СтруктураПредприятия")
					Или ТипЗнч(Строка.Контакт) = Тип("СправочникСсылка.ГруппыЛичныхАдресатов")
					Или ТипЗнч(Строка.Контакт) = Тип("СправочникСсылка.Контрагенты")
					Или ТипЗнч(Строка.Контакт) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
	
					РазвернутьГруппуВАдресаты(Строка.Контакт, АдресатыГруппы, ИспользованныеАдреса);
					
				КонецЕсли;	
				
			КонецЕсли;		
			
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Группа) = Тип("СправочникСсылка.Контрагенты") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Контрагенты.Ссылка,
			|	Контрагенты.Наименование КАК Наименование
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.Родитель В ИЕРАРХИИ(&Родитель)
			|	И Контрагенты.ЭтоГруппа = ЛОЖЬ
			|	И Контрагенты.ПометкаУдаления = ЛОЖЬ
			|
			|УПОРЯДОЧИТЬ ПО
			|	Наименование";
			
		Запрос.УстановитьПараметр("Родитель", Группа);	
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Адрес = ПолучитьОсновнойАдрес(Выборка.Ссылка);
			Если ЗначениеЗаполнено(Адрес) И ИспользованныеАдреса.Получить(Адрес) = Неопределено Тогда
				
				ГруппаТипАдреса = ТипАдресаКому;
				
				Контакт = Новый Структура("Адрес, Представление, Контакт, Группа", 
					Адрес, Выборка.Наименование, Выборка.Ссылка, ГруппаТипАдреса);
				АдресатыГруппы.Добавить(Контакт);
				ИспользованныеАдреса.Вставить(Адрес, 1);
				
			КонецЕсли;	
			
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Группа) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ФизическиеЛица.Ссылка,
			|	ФизическиеЛица.Наименование КАК Наименование
			|ИЗ
			|	Справочник.ФизическиеЛица КАК ФизическиеЛица
			|ГДЕ
			|	ФизическиеЛица.Родитель В ИЕРАРХИИ(&Родитель)
			|	И ФизическиеЛица.ЭтоГруппа = ЛОЖЬ
			|	И ФизическиеЛица.ПометкаУдаления = ЛОЖЬ
			|
			|УПОРЯДОЧИТЬ ПО
			|	Наименование";
			
		Запрос.УстановитьПараметр("Родитель", Группа);	
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Адрес = ПолучитьОсновнойАдрес(Выборка.Ссылка);
			Если ЗначениеЗаполнено(Адрес) И ИспользованныеАдреса.Получить(Адрес) = Неопределено Тогда
				
				ГруппаТипАдреса = ТипАдресаКому;
				
				Контакт = Новый Структура("Адрес, Представление, Контакт, Группа", 
					Адрес, Выборка.Наименование, Выборка.Ссылка, ГруппаТипАдреса);
				АдресатыГруппы.Добавить(Контакт);
				ИспользованныеАдреса.Вставить(Адрес, 1);
				
			КонецЕсли;		
				
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры	

// Помечает на удаления письма в папках, для которых настроена автоочистка.
//
Процедура ОчисткаПапокОтСтарыхПисем() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Очистка папок от старых писем'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,,,
		НСтр("ru = 'Начало выполнения'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
	
	Настройки = РегистрыСведений.НастройкиАвтоочисткиПапокПисем.Выбрать();
	Пока Настройки.Следующий() Цикл
		Запрос = Новый Запрос;
		
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1000
			|	ЭлектроннаяПочта.Ссылка КАК Письмо
			|ИЗ
			|	ЖурналДокументов.ЭлектроннаяПочта КАК ЭлектроннаяПочта
			|ГДЕ
			|	ЭлектроннаяПочта.ПометкаУдаления = ЛОЖЬ
			|	И ЭлектроннаяПочта.Папка = &Папка
			|	И ЭлектроннаяПочта.Дата < &ДатаУстаревания
			|
			|УПОРЯДОЧИТЬ ПО
			|	ЭлектроннаяПочта.Дата";
		Запрос.УстановитьПараметр("Папка", Настройки.Папка);
		ДатаУстаревания = ТекущаяДатаСеанса() - Настройки.ПериодУстаревания * 24 * 60 * 60;
		Запрос.УстановитьПараметр("ДатаУстаревания", ДатаУстаревания);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Попытка
				
				ЗаблокироватьДанныеДляРедактирования(Выборка.Письмо);
				ПисьмоОбъект = Выборка.Письмо.ПолучитьОбъект();
				ПисьмоОбъект.УстановитьПометкуУдаления(Истина);
				РазблокироватьДанныеДляРедактирования(Выборка.Письмо);
				
			Исключение
				ЗаписьЖурналаРегистрации(
					НСтр("ru = 'Очистка папок от старых писем'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
					УровеньЖурналаРегистрации.Ошибка,,,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
				
			КонецЦикла;
			
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Очистка папок от старых писем'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,,,
		НСтр("ru = 'Завершение'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
	
КонецПроцедуры

// Возвращает номер способа адресации письма
// Параметры:
//  Письмо - Входящее письмо - объект - письмо, для которого определяется способ адресации.
//
// Возвращаемое значение:
//  0 - не удалось определить способ адресации
//  1 - получатель письма находится в списке "Кому"
//  2 - получатель письма находится в списке "Копии"
//  3 - письмо получено в качестве слепой копии
//
Функция ПолучитьСпособАдресацииОбъекта(Письмо) Экспорт
	
	Адрес = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Письмо.УчетнаяЗапись, "АдресЭлектроннойПочты");
	Адрес = НРег(Адрес);
	
	Если Не ЗначениеЗаполнено(Адрес) Тогда 
		Возврат 0;
	КонецЕсли;
	
	Для Каждого Строка Из Письмо.ПолучателиПисьма Цикл
		Если НРег(Строка.Адресат.Адрес) = Адрес Тогда
			Возврат 1;
		КонецЕсли;	
	КонецЦикла;	
	
	Для Каждого Строка Из Письмо.ПолучателиКопий Цикл
		Если НРег(Строка.Адресат.Адрес) = Адрес Тогда
			Возврат 2;
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат 3;

КонецФункции

// Возвращает номер способа адресации письма
// Параметры:
//  Письмо - Входящее письмо - письмо, для которого определяется способ адресации.
//
// Возвращаемое значение:
//  0 - не удалось определить способ адресации
//  1 - получатель письма находится в списке "Кому"
//  2 - получатель письма находится в списке "Копии"
//  3 - письмо получено в качестве слепой копии
//
Функция ПолучитьСпособАдресации(Письмо) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВходящееПисьмоПолучателиПисьма.Адресат,
		|	1 КАК НомерСпособаАдресации
		|ИЗ
		|	Документ.ВходящееПисьмо.ПолучателиПисьма КАК ВходящееПисьмоПолучателиПисьма
		|ГДЕ
		|	ВходящееПисьмоПолучателиПисьма.Ссылка = &Письмо
		|	И ВходящееПисьмоПолучателиПисьма.Адресат.Адрес = &Адрес
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВходящееПисьмоПолучателиКопий.Адресат,
		|	2
		|ИЗ
		|	Документ.ВходящееПисьмо.ПолучателиКопий КАК ВходящееПисьмоПолучателиКопий
		|ГДЕ
		|	ВходящееПисьмоПолучателиКопий.Ссылка = &Письмо
		|	И ВходящееПисьмоПолучателиКопий.Адресат.Адрес = &Адрес
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВходящееПисьмоПолучателиОтвета.Адресат,
		|	4
		|ИЗ
		|	Документ.ВходящееПисьмо.ПолучателиОтвета КАК ВходящееПисьмоПолучателиОтвета
		|ГДЕ
		|	ВходящееПисьмоПолучателиОтвета.Ссылка = &Письмо
		|	И ВходящееПисьмоПолучателиОтвета.Адресат.Адрес = &Адрес";
			
	Адрес = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Письмо.УчетнаяЗапись, "АдресЭлектроннойПочты");
	
	Если Не ЗначениеЗаполнено(Адрес) Тогда 
		Возврат 0;
	КонецЕсли;
	
	Запрос.Параметры.Вставить("Письмо", Письмо);
	Запрос.Параметры.Вставить("Адрес", Адрес);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();

	Если Выборка.Количество() = 0 Тогда 
		Возврат 3;
	Иначе 	
		Возврат Выборка.НомерСпособаАдресации;
	КонецЕсли;	

КонецФункции

// Устанавливает пометку удаления на все письма указанной папки.
//
// Параметры:
//  Папка - СправочникСсылка.ПапкиПисем - папка, письма которой нужно пометить на удаление.
//
Процедура ПометитьВсеПисьмаВПапкеНаУдаление(Папка) Экспорт
	
	// Выборка всех писем в указанной папке 
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЭлектроннаяПочта.Ссылка КАК Письмо
		|ИЗ
		|	ЖурналДокументов.ЭлектроннаяПочта КАК ЭлектроннаяПочта
		|ГДЕ
		|	ЭлектроннаяПочта.Папка = &Папка
		|	И НЕ ЭлектроннаяПочта.ПометкаУдаления";
		
	Запрос.УстановитьПараметр("Папка", Папка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			
			ЗаблокироватьДанныеДляРедактирования(Выборка.Письмо);
			ПисьмоОбъект = Выборка.Письмо.ПолучитьОбъект();
			ПисьмоОбъект.УстановитьПометкуУдаления(Истина);
			РазблокироватьДанныеДляРедактирования(Выборка.Письмо);
			
		Исключение
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'ПометитьВсеПисьмаВПапкеНаУдаление'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,,,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
				
	КонецЦикла;
	
КонецПроцедуры

// Получает по адресу и представлению структуру с полями Контакт Адресат Адрес Внешний ВидМаршрутизации
Функция ПолучитьПараметрыАдресатаПоАдресуИПредставлению(Адрес, Представление) Экспорт
	
	Адресат = ПолучитьПочтовогоАдресата(Адрес, Представление);
	СтруктураАдресата = ПолучитьПредставлениеИКонтактАдресата(Адресат, Истина);
	
	ОписаниеАдресата = Новый Структура("Контакт, Адресат, Представление, Адрес, Внешний, ВидМаршрутизации",
		СтруктураАдресата.Контакт, Адресат, Представление, Адрес, 
		СтруктураАдресата.Внешний, СтруктураАдресата.ВидМаршрутизации);
		
	Возврат ОписаниеАдресата;	
	
КонецФункции	

// Переносит все письма из папки источника в папку приемник.
//
// Параметры:
//  ПапкаИсточник - СправочникСсылка.ПапкиПисем - папка, письма которой нужно перенести.
//  ПапкаПриемник - СправочникСсылка.ПапкиПисем - папка, в которую переносим письма.
//
Процедура ПеренестиВсеПисьмаВПапку(ПапкаИсточник, ПапкаПриемник) Экспорт
	
	// Выборка всех писем в указанной папке 
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЭлектроннаяПочта.Ссылка КАК Письмо
		|ИЗ
		|	ЖурналДокументов.ЭлектроннаяПочта КАК ЭлектроннаяПочта
		|ГДЕ
		|	ЭлектроннаяПочта.Папка = &Папка";
		
	Запрос.УстановитьПараметр("Папка", ПапкаИсточник);
	ТаблицаПисьма = Запрос.Выполнить().Выгрузить();
	Письма = ТаблицаПисьма.ВыгрузитьКолонку("Письмо");
	
	РезультатПереноса = ПоместитьПисьмаВПапку(Письма, ПапкаПриемник);
	
	Если ТипЗнч(РезультатПереноса.Ошибки) = Тип("Массив") 
		И РезультатПереноса.Ошибки.Количество() <> 0 Тогда
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Нет прав на изменение писем в папке ""%1"".'"), 
			Строка(ПапкаИсточник));
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
КонецПроцедуры

// Переносит все письма из папки источника в корзину.
//
// Параметры:
//  ПапкаИсточник - СправочникСсылка.ПапкиПисем - папка, письма которой нужно перенести.
//
Процедура ПеренестиВсеПисьмаВКорзину(ПапкаИсточник) Экспорт
	
	// Выборка всех писем в указанной папке 
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЭлектроннаяПочта.Ссылка КАК Письмо
		|ИЗ
		|	ЖурналДокументов.ЭлектроннаяПочта КАК ЭлектроннаяПочта
		|ГДЕ
		|	ЭлектроннаяПочта.Папка = &Папка";
		
	Запрос.УстановитьПараметр("Папка", ПапкаИсточник);
	ТаблицаПисьма = Запрос.Выполнить().Выгрузить();
	Письма = ТаблицаПисьма.ВыгрузитьКолонку("Письмо");
	
	РезультатПереноса = ПоместитьПисьмаВКорзину(Письма);
	
	Если РезультатПереноса = Ложь Тогда 
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Нет прав на изменение писем в папке ""%1"".'"), 
			Строка(ПапкаИсточник));
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
КонецПроцедуры

// Получает msg файл письма
Функция ПолучитьДвоичныеДанныеФайлаПисьма(Письмо) Экспорт
	
	ЭтоВложенноеПисьмо = Истина;
	ПараметрыОтправкиВложенногоПисьма 
		= ПолучитьСтруктуруПараметровОтправкиПисьма(Письмо, , ЭтоВложенноеПисьмо);
	
	ИнтернетПочтовоеСообщение = Новый ИнтернетПочтовоеСообщение;
	Почта.ЗаполнитьИнтернетПочтовоеСообщение(ИнтернетПочтовоеСообщение, ПараметрыОтправкиВложенногоПисьма);
	
	ДвДанные = ИнтернетПочтовоеСообщение.ПолучитьИсходныеДанные();
	Возврат ДвДанные;

КонецФункции	

// Определяет количество новых писем для пользователя.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи.
// 
// Возвращаемое значение:
//  Число - Количество новых писем пользователя.
//
Функция ВсегоНовыхПисем(Пользователь) Экспорт
	
	ВсегоНовыхПисем = 0;
	Для Каждого ВыбраннаяПапка Из ПапкиПисемВиджетаПочта(Пользователь) Цикл
		ВсегоНовыхПисем = ВсегоНовыхПисем
			+ ПолучитьКоличествоПисемВПапке(
				ВыбраннаяПапка,
				Перечисления.ВариантыОтображенияКоличестваПисемВПапке.Непрочтенные,
				Пользователь);
	КонецЦикла;
	
	Возврат ВсегоНовыхПисем;
	
КонецФункции

// Определяет последние новые письма.
// 
// Возвращаемое значение:
//  Массив из Структура - Данные последних новых писем. См. РаботаСВиджетами.ДанныеСтрокиСписка().
//
Функция ПоследниеНовыеПисьма() Экспорт
	
	ПоследниеНовыеПисьма = Новый Массив;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	ПапкиПисемВиджетаПочта = ПапкиПисемВиджетаПочта(ТекущийПользователь);
	
	ПапкиПисемСПисьмами = Новый Массив;
	КоличествоПисемВПапках = Новый Соответствие;
	КоличествоПисемВКорневыхПапках = Новый Соответствие;
	Для Каждого ВыбраннаяПапка Из ПапкиПисемВиджетаПочта Цикл
		
		//@skip-check query-in-loop
		КоличествоПисемВПапке = ПолучитьКоличествоПисемВПапке(
			ВыбраннаяПапка,
			Перечисления.ВариантыОтображенияКоличестваПисемВПапке.Непрочтенные,
			ТекущийПользователь);
		КоличествоПисемВПапках[ВыбраннаяПапка] = КоличествоПисемВПапке;
		Если КоличествоПисемВПапке = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ПапкиПисемСПисьмами.Добавить(ВыбраннаяПапка);
		
		УстановитьОтключениеБезопасногоРежима(Истина);
		УстановитьПривилегированныйРежим(Истина);
		КорневаяПапка = Справочники.ПапкиПисем.КорневаяПапка(ВыбраннаяПапка);
		УстановитьПривилегированныйРежим(Ложь);
		УстановитьОтключениеБезопасногоРежима(Ложь);
		
		Если КоличествоПисемВКорневыхПапках[КорневаяПапка] = Неопределено Тогда
			КоличествоПисемВКорневыхПапках[КорневаяПапка] = 0;
		КонецЕсли;
		КоличествоПисемВКорневыхПапках[КорневаяПапка] = КоличествоПисемВКорневыхПапках[КорневаяПапка]
			+ КоличествоПисемВПапке;
		
	КонецЦикла;
	
	КоличествоСтрок = 0;
	МаксимальноеКоличествоСтрок = РаботаСВиджетами.МаксимальноеКоличествоСтрокВиджетаСписок();
	КоличествоКорневыхПапок = КоличествоПисемВКорневыхПапках.Количество();
	Если КоличествоКорневыхПапок > 1 Тогда
		
		// Несколько корневых папок - отобразим корневые папки со счётчиками.
		Для Каждого КлючИЗначение Из КоличествоПисемВКорневыхПапках Цикл
			
			КорневаяПапка = КлючИЗначение.Ключ;
			КоличествоПисемВПапке = КлючИЗначение.Значение;
			
			Если КоличествоСтрок > МаксимальноеКоличествоСтрок Тогда
				Прервать;
			КонецЕсли;
			
			ДанныеСтрокиСписка = РаботаСВиджетами.ДанныеСтрокиСписка();
			ДанныеСтрокиСписка.ИндексКартинки = 1;
			ДанныеСтрокиСписка.Значение = Строка(КорневаяПапка);
			ДанныеСтрокиСписка.Комментарий = КоличествоПисемВПапке;
			ДанныеСтрокиСписка.Ссылка = "e1cib/list/ЖурналДокументов.ЭлектроннаяПочта";
			ПоследниеНовыеПисьма.Добавить(ДанныеСтрокиСписка);
			
			КоличествоСтрок = КоличествоСтрок + 1;
			
		КонецЦикла;
		
	ИначеЕсли КоличествоКорневыхПапок = 1 Тогда
		
		// Одна корневая папка, отобразим последние письма, а если последних писем нет - счётчики папок.
		МесяцНазад = ДобавитьМесяц(НачалоДня(ТекущаяДатаСеанса()), -1);
		
		ПоследниеПисьмаВПапках = Новый ТаблицаЗначений;
		ПоследниеПисьмаВПапках.Колонки.Добавить("Письмо", Новый ОписаниеТипов("ДокументСсылка.ВходящееПисьмо"));
		ПоследниеПисьмаВПапках.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
		Для Каждого ПапкаПисемСПисьмами Из ПапкиПисемСПисьмами Цикл
			
			Запрос = Новый Запрос(
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 100
				|	ЭлектроннаяПочта.Ссылка КАК Письмо,
				|	ЭлектроннаяПочта.Дата КАК Дата
				|ПОМЕСТИТЬ ПоследниеПисьма
				|ИЗ
				|	ЖурналДокументов.ЭлектроннаяПочта КАК ЭлектроннаяПочта
				|ГДЕ
				|	ЭлектроннаяПочта.Папка = &ПапкаПисемСПисьмами
				|	И ЭлектроннаяПочта.Дата > &МесяцНазад
				|	И НЕ ЭлектроннаяПочта.ПометкаУдаления
				|
				|УПОРЯДОЧИТЬ ПО
				|	Дата УБЫВ
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ ПЕРВЫЕ 5
				|	ПоследниеПисьма.Письмо КАК Письмо,
				|	ПоследниеПисьма.Дата КАК Дата
				|ИЗ
				|	ПоследниеПисьма КАК ПоследниеПисьма
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПрочтении КАК СведенияОПрочтении
				|		ПО ПоследниеПисьма.Письмо = СведенияОПрочтении.Объект
				|			И (СведенияОПрочтении.Пользователь = &ТекущийПользователь)
				|ГДЕ
				|	(СведенияОПрочтении.Прочтен ЕСТЬ NULL
				|			ИЛИ СведенияОПрочтении.Прочтен = ЛОЖЬ)
				|
				|УПОРЯДОЧИТЬ ПО
				|	ПоследниеПисьма.Дата УБЫВ");
			
			Запрос.УстановитьПараметр("ПапкаПисемСПисьмами", ПапкаПисемСПисьмами);
			Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
			Запрос.УстановитьПараметр("МесяцНазад", МесяцНазад);
			
			РезультатЗапроса = Запрос.Выполнить();
			ВыборкаПисем = РезультатЗапроса.Выбрать();
			Пока ВыборкаПисем.Следующий() Цикл
				СтрокаПоследниеПисьмаВПапках = ПоследниеПисьмаВПапках.Добавить();
				СтрокаПоследниеПисьмаВПапках.Письмо = ВыборкаПисем.Письмо;
				СтрокаПоследниеПисьмаВПапках.Дата = ВыборкаПисем.Дата;
			КонецЦикла;
			
		КонецЦикла;
		ПоследниеПисьмаВПапках.Сортировать("Дата УБЫВ");
		
		// Отобразим последние письма.
		Для Каждого СтрокаПоследниеПисьмаВПапках Из ПоследниеПисьмаВПапках Цикл
			
			Если КоличествоСтрок > МаксимальноеКоличествоСтрок Тогда
				Прервать;
			КонецЕсли;
			
			ДанныеСтрокиСписка = РаботаСВиджетами.ДанныеСтрокиСписка();
			ДанныеСтрокиСписка.ИндексКартинки = 0;
			ДанныеСтрокиСписка.Значение = Строка(СтрокаПоследниеПисьмаВПапках.Письмо);
			ДанныеСтрокиСписка.Ссылка = ПолучитьНавигационнуюСсылку(СтрокаПоследниеПисьмаВПапках.Письмо);
			ДанныеСтрокиСписка.Комментарий = РаботаСВиджетами.ПредставлениеДатыСписка(
				СтрокаПоследниеПисьмаВПапках.Дата);
			ПоследниеНовыеПисьма.Добавить(ДанныеСтрокиСписка);
			
			КоличествоСтрок = КоличествоСтрок + 1;
			
		КонецЦикла;
		
		Если КоличествоСтрок = 0 Тогда
			
			// Последних писем нет, отобразим количество непрочтенных писем в папках.
			Для Каждого ПапкаПисемСПисьмами Из ПапкиПисемСПисьмами Цикл
				
				Если КоличествоСтрок > МаксимальноеКоличествоСтрок Тогда
					Прервать;
				КонецЕсли;
				
				КоличествоПисемВПапке = КоличествоПисемВПапках[ПапкаПисемСПисьмами];
				
				ДанныеСтрокиСписка = РаботаСВиджетами.ДанныеСтрокиСписка();
				ДанныеСтрокиСписка.ИндексКартинки = 1;
				ДанныеСтрокиСписка.Значение = Строка(ПапкаПисемСПисьмами);
				ДанныеСтрокиСписка.Комментарий = КоличествоПисемВПапке;
				ДанныеСтрокиСписка.Ссылка = "e1cib/list/ЖурналДокументов.ЭлектроннаяПочта";
				ПоследниеНовыеПисьма.Добавить(ДанныеСтрокиСписка);
				
				КоличествоСтрок = КоличествоСтрок + 1;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПоследниеНовыеПисьма;
	
КонецФункции

// Возвращает текст приглашения iCalendar
//
// Параметры
// ФайлСсылка  -СправочникССылка.Файлы
// ИдентификаторПриглашения - Строка
// РеквизитыПриглашения - Структура
//
// Возвращаемое значение:
//  Строка
//
Функция ПолучитьТекстПриглашения(ФайлСсылка, ИдентификаторПриглашения, РеквизитыПриглашения) Экспорт
	
	ДвДанные = РаботаСФайламиВызовСервера.ПолучитьДвоичныеДанныеФайла(ФайлСсылка);
	
	ИмяВрФайла = ПолучитьИмяВременногоФайла("txt");
	ДвДанные.Записать(ИмяВрФайла);
	
	ЧтениеТекста = Новый ЧтениеТекста(ИмяВрФайла, "UTF-8");
	ТекстФайла = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	ЧтениеТекста = Неопределено;
	
	Тема = "";
	Владелец = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФайлСсылка, "ВладелецФайла");
	Если ВстроеннаяПочтаКлиентСервер.ЭтоПисьмо(Владелец) Тогда
		Тема = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "Тема");
	КонецЕсли;	
	
	ВозвращаемыеРеквизиты = Документы.ВходящееПисьмо.НовыйВозвращаемыеРеквизиты();
	
	ТекстСообщения = Документы.ВходящееПисьмо.СформироватьТекстПриглашения(
		ТекстФайла, Тема,
		РеквизитыПриглашения,,ВозвращаемыеРеквизиты);
		
	ИдентификаторПриглашения = ВозвращаемыеРеквизиты.ИдентификаторПриглашения;	
	
	Возврат ТекстСообщения;
	
КонецФункции	

// Прочитает реквизит или из базы, или из заголовка (если старое письмо, и в РС РеквизитыПисем его нет)
Функция ПрочитатьРеквизитПисьма(Письмо, ИмяРеквизита, ВнутреннийЗаголовок) Экспорт
	
	// тут не заменяем ПолучитьЗначениеПоляИзЗаголовкаПисьма
	ЗначениеРеквизита = ПолучитьЗначениеПоляИзЗаголовкаПисьма(
		ВнутреннийЗаголовок, ИмяРеквизита);
		
	Возврат ЗначениеРеквизита;
	
КонецФункции	

// Получит учетную запись почты для папки писем.
//
// Параметры
//  Папка - СправочникССылка.ПапкиПисем
//
Функция УчетнаяЗаписьДляПапки(Папка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КорневаяПапка = Справочники.ПапкиПисем.КорневаяПапка(Папка);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПапкиПисем.Ссылка КАК Ссылка,
		|	ПапкиУчетныхЗаписей.УчетнаяЗапись КАК УчетнаяЗапись
		|ИЗ
		|	Справочник.ПапкиПисем КАК ПапкиПисем
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПапкиУчетныхЗаписей КАК ПапкиУчетныхЗаписей
		|		ПО (ПапкиУчетныхЗаписей.Папка = ПапкиПисем.Ссылка)
		|ГДЕ
		|	ПапкиПисем.Родитель = &Родитель";
	Запрос.УстановитьПараметр("Родитель", КорневаяПапка);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;	
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.УчетнаяЗапись;
	
КонецФункции	

// Вернет email системной учетной записи почты
Функция ПолучитьАдресСистемнойУчетнойЗаписи() Экспорт
	
	УчетнаяЗаписьОтправителя =
		ПредопределенноеЗначение("Справочник.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты");
	ДанныеУчетнойЗаписиОтправителя = Почта.ПолучитьДанныеУчетнойЗаписи(УчетнаяЗаписьОтправителя);
	СтруктураПочтовогоАдреса = Новый Структура;
	Возврат ДанныеУчетнойЗаписиОтправителя.АдресЭлектроннойПочты;

КонецФункции

// Формирует ссылку mailto
Функция СформироватьСсылкуMailto(
	Кому = "",
	Копия = "",
	СкрытаяКопия = "",
	ТемаПисьма = "",
	ТекстПисьма = "",
	ТипТекста = Неопределено,
	Представление = "") Экспорт
	
	Если ТипТекста = Неопределено Тогда
		ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.HTML")
	КонецЕсли;
	
	СсылкаMailto = "";
	НачатоЗаполнениеПараметров = Ложь;
	
	ДобавитьПараметрКСсылкеMailto(СсылкаMailto, "to", Кому, НачатоЗаполнениеПараметров);
	ДобавитьПараметрКСсылкеMailto(СсылкаMailto, "cc", Копия, НачатоЗаполнениеПараметров);
	ДобавитьПараметрКСсылкеMailto(СсылкаMailto, "bcc", СкрытаяКопия, НачатоЗаполнениеПараметров);
	ДобавитьПараметрКСсылкеMailto(СсылкаMailto, "subject", ТемаПисьма, НачатоЗаполнениеПараметров);
	ДобавитьПараметрКСсылкеMailto(СсылкаMailto, "body", ТекстПисьма, НачатоЗаполнениеПараметров);
	
	Если Не ПустаяСтрока(СсылкаMailto) Тогда
		
		Если ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.HTML") Тогда
			
			Если ПустаяСтрока(Представление) Тогда
				Представление = СсылкаMailto;
			КонецЕсли;
			
			СсылкаMailto = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"<a href=""mailto:%1"">%2</a>", 
				СсылкаMailto,
				Представление);
			
		Иначе
			
			СсылкаMailto = "mailto:" + СсылкаMailto;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СсылкаMailto;
	
КонецФункции

//  Вернет структуру для отправки
// 
//  Параметры 
// Письмо - ДокументСсылка.ВходящееПисьмо
// УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты
// ТекстОтветаДляРезультата - Строка
//
// Возвращаемое значение
//	Структура
//
Функция СтруктураПараметровОтправкиПисьмаПриглашения(
		Письмо, УчетнаяЗапись, ТекстОтветаДляРезультата) Экспорт
		
	ПараметрыОтправки = Почта.СформироватьСтруктуруПараметровОтправки();
	
	СписокСвойствОбъекта 
		= "Дата, Важность, Кодировка, Проект, Тема, УчетнаяЗапись, УведомитьОДоставке, УведомитьОПрочтении";
	Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(Письмо) Тогда
		СписокСвойствОбъекта = СписокСвойствОбъекта + ", ОтправительАдресат";
	ИначеЕсли ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(Письмо) Тогда
		ВызватьИсключение НСтр("ru = 'Неверный тип письма'");
	КонецЕсли;
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Письмо, СписокСвойствОбъекта);
	
	ПараметрыОтправки.Вставить("Дата", ТекущаяДатаСеанса());
	
	ПараметрыОтправки.Важность = ЗначенияРеквизитов.Важность;
	ПараметрыОтправки.Кодировка = "utf-8";
	
	СодержаниеПисьма = Новый Структура("ТипТекста, Кодировка, Текст, ПроизвольныйТипТекста");
	СодержаниеПисьма.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.ПроизвольныйТекст;
	СодержаниеПисьма.Кодировка = "utf-8";
	СодержаниеПисьма.Текст = ТекстОтветаДляРезультата;
	СодержаниеПисьма.ПроизвольныйТипТекста = "text/calendar;method=REPLY";
	ПараметрыОтправки.Тексты.Добавить(СодержаниеПисьма);
	
	ЕстьПолучатели = Ложь;
	ПолучателиВсехКопий = Новый Массив;
	
	ДанныеУчетнойЗаписи = Почта.ПолучитьДанныеУчетнойЗаписи(ЗначенияРеквизитов.УчетнаяЗапись);
	
	СтруктураОтправителя = Новый Структура;
	СтруктураОтправителя.Вставить("Адрес", ДанныеУчетнойЗаписи.АдресЭлектроннойПочты);
	СтруктураОтправителя.Вставить("ОтображаемоеИмя", ДанныеУчетнойЗаписи.ИмяПользователя);
	ПараметрыОтправки.ИмяОтправителя = ДанныеУчетнойЗаписи.ИмяПользователя;
	
	ПараметрыОтправки.Отправитель = СтруктураОтправителя;

	АдресатПолучатель = ЗначенияРеквизитов.ОтправительАдресат; // Тип = Кому

	СтруктураПочтовогоАдреса = Новый Структура("Адрес, ОтображаемоеИмя");
	
	СведенияОбОтправителе = 
		ОбщегоНазначения.ЗначенияРеквизитовОбъекта(АдресатПолучатель, "Адрес, Наименование");
		
	СтруктураПочтовогоАдреса.Адрес = СведенияОбОтправителе.Адрес;
	СтруктураПочтовогоАдреса.ОтображаемоеИмя = СведенияОбОтправителе.Наименование;
	
	ПараметрыОтправки.Получатели.Добавить(СтруктураПочтовогоАдреса);
	ЕстьПолучатели = Истина;
	
		
	ПараметрыОтправки.Вставить("ЕстьПолучатели", ЕстьПолучатели);
	
	ПараметрыОтправки.Тема = ЗначенияРеквизитов.Тема;
	
	Возврат ПараметрыОтправки;
	
КонецФункции

// Формирует структуру данных приглашения.
// 
// Параметры:
//  Мероприятие - СправочникСсылка.Мероприятия.            
//  Состояние - ПеречислениеСсылка.СостоянияПриглашения
//	Письмо - ДокументССылка.ВходящееПисьмо
//  ИдентификаторПриглашения -Строка
// 
// Возвращаемое значение:
//  Структура - Данные приглашения.
//   * ДатаНачала - Дата.
//   * ДатаОкончания - Дата.
//   * ДатаСоздания - Дата.
//   * ДатаИзменения - Дата.
//   * МестоПроведения - Строка.
//   * СостояниеМероприятия - ПеречислениеСсылка.СостоянияМероприятий.
//   * ОрганизаторEmail - Строка.
//   * Наименование - Строка.
//   * Серия - Число.
//   * ОрганизаторПредставление - Строка.
//   * УникальныйИдентификатор - УникальныйИдентификатор.
//   * Участники - Массив из Структура.
//     ** Email - Строка.
//     ** Представление - Строка.
//     ** Состояние - ПеречислениеСсылка.СостоянияПриглашения.
//  
Функция ДанныеПриглашения(РеквизитыМероприятия, Состояние, Письмо, ИдентификаторПриглашения) Экспорт
	
	СостояниеМероприятия = Перечисления.СостоянияМероприятий.МероприятиеВСтадииПодготовки;
	
	УчетнаяЗаписьДляРаботыСПриглашениями = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Письмо, "УчетнаяЗапись");
	Если Не ЗначениеЗаполнено(УчетнаяЗаписьДляРаботыСПриглашениями) Тогда
		ВызватьИсключение НСтр("ru = 'Не указана учетная запись для работы с приглашениями.'");
	КонецЕсли;
	
	РеквизитыУчетнойЗаписи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		УчетнаяЗаписьДляРаботыСПриглашениями,
		"АдресЭлектроннойПочты, ИмяПользователя");
	
	ДанныеПриглашения = Новый Структура(
		"ДатаНачала, ДатаОкончания, ДатаСоздания, ДатаИзменения,
		|Описание, Состояние, МестоПроведения, ОрганизаторEmail, Наименование,
		|ОрганизаторПредставление, УникальныйИдентификатор, Серия, Участники");
	ДанныеПриглашения.ДатаНачала = РеквизитыМероприятия.ДатаНачала;
	ДанныеПриглашения.ДатаОкончания = РеквизитыМероприятия.ДатаОкончания;
	ДанныеПриглашения.ДатаСоздания = РеквизитыМероприятия.ДатаСоздания;
	ДанныеПриглашения.ДатаИзменения = РеквизитыМероприятия.ДатаИзменения;
	ДанныеПриглашения.Наименование = РеквизитыМероприятия.Наименование;
	ДанныеПриглашения.Описание = РеквизитыМероприятия.Описание;
	ДанныеПриглашения.Состояние = СостояниеМероприятия;
	ДанныеПриглашения.МестоПроведения = РеквизитыМероприятия.МестоПроведения;
	ДанныеПриглашения.Серия = РеквизитыМероприятия.Серия;  
	
	ДанныеПриглашения.ОрганизаторEmail = РеквизитыМероприятия.АдресОрганизатора;
	ДанныеПриглашения.ОрганизаторПредставление = РеквизитыМероприятия.ПредставлениеОрганизатора;
	
	ДанныеПриглашения.УникальныйИдентификатор = ИдентификаторПриглашения;
	
	ДанныеПриглашения.Участники = Новый Массив;
	
	ДанныеУчастника = Новый Структура("Email, Представление, Состояние");
	ДанныеУчастника.Email = РеквизитыУчетнойЗаписи.АдресЭлектроннойПочты;
	ДанныеУчастника.Представление = РеквизитыУчетнойЗаписи.ИмяПользователя;
	ДанныеУчастника.Состояние = Состояние;
	
	ДанныеПриглашения.Участники.Добавить(ДанныеУчастника);
	
	Возврат ДанныеПриглашения;
	
КонецФункции

// Отправляет письмо - ответ на приглашение
// 
// Параметры:
//  Письмо - ДокументССылка.ВходящееПисьмо
//  Состояние - ПеречислениеСсылка.СостоянияПриглашения
// 
Процедура ВыполнитьОтветНаСервере(Письмо, Состояние) Экспорт
	
	РеквизитыПриглашения = Новый Структура;
	ИдентификаторПриглашения = Неопределено;
	
	ФайлСсылка = Неопределено; // найдем первый ics файл - считаем что он один 
	Вложения = ПолучитьФайлыПисьма(Письмо);
	Для Каждого Вложение Из Вложения Цикл  
		
		Если НРег(Вложение.ТекущаяВерсияРасширение) = НРег("ics") Тогда  
			ФайлСсылка = Вложение.Ссылка;
			Прервать;
		КонецЕсли;	
		
	КонецЦикла;	
	
	Если Не ЗначениеЗаполнено(ФайлСсылка) Тогда
		ВызватьИсключение НСтр("ru = 'Отсутствует ics файл в письме'");
	КонецЕсли;	
	
	ТекстСообщения = ВстроеннаяПочтаСервер.ПолучитьТекстПриглашения(
		ФайлСсылка, ИдентификаторПриглашения, РеквизитыПриглашения);
	
	
	ДанныеПриглашения = ДанныеПриглашения(РеквизитыПриглашения, Состояние, Письмо, ИдентификаторПриглашения);
	
	ТекстОтветаДляРезультата = РаботаСICalendar.ТекстПриглашенияПоICSФайлу(ДанныеПриглашения, "REPLY");
	
	УчетнаяЗапись = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Письмо, "УчетнаяЗапись");
	
	// подготовить и отправить письмо
	
	ПараметрыОтправки 
		= СтруктураПараметровОтправкиПисьмаПриглашения(
			Письмо, УчетнаяЗапись, ТекстОтветаДляРезультата);   
			
	СообщениеОбОшибке = "";
	ИспользоватьДляПолучения = Ложь;
	ПротоколИнтернет = Неопределено;
	
	Соединение = Почта.ИнтернетПочтаУстановитьСоединение(
		УчетнаяЗапись,
		, 
		СообщениеОбОшибке,
		ИспользоватьДляПолучения,
		ПротоколИнтернет);
		
	Если Соединение = Неопределено Тогда
		ТекстПолный = СообщениеОбОшибке;	
		Сообщить(ТекстПолный);
		Возврат;
	КонецЕсли;
			
	// Отправка письма 
	СообщениеОбОшибке = "";
	Если Не Почта.ОтправитьСообщение(Соединение, ПараметрыОтправки, СообщениеОбОшибке, УчетнаяЗапись) Тогда
		Сообщить(СообщениеОбОшибке);
	КонецЕсли;	
	
	ВстроеннаяПочтаСервер.ОтключитьСоединение(Соединение, УчетнаяЗапись);
	
	ВстроеннаяПочтаСервер.ЗаписатьПротоколДоставкиПочты(
		,
		СтрШаблон(НСтр("ru = 'Отправка ответа на приглашение календаря, ответ: %1.
		|полный текст ответа:
		|%2 ' "), Состояние, ТекстОтветаДляРезультата),
		Ложь,
		Перечисления.ТипыСобытийДоставкиПочты.УспешнаяОтправкаПисьма,
		УчетнаяЗапись, 
		0,
		0,
		0);
	
КонецПроцедуры

Процедура ОтключитьСоединение(Соединение, УчетнаяЗапись) Экспорт
	Соединение.Отключиться();
КонецПроцедуры	

// Определяет способ кодирования параметра mailto.
// 
// Параметры:
//  НаименованиеПараметра - Строка.
// 
// Возвращаемое значение:
//  СпособКодированияСтроки.
// 
Функция СпособКодированияПараметраMailto(НаименованиеПараметра) Экспорт
	
	Если НаименованиеПараметра = "to"
		Или НаименованиеПараметра = "cc"
		Или НаименованиеПараметра = "bcc" Тогда
		
		СпособКодированияПараметраMailto = СпособКодированияСтроки.URLВКодировкеURL;
		
	Иначе
		
		СпособКодированияПараметраMailto = СпособКодированияСтроки.КодировкаURL;
		
	КонецЕсли;
	
	Возврат СпособКодированияПараметраMailto;
	
КонецФункции

// Проверяет что сотрудник, найденный по данному адресу - недействителен
//
// Параметры
//  Адрес - адрес email, который надо проверить
//
// Возвращаемое значение:
//  Булево - Истина, если адрес недействителен
//
Функция ЭтоНедействительныйАдрес(Адрес) Экспорт
	
	Если Не ЗначениеЗаполнено(Адрес) Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	АдресИнфо = РаботаСоСтроками.РазложитьПредставлениеАдресаЭлектроннойПочты(Адрес);
	Если Не ЗначениеЗаполнено(АдресИнфо.Домен) Тогда
		Возврат Ложь;
	КонецЕсли;	

	УстановитьПривилегированныйРежим(Истина);
	
	// Находим - есть ли хоть один недействительный сотрудник с этим адресом
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СотрудникиКонтактнаяИнформация.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Сотрудники.КонтактнаяИнформация КАК СотрудникиКонтактнаяИнформация
		|ГДЕ
		|	СотрудникиКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
		|	И СотрудникиКонтактнаяИнформация.АдресЭП = &Адрес
		|	И СотрудникиКонтактнаяИнформация.Ссылка.Действует = ЛОЖЬ";
		
	Запрос.УстановитьПараметр("Адрес", Адрес);
	НедействительныеПользователиОтсутствуют = Запрос.Выполнить().Пустой();    
	
	Если Не НедействительныеПользователиОтсутствуют Тогда // есть 1 недействительный
		
		// Находим - есть ли хоть один действительный сотрудник с этим адресом  (действительный !!!!)
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СотрудникиКонтактнаяИнформация.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Сотрудники.КонтактнаяИнформация КАК СотрудникиКонтактнаяИнформация
			|ГДЕ
			|	СотрудникиКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
			|	И СотрудникиКонтактнаяИнформация.АдресЭП = &Адрес
			|	И СотрудникиКонтактнаяИнформация.Ссылка.Действует = ИСТИНА";
			
		Запрос.УстановитьПараметр("Адрес", Адрес);
		ДействительныеПользователиОтсутствуют = Запрос.Выполнить().Пустой();               
		
		Если Не ДействительныеПользователиОтсутствуют Тогда // есть другие норм сотрудники (скажем того же физлица)
			НедействительныеПользователиОтсутствуют = Не ДействительныеПользователиОтсутствуют;
		КонецЕсли;	
		
	КонецЕсли;	
	
	Возврат Не НедействительныеПользователиОтсутствуют;
	
КонецФункции	

// Возвращает список недействительных пользователей, найденных по адресам
//
// Параметры
//  НедействительныеАдреса - Массив адресов email недействительных пользователей
//
// Возвращаемое значение:
//  СписокЗначений - содержит список строк вида "Иванов <fdssdf@1c.ru>"  - представление (с адресом) недействительных пользователей
//
Функция ПолучитьПредставленияНедействительныхПользователей(НедействительныеАдреса) Экспорт
	
	НедействительныеАдресаты = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СотрудникиКонтактнаяИнформация.Ссылка КАК Ссылка,
		|	СотрудникиКонтактнаяИнформация.АдресЭП КАК АдресЭП,
		|	СотрудникиКонтактнаяИнформация.Ссылка.Наименование КАК Наименование
		|ИЗ
		|	Справочник.Сотрудники.КонтактнаяИнформация КАК СотрудникиКонтактнаяИнформация
		|ГДЕ
		|	СотрудникиКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
		|	И СотрудникиКонтактнаяИнформация.АдресЭП В(&Адреса)
		|	И СотрудникиКонтактнаяИнформация.Ссылка.Действует = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
		
	Запрос.УстановитьПараметр("Адреса", НедействительныеАдреса);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат НедействительныеАдресаты;
	КонецЕсли;	
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ПредставлениеПользователя = Строка(Выборка.Ссылка) + " <" + Выборка.АдресЭП + ">";
		НедействительныеАдресаты.Добавить(ПредставлениеПользователя);
		
	КонецЦикла;	
	
	Возврат НедействительныеАдресаты;
	
КонецФункции	

Процедура КоличествоПисемВПапкахСвертка() Экспорт

	Отказ = Ложь;
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.КоличествоПисемВПапкахСвертка, Отказ);
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВстроеннуюПочту") <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.КоличествоПисемВПапкахИтоги.Свернуть();
	
КонецПроцедуры


// Получить почту с email сервера для указанного задания, и запишет статус получения почты
//
// Параметры:
//  НомерЗадания - Число - номер потока
//
Процедура ПолучитьПочтуФоновымЗаданием(НомерЗадания) Экспорт
	
	Описание = СтрШаблон(
		НСтр("ru = 'НомерЗадания: %1'"),
		Строка(НомерЗадания));
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Получение почты.Начало работы фонового задания.'"),
		УровеньЖурналаРегистрации.Информация,,,
		Описание);
	
	// При начале рег задания делаем запись в СтатусФоновыхЗаданийПриемкиПочты
	ЗаписатьСтатусПолученияПочты(НомерЗадания, ТекущаяДатаСеанса(), ТекущаяДатаСеанса(), Неопределено);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПринятьПочтуПоЗаданию(НомерЗадания);
	
	// При завершении рег задания стираем запись в СтатусФоновыхЗаданийПриемкиПочты
	УдалитьСтатусПолученияПочты(НомерЗадания);
	
 	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Получение почты.Конец работы фонового задания.'"),
		УровеньЖурналаРегистрации.Информация,,,
		Описание);
	
КонецПроцедуры

// Получить почту с email сервера для указанного задания
//
// Параметры:
//  НомерЗадания - Число - номер потока
//
Процедура ПринятьПочтуПоЗаданию(НомерЗадания) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	// Получаем ящик для обработки.
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПриемкаВнешнейПочты.УчетнаяЗапись КАК УчетнаяЗапись,
		|	ПриемкаВнешнейПочты.ДатаПоявленияПисем КАК ДатаПоявленияПисем
		|ИЗ
		|	РегистрСведений.ПриемкаВнешнейПочты КАК ПриемкаВнешнейПочты
		|ГДЕ
		|	ПриемкаВнешнейПочты.ИдетОбработка = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаПоявленияПисем";
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВремяНачала = ТекущаяДатаСеанса();
	КонтекстДоставки = КонтекстДоставки();
	КонтекстДоставки.НомерЗадания = НомерЗадания;
	КонтекстДоставки.Вставить("РаздельнаяПроверкаИПолучение", Истина);
	
	Пока Выборка.Следующий() Цикл
		
		УчетнаяЗапись = Выборка.УчетнаяЗапись;
		Если Не ПринятьПочтуИзЯщикаИВернутьЕслиСмоглиЗаблокировать(НомерЗадания, УчетнаяЗапись, КонтекстДоставки) Тогда
			Продолжить; // не смогли заблокировать
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры

// Получить почту с email сервера для указанного задания и учетной записи почты
//
// Параметры:
//  НомерЗадания - Число - номер потока
//  УчетнаяЗапись - СправочникССылка.УчетныеЗаписиЭлектроннойПочты
//  КонтекстДоставки - Структура
//
// Возвращаемое значение:
//  Булево - Истина, если смогли заблокировать и отработать. Ложь, если не смогли заблокировать (другое задание это сделало)
Функция ПринятьПочтуИзЯщикаИВернутьЕслиСмоглиЗаблокировать(НомерЗадания, УчетнаяЗапись, КонтекстДоставки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Успешно = РегистрыСведений.ПриемкаВнешнейПочты.ВзятьВРаботу(УчетнаяЗапись);
	Если Не Успешно Тогда
		Возврат Ложь;
	КонецЕсли;	

	// Примем почту.
	МассивУчетныхЗаписейПолучения = Новый Массив;
	МассивУчетныхЗаписейПолучения.Добавить(УчетнаяЗапись);
	
	Если НомерЗадания <> 0 Тогда
		ЗаписатьСтатусПолученияПочты(НомерЗадания,, ТекущаяДатаСеанса(), УчетнаяЗапись);
	КонецЕсли;	
	
	ЗаписатьСтатусПолученияПочты(
		НомерЗадания, 
		Неопределено, 
		ТекущаяДатаСеанса(), 
		УчетнаяЗапись);
	
	ВыполнитьПриемкуОтправкуПочты(НомерЗадания, УчетнаяЗапись, КонтекстДоставки);
	
	УдалитьЗаписьПриемкаВнешнейПочты = Истина;
	
	Если КонтекстДоставки.Свойство("ЗагруженыНеВсеПисьма") И 
		КонтекстДоставки.ЗагруженыНеВсеПисьма Тогда
		
		УдалитьЗаписьПриемкаВнешнейПочты = Ложь;
		
	КонецЕсли;	
	
	Если УдалитьЗаписьПриемкаВнешнейПочты Тогда
		// При завершении рег задания стираем запись в ПриемкаВнешнейПочты.
		РегистрыСведений.ПриемкаВнешнейПочты.Удалить(УчетнаяЗапись);
	Иначе	
		
		// тут приняты не все письма. Просто освободим запись.
		РегистрыСведений.ПриемкаВнешнейПочты.Освободить(УчетнаяЗапись);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Ищет только среди фоновых заданий. Вернет Истина, если есть активный сеанс с этим номером (именно фоновое)   
//
// Параметры:
//  НомерСеанса - Число
//
// Возвращаемое значение:
//  Булево - Истина, если есть активный сеанс с этим номером (именно фоновое)
Функция НомерСеансаЕстьСредиАктивныхСеансовИБ(НомерСеанса) Экспорт
	
	СеансыИнформационнойБазы = ПолучитьСеансыИнформационнойБазы();
	
	Для Каждого СеансИБ Из СеансыИнформационнойБазы Цикл
		
		Если СеансИБ.НомерСеанса = НомерСеанса
			И СеансИБ.ИмяПриложения = "BackgroundJob" Тогда
			
			Возврат Истина;
			
		КонецЕсли;	
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Обработчик фонового задания. Проверяет наличие писем на email сервере.
//
// Параметры:
//  НомерЗадания - Число - номер потока.
//
Процедура ПроверитьНаличиеПисемФоновымЗаданием(НомерЗадания) Экспорт
	
	Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'НомерЗадания: %1'"),
		Строка(НомерЗадания));
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Проверка внешней почты.Начало работы фонового задания.'"),
		УровеньЖурналаРегистрации.Информация,,,
		Описание);
	
	// При начале рег задания делаем запись в СтатусФоновыхЗаданийПриемкиПочты
	ЗаписатьСтатусПроверкиПочты(НомерЗадания, ТекущаяДатаСеанса(), ТекущаяДатаСеанса(), Неопределено);
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Все это (обход учеток в коде обработки фонового задания) делаем не однократно, а 20 раз в цикле. 
	//  Это нужно для того, чтобы не перестартовало постоянно фоновое задание.
	
	Для Итерация = 1 По 20 Цикл
		ПроверитьНаличиеПочты(НомерЗадания);
	КонецЦикла;	
	
	// При завершении рег задания стираем запись в СтатусФоновыхЗаданийПриемкиПочты.
	УдалитьСтатусПроверкиПочты(НомерЗадания);
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Проверка внешней почты.Конец работы фонового задания.'"),
		УровеньЖурналаРегистрации.Информация,,,
		Описание);
	
КонецПроцедуры

// Проверяет наличие писем для указанного задания.
//
// Параметры:
//  НомерЗадания - Число - номер потока.
//
Процедура ПроверитьНаличиеПочты(НомерЗадания) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПотокиПроверкиПочты.УчетнаяЗапись КАК УчетнаяЗапись
		|ИЗ
		|	РегистрСведений.ПотокиПроверкиПочты КАК ПотокиПроверкиПочты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПриемкаВнешнейПочты КАК ПриемкаВнешнейПочты
		|		ПО ПотокиПроверкиПочты.УчетнаяЗапись = ПриемкаВнешнейПочты.УчетнаяЗапись
		|ГДЕ
		|	ПотокиПроверкиПочты.НомерЗадания = &НомерЗадания
		|	И ПотокиПроверкиПочты.УчетнаяЗапись.ВариантИспользования = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияПочты.Встроенная)
		|	И ПотокиПроверкиПочты.УчетнаяЗапись.ИспользоватьДляПолучения = ИСТИНА
		|	И ПотокиПроверкиПочты.УчетнаяЗапись.ПометкаУдаления = ЛОЖЬ
		|	И ПриемкаВнешнейПочты.УчетнаяЗапись ЕСТЬ NULL";
		

	Запрос.Текст = Запрос.Текст + "  УПОРЯДОЧИТЬ ПО ПотокиПроверкиПочты.УчетнаяЗапись.Наименование";
		
	Запрос.УстановитьПараметр("НомерЗадания", НомерЗадания);
	МассивУчетныхЗаписейПолучения = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("УчетнаяЗапись");
	
	Если МассивУчетныхЗаписейПолучения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СообщенияОбОшибках = Новый Массив;
	
	ВремяНачала = ТекущаяДатаСеанса();
	КонтекстДоставки = КонтекстДоставки();
	КонтекстДоставки.НомерЗадания = НомерЗадания;
	ПроверитьНаличиеПисем(МассивУчетныхЗаписейПолучения, КонтекстДоставки, СообщенияОбОшибках);
	
	ВремяОкончания = ТекущаяДатаСеанса();
	ТекстЛога = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Длительность сеанса: %1 сек.'"),
		ВремяОкончания - ВремяНачала);
		
	ЗаписатьПротоколДоставкиПочты(
		Неопределено, 
		ТекстЛога, 
		Ложь, 
		Перечисления.ТипыСобытийДоставкиПочты.ЗавершениеСеансаПроверкиНаличияПочты,
		КонтекстДоставки.УчетнаяЗапись, 
		КонтекстДоставки.ПорядковыйНомерСобытия, 
		КонтекстДоставки.ИдентификаторСеанса,
		КонтекстДоставки.НомерЗадания);
	
КонецПроцедуры

// Возвращает структуру параметров доставки
//
// Возвращаемое значение:
//  ИдентификаторСеанса - УникальныйИдентификатор - Уникальный идентификатор сеанса.
//  ПорядковыйНомерСобытия - Число - счетчик события.
//  НомерЗадания - Число - номер фонового задания, выполняющего доставку.
//  ДатаПоследнегоПринятогоВнешнегоПисьма - Дата - Дата последнего принятого внешнего письма.
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - обрабатываемая учетная запись.
//  ДанныеУчетнойЗаписи - Структура - см. Почта.ПолучитьДанныеУчетнойЗаписи.
//  Соединение - ИнтернетПочта - соединение с почтовым сервером.
//
Функция КонтекстДоставки() Экспорт 
	
	КонтекстДоставки = Новый Структура;
	КонтекстДоставки.Вставить("ИдентификаторСеанса", Новый УникальныйИдентификатор);
	КонтекстДоставки.Вставить("ПорядковыйНомерСобытия", 0);
	КонтекстДоставки.Вставить("НомерЗадания", 0);
	КонтекстДоставки.Вставить("ДатаПоследнегоПринятогоВнешнегоПисьма", Неопределено);
	КонтекстДоставки.Вставить("УчетнаяЗапись", Неопределено);
	КонтекстДоставки.Вставить("ДанныеУчетнойЗаписи", Неопределено);
	КонтекстДоставки.Вставить("Соединение", Неопределено);
	
	Возврат КонтекстДоставки;
	
КонецФункции

// Поменяем на указанный поток
//
// Параметры:   
// УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты
//  НовыйНомерПотока - Число - номер потока.
//
Процедура ДобавитьУчетнуюЗаписьВПотокиПроверкиПочты(
	УчетнаяЗапись,
	НовыйНомерПотока)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.ПотокиПроверкиПочты.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
	МенеджерЗаписи.НомерЗадания = НовыйНомерПотока;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Возвращает ФО общее + персональное
//
// Возвращаемое значение:
//  Булево
//
Функция ВстроеннаяПочтаИспользуется() Экспорт
	
	ИспользованиеВстроеннойПочты =
		РегистрыСведений.ИспользованиеПочты.ВстроеннаяПочтаИспользуется()
		И ПолучитьФункциональнуюОпцию("ИспользоватьВстроеннуюПочту");
		
	Возврат ИспользованиеВстроеннойПочты;	
	
КонецФункции	

// Возвращает ФО общее + персональное (2 шт)
//
// Возвращаемое значение:
//  Булево
//
Функция ВстроеннаяИлиЛегкаяПочтаИспользуется() Экспорт
	
	ИспользованиеВстроеннойИлиЛегкойПочты =    
		РегистрыСведений.ИспользованиеПочты.ЛегкаяПочтаИспользуется()
		Или (РегистрыСведений.ИспользованиеПочты.ВстроеннаяПочтаИспользуется()
		И ПолучитьФункциональнуюОпцию("ИспользоватьВстроеннуюПочту"));
		
	Возврат ИспользованиеВстроеннойИлиЛегкойПочты;	
	
КонецФункции	

// Возвращает ФО легкой, встроенной почты
//
// Возвращаемое значение:
//  Структура(ИспользованиеВстроеннойПочты, ИспользованиеЛегкойПочты)
//
Функция ВстроеннаяИлиЛегкаяПочтаИспользуетсяНастройка() Экспорт
	
	ИспользованиеВстроеннойПочты =
		РегистрыСведений.ИспользованиеПочты.ВстроеннаяПочтаИспользуется()
		И ПолучитьФункциональнуюОпцию("ИспользоватьВстроеннуюПочту");

	ИспользованиеЛегкойПочты =    
		РегистрыСведений.ИспользованиеПочты.ЛегкаяПочтаИспользуется();
		
	Возврат Новый Структура("ИспользованиеВстроеннойПочты, ИспользованиеЛегкойПочты", 
		ИспользованиеВстроеннойПочты, ИспользованиеЛегкойПочты);	
	
КонецФункции	

// Возвращает ФО персональное
//
// Возвращаемое значение:
//  Булево
//
Функция ЛегкаяПочтаИспользуется() Экспорт
	
	ИспользованиеЛегкойПочты =
		РегистрыСведений.ИспользованиеПочты.ЛегкаяПочтаИспользуется();
		
	Возврат ИспользованиеЛегкойПочты;	
	
КонецФункции	

// Обработчик рег задания ДиспетчерПриемкиВнутреннейМаршрутизации
Процедура ДиспетчерПриемкиВнутреннейМаршрутизации() Экспорт

	Отказ = Ложь;
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ДиспетчерПриемкиВнутреннейМаршрутизации, Отказ);

	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);

	Если Не ВстроеннаяПочтаСерверПовтИсп.ИспользоватьВнутреннююМаршрутизацию() Тогда 
		Возврат;
	КонецЕсли;	
	
	ДиспетчерПриемкиВнутреннейМаршрутизацииРеализация(Перечисления.ПорядокДоставкиПисемПоВнутреннейМаршрутизации.БыстраяДоставка);
	ДиспетчерПриемкиВнутреннейМаршрутизацииРеализация(Перечисления.ПорядокДоставкиПисемПоВнутреннейМаршрутизации.ДолгаяДоставка);
	
КонецПроцедуры

// Заполняет данные ключевых операций.
// 
// Параметры:
//  ДанныеКлючевыхОпераций - см. ОбщегоНазначенияДокументооборот.НовыеДанныеКлючевыхОпераций
// 
Процедура ЗаполнитьДанныеКлючевыхОпераций(ДанныеКлючевыхОпераций) Экспорт
	
	КодОсновногоЯзыка = МультиязычностьСервер.СведенияОЯзыках().ОсновнойЯзык;
	
	ДанныеКлючевойОперации = ДанныеКлючевыхОпераций.Добавить();
	ДанныеКлючевойОперации.Имя = "ПочтаОткрытиеФормыВходящегоПисьма";
	ДанныеКлючевойОперации.Наименование = НСтр("ru = 'Почта: Открытие формы входящего письма'", КодОсновногоЯзыка);
	ДанныеКлючевойОперации.ЦелевоеВремя = 1;
	
	ДанныеКлючевойОперации = ДанныеКлючевыхОпераций.Добавить();
	ДанныеКлючевойОперации.Имя = "ПочтаОткрытиеФормыИсходящегоПисьма";
	ДанныеКлючевойОперации.Наименование = НСтр("ru = 'Почта: Открытие формы исходящего письма'", КодОсновногоЯзыка);
	ДанныеКлючевойОперации.ЦелевоеВремя = 1;
	
	ДанныеКлючевойОперации = ДанныеКлючевыхОпераций.Добавить();
	ДанныеКлючевойОперации.Имя = "ПочтаВыполнениеКомандыОтправитьВложенияДо100Кб";
	ДанныеКлючевойОперации.Наименование = НСтр("ru = 'Почта: Выполнение команды Отправить вложения до 100 Кб'", КодОсновногоЯзыка);
	ДанныеКлючевойОперации.ЦелевоеВремя = 1;
	
	ДанныеКлючевойОперации = ДанныеКлючевыхОпераций.Добавить();
	ДанныеКлючевойОперации.Имя = "ПочтаВыполнениеКомандыОтправитьВложенияДо1Мб";
	ДанныеКлючевойОперации.Наименование = НСтр("ru = 'Почта: Выполнение команды Отправить вложения от 100 Кб до 1 Мб'", КодОсновногоЯзыка);
	ДанныеКлючевойОперации.ЦелевоеВремя = 1;
	
	ДанныеКлючевойОперации = ДанныеКлючевыхОпераций.Добавить();
	ДанныеКлючевойОперации.Имя = "ПочтаВыполнениеКомандыОтправитьВложенияБолее1Мб";
	ДанныеКлючевойОперации.Наименование = НСтр("ru = 'Почта: Выполнение команды Отправить вложения более 1 Мб'", КодОсновногоЯзыка);
	ДанныеКлючевойОперации.ЦелевоеВремя = 10;
	
	ДанныеКлючевойОперации = ДанныеКлючевыхОпераций.Добавить();
	ДанныеКлючевойОперации.Имя = "ПочтаПометитьПисьмоКакПрочитанное";
	ДанныеКлючевойОперации.Наименование = НСтр("ru = 'Почта: Пометить письмо как прочитанное'", КодОсновногоЯзыка);
	ДанныеКлючевойОперации.ЦелевоеВремя = 1;
	
	ДанныеКлючевойОперации = ДанныеКлючевыхОпераций.Добавить();
	ДанныеКлючевойОперации.Имя = "ПочтаВыполнениеКомандыОтборПоПапкеПоиска";
	ДанныеКлючевойОперации.Наименование = НСтр("ru = 'Почта: Выполнение команды отбор по папке поиска'", КодОсновногоЯзыка);
	ДанныеКлючевойОперации.ЦелевоеВремя = 1;    
	
	ДанныеКлючевойОперации = ДанныеКлючевыхОпераций.Добавить();
	ДанныеКлючевойОперации.Имя = "ПочтаВыполнениеКомандыОтборПоПапке";
	ДанныеКлючевойОперации.Наименование = НСтр("ru = 'Почта: Выполнение команды отбор по папке'", КодОсновногоЯзыка);
	ДанныеКлючевойОперации.ЦелевоеВремя = 1;
	
	ДанныеКлючевойОперации = ДанныеКлючевыхОпераций.Добавить();
	ДанныеКлючевойОперации.Имя = "ПочтаОтображениеПисьмаВОбластиЧтения";
	ДанныеКлючевойОперации.Наименование = НСтр("ru = 'Почта: Отображение письма в области чтения'", КодОсновногоЯзыка);
	ДанныеКлючевойОперации.ЦелевоеВремя = 1;
	
	ДанныеКлючевойОперации = ДанныеКлючевыхОпераций.Добавить();
	ДанныеКлючевойОперации.Имя = "ПочтаПереносПисьмаИзПапкиВПапку";
	ДанныеКлючевойОперации.Наименование = НСтр("ru = 'Почта: Перенос письма из папки в папку'", КодОсновногоЯзыка);
	ДанныеКлючевойОперации.ЦелевоеВремя = 1;
	
	ДанныеКлючевойОперации = ДанныеКлючевыхОпераций.Добавить();
	ДанныеКлючевойОперации.Имя = "ПочтаОткрытиеФормыСписка";
	ДанныеКлючевойОперации.Наименование = НСтр("ru = 'Почта: Открытие формы списка'", КодОсновногоЯзыка);
	ДанныеКлючевойОперации.ЦелевоеВремя = 5;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получает письма всех учетных записей, с установленным признаком
// использования для встроенной почты и ИспользоватьДляПолучения.
//
Процедура ПолучитьВсеПисьма(СообщенияОбОшибках, ПараметрыЛогирования, МассивУчетныхЗаписей = Неопределено, КонтекстДоставки = Неопределено)
	
	УчетныеЗаписи = Новый Массив;
	
	Если МассивУчетныхЗаписей <> Неопределено Тогда
		УчетныеЗаписи = МассивУчетныхЗаписей;
	Иначе	
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ИспользоватьДляПолучения", Истина);
		ПараметрыОтбора.Вставить("ВариантИспользования", Перечисления.ВариантыИспользованияПочты.Встроенная);

		УчетныеЗаписи = Почта.ПолучитьУчетныеЗаписиЭлектроннойПочты(ПараметрыОтбора);
	КонецЕсли;
	
	Для каждого УчетнаяЗапись Из УчетныеЗаписи Цикл
		
		СообщениеОбОшибке = "";
		ПротоколИнтернет = Неопределено;
		Соединение = Почта.ИнтернетПочтаУстановитьСоединение(УчетнаяЗапись,, СообщениеОбОшибке,,ПротоколИнтернет);
		Если Соединение = Неопределено Тогда
			
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Учетная запись: %1
					|Ошибка при попытке подключения к почтовому ящику:
					|%2'"),
				УчетнаяЗапись,
				СообщениеОбОшибке);
			
			ЗаписатьОшибкуДоставки(СообщениеОбОшибке, Неопределено, УчетнаяЗапись, ПараметрыЛогирования,
				Неопределено, Перечисления.ТипыСобытийДоставкиПочты.ОшибкаПолученияПисьма);
			
			СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
			
			РегистрыСведений.СостоянияУчетныхЗаписей.ЗаписатьСобытие(
				УчетнаяЗапись,
				Перечисления.ОперацииСУчетнойЗаписью.СоединениеССервером,
				ТекущаяДатаСеанса(),
				Ложь, // ОперацияУспешноЗавершена
				СообщениеОбОшибке); // Комментарий
			
			Продолжить;
			
		Иначе
			
			РегистрыСведений.СостоянияУчетныхЗаписей.ЗаписатьСобытие(
				УчетнаяЗапись,
				Перечисления.ОперацииСУчетнойЗаписью.СоединениеССервером,
				ТекущаяДатаСеанса(),
				Истина, // ОперацияУспешноЗавершена
				""); // Комментарий
			
		КонецЕсли;
		
		ПолучитьПисьмаУчетнойЗаписи(Соединение, УчетнаяЗапись, СообщенияОбОшибках, ПараметрыЛогирования,
			ПротоколИнтернет, КонтекстДоставки);
		
		Соединение.Отключиться();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПолучитьПисьмаУчетнойЗаписи(Соединение, УчетнаяЗапись, СообщенияОбОшибках, ПараметрыЛогирования,
	ПротоколИнтернет = Неопределено, КонтекстДоставки = Неопределено)
	
	ЗаписатьПротоколДоставкиПочты(Неопределено, "", 
		Ложь, 
		Перечисления.ТипыСобытийДоставкиПочты.НачалоПолученияИдентификаторовВходящихСообщений,
		УчетнаяЗапись, ПараметрыЛогирования.ПорядковыйНомерСобытия, 
		ПараметрыЛогирования.ИдентификаторСеанса,
		ПараметрыЛогирования.НомерЗадания);
		
	ПротоколВходящейПочты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, 
		"ПротоколВходящейПочты");

	ПараметрыОтбора = Неопределено;
	Если ПротоколВходящейПочты = "IMAP" Тогда
		
		ПараметрыОтбора = Новый Структура();
		ПараметрыОтбора.Вставить("ПослеДатыОтправления", ТекущаяДатаСеанса() - 7 * 86400);
		ПараметрыОтбора.Вставить("Удаленные", Ложь);
		
	КонецЕсли;	
		
	// Получение идентификаторов всех сообщений в почтовом ящике
	СообщениеОбОшибке = "";
	
	Идентификаторы = Почта.ПолучитьИдентификаторыВходящихСообщений(
		Соединение,,ПараметрыОтбора, СообщениеОбОшибке);
		
	Если Идентификаторы = Неопределено И ПротоколВходящейПочты = "IMAP" Тогда
		
		ПараметрыОтбора.Удалить("ПослеДатыОтправления");
		
		Идентификаторы = Почта.ПолучитьИдентификаторыВходящихСообщений(
			Соединение,,ПараметрыОтбора, СообщениеОбОшибке);
			
	КонецЕсли;	
		
	Если Идентификаторы = Неопределено Тогда
		
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Учетная запись: %1
				|Ошибка при попытке получения идентификаторов писем в почтовом ящике:
				|%2'"),
			УчетнаяЗапись,
			СообщениеОбОшибке);
		
		ЗаписатьОшибкуДоставки(
			СообщениеОбОшибке,
			Неопределено,
			УчетнаяЗапись,
			ПараметрыЛогирования,
			Неопределено,
			Перечисления.ТипыСобытийДоставкиПочты.ОшибкаПолученияПисьма);
		
		СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		
		РегистрыСведений.СостоянияУчетныхЗаписей.ЗаписатьСобытие(
			УчетнаяЗапись,
			Перечисления.ОперацииСУчетнойЗаписью.ПолучениеПисем,
			ТекущаяДатаСеанса(),
			Ложь, // ОперацияУспешноЗавершена
			СообщениеОбОшибке); // Комментарий
			
		ЗаписатьОбработаннуюУчетнуюЗапись(УчетнаяЗапись);		
		
		Возврат;
		
	КонецЕсли;
	
	РезультатРазбораИдентификаторов = РегистрыСведений.ИдентификаторыПолученныхПисем.РазобратьИдентификаторы(
		УчетнаяЗапись,
		Идентификаторы);
	
	// Удаление записей из регистра сведений ИдентификаторыПолученныхПисем
	Если РезультатРазбораИдентификаторов.УдалитьВРегистре.Количество() > 0 Тогда
		РегистрыСведений.ИдентификаторыПолученныхПисем.УдалитьЗаписи(
			УчетнаяЗапись,
			РезультатРазбораИдентификаторов.УдалитьВРегистре);
	КонецЕсли;
	
	Если КонтекстДоставки <> Неопределено И КонтекстДоставки.Свойство("РаздельнаяПроверкаИПолучение") Тогда
		
		// запишем в начале, т.к. получение с email сервера тоже могло много времени занять.
		РегистрыСведений.ПриемкаВнешнейПочты.ОбновитьДатуОбработки(
			УчетнаяЗапись, ТекущаяДатаСеанса());
		
	КонецЕсли;	
	
	// Получение новых писем
	ТекстЛога = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Количество получаемых писем: %1'"),
		РезультатРазбораИдентификаторов.Загрузить.Количество());
		
	МаксимальноеЧислоПринимаемыхПисем = ВстроеннаяПочтаСерверПовтИсп.МаксимальноеЧислоПринимаемыхПисем();
	ДобавленоИд = 0;
		
	ПолученоПисем = 0;
	НеПолученоПисем = 0;
	НеЗаписаноПисем = 0;
	СообщенияОбОшибкахПриПолучении = "";
	Если РезультатРазбораИдентификаторов.Загрузить.Количество() > 0 Тогда
		
		ПараметрыЗагрузки = Почта.СформироватьСтруктуруПараметровЗагрузки();
		
		Если РезультатРазбораИдентификаторов.Загрузить.Количество() <= МаксимальноеЧислоПринимаемыхПисем Тогда
			ПараметрыЗагрузки.Идентификаторы = РезультатРазбораИдентификаторов.Загрузить;
		Иначе
			
			Для Каждого UIDL Из РезультатРазбораИдентификаторов.Загрузить Цикл
				
				ПараметрыЗагрузки.Идентификаторы.Добавить(UIDL);
				ДобавленоИд = ДобавленоИд + 1;
				
				Если ДобавленоИд >= МаксимальноеЧислоПринимаемыхПисем Тогда
					
					Прервать;
					
				КонецЕсли;	
				
			КонецЦикла;	
			
		КонецЕсли;	
		
		// Попытка получить все сообщения сразу
		СообщениеОбОшибке = "";
		Сообщения = Почта.ПолучитьВходящиеСообщения(Соединение, ПараметрыЗагрузки, СообщениеОбОшибке, ПротоколВходящейПочты);
		Если ЗначениеЗаполнено(Сообщения) Тогда
			
			ЗаписатьПротоколДоставкиПочты(
				Неопределено,
				"",
				Ложь,
				Перечисления.ТипыСобытийДоставкиПочты.УспешноеПолучениеВходящихСообщений,
				УчетнаяЗапись, ПараметрыЛогирования.ПорядковыйНомерСобытия,
				ПараметрыЛогирования.ИдентификаторСеанса,
				ПараметрыЛогирования.НомерЗадания);   
				
			Счетчик = 0; ТекНомер = 0;
			ВремяПоследнейЗаписиАктивности = ТекущаяДатаСеанса();
			
			Для каждого Сообщение Из Сообщения Цикл
				
				СообщениеОбОшибке = "";
				НеЗаписыватьКакОшибку = Ложь;
				ПисьмоСсылка = ЗаписатьВходящееПисьмо(Сообщение, УчетнаяЗапись, СообщениеОбОшибке, НеЗаписыватьКакОшибку);
				
				Счетчик = Счетчик + 1;
				ТекВремя = ТекущаяДатаСеанса();
				
				Если ТекВремя >= ВремяПоследнейЗаписиАктивности + ВремяЗаписиАктивности() Тогда
					ВремяПоследнейЗаписиАктивности = ТекВремя;
					
					Если КонтекстДоставки <> Неопределено И КонтекстДоставки.Свойство("РаздельнаяПроверкаИПолучение") Тогда
						// пишем дату при записи писем
						РегистрыСведений.ПриемкаВнешнейПочты.ОбновитьДатуОбработки(
							УчетнаяЗапись, ТекущаяДатаСеанса());
					КонецЕсли;	
						
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(ПисьмоСсылка) Тогда
					
			       СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			        НСтр("ru = 'Учетная запись: %1
			         |Ошибка при попытке записи входящего письма:
			         |%2
				     |Письмо:  
				     |Тема: ""%3"" 
				     |Дата: %4  
				     |От кого: %5 %6  
				     |Размер: %7'"), 
			         УчетнаяЗапись,
			         СообщениеОбОшибке,
					 Сообщение.Тема, 
					 Строка(Сообщение.ДатаПолучения), 
					 Строка(Сообщение.Отправитель.ОтображаемоеИмя),
					 Строка(Сообщение.Отправитель.Адрес), 
					 Строка(Сообщение.Размер));
					 
					Если НеЗаписыватьКакОшибку = Ложь Тогда
					 
						ЗаписатьОшибкуДоставки(
							СообщениеОбОшибке,
							Неопределено,
							УчетнаяЗапись,
							ПараметрыЛогирования,
							Неопределено,
							Перечисления.ТипыСобытийДоставкиПочты.ОшибкаПолученияПисьма);
						
						СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
						
						ДобавитьЗначениеКСтрокеЧерезРазделитель(
							СообщенияОбОшибкахПриПолучении,
							Символы.ПС,
							СообщениеОбОшибке);
							
					Иначе
						
						ЗаписатьПротоколДоставкиПочты(
							Неопределено,
							СообщениеОбОшибке,
							Ложь, // ЭтоОшибка
							Перечисления.ТипыСобытийДоставкиПочты.ОшибкаПолученияПисьма,
							УчетнаяЗапись, 
							ПараметрыЛогирования.ПорядковыйНомерСобытия,
							ПараметрыЛогирования.ИдентификаторСеанса,
							ПараметрыЛогирования.НомерЗадания);
						
					КонецЕсли;
							
					НеЗаписаноПисем = НеЗаписаноПисем + 1;
					
				Иначе
					
					ПолученоПисем = ПолученоПисем + 1;
					
					ЗаписатьПротоколДоставкиПочты(
						ПисьмоСсылка,
						"",
						Ложь,
						Перечисления.ТипыСобытийДоставкиПочты.УспешноеПолучениеПисьма,
						УчетнаяЗапись,
						ПараметрыЛогирования.ПорядковыйНомерСобытия,
						ПараметрыЛогирования.ИдентификаторСеанса,
						ПараметрыЛогирования.НомерЗадания);
					
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Учетная запись: %1
					|Ошибка при попытке получения входящих сообщений:
					|%2'"),
				УчетнаяЗапись,
				СообщениеОбОшибке);
			
			ЗаписатьОшибкуДоставки(
				СообщениеОбОшибке,
				Неопределено,
				УчетнаяЗапись,
				ПараметрыЛогирования,
				Неопределено,
				Перечисления.ТипыСобытийДоставкиПочты.ОшибкаПолученияПисьма);
				
			Счетчик = 0; ТекНомер = 0;
			ВремяПоследнейЗаписиАктивности = ТекущаяДатаСеанса();
				
			// Попытка получить все сообщения по отдельности
			Для каждого Идентификатор Из РезультатРазбораИдентификаторов.Загрузить Цикл
				МассивИдентификаторов = Новый Массив;
				МассивИдентификаторов.Добавить(Идентификатор);
				ПараметрыЗагрузки = Почта.СформироватьСтруктуруПараметровЗагрузки();
				ПараметрыЗагрузки.Идентификаторы = МассивИдентификаторов;
				
				СообщениеОбОшибке = "";
				Сообщения = Почта.ПолучитьВходящиеСообщения(Соединение, ПараметрыЗагрузки, СообщениеОбОшибке, ПротоколВходящейПочты);
				
				Счетчик = Счетчик + 1;
				ТекВремя = ТекущаяДатаСеанса();
				
				Если ТекВремя >= ВремяПоследнейЗаписиАктивности + ВремяЗаписиАктивности() Тогда
					ВремяПоследнейЗаписиАктивности = ТекВремя;
					
					Если КонтекстДоставки <> Неопределено И КонтекстДоставки.Свойство("РаздельнаяПроверкаИПолучение") Тогда
						// пишем дату при записи писем
						РегистрыСведений.ПриемкаВнешнейПочты.ОбновитьДатуОбработки(
							УчетнаяЗапись, ТекущаяДатаСеанса());
					КонецЕсли;	
						
				КонецЕсли;
				
				Если ЗначениеЗаполнено(Сообщения) Тогда
					
					ЗаписатьПротоколДоставкиПочты(
						Неопределено,
						"",
						Ложь,
						Перечисления.ТипыСобытийДоставкиПочты.УспешноеПолучениеВходящихСообщений,
						УчетнаяЗапись,
						ПараметрыЛогирования.ПорядковыйНомерСобытия,
						ПараметрыЛогирования.ИдентификаторСеанса,
						ПараметрыЛогирования.НомерЗадания);
					
					Для каждого Сообщение Из Сообщения Цикл
						
						СообщениеОбОшибке = "";
						НеЗаписыватьКакОшибку = Ложь;
						ПисьмоСсылка = ЗаписатьВходящееПисьмо(Сообщение, УчетнаяЗапись, СообщениеОбОшибке, НеЗаписыватьКакОшибку);
						Если Не ЗначениеЗаполнено(ПисьмоСсылка) Тогда
							
					       СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					        НСтр("ru = 'Учетная запись: %1
					         |Ошибка при попытке записи входящего письма:
					         |%2
						     |Письмо:  
						     |Тема: ""%3"" 
						     |Дата: %4  
						     |От кого: %5 %6  
						     |Размер: %7'"), 
					         УчетнаяЗапись,
					         СообщениеОбОшибке,
							 Сообщение.Тема, 
							 Строка(Сообщение.ДатаПолучения), 
							 Строка(Сообщение.Отправитель.ОтображаемоеИмя),
							 Строка(Сообщение.Отправитель.Адрес), 
							 Строка(Сообщение.Размер));
							 
							Если НеЗаписыватьКакОшибку = Ложь Тогда 
							
								ЗаписатьОшибкуДоставки(
									СообщениеОбОшибке,
									Неопределено,
									УчетнаяЗапись,
									ПараметрыЛогирования,
									Неопределено,
									Перечисления.ТипыСобытийДоставкиПочты.ОшибкаПолученияПисьма);
								
								СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
								
								ДобавитьЗначениеКСтрокеЧерезРазделитель(
									СообщенияОбОшибкахПриПолучении,
									Символы.ПС,
									СообщениеОбОшибке);
									
							Иначе
								
								ЗаписатьПротоколДоставкиПочты(
									Неопределено,
									СообщениеОбОшибке,
									Ложь, // ЭтоОшибка
									Перечисления.ТипыСобытийДоставкиПочты.ОшибкаПолученияПисьма,
									УчетнаяЗапись, 
									ПараметрыЛогирования.ПорядковыйНомерСобытия,
									ПараметрыЛогирования.ИдентификаторСеанса,
									ПараметрыЛогирования.НомерЗадания);
								
							КонецЕсли;
									
							НеЗаписаноПисем = НеЗаписаноПисем + 1;
							
						Иначе
							
							ПолученоПисем = ПолученоПисем + 1;
							
							ЗаписатьПротоколДоставкиПочты(
								ПисьмоСсылка,
								"",
								Ложь,
								Перечисления.ТипыСобытийДоставкиПочты.УспешноеПолучениеПисьма,
								УчетнаяЗапись,
								ПараметрыЛогирования.ПорядковыйНомерСобытия,
								ПараметрыЛогирования.ИдентификаторСеанса,
								ПараметрыЛогирования.НомерЗадания);
							
						КонецЕсли;
						
					КонецЦикла;
					
				Иначе // Ошибка получения сообщения
					
					СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Учетная запись: %1
							|Ошибка при попытке получения входящего сообщения:
							|%2'"),
						УчетнаяЗапись,
						СообщениеОбОшибке);
					
					ЗаписатьОшибкуДоставки(
						СообщениеОбОшибке,
						Неопределено,
						УчетнаяЗапись,
						ПараметрыЛогирования,
						Неопределено,
						Перечисления.ТипыСобытийДоставкиПочты.ОшибкаПолученияПисьма);
					
					СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
					
					ДобавитьЗначениеКСтрокеЧерезРазделитель(
						СообщенияОбОшибкахПриПолучении,
						Символы.ПС,
						СообщениеОбОшибке);
					
					РегистрыСведений.НеПолученныеВходящиеПисьма.НеуспешнаяПопыткаПолучения(УчетнаяЗапись, Идентификатор);
					
					НеПолученоПисем = НеПолученоПисем + 1;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru ='Получено писем: %1'"),
		ПолученоПисем);
	Если НеПолученоПисем > 0 Тогда
		Комментарий = Комментарий + Символы.ПС +
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru ='Не получено писем: %1'"),
			НеПолученоПисем);
	КонецЕсли;
	Если НеЗаписаноПисем > 0 Тогда
		Комментарий = Комментарий + Символы.ПС +
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru ='Не записано писем: %1'"),
			НеЗаписаноПисем);
		КонецЕсли;
	БылиОшибкиПриПолучении = (НеПолученоПисем > 0) Или (НеЗаписаноПисем > 0);
	Если БылиОшибкиПриПолучении Тогда
		Комментарий = Комментарий + Символы.ПС +
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru ='Сообщения об ошибках:
				|%1'"),
			СообщенияОбОшибкахПриПолучении);
	КонецЕсли;
	
	РегистрыСведений.СостоянияУчетныхЗаписей.ЗаписатьСобытие(
		УчетнаяЗапись,
		Перечисления.ОперацииСУчетнойЗаписью.ПолучениеПисем,
		ТекущаяДатаСеанса(),
		Не БылиОшибкиПриПолучении, // ОперацияУспешноЗавершена
		Комментарий); // Комментарий
		
	ЗаписатьОбработаннуюУчетнуюЗапись(УчетнаяЗапись);		
	
	// Удаление старых писем из почтового ящика
	Если РезультатРазбораИдентификаторов.УдалитьВПочтовомЯщике.Количество() > 0 Тогда
		СообщениеОбОшибке = "";
		Если Не Почта.УдалитьСообщенияВПочтовомЯщике(
			Соединение,
			РезультатРазбораИдентификаторов.УдалитьВПочтовомЯщике,
			СообщениеОбОшибке, ПротоколИнтернет) Тогда
			
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Учетная запись: %1
					|Ошибка при попытке удаления сообщений в почтовом ящике:
					|%2'"),
				УчетнаяЗапись,
				СообщениеОбОшибке);
			
			ЗаписатьОшибкуДоставки(
				СообщениеОбОшибке,
				Неопределено,
				УчетнаяЗапись,
				ПараметрыЛогирования,
				Неопределено,
				Перечисления.ТипыСобытийДоставкиПочты.ОшибкаПолученияПисьма);
			
			СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
			
			Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось удалить письма в ящике:
					|%1'"),
				СообщениеОбОшибке);
			
			РегистрыСведений.СостоянияУчетныхЗаписей.ЗаписатьСобытие(
				УчетнаяЗапись,
				Перечисления.ОперацииСУчетнойЗаписью.УдалениеПисем,
				ТекущаяДатаСеанса(),
				Ложь, // ОперацияУспешноЗавершена
				Комментарий); // Комментарий
			
		Иначе
			
			Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Удалено писем: %1'"),
				РезультатРазбораИдентификаторов.УдалитьВПочтовомЯщике.Количество());
			
			РегистрыСведений.СостоянияУчетныхЗаписей.ЗаписатьСобытие(
				УчетнаяЗапись,
				Перечисления.ОперацииСУчетнойЗаписью.УдалениеПисем,
				ТекущаяДатаСеанса(),
				Истина, // ОперацияУспешноЗавершена
				Комментарий); // Комментарий
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает результат отправки писем в виде структуры:
// Результат (Структура)
// - ПисемКОтправке (Число)
// - Отправлено (Число)
// - НеОтправлено(Число)
//
Функция ОтправитьВсеПисьма(Знач ТекДата, СообщенияОбОшибках, ПараметрыЛогирования, МассивУчетныхЗаписей = Неопределено)
	
	Результат = Новый Структура("ПисемКОтправке, Отправлено, НеОтправлено", 0, 0, 0);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПодготовленныеКОтправкеПисьма.УчетнаяЗапись КАК УчетнаяЗапись,
		|	ПодготовленныеКОтправкеПисьма.Письмо КАК Письмо
		|ИЗ
		|	РегистрСведений.ПодготовленныеКОтправкеПисьма КАК ПодготовленныеКОтправкеПисьма
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
		|		ПО ПодготовленныеКОтправкеПисьма.УчетнаяЗапись = УчетныеЗаписиЭлектроннойПочты.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтложеннойОтправкиПисем КАК НастройкиОтложеннойОтправкиПисем
		|		ПО ПодготовленныеКОтправкеПисьма.Автор = НастройкиОтложеннойОтправкиПисем.Пользователь
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НеотправленныеИсходящиеПисьма КАК НеОтправленныеИсходящиеПисьма
		|		ПО ПодготовленныеКОтправкеПисьма.Письмо = НеОтправленныеИсходящиеПисьма.Письмо
		|			И (НеОтправленныеИсходящиеПисьма.ОсталосьПопытокОтправки = 0)
		|ГДЕ
		|	НЕ УчетныеЗаписиЭлектроннойПочты.ПометкаУдаления
		|	И УчетныеЗаписиЭлектроннойПочты.ВариантИспользования = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияПочты.Встроенная)
		|	И УчетныеЗаписиЭлектроннойПочты.ИспользоватьДляОтправки
		|	И ДОБАВИТЬКДАТЕ(ПодготовленныеКОтправкеПисьма.ПодготовленоКОтправке, СЕКУНДА, ЕСТЬNULL(НастройкиОтложеннойОтправкиПисем.Задержка, 0)) <= &ТекДата
		|	И НеОтправленныеИсходящиеПисьма.Письмо ЕСТЬ NULL 
		|	И ПодготовленныеКОтправкеПисьма.ОтправкаОтменена = ЛОЖЬ
		|	И (НЕ &ИспользоватьВнутреннююМаршрутизацию
		|			ИЛИ ПодготовленныеКОтправкеПисьма.ВидМаршрутизации = ЗНАЧЕНИЕ(Перечисление.ВидыМаршрутизацииПисем.Внешняя))");
		
	Если МассивУчетныхЗаписей <> Неопределено Тогда
		
		// отбираем только среди указанных учетных записей
			
		Запрос.Текст = Запрос.Текст +
			" И УчетныеЗаписиЭлектроннойПочты.Ссылка В(&МассивУчетныхЗаписей)";
			
		Запрос.УстановитьПараметр("МассивУчетныхЗаписей", МассивУчетныхЗаписей);
			
	КонецЕсли;	
	
	Запрос.Текст = Запрос.Текст +
		" ИТОГИ ПО
		|	УчетнаяЗапись";
	
	Запрос.УстановитьПараметр("ИспользоватьВнутреннююМаршрутизацию", Константы.ИспользоватьВнутреннююМаршрутизацию.Получить());	
	Запрос.УстановитьПараметр("ТекДата", ТекДата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;	
		
	ДеревоВыборки = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);	
	
	Для каждого СтрокаУчетнойЗаписи Из ДеревоВыборки.Строки Цикл
		
		УчетнаяЗапись = СтрокаУчетнойЗаписи.УчетнаяЗапись;
		
		СообщениеОбОшибке = "";
		ИспользоватьДляПолучения = Ложь;
		Соединение = Почта.ИнтернетПочтаУстановитьСоединение(
			УчетнаяЗапись,, СообщениеОбОшибке, ИспользоватьДляПолучения);
		Если Соединение = Неопределено Тогда
			
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Учетная запись: %1
					|Ошибка при попытке подключения к почтовому ящику:
					|%2'"),
				УчетнаяЗапись,
				СообщениеОбОшибке);
			
			ЗаписатьОшибкуДоставки(СообщениеОбОшибке, Неопределено, УчетнаяЗапись, ПараметрыЛогирования);
			
			СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
			
			РегистрыСведений.СостоянияУчетныхЗаписей.ЗаписатьСобытие(
				УчетнаяЗапись,
				Перечисления.ОперацииСУчетнойЗаписью.СоединениеССервером,
				ТекущаяДатаСеанса(),
				Ложь, // ОперацияУспешноЗавершена
				СообщениеОбОшибке); // Комментарий
			
			Продолжить;
			
		КонецЕсли;
		
		РегистрыСведений.СостоянияУчетныхЗаписей.ЗаписатьСобытие(
			УчетнаяЗапись,
			Перечисления.ОперацииСУчетнойЗаписью.СоединениеССервером,
			ТекущаяДатаСеанса(),
			Истина, // ОперацияУспешноЗавершена
			""); // Комментарий
		
		ОтправленоПисем = 0;
		НеОтправленоПисем = 0;
		Для каждого СтрокаПисьма Из СтрокаУчетнойЗаписи.Строки Цикл			
			
			Результат.ПисемКОтправке = Результат.ПисемКОтправке + 1;
			
			Если Не ОтправитьПисьмо(СтрокаПисьма.Письмо, Соединение, СообщенияОбОшибках, УчетнаяЗапись, ПараметрыЛогирования) Тогда
				НеОтправленоПисем = НеОтправленоПисем + 1;
			Иначе
				ОтправленоПисем = ОтправленоПисем + 1;
			КонецЕсли;
			
		КонецЦикла;
		
		Результат.Отправлено = Результат.Отправлено + ОтправленоПисем;
		Результат.НеОтправлено = Результат.НеОтправлено + НеОтправленоПисем;
		
		Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru ='Отправлено писем: %1'"),
			ОтправленоПисем);
		
		Если НеОтправленоПисем > 0 Тогда
			
			Комментарий = Комментарий + Символы.ПС +
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru ='Не отправлено писем: %1'"),
				НеОтправленоПисем);
			
		КонецЕсли;
		
		РегистрыСведений.СостоянияУчетныхЗаписей.ЗаписатьСобытие(
			УчетнаяЗапись,
			Перечисления.ОперацииСУчетнойЗаписью.ОтправкаПисем,
			ТекущаяДатаСеанса(),
			(НеОтправленоПисем = 0), // ОперацияУспешноЗавершена
			Комментарий); // Комментарий
		
		Соединение.Отключиться();
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ОтправитьПисьмо(ПисьмоСсылка, Соединение, СообщенияОбОшибках, УчетнаяЗапись, ПараметрыЛогирования)
	
	СообщениеОбОшибке = "";
	
	ЗаписатьПротоколДоставкиПочты(
		ПисьмоСсылка,
		"",
		Ложь,
		Перечисления.ТипыСобытийДоставкиПочты.НачалоОтправкиПисьма,
		УчетнаяЗапись, ПараметрыЛогирования.ПорядковыйНомерСобытия,
		ПараметрыЛогирования.ИдентификаторСеанса,
		ПараметрыЛогирования.НомерЗадания);
	
	Попытка
		
		ЗаблокироватьДанныеДляРедактирования(ПисьмоСсылка);
		
	Исключение
		
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось заблокировать письмо:
				|%1'"),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		
		ЗаписатьПротоколДоставкиПочты(
			ПисьмоСсылка,
			СообщениеОбОшибке,
			Ложь, // логируем как НЕ-ошибку
			Перечисления.ТипыСобытийДоставкиПочты.ОшибкаОтправкиПисьма,
			УчетнаяЗапись, 
			ПараметрыЛогирования.ПорядковыйНомерСобытия,
			ПараметрыЛогирования.ИдентификаторСеанса,
			ПараметрыЛогирования.НомерЗадания);
		
		Возврат Ложь;
		
	КонецПопытки;
	
	ПисьмоОбъект = ПисьмоСсылка.ПолучитьОбъект();
	
	Если Не ПисьмоГотовоКОтправке(ПисьмоОбъект) Тогда
		
		РазблокироватьДанныеДляРедактирования(ПисьмоСсылка);
		
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Письмо не готово к отправке:
				|%1'"),
				ПисьмоСсылка);
		
		СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		
		ЗаписатьОшибкуДоставки(
			СообщениеОбОшибке,
			ПисьмоСсылка,
			УчетнаяЗапись,
			ПараметрыЛогирования);
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Попытка
		
		ПараметрыОтправки = ПолучитьСтруктуруПараметровОтправкиПисьма(ПисьмоОбъект);
		
	Исключение
		
		РазблокироватьДанныеДляРедактирования(ПисьмоСсылка);
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		
		ЗаписатьОшибкуДоставки(
			СообщениеОбОшибке,
			ПисьмоСсылка,
			УчетнаяЗапись,
			ПараметрыЛогирования);
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Если Не ПараметрыОтправки.ЕстьПолучатели Тогда
		
		// снимаем письмо с отправки
		ПисьмоОбъект.ПодготовленоКОтправке = Дата(1, 1, 1);
		ПисьмоОбъект.ОтправкаОтменена = Истина;
		ПисьмоОбъект.Записать();
		
		РазблокироватьДанныеДляРедактирования(ПисьмоСсылка);
		СообщениеОбОшибке = НСтр("ru = 'Письмо не содержит ни одного адреса электронной почты'");
		СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		
		ЗаписатьОшибкуДоставки(
			СообщениеОбОшибке,
			ПисьмоСсылка,
			УчетнаяЗапись,
			ПараметрыЛогирования);
		
		Возврат Ложь;
		
	КонецЕсли;
	
	СообщениеОбОшибке = "";
	Если Не Почта.ОтправитьСообщение(Соединение, ПараметрыОтправки, СообщениеОбОшибке, УчетнаяЗапись) Тогда
		
		РазблокироватьДанныеДляРедактирования(ПисьмоСсылка);
		СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		
		РегистрыСведений.НеОтправленныеИсходящиеПисьма.ЗарегистрироватьНеуспешнуюПопыткуОтправкиПисьма(
				ПисьмоСсылка);
				
		ЗаписатьОшибкуДоставки(
			СообщениеОбОшибке,
			ПисьмоСсылка,
			УчетнаяЗапись,
			ПараметрыЛогирования);
				
		Возврат Ложь;
		
	КонецЕсли;
	
	Попытка
		
		РегистрыСведений.НеОтправленныеИсходящиеПисьма.УдалитьСведенияОПисьме(ПисьмоСсылка);
		
		ПисьмоОбъект.ИдентификаторСообщения = ПараметрыОтправки.ИдентификаторСообщения;
		
		Если Не ПараметрыОтправки.Свойство("Размер") Тогда
			Размер = ВычислитьРазмерПочтовогоСообщения(ПараметрыОтправки);
			ПараметрыОтправки.Вставить("Размер", Размер);
		КонецЕсли;
		
		ТекущаяДатаСеанса = ТекущаяДатаСеанса();
		ПисьмоОбъект.Размер = ПараметрыОтправки.Размер;
		ПисьмоОбъект.ПодготовленоКОтправке = Дата(1, 1, 1);
		ПапкаДляОтправленных = РегистрыСведений.ПапкиУчетныхЗаписей.ПолучитьПапку(
			ПисьмоОбъект.УчетнаяЗапись,
			Перечисления.ВидыПапокПисем.Отправленные);
		ПисьмоОбъект.Папка = ПапкаДляОтправленных;
		ПисьмоОбъект.ДатаОтправки = ТекущаяДатаСеанса;
		ПисьмоОбъект.Дата = ТекущаяДатаСеанса;
		ПрименитьПравилоПриОтправке(ПисьмоОбъект, Истина); //применение пользовательских правил
		ПисьмоОбъект.Записать();
		
	Исключение
		
		РазблокироватьДанныеДляРедактирования(ПисьмоСсылка);
		
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Произошла ошибка при регистрации отправки письма:
				|%1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		
		ЗаписатьОшибкуДоставки(
			СообщениеОбОшибке,
			ПисьмоСсылка,
			УчетнаяЗапись,
			ПараметрыЛогирования);
		
		Возврат Ложь;
		
	КонецПопытки;
	
	РазблокироватьДанныеДляРедактирования(ПисьмоСсылка);
	
	ЗаписатьПротоколДоставкиПочты(
		ПисьмоСсылка,
		"",
		Ложь,
		Перечисления.ТипыСобытийДоставкиПочты.УспешнаяОтправкаПисьма,
		УчетнаяЗапись,
		ПараметрыЛогирования.ПорядковыйНомерСобытия,
		ПараметрыЛогирования.ИдентификаторСеанса,
		ПараметрыЛогирования.НомерЗадания);
	
	Возврат Истина;
	
КонецФункции

Функция ПисьмоГотовоКОтправке(ПисьмоОбъект)
	
	ТекДата = ТекущаяДатаСеанса();
	
	Если ПисьмоОбъект.ПометкаУдаления Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеУчетнойЗаписи = Почта.ПолучитьДанныеУчетнойЗаписи(ПисьмоОбъект.УчетнаяЗапись);
	
	Если ДанныеУчетнойЗаписи.ПометкаУдаления Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ДанныеУчетнойЗаписи.ВариантИспользования <> Перечисления.ВариантыИспользованияПочты.Встроенная Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ДанныеУчетнойЗаписи.ИспользоватьДляОтправки Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПисьмоОбъект.ПодготовленоКОтправке) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ПисьмоОбъект.ОтправкаОтменена Тогда 
		Возврат Ложь;
	КонецЕсли;
	
	Задержка = РегистрыСведений.НастройкиОтложеннойОтправкиПисем.ПолучитьНастройку(
		Сотрудники.ЛюбойПользовательСотрудника(ПисьмоОбъект.Автор));
	
	Если ПисьмоОбъект.ПодготовленоКОтправке + Задержка > ТекДата Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если РегистрыСведений.НеОтправленныеИсходящиеПисьма.ЗакончилисьПопыткиОтправкиПисьма(ПисьмоОбъект.Ссылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьСтруктуруПараметровОтправкиПисьма(
	ПисьмоОбъект,
	ТолькоАдресаЭлектроннойПочты = Истина, 
	ЭтоВложенноеПисьмо = Ложь,
	Знач Глубина = 0)
	
	Глубина = Глубина + 1;	
	
	ПараметрыОтправки = Почта.СформироватьСтруктуруПараметровОтправки();
	ПараметрыОтправки.Важность = ПисьмоОбъект.Важность;
	ПараметрыОтправки.Кодировка = ПисьмоОбъект.Кодировка;
	
	ПараметрыОтправки.Вставить("Дата", ПисьмоОбъект.Дата);
	
	ПараметрыОтправки.Тексты.Добавить(ПисьмоОбъект.ПолучитьСодержаниеПисьма());
	
	Если ЭтоВложенноеПисьмо = Ложь Или ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(ПисьмоОбъект) Тогда
		
		ДанныеУчетнойЗаписи = Почта.ПолучитьДанныеУчетнойЗаписи(ПисьмоОбъект.УчетнаяЗапись);
		СтруктураОтправителя = Новый Структура;
		СтруктураОтправителя.Вставить("Адрес", ДанныеУчетнойЗаписи.АдресЭлектроннойПочты);	
		СтруктураОтправителя.Вставить("ОтображаемоеИмя", ДанныеУчетнойЗаписи.ИмяПользователя);
		ПараметрыОтправки.ИмяОтправителя = ДанныеУчетнойЗаписи.ИмяПользователя;	
		ПараметрыОтправки.Отправитель = СтруктураОтправителя;
		
	Иначе // тут только входящие письма
		
		СтруктураОтправителя = Новый Структура("Адрес, ОтображаемоеИмя");
		
		Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(ПисьмоОбъект) Тогда
			СтруктураОтправителя.Адрес = ПисьмоОбъект.ОтправительАдресат.Адрес;
			СтруктураОтправителя.ОтображаемоеИмя = ПисьмоОбъект.ОтправительАдресат.Наименование;
		КонецЕсли;	
		
		ПараметрыОтправки.Отправитель = СтруктураОтправителя;
		
	КонецЕсли;	
	
	ТаблицаЗначений = Неопределено;
	Если ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(ПисьмоОбъект) Тогда
		ТаблицаЗначений = ПолучитьТаблицуПолучателейКомуКопияСкрытаяУИсходящегоПисьма(ПисьмоОбъект.Ссылка);
	Иначе
		ТаблицаЗначений = ПолучитьТаблицуПолучателейКомуКопияУВходящегоПисьма(ПисьмоОбъект.Ссылка);
	КонецЕсли;	
	
	ЕстьПолучатели = Ложь;
	
	Для каждого ПочтовыйАдресИнфо Из ТаблицаЗначений Цикл
		
		СтруктураПочтовогоАдреса = Новый Структура;
		Адрес = ПочтовыйАдресИнфо.Адрес;
		Представление = ПочтовыйАдресИнфо.Представление;
		СтруктураПочтовогоАдреса.Вставить("Адрес", Адрес);
		СтруктураПочтовогоАдреса.Вставить("ОтображаемоеИмя", Представление);
		
		Если РаботаСоСтроками.ЭтоАдресЭлектроннойПочты(Адрес) Или Не ТолькоАдресаЭлектроннойПочты Тогда
			
			Если ПочтовыйАдресИнфо.ТипАдреса = Перечисления.ТипыАдресатов.ОбратныйАдрес Тогда
				ПараметрыОтправки.ОбратныйАдрес.Добавить(СтруктураПочтовогоАдреса);
				ЕстьПолучатели = Истина;
			КонецЕсли;
			
			Если ПочтовыйАдресИнфо.ТипАдреса = Перечисления.ТипыАдресатов.Кому Тогда 
				ПараметрыОтправки.Получатели.Добавить(СтруктураПочтовогоАдреса);
				ЕстьПолучатели = Истина;
			ИначеЕсли ПочтовыйАдресИнфо.ТипАдреса = Перечисления.ТипыАдресатов.Копия Тогда 
				ПараметрыОтправки.Копии.Добавить(СтруктураПочтовогоАдреса);
				ЕстьПолучатели = Истина;
			ИначеЕсли ПочтовыйАдресИнфо.ТипАдреса = Перечисления.ТипыАдресатов.СкрытаяКопия Тогда
				ПараметрыОтправки.СлепыеКопии.Добавить(СтруктураПочтовогоАдреса);
				ЕстьПолучатели = Истина;
			КонецЕсли;	
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыОтправки.Вставить("ЕстьПолучатели", ЕстьПолучатели);
	
	ПараметрыОтправки.Тема = ПисьмоОбъект.Тема;
	ПараметрыОтправки.УведомитьОПрочтении = ПисьмоОбъект.УведомитьОПрочтении;
	
	Вложения = ПолучитьФайлыПисьма(
		ПисьмоОбъект.Ссылка, // Письмо
		Ложь, // ФормироватьПредставлениеРазмера
		Ложь); // ВключатьПомеченныеНаУдаление
	
	Для каждого Вложение Из Вложения Цикл
			
		СтруктураВложения = Новый Структура;
		
		Данные = РаботаСФайламиВызовСервера.ПолучитьДвоичныеДанныеФайла(Вложение.Ссылка);
		СтруктураВложения.Вставить("Наименование", Вложение.ИмяФайла);
		
		Если Вложение.ЭтоВложенноеПисьмо Тогда
			
			ИнтернетПочтовоеСообщение = Новый ИнтернетПочтовоеСообщение;
			ИнтернетПочтовоеСообщение.УстановитьИсходныеДанные(Данные);

			СтруктураВложения.Вставить("Данные", ИнтернетПочтовоеСообщение);
			СтруктураВложения.Вставить("Идентификатор", Неопределено);
			
		Иначе
			
			СтруктураВложения.Вставить("Данные", Данные);
			
			ИдентификаторПочтовогоВложения =
				РегистрыСведений.ИдентификаторыПочтовыхВложений.ПолучитьИдентификаторПочтовогоВложения(
					Вложение.Ссылка);
			
			СтруктураВложения.Вставить("Идентификатор", ИдентификаторПочтовогоВложения);
			
		КонецЕсли;	
			
		ПараметрыОтправки.Вложения.Добавить(СтруктураВложения);
		
	КонецЦикла;
	
	Если Глубина <= 10 Тогда    
		ВложенныеПисьма = РегистрыСведений.ВложенныеПисьма.ПолучитьВложенныеПисьма(ПисьмоОбъект.Ссылка);
		Для Каждого Вложение Из ВложенныеПисьма Цикл
			
			СтруктураВложения = Новый Структура;
			
			ВложенноеПисьмоОбъект = Вложение.Письмо.ПолучитьОбъект();
			ЭтоВложенноеПисьмо = Истина;
			ПараметрыОтправкиВложенногоПисьма 
				= ПолучитьСтруктуруПараметровОтправкиПисьма(ВложенноеПисьмоОбъект,,ЭтоВложенноеПисьмо,Глубина);
			
			ИнтернетПочтовоеСообщение = Новый ИнтернетПочтовоеСообщение;
			Почта.ЗаполнитьИнтернетПочтовоеСообщение(ИнтернетПочтовоеСообщение, ПараметрыОтправкиВложенногоПисьма);
	
			Если ПараметрыОтправкиВложенногоПисьма.Свойство("Дата")
				И ЗначениеЗаполнено(ПараметрыОтправкиВложенногоПисьма.Дата) Тогда
				СтрокаДаты = Формат(ПараметрыОтправкиВложенногоПисьма.Дата, 
					"ДЛФ=DT");
				ИнтернетПочтовоеСообщение.УстановитьПолеЗаголовка("X-1C-Date", СтрокаДаты, 
					СпособКодированияНеASCIIСимволовИнтернетПочтовогоСообщения.БезКодирования);
			КонецЕсли;	
			
			СтруктураВложения.Вставить("Данные", ИнтернетПочтовоеСообщение);
			СтруктураВложения.Вставить("Наименование", Вложение.ИмяФайла);
			СтруктураВложения.Вставить("Идентификатор", Неопределено);
			
			ПараметрыОтправки.Вложения.Добавить(СтруктураВложения);
			
		КонецЦикла;
	КонецЕсли;
	
	Если ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(ПисьмоОбъект) Тогда
		ПисьмоОтправленоВОтветНа = ПисьмоОбъект.ПисьмоОснование;	
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ПисьмоОтправленоВОтветНа) Тогда
		
		Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(ПисьмоОтправленоВОтветНа) Тогда
			
			ПисьмоОтправленоВОтветНаИнфо = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				ПисьмоОтправленоВОтветНа,
				"ИдентификаторСообщения, ВнутреннийЗаголовок");
				
			Если ЗначениеЗаполнено(ПисьмоОтправленоВОтветНаИнфо.ИдентификаторСообщения) Тогда
				
				// Формирование поля заголовка In-Reply-To
				ПараметрыОтправки.ПоляЗаголовка.Добавить(
					Почта.СформироватьСтруктуруПоляЗаголовка(
						"In-Reply-To", // ИмяПоля
						ВКавычках(ПисьмоОтправленоВОтветНаИнфо.ИдентификаторСообщения, "<", ">"), // ЗначениеПоля
						СпособКодированияНеASCIIСимволовИнтернетПочтовогоСообщения.БезКодирования)); // СпособКодирования
				
				// Формирование поля заголовка References
				References = ПолучитьЗначениеПоляИзЗаголовкаПисьма(
					ПисьмоОтправленоВОтветНаИнфо.ВнутреннийЗаголовок,
					"References");
					
				ДобавитьЗначениеКСтрокеЧерезРазделитель(
					References,
					" ",
					ВКавычках(ПисьмоОтправленоВОтветНаИнфо.ИдентификаторСообщения, "<", ">"));
					
				ПараметрыОтправки.ПоляЗаголовка.Добавить(
					Почта.СформироватьСтруктуруПоляЗаголовка(
						"References", // ИмяПоля
						References, // ЗначениеПоля
						СпособКодированияНеASCIIСимволовИнтернетПочтовогоСообщения.БезКодирования)); // СпособКодирования
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// перенаправление
	Если ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(ПисьмоОбъект) 
		И ПисьмоОбъект.ТипОтвета = Перечисления.ТипыОтвета.ПеренаправлениеПисьма 
		И ЗначениеЗаполнено(ПисьмоОбъект.ПисьмоОснование) 
		И ВстроеннаяПочтаКлиентСервер.ЭтоПисьмо(ПисьмоОбъект.ПисьмоОснование) Тогда 
		
		СтруктураОтправителя = Новый Структура;
		
		Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(ПисьмоОбъект.ПисьмоОснование) Тогда 
			
			ОтправительАдресат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПисьмоОбъект.ПисьмоОснование, "ОтправительАдресат");
			ПредставлениеАдресата = ПолучитьПредставлениеИКонтактАдресата(ОтправительАдресат);
			
			СтруктураОтправителя.Вставить("Адрес", ОтправительАдресат.Адрес);
			СтруктураОтправителя.Вставить("ОтображаемоеИмя", ПредставлениеАдресата.Представление); 
			
		Иначе 	
			
			УчетнаяЗаписьОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПисьмоОбъект.ПисьмоОснование, "УчетнаяЗапись");
			
			СтруктураОтправителя.Вставить("Адрес", УчетнаяЗаписьОснования.АдресЭлектроннойПочты);
			СтруктураОтправителя.Вставить("ОтображаемоеИмя", 
				Справочники.УчетныеЗаписиЭлектроннойПочты.ПолучитьПредставлениеАдреса(УчетнаяЗаписьОснования.Ссылка));
			
		КонецЕсли;	
		ПараметрыОтправки.Отправитель = СтруктураОтправителя;	
		ПараметрыОтправки.ИмяОтправителя = СтруктураОтправителя.ОтображаемоеИмя;	
		
		ДанныеУчетнойЗаписи = Почта.ПолучитьДанныеУчетнойЗаписи(ПисьмоОбъект.УчетнаяЗапись);
		ПараметрыОтправки.ПоляЗаголовка.Добавить(
			Почта.СформироватьСтруктуруПоляЗаголовка(
			"Resent-From", // ИмяПоля
			ДанныеУчетнойЗаписи.АдресЭлектроннойПочты, // ЗначениеПоля
			СпособКодированияНеASCIIСимволовИнтернетПочтовогоСообщения.БезКодирования)); // СпособКодирования
			
		ДатаПеренаправления = ТекущаяДатаСеанса();
		ПараметрыОтправки.ПоляЗаголовка.Добавить(
			Почта.СформироватьСтруктуруПоляЗаголовка(
			"Resent-Date", // ИмяПоля
			Почта.ПеревестиДатуВПолеЗаголовка(ДатаПеренаправления), // ЗначениеПоля
			СпособКодированияНеASCIIСимволовИнтернетПочтовогоСообщения.БезКодирования)); // СпособКодирования	
			
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПисьмоОбъект.Проект) Тогда
		
		// Формирование поля заголовка "X-1C-Project-ID"  - проект письма
		ПараметрыОтправки.ПоляЗаголовка.Добавить(
			Почта.СформироватьСтруктуруПоляЗаголовка(
				"X-1C-Project-ID", // ИмяПоля
				Строка(ПисьмоОбъект.Проект.УникальныйИдентификатор()), // ЗначениеПоля
				СпособКодированияНеASCIIСимволовИнтернетПочтовогоСообщения.БезКодирования)); // СпособКодирования
		
	КонецЕсли;	
			
	Если РегистрыСведений.АвтоматическиеОтветыПоАдресам.ПисьмоЯвляетсяАвтоОтветом(ПисьмоОбъект.Ссылка) Тогда
		
		// Формирование поля заголовка "X-1C-AutoReply"  - признак автоответа
		ПараметрыОтправки.ПоляЗаголовка.Добавить(
			Почта.СформироватьСтруктуруПоляЗаголовка(
				"X-1C-AutoReply", // ИмяПоля
				"true", // ЗначениеПоля
				СпособКодированияНеASCIIСимволовИнтернетПочтовогоСообщения.БезКодирования)); // СпособКодирования
		
	КонецЕсли;		
				
	Возврат ПараметрыОтправки;
	
КонецФункции

Функция ПолучитьМассивИдентификаторовПисем(Знач СтрокаИдентификаторовПисем)
	
	Результат = Новый Массив;
	
	СтрокаИдентификаторовПисем = СтрЗаменить(СтрокаИдентификаторовПисем, "> <", Символы.ПС);
	СтрокаИдентификаторовПисем = СтрЗаменить(СтрокаИдентификаторовПисем, "<", "");
	СтрокаИдентификаторовПисем = СтрЗаменить(СтрокаИдентификаторовПисем, ">", "");
	
	Для Индекс = 1 По СтрДлина(СтрокаИдентификаторовПисем) Цикл
		Строка = СтрПолучитьСтроку(СтрокаИдентификаторовПисем, Индекс);
		Если ЗначениеЗаполнено(Строка) Тогда
			ИдентификаторУрезанный = Лев(СокрЛП(Строка), 200);	
			Результат.Добавить(ИдентификаторУрезанный);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает входящее письмо по идентификатору.
// Если письмо не найдено, возвращает Неопределено.
//
Функция НайтиВходящееПисьмоПоИдентификаторуИУчетнойЗаписи(Идентификатор, УчетнаяЗапись)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВходящееПисьмо.Ссылка,
		|	ВходящееПисьмо.Дата
		|ИЗ
		|	Документ.ВходящееПисьмо КАК ВходящееПисьмо
		|ГДЕ
		|	ВходящееПисьмо.ИдентификаторСообщения = &Идентификатор
		|	И ВходящееПисьмо.УчетнаяЗапись = &УчетнаяЗапись");
		
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить()[0].Ссылка;	
	
КонецФункции

// Возвращает массив писем, найденных по идентификаторам, отсортированный по убыванию даты письма.
//
Функция НайтиИсходящиеПисьмаПоИдентификаторам(МассивИдентификаторовПисем, УчетнаяЗапись)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИсходящееПисьмо.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ИсходящееПисьмо КАК ИсходящееПисьмо
		|ГДЕ
		|	ИсходящееПисьмо.ИдентификаторСообщения В(&МассивИдентификаторовПисем)
		|	И ИсходящееПисьмо.УчетнаяЗапись = &УчетнаяЗапись
		|	И ИсходящееПисьмо.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИсходящееПисьмо.Дата УБЫВ");
	Запрос.УстановитьПараметр("МассивИдентификаторовПисем", МассивИдентификаторовПисем);
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Процедура ЗаполнитьВходящееПисьмоИзСтруктурыПочтовогоСообщения(ПисьмоОбъект, Сообщение, УчетнаяЗапись)
	
	ДатаПолучения = ТекущаяДатаСеанса();
	Если ЗначениеЗаполнено(Сообщение.ДатаПолучения) Тогда
		ДатаПолучения = Сообщение.ДатаПолучения;
	КонецЕсли;
	
	ПисьмоОбъект.Важность = Сообщение.Важность;
	
	ПисьмоОбъект.Дата = ТекущаяДатаСеанса();
	ПисьмоОбъект.ДатаОтправки = Сообщение.ДатаОтправки;
	ПисьмоОбъект.ДатаПолучения = ДатаПолучения;
	
	// Если дата письма старее, чем сутки назад - берем дату из письма.
	Если ЗначениеЗаполнено(ДатаПолучения) 
		И ПисьмоОбъект.Дата > ДатаПолучения
		И ПисьмоОбъект.Дата - ДатаПолучения > 86400 Тогда
		ПисьмоОбъект.Дата = ДатаПолучения;
	КонецЕсли;
	
	Если Год(ПисьмоОбъект.ДатаОтправки) > 3999 Тогда
		ПисьмоОбъект.ДатаОтправки = ТекущаяДатаСеанса();
	КонецЕсли;	
	
	ПисьмоОбъект.ВнутреннийЗаголовок = РаботаСоСтроками.УдалитьНедопустимыеСимволыXML(Сообщение.Заголовок);
	ПисьмоОбъект.ИдентификаторСообщения = РаботаСоСтроками.УдалитьНедопустимыеСимволыXML(Сообщение.ИдентификаторСообщения);
	ПисьмоОбъект.Кодировка = Сообщение.Кодировка;
	ПисьмоОбъект.УведомитьОПрочтении = Сообщение.УведомитьОПрочтении;
	
	Адресат = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПочтовогоАдресата(Сообщение.Отправитель.Адрес, Сообщение.Отправитель.ОтображаемоеИмя);
	ПисьмоОбъект.ОтправительАдресат = Адресат;
	
	СтруктураТекста = ПолучитьСтруктуруТекстаИзСтруктурыПочтовогоСообщения(Сообщение);
	
	УстановитьСодержаниеПисьмаИзСтруктурыТекстаПочтовогоСообщения(ПисьмоОбъект, СтруктураТекста);
	
	ПисьмоОбъект.Размер = Сообщение.Размер;
	ПисьмоОбъект.Тема = РаботаСоСтроками.УдалитьНедопустимыеСимволыXML(Сообщение.Тема);
	
	ЗаполнитьИнтернетПочтовыеАдреса(ПисьмоОбъект.ПолучателиПисьма, Сообщение.Получатели);
	ЗаполнитьИнтернетПочтовыеАдреса(ПисьмоОбъект.ПолучателиКопий, Сообщение.Копии);
	ЗаполнитьИнтернетПочтовыеАдреса(ПисьмоОбъект.ПолучателиОтвета, Сообщение.ОбратныйАдрес);
	ЗаполнитьИнтернетПочтовыеАдреса(ПисьмоОбъект.АдресаУведомленияОПрочтении, Сообщение.АдресаУведомленияОПрочтении);
	
	Если СтруктураТекста.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		КорректироватьВложения(Сообщение, СтруктураТекста.ТекстHTML);
	ИначеЕсли СтруктураТекста.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст Тогда	
		ОчиститьИдентификаторыВложений(Сообщение);
	КонецЕсли;	
	
	ЧислоВложений = ПодсчитатьЧислоВложений(Сообщение);
	ПисьмоОбъект.ЕстьВложения = (ЧислоВложений <> 0);
	
КонецПроцедуры

Процедура ОчиститьИдентификаторыВложений(Сообщение)
	
	Для каждого Вложение Из Сообщение.Вложения Цикл
		
		Если ТипЗнч(Вложение.Данные) = Тип("ДвоичныеДанные") Тогда
			
			Если ЗначениеЗаполнено(Вложение.Идентификатор) Тогда
				
				// очищаем Идентификатор, чтобы не попало в регистр ИдентификаторыВложений 
				Вложение.Идентификатор = ""; 
				
			КонецЕсли;	

		ИначеЕсли ТипЗнч(Вложение.Данные) = Тип("Структура") Тогда
			
			ОчиститьИдентификаторыВложений(Вложение.Данные);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура КорректироватьВложения(Сообщение, ТекстHTML)
	
	Для каждого Вложение Из Сообщение.Вложения Цикл
		
		Если ТипЗнч(Вложение.Данные) = Тип("ДвоичныеДанные") Тогда
			
			Если ЗначениеЗаполнено(Вложение.Идентификатор) Тогда
				
				ВложениеНайденоВHTML = Ложь;
				
				Если СтрНайти(ТекстHTML, "cid:" + Вложение.Идентификатор) <> 0 Тогда 
					ВложениеНайденоВHTML = Истина;
				ИначеЕсли СтрНайти(ТекстHTML, "CID:" + Вложение.Идентификатор) <> 0 Тогда 
					ВложениеНайденоВHTML = Истина;
				ИначеЕсли СтрДлина(Вложение.Идентификатор) > 18 И СтрНайти(ТекстHTML, Вложение.Идентификатор) <> 0 Тогда 
					ВложениеНайденоВHTML = Истина;
				КонецЕсли;	
				
				Если Не ВложениеНайденоВHTML Тогда 
					// Не нашли в тексте HTML ссылки на вложение - очищаем Идентификатор, чтобы не попало в регистр ИдентификаторыВложений. 
					Вложение.Идентификатор = ""; 
				КонецЕсли;			
				
			КонецЕсли;	

		ИначеЕсли ТипЗнч(Вложение.Данные) = Тип("Структура") Тогда
			
			КорректироватьВложения(Вложение.Данные, ТекстHTML);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПодсчитатьЧислоВложений(Сообщение)
	
	ЧислоВложений = 0;
	
	Для каждого Вложение Из Сообщение.Вложения Цикл
		
		Если ТипЗнч(Вложение.Данные) = Тип("ДвоичныеДанные") Тогда
			
			Если Не ЗначениеЗаполнено(Вложение.Идентификатор) Тогда
				ЧислоВложений = ЧислоВложений + 1;
			КонецЕсли;	

		ИначеЕсли ТипЗнч(Вложение.Данные) = Тип("Структура") Тогда
			
			ЧислоЛокальное = ПодсчитатьЧислоВложений(Вложение.Данные);
			ЧислоВложений = ЧислоВложений + ЧислоЛокальное;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЧислоВложений;
	
КонецФункции	

Процедура ВставитьШапкуСообщенияВСтруктуруТекста(СтруктураТекста, Сообщение, ПрефиксНомера)
	
	Отправлено = Формат(Сообщение.ДатаОтправки, "ДФ='dd.MM.yyyy HH:mm'");
	От = ПолучитьПредставлениеОтправителяИзСтруктуры(Сообщение.Отправитель);
	
	Шапка = Новый Структура;
	Шапка.Вставить("НомерПисьма", ПрефиксНомера);
	Шапка.Вставить("Тема", Сообщение.Тема);
	Шапка.Вставить("От", От);
	Шапка.Вставить("Отправлено", Отправлено);
	
	ПолучателиСтрока = ПолучитьПредставлениеПолучателей(Сообщение.Получатели);
	Шапка.Вставить("Кому", ПолучателиСтрока);
	
	ШапкаВложенногоПисьмаПростойТекст = СформироватьШапкуВложенногоПисьмаПростойТекст(Шапка);
	
	СтруктураТекста.Текст = ШапкаВложенногоПисьмаПростойТекст + Символы.ПС + СтруктураТекста.Текст;
	
	Если СтруктураТекста.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		
		ТекстHTMLИнфо = РаботаС_HTML.РазложитьТекстHTML(СтруктураТекста.ТекстHTML);
		
		ШапкаВложенногоПисьмаТекстHTML =
			РаботаС_HTML.ПолучитьHTMLИзТекста(ШапкаВложенногоПисьмаПростойТекст);
		
		ШапкаВложенногоПисьмаТекстHTMLИнфо = РаботаС_HTML.РазложитьТекстHTML(ШапкаВложенногоПисьмаТекстHTML);
		
		СтруктураТекста.ТекстHTML = ТекстHTMLИнфо.Заголовок
			+ ШапкаВложенногоПисьмаТекстHTMLИнфо.Тело
			+ ТекстHTMLИнфо.Тело + ТекстHTMLИнфо.Окончание;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьПредставлениеПолучателей(Получатели)
	
	ПолучателиСтрока = "";
	
	Если ТипЗнч(Получатели) = Тип("Массив") Тогда
		Для Каждого Получатель Из Получатели Цикл
			ПолучательОписание = ПолучитьПредставлениеОтправителяИзСтруктуры(Получатель);	
			Если ПолучателиСтрока <> "" Тогда
				ПолучателиСтрока = ПолучателиСтрока + "; ";
			КонецЕсли;	
			ПолучателиСтрока = ПолучателиСтрока + ПолучательОписание;
		КонецЦикла;	
	КонецЕсли;	
	
	Возврат ПолучателиСтрока;

КонецФункции

Функция ПолучитьПредставлениеОтправителяИзСтруктуры(Отправитель)
	
	ПредставлениеОтправителя = Отправитель.Адрес;
	Если Не ПустаяСтрока(Отправитель.ОтображаемоеИмя) Тогда
		Если Отправитель.ОтображаемоеИмя <> Отправитель.Адрес Тогда
			ПредставлениеОтправителя = Отправитель.ОтображаемоеИмя + " <" + Отправитель.Адрес + ">";
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПредставлениеОтправителя;
	
КонецФункции

Процедура ДобавитьСтруктуруТекстаВложения(СтруктураТекста, СтруктураТекстаДобавляемая)
	
	СтруктураТекста.Текст = СтруктураТекста.Текст + Символы.ПС + Символы.ПС
		+ СтруктураТекстаДобавляемая.Текст;
	
	Если СтруктураТекста.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		
		ТекстHTMLИнфо = РаботаС_HTML.РазложитьТекстHTML(СтруктураТекста.ТекстHTML);
		
		Если СтруктураТекстаДобавляемая.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
			
			ТекстHTMLДобавляемыйИнфо = РаботаС_HTML.РазложитьТекстHTML(СтруктураТекстаДобавляемая.ТекстHTML);
			
		Иначе
			
			ТекстHTMLДобавляемый =
				РаботаС_HTML.ПолучитьHTMLИзТекста(СтруктураТекстаДобавляемая.Текст);
			
			ТекстHTMLДобавляемыйИнфо = РаботаС_HTML.РазложитьТекстHTML(ТекстHTMLДобавляемый);
			
		КонецЕсли;
		
		СтруктураТекста.ТекстHTML = ТекстHTMLИнфо.Заголовок + ТекстHTMLИнфо.Тело
			+ ТекстHTMLДобавляемыйИнфо.Тело + ТекстHTMLИнфо.Окончание;
		
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьШапкуВложенногоПисьмаПростойТекст(Шапка)
	
	Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр(
		"ru ='---Вложенное письмо %1---------------------------------------------------------
			|%2
			|От: %3
			|Отправлено: %4
			|Кому: %5
			|--------------------------------------------------------------------------------'"),
		Шапка.НомерПисьма,
		Шапка.Тема,
		Шапка.От,
		Шапка.Отправлено,
		Шапка.Кому);
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьИнтернетПочтовыеАдреса(ТаблицаАдресов, Адреса)
	
	Для каждого Адрес Из Адреса Цикл
		НайденВТаблице = Ложь;
		Адресат = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПочтовогоАдресата(Адрес.Адрес, Адрес.ОтображаемоеИмя);
		Для Каждого Строка Из ТаблицаАдресов Цикл
			Если Строка.Адресат = Адресат Тогда
				НайденВТаблице = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Не НайденВТаблице Тогда
			ТаблицаАдресовСтрока = ТаблицаАдресов.Добавить();
			ТаблицаАдресовСтрока.Адресат = Адресат;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПрименитьПравилаДляВходящихПисем(ПисьмоОбъект)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПравилаОбработкиПисем.Ссылка
		|ИЗ
		|	Справочник.ПравилаОбработкиПисем КАК ПравилаОбработкиПисем
		|ГДЕ
		|	ПравилаОбработкиПисем.ПометкаУдаления = ЛОЖЬ
		|	И ПравилаОбработкиПисем.УчетнаяЗапись = &УчетнаяЗапись
		|	И ПравилаОбработкиПисем.ДляВходящихПисем = ИСТИНА
		|	И ПравилаОбработкиПисем.ДляИсходящихПисем = ЛОЖЬ
		|	И ПравилаОбработкиПисем.Используется = ИСТИНА
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПравилаОбработкиПисем.Порядок";
	Запрос.УстановитьПараметр("УчетнаяЗапись", ПисьмоОбъект.УчетнаяЗапись);
	Выборка = Запрос.Выполнить().Выбрать();
	ДействияВыполнялись = Ложь;
	НеобходимоЗаписатьПисьмо = Ложь;
	Пока Выборка.Следующий() Цикл
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗначенияУсловийПриОбработкеПисем.Правило,
			|	ЗначенияУсловийПриОбработкеПисем.ПараметрУсловия КАК Параметр,
			|	ЗначенияУсловийПриОбработкеПисем.ВидУсловия КАК Вид,
			|	ЗначенияУсловийПриОбработкеПисем.Порядок КАК Порядок,
			|	ЗначенияУсловийПриОбработкеПисем.ЭтоЗначенияИсключения КАК ЭтоЗначенияИсключения,
			|	NULL КАК ТекстАвтоответа
			|ИЗ
			|	РегистрСведений.ЗначенияУсловийПриОбработкеПисем КАК ЗначенияУсловийПриОбработкеПисем
			|ГДЕ
			|	ЗначенияУсловийПриОбработкеПисем.Правило = &Правило
			|
			|СГРУППИРОВАТЬ ПО
			|	ЗначенияУсловийПриОбработкеПисем.ВидУсловия,
			|	ЗначенияУсловийПриОбработкеПисем.Правило,
			|	ЗначенияУсловийПриОбработкеПисем.ПараметрУсловия,
			|	ЗначенияУсловийПриОбработкеПисем.Порядок,
			|	ЗначенияУсловийПриОбработкеПисем.ЭтоЗначенияИсключения
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЗначенияДействийПриОбработкеПисем.Правило,
			|	ЗначенияДействийПриОбработкеПисем.ПараметрДействия,
			|	ЗначенияДействийПриОбработкеПисем.ВидДействия,
			|	0,
			|	ЛОЖЬ,
			|	ЗначенияДействийПриОбработкеПисем.ТекстАвтоответа
			|ИЗ
			|	РегистрСведений.ЗначенияДействийПриОбработкеПисем КАК ЗначенияДействийПриОбработкеПисем
			|ГДЕ
			|	ЗначенияДействийПриОбработкеПисем.Правило = &Правило
			|
			|УПОРЯДОЧИТЬ ПО
			|	Порядок";
			
		Запрос.УстановитьПараметр("Правило", Выборка.Ссылка);
		ТаблицаПараметров = Запрос.Выполнить().Выгрузить();
				
		Если ПроверитьУсловияПравилаДляВходящих(ПисьмоОбъект, Выборка.Ссылка, ТаблицаПараметров, ПисьмоОбъект, Ложь) 
			И НЕ ПроверитьУсловияПравилаДляВходящих(ПисьмоОбъект, Выборка.Ссылка, ТаблицаПараметров, ПисьмоОбъект, Истина) Тогда
			ПрерватьВыполнениеПравил = ВыполнитьДействияПравилаДляВходящих(ПисьмоОбъект, Выборка.Ссылка, ТаблицаПараметров, ДействияВыполнялись, НеобходимоЗаписатьПисьмо);
			Если ПрерватьВыполнениеПравил Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НеобходимоЗаписатьПисьмо;
	
КонецФункции

Процедура ПрименитьПравилоПриОтправке(ПисьмоОбъект, ПрименятьДействияНаОтправляемоеПисьмо)
	
	Попытка
		Если ТипЗнч(ПисьмоОбъект.Ссылка) = Тип("ДокументСсылка.ИсходящееПисьмо") Тогда
			ПрименитьПравилаДляИсходящихПисем(ПисьмоОбъект, ПрименятьДействияНаОтправляемоеПисьмо);
		КонецЕсли;
	Исключение
		Инфо = ИнформацияОбОшибке();
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка при обработке письма правилами: 
				|%1'"),
			ПодробноеПредставлениеОшибки(Инфо));
		Почта.ЗаписатьОшибкуВЖурналРегистрации(СообщениеОбОшибке, ПисьмоОбъект.Ссылка);
	КонецПопытки;
	
КонецПроцедуры

// Проверяет настройки оповещения о новых письмах у пользователей, ответственных за учетную запись письма, 
// если настройки включены, то выполняется проверка письма на удовлетворение правилам оповещения, если они заданы.
// В случае удовлетворения правилам письмо заносится в регистр ОповещенияОПисьмах 
// Параметры:
//  ПисьмоОбъект - ВходящееПисьмо - проверяемое письмо.
//
Процедура ПрименитьПравилаОповещенияДляВходящихПисем(ПисьмоОбъект)
	
	УстановитьПривилегированныйРежим(Истина);	
	Запрос = Новый Запрос;
	Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВЫБОР
			|		КОГДА СотрудникиПользователей.Пользователь ЕСТЬ NULL
			|			ТОГДА УчетныеЗаписиЭлектроннойПочтыОтветственныеЗаОбработкуПисем.Сотрудник
			|		ИНАЧЕ СотрудникиПользователей.Пользователь
			|	КОНЕЦ КАК Пользователь,
			|	&Письмо КАК Письмо
			|ПОМЕСТИТЬ Пользователи
			|ИЗ
			|	Справочник.УчетныеЗаписиЭлектроннойПочты.ОтветственныеЗаОбработкуПисем КАК УчетныеЗаписиЭлектроннойПочтыОтветственныеЗаОбработкуПисем
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|		ПО УчетныеЗаписиЭлектроннойПочтыОтветственныеЗаОбработкуПисем.Сотрудник = СотрудникиПользователей.Сотрудник
			|ГДЕ
			|	УчетныеЗаписиЭлектроннойПочтыОтветственныеЗаОбработкуПисем.Ссылка = &УчетнаяЗапись
			|	И УчетныеЗаписиЭлектроннойПочтыОтветственныеЗаОбработкуПисем.Ссылка.ВариантИспользования = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияПочты.Встроенная)
			|	И УчетныеЗаписиЭлектроннойПочтыОтветственныеЗаОбработкуПисем.Ссылка.ПометкаУдаления = ЛОЖЬ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Пользователи.Пользователь КАК Пользователь,
			|	ЕСТЬNULL(СведенияОПрочтении.Прочтен, ЛОЖЬ) КАК Прочтен
			|ПОМЕСТИТЬ ПользователиИПрочтение
			|ИЗ
			|	Пользователи КАК Пользователи
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПрочтении КАК СведенияОПрочтении
			|		ПО Пользователи.Пользователь = СведенияОПрочтении.Пользователь
			|			И Пользователи.Письмо = СведенияОПрочтении.Объект
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПользователиИПрочтение.Пользователь КАК Пользователь,
			|	НастройкиОповещенийОПисьмах.ПоказыватьСписокОповещений КАК ПоказыватьСписокОповещений
			|ИЗ
			|	ПользователиИПрочтение КАК ПользователиИПрочтение
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОповещенийОПисьмах КАК НастройкиОповещенийОПисьмах
			|		ПО ПользователиИПрочтение.Пользователь = НастройкиОповещенийОПисьмах.Пользователь
			|			И (&УчетнаяЗапись = НастройкиОповещенийОПисьмах.УчетнаяЗапись)
			|ГДЕ
			|	НастройкиОповещенийОПисьмах.ПоказыватьСписокОповещений
			|	И НЕ ПользователиИПрочтение.Прочтен";
			
	Запрос.УстановитьПараметр("УчетнаяЗапись", ПисьмоОбъект.УчетнаяЗапись);
	Запрос.УстановитьПараметр("Письмо", ПисьмоОбъект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
				
		Пользователь = Выборка.Пользователь;	
					
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	УсловияОповещенийОПисьмах.ВидУсловия,
			|	УсловияОповещенийОПисьмах.ЗначениеУсловия,
			|	УсловияОповещенийОПисьмах.ЭтоИсключение
			|ИЗ
			|	РегистрСведений.УсловияОповещенийОПисьмах КАК УсловияОповещенийОПисьмах
			|ГДЕ
			|	УсловияОповещенийОПисьмах.Пользователь = &Пользователь
			|	И УсловияОповещенийОПисьмах.УчетнаяЗапись = &УчетнаяЗапись";
		
		Запрос.УстановитьПараметр("Пользователь", Пользователь);
		Запрос.УстановитьПараметр("УчетнаяЗапись", ПисьмоОбъект.УчетнаяЗапись);				
		ТаблицаПараметров = Запрос.Выполнить().Выгрузить();
		
		Если ТаблицаПараметров.Количество() > 0 Тогда 
			Если ПроверитьПравилаОповещенияДляВходящих(ПисьмоОбъект, ТаблицаПараметров, ПисьмоОбъект, Ложь) 
				И Не ПроверитьПравилаОповещенияДляВходящих(ПисьмоОбъект, ТаблицаПараметров, ПисьмоОбъект, Истина) Тогда  
				РегистрыСведений.ОповещенияОПисьмах.Записать(ПисьмоОбъект.Ссылка, Пользователь);
			КонецЕсли;
		Иначе 
			РегистрыСведений.ОповещенияОПисьмах.Записать(ПисьмоОбъект.Ссылка, Пользователь);						
		КонецЕсли;	

	КонецЦикла;
	
КонецПроцедуры

// Проверяет письмо на удовлетворение/не удовлетворение заданным правилам/исключениям оповещения о новом письме
// Параметры:
//  ПисьмоОбъектСсылка - ВходящееПисьмо - проверяемое письмо
//  ТаблицаПараметров  - Таблица значений - содержит правила и исключения оповещений о новых письмах
//  РеквизитыПисьма	   - ВходящееПисьмо - реквизиты проверяемого письма
//  Исключения 		   - Булево - истина, если проверяются исключения из правил.
//
// Возвращаемое значение:
//  Истина - Булево - если письмо удовлетворяет заданным правилам оповещения о новом письме.
//
Функция ПроверитьПравилаОповещенияДляВходящих(ПисьмоОбъектСсылка, ТаблицаПараметров, РеквизитыПисьма, Исключения) 
	
	Результат = Не Исключения;
	ПервоеСрабатываниеУсловия = Истина;
	
	Если Исключения = Ложь Тогда
		Связь = Перечисления.ТипыЛогическойСвязи.И;	
	Иначе
		Связь = Перечисления.ТипыЛогическойСвязи.Или;
	КонецЕсли;
	
	Если ТаблицаПараметров.Количество() > 0 Тогда
		Для Каждого ЗначениеПеречисления Из Перечисления.ВидыУсловийОтбораВходящихПисем Цикл

			Отбор = Новый Структура("ВидУсловия, ЭтоИсключение", ЗначениеПеречисления, Исключения);
			МассивСтрок = ТаблицаПараметров.НайтиСтроки(Отбор);
			Если МассивСтрок.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			Если ПервоеСрабатываниеУсловия Тогда
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Истина;
				Иначе 
					Результат = Ложь;
				КонецЕсли;
				ПервоеСрабатываниеУсловия = Ложь;
			КонецЕсли;
			Если ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.ТемаСодержитУказанныеСлова Тогда
				
				РезультатПроверкиУсловия = Ложь;
				Для Каждого Строка Из МассивСтрок Цикл
					РезультатПроверкиУсловия = РезультатПроверкиУсловия 
						ИЛИ Найти(НРег(РеквизитыПисьма.Тема), НРег(Строка.ЗначениеУсловия)) > 0;
				КонецЦикла;
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат Или РезультатПроверкиУсловия;
				КонецЕсли;

			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.ТемаНачинаетсяС Тогда
				
				РезультатПроверкиУсловия = Ложь;
				Для Каждого Строка Из МассивСтрок Цикл
					РезультатПроверкиУсловия = РезультатПроверкиУсловия 
						ИЛИ Найти(НРег(РеквизитыПисьма.Тема), НРег(Строка.ЗначениеУсловия)) = 1;
				КонецЦикла;
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат Или РезультатПроверкиУсловия;
				КонецЕсли;

			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.АдресОтправителяСодержитУказанныеСлова Тогда
				
				РезультатПроверкиУсловия = Ложь;
				ПредставлениеОтправителя = ПолучитьПредставлениеИКонтактАдресата(РеквизитыПисьма.ОтправительАдресат).Представление;
				Для Каждого Строка Из МассивСтрок Цикл
					РезультатПроверкиУсловия = РезультатПроверкиУсловия 
						ИЛИ Найти(НРег(РеквизитыПисьма.ОтправительАдресат.Адрес), НРег(Строка.ЗначениеУсловия)) > 0
						ИЛИ Найти(НРег(РеквизитыПисьма.ОтправительАдресат.Наименование), НРег(Строка.ЗначениеУсловия)) > 0
						ИЛИ Найти(НРег(ПредставлениеОтправителя), НРег(Строка.ЗначениеУсловия)) > 0;
				КонецЦикла;
					
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат Или РезультатПроверкиУсловия;
				КонецЕсли;
								
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.ОтправленоНаУказанныеАдреса Тогда
				
				РезультатПроверкиУсловия = Ложь;
				ТаблицаПолучателей = ПолучитьТаблицуПолучателейКомуКопияУВходящегоПисьма(РеквизитыПисьма.Ссылка);
				Для Каждого Строка Из МассивСтрок Цикл
					
					Для Каждого СтрокаПолучатель Из ТаблицаПолучателей Цикл
						РезультатПроверкиУсловия = РезультатПроверкиУсловия
							ИЛИ Найти(НРег(СтрокаПолучатель.Адрес), НРег(Строка.ЗначениеУсловия)) > 0
							ИЛИ Найти(НРег(СтрокаПолучатель.Наименование), НРег(Строка.ЗначениеУсловия)) > 0
							ИЛИ Найти(НРег(СтрокаПолучатель.Представление), НРег(Строка.ЗначениеУсловия)) > 0;	
						Если РезультатПроверкиУсловия Тогда
							Прервать;
						КонецЕсли;	
					КонецЦикла;
					
					Если РезультатПроверкиУсловия Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;

				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат Или РезультатПроверкиУсловия;
				КонецЕсли;
				

			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.ПолученоВТечениеУказанногоПериода Тогда
		
				РезультатПроверкиУсловия = Ложь;
				Дата1 = Дата(1,1,1);
				Дата2 = Дата(1,1,1);
				
				Если МассивСтрок.Количество() > 0 Тогда
					Дата1 = МассивСтрок[0].ЗначениеУсловия;
					Если ТипЗнч(Дата1) <> Тип("Дата") Тогда
						Дата1 = Дата(1,1,1);
					КонецЕсли;
				конецЕсли;                                                                                                            
				Если МассивСтрок.Количество() > 1 Тогда
					Дата2 = МассивСтрок[1].ЗначениеУсловия;
					Если ТипЗнч(Дата2) <> Тип("Дата") Тогда
						Дата2 = Дата(1,1,1);
					КонецЕсли;
				КонецЕсли;
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат 
						И РеквизитыПисьма.ДатаПолучения >= НачалоДня(Дата1)
						И (ЗначениеЗаполнено(Дата2) И РеквизитыПисьма.ДатаПолучения <= КонецДня(Дата2)
						ИЛИ Не ЗначениеЗаполнено(Дата2));
				Иначе
					Результат = Результат 
						ИЛИ РеквизитыПисьма.ДатаПолучения >= НачалоДня(Дата1)
						И (ЗначениеЗаполнено(Дата2) И РеквизитыПисьма.ДатаПолучения <= КонецДня(Дата2)
						ИЛИ Не ЗначениеЗаполнено(Дата2));
				КонецЕсли;
											
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.ИмеетВложения Тогда
				
				Для Каждого Строка Из МассивСтрок Цикл
					Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
						Результат = Результат                    
							И (РеквизитыПисьма.ЕстьВложения И Строка.ЗначениеУсловия = Истина
							ИЛИ Не РеквизитыПисьма.ЕстьВложения И Строка.ЗначениеУсловия = Ложь);
					Иначе
						Результат = Результат 
							ИЛИ (РеквизитыПисьма.ЕстьВложения И Строка.ЗначениеУсловия = Истина
							ИЛИ Не РеквизитыПисьма.ЕстьВложения И Строка.ЗначениеУсловия = Ложь);
					КонецЕсли;
				КонецЦикла;
				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.АдресованоМне Тогда
				
				ТаблицаПолучателейПисьма = ПолучитьТаблицуПолучателейКомуКопияУВходящегоПисьма(РеквизитыПисьма.Ссылка);
				
				Для Каждого Строка Из МассивСтрок Цикл
					
					РезультатПроверкиУсловия = Ложь;
					Для Каждого СтрокаПолучатель Из ТаблицаПолучателейПисьма Цикл
						РезультатПроверкиУсловия = РезультатПроверкиУсловия 
							ИЛИ СтрокаПолучатель.Адрес = РеквизитыПисьма.УчетнаяЗапись.АдресЭлектроннойПочты;
						Если РезультатПроверкиУсловия Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;
		
					Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
						Результат = Результат 
							И (РезультатПроверкиУсловия И Строка.ЗначениеУсловия = Истина
							ИЛИ Не РезультатПроверкиУсловия И Строка.ЗначениеУсловия = Ложь);
					Иначе
						Результат = Результат 
							ИЛИ (РезультатПроверкиУсловия И Строка.ЗначениеУсловия = Истина
							ИЛИ Не РезультатПроверкиУсловия И Строка.ЗначениеУсловия = Ложь);
					КонецЕсли;
					
				КонецЦикла;
				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.АдресованоТолькоМне Тогда
				
				ТаблицаПолучателейПисьма = ПолучитьТаблицуПолучателейКомуКопияУВходящегоПисьма(РеквизитыПисьма.Ссылка);
				Для Каждого Строка Из МассивСтрок Цикл
					РезультатПроверкиУсловия = Ложь;
					
					Если ТаблицаПолучателейПисьма.Количество() = 1 Тогда
						РезультатПроверкиУсловия = РезультатПроверкиУсловия 
							ИЛИ ТаблицаПолучателейПисьма[0].Адрес = РеквизитыПисьма.УчетнаяЗапись.АдресЭлектроннойПочты;
					КонецЕсли;

					Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
						Результат = Результат
							И (РезультатПроверкиУсловия И Строка.ЗначениеУсловия = Истина
							ИЛИ Не РезультатПроверкиУсловия И Строка.ЗначениеУсловия = Ложь);
					Иначе
						Результат = Результат
							ИЛИ (РезультатПроверкиУсловия И Строка.ЗначениеУсловия = Истина
							ИЛИ Не РезультатПроверкиУсловия И Строка.ЗначениеУсловия = Ложь);
					КонецЕсли;
				КонецЦикла;
			
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.ОтправительВходитВГруппы Тогда	
				
				РезультатПроверкиУсловия = Ложь;
				Запрос = Новый Запрос;
				Запрос.Текст = 
					"ВЫБРАТЬ РАЗРЕШЕННЫЕ
					|	СведенияОбАдресатах.Контакт
					|ИЗ
					|	РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
					|ГДЕ
					|	СведенияОбАдресатах.АдресатСообщения = &АдресатСообщения
					|	И ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Сотрудники)";
					
				Запрос.УстановитьПараметр("АдресатСообщения", РеквизитыПисьма.ОтправительАдресат);
				РезультатЗапроса = Запрос.Выполнить();
				Если Не РезультатЗапроса.Пустой() Тогда
					ВыборкаКонтакты = РезультатЗапроса.Выбрать();
					Пока ВыборкаКонтакты.Следующий() Цикл
						Для Каждого Строка Из МассивСтрок Цикл
							Группа = Строка.ЗначениеУсловия;
							СоставГруппы = ДокументооборотПраваДоступаПовтИсп.ПользователиРабочейГруппы(Группа);
							РезультатПроверкиУсловия = 
								РезультатПроверкиУсловия Или СоставГруппы.Найти(ВыборкаКонтакты.Контакт) <> Неопределено;
							Если РезультатПроверкиУсловия = Истина Тогда
								Прервать;
							КонецЕсли;
						КонецЦикла;
						Если РезультатПроверкиУсловия = Истина Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;		
				КонецЕсли;
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат Или РезультатПроверкиУсловия;
				КонецЕсли;
				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.ПолучательВходитВГруппы Тогда	
				
				РезультатПроверкиУсловия = Ложь;
				ТаблицаПолучателей = ПолучитьТаблицуПолучателейКомуКопияУВходящегоПисьма(РеквизитыПисьма.Ссылка);
				Для Каждого СтрокаПолучатель Из ТаблицаПолучателей Цикл					
					Для Каждого Строка Из МассивСтрок Цикл
						Группа = Строка.ЗначениеУсловия;
						СоставГруппы = ДокументооборотПраваДоступаПовтИсп.ПользователиРабочейГруппы(Группа);
						РезультатПроверкиУсловия = 
							РезультатПроверкиУсловия Или СоставГруппы.Найти(СтрокаПолучатель.Контакт) <> Неопределено;
						Если РезультатПроверкиУсловия = Истина Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;
					Если РезультатПроверкиУсловия = Истина Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат Или РезультатПроверкиУсловия;
				КонецЕсли;
				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.АдресОтправителяВСписке Тогда	
				
				РезультатПроверкиУсловия = Ложь;
				
				СписокАдресовЭлектроннойПочты = Неопределено;
				Если МассивСтрок.Количество() = 1 Тогда
					СписокАдресовЭлектроннойПочты = МассивСтрок[0].ЗначениеУсловия;
				КонецЕсли;	
				
				Если ЗначениеЗаполнено(РеквизитыПисьма.ОтправительАдресат) Тогда
					АдресОтправителя = РеквизитыПисьма.ОтправительАдресат.Адрес;
				КонецЕсли;	
				
				Если ЗначениеЗаполнено(АдресОтправителя) И ЗначениеЗаполнено(СписокАдресовЭлектроннойПочты) Тогда
					
					Для Каждого Строка Из СписокАдресовЭлектроннойПочты.Адреса Цикл
						
						Если НРег(Строка.Адрес) = НРег(НСтр("ru='Все внутренние адресаты'")) Тогда
							
							Запрос = Новый Запрос;
							Запрос.Текст = 
								"ВЫБРАТЬ РАЗРЕШЕННЫЕ
								|	СведенияОбАдресатах.Контакт
								|ИЗ
								|	РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
								|ГДЕ
								|	СведенияОбАдресатах.АдресатСообщения = &АдресатСообщения
								|	И (ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.РолиИсполнителей)
								|	ИЛИ ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Сотрудники))";
								
							Запрос.УстановитьПараметр("АдресатСообщения", РеквизитыПисьма.ОтправительАдресат);
							РезультатЗапроса = Запрос.Выполнить();
							Если Не РезультатЗапроса.Пустой() Тогда
								РезультатПроверкиУсловия = Истина;
								Прервать;
							КонецЕсли;		
							
						Иначе
							
							Если Найти(НРег(АдресОтправителя), НРег(Строка.Адрес)) <> 0 Тогда
								РезультатПроверкиУсловия = Истина;
								Прервать;
							КонецЕсли;	
							
						КонецЕсли;	
						
					КонецЦикла;	
					
				КонецЕсли;	
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат Или РезультатПроверкиУсловия;
				КонецЕсли;
				
			ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыУсловийОтбораВходящихПисем.НаходитсяВПапке Тогда	
				
				РезультатПроверкиУсловия = Ложь;
				ПапкаПисьма = РеквизитыПисьма.Папка;
				Для Каждого Строка Из МассивСтрок Цикл
					РезультатПроверкиУсловия = Строка.ЗначениеУсловия = ПапкаПисьма;
					Если РезультатПроверкиУсловия = Истина Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если Связь = Перечисления.ТипыЛогическойСвязи.И Тогда
					Результат = Результат И РезультатПроверкиУсловия;
				Иначе
					Результат = Результат Или РезультатПроверкиУсловия;
				КонецЕсли;
				
			КонецЕсли;
			
			Если Результат = Ложь И Связь = Перечисления.ТипыЛогическойСвязи.И
				ИЛИ Результат = Истина И Связь = Перечисления.ТипыЛогическойСвязи.Или Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Устанавливает значение реквизита Папка в письме.
// Возвращает: Истина - если удалось установить папку для письма, иначе Ложь.
//
Функция УстановитьПапкуПисьма(ПисьмоОбъект, Папка, СообщениеОбОшибке)
	
	СообщениеОбОшибке = "";
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Если ЗначениеЗаполнено(ПисьмоОбъект.Папка) Тогда
		
		Права = ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(ПисьмоОбъект.Ссылка, ТекущийПользователь);
		Если Не Права.Изменение Тогда
			
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У вас нет прав на изменение письма ""%1"".'"),
				Строка(ПисьмоОбъект));
			
			Возврат Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПисьмоОбъект.Папка = Папка;
	
	Возврат Истина;
	
КонецФункции

Процедура ДобавитьВПерепискуПодчиненныеПисьмаИерархически(Переписка, ТекКореньПереписки)
	
	МассивПисем = Новый Массив;
	ПолучитьПодчиненныеПисьма(ТекКореньПереписки, МассивПисем);
	Для каждого ПодчиненноеПисьмо Из МассивПисем Цикл
		Переписка.Добавить(ПодчиненноеПисьмо);
		ДобавитьВПерепискуПодчиненныеПисьмаИерархически(Переписка, ПодчиненноеПисьмо);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает строку получателей. Если получателей больше 10,
// то выводятся только первые 3 и общее количество.
//
Функция СформироватьСтрокуПолучателейДляПредпросмотра(ТаблицаПолучателей, ТипАдреса)
	
	КоличествоПолучателей = ТаблицаПолучателей.Количество();
	Если КоличествоПолучателей = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	МаксимальноеКоличествоОтображаемыхПолучателей = 10;
	КоличествоОтображаемыхПолучателейПриПревышении = 3;
	Если КоличествоПолучателей > МаксимальноеКоличествоОтображаемыхПолучателей Тогда
		КоличествоОтображаемыхПолучателей = КоличествоОтображаемыхПолучателейПриПревышении;
	Иначе
		КоличествоОтображаемыхПолучателей = КоличествоПолучателей;
	КонецЕсли;
	
	Результат = "";
	МассивАдресатов = Новый Массив;
	Для Индекс = 0 По КоличествоОтображаемыхПолучателей - 1 Цикл
		Получатель = ТаблицаПолучателей[Индекс];
		
		МассивАдресатов.Добавить(Получатель.Адресат);
		
	КонецЦикла;
	Результат = ПолучитьПредставлениеМассиваАдресатовСГиперссылками(МассивАдресатов);
	
	Если КоличествоПолучателей > МаксимальноеКоличествоОтображаемыхПолучателей Тогда
		
		ПодписьПолучателей = ВстроеннаяПочтаКлиентСервер.ПодписьКЧислу(
			КоличествоПолучателей,
			НСтр("ru = 'получатель'"),
			НСтр("ru = 'получателя'"),
			НСтр("ru = 'получателей'"));
			
		СтрокаUrl = "";	
			
		Если ТипАдреса = "Кому" Тогда
			СтрокаUrl = "allrecipientsto";
		Иначе
			СтрокаUrl = "allrecipientscopy";
		КонецЕсли;	
		
		Результат = Результат +
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = ' и другие (всего <a href=""v8doc:mail/%1"">%2 %3</a>)'"),
				СтрокаUrl,
				Формат(КоличествоПолучателей, "ЧГ=0"),
				ПодписьПолучателей);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СформироватьHTMLПредставлениеВходящегоПисьма(
	Письмо,
	УникальныйИдентификаторФормы,
	ВыводитьШапку,
	СокращенныйСписокАдресатов,
	ВключитьРежимРедактированияHTML = Ложь,
	ВыводитьИсториюПереписки = Истина,
	ДляКарточкиВходящегоПисьма = Ложь,
	ДляСохраненияПисьма = Ложь)
	
	РеквизитыПисьма = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Письмо, 
		"ТипТекста, Ссылка, ТекстПисьмаHTMLХранилище, ТекстПисьмаПростойТекстХранилище, Кодировка, ОтправительАдресат, ДатаОтправки, Тема, ПолучателиПисьма, ПолучателиКопий, ПолучателиОтвета, Дата, ВнутреннийЗаголовок, Важность, Комментарий");
		
	ТекстHTML = Документы.ВходящееПисьмо.ПолучитьHTMLПредставлениеСодержанияПисьма(
		РеквизитыПисьма, ДляКарточкиВходящегоПисьма, ДляСохраненияПисьма);
	
	Если РеквизитыПисьма.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		
		ДобавитьНеобходимыеТэгиHTML(ТекстHTML);
		ВставитьКартинкиВТекстHTML(
			ТекстHTML, Письмо, УникальныйИдентификаторФормы,,ДляСохраненияПисьма);
		УдалитьТеги(ТекстHTML, "script");
		УдалитьСвойствоТегаА(ТекстHTML, "onmouseover");
		УдалитьСвойствоТегаА(ТекстHTML, "onmouseout");
		Удалить_position_absolute(ТекстHTML);
		
	КонецЕсли;
	
	Если ВыводитьШапку Тогда
		
		ПредставлениеАдресата = ПолучитьПредставлениеИКонтактОтправителя(
			РеквизитыПисьма.ОтправительАдресат, УникальныйИдентификаторФормы);
		
		ПредставлениеОт = ПредставлениеАдресата.Представление;
		
		ПредставлениеДата = Формат(РеквизитыПисьма.Дата, "ДФ='dd.MM.yyyy HH:mm'");
		
		ПолучателиОтвета = РеквизитыПисьма.ПолучателиОтвета.Выгрузить();
		
		Если СокращенныйСписокАдресатов Тогда
			
			ПредставлениеКому = СформироватьСтрокуПолучателейДляПредпросмотра(
				РеквизитыПисьма.ПолучателиПисьма.Выгрузить(), "Кому");
			
			ПредставлениеКопии = СформироватьСтрокуПолучателейДляПредпросмотра(
				РеквизитыПисьма.ПолучателиКопий.Выгрузить(), "Копия");
	
			ПредставлениеОбратныеАдреса = СформироватьСтрокуПолучателейДляПредпросмотра(
				ПолучателиОтвета, "Обратный адрес");
			
			
		Иначе
			
			ПредставлениеКому = ТаблицаПолучателейВСтроку(РеквизитыПисьма.ПолучателиПисьма.Выгрузить());
			ПредставлениеКопии = ТаблицаПолучателейВСтроку(РеквизитыПисьма.ПолучателиКопий.Выгрузить());
			ПредставлениеОбратныеАдреса = ТаблицаПолучателейВСтроку(ПолучателиОтвета);
			
		КонецЕсли;
		
		ПредставлениеТема = РеквизитыПисьма.Тема;
		
		ПредставлениеАдресата = ПолучитьПредставлениеИКонтактАдресата(
			РеквизитыПисьма.ОтправительАдресат);
		
		Если ВыводитьИсториюПереписки Тогда
			ЧислоПисемВПереписке = ПолучитьКоличествоПисемВПереписке(Письмо);
			ЕстьПисьмаВПереписке = (ЧислоПисемВПереписке > 1);
		Иначе
			ЧислоПисемВПереписке = 0;
			ЕстьПисьмаВПереписке = Ложь;
		КонецЕсли;
		
		Шапка = Новый Структура;
		Шапка.Вставить("От", ПредставлениеОт);
		Шапка.Вставить("Дата", ПредставлениеДата);
		Шапка.Вставить("Кому", ПредставлениеКому);
		Шапка.Вставить("Копии", ПредставлениеКопии);
		Шапка.Вставить("ОбратныеАдреса", ПредставлениеОбратныеАдреса);
		Шапка.Вставить("КоличествоОбратныхАдресов", РеквизитыПисьма.ПолучателиОтвета.Выгрузить().Количество());
		
		Шапка.Вставить("Тема", ПредставлениеТема);
		Шапка.Вставить("ОтправительКонтакт", ПредставлениеАдресата.Контакт);
		Шапка.Вставить("ЧислоПисемВПереписке", ЧислоПисемВПереписке);
		Шапка.Вставить("ЕстьПисьмаВПереписке", ЕстьПисьмаВПереписке);
		Шапка.Вставить("Важность", РеквизитыПисьма.Важность);
		Шапка.Вставить("Комментарий", РеквизитыПисьма.Комментарий);
		
		Если Не ДляКарточкиВходящегоПисьма Тогда
			// если 1 - тут ссылка, если более 1 - Число (строка "2" например)
			Шапка.Вставить("ОписаниеДокументов", ДокументыНаОсновании(РеквизитыПисьма.Ссылка));
		Иначе
			Шапка.Вставить("ОписаниеДокументов", "");
		КонецЕсли;
		
		ВставитьШапкуПисьмаВТекстHTML(ТекстHTML, Шапка, УникальныйИдентификаторФормы,
			ВключитьРежимРедактированияHTML, ДляКарточкиВходящегоПисьма);
		
	КонецЕсли;
		
	ТекстHTML = ОбработкаСтрокиXML.УдалитьНедопустимыеСимволыXML(ТекстHTML);	
	
	Возврат ТекстHTML;
	
КонецФункции

// формирует HTML для фото - только для тонкого клиента
//
Функция ПолучитьТекстHTMLДляФото(Шапка, УникальныйИдентификатор)
	
	ТекстHTML = "";
	
	Если ЗначениеЗаполнено(Шапка.ОтправительКонтакт)
		И ТипЗнч(Шапка.ОтправительКонтакт) <> Тип("СправочникСсылка.РолиИсполнителей") Тогда
		
		ОтображатьФотографииПерсональнаяНастройка =
			ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиПрограммы",
			"ОтображатьФотографииПерсональнаяНастройка",
			Истина);
		
		ОтображатьФотографииОбщаяНастройка = ПолучитьФункциональнуюОпцию("ОтображатьФотографииОбщаяНастройка");	
		ПриложениеЯвляетсяВебКлиентом = ОбщегоНазначенияДокументооборот.ПриложениеЯвляетсяВебКлиентом();
		
		ПолучатьФотографии = Истина;
		
		Если Не ОтображатьФотографииОбщаяНастройка
			Или Не ОтображатьФотографииПерсональнаяНастройка
			Или ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая Тогда
			ПолучатьФотографии = Ложь;
		КонецЕсли;
		
		Если ПолучатьФотографии Тогда
			
			НавигационнаяСсылкаПользователя = ПолучитьНавигационнуюСсылку(
				Шапка.ОтправительКонтакт);
			
			Если Истина Тогда  // ПриложениеРаботаетСВебСервером()
				
				ДвоичныеДанныеФото = РаботаСФотографиями.ПолучитьДвоичныеДанныеФото(
					Шапка.ОтправительКонтакт);
				
				Если ЗначениеЗаполнено(ДвоичныеДанныеФото) И ДвоичныеДанныеФото.Размер() > 0 Тогда	
					
					Подсказка = "";
					Если ТипЗнч(Шапка.ОтправительКонтакт) = Тип("СправочникСсылка.Сотрудники") Тогда
						
						Подсказка = РаботаСФотографиями.СформироватьПодсказку(
							Шапка.ОтправительКонтакт);
						
					КонецЕсли;	
					
					Попытка
						Картинка = Новый Картинка(ДвоичныеДанныеФото);
						Расширение = Строка(Картинка.Формат());
						СтрокаИсточника = "data:image/" + Расширение + ";base64," + Base64Строка(ДвоичныеДанныеФото);
						
						ТекстHTML = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							"<a href=""%1""><img border=""1"" src=""%2"" title=""%3"" height=""70"" style=""border-color: #bdbdbd"" ></a>", 
							"v8doc:" + НавигационнаяСсылкаПользователя,
							СтрокаИсточника,
							Подсказка);
						
					Исключение
						// Двоичные данные могут не соответствовать никакой картинке
					КонецПопытки;
					
				КонецЕсли;
				
			Иначе
				
				ЕстьКартинка = Ложь;
				Фотография = РаботаСФотографиями.ПолучитьАдресФото(
					Шапка.ОтправительКонтакт,
					УникальныйИдентификатор, ЕстьКартинка);
				
				Если ЕстьКартинка Тогда
					
					Подсказка = "";
					Если ТипЗнч(Шапка.ОтправительКонтакт) = Тип("СправочникСсылка.Сотрудники") Тогда
						
						Подсказка = РаботаСФотографиями.СформироватьПодсказку(
							Сотрудники.ЛюбойПользовательСотрудника(
								Шапка.ОтправительКонтакт));
						
					КонецЕсли;	
					
					ТекстHTML = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"<a href=""%1""><img border=""1"" src=""%2"" title=""%3"" height=""70"" style=""border-color: #bdbdbd"" ></a>", 
						НавигационнаяСсылкаПользователя,
						Фотография,
						Подсказка);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТекстHTML;
	
КонецФункции

// Формирует текст шапки письма и вставляет его в ТекстHTML
// сразу после тэга <body>.
//
Процедура ВставитьШапкуПисьмаВТекстHTML(ТекстHTML, Шапка, УникальныйИдентификаторФормы,
	ВключитьРежимРедактированияHTML = Ложь, ДляКарточкиВходящегоПисьма = Ложь)
	
	НРегТекстHTML = НРег(ТекстHTML);
	
	// Нахождение места для вставки текста шапки
	ВставитьПеред = 1;
	ПозицияHTML = СтрНайти(НРегТекстHTML, "<html");
	ПозицияBody = 0;
	Если ПозицияHTML > 0 Тогда
		ПозицияBody = РаботаСоСтроками.НайтиПосле(НРегТекстHTML, "<body", ПозицияHTML);
		Если ПозицияBody > 0 Тогда
			ПозицияЗакрывающейсяСкобки = РаботаСоСтроками.НайтиПосле(НРегТекстHTML, ">", ПозицияBody);
			Если ПозицияЗакрывающейсяСкобки > 0 Тогда
				ВставитьПеред = ПозицияЗакрывающейсяСкобки + 1;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Формирование текста шапки письма
	ШапкаHTML =
		"<table>
		|<tr>
		|<td width=100%>
		|<div class=v8doc-mail-header><b>[Тема]</b></div>
		|<div class=v8doc-mail-header>[От]</div>
		|<div class=v8doc-mail-information>
		|[НадписьКому]: [Кому]<br>
		|[НадписьКопии]: [Копии]<br>
		|[НадписьОбратныеАдреса]: [ОбратныеАдреса]<br>
		|</div>
		|<div class=v8doc-mail-information-date>[Дата]<br></div>
		|<div class=v8doc-mail-information>
		|<a href=""v8doc:mail/correspondencehistory"">[НадписьПереписка]: [Переписка]</a><br>
		|<b>[НадписьКомментарий]:</b> [Комментарий]<br>
		|</div>   
		|<div class=v8doc-mail-information style=""margin-top:8px;"" >
		|[ЕстьДокументыСсылка]
		|</div>
		|</td>
		|<td valign=top>
		|<table cellpadding=""0"" cellspacing=""0""><tr><td> <div style=""font-size:1pt; line-height:1px;""> &nbsp; </div> </td></tr>  
		| <tr><td> [ФотоПользователя] </td></tr> </table> 
		|</td>
		|</tr>
		|</table>
		|<hr>";
		
	ТекстHTMLДляФото = ПолучитьТекстHTMLДляФото(Шапка, УникальныйИдентификаторФормы);
	ШапкаHTML = СтрЗаменить(ШапкаHTML, "[ФотоПользователя]", ТекстHTMLДляФото);
	
	Если ПустаяСтрока(Шапка.Комментарий) Тогда
		ШапкаHTML = СтрЗаменить(ШапкаHTML, Символы.ПС + "<b>[НадписьКомментарий]:</b> [Комментарий]<br>", "");
	КонецЕсли;
	
	Если ПустаяСтрока(Шапка.Кому) Тогда
		ШапкаHTML = СтрЗаменить(ШапкаHTML, Символы.ПС + "[НадписьКому]: [Кому]<br>", "");
	КонецЕсли;
	Если ПустаяСтрока(Шапка.Копии) Тогда
		ШапкаHTML = СтрЗаменить(ШапкаHTML, Символы.ПС + "[НадписьКопии]: [Копии]<br>", "");
	КонецЕсли;
	
	Если ПустаяСтрока(Шапка.ОбратныеАдреса) Тогда
		ШапкаHTML = СтрЗаменить(ШапкаHTML, Символы.ПС + "[НадписьОбратныеАдреса]: [ОбратныеАдреса]<br>", "");
	КонецЕсли;
	
	Если ПустаяСтрока(Шапка.Дата) Или ДляКарточкиВходящегоПисьма = Истина Тогда
		ШапкаHTML = СтрЗаменить(ШапкаHTML, Символы.ПС + "[Дата]<br>", "");
	КонецЕсли;
	Если Не Шапка.ЕстьПисьмаВПереписке Или ДляКарточкиВходящегоПисьма = Истина Тогда
		ШапкаHTML = СтрЗаменить(ШапкаHTML, "<a href=""v8doc:mail/correspondencehistory"">[НадписьПереписка]: [Переписка]</a><br>", "");
	КонецЕсли;
	
	//  ЕстьДокументыСсылка
	Если Не ЗначениеЗаполнено(Шапка.ОписаниеДокументов) Тогда
		ШапкаHTML = СтрЗаменить(ШапкаHTML, Символы.ПС + "[ЕстьДокументыСсылка]", "");
	Иначе
		
		Если ТипЗнч(Шапка.ОписаниеДокументов) <> Тип("Строка") Тогда
			
			СсылкаДокумента = РаботаС_HTML.ПолучитьСсылкуНаПредмет(Шапка.ОписаниеДокументов);	

			ШапкаHTML = СтрЗаменить(ШапкаHTML, "[ЕстьДокументыСсылка]",
				СтрШаблон(НСтр("ru = 'На основании создан: %1<br>'"), 
					СсылкаДокумента));
				
		Иначе		
			
			ДокументСтрока = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = ';%1 документ;;%1 документа;%1 документов;%1 документов'"),
				Шапка.ОписаниеДокументов);
			
			ШапкаHTML = СтрЗаменить(ШапкаHTML, "[ЕстьДокументыСсылка]",
				СтрШаблон(НСтр("ru = '<a href=""v8doc:mail/documentlinks"">На основании создано: %1</a><br>'"), 
					ДокументСтрока));
				
		КонецЕсли; 	
			
	КонецЕсли;
	
	ШапкаHTML = СтрЗаменить(ШапкаHTML, "[НадписьДата]", НСтр("ru = 'Дата'"));
	ШапкаHTML = СтрЗаменить(ШапкаHTML, "[НадписьКому]", НСтр("ru = 'Кому'"));
	ШапкаHTML = СтрЗаменить(ШапкаHTML, "[НадписьКопии]", НСтр("ru = 'Копии'"));
	ШапкаHTML = СтрЗаменить(ШапкаHTML, "[НадписьОбратныеАдреса]", НСтр("ru = 'Обратные адреса'"));
	ШапкаHTML = СтрЗаменить(ШапкаHTML, "[НадписьПереписка]", НСтр("ru = 'Переписка'"));
	
	ШапкаHTML = СтрЗаменить(ШапкаHTML, "[НадписьКомментарий]", НСтр("ru = 'Комментарий'"));
	
	ШапкаHTML = СтрЗаменить(ШапкаHTML, "[От]", Шапка.От);
	ШапкаHTML = СтрЗаменить(ШапкаHTML, "[Дата]", РаботаС_HTML.ЗаменитьСпецСимволыHTML(Шапка.Дата));
	ШапкаHTML = СтрЗаменить(ШапкаHTML, "[Кому]", Шапка.Кому);
	ШапкаHTML = СтрЗаменить(ШапкаHTML, "[Копии]", Шапка.Копии);
	ШапкаHTML = СтрЗаменить(ШапкаHTML, "[ОбратныеАдреса]", Шапка.ОбратныеАдреса);
	ШапкаHTML = СтрЗаменить(ШапкаHTML, "[Переписка]", Формат(Шапка.ЧислоПисемВПереписке, "ЧН=0")
		+ " " + ВстроеннаяПочтаКлиентСервер.ПодписьКЧислуПисемСтрокой(Шапка.ЧислоПисемВПереписке));
	
	ТекстHTMLДляТемы = РаботаС_HTML.ЗаменитьСпецСимволыHTML(Шапка.Тема);
	РаботаС_HTML.ДобавитьТегиКСсылкам(ТекстHTMLДляТемы);
	Если Шапка.Важность = Перечисления.ВажностьПисем.Высокая Тогда 

		ДвоичныеДанныеКартинки = БиблиотекаКартинок.ВажностьВОбластиПросмотра.ПолучитьДвоичныеДанные();
			
		Расширение = "png";
		СтрокаИсточника = "data:image/" + Расширение + ";base64," + Base64Строка(ДвоичныеДанныеКартинки);
				
		ТекстHTMLВажности = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<img border=""0"" src=""%1"" title=""Высокая важность""> &nbsp;", 
			СтрокаИсточника);
		
		ТекстHTMLДляТемы = ТекстHTMLВажности + ТекстHTMLДляТемы;
		
	КонецЕсли;	
	ШапкаHTML = СтрЗаменить(ШапкаHTML, "[Тема]", ТекстHTMLДляТемы);
	
	ТекстHTMLДляКомментария= РаботаС_HTML.ЗаменитьСпецСимволыHTML(Шапка.Комментарий);
	РаботаС_HTML.ДобавитьТегиКСсылкам(ТекстHTMLДляКомментария);
	ШапкаHTML = СтрЗаменить(ШапкаHTML, "[Комментарий]", ТекстHTMLДляКомментария);
	
	// Нахождение места для вставки конца div
	ВставитьDIVПеред = 1;
	ВставитьDIVПеред = СтрНайти(НРегТекстHTML, "</body");
	
	// Вставка
	Если (ВставитьDIVПеред <> 0) И ВключитьРежимРедактированияHTML Тогда
		
		ТекстHTML = Лев(ТекстHTML, ВставитьПеред - 1) + ШапкаHTML 
			+ "<div contentEditable=true>" +  Сред(ТекстHTML, ВставитьПеред, ВставитьDIVПеред - ВставитьПеред)
			+ "</div>" + Сред(ТекстHTML, ВставитьDIVПеред);
			
	Иначе
			
		ТекстHTML = Лев(ТекстHTML, ВставитьПеред - 1) + ШапкаHTML + Сред(ТекстHTML, ВставитьПеред);
		
	КонецЕсли;
	
	Если ПозицияBody <> 0 Тогда
		ТекстHTML = СтрЗаменить(ТекстHTML, "<body", "<body style=""margin-top:1px; padding-top:1px""");
		ТекстHTML = СтрЗаменить(ТекстHTML, "<BODY", "<BODY style=""margin-top:1px; padding-top:1px""");
	КонецЕсли;	
	
КонецПроцедуры

// Формирует и возвращает HTML текст письма. Если в тексте есть ссылки на картинки,
// они загружаются во временное хранилище и ссылки в HTML
// заменяются на навигационные ссылки картинок во временном хранилище.
//
Функция СформироватьHTMLПредставлениеИсходящегоПисьма(
	Письмо,
	УникальныйИдентификаторФормы,
	ВыводитьШапку,
	СокращенныйСписокАдресатов,
	ВключитьРежимРедактированияHTML = Ложь,
	ВыводитьИсториюПереписки = Истина,
	ДляКарточкиВходящегоПисьма = Ложь,
	ДляСохраненияПисьма = Ложь)
	
	РеквизитыПисьма = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Письмо, 
		"ТипТекста, Ссылка, ТекстПисьмаHTMLХранилище, ТекстХранилище, Кодировка, ОтправительКонтакт, ДатаОтправки, Тема, ПолучателиПисьма, ПолучателиКопий, ПолучателиСкрытыхКопий, ПолучателиОтвета, УчетнаяЗапись, Дата, Важность, Комментарий");
		
	ТекстHTML = Документы.ИсходящееПисьмо.ПолучитьHTMLПредставлениеСодержанияПисьма(РеквизитыПисьма, ДляСохраненияПисьма);
	
	Если РеквизитыПисьма.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		
		ДобавитьНеобходимыеТэгиHTML(ТекстHTML);
		ВставитьКартинкиВТекстHTML(
			ТекстHTML, Письмо, УникальныйИдентификаторФормы,,ДляСохраненияПисьма);
		УдалитьТеги(ТекстHTML, "script");
		УдалитьСвойствоТегаА(ТекстHTML, "onmouseover");
		УдалитьСвойствоТегаА(ТекстHTML, "onmouseout");
		Удалить_position_absolute(ТекстHTML);
		
	КонецЕсли;
	
	Если ВыводитьШапку Тогда
		
		ДанныеУчетнойЗаписи = Почта.ПолучитьДанныеУчетнойЗаписи(РеквизитыПисьма.УчетнаяЗапись);		
		ОтправительОтображаемоеИмя = ДанныеУчетнойЗаписи.ИмяПользователя;
				
		ПредставлениеОт = РаботаС_HTML.ЗаменитьСпецСимволыHTML(
			ПолучитьПочтовоеПредставлениеКонтакта(
			Сотрудники.ЛюбойПользовательСотрудника(Письмо.ОтправительКонтакт), ""));
		
		ПредставлениеДата = Формат(РеквизитыПисьма.Дата, "ДФ='dd.MM.yyyy HH:mm'");
		
		Если СокращенныйСписокАдресатов Тогда
			ПредставлениеКому = СформироватьСтрокуПолучателейДляПредпросмотра(РеквизитыПисьма.ПолучателиПисьма.Выгрузить(), "Кому");
			ПредставлениеКопии = СформироватьСтрокуПолучателейДляПредпросмотра(РеквизитыПисьма.ПолучателиКопий.Выгрузить(), "Копия");

			ПредставлениеОбратныеАдреса = СформироватьСтрокуПолучателейДляПредпросмотра(РеквизитыПисьма.ПолучателиОтвета.Выгрузить(), "Обратный адрес");
			
		Иначе
			ПредставлениеКому = ТаблицаПолучателейВСтроку(РеквизитыПисьма.ПолучателиПисьма.Выгрузить());
			ПредставлениеКопии = ТаблицаПолучателейВСтроку(РеквизитыПисьма.ПолучателиКопий.Выгрузить());
			
			ПредставлениеОбратныеАдреса = ТаблицаПолучателейВСтроку(РеквизитыПисьма.ПолучателиОтвета.Выгрузить());
		КонецЕсли;
		
		ПредставлениеТема = РеквизитыПисьма.Тема;
		
		Если ВыводитьИсториюПереписки Тогда
			ЧислоПисемВПереписке = ПолучитьКоличествоПисемВПереписке(Письмо);
			ЕстьПисьмаВПереписке = (ЧислоПисемВПереписке > 1);
		Иначе
			ЧислоПисемВПереписке = 0;
			ЕстьПисьмаВПереписке = Ложь;
		КонецЕсли;
		
		Шапка = Новый Структура;
		Шапка.Вставить("От", ПредставлениеОт);
		Шапка.Вставить("Дата", ПредставлениеДата);
		Шапка.Вставить("Кому", ПредставлениеКому);
		Шапка.Вставить("Копии", ПредставлениеКопии);
		
		Шапка.Вставить("ОбратныеАдреса", ПредставлениеОбратныеАдреса);
		Шапка.Вставить("КоличествоОбратныхАдресов", РеквизитыПисьма.ПолучателиОтвета.Выгрузить().Количество());
		
		Шапка.Вставить("Тема", ПредставлениеТема);
		Шапка.Вставить("ОтправительКонтакт", РеквизитыПисьма.ОтправительКонтакт);
		Шапка.Вставить("ЧислоПисемВПереписке", ЧислоПисемВПереписке);
		Шапка.Вставить("ЕстьПисьмаВПереписке", ЕстьПисьмаВПереписке);
		Шапка.Вставить("Важность", РеквизитыПисьма.Важность);
		Шапка.Вставить("Комментарий", РеквизитыПисьма.Комментарий);
		
		// если 1 - тут ссылка, если более 1 - Число (строка "2" например)
		Шапка.Вставить("ОписаниеДокументов", ДокументыНаОсновании(РеквизитыПисьма.Ссылка));
		
		ВставитьШапкуПисьмаВТекстHTML(ТекстHTML, Шапка, УникальныйИдентификаторФормы,
			ВключитьРежимРедактированияHTML, ДляКарточкиВходящегоПисьма);
		
	КонецЕсли;
		
	ТекстHTML = ОбработкаСтрокиXML.УдалитьНедопустимыеСимволыXML(ТекстHTML);	
	
	Возврат ТекстHTML;
	
КонецФункции

Процедура ДобавитьСсылкиККартинкам(ТекстHTML, АбсолютнаяСсылкаНаКартинку, ФайлПисьмаСсылка)
	
	ТекстТега = ВыделитьТегImg(ТекстHTML, АбсолютнаяСсылкаНаКартинку);
	
	Если Не ПустаяСтрока(ТекстТега) Тогда
		
		НавигационнаяСсылкаФайла = "v8doc:" + ПолучитьНавигационнуюСсылку(ФайлПисьмаСсылка);
		
		ТекстТегаНРег = НРег(ТекстТега);
		ТекстТегаИзмененный = ТекстТега;
		Если СтрНайти(ТекстТегаНРег, "border") = 0 Тогда
			ТекстТегаИзмененный = СтрЗаменить(ТекстТегаИзмененный, "<img", "<img border=""0""");
			ТекстТегаИзмененный = СтрЗаменить(ТекстТегаИзмененный, "<IMG", "<IMG border=""0""");
		КонецЕсли;
		
		Расширение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФайлПисьмаСсылка, "ТекущаяВерсияРасширение");
		ТекстТегаИзмененный = Лев(ТекстТегаИзмененный, СтрДлина(ТекстТегаИзмененный) - 1) + " data-ext=""" + Расширение + """>";
		
		НовыйТекстТега = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<a href=""%1"">%2</a>", 
			НавигационнаяСсылкаФайла,
			ТекстТегаИзмененный);
			
		ТекстHTML = СтрЗаменить(ТекстHTML, ТекстТега, НовыйТекстТега);	
		
	КонецЕсли;
	
КонецПроцедуры	

Процедура УдалитьДублиВИдентификаторахКартинокПисьма(ИдентификаторыКартинокПисьма, ИсточникФайла)
	
	Для Каждого Строка Из ИдентификаторыКартинокПисьма Цикл
		
		ОписаниеИсточника = Строка.Значение;
		Если ОписаниеИсточника.ИсточникФайла = ИсточникФайла Тогда
			ИдентификаторыКартинокПисьма.Удалить(Строка);
			ВОзврат;
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры	

// Возвращает текст запроса для получения таблицы адресатов для автоподбора
// по личному списку автоподбора пользователя (регистр сведений ОбъектыДляАвтоподбораАдресатов)
//
// Возвращаемое значение:
//   - Строка - текст запроса
//
Функция ТекстЗапросаВычисленияАдресатовДляАвтоподбора()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Адресат
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Действует = ИСТИНА
		|	И Сотрудники.ПометкаУдаления = ЛОЖЬ
		|	И Сотрудники.Подразделение В ИЕРАРХИИ
		|		(ВЫБРАТЬ
		|			ОбъектыДляАвтоподбораАдресатов.ОбъектДанных
		|		ИЗ
		|			РегистрСведений.ОбъектыДляАвтоподбораАдресатов КАК ОбъектыДляАвтоподбораАдресатов
		|		ГДЕ
		|			ОбъектыДляАвтоподбораАдресатов.Владелец = &ТекущийПользователь
		|			И ОбъектыДляАвтоподбораАдресатов.ОбъектДанных ССЫЛКА Справочник.СтруктураПредприятия)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ОбъектыДляАвтоподбораАдресатов.ОбъектДанных
		|ИЗ
		|	РегистрСведений.ОбъектыДляАвтоподбораАдресатов КАК ОбъектыДляАвтоподбораАдресатов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО ОбъектыДляАвтоподбораАдресатов.ОбъектДанных = Сотрудники.Ссылка
		|ГДЕ
		|	ОбъектыДляАвтоподбораАдресатов.Владелец = &ТекущийПользователь
		|	И Сотрудники.Действует = ИСТИНА
		|	И Сотрудники.ПометкаУдаления = ЛОЖЬ";
		
	Возврат ТекстЗапроса;
		
КонецФункции

// Дополняет данные выбора адресатов описанием отсутствий.
//
// Параметры:
//  ДанныеВыбора - СписокЗначений - Данные выбора адресатов.
//
Процедура ДополнитьДанныеВыбораОписаниемОтсутствий(ДанныеВыбора)
	
	// Дополним информацией об отсутствии.
	ВыбранныеКонтакты = Новый Массив;
	Для Каждого ЭлементСписка Из ДанныеВыбора Цикл
		
		Если Не ТипЗнч(ЭлементСписка.Значение) = Тип("Структура")
			Или Не ЭлементСписка.Значение.Свойство("Контакт") Тогда
			Продолжить;
		КонецЕсли;
		
		ВыбранныеКонтакты.Добавить(ЭлементСписка.Значение.Контакт);
		
	КонецЦикла;
	
	ОписанияОтсутствий = Отсутствия.ПолучитьОписанияТекущихОтсутствийСотрудников(ВыбранныеКонтакты);
	Для Каждого ЭлементСписка Из ДанныеВыбора Цикл
		
		Если Не ТипЗнч(ЭлементСписка.Значение) = Тип("Структура") Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеОтсутствия = Неопределено;
		Если ЭлементСписка.Значение.Свойство("Контакт") Тогда
			ОписаниеОтсутствия = ОписанияОтсутствий[ЭлементСписка.Значение.Контакт];
		КонецЕсли;
		
		ЭлементСписка.Значение.Вставить("ОписаниеОтсутствия", ОписаниеОтсутствия);
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает понятное для пользователя имя типа группы
//
// Параметры
//  Группа - группа пользователей, контрагентов, физлиц, 
//		личных адресатов, групп Мои контакты, Отдел
Функция ПолучитьИмяТипа(Группа)

	ИмяТипа = Строка(ТипЗнч(Группа));
	
	Если ТипЗнч(Группа) = Тип("СправочникСсылка.РабочиеГруппы") Тогда
		ИмяТипа = НСтр("ru = 'рабочая группа'");
	ИначеЕсли ТипЗнч(Группа) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
		ИмяТипа = НСтр("ru = 'отдел'");
	ИначеЕсли ТипЗнч(Группа) = Тип("СправочникСсылка.ГруппыЛичныхАдресатов") Тогда
		ИмяТипа = НСтр("ru = 'группа личных адресатов'");
	ИначеЕсли ТипЗнч(Группа) 
		= Тип("СправочникСсылка.ГруппыКонтактовПользователей") Тогда
		ИмяТипа = НСтр("ru = 'группа «Мои контакты»'");
	ИначеЕсли ТипЗнч(Группа) = Тип("СправочникСсылка.Контрагенты") Тогда
		ИмяТипа = НСтр("ru = 'группа контрагентов'");
	ИначеЕсли ТипЗнч(Группа) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		ИмяТипа = НСтр("ru = 'группа физических лиц'");
	КонецЕсли;	
	
	Возврат ИмяТипа;

КонецФункции				

// Возвращает текст запроса для поиска групп по подстроке
Функция ПолучитьЗапросГрупп()
	
	Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	        |	СтруктураПредприятия.Ссылка КАК Ссылка,
	        |	СтруктураПредприятия.Наименование КАК Наименование
	        |ИЗ
	        |	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	        |ГДЕ
	        |	СтруктураПредприятия.ПометкаУдаления = ЛОЖЬ
	        |	И СтруктураПредприятия.Наименование ПОДОБНО &Наименование
	        |
	        |ОБЪЕДИНИТЬ ВСЕ
	        |
	        |ВЫБРАТЬ
	        |	РабочиеГруппы.Ссылка,
	        |	РабочиеГруппы.Наименование
	        |ИЗ
	        |	Справочник.РабочиеГруппы КАК РабочиеГруппы
	        |ГДЕ
	        |	РабочиеГруппы.ПометкаУдаления = ЛОЖЬ
	        |	И РабочиеГруппы.Наименование ПОДОБНО &Наименование
	        |
	        |ОБЪЕДИНИТЬ ВСЕ
	        |
	        |ВЫБРАТЬ
	        |	ГруппыЛичныхАдресатов.Ссылка,
	        |	ГруппыЛичныхАдресатов.Наименование
	        |ИЗ
	        |	Справочник.ГруппыЛичныхАдресатов КАК ГруппыЛичныхАдресатов
	        |ГДЕ
	        |	ГруппыЛичныхАдресатов.ПометкаУдаления = ЛОЖЬ
	        |	И ГруппыЛичныхАдресатов.Наименование ПОДОБНО &Наименование
	        |	И ГруппыЛичныхАдресатов.Сотрудник В (&СотрудникиПользователя)
	        |
	        |ОБЪЕДИНИТЬ ВСЕ
	        |
	        |ВЫБРАТЬ
	        |	ГруппыКонтактовПользователей.Ссылка,
	        |	ГруппыКонтактовПользователей.Наименование
	        |ИЗ
	        |	Справочник.ГруппыКонтактовПользователей КАК ГруппыКонтактовПользователей
	        |ГДЕ
	        |	ГруппыКонтактовПользователей.ПометкаУдаления = ЛОЖЬ
	        |	И ГруппыКонтактовПользователей.Наименование ПОДОБНО &Наименование
	        |	И ГруппыКонтактовПользователей.Родитель <> &Родитель
	        |
	        |ОБЪЕДИНИТЬ ВСЕ
	        |
	        |ВЫБРАТЬ
	        |	Контрагенты.Ссылка,
	        |	Контрагенты.Наименование
	        |ИЗ
	        |	Справочник.Контрагенты КАК Контрагенты
	        |ГДЕ
	        |	Контрагенты.ПометкаУдаления = ЛОЖЬ
	        |	И Контрагенты.Наименование ПОДОБНО &Наименование
	        |	И Контрагенты.ЭтоГруппа = ИСТИНА
	        |
	        |ОБЪЕДИНИТЬ ВСЕ
	        |
	        |ВЫБРАТЬ
	        |	ФизическиеЛица.Ссылка,
	        |	ФизическиеЛица.Наименование
	        |ИЗ
	        |	Справочник.ФизическиеЛица КАК ФизическиеЛица
	        |ГДЕ
	        |	ФизическиеЛица.ПометкаУдаления = ЛОЖЬ
	        |	И ФизическиеЛица.Наименование ПОДОБНО &Наименование
	        |	И ФизическиеЛица.ЭтоГруппа = ИСТИНА";
	
	Возврат Текст;
	
КонецФункции

Функция ПолучитьФорматированноеПредставление(Представление, МассивСлов)
	
	ФорматированноеПредставление = Представление;
	
	МассивПодсветки = Новый Массив;
	МассивСловПредставления = ВстроеннаяПочтаКлиентСервер.СтрокуПочтовогоАдресаВСтруктурыСлов(Представление);
	
	Для Каждого СтруктураСлова Из МассивСловПредставления Цикл
		
		СловоПредставления = НРег(СтруктураСлова.Слово);
		
		Для Каждого СловоПоиска Из МассивСлов Цикл
			
			ПозицияНачала = Найти(СловоПредставления, НРег(СловоПоиска));
			Если ПозицияНачала = 1 Тогда
				МассивПодсветки.Добавить(
					Новый Структура("Начало, ДлинаСлова, ДлинаПодсвеченного", 
					СтруктураСлова.Позиция, СтрДлина(СловоПредставления), СтрДлина(СловоПоиска)));
			КонецЕсли;	
			
		КонецЦикла;	
		
	КонецЦикла;	
	
	Если МассивПодсветки.Количество() <> 0 Тогда
		
		ФорматированноеПредставление = "";
		ПозицияКонцаПредыдущегоСлова = 1;
		
		Для Каждого Подсветка Из МассивПодсветки Цикл			
			
			ПозицияНачала = Подсветка.Начало;
			Длина = Подсветка.ДлинаПодсвеченного;
			
			СтрокаДо =    Сред(Представление, 
				ПозицияКонцаПредыдущегоСлова, ПозицияНачала - ПозицияКонцаПредыдущегоСлова);
			СтрокаВыделенная = Сред(Представление, ПозицияНачала, Длина);
			
			ФорматированноеПредставление = Новый ФорматированнаяСтрока(
				ФорматированноеПредставление,
				СтрокаДо, 
				Новый ФорматированнаяСтрока(СтрокаВыделенная, , WebЦвета.Красный) 
				);
				
			ПозицияКонцаПредыдущегоСлова = ПозицияНачала + Длина;	
			
		КонецЦикла;	
		
		Если ПозицияКонцаПредыдущегоСлова <= СтрДлина(Представление) Тогда
			
			СтрокаПосле = Сред(Представление, ПозицияКонцаПредыдущегоСлова);
			
			ФорматированноеПредставление = Новый ФорматированнаяСтрока(
				ФорматированноеПредставление,
				СтрокаПосле);
			
		КонецЕсли;	
			
	КонецЕсли;	
	
	Возврат ФорматированноеПредставление;
	
КонецФункции	

// Находит адресата по уже известной ссылке
//
// Параметры:
//  ГУИД - УникальныйИдентификатор
//
// Возвращаемое значение:
//  СправочникСсылка.АдресатыПочтовыхСообщений
//
Функция НайтиПочтовогоАдресатаПоСсылке(ГУИД)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АдресатыПочтовыхСообщений.Ссылка
		|ИЗ
		|	Справочник.АдресатыПочтовыхСообщений КАК АдресатыПочтовыхСообщений
		|ГДЕ
		|	АдресатыПочтовыхСообщений.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	АдресатыПочтовыхСообщений.Код";
	
	Запрос.УстановитьПараметр("Ссылка", Справочники.АдресатыПочтовыхСообщений.ПолучитьСсылку(ГУИД));
	РезультатЗапроса = Запрос.Выполнить();
	Адресат = Справочники.АдресатыПочтовыхСообщений.ПустаяСсылка();
	
	Если РезультатЗапроса.Пустой() Тогда			
		
		Возврат Адресат;
		
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			Адресат = Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Адресат;
		
КонецФункции	

// Найти в справочнике АдресатыПочтовыхСообщений по адресу - не создавая 
Функция НайтиПочтовогоАдресата(Адрес)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	АдресатыПочтовыхСообщений.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.АдресатыПочтовыхСообщений КАК АдресатыПочтовыхСообщений
		|ГДЕ
		|	АдресатыПочтовыхСообщений.Адрес = &Адрес
		|
		|УПОРЯДОЧИТЬ ПО
		|	АдресатыПочтовыхСообщений.Код";
	
	Запрос.УстановитьПараметр("Адрес", Адрес);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Справочники.АдресатыПочтовыхСообщений.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции	

// Результат (ТаблицаЗначений)
// - Контрагент (СправочникСсылка.Контрагенты)
// - КонтактноеЛицо (СправочникСсылка.КонтактныеЛица)
//
// Параметры
// - АдресЭлектроннойПочты (Строка) Адрес электронной почты
//
Функция ПолучитьКонтрагентовПоАдресуЭлектроннойПочты(АдресЭлектроннойПочты)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	КонтрагентыКонтактнаяИнформация.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ Контрагенты
		|ИЗ
		|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
		|ГДЕ
		|	КонтрагентыКонтактнаяИнформация.АдресЭП = &АдресЭП
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КонтактныеЛицаКонтактнаяИнформация.Ссылка.Владелец
		|ИЗ
		|	Справочник.КонтактныеЛица.КонтактнаяИнформация КАК КонтактныеЛицаКонтактнаяИнформация
		|ГДЕ
		|	КонтактныеЛицаКонтактнаяИнформация.АдресЭП = &АдресЭП
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Контрагент,
		|	КонтактныеЛицаКонтактнаяИнформация.Ссылка КАК КонтактноеЛицо
		|ИЗ
		|	Контрагенты КАК Контрагенты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица.КонтактнаяИнформация КАК КонтактныеЛицаКонтактнаяИнформация
		|		ПО Контрагенты.Ссылка = КонтактныеЛицаКонтактнаяИнформация.Ссылка.Владелец
		|			И (КонтактныеЛицаКонтактнаяИнформация.АдресЭП = &АдресЭП)");
	Запрос.УстановитьПараметр("АдресЭП", АдресЭлектроннойПочты);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ОтправитьПисьмаПоВнутреннейМаршрутизации(СообщенияОбОшибках, ПараметрыЛогирования) 
	
	Если Не ВстроеннаяПочтаСерверПовтИсп.ИспользоватьВнутреннююМаршрутизацию() Тогда 
		Возврат;
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВремяНачала = ТекущаяДатаСеанса();
	
	ОтправитьВсеПисьмаПоВнутреннейМаршрутизации(ТекущаяДатаСеанса(), СообщенияОбОшибках, ПараметрыЛогирования);
	
	ВремяОкончания = ТекущаяДатаСеанса();
	ТекстЛога = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Длительность сеанса: %1 сек.'"),
		ВремяОкончания - ВремяНачала);
	
КонецПроцедуры

Процедура ОтправитьВсеПисьмаПоВнутреннейМаршрутизации(Знач ТекДата, СообщенияОбОшибках, ПараметрыЛогирования)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПодготовленныеКОтправкеПисьма.УчетнаяЗапись КАК УчетнаяЗапись,
		|	ПодготовленныеКОтправкеПисьма.Письмо КАК Письмо
		|ИЗ
		|	РегистрСведений.ПодготовленныеКОтправкеПисьма КАК ПодготовленныеКОтправкеПисьма
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
		|		ПО ПодготовленныеКОтправкеПисьма.УчетнаяЗапись = УчетныеЗаписиЭлектроннойПочты.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтложеннойОтправкиПисем КАК НастройкиОтложеннойОтправкиПисем
		|		ПО ПодготовленныеКОтправкеПисьма.Автор = НастройкиОтложеннойОтправкиПисем.Пользователь
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НеотправленныеИсходящиеПисьма КАК НеОтправленныеИсходящиеПисьма
		|		ПО ПодготовленныеКОтправкеПисьма.Письмо = НеОтправленныеИсходящиеПисьма.Письмо
		|			И (НеОтправленныеИсходящиеПисьма.ОсталосьПопытокОтправки = 0)
		|ГДЕ
		|	НЕ УчетныеЗаписиЭлектроннойПочты.ПометкаУдаления
		|	И УчетныеЗаписиЭлектроннойПочты.ВариантИспользования = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияПочты.Встроенная)
		|	И ДОБАВИТЬКДАТЕ(ПодготовленныеКОтправкеПисьма.ПодготовленоКОтправке, СЕКУНДА, ЕСТЬNULL(НастройкиОтложеннойОтправкиПисем.Задержка, 0)) <= &ТекДата
		|	И НеОтправленныеИсходящиеПисьма.Письмо ЕСТЬ NULL 
		|	И ПодготовленныеКОтправкеПисьма.ОтправкаОтменена = ЛОЖЬ
		|	И &ИспользоватьВнутреннююМаршрутизацию
		|	И ПодготовленныеКОтправкеПисьма.ВидМаршрутизации = ЗНАЧЕНИЕ(Перечисление.ВидыМаршрутизацииПисем.Внутренняя)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДОБАВИТЬКДАТЕ(ПодготовленныеКОтправкеПисьма.ПодготовленоКОтправке, СЕКУНДА, ЕСТЬNULL(НастройкиОтложеннойОтправкиПисем.Задержка, 0))");
		
	Запрос.УстановитьПараметр("ИспользоватьВнутреннююМаршрутизацию", Константы.ИспользоватьВнутреннююМаршрутизацию.Получить());	
	Запрос.УстановитьПараметр("ТекДата", ТекДата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;

	Таблица = РезультатЗапроса.Выгрузить();	
	
	Для Каждого Строка Из Таблица Цикл
		
		ОтправитьПисьмоПоВнутреннейМаршрутизации(Строка.Письмо, Строка.УчетнаяЗапись, СообщенияОбОшибках, ПараметрыЛогирования);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПисьмоГотовоКОтправкеПоВнутреннейМаршрутизации(ПисьмоОбъект, НеНужноПисатьОшибкуДоставки, ТекстСообщения)
	
	ТекДата = ТекущаяДатаСеанса();
	
	Если ПисьмоОбъект.ПометкаУдаления Тогда
		ТекстСообщения = НСтр("ru = 'Письмо помечено на удаление'");
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеУчетнойЗаписи = Почта.ПолучитьДанныеУчетнойЗаписи(ПисьмоОбъект.УчетнаяЗапись);
	
	Если ДанныеУчетнойЗаписи.ПометкаУдаления Тогда
		ТекстСообщения = НСтр("ru = 'Учетная запись помечена на удаление'");
		Возврат Ложь;
	КонецЕсли;
	
	Если ДанныеУчетнойЗаписи.ВариантИспользования <> Перечисления.ВариантыИспользованияПочты.Встроенная Тогда
		ТекстСообщения = НСтр("ru = 'Учетная запись не используется для встроенной почты'");
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ДанныеУчетнойЗаписи.ИспользоватьДляОтправки Тогда
		ТекстСообщения = НСтр("ru = 'Учетная запись не используется для отправки'");
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПисьмоОбъект.ПодготовленоКОтправке) Тогда
		// Пользователь мог успеть открыть карточку письма и сбросить признак ПодготовленоКОтправке - это не ошибка.
		НеНужноПисатьОшибкуДоставки = Истина; 
		Возврат Ложь;
	КонецЕсли;
	
	Если ПисьмоОбъект.ОтправкаОтменена Тогда 
		ТекстСообщения = НСтр("ru = 'Отправка письма отменена'");
		Возврат Ложь;
	КонецЕсли;
	
	Задержка = РегистрыСведений.НастройкиОтложеннойОтправкиПисем.ПолучитьНастройку(
		Сотрудники.ЛюбойПользовательСотрудника(ПисьмоОбъект.Автор));
	
	Если ПисьмоОбъект.ПодготовленоКОтправке + Задержка > ТекДата Тогда
		НеНужноПисатьОшибкуДоставки = Истина; 
		Возврат Ложь;
	КонецЕсли;
	
	Если РегистрыСведений.НеОтправленныеИсходящиеПисьма.ЗакончилисьПопыткиОтправкиПисьма(ПисьмоОбъект.Ссылка) Тогда
		ТекстСообщения = НСтр("ru = 'Закончились попытки отправки письма'");
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ОтправитьПисьмоПоВнутреннейМаршрутизации(ПисьмоСсылка, УчетнаяЗапись, СообщенияОбОшибках, ПараметрыЛогирования)
	
	СообщениеОбОшибке = "";
	
	ЗаписатьПротоколДоставкиПочты(
		ПисьмоСсылка,
		"",
		Ложь,
		Перечисления.ТипыСобытийДоставкиПочты.НачалоОтправкиПисьмаПоВнутреннейМаршрутизации,
		УчетнаяЗапись, ПараметрыЛогирования.ПорядковыйНомерСобытия,
		ПараметрыЛогирования.ИдентификаторСеанса,
		ПараметрыЛогирования.НомерЗадания);
	
	Попытка
		
		ЗаблокироватьДанныеДляРедактирования(ПисьмоСсылка);
		
	Исключение
		
		Возврат Ложь;
		
	КонецПопытки;
	
	ПисьмоОбъект = ПисьмоСсылка.ПолучитьОбъект();
	
	НеНужноПисатьОшибкуДоставки = Ложь;
	ТекстСообщения = "";
	Если Не ПисьмоГотовоКОтправкеПоВнутреннейМаршрутизации(ПисьмоОбъект, НеНужноПисатьОшибкуДоставки, ТекстСообщения) Тогда
		
		РазблокироватьДанныеДляРедактирования(ПисьмоСсылка);
		
		Если Не НеНужноПисатьОшибкуДоставки Тогда
		
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Письмо не готово к отправке:
				|%1
				|%2'"),
				ПисьмоСсылка,
				ТекстСообщения);
		
		СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		
		ЗаписатьОшибкуДоставки(
			СообщениеОбОшибке,
			ПисьмоСсылка,
			УчетнаяЗапись,
			ПараметрыЛогирования);
			
		КонецЕсли;	
			
		Возврат Ложь;
		
	КонецЕсли;
	
	Попытка
		
		ТекущаяДатаСеанса = ТекущаяДатаСеанса();
		ИдентификаторСообщения = Новый УникальныйИдентификатор;
		
		// Формирование параметров отправки
		ПараметрыОтправки = Новый Структура;
		ПараметрыОтправки.Вставить("ДатаОтправки", ТекущаяДатаСеанса);
		ПараметрыОтправки.Вставить("ИдентификаторСообщения", Строка(ИдентификаторСообщения));
		
		Вложения = ВстроеннаяПочтаСервер.ПолучитьФайлыПисьма(
			ПисьмоСсылка, // Письмо
			Ложь, // ФормироватьПредставлениеРазмера
			Ложь); // ВключатьПомеченныеНаУдаление
		
		Размер = СтрДлина(ПисьмоОбъект.Тема) + СтрДлина(ПисьмоОбъект.ПолучитьСодержаниеПисьма().Текст);
		Для Каждого Вложение Из Вложения Цикл
			Размер = Размер + Вложение.Размер;
		КонецЦикла;	
			
		ПараметрыОтправки.Вставить("Размер", Размер);
		
	Исключение
		
		РазблокироватьДанныеДляРедактирования(ПисьмоСсылка);
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		
		ЗаписатьОшибкуДоставки(
			СообщениеОбОшибке,
			ПисьмоСсылка,
			УчетнаяЗапись,
			ПараметрыЛогирования);
		
		Возврат Ложь;
		
	КонецПопытки;
	
	
	НачатьТранзакцию();
	Попытка
		
		МассивАдресатов = Новый ТаблицаЗначений;
		МассивАдресатов.Колонки.Добавить("Адресат");
		
		Для каждого Получатель Из ПисьмоОбъект.ПолучателиПисьма Цикл
			НовСтр = МассивАдресатов.Добавить();
			НовСтр.Адресат = Получатель.Адресат;
		КонецЦикла;
		Для каждого Получатель Из ПисьмоОбъект.ПолучателиКопий Цикл
			НовСтр = МассивАдресатов.Добавить();
			НовСтр.Адресат = Получатель.Адресат;
		КонецЦикла;
		Для каждого Получатель Из ПисьмоОбъект.ПолучателиСкрытыхКопий Цикл
			НовСтр = МассивАдресатов.Добавить();
			НовСтр.Адресат = Получатель.Адресат;
		КонецЦикла;
		
		Если МассивАдресатов.Количество() > 50 Тогда 
			ПорядокДоставки = Перечисления.ПорядокДоставкиПисемПоВнутреннейМаршрутизации.ДолгаяДоставка;
		Иначе
			ПорядокДоставки = Перечисления.ПорядокДоставкиПисемПоВнутреннейМаршрутизации.БыстраяДоставка;
		КонецЕсли;	
		
		// проверка адресов получателей на возможность внутренней маршрутизации
		Для Каждого Стр Из МассивАдресатов Цикл
			
			Адресат = Стр.Адресат;
			
			УчетныеЗаписи = ПолучитьУчетныеЗаписиПоАдресу(Адресат.Адрес, Истина);
			Если УчетныеЗаписи.Количество() = 0 Тогда 
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Письмо не может быть отправлено по внутренней маршрутизации на адрес %1.
					|Возможно изменились настройки учетной записи этого получателя.
					|Повторите отправку письма.'"),
					Адресат.Адрес);
				ВызватьИсключение ТекстСообщения;
			КонецЕсли;
			
		КонецЦикла;	
		
		ДатаПодготовкиКОтправке = ПисьмоОбъект.ПодготовленоКОтправке;
		
		ПисьмоОбъект.ИдентификаторСообщения = ПараметрыОтправки.ИдентификаторСообщения;
		ПисьмоОбъект.Размер = ПараметрыОтправки.Размер;
		ПисьмоОбъект.ПодготовленоКОтправке = Дата(1, 1, 1);
		ПапкаДляОтправленных = РегистрыСведений.ПапкиУчетныхЗаписей.ПолучитьПапку(
			ПисьмоОбъект.УчетнаяЗапись,
			Перечисления.ВидыПапокПисем.Отправленные);
		ПисьмоОбъект.Папка = ПапкаДляОтправленных;
		ПисьмоОбъект.ДатаОтправки = ТекущаяДатаСеанса;
		ПисьмоОбъект.Дата = ТекущаяДатаСеанса;
		ПрименитьПравилоПриОтправке(ПисьмоОбъект, Истина);//применение пользовательских правил
		ПисьмоОбъект.Записать();
		
		// Добавление в регистр писем в процессе отправки.
		БлокировкаДанных = Новый БлокировкаДанных;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОчередьВнутреннейПриемкиПисем");
		ЭлементБлокировки.УстановитьЗначение("Письмо", ПисьмоСсылка);
		ЭлементБлокировки.ИсточникДанных = МассивАдресатов;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Адресат", "Адресат");
		
		БлокировкаДанных.Заблокировать();			
		
		СообщениеОбОшибке = "";
		
		ДатаОтправки = ТекущаяДатаСеанса();
						
		Приоритет = ПриоритетОчередиВнутреннейМаршрутизации(ПисьмоСсылка, УчетнаяЗапись);
		
		СвернутыеАдресаты = ОбщегоНазначенияКлиентСервер.СвернутьМассив(
			МассивАдресатов.ВыгрузитьКолонку("Адресат"));
		
		РегистрыСведений.ОчередьВнутреннейПриемкиПисем.ДобавитьПисьмоДляСпискаАдресатов(
			ПисьмоСсылка,
			СвернутыеАдресаты,
			ДатаОтправки,
			ДатаПодготовкиКОтправке,
			ПорядокДоставки,
			Приоритет);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		РазблокироватьДанныеДляРедактирования(ПисьмоСсылка);
		
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Произошла ошибка при регистрации отправки письма:
				|%1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		
		ЗаписатьОшибкуДоставки(
			СообщениеОбОшибке,
			ПисьмоСсылка,
			УчетнаяЗапись,
			ПараметрыЛогирования);
		
		Возврат Ложь;
		
	КонецПопытки;
	
	РазблокироватьДанныеДляРедактирования(ПисьмоСсылка);
	
	ЗаписатьПротоколДоставкиПочты(
		ПисьмоСсылка,
		"",
		Ложь,
		Перечисления.ТипыСобытийДоставкиПочты.УспешнаяОтправкаПисьмаПоВнутреннейМаршрутизации,
		УчетнаяЗапись,
		ПараметрыЛогирования.ПорядковыйНомерСобытия,
		ПараметрыЛогирования.ИдентификаторСеанса,
		ПараметрыЛогирования.НомерЗадания);
	
	Возврат Истина;
	
КонецФункции

Процедура ПолучитьПисьмаПоВнутреннейМаршрутизации(СообщенияОбОшибках, ПараметрыЛогирования, ПорядокДоставки = Неопределено)
	
	Если Не ВстроеннаяПочтаСерверПовтИсп.ИспользоватьВнутреннююМаршрутизацию() Тогда 
		Возврат;
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВремяНачала = ТекущаяДатаСеанса();
	
	ПолучитьВсеПисьмаПоВнутреннейМаршрутизации(СообщенияОбОшибках, ПараметрыЛогирования, ПорядокДоставки);
	
	ВремяОкончания = ТекущаяДатаСеанса();
	ТекстЛога = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Длительность сеанса: %1 сек.'"),
		ВремяОкончания - ВремяНачала);
	
КонецПроцедуры	
		
Процедура ПолучитьВсеПисьмаПоВнутреннейМаршрутизации(СообщенияОбОшибках, ПараметрыЛогирования, ПорядокДоставки)	
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОчередьВнутреннейПриемкиПисем.Письмо,
	|	ОчередьВнутреннейПриемкиПисем.Адресат,
	|	ОчередьВнутреннейПриемкиПисем.ДатаПодготовки КАК ДатаПодготовки
	|ИЗ
	|	РегистрСведений.ОчередьВнутреннейПриемкиПисем КАК ОчередьВнутреннейПриемкиПисем";
	
	Если ЗначениеЗаполнено(ПорядокДоставки) Тогда 
		ТекстЗапроса = ТекстЗапроса + " ГДЕ (ПорядокДоставки = &ПорядокДоставки) ";
	КонецЕсли;	
	ТекстЗапроса = ТекстЗапроса + " УПОРЯДОЧИТЬ ПО ДатаОтправки ";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Если ЗначениеЗаполнено(ПорядокДоставки) Тогда 
		Запрос.УстановитьПараметр("ПорядокДоставки", ПорядокДоставки);
	КонецЕсли;	
	
	РезультатЗапроса = Запрос.Выполнить();
			
	Таблица = РезультатЗапроса.Выгрузить();	
	
	Для Каждого Строка Из Таблица Цикл
		
		ПолучитьПисьмоПоВнутреннейМаршрутизации(
			Строка.Письмо, 
			Строка.Адресат, 
			Строка.ДатаПодготовки, 
			СообщенияОбОшибках, 
			ПараметрыЛогирования);
		
	КонецЦикла;	
	
КонецПроцедуры

Функция ПолучитьПисьмоПоВнутреннейМаршрутизации(ПисьмоСсылка, Адресат, ДатаПодготовки, СообщенияОбОшибках, ПараметрыЛогирования)
	
	ПисьмоОбъект = ПисьмоСсылка.ПолучитьОбъект(); 
	НовоеПисьмоСсылка = Неопределено;
	
	НачатьТранзакцию();
	Попытка
		
		// Проверки возможности конкурентной записи регистра.
		// Если запись уже занята - значит конкурирующий поток ей занимается, не ждём когда станет доступна.
		КлючЗаписи = РегистрыСведений.ОчередьВнутреннейПриемкиПисем.СоздатьКлючЗаписи(
			Новый Структура("Письмо, Адресат", ПисьмоСсылка, Адресат));
		Попытка
			ЗаблокироватьДанныеДляРедактирования(КлючЗаписи);
		Исключение
			ЗафиксироватьТранзакцию();
			Возврат Неопределено;
		КонецПопытки;
		
		// Проверка наличия записи в регистре писем в процессе отправки.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОчередьВнутреннейПриемкиПисем");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Письмо", ПисьмоСсылка);
		ЭлементБлокировки.УстановитьЗначение("Адресат", Адресат);
		Блокировка.Заблокировать();
		
		МенеджерЗаписи = РегистрыСведений.ОчередьВнутреннейПриемкиПисем.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Письмо = ПисьмоСсылка;
		МенеджерЗаписи.Адресат = Адресат;
		МенеджерЗаписи.Прочитать();
		Если Не МенеджерЗаписи.Выбран() Тогда 
			ЗафиксироватьТранзакцию();
			Возврат Неопределено;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПисьмоОбъект.ИдентификаторСообщения) Тогда 
			ВызватьИсключение НСтр("ru = 'Неверный идентификатор письма. Обратитесь к Администратору.'");
		КонецЕсли;	
		
		// Формирование параметров отправки
		ПараметрыОтправки = ПолучитьСтруктуруПараметровОтправкиПисьма(ПисьмоОбъект);
		ПараметрыОтправки.Вставить("ИдентификаторСообщения", ПисьмоОбъект.ИдентификаторСообщения);
		ПараметрыОтправки.Вставить("Предмет", ПисьмоОбъект.Предмет);
		ПараметрыОтправки.Вставить("Проект", ПисьмоОбъект.Проект);
		ПараметрыОтправки.Вставить("Размер", ПисьмоОбъект.Размер);
		ПараметрыОтправки.Вставить("ДатаОтправки", ПисьмоОбъект.ДатаОтправки);   
		ПараметрыОтправки.Вставить("ДатаПодготовки", ДатаПодготовки);
		
		// Формирование поля заголовка Message-Id
		ПараметрыОтправки.ПоляЗаголовка.Добавить(
			Почта.СформироватьСтруктуруПоляЗаголовка(
			"Message-Id", // ИмяПоля
			ПисьмоОбъект.ИдентификаторСообщения, // ЗначениеПоля
			СпособКодированияНеASCIIСимволовИнтернетПочтовогоСообщения.БезКодирования)); // СпособКодирования
		
		// Формирование заголовка In-Reply-To если основания нет идентификатора
		ПисьмоОтправленоВОтветНа = СвязиОбъектов.ПолучитьСвязанныйОбъект(
			ПисьмоОбъект.Ссылка,
				Справочники.ТипыСвязей.ПисьмоОтправленоВОтветНа);
				
		Если ЗначениеЗаполнено(ПисьмоОтправленоВОтветНа) Тогда
			Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(ПисьмоОтправленоВОтветНа) Тогда
				ПисьмоОтправленоВОтветНаИнфо = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
					ПисьмоОтправленоВОтветНа,
					"ИдентификаторСообщения, ВнутреннийЗаголовок");
					
				Если Не ЗначениеЗаполнено(ПисьмоОтправленоВОтветНаИнфо.ИдентификаторСообщения) Тогда // письмо без ИД
					
					// Формирование поля заголовка In-Reply-To
					ПараметрыОтправки.ПоляЗаголовка.Добавить(
						Почта.СформироватьСтруктуруПоляЗаголовка(
						"In-Reply-To", // ИмяПоля
						ВКавычках("NULL", "<", ">"), // ЗначениеПоля
						СпособКодированияНеASCIIСимволовИнтернетПочтовогоСообщения.БезКодирования)); // СпособКодирования
					
				КонецЕсли;
					
			КонецЕсли;
		КонецЕсли;	
	
		// прием писем по внутренней маршрутизации
		УчетныеЗаписи = ПолучитьУчетныеЗаписиПоАдресу(Адресат.Адрес, Истина);
		Если УчетныеЗаписи.Количество() = 0 Тогда 
			ВызватьИсключение НСтр("ru = 'Неверный вид маршрутизации письма. Обратитесь к Администратору.'");
		КонецЕсли;
		
		Для Каждого УчетнаяЗапись Из УчетныеЗаписи Цикл 
			
			СообщениеОбОшибке = "";  
			НовоеПисьмоСсылка = ПолучитьСообщениеПоВнутреннейМаршрутизации(
				ПисьмоОбъект, УчетнаяЗапись, ПараметрыОтправки, СообщениеОбОшибке, ПараметрыЛогирования);
				
			Если НовоеПисьмоСсылка = Неопределено Тогда
				
				ОтменитьТранзакцию();
				
				СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
				
				СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Учетная запись: %1
					|Ошибка при получении письма по внутренней маршрутизации:
					|%2'"),
					УчетнаяЗапись,
					СообщениеОбОшибке);
				
				ЗаписатьОшибкуДоставки(СообщениеОбОшибке, 
					Неопределено, 
					УчетнаяЗапись, 
					ПараметрыЛогирования,
					Перечисления.ТипыСобытийДоставкиПочты.ОшибкаПолученияПисьма);
				
				СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
				
				Возврат Неопределено;
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Удаление из регистра писем в процессе отправки 
		РегистрыСведений.ОчередьВнутреннейПриемкиПисем.ПисьмоАдресатаБылоУдалено(
			ПисьмоСсылка,
			Адресат);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Произошла ошибка при получении письма по внутренней маршрутизации:
			|%1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		
		ЗаписатьОшибкуДоставки(
			СообщениеОбОшибке,
			ПисьмоСсылка,
			УчетнаяЗапись,
			ПараметрыЛогирования,
			Перечисления.ТипыСобытийДоставкиПочты.ОшибкаПолученияПисьма);
		
	КонецПопытки;     
		
	Возврат НовоеПисьмоСсылка;
	
КонецФункции
	
Функция ПолучитьСообщениеПоВнутреннейМаршрутизации(ИсходящееПисьмоОбъект, УчетнаяЗапись, ПараметрыОтправки, СообщениеОбОшибке, ПараметрыЛогирования)
	
	Попытка
		ВходящееПисьмоОбъект = Документы.ВходящееПисьмо.СоздатьДокумент();
		ВходящееПисьмоОбъект.ДополнительныеСвойства.Вставить("НеЗаписыватьОперациюСПисьмом", Истина);
		
		Предмет = ПараметрыОтправки.Предмет;
		
		Если ЗначениеЗаполнено(Предмет) Тогда
		
			ПредметДоступенОтветственнымУчетнойЗаписи = 
				ТипЗнч(Предмет) = Тип("СправочникСсылка.УведомленияПрограммы")
					ИЛИ ДоступенПоПравамОтветственнымУчетнойЗаписи(Предмет, УчетнаяЗапись);
			
			Если ПредметДоступенОтветственнымУчетнойЗаписи Тогда
				ВходящееПисьмоОбъект.Предмет = Предмет;
			КонецЕсли;
		
		КонецЕсли;
		
		Проект = ПараметрыОтправки.Проект;
		Если ЗначениеЗаполнено(Проект) И ДоступенПоПравамОтветственнымУчетнойЗаписи(Проект, УчетнаяЗапись) Тогда 
			ВходящееПисьмоОбъект.Проект = Проект;
		КонецЕсли;
		
		// Заполнение структуры сообщения
		Сообщение = Почта.СформироватьСтруктуруПочтовогоСообщения();
		ЗаполнитьЗначенияСвойств(Сообщение, ПараметрыОтправки,, "Вложения");
		
		Для Каждого ПолеЗаголовка Из ПараметрыОтправки.ПоляЗаголовка Цикл
			Если ЗначениеЗаполнено(ПолеЗаголовка.ЗначениеПоля) Тогда 
				Сообщение.Заголовок = Сообщение.Заголовок + СокрЛП(ПолеЗаголовка.ИмяПоля) + ": " + СокрЛП(ПолеЗаголовка.ЗначениеПоля) + Символы.ПС;
			КонецЕсли;	
		КонецЦикла;	
		
		Сообщение.Вложения.Очистить();
		Для Каждого Вложение Из ПараметрыОтправки.Вложения Цикл
			СтруктураВложения = Новый Структура;
			СтруктураВложения.Вставить("Имя", Вложение.Наименование);
			СтруктураВложения.Вставить("Данные", Вложение.Данные);
			СтруктураВложения.Вставить("Идентификатор", Вложение.Идентификатор);
			
			Если ТипЗнч(Вложение.Данные) = Тип("ИнтернетПочтовоеСообщение") Тогда
				СтруктураВложения.Данные = Вложение.Данные.ПолучитьИсходныеДанные();
				ПредставлениеТема = Вложение.Данные.Тема;
				СтруктураВложения.Имя = ПредставлениеТема + ".msg";
				СтруктураВложения.Вставить("ЭтоВложенноеПисьмо", Истина);
			КонецЕсли;	
			
			Сообщение.Вложения.Добавить(СтруктураВложения);
		КонецЦикла;	
		
		Сообщение.ДатаПолучения = ТекущаяДатаСеанса();
		
		ЗаполнитьВходящееПисьмоПриВнутреннейМаршрутизации(ВходящееПисьмоОбъект, Сообщение, УчетнаяЗапись);
		
		ЗаписатьПротоколДоставкиПочты(
			ВходящееПисьмоОбъект.Ссылка,
			"",
			Ложь,
			Перечисления.ТипыСобытийДоставкиПочты.УспешноеПолучениеПисьмаПоВнутреннейМаршрутизации,
			УчетнаяЗапись,
			ПараметрыЛогирования.ПорядковыйНомерСобытия,
			ПараметрыЛогирования.ИдентификаторСеанса,
			ПараметрыЛогирования.НомерЗадания);
		
	Исключение
		
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Процедура ДобавитьФайлыИзИнтернетПочтовогоСообщения(
	Письмо,
	Сообщение,
	ВремяИзменения,
	ПрефиксНомераПисьма,
	СобственныеФайлыВозвращаемый, // в этом параметре возвращается массив ссылок на созданные файлы,
	// Ссылки на файлы, полученные из вложенных сообщений в массив СобственныеФайлыВозвращаемый.
	// не добавляются.
	ПорядковыйНомерВложения,
	РасширенияICS = "",
	ФайлПриглашение = Ложь,
	МассивПутейВТоме = Неопределено)  
	
	ТипХраненияФайлов = ФайловыеФункции.ПолучитьТипХраненияФайлов();
	
	ИндексНомераПисьма = 1;
	
	Для каждого Вложение Из Сообщение.Вложения Цикл
		
		Если ТипЗнч(Вложение.Данные) = Тип("ДвоичныеДанные") Тогда
			
			ИмяФайла =
				ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(Вложение.Имя, "_");
			
			Если Не ПустаяСтрока(ПрефиксНомераПисьма) Тогда
				
				ИмяФайла = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Письмо %1 %2'"),
					ПрефиксНомераПисьма,
					ИмяФайла);
				
			КонецЕсли;
			
			ИмяФайлаИнфо = РаботаСоСтроками.РазложитьИмяФайла(ИмяФайла);	
			Если Не ЗначениеЗаполнено(ИмяФайлаИнфо.Расширение)
				И Вложение.Свойство("ТипСодержимого") Тогда
				
				Если НРег(Вложение.ТипСодержимого) = НРег("image/gif") Тогда
					ИмяФайла = ИмяФайла + ".gif";
				ИначеЕсли НРег(Вложение.ТипСодержимого) = НРег("image/jpeg") Тогда
					ИмяФайла = ИмяФайла + ".jpeg";
				ИначеЕсли НРег(Вложение.ТипСодержимого) = НРег("image/png") Тогда
					ИмяФайла = ИмяФайла + ".png";
				ИначеЕсли НРег(Вложение.ТипСодержимого) = НРег("image/tiff") Тогда
					ИмяФайла = ИмяФайла + ".tiff";
				КонецЕсли;			
				
			КонецЕсли;	
			
			ФайлСсылка = ДобавитьВложениеПисьмаИзДвоичныхДанных(
				Письмо,
				Вложение.Данные, // ДвоичныеДанные
				ИмяФайла, // ИмяФайла
				ВремяИзменения,
				Вложение.Идентификатор);  
				
			Если МассивПутейВТоме <> Неопределено И ТипХраненияФайлов = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
				ПутьФайла = РаботаСФайламиВызовСервера.ПутьФайлаВТомеАктивнойВерсии(ФайлСсылка);
				Если ЗначениеЗаполнено(ПутьФайла) Тогда
					МассивПутейВТоме.Добавить(ПутьФайла);  
				КонецЕсли;
			КонецЕсли;	
				
			Если ВстроеннаяПочтаКлиентСервер.ЭтоФайлПриглашение(Вложение.Имя, РасширенияICS) Тогда
				ФайлПриглашение = ФайлСсылка;
			КонецЕсли;	
				
			РегистрыСведений.СведенияОФайлахДокументооборот.УстановитьПризнак(
				ФайлСсылка, "ПорядковыйНомерВложения", ПорядковыйНомерВложения);
			
			ПорядковыйНомерВложения = ПорядковыйНомерВложения + 1;
				
			Если Вложение.Свойство("ЭтоВложенноеПисьмо") Тогда
				РегистрыСведений.СведенияОФайлахДокументооборот.УстановитьПризнак(
					ФайлСсылка, "ЭтоВложенноеПисьмо", Истина);
			КонецЕсли;	
				
			Если ПустаяСтрока(ПрефиксНомераПисьма) Тогда
				СобственныеФайлыВозвращаемый.Добавить(ФайлСсылка);
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Вложение.Данные) = Тип("Структура") Тогда
			
			Если ПустаяСтрока(ПрефиксНомераПисьма) Тогда
				ПрефиксВложенногоПисьма = Строка(ИндексНомераПисьма);
			Иначе
				ПрефиксВложенногоПисьма = ПрефиксНомераПисьма + "." + ИндексНомераПисьма;
			КонецЕсли;
			ИндексНомераПисьма = ИндексНомераПисьма + 1;
			
			// рекурсия
			ДобавитьФайлыИзИнтернетПочтовогоСообщения(
				Письмо, // ДокументСсылка.ВходящееПисьмо, ДокументСсылка.ИсходящееПисьмо
				Вложение.Данные, // ИнтернетПочтовоеСообщение
				ВремяИзменения,  // ДатаВремя
				ПрефиксВложенногоПисьма,
				СобственныеФайлыВозвращаемый,
				ПорядковыйНомерВложения);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Формирует шапку письма в формате HTML
Функция СформироватьШапкуПисьма(ПисьмоОбъект, ТипТекста, СокращатьИнформациюОПисьмеПриОтвете)
	
	ПредставлениеОтправителя = "";
	
	Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(ПисьмоОбъект) Тогда
		
		ПредставлениеОтправителя = ПолучитьПредставлениеИКонтактАдресата(
			ПисьмоОбъект.ОтправительАдресат,
			Истина).Представление;
		
	ИначеЕсли ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(ПисьмоОбъект) Тогда
		
		ДанныеУчетнойЗаписи = Почта.ПолучитьДанныеУчетнойЗаписи(ПисьмоОбъект.УчетнаяЗапись);
		ПредставлениеОтправителя = ДанныеУчетнойЗаписи.ПредставлениеАдресаЭлектроннойПочты;
		
	КонецЕсли;
	
	ДатаОтправки = ПисьмоОбъект.ДатаОтправки;
	Если Не ЗначениеЗаполнено(ДатаОтправки) Тогда
		Если ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(ПисьмоОбъект) Тогда
			ДатаОтправки = ПисьмоОбъект.ДатаПолучения;
		КонецЕсли;		
	КонецЕсли;	
	ПредставлениеДатыОтправления = Формат(ДатаОтправки, "ДФ='dd.MM.yyyy HH:mm'");
	
	Если ПустаяСтрока(ПредставлениеДатыОтправления) Тогда
		ПредставлениеДатыОтправления = НСтр("ru = 'Не отправлено'");
	КонецЕсли;
	ПредставлениеТема = ПисьмоОбъект.Тема;
	ПредставлениеКому = СформироватьПредставлениеАдресатов(ПисьмоОбъект.ПолучателиПисьма);
	ПредставлениеКопии = СформироватьПредставлениеАдресатов(ПисьмоОбъект.ПолучателиКопий);
	ПредставлениеОбратныеАдреса = СформироватьПредставлениеАдресатов(ПисьмоОбъект.ПолучателиОтвета);
	
	Если ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		
		РаботаС_HTML.ЗаменитьСпецСимволыHTML(ПредставлениеОтправителя);
		РаботаС_HTML.ЗаменитьСпецСимволыHTML(ПредставлениеДатыОтправления);
		РаботаС_HTML.ЗаменитьСпецСимволыHTML(ПредставлениеКому);
		РаботаС_HTML.ЗаменитьСпецСимволыHTML(ПредставлениеКопии);
		РаботаС_HTML.ЗаменитьСпецСимволыHTML(ПредставлениеОбратныеАдреса);
		РаботаС_HTML.ЗаменитьСпецСимволыHTML(ПредставлениеТема);
		
		Если Не СокращатьИнформациюОПисьмеПриОтвете Тогда
			
			Шапка = 
				"<div><b>[НадписьОт]:</b> %1<br>
				|<b>[НадписьОтправлено]:</b> %2<br>
				|<b>[НадписьКому]:</b> %3<br>
				|<b>[НадписьКопии]:</b> %4<br>
				|<b>[НадписьОбратныеАдреса]:</b> %5<br>
				|<b>[НадписьТема]:</b> %6</div>";
				
			Если ПустаяСтрока(ПредставлениеКому) Тогда
				Шапка = СтрЗаменить(Шапка, Символы.ПС + "<b>[НадписьКому]:</b> %3<br>", "");
			КонецЕсли;
			
			Если ПустаяСтрока(ПредставлениеКопии) Тогда
				Шапка = СтрЗаменить(Шапка, Символы.ПС + "<b>[НадписьКопии]:</b> %4<br>", "");
			КонецЕсли;
			
			Если ПустаяСтрока(ПредставлениеОбратныеАдреса) Тогда
				Шапка = СтрЗаменить(Шапка, Символы.ПС + "<b>[НадписьОбратныеАдреса]:</b> %5<br>", "");
			КонецЕсли;
			
		Иначе
			
			Шапка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"<div><b>%1, %2:</b></div>",
				ПредставлениеДатыОтправления,
				ПредставлениеОтправителя);
			
		КонецЕсли;
		
	Иначе // ПростойТекст
		
		Если Не СокращатьИнформациюОПисьмеПриОтвете Тогда
			
			Шапка = "[НадписьОт]: %1
				|[НадписьОтправлено]: %2
				|[НадписьКому]: %3
				|[НадписьКопии]: %4
				|[НадписьОбратныеАдреса]: %5
				|[НадписьТема]: %5";
			
			Если ПустаяСтрока(ПредставлениеКому) Тогда
				Шапка = СтрЗаменить(Шапка, Символы.ПС + "[НадписьКому]: %3", "");
			КонецЕсли;
			Если ПустаяСтрока(ПредставлениеКопии) Тогда
				Шапка = СтрЗаменить(Шапка, Символы.ПС + "[НадписьКопии]: %4", "");
			КонецЕсли;
			Если ПустаяСтрока(ПредставлениеОбратныеАдреса) Тогда
				Шапка = СтрЗаменить(Шапка, Символы.ПС + "[НадписьОбратныеАдреса]: %5", "");
			КонецЕсли;
			
		Иначе
			
			Шапка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"%1, %2:",
				ПредставлениеДатыОтправления,
				ПредставлениеОтправителя);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не СокращатьИнформациюОПисьмеПриОтвете Тогда
		
		Шапка = СтрЗаменить(Шапка, "[НадписьОт]", НСтр("ru = 'От'"));
		Шапка = СтрЗаменить(Шапка, "[НадписьОтправлено]", НСтр("ru = 'Отправлено'"));
		Шапка = СтрЗаменить(Шапка, "[НадписьКому]", НСтр("ru = 'Кому'"));
		Шапка = СтрЗаменить(Шапка, "[НадписьКопии]", НСтр("ru = 'Копии'"));
		Шапка = СтрЗаменить(Шапка, "[НадписьТема]", НСтр("ru = 'Тема'"));
		Если ПисьмоОбъект.ПолучателиОтвета.Количество() = 1 Тогда
			Шапка = СтрЗаменить(Шапка, "[НадписьОбратныеАдреса]", НСтр("ru = 'Обратный адрес'"));
		Иначе
			Шапка = СтрЗаменить(Шапка, "[НадписьОбратныеАдреса]", НСтр("ru = 'Обратные адреса'"));
		КонецЕсли;
		
		Шапка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Шапка,
			ПредставлениеОтправителя,
			ПредставлениеДатыОтправления,
			ПредставлениеКому,
			ПредставлениеКопии,
			ПредставлениеОбратныеАдреса,
			ПредставлениеТема);
		
	КонецЕсли;
	
	Возврат Шапка;
	
КонецФункции

// Формирует полное или краткое представление адресатов
// в соответствии с настройками программы
Функция СформироватьПредставлениеАдресатов(ТаблицаПолучателей)
	
	КоличествоПолучателей = ТаблицаПолучателей.Количество();
	Если КоличествоПолучателей = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	СокращатьПредставлениеАдресатов = ВстроеннаяПочтаСерверПовтИсп.ПолучитьСокращатьПредставлениеАдресатов();
	ЧислоАдресатовДляКраткогоПредставления = ВстроеннаяПочтаСерверПовтИсп.ПолучитьЧислоАдресатовДляКраткогоПредставления();
	КоличествоОтображаемыхПолучателейПриПревышении = 3;
	
	Если СокращатьПредставлениеАдресатов
		И (КоличествоПолучателей >= ЧислоАдресатовДляКраткогоПредставления)
		И (КоличествоПолучателей > КоличествоОтображаемыхПолучателейПриПревышении) Тогда
		
		ПредставлениеАдресатов = "";
		
		Для Индекс = 0 По КоличествоОтображаемыхПолучателейПриПревышении - 1 Цикл
			
			Получатель = ТаблицаПолучателей[Индекс];
			
			ПредставлениеАдресатов = ПредставлениеАдресатов +
				ПолучитьПредставлениеИКонтактАдресата(Получатель.Адресат).Представление + "; ";
			
		КонецЦикла;
		
		ПодписьПолучателей = ВстроеннаяПочтаКлиентСервер.ПодписьКЧислу(
			КоличествоПолучателей,
			НСтр("ru = 'получатель'"),
			НСтр("ru = 'получателя'"),
			НСтр("ru = 'получателей'"));
		
		ПредставлениеАдресатов = ПредставлениеАдресатов +
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = ' и другие (всего %1 %2)'"),
				Формат(КоличествоПолучателей, "ЧГ=0"),
				ПодписьПолучателей);
		
	Иначе
		
		ПредставлениеАдресатов = ТаблицаПолучателейВСтроку(ТаблицаПолучателей.Выгрузить());
		
	КонецЕсли;
	
	Возврат ПредставлениеАдресатов;
	
КонецФункции

// Добавляет запись в регистр ОбработанныеУчетныеЗаписи
Процедура ЗаписатьОбработаннуюУчетнуюЗапись(УчетнаяЗапись)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДатаОбращения = ТекущаяДатаСеанса();
	
	МенеджерЗаписи = РегистрыСведений.ОбработанныеУчетныеЗаписи.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
	МенеджерЗаписи.ДатаПоследнейОбработки = ДатаОбращения;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры	

Функция ВыделитьПараметр(СсылкаMailto, АнализПараметров)
	
	ПервыйСимвол = Сред(СсылкаMailto, 1, 1);
	
	Если ПервыйСимвол = "?" И НЕ АнализПараметров Тогда
		
		АнализПараметров = Истина;
		СсылкаMailto = Сред(СсылкаMailto, 2);
		
	ИначеЕсли ПервыйСимвол = "&" И АнализПараметров Тогда
		
		СсылкаMailto = Сред(СсылкаMailto, 2);
		
	КонецЕсли;
	
	Если АнализПараметров Тогда
		
		ПозицияРазделителя = СтрНайти(СсылкаMailto, "=");
		НаименованиеПараметра = Лев(СсылкаMailto, ПозицияРазделителя - 1);
		СсылкаMailto = Сред(СсылкаMailto, ПозицияРазделителя + 1);
		
	Иначе
		
		НаименованиеПараметра = "to";
		
	КонецЕсли;
	
	Если АнализПараметров Тогда
		
		ЗнакРазделителя = "&";
		
	Иначе
		
		ЗнакРазделителя = "?";
		
	КонецЕсли;
	
	ПозицияРазделителя = СтрНайти(СсылкаMailto, ЗнакРазделителя);
	Если ПозицияРазделителя <> 0 Тогда
		
		ЗначениеПараметра = Лев(СсылкаMailto, ПозицияРазделителя - 1);
		СсылкаMailto = Сред(СсылкаMailto, ПозицияРазделителя);
		
	Иначе
		
		ЗначениеПараметра = СсылкаMailto;
		СсылкаMailto = "";
		
	КонецЕсли;
	
	ВыделенныйПараметр = Новый Структура;
	ВыделенныйПараметр.Вставить("НаименованиеПараметра", НаименованиеПараметра);
	ВыделенныйПараметр.Вставить("ЗначениеПараметра", ЗначениеПараметра);
	
	Возврат ВыделенныйПараметр;
	
КонецФункции

Процедура ВнестиПараметрВСтруктуру(СтруктураMailto, НаименованиеПараметра, ЗначениеПараметра)
	
	СпособКодированияПараметраMailto = СпособКодированияПараметраMailto(НаименованиеПараметра);
	
	НаименованиеПараметра = НРег(НаименованиеПараметра);
	Если НаименованиеПараметра = "to" Тогда
		
		ДобавляемоеНаименованиеПараметра = "АдресатыКому";
		ДобавляемоеЗначениеПараметра = 
			ПолучитьМассивПолучателей(
				РаскодироватьСтроку(ЗначениеПараметра, СпособКодированияПараметраMailto),
				Перечисления.ТипыАдресатов.Кому);
		
	ИначеЕсли НаименованиеПараметра = "cc" Тогда
		
		ДобавляемоеНаименованиеПараметра = "АдресатыКопия";
		ДобавляемоеЗначениеПараметра = 
			ПолучитьМассивПолучателей(
				РаскодироватьСтроку(ЗначениеПараметра, СпособКодированияПараметраMailto),
				Перечисления.ТипыАдресатов.Копия);
		
	ИначеЕсли НаименованиеПараметра = "bcc" Тогда
		
		ДобавляемоеНаименованиеПараметра = "АдресатыСкрытаяКопия";
		ДобавляемоеЗначениеПараметра = 
			ПолучитьМассивПолучателей(
				РаскодироватьСтроку(ЗначениеПараметра, СпособКодированияПараметраMailto),
				Перечисления.ТипыАдресатов.СкрытаяКопия);
		
	ИначеЕсли НаименованиеПараметра = "subject" Тогда
		
		ДобавляемоеНаименованиеПараметра = "ТемаПисьма";
		ДобавляемоеЗначениеПараметра = РаскодироватьСтроку(
			ЗначениеПараметра,
			СпособКодированияПараметраMailto);
		
	ИначеЕсли НаименованиеПараметра = "body" Тогда
		
		ДобавляемоеНаименованиеПараметра = "ТекстПисьма";
		ДобавляемоеЗначениеПараметра = РаскодироватьСтроку(
			ЗначениеПараметра,
			СпособКодированияПараметраMailto);
		
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	СтруктураMailto.Вставить(ДобавляемоеНаименованиеПараметра, ДобавляемоеЗначениеПараметра);
	
КонецПроцедуры

Функция ПолучитьМассивПолучателей(СтрокаПочтовыхПолучателей, ТипПолучателя)
	
	МассивАдресов = РаботаСоСтроками.РазложитьСтрокуПочтовыхАдресов(СтрокаПочтовыхПолучателей);
	МассивПолучателей = Новый Массив;
	
	Для каждого ПочтовыйАдресИнфо Из МассивАдресов Цикл
		
		Если ЗначениеЗаполнено(ПочтовыйАдресИнфо.Адрес) Тогда
			СтруктураПочтовогоАдреса = Новый Структура;
			СтруктураПочтовогоАдреса.Вставить("Адрес", ПочтовыйАдресИнфо.Адрес);
			СтруктураПочтовогоАдреса.Вставить("Представление", ПочтовыйАдресИнфо.ОтображаемоеИмя);
			СтруктураПочтовогоАдреса.Вставить("ТипПолучателя", ТипПолучателя);
			СтруктураПочтовогоАдреса.Вставить("Контакт", Неопределено);
			МассивПолучателей.Добавить(СтруктураПочтовогоАдреса);
			
			Адресат = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПочтовогоАдресата(ПочтовыйАдресИнфо.Адрес, ПочтовыйАдресИнфо.ОтображаемоеИмя);
			ПредставлениеАдресата = ПолучитьПредставлениеИКонтактАдресата(Адресат);
			Контакт = ПредставлениеАдресата.Контакт;
			СтруктураПочтовогоАдреса.Вставить("Контакт", Контакт);
			СтруктураПочтовогоАдреса.Представление = ПредставлениеАдресата.Представление;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивПолучателей;
	
КонецФункции

// Функция возвращает массив символов, которые используются в качестве разделителей слов адреса.
Функция ПолучитьРазделителиСловТекстаПисьма()
	
	РазделителиСлов = Новый Массив;
	РазделителиСлов.Добавить(" ");
	РазделителиСлов.Добавить(",");
	РазделителиСлов.Добавить(":");
	РазделителиСлов.Добавить(";");
	РазделителиСлов.Добавить("~");
	РазделителиСлов.Добавить("!");
	РазделителиСлов.Добавить("#");
	РазделителиСлов.Добавить("%");
	РазделителиСлов.Добавить("^");
	РазделителиСлов.Добавить("&");
	РазделителиСлов.Добавить("*");
	РазделителиСлов.Добавить("(");
	РазделителиСлов.Добавить(")");
	РазделителиСлов.Добавить("+");
	РазделителиСлов.Добавить("-");
	РазделителиСлов.Добавить("=");
	РазделителиСлов.Добавить("/");
	РазделителиСлов.Добавить("\");
	РазделителиСлов.Добавить("|");
	РазделителиСлов.Добавить("[");
	РазделителиСлов.Добавить("]");
	РазделителиСлов.Добавить("{");
	РазделителиСлов.Добавить("}");
	РазделителиСлов.Добавить("?");
	РазделителиСлов.Добавить("'");
	РазделителиСлов.Добавить("""");
	РазделителиСлов.Добавить("«");
	РазделителиСлов.Добавить("»");
	РазделителиСлов.Добавить("<");
	РазделителиСлов.Добавить(">");
	РазделителиСлов.Добавить("№");
	РазделителиСлов.Добавить(Символы.ВК);
	РазделителиСлов.Добавить(Символы.Таб);
	РазделителиСлов.Добавить(Символы.ПС);

	Возврат РазделителиСлов;
	
КонецФункции

Процедура СортироватьИерархически(СтрокиДерева, Знач Колонки)
	
	СтрокиДерева.Сортировать(Колонки);
	Для каждого СтрокаДерева Из СтрокиДерева Цикл
		Если СтрокаДерева.Строки.Количество() > 0 Тогда
			СортироватьИерархически(СтрокаДерева.Строки, Колонки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьПапкиВДерево(СтрокиДерева, СтрокиИсточника, СведенияОПапках)
	
	Для каждого СтрокаИсточника Из СтрокиИсточника Цикл
		
		НоваяСтрока = СтрокиДерева.Добавить();
		НоваяСтрока.Ссылка = СтрокаИсточника.Папка;
		
		НоваяСтрока.Представление = СтрокаИсточника.ПредставлениеПапки;
		НоваяСтрока.Количество = 0;
		НоваяСтрока.Выделена = Ложь;
		
		СведенияОПапке = СведенияОПапках.Получить(СтрокаИсточника.Папка);
		
		Если СведенияОПапке <> Неопределено Тогда
			
			НоваяСтрока.Представление = СведенияОПапке.Представление;
			НоваяСтрока.Количество = СведенияОПапке.Количество;
			НоваяСтрока.Выделена = СведенияОПапке.Выделена;
			
		КонецЕсли;
		
		НоваяСтрока.НомерКартинки = СтрокаИсточника.НомерКартинки;
		
		НоваяСтрока.ВидПапки = СтрокаИсточника.ВидПапки;
		НоваяСтрока.Порядок = СтрокаИсточника.Порядок;
		НоваяСтрока.ПапкаБыстрогоДоступа = СтрокаИсточника.ПапкаБыстрогоДоступа;
		
		ДобавитьПапкиВДерево(НоваяСтрока.Строки, СтрокаИсточника.Строки, СведенияОПапках);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьПользователяПоАдресату(Адресат)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	СведенияОбАдресатах.Контакт КАК Контакт
		|ИЗ
		|	РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|ГДЕ
		|	СведенияОбАдресатах.АдресатСообщения = &АдресатСообщения
		|	И ТИПЗНАЧЕНИЯ(СведенияОбАдресатах.Контакт) = ТИП(Справочник.Сотрудники)
		|	И СведенияОбАдресатах.Активна = ИСТИНА";
		
	Запрос.УстановитьПараметр("АдресатСообщения", Адресат);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		ВыборкаКонтакты = РезультатЗапроса.Выбрать();
		ВыборкаКонтакты.Следующий();
		Возврат ВыборкаКонтакты.Контакт;
	КонецЕсли;	
	
	Возврат Неопределено;
	
КонецФункции	

// Определяет папки писем, отображаемые в виджете "Почта".
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи.
// 
// Возвращаемое значение:
//  Массив из СправочникСсылка.ПапкиПисем - Папки писем, отображаемые в виджете "Почта".
//
Функция ПапкиПисемВиджетаПочта(Пользователь)
	
	// Явно настроенный список папок.
	ПапкиПисемВиджетаПочта = РегистрыСведений.ПапкиПисемВиджетаПочта.ВыбранныеПапкиПользователя(Пользователь);
	Если ПапкиПисемВиджетаПочта.Количество() > 0 Тогда
		Возврат ПапкиПисемВиджетаПочта;
	КонецЕсли;
	
	// Определим папки, используемые в компатном режиме, с типом "Входящие".
	ВозможныеПапкиПисемВиджетаПочта = Новый Массив;
	ВидСписка = ВстроеннаяПочтаСервер.ПолучитьПерсональнуюНастройку("ВидСписка", Пользователь);
	Если ВидСписка = "Компактный" Тогда
		ТаблицаПапок = РегистрыСведений.ПапкиПисемЧастоИспользуемые.ПолучитьПапкиПользователя(Пользователь);
		ВозможныеПапкиПисемВиджетаПочта = ТаблицаПапок.ВыгрузитьКолонку("Папка");
	КонецЕсли;
	
	Если ВозможныеПапкиПисемВиджетаПочта.Количество() <> 0 Тогда
		
		ВидыПапок = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ВозможныеПапкиПисемВиджетаПочта, "ВидПапки");
		Для Каждого ВозможнаяПапки Из ВозможныеПапкиПисемВиджетаПочта Цикл
			
			ВидПапки = ВидыПапок[ВозможнаяПапки];
			Если ВидПапки <> Перечисления.ВидыПапокПисем.Входящие Тогда
				Продолжить;
			КонецЕсли;
			
			ПапкиПисемВиджетаПочта.Добавить(ВозможнаяПапки);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ПапкиПисемВиджетаПочта.Количество() > 0 Тогда
		Возврат ПапкиПисемВиджетаПочта;
	КонецЕсли;
	
	// Определим "Мои папки", с типом "Входящие".
	ВозможныеПапкиПисемВиджетаПочта = ПолучитьВсеПапкиПисемПользователя(Пользователь);
	
	Если ВозможныеПапкиПисемВиджетаПочта.Количество() <> 0 Тогда
		
		ВидыПапок = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ВозможныеПапкиПисемВиджетаПочта, "ВидПапки");
		Для Каждого ВозможнаяПапки Из ВозможныеПапкиПисемВиджетаПочта Цикл
			
			ВидПапки = ВидыПапок[ВозможнаяПапки];
			Если ВидПапки <> Перечисления.ВидыПапокПисем.Входящие Тогда
				Продолжить;
			КонецЕсли;
			
			ПапкиПисемВиджетаПочта.Добавить(ВозможнаяПапки);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ПапкиПисемВиджетаПочта.Количество() > 0 Тогда
		Возврат ПапкиПисемВиджетаПочта;
	КонецЕсли;
	
	// Выберем учётные записи, где я ответственный и для них определим папки "Входящие".
	УчетныеЗаписиПользователя = Справочники.УчетныеЗаписиЭлектроннойПочты.УчетныеЗаписиПользователя(Пользователь);
	Для Каждого УчетнаяЗапись Из УчетныеЗаписиПользователя Цикл
		
		ПапкаВходящие = РегистрыСведений.ПапкиУчетныхЗаписей.ПолучитьПапку(
			УчетнаяЗапись,
			Перечисления.ВидыПапокПисем.Входящие);
		Если ПапкаВходящие = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПапкиПисемВиджетаПочта.Добавить(ПапкаВходящие);
		
	КонецЦикла;
	
	Возврат ПапкиПисемВиджетаПочта;
	
КонецФункции

// Дописывает в ссылку новый параметр, если значение параметра заполнено
Процедура ДобавитьПараметрКСсылкеMailto(СсылкаMailto, НазваниеПараметра, ЗначениеПараметра, НачатоЗаполнениеПараметров)
	
	Если ПустаяСтрока(ЗначениеПараметра) Тогда
		Возврат;
	КонецЕсли;
	
	СпособКодированияПараметраMailto = СпособКодированияПараметраMailto(НазваниеПараметра);
	ЗначениеПараметра = КодироватьСтроку(ЗначениеПараметра, СпособКодированияПараметраMailto);
	
	Если НазваниеПараметра = "to" Тогда
		
		СсылкаMailto = СсылкаMailto + ЗначениеПараметра;
		Возврат;
		
	КонецЕсли;
	
	Если НачатоЗаполнениеПараметров Тогда
		
		СимволРазделитель = "&";
		
	Иначе
		
		СимволРазделитель = "?";
		НачатоЗаполнениеПараметров = Истина;
		
	КонецЕсли;
	
	СсылкаMailto = СсылкаMailto + СимволРазделитель + НазваниеПараметра + "=" + ЗначениеПараметра;
	
	Возврат;
	
КонецПроцедуры

Процедура СоздатьЗаписиКалендаряОтветственным(ПисьмоСсылка, ФайлПриглашение, УчетнаяЗапись)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРабочийКалендарь") Тогда
		Возврат;
	КонецЕсли;	
	
	ОтветственныеЗаОбработкуПисем = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ОтветственныеЗаОбработкуПисем");
	ОтветственныеЗаОбработкуПисем = ОтветственныеЗаОбработкуПисем.Выгрузить();
	
	Описание = "";
	
	ДвДанные = РаботаСФайламиВызовСервера.ПолучитьДвоичныеДанныеФайла(ФайлПриглашение);
	
	ИмяВрФайла = ПолучитьИмяВременногоФайла("txt");
	ДвДанные.Записать(ИмяВрФайла);
	
	ЧтениеТекста = Новый ЧтениеТекста(ИмяВрФайла, "UTF-8");
	ТекстФайла = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	ЧтениеТекста = Неопределено;
	
	Тема = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПисьмоСсылка, "Тема");

	ДатаНачала = Неопределено;
	ДатаОкончания = Неопределено;
	ИдентификаторПриглашения = "";
	ИдПоследовательности = 0;  
	ЭтоОтменаПриглашения = Ложь;
	РеквизитыПриглашения = Новый Структура; 
	
	ВозвращаемыеРеквизиты = Документы.ВходящееПисьмо.НовыйВозвращаемыеРеквизиты();
	
	ТекстHtml = Документы.ВходящееПисьмо.СформироватьТекстПриглашения(ТекстФайла, Тема, 
		РеквизитыПриглашения,
		Истина, ВозвращаемыеРеквизиты);
		
	ДатаНачала = ВозвращаемыеРеквизиты.ДатаНачала;
	ДатаОкончания = ВозвращаемыеРеквизиты.ДатаКонца;
	ДобавлятьЗаголовок = ВозвращаемыеРеквизиты.ДобавлятьЗаголовок;
	ДобавлятьТему = ВозвращаемыеРеквизиты.ДобавлятьТему;
	ИдентификаторПриглашения = ВозвращаемыеРеквизиты.ИдентификаторПриглашения;
	КонвертироватьHTMLВТекст = ВозвращаемыеРеквизиты.КонвертироватьHTMLВТекст;
	ИдПоследовательности = ВозвращаемыеРеквизиты.ИдПоследовательности;
	ЭтоОтменаПриглашения = ВозвращаемыеРеквизиты.ЭтоОтменаПриглашения;
		
	Описание = РаботаС_HTML.ПолучитьТекстИзHTML(ТекстHtml);	
	ДваПереводаСтроки = Символы.ВК + Символы.ВК; 
	Описание = СтрЗаменить(Описание, ДваПереводаСтроки, Символы.ВК);
	ДваПереводаСтроки = Символы.ПС + Символы.ПС; 
	Описание = СтрЗаменить(Описание, ДваПереводаСтроки, Символы.ПС);
	
	
	Если ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаОкончания)
		И ТипЗнч(ДатаНачала) = Тип("Дата") И ТипЗнч(ДатаОкончания) = Тип("Дата")
		И ДатаНачала > ДатаОкончания Тогда
		Возврат;
	КонецЕсли;	
	
	Для Каждого Стр Из ОтветственныеЗаОбработкуПисем Цикл

		СсылкаЗаписьКалендаря = Неопределено;
		УжеЕстьТакаяЗапись = РаботаСРабочимКалендаремСервер.УжеЕстьТакаяЗаписьКалендаря(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Стр.Сотрудник), 
			ИдентификаторПриглашения,
			СсылкаЗаписьКалендаря);
		
		Если Стр.ДобавлятьПриглашенияВКалендарь = Истина Или УжеЕстьТакаяЗапись Тогда
			
				
			Если Не УжеЕстьТакаяЗапись Тогда	
				РаботаСРабочимКалендаремСервер.СоздатьЗаписьКалендаряПоПредмету(
					ПисьмоСсылка, Стр.Сотрудник, 
					Описание, ДатаНачала, ДатаОкончания, Тема,
					ИдентификаторПриглашения);
			Иначе
				Если ИдПоследовательности <> 0 Тогда					
					
					РаботаСРабочимКалендаремСервер.ОбновитьЗаписьКалендаря(
						ПисьмоСсылка,
						Стр.Сотрудник, 
						ДатаНачала, ДатаОкончания,
						ИдентификаторПриглашения,   
						Описание, Тема,
						ЭтоОтменаПриглашения);
					
				КонецЕсли;		
			КонецЕсли;	
			
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры

// Вычисляет для каждого адреса контакта приоритетное представление адресатов почтовых сообщений
Процедура ОбновитьПредставлениеАдресатовПоКонтакту(Контакт, ОбновитьСведенияОбАдресатах = Истина,
	ОбработатьДанныеДляПереходаНаНовуюВерсию = Ложь)
	
	// Получение текущих контактных данных Источника.
	МассивТекущиеПочтовыеАдреса = Новый Массив;
	Для каждого Строка Из Контакт.КонтактнаяИнформация Цикл
		
		Если Строка.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты 
			И ЗначениеЗаполнено(Строка.Представление) Тогда
			МассивТекущиеПочтовыеАдреса.Добавить(Строка.Представление);
		КонецЕсли;	
		
	КонецЦикла;	
	
	ОбновитьПредставлениеАдресатовДляМассиваАдресов(МассивТекущиеПочтовыеАдреса, ОбновитьСведенияОбАдресатах, Контакт,
		ОбработатьДанныеДляПереходаНаНовуюВерсию);
	
КонецПроцедуры

// Перезаписывает все адресаты с данным адресом ставя им указанное представление
Процедура ОбновитьАдресатыПочтовыхСообщений(ТекущийАдрес, ПриоритетноеПредставление, ЕстьВКонтактах,
	Внешний, Контакт, ЕстьКонтактыСПравами, ОбновитьСведенияОбАдресатах = Истина,
	ОбработатьДанныеДляПереходаНаНовуюВерсию = Ложь)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЕстьКонтактыОбщие = ЗначениеЗаполнено(ПриоритетноеПредставление);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АдресатыПочтовыхСообщений.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.АдресатыПочтовыхСообщений КАК АдресатыПочтовыхСообщений
		|ГДЕ
		|	АдресатыПочтовыхСообщений.Адрес = &Адрес";
	Запрос.УстановитьПараметр("Адрес", ТекущийАдрес);
		
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			АдресатОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			Если ЗначениеЗаполнено(ПриоритетноеПредставление) Тогда
				АдресатОбъект.Наименование = ПриоритетноеПредставление + " <" + ТекущийАдрес + ">";
			КонецЕсли;	
			
			АдресатОбъект.ЕстьВКонтактах = ЕстьВКонтактах;
			АдресатОбъект.Контакт = Контакт;
			
			АдресатОбъект.ДополнительныеСвойства.Вставить("ЕстьКонтактыСПравами", ЕстьКонтактыСПравами);
			АдресатОбъект.ДополнительныеСвойства.Вставить("ЕстьКонтактыОбщие", ЕстьКонтактыОбщие);
			
			Если ОбработатьДанныеДляПереходаНаНовуюВерсию Тогда
				ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные(АдресатОбъект);
			Иначе	
				АдресатОбъект.Записать();
			КонецЕсли;	
			
			// перезапишем СведенияОбАдресатах
			Если ОбновитьСведенияОбАдресатах = Истина Тогда
				
				ЗапросСведения = Новый Запрос;
				ЗапросСведения.Текст = 
					"ВЫБРАТЬ
					|	СведенияОбАдресатах.АдресатСообщения КАК АдресатСообщения,
					|	СведенияОбАдресатах.Контакт КАК Контакт,
					|	СведенияОбАдресатах.Представление КАК Представление,
					|	СведенияОбАдресатах.Активна КАК Активна
					|ИЗ
					|	РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
					|ГДЕ
					|	СведенияОбАдресатах.АдресатСообщения = &АдресатСообщения";
				ЗапросСведения.УстановитьПараметр("АдресатСообщения", Выборка.Ссылка);	
				ТаблицаСведений = ЗапросСведения.Выполнить().Выгрузить();
				Для Каждого Строка Из ТаблицаСведений Цикл
					
					МенеджерЗаписи = РегистрыСведений.СведенияОбАдресатах.СоздатьМенеджерЗаписи();
					ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Строка);
					Если ЗначениеЗаполнено(ПриоритетноеПредставление) Тогда
						МенеджерЗаписи.Представление = АдресатОбъект.Наименование;
					КонецЕсли;	
					МенеджерЗаписи.Записать();
					
				КонецЦикла;	
				
			КонецЕсли;	
			
		Исключение	
		
	        ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'ОбновитьАдресатыПочтовыхСообщений'"),
				УровеньЖурналаРегистрации.Ошибка,,
				,
				ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;	
	
КонецПроцедуры	

Функция ПрименитьПравилаДляИсходящихПисем(ПисьмоОбъект, ПрименятьДействияНаОтправляемоеПисьмо = Истина)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПравилаОбработкиПисем.Ссылка
		|ИЗ
		|	Справочник.ПравилаОбработкиПисем КАК ПравилаОбработкиПисем
		|ГДЕ
		|	ПравилаОбработкиПисем.ПометкаУдаления = ЛОЖЬ
		|	И ПравилаОбработкиПисем.УчетнаяЗапись = &УчетнаяЗапись
		|	И ПравилаОбработкиПисем.ДляВходящихПисем = ЛОЖЬ
		|	И ПравилаОбработкиПисем.ДляИсходящихПисем = ИСТИНА
		|	И ПравилаОбработкиПисем.Используется = ИСТИНА
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПравилаОбработкиПисем.Порядок";
	Запрос.УстановитьПараметр("УчетнаяЗапись", ПисьмоОбъект.УчетнаяЗапись);
	Выборка = Запрос.Выполнить().Выбрать();
	ДействияВыполнялись = Ложь;
	НеобходимоЗаписатьПисьмо = Ложь;
	Пока Выборка.Следующий() Цикл
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗначенияУсловийПриОбработкеПисем.Правило,
			|	ЗначенияУсловийПриОбработкеПисем.ПараметрУсловия КАК Параметр,
			|	ЗначенияУсловийПриОбработкеПисем.ВидУсловия КАК Вид,
			|	ЗначенияУсловийПриОбработкеПисем.Порядок КАК Порядок,
			|	ЗначенияУсловийПриОбработкеПисем.ЭтоЗначенияИсключения КАК ЭтоЗначенияИсключения,
			|	NULL КАК ТекстАвтоответа
			|ИЗ
			|	РегистрСведений.ЗначенияУсловийПриОбработкеПисем КАК ЗначенияУсловийПриОбработкеПисем
			|ГДЕ
			|	ЗначенияУсловийПриОбработкеПисем.Правило = &Правило
			|
			|СГРУППИРОВАТЬ ПО
			|	ЗначенияУсловийПриОбработкеПисем.ВидУсловия,
			|	ЗначенияУсловийПриОбработкеПисем.Правило,
			|	ЗначенияУсловийПриОбработкеПисем.ПараметрУсловия,
			|	ЗначенияУсловийПриОбработкеПисем.Порядок,
			|	ЗначенияУсловийПриОбработкеПисем.ЭтоЗначенияИсключения
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЗначенияДействийПриОбработкеПисем.Правило,
			|	ЗначенияДействийПриОбработкеПисем.ПараметрДействия,
			|	ЗначенияДействийПриОбработкеПисем.ВидДействия,
			|	0,
			|	ЛОЖЬ,
			|	ЗначенияДействийПриОбработкеПисем.ТекстАвтоответа
			|ИЗ
			|	РегистрСведений.ЗначенияДействийПриОбработкеПисем КАК ЗначенияДействийПриОбработкеПисем
			|ГДЕ
			|	ЗначенияДействийПриОбработкеПисем.Правило = &Правило
			|
			|УПОРЯДОЧИТЬ ПО
			|	Порядок";
			
		Запрос.УстановитьПараметр("Правило", Выборка.Ссылка);
		ТаблицаПараметров = Запрос.Выполнить().Выгрузить();
		
		ДополнительныеСвойстваОбъекта = Новый Структура;
		Если ПроверитьУсловияПравилаДляИсходящих(ПисьмоОбъект, Выборка.Ссылка, ТаблицаПараметров, ПисьмоОбъект, Ложь, ДополнительныеСвойстваОбъекта) 
			И НЕ ПроверитьУсловияПравилаДляИсходящих(ПисьмоОбъект, Выборка.Ссылка, ТаблицаПараметров, ПисьмоОбъект, Истина, ДополнительныеСвойстваОбъекта) Тогда
			
			Для Каждого КлючЗначение Из ДополнительныеСвойстваОбъекта Цикл
				ПисьмоОбъект.ДополнительныеСвойства.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);	
			КонецЦикла;
			
			ПрерватьВыполнениеПравил = ВыполнитьДействияПравилаДляИсходящих(
				ПисьмоОбъект, 
				Выборка.Ссылка, 
				ТаблицаПараметров, 
				ПрименятьДействияНаОтправляемоеПисьмо, 
				ДействияВыполнялись, 
				НеобходимоЗаписатьПисьмо);
			Если ПрерватьВыполнениеПравил Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НеобходимоЗаписатьПисьмо;
	
КонецФункции

Процедура ОтправитьУведомлениеОбОтзыве(Адресаты, ОтзываемоеПисьмо)
			
	ИсходящееПисьмо = Документы.ИсходящееПисьмо.СоздатьДокумент();
	ИсходящееПисьмо.Дата = ТекущаяДатаСеанса();
	ИсходящееПисьмо.ВидМаршрутизации = ОтзываемоеПисьмо.ВидМаршрутизации;
	
	ИсходящееПисьмо.Тема = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Письмо отозвано: %1'"),
		ОтзываемоеПисьмо.Тема);
		
	КемОтозвано = Справочники.УчетныеЗаписиЭлектроннойПочты.ПолучитьПредставлениеАдреса(ОтзываемоеПисьмо.УчетнаяЗапись);	
	ДатаОтзыва = ТекущаяДатаСеанса();
		
	ТекстПисьма	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Сообщение
		    |
			|	от: %1
			|	тема: %2
			|	отправленное: %3
			|
			|отозвано автором.'"),
		КемОтозвано,
		ОтзываемоеПисьмо.Тема,
		Формат(ОтзываемоеПисьмо.ДатаОтправки, "ДФ='дд.ММ.гг ЧЧ:мм'"));
			
	ИсходящееПисьмо.ТекстХранилище = Новый ХранилищеЗначения(ТекстПисьма, Новый СжатиеДанных);
	ИсходящееПисьмо.УчетнаяЗапись = ОтзываемоеПисьмо.УчетнаяЗапись;
	ИсходящееПисьмо.ТипТекста = Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст;
	
	КодировкаИсходящихПисем = ВстроеннаяПочтаСервер.ПолучитьПерсональнуюНастройку("КодировкаИсходящихПисем");
	Если ПустаяСтрока(КодировкаИсходящихПисем) Тогда
		КодировкаИсходящихПисем = "utf-8";
	КонецЕсли;
	
	АвтовыборКодировкиИсходящихПисем = ВстроеннаяПочтаСервер.ПолучитьПерсональнуюНастройку("АвтовыборКодировкиИсходящихПисем");
	Если АвтовыборКодировкиИсходящихПисем Тогда
		КодировкаОтзываемогоПисьма = ОтзываемоеПисьмо.Кодировка;
		Если КодировкаОтзываемогоПисьма <> "us-ascii" Тогда 
			КодировкаИсходящихПисем = КодировкаОтзываемогоПисьма;
		КонецЕсли;	
	КонецЕсли;
	
	ИсходящееПисьмо.Кодировка = КодировкаИсходящихПисем;
	
	Для Каждого Адресат Из Адресаты Цикл 
		ПолучательПисьма = ИсходящееПисьмо.ПолучателиПисьма.Добавить();
		ПолучательПисьма.Адресат = Адресат;
	КонецЦикла;	
	
	ИсходящееПисьмо.ПолучателиПисьмаСтрокой = ТаблицаПолучателейВСтроку(ИсходящееПисьмо.ПолучателиПисьма);
	
	ИсходящееПисьмо.ПодготовленоКОтправке = ТекущаяДатаСеанса();
	ИсходящееПисьмо.Записать();
	
	РаботаСПрочтениями.УстановитьСвойствоПрочтен(ИсходящееПисьмо.Ссылка);
	
КонецПроцедуры	

// Статусы проверки.

Процедура ЗаписатьСтатусПроверкиПочты(
			Поток, ДатаНачала, ДатаПоследнейАктивности, ТекущаяОбрабатываемаяУчетнаяЗапись)
	
	РегистрыСведений.СтатусФоновыхЗаданийПроверкиПочты.ЗаписатьСтатус(
		Поток, ДатаНачала, ДатаПоследнейАктивности, ТекущаяОбрабатываемаяУчетнаяЗапись);
	
КонецПроцедуры

Функция СтатусПроверкиПочты(Поток)
	
	Результат = Новый Структура(
		"ЕстьЗапись, ДатаНачала, ДатаПоследнейАктивности, ТекущаяОбрабатываемаяУчетнаяЗапись", Ложь);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СтатусФоновыхЗаданийПроверкиПочты.Поток КАК Поток,
		|	СтатусФоновыхЗаданийПроверкиПочты.ДатаНачала КАК ДатаНачала,
		|	СтатусФоновыхЗаданийПроверкиПочты.ДатаПоследнейАктивности КАК ДатаПоследнейАктивности,
		|	СтатусФоновыхЗаданийПроверкиПочты.ТекущаяОбрабатываемаяУчетнаяЗапись КАК ТекущаяОбрабатываемаяУчетнаяЗапись
		|ИЗ
		|	РегистрСведений.СтатусФоновыхЗаданийПроверкиПочты КАК СтатусФоновыхЗаданийПроверкиПочты
		|ГДЕ
		|	СтатусФоновыхЗаданийПроверкиПочты.Поток = &Поток";
	Запрос.УстановитьПараметр("Поток", Поток);
	
	РезультатЗапроса = Запрос.Выполнить();         
	Результат.ЕстьЗапись = Не РезультатЗапроса.Пустой();

	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура УдалитьСтатусПроверкиПочты(Поток)
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей = РегистрыСведений.СтатусФоновыхЗаданийПроверкиПочты.СоздатьНаборЗаписей();	
	НаборЗаписей.Отбор.Поток.Установить(Поток);
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Возвращает Истина, если есть старые (давно не обрабатываемые) учетные записи в этом потоке.
//
// Параметры:
//  НомерЗадания - ЛюбаяСсылка - ссылка на объект информационной базы.
//  СтрокаСтарыхУчетныхЗаписей - Строка - возвращаемое значение - список перенесенных учетных записей.
//
// Возвращаемое значение:
//  Булево - Истина, если есть старые (давно не обрабатываемые) учетные записи в этом потоке.
//
Функция ЕстьСтарыеУчетныеЗаписиВЭтомПотокеПроверки(НомерЗадания, СтрокаСтарыхУчетныхЗаписей) 
	
	СтрокаСтарыхУчетныхЗаписей = "";
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОбработанныеУчетныеЗаписи.УчетнаяЗапись КАК УчетнаяЗапись,
		|	ОбработанныеУчетныеЗаписи.ДатаПоследнейОбработки КАК ДатаПоследнейОбработки
		|ИЗ
		|	РегистрСведений.ОбработанныеУчетныеЗаписи КАК ОбработанныеУчетныеЗаписи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПотокиПроверкиПочты КАК ПотокиПроверкиПочты
		|		ПО ОбработанныеУчетныеЗаписи.УчетнаяЗапись = ПотокиПроверкиПочты.УчетнаяЗапись
		|ГДЕ
		|	ОбработанныеУчетныеЗаписи.ДатаПоследнейОбработки < &ДатаОтсечения
		|	И ОбработанныеУчетныеЗаписи.УчетнаяЗапись.ПометкаУдаления = ЛОЖЬ
		|	И ПотокиПроверкиПочты.НомерЗадания = &НомерЗадания";
	

	Запрос.Текст = Запрос.Текст + "  УПОРЯДОЧИТЬ ПО УчетнаяЗапись";
	
	// тут 60 минут, а не 40. чтобы исключить ложное срабатывание.
	// если 40 минут ящик не обрабатывался - считаем ошибкой
	ДатаОтсечения = ТекущаяДатаСеанса() - 60 * 60; // если 60 минут ящик не обрабатывался - считаем ошибкой
	
	Запрос.УстановитьПараметр("ДатаОтсечения", ДатаОтсечения);
	Запрос.УстановитьПараметр("НомерЗадания", НомерЗадания);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;	   
	
	ТаблицаЗаписей = Результат.Выгрузить();
	Для Каждого Стр Из ТаблицаЗаписей Цикл
		СтрокаСтарыхУчетныхЗаписей = СтрокаСтарыхУчетныхЗаписей 
			+ Строка(Стр.УчетнаяЗапись) + " " + Строка(Стр.ДатаПоследнейОбработки) + Символы.ВК;
	КонецЦикла;	
	
	Возврат Истина;
		
КонецФункции		

// Переносит учетные записи  на другой поток.
//
// Параметры:
//  НомерПотока - Число  -номер потока, откуда переносим.
//  УчетныеЗаписиПотока - Массив - те учетные записи, которые переносим (кроме ПроблемнаяУчетнаяЗапись)
//  НовыйНомерПотока - Число
//
Процедура ПеренестиНаДругойПотокПроверки(ЗависшийНомерПотока, УчетныеЗаписиПотока, НовыйНомерПотока)
	
	Для Каждого УчетнаяЗапись Из УчетныеЗаписиПотока Цикл
		
		ДобавитьУчетнуюЗаписьВПотокиПроверкиПочты(УчетнаяЗапись, НовыйНомерПотока);
		
		ИспользоватьДляПолучения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ИспользоватьДляПолучения");
		
		Если ИспользоватьДляПолучения = Истина Тогда
		
			// продлили срок перенесенным учетным записям (дата последней обработки)
			ДатаОбращения = ТекущаяДатаСеанса();
			
			МенеджерЗаписи = РегистрыСведений.ОбработанныеУчетныеЗаписи.СоздатьМенеджерЗаписи();
			
			МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
			МенеджерЗаписи.ДатаПоследнейОбработки = ДатаОбращения;
			МенеджерЗаписи.Записать();
			
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры

// Выполнить запуск фонового задания проверки наличия писем в ящике email сервера.
//
// Параметры:
//  НомерПотока - Число - номер потока.  (используется как ключ(XXX) фонового задания "ПриемкаВнешнейПочтыXXX").
//    и также для обработки из РС ПотокиПроверкиПочты только нужных учетных записей
//
Процедура ВыполнитьЗапускФоновогоЗаданияПроверки(НомерПотока)
	
	ПараметрыВыполнения = Новый Массив;
	ПараметрыВыполнения.Добавить(НомерПотока);
	
	ОписаниеЗадания = СтрШаблон(
		НСтр("ru = 'Проверка писем потока: %1'"),
		НомерПотока);
	
	КлючФоновогоЗадания = "ПриемкаВнешнейПочты" + Строка(НомерПотока);
	
	Попытка
		
		ОтборФоновыхЗаданий = Новый Структура;
		ОтборФоновыхЗаданий.Вставить("Ключ", КлючФоновогоЗадания);
		ОтборФоновыхЗаданий.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
		
		МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборФоновыхЗаданий);
		
		Если МассивФоновыхЗаданий.Количество() = 0 Тогда
			
			ОписаниеШаблон = НСтр("ru = 'НомерПотока: %1
				|Ключ фонового задания: %2'");
				
			Описание = СтрШаблон(
				ОписаниеШаблон,
				Строка(НомерПотока),
				КлючФоновогоЗадания);
				
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Проверка внешней почты. Запускаем фоновое задание для проверки наличия писем.'"),
				УровеньЖурналаРегистрации.Информация,,,
				Описание);
			
			ФоновыеЗадания.Выполнить(
				"ВстроеннаяПочтаСервер.ПроверитьНаличиеПисемФоновымЗаданием",
				ПараметрыВыполнения,
				КлючФоновогоЗадания,
				ОписаниеЗадания);
			
		КонецЕсли;
		
	Исключение
		
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		Описание = НСтр("ru = 'НомерПотока: %1
			|Ключ фонового задания: %2
			|Ошибка: %3'");
		
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Описание,
			Строка(НомерПотока),
			КлючФоновогоЗадания,
			ТекстСообщения);
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Проверка внешней почты. Не удалось запустить фоновое задание для проверки наличия писем.'"),
			УровеньЖурналаРегистрации.Ошибка,,,
			Описание);
		
	КонецПопытки;
	
КонецПроцедуры

// Возвращает массив учетных записей этого потока проверки наличия писем.
//
// Параметры:
//  НомерЗадания - Число - номер потока
//
// Возвращаемое значение:
//  Массив - массив учетных записей этого потока.
//
Функция ПолучитьУчетныеЗаписиПотокаПроверки(НомерЗадания)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПотокиПроверкиПочты.УчетнаяЗапись КАК УчетнаяЗапись
		|ИЗ
		|	РегистрСведений.ПотокиПроверкиПочты КАК ПотокиПроверкиПочты
		|ГДЕ
		|	ПотокиПроверкиПочты.НомерЗадания = &НомерЗадания";

	Запрос.Текст = Запрос.Текст + " УПОРЯДОЧИТЬ ПО ПотокиПроверкиПочты.УчетнаяЗапись.Наименование";
		
	Запрос.УстановитьПараметр("НомерЗадания", НомерЗадания);	
		
	УчетныеЗаписиПотока = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("УчетнаяЗапись");	
	Возврат УчетныеЗаписиПотока;
	
КонецФункции	

// Возвращает массив всех учетных записей проверки наличия писем, обрабатывающиеся в данный момент.
//
// Возвращаемое значение:
//  Массив - массив учетных записей.
//
Функция ВсеРаботающиеНомераПотоковПроверки() 
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивПотоков = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтатусФоновыхЗаданийПроверкиПочты.Поток КАК Поток
		|ИЗ
		|	РегистрСведений.СтатусФоновыхЗаданийПроверкиПочты КАК СтатусФоновыхЗаданийПроверкиПочты
		|
		|УПОРЯДОЧИТЬ ПО
		|	Поток";
		МассивПотоков = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Поток");
	
	Возврат МассивПотоков;
	
КонецФункции	

// Возвращает время  (в сек), после которого считаем что фоновое задание (например приемки почты) перестало работать
//
// Возвращаемое значение:
//  Число
//
Функция ПредельноеВремяОтсутствияРаботыПочты()
	
	Возврат 40 * 60; // 40 минут
	
КонецФункции	

// Проверяет, если ли новые письма для переданных учетных записей.
//
// Параметры:
//  УчетныеЗаписи - Массив - массив учетных записей
//  КонтекстДоставки - Структура - см. ВстроеннаяПочтаСервер.СформироватьКонтекстДоставки
//  СообщенияОбОшибках - Массив - массив строк, сообщения об ошибках.
//
Процедура ПроверитьНаличиеПисем(УчетныеЗаписи, КонтекстДоставки, СообщенияОбОшибках)
	
	Для Каждого УчетнаяЗапись Из УчетныеЗаписи Цикл
		
		КонтекстДоставки.УчетнаяЗапись = УчетнаяЗапись;
		
		ДанныеУчетнойЗаписи = Почта.ПолучитьДанныеУчетнойЗаписи(УчетнаяЗапись);
		КонтекстДоставки.ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписи;
		
		ПроверитьНаличиеПисемУчетнойЗаписи(КонтекстДоставки, СообщенияОбОшибках);
		
	КонецЦикла;
	
КонецПроцедуры

// Проверяет, если ли новые письма в ящике почтового сервера - для данной учетной записи.
//
// Параметры:
//  КонтекстДоставки - Структура - см. ВстроеннаяПочтаСервер.СформироватьКонтекстДоставки
//  СообщенияОбОшибках - Массив - массив строк, сообщения об ошибках.
//
Процедура ПроверитьНаличиеПисемУчетнойЗаписи(КонтекстДоставки, СообщенияОбОшибках)

	ЗаписатьСтатусПроверкиПочты(
		КонтекстДоставки.НомерЗадания, Неопределено, ТекущаяДатаСеанса(), КонтекстДоставки.УчетнаяЗапись);
		
	СообщениеОбОшибке = "";		
	Соединение = Почта.ИнтернетПочтаУстановитьСоединение(КонтекстДоставки.УчетнаяЗапись,, 
		СообщениеОбОшибке, Истина);
		
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
	КонецЕсли;	
		
	Если Соединение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстДоставки.Соединение = Соединение;
	
	Идентификаторы = ИдентификаторыСообщенийПочтовогоСервера(
		КонтекстДоставки, СообщенияОбОшибках);
		
	ОтключитьСоединение(Соединение, КонтекстДоставки.УчетнаяЗапись);
		
	Если Идентификаторы = Неопределено 
		Или Идентификаторы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РезультатРазбораИдентификаторов = РегистрыСведений.ИдентификаторыПолученныхПисем.РазобратьИдентификаторы(
		КонтекстДоставки.УчетнаяЗапись, Идентификаторы);
		
	ЧислоПисем = РезультатРазбораИдентификаторов.Загрузить.Количество();
	
	Если ЧислоПисем > 0 Тогда
		
		РегистрыСведений.ПриемкаВнешнейПочты.Добавить(
			КонтекстДоставки.УчетнаяЗапись, ЧислоПисем, Ложь); // ИдетОбработка Ложь
			
		ЗаписатьПротоколДоставкиПочты(
			Неопределено, 
			"", 
			Ложь, 
			Перечисления.ТипыСобытийДоставкиПочты.НовыеПисьмаВЯщике,
			КонтекстДоставки.УчетнаяЗапись, 
			КонтекстДоставки.ПорядковыйНомерСобытия, 
			КонтекстДоставки.ИдентификаторСеанса,
			КонтекстДоставки.НомерЗадания);
	КонецЕсли;
	
	ЗаписатьОбработаннуюУчетнуюЗапись(КонтекстДоставки.УчетнаяЗапись);
		
КонецПроцедуры

// Возвращает массив идентификаторов сообщеий с почтового сервера.
//
// Параметры:
// КонтекстДоставки - Структура - см. СформироватьКонтекстДоставки
// СообщенияОбОшибках- Массив - массив с ошибками
//
// Возвращаемое значение:
//  Идентификаторы - Массив - массив идентификаторов сообщеий, полученных с почтового сервера.
//
Функция ИдентификаторыСообщенийПочтовогоСервера(КонтекстДоставки, СообщенияОбОшибках)
	
	ДанныеУчетнойЗаписи = КонтекстДоставки.ДанныеУчетнойЗаписи;
	
	ПараметрыОтбора = Неопределено;
	Если ДанныеУчетнойЗаписи.ПротоколВходящейПочты = "IMAP" Тогда
		
		ПараметрыОтбора = Новый Структура();
		ПараметрыОтбора.Вставить("ПослеДатыОтправления", ТекущаяДатаСеанса() - 7 * 86400);
		ПараметрыОтбора.Вставить("Удаленные", Ложь);
		
	КонецЕсли;
	
	СообщениеОбОшибке = Неопределено;
	Идентификаторы = Почта.ПолучитьИдентификаторыВходящихСообщений(
		КонтекстДоставки.Соединение,, ПараметрыОтбора, СообщениеОбОшибке);       
		
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
	КонецЕсли;	
			
	Если Идентификаторы = Неопределено И ДанныеУчетнойЗаписи.ПротоколВходящейПочты = "IMAP" Тогда
		
		СообщениеОбОшибке = "";
		ПараметрыОтбора.Удалить("ПослеДатыОтправления");
		Идентификаторы = Почта.ПолучитьИдентификаторыВходящихСообщений(
			КонтекстДоставки,, ПараметрыОтбора, СообщениеОбОшибке);

		Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
			СообщенияОбОшибках.Добавить(СообщениеОбОшибке);
		КонецЕсли;	
			
	КонецЕсли;
	
	Возврат Идентификаторы;
	
КонецФункции

// Возвращает время  (в сек)
Функция ВремяЗаписиАктивности()
	
	Возврат 2 * 60; // 2 минута  
	
КонецФункции	

// Возвращает время  (в сек)
Функция ВремяПросроченнойАктивности()
	
	Возврат ВремяЗаписиАктивности() * 2; //  в 2 раза больше
	
КонецФункции	

// Возвращает время  (в сек)
Функция ВремяПолностьюПросроченнойАктивности()
	
	Возврат ВремяЗаписиАктивности() * 4; //  в 4 раза больше
	
КонецФункции	

// Возвращает массив всех учетных записей проверки наличия писем, обрабатывающиеся в данный момент.
//
// Возвращаемое значение:
//  Массив - массив учетных записей.
//
Функция ВсеРаботающиеНомераПотоковПолучения() 
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивПотоков = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтатусФоновыхЗаданийПолученияПочты.Поток КАК Поток
		|ИЗ
		|	РегистрСведений.СтатусФоновыхЗаданийПолученияПочты КАК СтатусФоновыхЗаданийПолученияПочты
		|
		|УПОРЯДОЧИТЬ ПО
		|	Поток";
	МассивПотоков = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Поток");
	
	Возврат МассивПотоков;
	
КонецФункции	

// Статусы получения (загрузка).

Процедура ЗаписатьСтатусПолученияПочты(
			Поток, ДатаНачала, ДатаПоследнейАктивности, ТекущаяОбрабатываемаяУчетнаяЗапись)
	
	РегистрыСведений.СтатусФоновыхЗаданийПолученияПочты.ЗаписатьСтатус(
		Поток, ДатаНачала, ДатаПоследнейАктивности, ТекущаяОбрабатываемаяУчетнаяЗапись);
	
КонецПроцедуры

Функция СтатусПолученияПочты(Поток)
	
	Результат = Новый Структура(
		"ЕстьЗапись, ДатаНачала, ДатаПоследнейАктивности, ТекущаяОбрабатываемаяУчетнаяЗапись", Ложь);

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СтатусФоновыхЗаданийПолученияПочты.Поток КАК Поток,
		|	СтатусФоновыхЗаданийПолученияПочты.ДатаНачала КАК ДатаНачала,
		|	СтатусФоновыхЗаданийПолученияПочты.ДатаПоследнейАктивности КАК ДатаПоследнейАктивности,
		|	СтатусФоновыхЗаданийПолученияПочты.ТекущаяОбрабатываемаяУчетнаяЗапись КАК ТекущаяОбрабатываемаяУчетнаяЗапись
		|ИЗ
		|	РегистрСведений.СтатусФоновыхЗаданийПолученияПочты КАК СтатусФоновыхЗаданийПолученияПочты
		|ГДЕ
		|	СтатусФоновыхЗаданийПолученияПочты.Поток = &Поток";
	Запрос.УстановитьПараметр("Поток", Поток);
	
	РезультатЗапроса = Запрос.Выполнить();         
	Результат.ЕстьЗапись = Не РезультатЗапроса.Пустой();

	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура УдалитьСтатусПолученияПочты(Поток)
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей = РегистрыСведений.СтатусФоновыхЗаданийПолученияПочты.СоздатьНаборЗаписей();	
	НаборЗаписей.Отбор.Поток.Установить(Поток);
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Выполнить запуск фонового задания получения почты с email сервера
//
// Параметры:
//  НомерПотока - Число - номер потока
//
Процедура ВыполнитьЗапускФоновогоЗаданияПолучения(НомерПотока)
	
	ПараметрыВыполнения = Новый Массив;
	ПараметрыВыполнения.Добавить(НомерПотока);
	
	ОписаниеЗадания = СтрШаблон(
		НСтр("ru = 'Получение писем потока: %1'"),
		Строка(НомерПотока));
	
	КлючФоновогоЗадания = "ПолучениеПочты" + Строка(НомерПотока);
	
	Попытка
		
		ОтборФоновыхЗаданий = Новый Структура;
		ОтборФоновыхЗаданий.Вставить("Ключ", КлючФоновогоЗадания);
		ОтборФоновыхЗаданий.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
		
		МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборФоновыхЗаданий);
		
		Если МассивФоновыхЗаданий.Количество() = 0 Тогда
			
			ОписаниеШаблон = НСтр("ru = 'НомерПотока: %1
				|Ключ фонового задания: %2'");
				
			Описание = СтрШаблон(
				ОписаниеШаблон,
				Строка(НомерПотока),
				КлючФоновогоЗадания);
				
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Получение почты. Запускаем фоновое задание для получения писем.'"),
				УровеньЖурналаРегистрации.Информация,,,
				Описание);
			
			ФоновыеЗадания.Выполнить(
				"ВстроеннаяПочтаСервер.ПолучитьПочтуФоновымЗаданием",
				ПараметрыВыполнения,
				КлючФоновогоЗадания,
				ОписаниеЗадания);
			
		КонецЕсли;
		
	Исключение
		
		Описание = НСтр("ru = 'НомерПотока: %1
			|Ключ фонового задания: %2'");
		
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Описание,
		Строка(НомерПотока),
		КлючФоновогоЗадания);
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Получение почты. Не удалось запустить фоновое задание для получения писем.'"),
			УровеньЖурналаРегистрации.Ошибка,,,
			Описание);
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ОбработатьЗависаниеПотокаПроверки(НомерПотока, СтатусПотока, ДатаОбработкиСвежая, ПродолжитьЦикл, 
	ТаблицаНомеровЗаданий, МассивНомеровЗаданий, ПричинаЗависания)
	
	ПродолжитьЦикл = Ложь;
	
	Если ДатаОбработкиСвежая Тогда
		ПричинаЗависания = ПричинаЗависания +
			НСтр("ru = ' Есть свежая запись в РС СтатусФоновыхЗаданий, но дата последней обработки старая.
			|Вероятно рег задание падает при приемке плохого письма.'");
	Иначе		
		ПричинаЗависания = ПричинаЗависания +
			НСтр("ru = ' Есть старая запись в РС СтатусФоновыхЗаданий.
			|Вероятно зависло рег задание.'");
	КонецЕсли;		
	
	НайденнаяСтрока = ТаблицаНомеровЗаданий.Найти(НомерПотока, "НомерЗадания");
	ЧислоУчетныхЗаписейВПотоке = 0;
	Если НайденнаяСтрока <> Неопределено Тогда
		ЧислоУчетныхЗаписейВПотоке = НайденнаяСтрока.ЧислоУчетныхЗаписей;
	КонецЕсли;
	
	// Ничего не делаем - зависший поток уже обработан. НЕ уведомляем.
	//Если ЧислоУчетныхЗаписейВПотоке <= 1  Тогда
	Если ЧислоУчетныхЗаписейВПотоке = 0  Тогда
		
		ПродолжитьЦикл = Истина;
		Возврат;
		
	КонецЕсли;	
	
	ПроблемнаяУчетнаяЗапись = СтатусПотока.ТекущаяОбрабатываемаяУчетнаяЗапись;
	
	// Все учетные записи этого потока, кроме "плохой", переносим на незанятый поток.
	
	// Находим незанятый поток.
	НомераПотоков = ВсеРаботающиеНомераПотоковПроверки(); 
	
	ОсобыеПотоки = Новый Массив;
	
	МаксимальныйНомер = 1;
	Если НомераПотоков.Количество() <> 0 Тогда
		МаксимальныйНомер = НомераПотоков[НомераПотоков.Количество() - 1];
	КонецЕсли;	
	Если МассивНомеровЗаданий.Количество() <> 0 Тогда
		МаксимальныйНомерПоЗаданию = МассивНомеровЗаданий[МассивНомеровЗаданий.Количество() - 1];
		МаксимальныйНомер = Макс(МаксимальныйНомер, МаксимальныйНомерПоЗаданию);
	КонецЕсли;	
	
	НовыйНомерПотока = -1;
	Для СчетчикПотока = 1 По МаксимальныйНомер + 1 Цикл
		
		Если НомераПотоков.Найти(СчетчикПотока) = Неопределено
			И МассивНомеровЗаданий.Найти(СчетчикПотока) = Неопределено 
			И ОсобыеПотоки.Найти(СчетчикПотока) = Неопределено Тогда  // особый поток тоже нельзя задействовать
			
			НовыйНомерПотока = СчетчикПотока;
			Прервать;
			
		КонецЕсли;	
		
	КонецЦикла;
	
	Если НовыйНомерПотока <> -1 Тогда
		
		УчетныеЗаписиПотока = ПолучитьУчетныеЗаписиПотокаПроверки(НомерПотока);
		
		// Стираем запись в СтатусФоновыхЗаданийПроверкиПочты.
		УдалитьСтатусПроверкиПочты(НомерПотока);
		
		ПеренестиНаДругойПотокПроверки(НомерПотока, УчетныеЗаписиПотока, НовыйНомерПотока);
		// запуск фонового не делаем, это сделает следующий запуск диспетчера
		
		Наименование = СтрШаблон(
			НСтр("ru ='Завис поток проверки почты %1.'"),
			Строка(НомерПотока)
			);	
		
		Описание = СтрШаблон(
			НСтр("ru ='Завис поток проверки почты %1.  
			|Учетные записи (%2 шт) перенесены на другой поток (%3). 
			|С учетной записью %4 надо разобраться вручную
			|Текущая дата: %5'"),
			НомерПотока,
			УчетныеЗаписиПотока.Количество(),
			НовыйНомерПотока,
			ПроблемнаяУчетнаяЗапись,
			ТекущаяДатаСеанса()
			);	
			
		Описание = Описание + Символы.ПС + ПричинаЗависания;
		Описание = Описание + Символы.ПС + НСтр("ru='Перенесенные учетные записи:'");
		Описание = Описание + Символы.ПС + Символы.ПС;
			
		Для Каждого УчетнаяЗапись Из УчетныеЗаписиПотока Цикл
			Описание = Описание + Символы.ПС + Строка(УчетнаяЗапись);
		КонецЦикла;
		

		ОбщегоНазначенияДокументооборот.УведомитьОтветственныхОПроблеме(Наименование, Описание,
			НСтр("ru = 'Проверка почты по внешней маршрутизации.'"));
					
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриемкаВнешнихПисемФоновымиЗаданиями()

	МаксимальноеЧислоПотоковДляПолучения 
		= ВстроеннаяПочтаСерверПовтИсп.МаксимальноеЧислоПотоковДляПолучения();
		
	// Смотрим, сколько надо принять ящиков.
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(КОЛИЧЕСТВО(ПриемкаВнешнейПочты.УчетнаяЗапись), 0) КАК ЧислоЯщиков
		|ИЗ
		|	РегистрСведений.ПриемкаВнешнейПочты КАК ПриемкаВнешнейПочты
		|ГДЕ
		|	ПриемкаВнешнейПочты.ИдетОбработка = ЛОЖЬ";
		
	Выборка = Запрос.Выполнить().Выбрать();	
	Выборка.Следующий();
	ЧислоЯщиков = Выборка.ЧислоЯщиков;
	
	Если ЧислоЯщиков = 0 Тогда  // ничего не надо принимать
		Возврат;
	КонецЕсли;
	
	ЧислоПотоковДляЗапуска = 0;
	Если ЧислоЯщиков = 1 Тогда 
		ЧислоПотоковДляЗапуска = 1;
	Иначе
		
		ЧислоПотоковДляЗапуска = Цел(ЧислоЯщиков / 3); // 3 учетки на поток
		Если ЧислоПотоковДляЗапуска = 0 Тогда 
			ЧислоПотоковДляЗапуска = 1;
		КонецЕсли;		
		
	КонецЕсли;	
	
	Если ЧислоЯщиков > МаксимальноеЧислоПотоковДляПолучения Тогда 
		ЧислоПотоковДляЗапуска = МаксимальноеЧислоПотоковДляПолучения;
	КонецЕсли;	
	
	// Проверяем старые потоки получения - что рег задания еще существуют.
	МассивПотоков = ВсеРаботающиеНомераПотоковПолучения();
	Для Каждого НомерПотока Из МассивПотоков Цикл
		
		СтатусПотока = СтатусПолученияПочты(НомерПотока);
		Если Не СтатусПотока.ЕстьЗапись 
			Или СтатусПотока.ДатаПоследнейАктивности >= ТекущаяДатаСеанса() - 10*60 Тогда // -10 минут
			Продолжить;
		КонецЕсли;
		
		КлючФоновогоЗадания = "ПолучениеПочты" + Строка(НомерПотока);
		
		ОтборФоновыхЗаданий = Новый Структура;
		ОтборФоновыхЗаданий.Вставить("Ключ", КлючФоновогоЗадания);
		ОтборФоновыхЗаданий.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
		
		МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборФоновыхЗаданий);
		
		РегЗаданиеЗавершилосьСОшибкой = Ложь;
		ПричинаЗависания = "";
		СтрокаСтарыхУчетныхЗаписей = "";
		
		// Был перезапуск сервера (или задание вылетело с ошибкой), и задания уже нет - стираем запись.
		Если МассивФоновыхЗаданий.Количество() = 0 Тогда
			// стираем запись в СтатусФоновыхЗаданийПриемкиПочты
			УдалитьСтатусПолученияПочты(НомерПотока);
		КонецЕсли;	
		
	КонецЦикла;	
	
	// Смотрим, сколько сейчас запущено потоков получения.
	ЧислоПотоковРаботающих = 0;
	МассивПотоков = ВсеРаботающиеНомераПотоковПолучения();
	Для Каждого НомерПотока Из МассивПотоков Цикл
		СтатусПотока = СтатусПолученияПочты(НомерПотока);
		// -10 минут
		Если СтатусПотока.ЕстьЗапись И СтатусПотока.ДатаПоследнейАктивности >= ТекущаяДатаСеанса() - 10*60 Тогда
			ЧислоПотоковРаботающих = ЧислоПотоковРаботающих + 1;
		КонецЕсли;
	КонецЦикла;
	
	МаксимумПотоков = МаксимальноеЧислоПотоковДляПолучения; //10;
	ОсталосьСвободныхПотоков = МаксимумПотоков - ЧислоПотоковРаботающих;
	Если ОсталосьСвободныхПотоков < 0 Тогда
		ОсталосьСвободныхПотоков = 0;
	КонецЕсли;
	
	СколькоЗапускаемПотоков = Мин(ЧислоПотоковДляЗапуска, ОсталосьСвободныхПотоков);
	
	// Получаем массив номеров потоков, чтобы не использовать занятые номера.
	НомераПотоков = ВсеРаботающиеНомераПотоковПолучения();
	ТолькоЧтоЗапущенныеПотоки = Новый Массив;
	Для СчетчикЗапуска = 1 По СколькоЗапускаемПотоков Цикл
		
		// Находим незанятый поток.
		МаксимальныйНомер = 1;
		Если НомераПотоков.Количество() <> 0 Тогда
			МаксимальныйНомер = НомераПотоков[НомераПотоков.Количество() - 1];
		КонецЕсли;
		
		НовыйНомерПотока = -1;
		Для СчетчикПотока = 1 По МаксимальныйНомер + 100 Цикл
			
			Если НомераПотоков.Найти(СчетчикПотока) = Неопределено
				И ТолькоЧтоЗапущенныеПотоки.Найти(СчетчикПотока) = Неопределено Тогда
				
				НовыйНомерПотока = СчетчикПотока;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если НовыйНомерПотока <> -1 Тогда
			ТолькоЧтоЗапущенныеПотоки.Добавить(НовыйНомерПотока);
			ВыполнитьЗапускФоновогоЗаданияПолучения(НовыйНомерПотока);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры     

Процедура ПроверитьЗависшиеПотокиВнутреннейМаршрутизации(ПорядокДоставки, ЗависшиеПотоки)
	
	ЗависшиеПотоки = Новый Массив;
	
	ИмяМетода = "";
	Если ПорядокДоставки = Перечисления.ПорядокДоставкиПисемПоВнутреннейМаршрутизации.БыстраяДоставка Тогда
		ИмяМетода = "ВстроеннаяПочтаСервер.ПринятьПочтуВнутреннейМаршрутизацииФоновымЗаданием_Быстрая";
	Иначе	
		ИмяМетода = "ВстроеннаяПочтаСервер.ПринятьПочтуВнутреннейМаршрутизацииФоновымЗаданием_Долгая";
	КонецЕсли;	
	
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяМетода", ИмяМетода);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);  
	
	Длина5Минут = 5 * 60;
	
	ЕстьСостояниеЗависшихПотоков = Новый Соответствие;
	Для Каждого Фоновое Из МассивФоновыхЗаданий Цикл
		
		Ключ = Фоновое.Ключ; // "ПолучениеВнутреннейМаршр_" + Строка(ПорядокДоставки) + "_" + Строка(НомерПотока)
		НомерПотока = 0;
		
		Позиция = СтрДлина(Ключ);
		ПозицияПодчерк = СтрНайти(Ключ, "_", НаправлениеПоиска.СКонца, Позиция);
		Если ПозицияПодчерк <> 0 Тогда
			СтрПоток = Сред(Ключ, ПозицияПодчерк + 1);
			
			Попытка
				НомерПотока = Число(СтрПоток);
			Исключение
				// в строке могло быть некорректно
			КонецПопытки;	
			
		КонецЕсли;	
		
		МенеджерЗаписи = РегистрыСведений.СостоянияФоновыхЗаданийПриемкиВнутреннейМаршрутизации.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ПорядокДоставки = ПорядокДоставки;
		МенеджерЗаписи.Поток = НомерПотока;
		МенеджерЗаписи.Прочитать();
		
		Если Не МенеджерЗаписи.Выбран() Тогда
			
			ОтборПоКлючу = Новый Структура;
			ОтборПоКлючу.Вставить("Ключ", Ключ);
			ОтборПоКлючу.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
			МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
			Если МассивФоновыхЗаданий.Количество() = 0 Тогда
				// Нет записи в РС, но фоновое задание работало недавно.
				// Значит с момента проверки фонового задания до проверки записи в РС
				// фоновое задание успело завершить свою работу и удалило запись в РС.
				// Не трогаем его.
			Иначе
				// Фоновое задание зависло не успев начать нормально работать, либо уже удалил запись.
				// Будем считать его зависшим потоком, но не будем уведомлять ответственных.
				ЗависшиеПотоки.Добавить(НомерПотока);
				ЕстьСостояниеЗависшихПотоков[НомерПотока] = Ложь;
			КонецЕсли;
			
		Иначе
			
			// зависло, долго не меняет  ДатаПоследнейАктивности
			Если МенеджерЗаписи.ДатаПоследнейАктивности < ТекущаяДатаСеанса() - Длина5Минут Тогда
				ЗависшиеПотоки.Добавить(НомерПотока);
				ЕстьСостояниеЗависшихПотоков[НомерПотока] = Истина;
			КонецЕсли;					
			
		КонецЕсли;	
		
	КонецЦикла;	
	
	Для Каждого НомерПотока Из ЗависшиеПотоки Цикл
		
		// Находим незанятый поток
		НомераПотоков = РегистрыСведений.ОчередьВнутреннейПриемкиПисем.ПотокиСПисьмами(ПорядокДоставки);
		
		МаксимальныйНомер = 1;
		Если НомераПотоков.Количество() <> 0 Тогда
			МаксимальныйНомер = НомераПотоков[НомераПотоков.Количество() - 1];
		КонецЕсли;	
		
		НовыйНомерПотока = -1;
		Для СчетчикПотока = 1 По МаксимальныйНомер + 100 Цикл
			
			Если НомераПотоков.Найти(СчетчикПотока) = Неопределено
				И ЗависшиеПотоки.Найти(СчетчикПотока) = Неопределено Тогда
				
				НовыйНомерПотока = СчетчикПотока;
				Прервать;
				
			КонецЕсли;	
			
		КонецЦикла;
		
		Если НовыйНомерПотока <> -1 Тогда
			
			ЕстьСостояниеЗависшегоПотока = ЕстьСостояниеЗависшихПотоков[НомерПотока];
			Если ЕстьСостояниеЗависшегоПотока Тогда
				
				// Есть состояние зависшего потока - сотрём состояние и уведомим ответственных.
				НаборЗаписей = РегистрыСведений.СостоянияФоновыхЗаданийПриемкиВнутреннейМаршрутизации.СоздатьНаборЗаписей();	
				НаборЗаписей.Отбор.Поток.Установить(НомерПотока);
				НаборЗаписей.Отбор.ПорядокДоставки.Установить(ПорядокДоставки);
				НаборЗаписей.Записать();
				
				Наименование = СтрШаблон(
					НСтр("ru ='Завис поток приемки почты внутр маршр %1.'"),
					Строка(НомерПотока)
					);	
				
				Описание = Наименование;
				

				ОбщегоНазначенияДокументооборот.УведомитьОтветственныхОПроблеме(Наименование, Описание,
					НСтр("ru = 'Приемка почты по внутренней маршрутизации диспетчером.'"));
				
			Иначе
				
				// Нет состояние зависшего потока - стирать нечего.
				// Возможно уже уведомляли ответственного, когда стирали состояние - тогда уведомлять не нужно.
				// Или возможно само задание зависло до записи состояния - в таком случае тоже не уведомим ответственного.
				
			КонецЕсли;
			
			// Вне зависимости от наличия состояния поток считаем зависшим,
			// и на всякий случай переместим с него письма - хотя должны
			// были перенести когда стирали состояние ранее.
			ПеренестиНаДругойПотокВнутреннейМаршрутизации(ПорядокДоставки, НомерПотока, НовыйНомерПотока);
			
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ПеренестиНаДругойПотокВнутреннейМаршрутизации(ПорядокДоставки, НомерПотока, НовыйНомерПотока)
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОчередьВнутреннейПриемкиПисем.Письмо КАК Письмо,
		|	ОчередьВнутреннейПриемкиПисем.Адресат КАК Адресат
		|ИЗ
		|	РегистрСведений.ОчередьВнутреннейПриемкиПисем КАК ОчередьВнутреннейПриемкиПисем
		|ГДЕ
		|	ОчередьВнутреннейПриемкиПисем.Поток = &НомерПотока
		|	И ОчередьВнутреннейПриемкиПисем.ПорядокДоставки = &ПорядокДоставки";
	
	Запрос.УстановитьПараметр("ПорядокДоставки", ПорядокДоставки);
	Запрос.УстановитьПараметр("НомерПотока", НомерПотока);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.ОчередьВнутреннейПриемкиПисем.ПотокПисьмаДляАдресатаБылИзменен(
			Выборка.Письмо,
			Выборка.Адресат,
			НовыйНомерПотока);
	КонецЦикла;
	
КонецПроцедуры

Функция ВсегоВОчередиВнутреннейМаршрутизации(ПорядокДоставки)
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(*) КАК Колво
		|ИЗ
		|	РегистрСведений.ОчередьВнутреннейПриемкиПисем КАК ОчередьВнутреннейПриемкиПисем
		|ГДЕ
		|	ОчередьВнутреннейПриемкиПисем.ПорядокДоставки = &ПорядокДоставки";
	
	Запрос.УстановитьПараметр("ПорядокДоставки", ПорядокДоставки);
	Результат = Запрос.Выполнить();	
	Если Результат.Пустой() Тогда
		Возврат 0;
	КонецЕсли;	
	
	Выборка = Результат.Выбрать();	
	Выборка.Следующий();
	
	Возврат Выборка.Колво;

КонецФункции	

Функция СколькоПисемНаПотоке(ПорядокДоставки, Поток)
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(*) КАК Колво
		|ИЗ
		|	РегистрСведений.ОчередьВнутреннейПриемкиПисем КАК ОчередьВнутреннейПриемкиПисем
		|ГДЕ
		|	ОчередьВнутреннейПриемкиПисем.Поток = &Поток
		|	И ОчередьВнутреннейПриемкиПисем.ПорядокДоставки = &ПорядокДоставки";
	
	Запрос.УстановитьПараметр("ПорядокДоставки", ПорядокДоставки);
	Запрос.УстановитьПараметр("Поток", Поток);
	
	Результат = Запрос.Выполнить();	
	Если Результат.Пустой() Тогда
		Возврат 0;
	КонецЕсли;	
	
	Выборка = Результат.Выбрать();	
	Выборка.Следующий();
	
	Возврат Выборка.Колво;

КонецФункции	

// Принять почту для указанного задания
//
//  Параметры
// ПорядокДоставки - ПеречислениеСсылка.ПорядокДоставкиПисемПоВнутреннейМаршрутизации - быстрая или долгая
// НомерЗадания - Число - номер потока
Процедура ПринятьПочтуВнутреннейМаршрутизацииФоновымЗаданием_Быстрая(ПорядокДоставки, НомерЗадания) Экспорт
	
	ПринятьПочтуВнутреннейМаршрутизацииФоновымЗаданием(ПорядокДоставки, НомерЗадания);
	
КонецПроцедуры

// Принять почту для указанного задания
//
//  Параметры
// ПорядокДоставки - ПеречислениеСсылка.ПорядокДоставкиПисемПоВнутреннейМаршрутизации - быстрая или долгая
// НомерЗадания - Число - номер потока
Процедура ПринятьПочтуВнутреннейМаршрутизацииФоновымЗаданием_Долгая(ПорядокДоставки, НомерЗадания) Экспорт
	
	ПринятьПочтуВнутреннейМаршрутизацииФоновымЗаданием(ПорядокДоставки, НомерЗадания);
	
КонецПроцедуры

// Реализация - Принять почту для указанного задания
//
//  Параметры
// ПорядокДоставки - ПеречислениеСсылка.ПорядокДоставкиПисемПоВнутреннейМаршрутизации - быстрая или долгая
// НомерЗадания - Число - номер потока
Процедура ПринятьПочтуВнутреннейМаршрутизацииФоновымЗаданием(ПорядокДоставки, НомерЗадания) Экспорт
	
	Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'НомерЗадания: %1'"),
		Строка(НомерЗадания));
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Приемка внутренней почты.Начало работы фонового задания.'"),
		УровеньЖурналаРегистрации.Информация,,,
		Описание);
		
	УстановитьПривилегированныйРежим(Истина);
		
	// При начале рег задания делаем запись в РС СостоянияФоновыхЗаданийПриемкиВнутреннейМаршрутизации

	РегистрыСведений.СостоянияФоновыхЗаданийПриемкиВнутреннейМаршрутизации.ЗаписатьСтатус(
		ПорядокДоставки, НомерЗадания, ТекущаяДатаСеанса(), ТекущаяДатаСеанса(), Неопределено);
		
	ВыполнитьПриемкуВнутреннейПочты(ПорядокДоставки, НомерЗадания);
	
	// При завершении рег задания стираем запись в СостоянияФоновыхЗаданийПриемкиВнутреннейМаршрутизации
	НаборЗаписей = РегистрыСведений.СостоянияФоновыхЗаданийПриемкиВнутреннейМаршрутизации.СоздатьНаборЗаписей();	
	НаборЗаписей.Отбор.ПорядокДоставки.Установить(ПорядокДоставки);
	НаборЗаписей.Отбор.Поток.Установить(НомерЗадания);
	НаборЗаписей.Записать();
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Приемка внутренней почты.Конец работы фонового задания.'"),
		УровеньЖурналаРегистрации.Информация,,,
		Описание);
		
КонецПроцедуры

// Получает письма  по внутренней маршрутизации - с указанным потоком
//
// Параметры
// ПорядокДоставки - быстрая или долгая
// НомерЗадания - номер потока - число
//
Процедура ВыполнитьПриемкуВнутреннейПочты(ПорядокДоставки, НомерЗадания)
	
	Если Не ВстроеннаяПочтаСерверПовтИсп.ИспользоватьВнутреннююМаршрутизацию() Тогда 
		Возврат;
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВремяНачала = ТекущаяДатаСеанса();
	
	ИдентификаторСеанса = Новый УникальныйИдентификатор;
	ПорядковыйНомерСобытия = 0;
	ПараметрыЛогирования = Новый Структура("ИдентификаторСеанса, ПорядковыйНомерСобытия, НомерЗадания",
		ИдентификаторСеанса, ПорядковыйНомерСобытия, 0);
		
	ДанныеДоставки = "ВыполнитьПриемкуВнутреннейПочты " + Строка(ПорядокДоставки) + " " + Строка(НомерЗадания);
	ПараметрыЛогирования.Вставить("ДанныеДоставки", ДанныеДоставки);	
		
	ПолучитьПисьмаПоВнутреннейМаршрутизации_ПоПотоку(ПараметрыЛогирования, ПорядокДоставки, НомерЗадания);
	
	ВремяОкончания = ТекущаяДатаСеанса();
	ТекстЛога = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Длительность сеанса: %1 сек.'"),
		ВремяОкончания - ВремяНачала);
	
КонецПроцедуры	

// Принимает почту по внутр маршрутизации, для указанного НомерЗадания и ПорядокДоставки
Процедура ПолучитьПисьмаПоВнутреннейМаршрутизации_ПоПотоку(
	ПараметрыЛогирования, ПорядокДоставки, НомерЗадания)	
	
	Пока Истина Цикл
	
		СообщенияОбОшибках = Новый Массив;
		
		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 20
		|	ОчередьВнутреннейПриемкиПисем.Письмо КАК Письмо,
		|	ОчередьВнутреннейПриемкиПисем.Адресат КАК Адресат,
		|	ОчередьВнутреннейПриемкиПисем.ДатаПодготовки КАК ДатаПодготовки,
		|	ОчередьВнутреннейПриемкиПисем.Приоритет КАК Приоритет
		|ИЗ
		|	РегистрСведений.ОчередьВнутреннейПриемкиПисем КАК ОчередьВнутреннейПриемкиПисем
		|ГДЕ
		|	ОчередьВнутреннейПриемкиПисем.ПорядокДоставки = &ПорядокДоставки
		|	И ОчередьВнутреннейПриемкиПисем.Поток = &Поток
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет УБЫВ,
		|	ОчередьВнутреннейПриемкиПисем.ДатаОтправки";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		
		Запрос.УстановитьПараметр("ПорядокДоставки", ПорядокДоставки);
		Запрос.УстановитьПараметр("Поток", НомерЗадания);
		
		РезультатЗапроса = Запрос.Выполнить();
				
		Таблица = РезультатЗапроса.Выгрузить();	
		
		Если Таблица.Количество() = 0 Тогда  // нет записей, выходим
			Возврат;
		КонецЕсли;	
		
		НомерПисьма = 0;
		
		Для Каждого Строка Из Таблица Цикл
			
			ПолучитьПисьмоПоВнутреннейМаршрутизации(
				Строка.Письмо,
				Строка.Адресат,
				Строка.ДатаПодготовки,
				СообщенияОбОшибках,
				ПараметрыЛогирования);
			
			Описание = СтрШаблон("%1 %2", Строка(Строка.Письмо), Строка(Строка.Адресат));
			
			НомерПисьма = НомерПисьма + 1;
			
			Если НомерПисьма%2 = 0 Тогда
				РегистрыСведений.СостоянияФоновыхЗаданийПриемкиВнутреннейМаршрутизации.ЗаписатьСтатус(
					ПорядокДоставки, НомерЗадания, Неопределено, ТекущаяДатаСеанса(), Описание);
			КонецЕсли;	
			
		КонецЦикла;	
		
	КонецЦикла;	
	
КонецПроцедуры

// Выполнить запуск фонового задания
Процедура ЗапуститьФоновоеЗадание_ВнутреннейМаршрутизации(НомерПотока, ПорядокДоставки, КлючФоновогоЗадания)
	
	ПараметрыВыполнения = Новый Массив;
	ПараметрыВыполнения.Добавить(ПорядокДоставки);
	ПараметрыВыполнения.Добавить(НомерПотока);
	
	ОписаниеЗадания = СтрШаблон(
		НСтр("ru = 'Приемка писем внутр маршр потока: %1. Порядок доставки: %2'"),
		НомерПотока, ПорядокДоставки);
	
	Попытка
		
		ОтборФоновыхЗаданий = Новый Структура;
		ОтборФоновыхЗаданий.Вставить("Ключ", КлючФоновогоЗадания);
		ОтборФоновыхЗаданий.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
		
		МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборФоновыхЗаданий);
		
		Если МассивФоновыхЗаданий.Количество() = 0 Тогда
			
			Описание = НСтр("ru = 'НомерПотока: %1
				|Ключ фонового задания: %2'");
				
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Описание,
			Строка(НомерПотока),
			КлючФоновогоЗадания);
				
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Приемка писем внутр маршр. Запускаем фоновое задание для приемки писем.'"),
				УровеньЖурналаРегистрации.Информация,,,
				Описание);
				
			ИмяМетода = "";
			Если ПорядокДоставки = Перечисления.ПорядокДоставкиПисемПоВнутреннейМаршрутизации.БыстраяДоставка Тогда
				ИмяМетода = "ВстроеннаяПочтаСервер.ПринятьПочтуВнутреннейМаршрутизацииФоновымЗаданием_Быстрая";
			Иначе	
				ИмяМетода = "ВстроеннаяПочтаСервер.ПринятьПочтуВнутреннейМаршрутизацииФоновымЗаданием_Долгая";
			КонецЕсли;	
				
			ФоновыеЗадания.Выполнить(
			ИмяМетода,
			ПараметрыВыполнения,
			КлючФоновогоЗадания,
			ОписаниеЗадания);
			
		КонецЕсли;
		
	Исключение
		
		Описание = НСтр("ru = 'НомерПотока: %1
			|Ключ фонового задания: %2'");
		
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Описание,
		Строка(НомерПотока),
		КлючФоновогоЗадания);
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Приемка писем внутр маршр. Не удалось запустить фоновое задание для приемки писем.'"),
			УровеньЖурналаРегистрации.Ошибка,,,
			Описание);
		
	КонецПопытки;
	
КонецПроцедуры

// Вычисляет приоритет для очереди писем по внутр маршрутизации
Функция ПриоритетОчередиВнутреннейМаршрутизации(Письмо, УчетнаяЗапись)
	
	Приоритет = 0;

	Важность = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Письмо, "Важность");
	Если Важность = Перечисления.ВажностьПисем.Высокая Тогда 
		Приоритет = 1;
	КонецЕсли;	
	
	Возврат Приоритет;
	
КонецФункции	

// Код реализации рег задания ДиспетчерПриемкиВнутреннейМаршрутизации
Процедура ДиспетчерПриемкиВнутреннейМаршрутизацииРеализация(ПорядокДоставки) 
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания();
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗависшиеПотоки = Новый Массив;
	ПроверитьЗависшиеПотокиВнутреннейМаршрутизации(ПорядокДоставки, ЗависшиеПотоки);
	
	ВсегоВОЧереди = ВсегоВОчередиВнутреннейМаршрутизации(ПорядокДоставки);
	Если ВсегоВОЧереди = 0 Тогда
		Возврат;
	КонецЕсли;	

	// смотрим сколько всего писем, и сколько с незаполненным потоком.
	ДанныеПотоков = Новый Соответствие; // ключ - Поток, значение - СколькоПисем
	МассивНомеров = Новый Массив; 
	ЧислоПисемНаПоток = 1;
	ЗаполнитьДанныеЧислаПисемПоПотокамВнутреннейМаршрутизации(
		ПорядокДоставки, ЧислоПисемНаПоток, ДанныеПотоков, МассивНомеров, ВсегоВОЧереди, ЗависшиеПотоки);
	
	// обходим все незаполненные, ставим Поток
	ЗаполнитьПотокДляВсехНезаполненныхВнутреннейМаршрутизации(
		ПорядокДоставки, ЧислоПисемНаПоток, ДанныеПотоков, МассивНомеров);
	
	// обходим, делаем запуск фоновых, если они еще не запущены.
	ЗапускФоновыхВнутреннейМаршрутизации(ПорядокДоставки);
	
КонецПроцедуры

Процедура ЗаполнитьДанныеЧислаПисемПоПотокамВнутреннейМаршрутизации(
	ПорядокДоставки, ЧислоПисемНаПоток, ДанныеПотоков, МассивНомеров, ВсегоВОЧереди, ЗависшиеПотоки)
	
	МаксЧислоПотоков = 1;
	Если ПорядокДоставки = Перечисления.ПорядокДоставкиПисемПоВнутреннейМаршрутизации.БыстраяДоставка Тогда
		МаксЧислоПотоков = Константы.МаксимальноеЧислоПотоковПриемкиВнутреннейМаршрутизацииБыстрой.Получить();
	Иначе	
		МаксЧислоПотоков = Константы.МаксимальноеЧислоПотоковПриемкиВнутреннейМаршрутизацииДолгой.Получить();
	КонецЕсли;
	
	ЧислоПисемНаПоток = 5;
	НужноеЧислоПотоков = Окр(ВсегоВОЧереди / ЧислоПисемНаПоток);
	Если НужноеЧислоПотоков = 0 Тогда
		НужноеЧислоПотоков = 1;
	КонецЕсли;	
	Если НужноеЧислоПотоков > МаксЧислоПотоков Тогда
		НужноеЧислоПотоков = МаксЧислоПотоков;
		ЧислоПисемНаПоток = Окр(ВсегоВОЧереди / НужноеЧислоПотоков);
	КонецЕсли;	
	
	РазмерМассиваПотоков = НужноеЧислоПотоков + ЗависшиеПотоки.Количество();
	
	Для Поток = 1 По РазмерМассиваПотоков Цикл
		
		Если ЗависшиеПотоки.Найти(Поток) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;	
		
		ПисемНаПотоке = СколькоПисемНаПотоке(ПорядокДоставки, Поток);
		ДанныеПотоков.Вставить(Поток, ПисемНаПотоке);
		МассивНомеров.Добавить(Поток);
		
	КонецЦикла;	
	
	КонечнаяПозиция = 0;
	Для Каждого КлючИЗначение Из ДанныеПотоков Цикл
		КонечнаяПозиция = Макс(КонечнаяПозиция, КлючИЗначение.Ключ);
	КонецЦикла;	
	
КонецПроцедуры

Процедура ЗаполнитьПотокДляВсехНезаполненныхВнутреннейМаршрутизации(
		ПорядокДоставки, ЧислоПисемНаПоток, ДанныеПотоков, МассивНомеров)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОчередьВнутреннейПриемкиПисем.Письмо КАК Письмо,
		|	ОчередьВнутреннейПриемкиПисем.Адресат КАК Адресат
		|ИЗ
		|	РегистрСведений.ОчередьВнутреннейПриемкиПисем КАК ОчередьВнутреннейПриемкиПисем
		|ГДЕ
		|	ОчередьВнутреннейПриемкиПисем.Поток = 0
		|	И ОчередьВнутреннейПриемкиПисем.ПорядокДоставки = &ПорядокДоставки";
	
	Запрос.УстановитьПараметр("ПорядокДоставки", ПорядокДоставки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПозицияСНуля = 0;
	ТекПозиция = МассивНомеров[ПозицияСНуля];
	
	Пока Выборка.Следующий() Цикл
		
		СколькоПисем = ДанныеПотоков.Получить(ТекПозиция);
		
		Если СколькоПисем >= ЧислоПисемНаПоток Тогда
			// тут уже заполнено, переходим дальше
			
			Если ПозицияСНуля <> МассивНомеров.Количество() - 1 Тогда // тут уже не переходим
				ПозицияСНуля = ПозицияСНуля + 1;
				ТекПозиция = МассивНомеров[ПозицияСНуля];
				СколькоПисем = ДанныеПотоков.Получить(ТекПозиция);
			КонецЕсли;	
			
		КонецЕсли;
		
		НовыйНомерПотока = ТекПозиция;
		
		ПотокБылИзменен = РегистрыСведений.ОчередьВнутреннейПриемкиПисем.ПотокПисьмаДляАдресатаБылИзменен(
			Выборка.Письмо,
			Выборка.Адресат,
			НовыйНомерПотока);
		Если ПотокБылИзменен Тогда
			ДанныеПотоков[ТекПозиция] = СколькоПисем + 1;
		КонецЕсли;
		
	КонецЦикла;	
		
КонецПроцедуры		

Процедура ЗапускФоновыхВнутреннейМаршрутизации(ПорядокДоставки)   
	
	Длина5Минут = 5 * 60;
	
	МассивПотоки = РегистрыСведений.ОчередьВнутреннейПриемкиПисем.ПотокиСПисьмами(ПорядокДоставки);
	Для Каждого НомерПотока Из МассивПотоки Цикл
		
		МенеджерЗаписи = РегистрыСведений.СостоянияФоновыхЗаданийПриемкиВнутреннейМаршрутизации.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ПорядокДоставки = ПорядокДоставки;
		МенеджерЗаписи.Поток = НомерПотока;
		МенеджерЗаписи.Прочитать();
		
		КлючФоновогоЗадания = "ПолучениеВнутреннейМаршр_" 
			+ Строка(ПорядокДоставки) + "_" + Строка(НомерПотока);
		
		ОтборФоновыхЗаданий = Новый Структура;
		ОтборФоновыхЗаданий.Вставить("Ключ", КлючФоновогоЗадания);
		ОтборФоновыхЗаданий.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
		
		МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборФоновыхЗаданий);
		
		ПричинаЗависания = "";
		СтрокаСтарыхУчетныхЗаписей = "";
		
		// был перезапуск сервера (или задание вылетело с ошибкой), и задания уже нет
		Если МассивФоновыхЗаданий.Количество() = 0 Тогда
			
			// стираем запись в СостоянияФоновыхЗаданийПриемкиВнутреннейМаршрутизации
			НаборЗаписей = РегистрыСведений.СостоянияФоновыхЗаданийПриемкиВнутреннейМаршрутизации.СоздатьНаборЗаписей();	
			НаборЗаписей.Отбор.Поток.Установить(НомерПотока);
			НаборЗаписей.Отбор.ПорядокДоставки.Установить(ПорядокДоставки);
			НаборЗаписей.Записать();
			
			ЗапуститьФоновоеЗадание_ВнутреннейМаршрутизации(НомерПотока, ПорядокДоставки, КлючФоновогоЗадания);
			Продолжить;
				
		КонецЕсли;				
		
		// фоновое задание еще не завершилось, 
		// работает нормально (последняя активность не более 5 минут назад)
		Если МенеджерЗаписи.ДатаПоследнейАктивности >= ТекущаяДатаСеанса() - Длина5Минут Тогда
			
			Продолжить;
			
		Иначе // поток завис - тут ничего не делаем
			
		КонецЕсли;	
		
		
	КонецЦикла;	
	
КонецПроцедуры		

#КонецОбласти