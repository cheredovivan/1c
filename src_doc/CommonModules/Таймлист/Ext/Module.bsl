
#Область ПрограммныйИнтерфейс

// Возвращает признак использования сервиса в программе
//
// Возвращаемое значение:
//  Булево - Истина, если сервис используется в программе.
//
Функция ТаймлистИспользуется() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользуетсяРаспознаваниеТаймлист");
	
КонецФункции

// Записывает логин и пароль доступа сервиса в информационную базу
//
// Параметры:
//  ДанныеАвторизации - Структура Из КлючИЗначение:
//   * Логин - Строка - логин пользователя Таймлист.
//   * Пароль - Строка - пароль пользователя Таймлист.
//
Процедура ЗаписатьДанныеАвторизации(ДанныеАвторизации) Экспорт
	
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
		"Таймлист",
		ДанныеАвторизации.Логин,
		"login");
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
		"Таймлист",
		ДанныеАвторизации.Пароль,
		"password");
	
КонецПроцедуры

// Получает логин и пароль доступа к сервису Таймлист
//
// Возвращаемое значение:
//  Структура:
//   * Логин - Строка
//   * Пароль - Строка
//  Неопределено - при отсутствии сохраненных данных аутентификации.
//
Функция ДанныеАвторизацииВСервисе() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеВБезопасномХранилище = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		"Таймлист",
		"login, password");
	
	Если ДанныеВБезопасномХранилище.login <> Неопределено
		И ДанныеВБезопасномХранилище.password <> Неопределено Тогда
		Возврат Новый Структура(
			"Логин, Пароль",
			ДанныеВБезопасномХранилище.login,
			ДанныеВБезопасномХранилище.password)
	КонецЕсли;
	
КонецФункции

// Выполняет проверку подключения к сервису Таймлист по указанным логину и паролю.
//
// Параметры:
//  Логин - Строка
//  Пароль - Строка
//  Порт - Число
//  ЗащищенноеСоединение - Булево
//
// Возвращаемое значение:
//  Булево - Истина, если подключение было успешно установлено
//
Функция СервисПоДаннымАвторизацииПодключен(Логин, Пароль, Порт, ЗащищенноеСоединение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Соединение = НовоеHTTPСоединение();
	
	СтрокаЗапроса = СтрШаблон("login?phone=%1&password=%2", Логин, Пароль);
	
	HTTPЗапрос = Новый HTTPЗапрос(СтрокаЗапроса);
	
	ОтветСервиса = Соединение.Получить(HTTPЗапрос);
	
	Если ОтветСервиса.КодСостояния = 200 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Обработчик регламентного задания ТаймлистОбработкаЗаданий.
// 
Процедура ОбработатьЗадания() Экспорт
	
	Отказ = Ложь;
	РегламентноеЗадание = Метаданные.РегламентныеЗадания.ТаймлистОбработкаЗаданий;
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(РегламентноеЗадание, Отказ);
	

	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаймлистФайлыВОбработке.ВерсияФайла,
		|	ТаймлистФайлыВОбработке.Задание,
		|	ТаймлистФайлыВОбработке.Параметры,
		|	ЕСТЬNULL(ТаймлистОшибкиРаботыССервисом.КоличествоПопыток, 0) КАК КоличествоПопыток
		|ИЗ
		|	РегистрСведений.ТаймлистФайлыВОбработке КАК ТаймлистФайлыВОбработке
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаймлистОшибкиРаботыССервисом КАК ТаймлистОшибкиРаботыССервисом
		|		ПО ТаймлистОшибкиРаботыССервисом.ВерсияФайла = ТаймлистФайлыВОбработке.ВерсияФайла";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.КоличествоПопыток > 3 Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыИзХранилищаЗначения = Выборка.Параметры.Получить();
		ВыполнитьЗадание(Выборка.ВерсияФайла, Выборка.Задание, ПараметрыИзХранилищаЗначения);
	КонецЦикла;
	
КонецПроцедуры

// Помещает файл в очередь обработки.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов - обрабатываемая версия файла.
//  Задание - ПеречислениеСсылка.ТаймлистТипыЗаданий - выполняемое задание.
//  Параметры - Структура - параметры выполнения задания.
// 
Процедура ПоместитьФайлВОчередьОбработки(ВерсияФайла, Задание, Параметры = Неопределено) Экспорт
	
	РегистрыСведений.ТаймлистФайлыВОбработке.Добавить(ВерсияФайла, Задание, Параметры);
	
КонецПроцедуры

// Возвращает данные работы сервиса с указанным файлом.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов - обрабатываемая версия файла.
//  Поля - Строка - список полей через разделитель ",". Если не указаны, то берутся все поля.
//
// Возвращаемое значение:
//  Структура - см. РегистрСведений.ТаймлистДанныеРаботыСервиса.ОписаниеДанных
// 
Функция ДанныеРаботыСервиса(ВерсияФайла, Поля = "") Экспорт
	
	Возврат РегистрыСведений.ТаймлистДанныеРаботыСервиса.Данные(ВерсияФайла, Поля);
	
КонецФункции

// Удаляет данные работы сервиса по переданной версии файла, а также выполняет очистку в самом сервисе.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов - обрабатываемый файл.
//  ИдентификаторЗадания - Строка
// 
Процедура УдалитьДанныеФайла(ВерсияФайла, ИдентификаторЗадания = "") Экспорт
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ВыполнитьУдалениеЗаданияВСервисе(ИдентификаторЗадания);
	Иначе
		ИдентификаторЗадания = ДанныеРаботыСервиса(ВерсияФайла, "ИдентификаторЗадания").ИдентификаторЗадания;
		Если ИдентификаторЗадания <> Неопределено Тогда
			ВыполнитьУдалениеЗаданияВСервисе(ИдентификаторЗадания);
		КонецЕсли;
	КонецЕсли;
	
	РегистрыСведений.ТаймлистДанныеРаботыСервиса.Удалить(ВерсияФайла);
	РегистрыСведений.ТаймлистФайлыВОбработке.Удалить(ВерсияФайла);
	РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Удалить(ВерсияФайла);
	
КонецПроцедуры

// Формирует описание параметров редактирования.
// 
// Возвращаемое значение:
//  Структура:
//   * Расшифровка - Неопределено
//   * Спикеры - Неопределено
//   * Автопротокол - Неопределено
//   * СледующиеШаги - Неопределено
// 
Функция ПараметрыЗаписиДанныхРедактирования() Экспорт
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("Расшифровка", Неопределено);
	ПараметрыЗаписи.Вставить("Спикеры", Неопределено);
	ПараметрыЗаписи.Вставить("Автопротокол", Неопределено);
	ПараметрыЗаписи.Вставить("СледующиеШаги", Неопределено);
	
	Возврат ПараметрыЗаписи;
	
КонецФункции

// Позволяет дополнить запрос для получения данных работы сервиса.
// 
// Параметры:
//  Запрос - Запрос
// 
Процедура ДополнитьЗапросПолученияДанных(Запрос) Экспорт
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДополнительныеПоляТаймлист",
		"ЕСТЬNULL(ТаймлистДанныеРаботыСервиса.Статус, ЗНАЧЕНИЕ(Перечисление.ТаймлистСтатусы.ПустаяСсылка)) КАК ТаймлистСтатус,
		|	ЕСТЬNULL(ТаймлистДанныеРаботыСервиса.СледующиеШаги, """") КАК ТаймлистСледующиеШаги,
		|	ЕСТЬNULL(ТаймлистФайлыВОбработке.Задание, ЗНАЧЕНИЕ(Перечисление.ТаймлистТипыЗаданий.ПустаяСсылка)) КАК ТаймлистЗадание");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДополнительныеТаблицыТаймлист",
		"ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаймлистДанныеРаботыСервиса КАК ТаймлистДанныеРаботыСервиса
		|		ПО (ТаймлистДанныеРаботыСервиса.ВерсияФайла = Файлы.ТекущаяВерсия)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаймлистФайлыВОбработке КАК ТаймлистФайлыВОбработке
		|		ПО (ТаймлистФайлыВОбработке.ВерсияФайла = Файлы.ТекущаяВерсия)");
	
КонецПроцедуры

#Область ОбзорHTML

// Формирует HTML представление файла с предложением распознать в Таймлист.
// 
// Параметры:
//  Файл - СправочникСсылка.Файлы
//  РазмерФайла - Число
//  ДоступноРаспознаваниеТаймлист - Булево
// 
// Возвращаемое значение:
//  Строка - HTML представление файла с предложением распознать в Таймлист.
//
Функция HTMLПредставлениеРаспознатьВТаймлист(Файл, РазмерФайла, ДоступноРаспознаваниеТаймлист = Ложь) Экспорт
	
	Если ДоступноРаспознаваниеТаймлист Тогда
		
		РазмерФайлаВМб = РазмерФайла / (1024 * 1024);
		
		Если РазмерФайлаВМб > 1024 Тогда
			ТекстHTML = НСтр("ru = 'Размер файла превышает пороговое значение (1 Гб)<br>
			|<font color=""#006699""><a href = ""OpenForView"">Открыть</a></font><br><br>'");
		Иначе
			ШаблонТекстаHTML = НСтр("ru = 'Данные предпросмотра отсутствуют<br>
			|<font color=""#006699""><a href = ""OpenForView"">Открыть</a></font><br><br>
			|<font color=""#006699""><a href = ""v8doc:files/Recognize/%1"">Распознать в Таймлист</a></font>'");
			
			ТекстHTML = СтрШаблон(ШаблонТекстаHTML, ПолучитьНавигационнуюСсылку(Файл));
		КонецЕсли;
		
	Иначе
		ТекстHTML = НСтр("ru = 'Данные предпросмотра отсутствуют<br>
		|<font color=""#006699""><a href = ""OpenForView"">Открыть</a></font><br><br>'");
	КонецЕсли;
	
	Результат = ОбзорФайловКлиентСервер.СообщениеВПредпросмотр(ТекстHTML);
	
	Возврат Результат;
	
КонецФункции

// Возвращает HTML с текстом ожидания получения расшифровки.
//
// Параметры:
//  ТекстОшибки - Строка
//  ИдентификаторЗадания - Строка
//
// Возвращаемое значение:
//  Строка
//
Функция HTMLПредставлениеПолучениеРасшифровки(ТекстОшибки, ИдентификаторЗадания) Экспорт
	
	Если ЗначениеЗаполнено(ТекстОшибки) И ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ТекстПолучениеРасшифровки =
			СтрШаблон(НСтр("ru = 'Во время получения расшифровки возникла ошибка: %1.<br>
			|Обратитесь в службу поддержки Таймлист, приложив номер запроса %2.<br>
			|<font color=""#006699""><a href = ""OpenForView"">Открыть</a></font>'"),
			ТекстОшибки, ИдентификаторЗадания);
	Иначе
		ТекстПолучениеРасшифровки = НСтр("ru = 'Выполняется получение расшифровки файла. Пожалуйста, подождите...<br>
		|<font color=""#006699""><a href = ""OpenForView"">Открыть</a></font>'");
	КонецЕсли;
	
	ЗаголовокСообщения = ОбзорФайловКлиентСервер.ЗаголовокСообщенияВОбластиПредпросмотра();
	
	ВставляемыйБлок = Новый Массив;
	ВставляемыйБлок.Добавить("<html>");
	ВставляемыйБлок.Добавить(ЗаголовокСообщения);
	ВставляемыйБлок.Добавить("<body>");
	ВставляемыйБлок.Добавить("<div class=""block"">");
	ВставляемыйБлок.Добавить(ТекстПолучениеРасшифровки);
	ВставляемыйБлок.Добавить(
		"</div>
		|</body>
		|</html>");
	
	Результат = СтрСоединить(ВставляемыйБлок, Символы.ПС);
	
	Возврат Результат;
	
КонецФункции

// Возвращает HTML расшифровки с командой получения автопротокола.
// 
// Параметры:
//  РасшифровкаHTML - Строка
//  Файл - СправочникСсылка.Файлы
//
// Возвращаемое значение:
//  Строка
//
Функция HTMLПредставлениеКомандаПолучитьАвтопротокол(РасшифровкаHTML, Файл) Экспорт
	
	ДокументHTML = РаботаС_HTML.ПолучитьДокументHTMLИзТекстаHTML(РасшифровкаHTML);
	
	ВставляемыйБлок = Новый Массив;
	ВставляемыйБлок.Добавить(ТаймлистКлиентСервер.СтильГиперссылки());
	ВставляемыйБлок.Добавить("<div class=""block"">");
	ВставляемыйБлок.Добавить(СтрШаблон(
		НСтр("ru = '<font color=""#006699""><a href = ""v8doc:files/GetAutoprotocol/%1"">Получить автопротокол</a></font>'"),
		ПолучитьНавигационнуюСсылку(Файл)));
	ВставляемыйБлок.Добавить("<br>");
	ВставляемыйБлок.Добавить(СтрШаблон(
		НСтр("ru = '<font color=""#006699""><a href = ""v8doc:files/TimelistEditTranscript /%1"">Редактировать</a></font>'"),
		ПолучитьНавигационнуюСсылку(Файл)));
	ВставляемыйБлок.Добавить("</div>");
	РезультатHTML = СтрСоединить(ВставляемыйБлок, Символы.ПС);
	
	ФрагментHTML = РаботаС_HTML.ПолучитьДокументHTMLИзТекстаHTML(РезультатHTML);
	Документ_HTML = ДокументHTML.ИмпортироватьУзел(ФрагментHTML.ЭлементДокумента, Истина);
	ДокументHTML.Тело.ВставитьПеред(Документ_HTML, ДокументHTML.Тело.ДочерниеУзлы[0]);
	
	Результат = РаботаС_HTML.ПолучитьТекстHTMLИзОбъектаДокументHTML(ДокументHTML);
	
	Возврат Результат;
	
КонецФункции

// Возвращает HTML расшифровки с текстом ожидания получения автопротокола.
// 
// Параметры:
//  РасшифровкаHTML - Строка
//  ТекстОшибки - Строка
//  ИдентификаторЗадания - Строка
//
// Возвращаемое значение:
//  Строка
//
Функция HTMLПредставлениеПолучениеАвтопротокола(РасшифровкаHTML, ТекстОшибки, ИдентификаторЗадания) Экспорт
	
	ДокументHTML = РаботаС_HTML.ПолучитьДокументHTMLИзТекстаHTML(РасшифровкаHTML);
	
	ВставляемыйБлок = Новый Массив;
	ВставляемыйБлок.Добавить(ТаймлистКлиентСервер.СтильГиперссылки());
	ВставляемыйБлок.Добавить("<div class=""block"">");
	Если ЗначениеЗаполнено(ТекстОшибки) И ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ВставляемыйБлок.Добавить(СтрШаблон(НСтр(
			"ru = '<font color=""#006699"">Во время получения автопротокола возникла ошибка: %1.<br>
			|Обратитесь в службу поддержки Таймлист, приложив номер запроса %2.<br></font>'"),
			ТекстОшибки, ИдентификаторЗадания));
	Иначе
		ВставляемыйБлок.Добавить(НСтр(
			"ru = '<font color=""#006699"">Выполняется получение автопротокола... Ожидайте.</font>'"));
	КонецЕсли;
	ВставляемыйБлок.Добавить("</div>");
	РезультатHTML = СтрСоединить(ВставляемыйБлок, Символы.ПС);
	
	ФрагментHTML = РаботаС_HTML.ПолучитьДокументHTMLИзТекстаHTML(РезультатHTML);
	Документ_HTML = ДокументHTML.ИмпортироватьУзел(ФрагментHTML.ЭлементДокумента, Истина);
	РаботаС_HTML.ВставитьЭлементHTMLПервымДочернимЭлементом(ДокументHTML.Тело, Документ_HTML);
	
	Результат = РаботаС_HTML.ПолучитьТекстHTMLИзОбъектаДокументHTML(ДокументHTML);
	
	Возврат Результат;
	
КонецФункции

// Возвращает HTML расшифровки/автопротокола для вывода в обзор.
// 
// Параметры:
//  ТекстHTML - Строка
//  Файл - СправочникСсылка.Файлы
//  ЭтоРасшифровка - Булево - Истина, если отображается расшифровка, иначе автопротокол.
//
// Возвращаемое значение:
//  Строка
//
Функция HTMLПредставлениеОбзор(ТекстHTML, Файл, ЭтоРасшифровка = Истина) Экспорт
	
	ДокументHTML = РаботаС_HTML.ПолучитьДокументHTMLИзТекстаHTML(ТекстHTML);
	
	ВставляемыйБлок = Новый Массив;
	ВставляемыйБлок.Добавить(ТаймлистКлиентСервер.СтильГиперссылки());
	ВставляемыйБлок.Добавить("<div class=""block"">");
	Если ЭтоРасшифровка Тогда
		ВставляемыйБлок.Добавить(СтрШаблон(
			НСтр("ru = '<font color=""#006699""><a href = ""v8doc:files/TimelistEditTranscript/%1"">Редактировать</a></font>'"),
			ПолучитьНавигационнуюСсылку(Файл)));
	Иначе
		ВставляемыйБлок.Добавить(СтрШаблон(
			НСтр("ru = '<font color=""#006699""><a href = ""v8doc:files/TimelistEditAutoprotocol/%1"">Редактировать</a></font>'"),
			ПолучитьНавигационнуюСсылку(Файл)));
	КонецЕсли;
	ВставляемыйБлок.Добавить("</div>");
	РезультатHTML = СтрСоединить(ВставляемыйБлок, Символы.ПС);
	
	ФрагментHTML = РаботаС_HTML.ПолучитьДокументHTMLИзТекстаHTML(РезультатHTML);
	Документ_HTML = ДокументHTML.ИмпортироватьУзел(ФрагментHTML.ЭлементДокумента, Истина);
	РаботаС_HTML.ВставитьЭлементHTMLПервымДочернимЭлементом(ДокументHTML.Тело, Документ_HTML);
	
	Результат = РаботаС_HTML.ПолучитьТекстHTMLИзОбъектаДокументHTML(ДокументHTML);
	
	Возврат Результат;
	
КонецФункции

// Возвращает HTML расшифровки/автопротокола без служебных тегов.
// 
// Параметры:
//  ТекстHTML - Строка
//
// Возвращаемое значение:
//  Строка
//
Функция HTMLПредставлениеБезСлужебныхТегов(ТекстHTML) Экспорт
	
	ДокументHTML = РаботаС_HTML.ПолучитьДокументHTMLИзТекстаHTML(ТекстHTML);
	ДокументHTML.Тело.УдалитьДочерний(ДокументHTML.Тело.ДочерниеУзлы[0]);
	ДокументHTML.Тело.УдалитьДочерний(ДокументHTML.Тело.ДочерниеУзлы[0]);
	
	Результат = РаботаС_HTML.ПолучитьТекстHTMLИзОбъектаДокументHTML(ДокументHTML);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Определяет следующие свойств регламентных заданий:
//  - зависимость от функциональных опций.
//  - возможность выполнения в различных режимах работы программы.
//  - прочие параметры.
//
// Параметры:
//  Настройки - ТаблицаЗначений - таблица значений с колонками:
//    * РегламентноеЗадание - ОбъектМетаданныхРегламентноеЗадание - регламентное задание.
//    * ФункциональнаяОпция - ОбъектМетаданныхФункциональнаяОпция - функциональная опция,
//        от которой зависит регламентное задание.
//    * ЗависимостьПоИ      - Булево - если регламентное задание зависит более, чем
//        от одной функциональной опции и его необходимо включать только тогда,
//        когда все функциональные опции включены, то следует указывать Истина
//        для каждой зависимости.
//        По умолчанию Ложь - если хотя бы одна функциональная опция включена,
//        то регламентное задание тоже включено.
//    * ВключатьПриВключенииФункциональнойОпции - Булево, Неопределено - если Ложь, то при
//        включении функциональной опции регламентное задание не будет включаться. Значение
//        Неопределено соответствует значению Истина.
//        По умолчанию - неопределено.
//    * ДоступноВПодчиненномУзлеРИБ - Булево, Неопределено - Истина или Неопределено, если регламентное
//        задание доступно в РИБ.
//        По умолчанию - неопределено.
//    * ДоступноВПодчиненномУзлеКОД - Булево, Неопределено - Истина или Неопределено, если регламентное
//        задание доступно в подчиненном узле КОД.
//        По умолчанию - неопределено.
//    * ДоступноВАвтономномРабочемМесте - Булево, Неопределено - Истина или Неопределено, если регламентное
//        задание доступно в автономном рабочем месте.
//        По умолчанию - неопределено.
//    * ДоступноВМоделиСервиса      - Булево, Неопределено - Истина или Неопределено, если регламентное
//        задание доступно в модели сервиса.
//        По умолчанию - неопределено.
//    * РаботаетСВнешнимиРесурсами  - Булево - Истина, если регламентное задание модифицирует данные
//        во внешних источниках (получение почты, синхронизация данных и т.п.). Не следует устанавливать
//        значение Истина, для регламентных заданий, не модифицирующих данные во внешних источниках.
//        Например, регламентное задание ЗагрузкаКурсовВалют. По умолчанию - Ложь.
//    * Параметризуется             - Булево - Истина, если регламентное задание параметризованное.
//        По умолчанию - Ложь.
//
// Например:
//	Настройка = Настройки.Добавить();
//	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ОбновлениеСтатусовДоставкиSMS;
//	Настройка.ФункциональнаяОпция = Метаданные.ФункциональныеОпции.ИспользоватьПочтовыйКлиент;
//	Настройка.ДоступноВМоделиСервиса = Ложь;
//
Процедура ПриОпределенииНастроекРегламентныхЗаданий(Настройки) Экспорт
	
	Настройка = Настройки.Добавить();
	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ТаймлистОбработкаЗаданий;
	Настройка.ФункциональнаяОпция = Метаданные.ФункциональныеОпции.ИспользуетсяРаспознаваниеТаймлист;
	Настройка.РаботаетСВнешнимиРесурсами = Истина;
	Настройка.ДоступноВПодчиненномУзлеКОД = Ложь;
	Настройка.ДоступноВМоделиСервиса = Ложь;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область МетодыРаботыССервисом

// Выполняет задание с файлом.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов - обрабатываемый файл.
//  Задание - ПеречислениеСсылка.ТаймлистТипыЗаданий - выполняемое задание.
//  Параметры - Структура - параметры выполнения задания.
// 
Процедура ВыполнитьЗадание(ВерсияФайла, Задание, Параметры = Неопределено)
	
	Если Не ТаймлистИспользуется() Тогда
		Возврат;
	КонецЕсли;
	
	ОшибкаПроверкиТокена = ТокенПроверен();
	Если ЗначениеЗаполнено(ОшибкаПроверкиТокена) Тогда
		РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Добавить(ВерсияФайла, ОшибкаПроверкиТокена);
		Возврат;
	КонецЕсли;
	
	ДанныеРаботыСервиса = ДанныеРаботыСервиса(ВерсияФайла);
	
	Если Задание = Перечисления.ТаймлистТипыЗаданий.ЗагрузитьФайлВСервис Тогда
		
		Если Не ЗначениеЗаполнено(ДанныеРаботыСервиса.Статус) Тогда
			
			ПараметрыЗагрузкиФайла = ПараметрыЗагрузкиФайлаВСервис(ВерсияФайла);
			Если ПараметрыЗагрузкиФайла <> Неопределено Тогда
				ЗагрузитьФайлВСервис(ВерсияФайла, ПараметрыЗагрузкиФайла);
			КонецЕсли;
			
		ИначеЕсли ДанныеРаботыСервиса.Статус = Перечисления.ТаймлистСтатусы.ОтправленНаЗагрузку Тогда
			
			ПроверитьЗагрузкуФайлаВСервис(ВерсияФайла, ДанныеРаботыСервиса.ИдентификаторЗадания);
			
		КонецЕсли;
		
	ИначеЕсли Задание = Перечисления.ТаймлистТипыЗаданий.НачатьРасшифровку Тогда
		
		НачатьРасшифровку(ВерсияФайла, ДанныеРаботыСервиса.ИдентификаторЗадания, Параметры);
		
	ИначеЕсли Задание = Перечисления.ТаймлистТипыЗаданий.ПроверитьРасшифровку Тогда
		
		ПроверитьРасшифровку(ВерсияФайла, ДанныеРаботыСервиса.ИдентификаторЗадания);
		
	ИначеЕсли Задание = Перечисления.ТаймлистТипыЗаданий.ПодготовитьДанныеКПолучениюАвтопротокола Тогда
		
		Если Параметры = Неопределено Или Не Параметры.Свойство("СпикерыДляАвтопротокола") Тогда
			ОписаниеОшибки = НСтр("ru = 'Отсутствуют данные спикеров для формирования автопротокола.'");
			РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Добавить(ВерсияФайла, ОписаниеОшибки);
			Возврат;
		КонецЕсли;
		
		ПодготовитьДанныеКПолучениюАвтопротокола(ВерсияФайла, ДанныеРаботыСервиса.ИдентификаторЗадания,
			Параметры.СпикерыДляАвтопротокола);
		
		Если Параметры.СпикерыДляАвтопротокола.Количество() = 0 Тогда
			ЗаменитьСпикеровВРасшифровке(ВерсияФайла, ДанныеРаботыСервиса.Расшифровка);
		КонецЕсли;
		
	ИначеЕсли Задание = Перечисления.ТаймлистТипыЗаданий.ПолучитьАвтопротокол Тогда
		
		НачатьФормированиеАвтопротокола(ВерсияФайла, ДанныеРаботыСервиса.ИдентификаторЗадания);
		
	ИначеЕсли Задание = Перечисления.ТаймлистТипыЗаданий.ПроверитьАвтопротокол Тогда
		
		ПроверитьАвтопротокол(ВерсияФайла, ДанныеРаботыСервиса.ИдентификаторАвтопротокола);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет загрузку файла в сервис.
//
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  ПараметрыЗагрузкиФайла - Структура:
//   * ИмяФайла - Строка
//   * ИмяФайлаСПутем - Строка
// 
Процедура ЗагрузитьФайлВСервис(ВерсияФайла, ПараметрыЗагрузкиФайла)
	
	ДанныеТокена = ТаймлистПовтИсп.ДанныеТокена();
	
	ПодключениеAPI = Новый Структура;
	ПодключениеAPI.Вставить("Заголовки", Новый Соответствие);
	ПодключениеAPI.Вставить("Данные", Новый Массив);
	
	ВебЗапрос = Новый HTTPЗапрос("/UploadUserFiles/", ПодключениеAPI.Заголовки);
	
	Разделитель = Прав(Формат(ТекущаяУниверсальнаяДатаВМиллисекундах(), "ЧЦ=16; ЧВН=; ЧГ=0"), 8)
		+ Формат(ТекущаяУниверсальнаяДатаВМиллисекундах(), "ЧЦ=16; ЧВН=; ЧГ=0");
	
	Тело = Новый ПотокВПамяти();
	ИмяПараметра = "files";
	ЗаписьДанных = Новый ЗаписьДанных(Тело);
	ЗаписьДанных.ЗаписатьСтроку("----------------------------" + Разделитель);
	ЗаписьДанных.ЗаписатьСтроку("Content-Disposition: form-data; name="""
		+ ИмяПараметра + """; filename=""" + ПараметрыЗагрузкиФайла.ИмяФайла + """");
	ЗаписьДанных.ЗаписатьСтроку("Content-Type: audio/mpeg");
	ЗаписьДанных.ЗаписатьСтроку("");
	ДвоичныеДанные = Новый ДвоичныеДанные(ПараметрыЗагрузкиФайла.ИмяФайлаСПутем);
	ЗаписьДанных.Записать(ДвоичныеДанные);
	ЗаписьДанных.ЗаписатьСтроку("");
	ИмяПараметра = "files";
	ЗаписьДанных.ЗаписатьСтроку("----------------------------" + Разделитель + "--");
	ЗаписьДанных.Закрыть();
	
	ДвоичныеДанные = Тело.ЗакрытьИПолучитьДвоичныеДанные();
	
	ПодключениеAPI.Заголовки.Вставить("Content-Type", "multipart/form-data; boundary=--------------------------"
		+ Разделитель);
	ПодключениеAPI.Заголовки.Вставить("Accept", "*/*");
	ПодключениеAPI.Заголовки.Вставить("Accept-Encoding", "gzip, deflate, br");
	
	ПодключениеAPI.Заголовки.Вставить("authorization", ДанныеТокена.Токен);
	
	Соединение = НовоеHTTPСоединение(3600);
	
	ВебЗапрос.УстановитьТелоИзДвоичныхДанных(ДвоичныеДанные);
	
	Ответ = Соединение.ВызватьHTTPМетод("POST", ВебЗапрос);
	ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
	
	ИдентификаторЗаданияРаспознавания = "";
	
	Если Ответ.КодСостояния = 200 Тогда
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТелоОтвета);
		РезультатОтвета = ПрочитатьJSON(
			ЧтениеJSON, Истина, ?(ПодключениеAPI.Свойство("ПоляДата"), ПодключениеAPI.ПоляДата, ""));
		
		Для Каждого Соответствие Из РезультатОтвета[0] Цикл
			
			Если Соответствие.Ключ = "orderId" И ЗначениеЗаполнено(Соответствие.Значение) Тогда
				
				ИдентификаторЗаданияРаспознавания = Соответствие.Значение;
				РегистрыСведений.ТаймлистДанныеРаботыСервиса.ЗаписатьНачалоЗагрузкиФайлаВСервис(
					ВерсияФайла, ИдентификаторЗаданияРаспознавания);
				Возврат;
			КонецЕсли;
			
			Если Соответствие.Ключ = "error" И ЗначениеЗаполнено(Соответствие.Значение) Тогда
				
				ОписаниеОшибки = Соответствие.Значение;
				РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Добавить(ВерсияФайла, ОписаниеОшибки);
				Возврат;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЧтениеJSON.Закрыть();
		
	Иначе
		
		ОписаниеОшибки = СтрШаблон(
			НСтр("ru = 'Код ответа сервера - %1. %2'"), Строка(Ответ.КодСостояния), ТелоОтвета);
		РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Добавить(ВерсияФайла, ОписаниеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

// Проверить загрузка файла в сервис.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  ИдентификаторФайлаВСервисе - Строка
// 
Процедура ПроверитьЗагрузкуФайлаВСервис(ВерсияФайла, ИдентификаторФайлаВСервисе)
	
	ДанныеТокена = ТаймлистПовтИсп.ДанныеТокена();
	
	Статус = "UNKNOWN";
	АдресРесурса = "orderStatus?orderId=" + ИдентификаторФайлаВСервисе;
	
	Соединение = НовоеHTTPСоединение();
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("User-Agent", "PostmanRuntime/7.36.0");
	Заголовки.Вставить("Accept", "*/*");
	Заголовки.Вставить("Accept-Encoding", "gzip, deflate, br");
	Заголовки.Вставить("Connection", "keep-alive");
	Заголовки.Вставить("authorization", ДанныеТокена.Токен);
	
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	Ответ = Соединение.Получить(Запрос);
	ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
	
	Если Ответ.КодСостояния = 200 Тогда
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТелоОтвета);
		
		МассивДанных = ПрочитатьJSON(ЧтениеJSON, Истина);
		
		Для Каждого Элемент Из МассивДанных Цикл
			
			Если Элемент.Ключ = "lastCheckStatus" Тогда
				
				Статус = Элемент.Значение;
				
			ИначеЕсли Статус = "UPLOADED" И Элемент.Ключ = "file" Тогда
				
				ПараметрыРасшифровки = Новый Структура("ИмяФайла", Элемент.Значение);
				
				РегистрыСведений.ТаймлистДанныеРаботыСервиса.ЗаписатьСтатусРаботыССервисом(ВерсияФайла,
					Перечисления.ТаймлистСтатусы.Загружен);
				ПоместитьФайлВОчередьОбработки(ВерсияФайла, Перечисления.ТаймлистТипыЗаданий.НачатьРасшифровку,
					ПараметрыРасшифровки);
				
				Возврат;
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Код ответа сервера - %1. %2'"),
			Строка(Ответ.КодСостояния), ТелоОтвета);
		РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Добавить(ВерсияФайла, ОписаниеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

// Отправляет запрос в сервис на запуск формирования расшифровки.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  ИдентификаторФайлаВСервисе - Строка
//  Параметры - Структура:
//   * КоличествоСпикеров - Число
//   * ИмяФайла - Строка
// 
Процедура НачатьРасшифровку(ВерсияФайла, ИдентификаторФайлаВСервисе, Параметры)
	
	ДанныеТокена = ТаймлистПовтИсп.ДанныеТокена();
	
	АдресРесурса = "translateFile?orderId=" + ИдентификаторФайлаВСервисе
		+ "&speakers=" + Строка(Параметры.КоличествоСпикеров) + "&filename=" + Параметры.ИмяФайла;
	
	Соединение = НовоеHTTPСоединение();
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("User-Agent", "PostmanRuntime/7.36.0");
	Заголовки.Вставить("Accept", "*/*");
	Заголовки.Вставить("Accept-Encoding", "gzip, deflate, br");
	Заголовки.Вставить("Connection", "keep-alive");
	
	Заголовки.Вставить("authorization", ДанныеТокена.Токен);
	
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	Ответ = Соединение.Получить(Запрос);
	ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
	
	Если Ответ.КодСостояния = 200 Тогда
		
		РегистрыСведений.ТаймлистДанныеРаботыСервиса.ЗаписатьСтатусРаботыССервисом(ВерсияФайла,
			Перечисления.ТаймлистСтатусы.ПолучениеРасшифровки);
		ПоместитьФайлВОчередьОбработки(ВерсияФайла, Перечисления.ТаймлистТипыЗаданий.ПроверитьРасшифровку);
	
	Иначе
		
		ОписаниеОшибки = СтрШаблон(
			НСтр("ru = 'Код ответа сервера - %1. %2'"), Строка(Ответ.КодСостояния), ТелоОтвета);
		РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Добавить(ВерсияФайла, ОписаниеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

// Проверяет статус расшифровки файла. После успешного выполнения записывает расшифровку в базу данных.
//
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  ИдентификаторФайлаВСервисе - Строка - идентификатор задания распознавания в сервисе.
// 
Процедура ПроверитьРасшифровку(ВерсияФайла, ИдентификаторФайлаВСервисе)
	
	ДанныеТокена = ТаймлистПовтИсп.ДанныеТокена();
	
	Статус = "UNKNOWN";
	АдресРесурса = "orderStatus?orderId=" + ИдентификаторФайлаВСервисе;
	
	Соединение = НовоеHTTPСоединение();
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("User-Agent", "PostmanRuntime/7.36.0");
	Заголовки.Вставить("Accept", "*/*");
	Заголовки.Вставить("Accept-Encoding", "gzip, deflate, br");
	Заголовки.Вставить("Connection", "keep-alive");
	Заголовки.Вставить("authorization", ДанныеТокена.Токен);
	
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	Ответ = Соединение.Получить(Запрос);
	ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
	
	Если Ответ.КодСостояния = 200 Тогда
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТелоОтвета);
		
		МассивДанных = ПрочитатьJSON(ЧтениеJSON, Истина);
		ЗначениеСоответствияОтветаСТаймКодами = МассивДанных.Получить("time_code");
		ЗначениеСоответствияОтветаСпикеры = МассивДанных.Получить("speakersData");
		
		Для Каждого Элемент Из МассивДанных Цикл
			
			Если Элемент.Ключ = "lastCheckStatus" Тогда
				
				Статус = Элемент.Значение;
				
			ИначеЕсли Статус = "SUCCESS" И Элемент.Ключ = "lastCheckAnswer" Тогда
				
				РасшифровкаHTMLХранилище = СформироватьРасшифровку(ЗначениеСоответствияОтветаСТаймКодами);
				СпикерыХранилище = СформироватьХранилищеСпикеров(ЗначениеСоответствияОтветаСпикеры);
				
				РегистрыСведений.ТаймлистДанныеРаботыСервиса.ЗаписатьРасшифровкуИСпикеров(ВерсияФайла,
					РасшифровкаHTMLХранилище, СпикерыХранилище);
				РегистрыСведений.ТаймлистФайлыВОбработке.Удалить(ВерсияФайла);
				
				Возврат;
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		ОписаниеОшибки = СтрШаблон(
			НСтр("ru = 'Код ответа сервера - %1. %2'"), Строка(Ответ.КодСостояния), ТелоОтвета);
		РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Добавить(ВерсияФайла, ОписаниеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

// Отправляет запрос в сервис на переименование спикеров и назначение им ролей.После успешной операции
// выполняет подстановку новых имен спикеров в текущую расшифровку. 
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  ИдентификаторФайлаВСервисе - Строка
//  Спикеры - ХранилищеЗначения
//  СпикерыДляАвтопротокола - Массив Из Структура:
//   * initialName - Строка
//   * newName - Строка
//   * role - Строка
// 
Процедура ПодготовитьДанныеКПолучениюАвтопротокола(ВерсияФайла, ИдентификаторФайлаВСервисе,
	СпикерыДляАвтопротокола)
	
	ОписаниеОшибки = "";
	
	Пока СпикерыДляАвтопротокола.Количество() > 0 Цикл
		
		Если СпикерыДляАвтопротокола[0].role = "true" Тогда
			
			ОписаниеОшибки = УстановитьГлавнуюРольСпикеру(СпикерыДляАвтопротокола[0].initialName,
				ВерсияФайла, ИдентификаторФайлаВСервисе);
			
			Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
				Прервать;
			КонецЕсли;
			
		КонецЕсли;
		
		Если СпикерыДляАвтопротокола[0].initialName <> СпикерыДляАвтопротокола[0].newName Тогда
			
			ОписаниеОшибки = УстановитьНовоеИмяСпикера(
				СпикерыДляАвтопротокола[0].initialName, СпикерыДляАвтопротокола[0].newName,
					ВерсияФайла, ИдентификаторФайлаВСервисе);
			
			Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
				Прервать;
			КонецЕсли;
			
		КонецЕсли;
		
		СпикерыДляАвтопротокола.Удалить(0);
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Добавить(ВерсияФайла, ОписаниеОшибки);
		ПоместитьФайлВОчередьОбработки(ВерсияФайла, Перечисления.ТаймлистТипыЗаданий.ПодготовитьДанныеКПолучениюАвтопротокола,
			Новый Структура("СпикерыДляАвтопротокола", СпикерыДляАвтопротокола));
	КонецЕсли;
	
КонецПроцедуры

// Отправляет запрос в сервис на запуск формирования расшифровки.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  ИдентификаторФайлаВСервисе - Строка
// 
Процедура НачатьФормированиеАвтопротокола(ВерсияФайла, ИдентификаторФайлаВСервисе)
	
	АдресРесурса = "generateProtocol?orderId=" + ИдентификаторФайлаВСервисе;
	
	Соединение = НовоеHTTPСоединение();
	Заголовки = Новый Соответствие;
	
	ДанныеТокена = ТаймлистПовтИсп.ДанныеТокена();
	
	Заголовки.Вставить("User-Agent", "PostmanRuntime/7.36.0");
	Заголовки.Вставить("Accept", "*/*");
	Заголовки.Вставить("Accept-Encoding", "gzip, deflate, br");
	Заголовки.Вставить("Connection", "keep-alive");
	Заголовки.Вставить("authorization", ДанныеТокена.Токен);

	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	Ответ = Соединение.Получить(Запрос);
	
	Если Ответ.КодСостояния = 200 Тогда
		
		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТелоОтвета);
		
		МассивДанных = ПрочитатьJSON(ЧтениеJSON, Истина);
		ИдентификаторАвтопротокола = МассивДанных.Получить("protocolQueryId");
		
		РегистрыСведений.ТаймлистДанныеРаботыСервиса.ЗаписатьНачалоФормированияАвтопротокола(ВерсияФайла,
			ИдентификаторАвтопротокола);
		ПоместитьФайлВОчередьОбработки(ВерсияФайла, Перечисления.ТаймлистТипыЗаданий.ПроверитьАвтопротокол);
		
	Иначе
		
		ОписаниеОшибки = СтрШаблон(
			НСтр("ru = 'Код ответа сервера - %1. %2'"), Строка(Ответ.КодСостояния), ТелоОтвета);
		РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Добавить(ВерсияФайла, ОписаниеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

// Проверяет статус формирования автопротокола. После успешного выполнения записывает автопротокол в базу данных.
//
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  ИдентификаторАвтопротокола - Строка - идентификатор задания распознавания в сервисе.
// 
Процедура ПроверитьАвтопротокол(ВерсияФайла, ИдентификаторАвтопротокола)
	
	АдресРесурса = "protocolStatus?protocolQueryId=" + ИдентификаторАвтопротокола;
	
	Соединение = НовоеHTTPСоединение();
	Заголовки = Новый Соответствие;
	
	ДанныеТокена = ТаймлистПовтИсп.ДанныеТокена();
	
	Заголовки.Вставить("User-Agent", "PostmanRuntime/7.36.0");
	Заголовки.Вставить("Accept", "*/*");
	Заголовки.Вставить("Accept-Encoding", "gzip, deflate, br");
	Заголовки.Вставить("Connection", "keep-alive");
	Заголовки.Вставить("authorization", ДанныеТокена.Токен);
	
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	Ответ = Соединение.Получить(Запрос);
	ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
	
	Если Ответ.КодСостояния = 200 Тогда
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТелоОтвета);
		
		МассивДанных = ПрочитатьJSON(ЧтениеJSON, Истина);
		
		ЗначениеСоответствияПротокола = МассивДанных.Получить("protocolAnswer");
		ЗначениеСоответствияСтатусаПротокола = МассивДанных.Получить("status");
		
		Если ЗначениеСоответствияСтатусаПротокола = "SUCCESS" Тогда
			
			ДанныеАвтопротокола = СформироватьАвтопротокол(ЗначениеСоответствияПротокола);
			РегистрыСведений.ТаймлистДанныеРаботыСервиса.ЗаписатьАвтопротокол(ВерсияФайла, ДанныеАвтопротокола);
			РегистрыСведений.ТаймлистФайлыВОбработке.Удалить(ВерсияФайла);
			
		КонецЕсли;
		
	Иначе
		
		ОписаниеОшибки = СтрШаблон(
			НСтр("ru = 'Код ответа сервера - %1. %2'"), Строка(Ответ.КодСостояния), ТелоОтвета);
		РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Добавить(ВерсияФайла, ОписаниеОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет удаление задания и связанных данных в сервисе.
//
// Параметры:
//  ИдентификаторЗадания - Строка - идентификатор задания распознавания в сервисе.
// 
Процедура ВыполнитьУдалениеЗаданияВСервисе(ИдентификаторЗадания)
	
	АдресРесурса = "deleteorder?orderId=" + ИдентификаторЗадания;
	
	ОписаниеОшибкиПроверкиТокена = ТокенПроверен();
	Если ЗначениеЗаполнено(ОписаниеОшибкиПроверкиТокена) Тогда
		ВызватьИсключение ОписаниеОшибкиПроверкиТокена;
	КонецЕсли;
	
	ДанныеТокена = ТаймлистПовтИсп.ДанныеТокена();
	
	Соединение = НовоеHTTPСоединение();
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("User-Agent", "PostmanRuntime/7.36.0");
	Заголовки.Вставить("Accept", "*/*");
	Заголовки.Вставить("Accept-Encoding", "gzip, deflate, br");
	Заголовки.Вставить("Connection", "keep-alive");
	Заголовки.Вставить("authorization", ДанныеТокена.Токен);
	
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	Ответ = Соединение.ВызватьHTTPМетод("DELETE", Запрос);
	ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
	
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Код ответа сервера - %1. %2'"), Строка(Ответ.КодСостояния), ТелоОтвета);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Функция ТокенПроверен()
	
	ОписаниеОшибки = "";
	
	ДанныеТокена = ТаймлистПовтИсп.ДанныеТокена();
	Если ДанныеТокена = Неопределено Или ТекущаяДатаСеанса() > (ДанныеТокена.ВремяПолучения + 86400) Тогда
		
		РезультатОбновленияТокена = ОбновитьТокен();
		ДанныеТокена = ТаймлистПовтИсп.ДанныеТокена();
		Если Не РезультатОбновленияТокена.ТокенПолучен Тогда
			ОписаниеОшибки = РезультатОбновленияТокена.ТекстОшибки;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДанныеТокена = Неопределено Тогда
		ОписаниеОшибки = НСтр("ru = 'Ошибка получения токена.'");
	КонецЕсли;
	
	Возврат ОписаниеОшибки;
	
КонецФункции

Функция ОбновитьТокен()
	
	Результат = Новый Структура("ТокенПолучен, ТекстОшибки", Ложь, "");
	
	ДанныеАвторизации = ДанныеАвторизацииВСервисе();
	
	Если Не ЗначениеЗаполнено(ДанныеАвторизации) Тогда
		Возврат Результат;
	КонецЕсли;
	
	АдресРесурса = "login?phone=" + ДанныеАвторизации.Логин + "&password=" + ДанныеАвторизации.Пароль;
	
	Если ЗначениеЗаполнено(ДанныеАвторизации) Тогда
		
		Соединение = НовоеHTTPСоединение();
		Заголовки = Новый Соответствие;
		
		Заголовки.Вставить("User-Agent", "PostmanRuntime/7.36.0");
		Заголовки.Вставить("Accept", "*/*");
		Заголовки.Вставить("Accept-Encoding", "gzip, deflate, br");
		Заголовки.Вставить("Connection", "keep-alive");
		
		Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
		
		Ответ = Соединение.Получить(Запрос);
			
			Если Ответ.КодСостояния = 200 Тогда
				
				ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
				
				ЧтениеJSON = Новый ЧтениеJSON;
				ЧтениеJSON.УстановитьСтроку(ТелоОтвета);
				
				МассивДанных = ПрочитатьJSON(ЧтениеJSON, Истина);
				
				ДанныеТокена = ОписаниеДанныхТокена();
				
				ДанныеТокена.Токен = МассивДанных.Получить("tocken");
				ДанныеТокена.ВремяПолучения = ТекущаяДатаСеанса();
				ДанныеТокена.Минут = МассивДанных.Получить("minutes");
				ДанныеТокена.ИмяЗарегистрироованногоПользователя = МассивДанных.Получить("i");
				ДанныеТокена.Идентификатор = МассивДанных.Получить("_id");
				ДанныеТокена.ВремяСозданияАккаунта = МассивДанных.Получить("createdAt");
				ДанныеТокена.ВремяИзмененияАккаунта = МассивДанных.Получить("updatedAt");
				ДанныеТокена.АктивностьАккаунта = МассивДанных.Получить("Actived");
				ДанныеТокена.ПользовательСАдминистративнымиПравами = МассивДанных.Получить("Admin");
				ДанныеТокена.ЭлектроннаяПочта = МассивДанных.Получить("email");
				ДанныеТокена.Баланс = МассивДанных.Получить("balance");
				ДанныеТокена.Страна = МассивДанных.Получить("country");
				ДанныеТокена.Телефон = МассивДанных.Получить("phone");
				
				ЗаписатьТокен(ДанныеТокена);
				
				Результат.ТокенПолучен = Истина;
				
				
			Иначе
				
				Результат.ТекстОшибки = СтрШаблон(
					НСтр("ru = 'Код ответа сервера - %1. Ошибка %2'"), Строка(Ответ.КодСостояния), ТелоОтвета);
				
			КонецЕсли;
		
	Иначе
		
		Результат.ТекстОшибки = НСтр("ru = 'Заполните логин и пароль.'");
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ОписаниеДанныхТокена()
	
	НовыйОписаниеДанныхТокена = Новый Структура;
	
	НовыйОписаниеДанныхТокена.Вставить("Токен");
	НовыйОписаниеДанныхТокена.Вставить("ВремяПолучения");
	НовыйОписаниеДанныхТокена.Вставить("Минут");
	НовыйОписаниеДанныхТокена.Вставить("ИмяЗарегистрироованногоПользователя");
	НовыйОписаниеДанныхТокена.Вставить("Идентификатор");
	НовыйОписаниеДанныхТокена.Вставить("ВремяСозданияАккаунта");
	НовыйОписаниеДанныхТокена.Вставить("ВремяИзмененияАккаунта");
	НовыйОписаниеДанныхТокена.Вставить("АктивностьАккаунта");
	НовыйОписаниеДанныхТокена.Вставить("ПользовательСАдминистративнымиПравами");
	НовыйОписаниеДанныхТокена.Вставить("ЭлектроннаяПочта");
	НовыйОписаниеДанныхТокена.Вставить("Баланс");
	НовыйОписаниеДанныхТокена.Вставить("Страна");
	НовыйОписаниеДанныхТокена.Вставить("Телефон");
	
	Возврат НовыйОписаниеДанныхТокена;
	
КонецФункции

Процедура ЗаписатьТокен(ДанныеТокена)
	
	Константы.ТаймлистТокен.Установить(Новый ХранилищеЗначения(ДанныеТокена));
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

Функция ПараметрыЗагрузкиФайлаВСервис(ВерсияФайла)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеЗагрузкиФайла = Новый Структура("ИмяФайла, ИмяФайлаСПутем", "", "");
	
	РеквизитыВерсииСтрока = "ТипХраненияФайла, Расширение, Том, ПутьКФайлу, Владелец.ЯзыкРаспознавания";
	РеквизитыВерсии = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВерсияФайла, РеквизитыВерсииСтрока);
	
	Попытка
		ДанныеЗагрузкиФайла = ВременныйФайлДляРаспознавания(
			ВерсияФайла,
			РеквизитыВерсии.ТипХраненияФайла,
			РеквизитыВерсии.Расширение,
			РеквизитыВерсии.Том,
			РеквизитыВерсии.ПутьКФайлу);
	Исключение
		ОписаниеОшибки =
			НСтр("ru = 'Не удалось получить данные файла для распознавания.
			|Убедитесь, что файл не поврежден и повторите попытку.'");
		РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Добавить(ВерсияФайла, ОписаниеОшибки);
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат ДанныеЗагрузкиФайла;
	
КонецФункции

Функция ВременныйФайлДляРаспознавания(ВерсияФайла, ТипХранения, Расширение, Том, ПутьКФайлу)
	
	ИмяФайла = ИмяВременногоФайла(ВерсияФайла, Расширение);
	
	ИмяФайлаСПутем = КаталогВременныхФайлов() + ИмяФайла;
	
	Если ТипХранения = Перечисления.ТипыХраненияФайлов.ВИнформационнойБазе Тогда
		
		ХранилищеФайла = РаботаСФайламиВызовСервера.ПолучитьХранилищеФайлаИзИнформационнойБазы(ВерсияФайла);
		ДвоичныеДанныеФайла = ХранилищеФайла.Получить();
		
		ДвоичныеДанныеФайла.Записать(ИмяФайлаСПутем);
		
	Иначе
		
		Если Не Том.Пустая() Тогда
			ИмяФайлаСПутемВТоме = ФайловыеФункции.ПолныйПутьТома(Том) + ПутьКФайлу; 
			КопироватьФайл(ИмяФайлаСПутемВТоме, ИмяФайлаСПутем);
			Файл = Новый Файл(ИмяФайлаСПутем);
			Файл.УстановитьТолькоЧтение(Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Новый Структура("ИмяФайла, ИмяФайлаСПутем", ИмяФайла, ИмяФайлаСПутем);
	
КонецФункции

Функция ИмяВременногоФайла(ВерсияФайла, Расширение)
	
	Возврат СтрШаблон("%1.%2", ВерсияФайла.УникальныйИдентификатор(), Расширение);
	
КонецФункции

Функция СформироватьРасшифровку(ОтветСервисаРасшифровка)
	
	Расшифровка = "";
	
	ЧтениеJSONРасшифровка = Новый ЧтениеJSON;
	ЧтениеJSONРасшифровка.УстановитьСтроку(ОтветСервисаРасшифровка);
	МассивДанныхРасшифровки = ПрочитатьJSON(ЧтениеJSONРасшифровка, Истина);
	ЧастьСоСпикером = Истина;
	
	ТаблицаРасшифровки = НоваяТаблицаРасшифровки();
	
	Для Каждого Элемент Из МассивДанныхРасшифровки Цикл
		
		Строка = ТаблицаРасшифровки.Добавить();
		Строка.Время = ВремяПоСекундам(Элемент[0]);
		
		ЧастьСоСпикером = Лев(Элемент[1], 7) = "СПИКЕР_";
		
		Если ЧастьСоСпикером  Тогда
			
			КонецСпикера = СтрНайти(Элемент[1], ": ", НаправлениеПоиска.СНачала, 1, 1);
			
			СпикерЧасть = Лев(Элемент[1], КонецСпикера + 1);
			Строка.ЧастьРасшифровки = СтрЗаменить(Элемент[1], СпикерЧасть, "");
			
			Строка.Спикер = Лев(СпикерЧасть, КонецСпикера - 1);
			Строка.НачалоСпикера = Истина;
			
		Иначе
			
			Строка.ЧастьРасшифровки = Элемент[1];
			Строка.Спикер = СпикерЧасть;
			Строка.НачалоСпикера = Ложь;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Строка Из ТаблицаРасшифровки Цикл
		
		Если Строка.НачалоСпикера И Строка.НомерСтроки = 1 Тогда 
			
			Расшифровка = Расшифровка + Строка.Спикер + " (" + Строка.Время + "): "
				+ Строка.ЧастьРасшифровки;
			
		ИначеЕсли Строка.НачалоСпикера И Строка.НомерСтроки > 1 Тогда
			
			Расшифровка = Расшифровка + Символы.ВК + Символы.ПС + Символы.ВК + Символы.ПС + Строка.Спикер
				+ " (" + Строка.Время + "): " + Строка.ЧастьРасшифровки;
			
		Иначе
			
			Расшифровка = Расшифровка + Строка.ЧастьРасшифровки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	РасшифровкаHTML = ШапкаРасшифровкиHTML();
	
	СодержаниеРасшифровкиHTML = "";
	
	Для Каждого СтрокаРасшифровки Из ТаблицаРасшифровки Цикл
		
		СодержаниеРасшифровкиHTML = СодержаниеРасшифровкиHTML
			+ ?(СтрокаРасшифровки.НачалоСпикера, "</p><p><b><u>" + СтрокаРасшифровки.Спикер
				+ "</u> (" + СтрокаРасшифровки.Время + "): </b>" + СтрокаРасшифровки.ЧастьРасшифровки,
		СтрокаРасшифровки.ЧастьРасшифровки);
		
	КонецЦикла;
	
	РасшифровкаHTML = СтрЗаменить(РасшифровкаHTML, "TimelistRecognizeResult", СодержаниеРасшифровкиHTML);
	
	Возврат Новый ХранилищеЗначения(РасшифровкаHTML);
	
КонецФункции

Функция СформироватьАвтопротокол(ОтветСервисаАвтопротокол)
	
	Автопротокол = "";
	
	ЧтениеJSONАвтопротокол = Новый ЧтениеJSON;
	ЧтениеJSONАвтопротокол.УстановитьСтроку(ОтветСервисаАвтопротокол);
	МассивДанныхАвтопротокола = ПрочитатьJSON(ЧтениеJSONАвтопротокол, Истина);
	МассивЧастейАвтопротокола = Новый Массив;
	МассивПунктовАвтопротокола = Новый Массив;
	
	Для Каждого ЭлементДанныхАвтопротокола Из МассивДанныхАвтопротокола Цикл
		
		ЭтоНачалоПунктовПротокола = Ложь;
		
		Для Каждого Элемент Из ЭлементДанныхАвтопротокола Цикл
			
			МассивЧастейАвтопротокола.Добавить(Элемент);
			
			Если ЭтоНачалоПунктовПротокола Тогда
				РезультатПоиска = СтрНайтиПоРегулярномуВыражению(Элемент, "((\d+\.)|(-)){1}(\s)*");
				Если РезультатПоиска.НачальнаяПозиция = 1 Тогда
					СодержаниеПункта = Сред(Элемент, РезультатПоиска.Длина + 1);
					МассивПунктовАвтопротокола.Добавить(СодержаниеПункта);
				КонецЕсли
			КонецЕсли;
			
			Если Элемент = "***Следующие шаги ***" Тогда
				ЭтоНачалоПунктовПротокола = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого Элемент Из МассивЧастейАвтопротокола Цикл
			Автопротокол = Автопротокол + "→ " + Элемент + Символы.ПС;
		КонецЦикла;
		
	КонецЦикла;
	
	АвтоПротоколHTML = ШапкаАвтопротоколаHTML();
	Вставка = "";
	
	Для Каждого СтрокаАвтопротокола Из МассивЧастейАвтопротокола Цикл
		Если СтрокаАвтопротокола = "***Следующие шаги ***" Тогда
			Вставка = Вставка + "<p><b>" + СтрокаАвтопротокола
				+ НСтр("ru = '(данные этого раздела используются для заполнения протокола мероприятия и пунктов исполнения)'")
				+ "</b></p>";
		Иначе
			Вставка = Вставка + "<p>" + СтрокаАвтопротокола + "</p>";
		КонецЕсли;
	КонецЦикла;
	
	АвтоПротоколHTML = СтрЗаменить(АвтоПротоколHTML, "TimelistAutoprotocol", Вставка);
	АвтопротоколХранилище = Новый ХранилищеЗначения(АвтоПротоколHTML);
	
	СледующиеШаги = СтрСоединить(МассивПунктовАвтопротокола, ";");
	
	ДанныеАвтопротокола = Новый Структура("Автопротокол, СледующиеШаги", АвтопротоколХранилище, СледующиеШаги);
	
	Возврат ДанныеАвтопротокола;
	
КонецФункции

Функция НоваяТаблицаРасшифровки()
	
	ТаблицаРасшифровки = Новый ТаблицаЗначений();
	ТаблицаРасшифровки.Колонки.Добавить("НомерСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(2,,
		ДопустимыйЗнак.Неотрицательный));
	ТаблицаРасшифровки.Колонки.Добавить("Время", ОбщегоНазначения.ОписаниеТипаСтрока(8));
	ТаблицаРасшифровки.Колонки.Добавить("Спикер", ОбщегоНазначения.ОписаниеТипаСтрока(9));
	ТаблицаРасшифровки.Колонки.Добавить("ЧастьРасшифровки", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаРасшифровки.Колонки.Добавить("НачалоСпикера", Новый ОписаниеТипов("Булево"));
	
	Возврат ТаблицаРасшифровки;
	
КонецФункции

Функция ВремяПоСекундам(КоличествоСекунд)
	
	Если КоличествоСекунд = 0  Тогда
		Возврат "00:00:00";
	Иначе
		Дата = Дата(1,1,1) + КоличествоСекунд;
		Возврат Формат(Дата, "ДФ=HH:mm:ss");
	КонецЕсли;
	
КонецФункции

Функция ШапкаРасшифровкиHTML()
	
	ДокументHTML = Новый ДокументHTML;
	
	ЭлементТело = ДокументHTML.СоздатьЭлемент("body");
	ДокументHTML.Тело = ЭлементТело;
	
	ЭлементТекст = ДокументHTML.СоздатьТекстовыйУзел("TimelistRecognizeResult");
	ЭлементТело.ДобавитьДочерний(ЭлементТекст);
	
	ЗаписьDOM = Новый ЗаписьDOM;
	ЗаписьHTML = Новый ЗаписьHTML;
	ЗаписьHTML.УстановитьСтроку();
	ЗаписьDOM.Записать(ДокументHTML, ЗаписьHTML);
	РасшифровкаHTML = ЗаписьHTML.Закрыть();
	
	Возврат РасшифровкаHTML;
	
КонецФункции

Функция ШапкаАвтопротоколаHTML()
	
	ДокументHTML = Новый ДокументHTML;
	
	ЭлементТело = ДокументHTML.СоздатьЭлемент("body");
	ДокументHTML.Тело = ЭлементТело;
	
	ЭлементТекст = ДокументHTML.СоздатьТекстовыйУзел("TimelistAutoprotocol");
	ЭлементТело.ДобавитьДочерний(ЭлементТекст);
	
	ЗаписьDOM = Новый ЗаписьDOM;
	ЗаписьHTML = Новый ЗаписьHTML;
	ЗаписьHTML.УстановитьСтроку();
	ЗаписьDOM.Записать(ДокументHTML, ЗаписьHTML);
	АвтопротоколHTML = ЗаписьHTML.Закрыть();
	
	Возврат АвтопротоколHTML;
	
КонецФункции

Функция СформироватьХранилищеСпикеров(ОтветСервисаСпикеры)
	
	ЧтениеJSONСпикеры = Новый ЧтениеJSON;
	ЧтениеJSONСпикеры.УстановитьСтроку(ОтветСервисаСпикеры);
	
	МассивДанныхСпикеров = ПрочитатьJSON(ЧтениеJSONСпикеры, Истина);
	
	ТаблицаСпикеров = НоваяТаблицаСпикеров();
	
	Для Каждого Элемент Из МассивДанныхСпикеров Цикл
		
		Строка = ТаблицаСпикеров.Добавить();
		Строка.initialName = Элемент.Получить("initialName");
		Строка.name = Элемент.Получить("name");
		Строка.role = Элемент.Получить("role");
		
	КонецЦикла;
	
	Возврат Новый ХранилищеЗначения(ТаблицаСпикеров);
	
КонецФункции

Функция НоваяТаблицаСпикеров()
	
	ТаблицаСпикеров = Новый ТаблицаЗначений;
	ТаблицаСпикеров.Колонки.Добавить("initialName", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаСпикеров.Колонки.Добавить("name", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаСпикеров.Колонки.Добавить("role", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	
	Возврат ТаблицаСпикеров;
	
КонецФункции

Функция УстановитьГлавнуюРольСпикеру(ИмяСпикера, ВерсияФайла, ИдентификаторФайлаВСервисе)
	
	ОписаниеОшибки = "";
	
	АдресРесурса = "changeSpeakerRole";
	
	Разделитель = СтрЗаменить(Новый УникальныйИдентификатор(), "-", "");
	Тело = Новый ПотокВПамяти;
	ЗаписьДанных = Новый ЗаписьДанных(Тело,,,Символы.ВК + Символы.ПС,"");
	
	ЗаписьДанных.ЗаписатьСтроку("----------------------------" + Разделитель);
	ЗаписьДанных.ЗаписатьСтроку("Content-Disposition: form-data; name=""orderId"""
		+ Символы.ВК + Символы.ПС + Символы.ВК + Символы.ПС + ИдентификаторФайлаВСервисе);
	ЗаписьДанных.ЗаписатьСтроку("----------------------------" + Разделитель);
	ЗаписьДанных.ЗаписатьСтроку("Content-Disposition: form-data; name=""initialName"""
		+ Символы.ВК + Символы.ПС + Символы.ВК + Символы.ПС + ИмяСпикера);
	ЗаписьДанных.ЗаписатьСтроку("----------------------------" + Разделитель);
	ЗаписьДанных.ЗаписатьСтроку("Content-Disposition: form-data; name=""role"""
		+ Символы.ВК + Символы.ПС + Символы.ВК + Символы.ПС + "LEAD");
	ЗаписьДанных.ЗаписатьСтроку("----------------------------" + Разделитель + "--");
	ЗаписьДанных.Закрыть();
	
	ДанныеТела = Тело.ЗакрытьИПолучитьДвоичныеДанные();
	
	Соединение = НовоеHTTPСоединение();
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("User-Agent", "PostmanRuntime/7.36.0");
	Заголовки.Вставить("Accept", "*/*");
	Заголовки.Вставить("Accept-Encoding", "gzip, deflate, br");
	Заголовки.Вставить("Connection", "keep-alive");
	Заголовки.Вставить("Content-Type", "multipart/form-data; boundary=--------------------------" + Разделитель);
	
	ДанныеТокена = ТаймлистПовтИсп.ДанныеТокена();
	Заголовки.Вставить("authorization", ДанныеТокена.Токен);
	
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	Запрос.УстановитьТелоИзДвоичныхДанных(ДанныеТела);
	Ответ = Соединение.ВызватьHTTPМетод("PUT", Запрос);
	
	Если Ответ.КодСостояния = 200 Тогда
		
		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТелоОтвета);
		МассивДанных = ПрочитатьJSON(ЧтениеJSON, Истина);
		ЗначениеСоответствияОтветаСпикеры = МассивДанных.Получить("speakersData");
		СпикерыХранилище = СформироватьХранилищеСпикеров(ЗначениеСоответствияОтветаСпикеры);
		
		РегистрыСведений.ТаймлистДанныеРаботыСервиса.ЗаписатьСпикеров(ВерсияФайла, СпикерыХранилище);
		
	Иначе
		
		ОписаниеОшибки = СтрШаблон(
			НСтр("ru = 'Код ответа сервера - %1. %2'"), Строка(Ответ.КодСостояния), ТелоОтвета);
		
	КонецЕсли;
	
	Возврат ОписаниеОшибки;
	
КонецФункции

Функция УстановитьНовоеИмяСпикера(ТекущееИмя, НовоеИмя, ВерсияФайла, ИдентификаторФайлаВСервисе)
	
	ОписаниеОшибки = "";
	
	АдресРесурса = "changeSpeakerName";
	
	Разделитель = СтрЗаменить(Новый УникальныйИдентификатор(), "-", "");
	
	Тело = Новый ПотокВПамяти;
	
	ЗаписьДанных = Новый ЗаписьДанных(Тело,,,Символы.ВК + Символы.ПС,"");
	
	ЗаписьДанных.ЗаписатьСтроку("----------------------------" + Разделитель);
	ЗаписьДанных.ЗаписатьСтроку("Content-Disposition: form-data; name=""orderId"""
		+ Символы.ВК + Символы.ПС + Символы.ВК + Символы.ПС + ИдентификаторФайлаВСервисе);
	ЗаписьДанных.ЗаписатьСтроку("----------------------------" + Разделитель);
	ЗаписьДанных.ЗаписатьСтроку("Content-Disposition: form-data; name=""initialName"""
		+ Символы.ВК + Символы.ПС + Символы.ВК + Символы.ПС + ТекущееИмя);
	ЗаписьДанных.ЗаписатьСтроку("----------------------------" + Разделитель);
	ЗаписьДанных.ЗаписатьСтроку("Content-Disposition: form-data; name=""newName"""
		+ Символы.ВК + Символы.ПС + Символы.ВК + Символы.ПС + НовоеИмя);
	ЗаписьДанных.ЗаписатьСтроку("----------------------------" + Разделитель + "--");
	ЗаписьДанных.Закрыть();
	
	ДанныеТела = Тело.ЗакрытьИПолучитьДвоичныеДанные();
	
	Соединение = НовоеHTTPСоединение();
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("User-Agent", "PostmanRuntime/7.36.0");
	Заголовки.Вставить("Accept", "*/*");
	Заголовки.Вставить("Accept-Encoding", "gzip, deflate, br");
	Заголовки.Вставить("Connection", "keep-alive");
	Заголовки.Вставить("Content-Type", "multipart/form-data; boundary=--------------------------" + Разделитель);
	
	ДанныеТокена = ТаймлистПовтИсп.ДанныеТокена();
	
	Заголовки.Вставить("authorization", ДанныеТокена.Токен);

	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	Запрос.УстановитьТелоИзДвоичныхДанных(ДанныеТела);
	Ответ = Соединение.ВызватьHTTPМетод("PUT", Запрос);
	
	Если Ответ.КодСостояния = 200 Тогда 
		
		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТелоОтвета);
		МассивДанных = ПрочитатьJSON(ЧтениеJSON, Истина);
		ЗначениеСоответствияОтветаСпикеры = МассивДанных.Получить("speakersData");
		СпикерыХранилище = СформироватьХранилищеСпикеров(ЗначениеСоответствияОтветаСпикеры);
		
		РегистрыСведений.ТаймлистДанныеРаботыСервиса.ЗаписатьСпикеров(ВерсияФайла, СпикерыХранилище);
		
	Иначе
		
		ОписаниеОшибки = СтрШаблон(
			НСтр("ru = 'Код ответа сервера - %1. %2'"), Строка(Ответ.КодСостояния), ТелоОтвета);
		
	КонецЕсли;
	
	Возврат ОписаниеОшибки;
	
КонецФункции

Процедура ЗаменитьСпикеровВРасшифровке(ВерсияФайла, РасшифровкаХранилище)
	
	НовыеСпикерыХранилище = РегистрыСведений.ТаймлистДанныеРаботыСервиса.Данные(ВерсияФайла, "Спикеры").Спикеры;
	
	СпикерыТаблица = НовыеСпикерыХранилище.Получить();
	РасшифровкаHTML = РасшифровкаХранилище.Получить();
	
	Для Каждого Строка Из СпикерыТаблица Цикл
		РасшифровкаHTML = СтрЗаменить(РасшифровкаHTML, Строка.initialName, Строка.name);
	КонецЦикла;
	
	РасшифровкаХранилище = Новый ХранилищеЗначения(РасшифровкаHTML);
	
	РегистрыСведений.ТаймлистДанныеРаботыСервиса.ЗаписатьРасшифровку(ВерсияФайла, РасшифровкаХранилище,
		Перечисления.ТаймлистСтатусы.АвтопротоколГотовКПолучению);
	ПоместитьФайлВОчередьОбработки(ВерсияФайла, Перечисления.ТаймлистТипыЗаданий.ПолучитьАвтопротокол);
	
КонецПроцедуры

Функция НовоеHTTPСоединение(Таймаут = 0)
	
	АдресСервиса = Константы.ТаймлистАдресСервиса.Получить();
	Порт = Константы.ТаймлистПорт.Получить();
	ЭтоЗащищенноеСоединение = Константы.ТаймлистЗащищенноеСоединение.Получить();
	
	Если ЭтоЗащищенноеСоединение Тогда
		Возврат Новый HTTPСоединение(АдресСервиса, Порт,,,, Таймаут, Новый ЗащищенноеСоединениеOpenSSL);
	КонецЕсли;
	
	Возврат Новый HTTPСоединение(АдресСервиса, Порт,,,, Таймаут);
	
КонецФункции

#КонецОбласти
