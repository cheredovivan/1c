
#Область ПрограммныйИнтерфейс

// Меняет пометку удаления группы на противоположную.
// 
// Параметры:
//  Группа - СправочникСсылка.Контрагенты
// 
// Возвращаемое значение:
//  Булево - Истина, если установлена пометка удаления.
//
Функция ИзменитьПометкуУдаленияГруппы(Знач Группа) Экспорт
	
	ГруппаОбъект = Группа.ПолучитьОбъект();
	ГруппаОбъект.УстановитьПометкуУдаления(Не ГруппаОбъект.ПометкаУдаления, Истина);
	
	Возврат ГруппаОбъект.ПометкаУдаления;
	
КонецФункции

// Устанавливает новую группы контрагентов
// 
// Параметры:
//  МассивКонтрагентов - Массив Из СправочникСсылка.Контрагенты
//  НоваяГруппа - СправочникСсылка.Контрагенты
// 
// Возвращаемое значение:
//  Булево - Истина, если первый контрагент является группой.
//
Функция УстановитьНовуюГруппуКонтрагентов(Знач МассивКонтрагентов, Знач НоваяГруппа) Экспорт
	
	ПервыйКонтрагентГруппа = Ложь;
	СчетчикГрупп = 1;
	
	Для Каждого Контрагент Из МассивКонтрагентов Цикл
		
		КонтрагентОбъект = Контрагент.ПолучитьОбъект();
		КонтрагентОбъект.Родитель = НоваяГруппа;
		КонтрагентОбъект.Записать();
		
		Если СчетчикГрупп = 1 И КонтрагентОбъект.ЭтоГруппа Тогда
			ПервыйКонтрагентГруппа = Истина;
		КонецЕсли;
		
		СчетчикГрупп = СчетчикГрупп + 1;
		
	КонецЦикла;
	
	Возврат ПервыйКонтрагентГруппа;
	
КонецФункции

// Формирует дерево групп по данным информационной базы.
// 
// Параметры:
// ДанныеДерева - ДанныеФормыДерево
//  ПоказыватьУдаленные - Булево
// 
// Возвращаемое значение:
//  ДанныеФормыДерево - сформированное дерево групп.
// 
Функция СформироватьДеревоГрупп(Знач ДанныеДерева, Знач ПоказыватьУдаленные) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = РаботаСоСпискамиКонтрагентов.ТекстЗапросаФормированияДереваГрупп(ПоказыватьУдаленные);
	
	Дерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ЗначениеВДанныеФормы(Дерево, ДанныеДерева);
	
	Возврат ДанныеДерева;
	
КонецФункции

#КонецОбласти
