////////////////////////////////////////////////////////////////////////////////////////////////////
// Обработка постановки предмета на контроль
//  
////////////////////////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс

// Выполняет обработку при нажатии на кнопку Контроль
//
// Параметры
// 	  Предмет - ссылка на предмет справочника Контроль -  
//	      ссылка на предмет, из которого вызывается команда
//	  Остальные параметры - соответствуют параметрам процедуры ОткрытьФорму
//
Процедура ОбработкаКомандыКонтроль(Предмет,
	Владелец,
	Уникальность = Неопределено,
	Окно = Неопределено,
	НавигационнаяСсылка = Неопределено) Экспорт
	
	КлючеваяОперация = КонтрольКлиентСервер.КлючеваяОперацияВыполнениеКомандыКонтроль();
	ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
	
	ИмяФормы = "";
	ПараметрыФормы = "";
	
	Контроль.ОбработкаКомандыКонтроль(Предмет, ИмяФормы, ПараметрыФормы);
	
	Если ИмяФормы = "Справочник.Контроль.Форма.ФормаНового" И ПараметрыФормы.Свойство("Основание") Тогда 
		ОткрытьФорму(ИмяФормы, 
			ПараметрыФормы,
			Владелец, 
			ПараметрыФормы.Основание,
			Окно, 
			НавигационнаяСсылка);
	Иначе	
		ОткрытьФорму(ИмяФормы, 
			ПараметрыФормы,
			Владелец, 
			Уникальность, 
			Окно, 
			НавигационнаяСсылка);
	КонецЕсли;		
	
КонецПроцедуры	

// Выполняет оповещения при записи карточки контроля.
// 
// Параметры:
//  ПараметрыОповещения - см. КонтрольКлиентСервер.ПараметрыОповещения
// 
Процедура ОповеститьОЗаписиКонтроля(ПараметрыОповещения) Экспорт 
	
	Оповестить("ЗаписьКонтроля", ПараметрыОповещения);
	
	Если ТипЗнч(ПараметрыОповещения.Предмет) = Тип("БизнесПроцессСсылка.Исполнение")
		Или ТипЗнч(ПараметрыОповещения.Предмет) = Тип("БизнесПроцессСсылка.КомплексныйПроцесс")
		Или ТипЗнч(ПараметрыОповещения.Предмет) = Тип("БизнесПроцессСсылка.Ознакомление")
		Или ТипЗнч(ПараметрыОповещения.Предмет) = Тип("БизнесПроцессСсылка.Приглашение")
		Или ТипЗнч(ПараметрыОповещения.Предмет) = Тип("БизнесПроцессСсылка.Рассмотрение")
		Или ТипЗнч(ПараметрыОповещения.Предмет) = Тип("БизнесПроцессСсылка.Регистрация")
		Или ТипЗнч(ПараметрыОповещения.Предмет) = Тип("БизнесПроцессСсылка.РешениеВопросовВыполненияЗадач")
		Или ТипЗнч(ПараметрыОповещения.Предмет) = Тип("БизнесПроцессСсылка.Согласование")
		Или ТипЗнч(ПараметрыОповещения.Предмет) = Тип("БизнесПроцессСсылка.Подписание")
		Или ТипЗнч(ПараметрыОповещения.Предмет) = Тип("БизнесПроцессСсылка.Утверждение") Тогда 
		
		ОсновныеПредметы = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(ПараметрыОповещения.Предмет, , Истина);
		Для Каждого Предмет Из ОсновныеПредметы Цикл
			Параметр = Новый Структура;
			Параметр.Вставить("Ссылка", ПараметрыОповещения.Ссылка);
			Параметр.Вставить("Предмет", Предмет);
			Параметр.Вставить("Контролер", ПараметрыОповещения.Контролер);
	
			Оповестить("ЗаписьКонтроля", Параметр);
		КонецЦикла;
	КонецЕсли;	
		
КонецПроцедуры	

// Снимает с контроля.
// 
// Параметры:
//  КонтрольСсылка - СправочникСсылка.Контроль
// 
Процедура СнятьСКонтроля(КонтрольСсылка) Экспорт
	
	ПараметрыОповещения = КонтрольВызовСервера.СнятьСКонтроля(КонтрольСсылка);
	ОповеститьОЗаписиКонтроля(ПараметрыОповещения);
	
КонецПроцедуры

// Переносит срок.
// 
// Параметры:
//  КонтрольСсылка - СправочникСсылка.Контроль
//  НовыйСрок - Дата
// 
Процедура ПеренестиСрок(КонтрольСсылка, НовыйСрок) Экспорт
	
	ПараметрыОповещения = КонтрольВызовСервера.ПеренестиСрок(КонтрольСсылка, НовыйСрок);
	ОповеститьОЗаписиКонтроля(ПараметрыОповещения);
	
КонецПроцедуры

#КонецОбласти
