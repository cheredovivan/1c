// @strict-types

#Область ПрограммныйИнтерфейс

// Вызывается из ПриНачалеРаботыСистемы общего модуля ОбщегоНазначенияКлиентПереопределяемый
// 
// Параметры:
//  Параметры - Структура:
//   * Отказ         - Булево - возвращаемое значение. Если установить Истина, то работа программы будет прекращена.
//   * Перезапустить - Булево - возвращаемое значение. Если установить Истина и параметр Отказ тоже установлен
//                              в Истина, то выполняется перезапуск программы.
//
Процедура ПриНачалеРаботыСистемы(Параметры) Экспорт

// Локализация
	// ИнтернетПоддержкаПользователей
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		ИнтернетПоддержкаПользователейКлиент.ПриНачалеРаботыСистемы(Параметры);
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей
// Локализация Конец	
	
КонецПроцедуры

// См. ОбщегоНазначенияКлиентПереопределяемый.ПослеНачалаРаботыСистемы
//
Процедура ПослеНачалаРаботыСистемы() Экспорт

// Локализация
	// Нужно ли обновить классификатор обращений граждан:
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	ОбновитьКлассификаторОбращенийГраждан = Ложь;
	Если ПараметрыРаботыКлиента.Свойство("ОбновитьКлассификаторОбращенийГраждан") Тогда
		//@skip-check property-return-type
		Если ПараметрыРаботыКлиента.ОбновитьКлассификаторОбращенийГраждан Тогда
			ОбновитьКлассификаторОбращенийГраждан = Истина;
		ИначеЕсли ПараметрыРаботыКлиента.Свойство("ОбновлениеВерсииПрограммы") 
			И ПараметрыРаботыКлиента.ОбновлениеВерсииПрограммы = Истина Тогда 
			ОбновитьКлассификаторОбращенийГраждан
				= РаботаСОбращениямиВызовСервера.АктуализироватьКлассификаторОбращенийГраждан();
		КонецЕсли;
		Если ОбновитьКлассификаторОбращенийГраждан Тогда
			ОткрытьФорму("Обработка.ОбновлениеКлассификатораОбращенийГраждан.Форма");
		КонецЕсли;
	КонецЕсли;
// Локализация Конец
	
КонецПроцедуры

#КонецОбласти