///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Вернет константу ПоказыватьPdfСредствами1С
Функция ПоказыватьPdfСредствами1С() Экспорт

	УстановитьПривилегированныйРежим(Истина);

	Возврат Константы.ПоказыватьPdfСредствами1С.Получить();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Общие и персональные настройки работы с файлами

// Возвращает структуру, содержащую ОбщиеНастройки и ПерсональныеНастройки.
Функция НастройкиРаботыСФайлами() Экспорт
	
	ОбщиеНастройки        = Новый Структура;
	ПерсональныеНастройки = Новый Структура;
	
	ФайловыеФункцииСлужебный.ПриДобавленииНастроекРаботыСФайлами(
		ОбщиеНастройки, ПерсональныеНастройки);
	
	ДобавитьНастройкиРаботыСФайлами(ОбщиеНастройки, ПерсональныеНастройки);
	
	Настройки = Новый Структура;
	Настройки.Вставить("ОбщиеНастройки",        ОбщиеНастройки);
	Настройки.Вставить("ПерсональныеНастройки", ПерсональныеНастройки);
	
	Возврат Настройки;
	
КонецФункции

// Устанавливает общие и персональные настройки файловых функций.
Процедура ДобавитьНастройкиРаботыСФайлами(ОбщиеНастройки, ПерсональныеНастройки)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Заполнение общих настроек.
	
	// ИзвлекатьТекстыФайловНаСервере.
	ОбщиеНастройки.Вставить(
		"ИзвлекатьТекстыФайловНаСервере", ФайловыеФункцииСлужебный.ИзвлекатьТекстыФайловНаСервере());
	
	// МаксимальныйРазмерФайла.
	ОбщиеНастройки.Вставить("МаксимальныйРазмерФайла", ФайловыеФункции.МаксимальныйРазмерФайла());
	
	// ЗапрещатьЗагрузкуФайловПоРасширению.
	ЗапрещатьЗагрузкуФайловПоРасширению = Константы.ЗапрещатьЗагрузкуФайловПоРасширению.Получить();
	Если ЗапрещатьЗагрузкуФайловПоРасширению = Неопределено Тогда
		ЗапрещатьЗагрузкуФайловПоРасширению = Ложь;
		Константы.ЗапрещатьЗагрузкуФайловПоРасширению.Установить(ЗапрещатьЗагрузкуФайловПоРасширению);
	КонецЕсли;
	ОбщиеНастройки.Вставить("ЗапретЗагрузкиФайловПоРасширению", ЗапрещатьЗагрузкуФайловПоРасширению);
	
	// СписокЗапрещенныхРасширений.
	ОбщиеНастройки.Вставить("СписокЗапрещенныхРасширений", СписокЗапрещенныхРасширений());
	
	// РасширенияФайловДляПроверкиБлокировкиФайлаНаДиске.
	ОбщиеНастройки.Вставить("РасширенияФайловДляПроверкиБлокировкиФайлаНаДиске", 
		РасширенияФайловДляПроверкиБлокировкиФайлаНаДиске());
	
	// СписокРасширенийФайловOpenDocument.
	ОбщиеНастройки.Вставить("СписокРасширенийФайловOpenDocument", СписокРасширенийФайловOpenDocument());
	
	//СписокРасширенийСканКопийОригиналов
	ОбщиеНастройки.Вставить("СписокРасширенийСканКопийОригиналов", СписокРасширенийСканКопийОригиналов());
	
	// СписокРасширенийТекстовыхФайлов.
	ОбщиеНастройки.Вставить("СписокРасширенийТекстовыхФайлов", СписокРасширенийТекстовыхФайлов());
	
	// Заполнение персональных настроек.
	
	// МаксимальныйРазмерЛокальногоКэшаФайлов.
	МаксимальныйРазмерЛокальногоКэшаФайлов = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ЛокальныйКэшФайлов", "МаксимальныйРазмерЛокальногоКэшаФайлов");
	
	Если МаксимальныйРазмерЛокальногоКэшаФайлов = Неопределено Тогда
		МаксимальныйРазмерЛокальногоКэшаФайлов = 100*1024*1024; // 100 МБ.
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"ЛокальныйКэшФайлов",
			"МаксимальныйРазмерЛокальногоКэшаФайлов",
			МаксимальныйРазмерЛокальногоКэшаФайлов);
	КонецЕсли;
	
	ПерсональныеНастройки.Вставить(
		"МаксимальныйРазмерЛокальногоКэшаФайлов",
		МаксимальныйРазмерЛокальногоКэшаФайлов);
	
	// ПутьКЛокальномуКэшуФайлов.
	ПутьКЛокальномуКэшуФайлов = ФайловыеФункции.ПолучитьПутьКРабочемуКаталогуПользователя();
	// Не следует получать эту переменную непосредственно.
	// Нужно использовать функцию РабочийКаталогПользователя
	// модуля ФайловыеФункцииСлужебныйКлиент.
	ПерсональныеНастройки.Вставить("ПутьКЛокальномуКэшуФайлов", ПутьКЛокальномуКэшуФайлов);
	
	// УдалятьФайлИзЛокальногоКэшаФайловПриЗавершенииРедактирования.
	УдалятьФайлИзЛокальногоКэшаФайловПриЗавершенииРедактирования =
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ЛокальныйКэшФайлов", "УдалятьФайлИзЛокальногоКэшаФайловПриЗавершенииРедактирования");
	
	Если УдалятьФайлИзЛокальногоКэшаФайловПриЗавершенииРедактирования = Неопределено Тогда
		УдалятьФайлИзЛокальногоКэшаФайловПриЗавершенииРедактирования = Ложь;
	КонецЕсли;
	
	ПерсональныеНастройки.Вставить(
		"УдалятьФайлИзЛокальногоКэшаФайловПриЗавершенииРедактирования",
		УдалятьФайлИзЛокальногоКэшаФайловПриЗавершенииРедактирования);
	
	// ПоказыватьПодсказкиПриРедактированииФайлов.
	ПоказыватьПодсказкиПриРедактированииФайлов = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиПрограммы", "ПоказыватьПодсказкиПриРедактированииФайлов");
	
	Если ПоказыватьПодсказкиПриРедактированииФайлов = Неопределено Тогда
		ПоказыватьПодсказкиПриРедактированииФайлов = Истина;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"НастройкиПрограммы",
			"ПоказыватьПодсказкиПриРедактированииФайлов",
			ПоказыватьПодсказкиПриРедактированииФайлов);
	КонецЕсли;
	ПерсональныеНастройки.Вставить(
		"ПоказыватьПодсказкиПриРедактированииФайлов",
		ПоказыватьПодсказкиПриРедактированииФайлов);
	
	// ПоказыватьИнформациюЧтоФайлНеБылИзменен.
	ПоказыватьИнформациюЧтоФайлНеБылИзменен = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиПрограммы", "ПоказыватьИнформациюЧтоФайлНеБылИзменен");
	
	Если ПоказыватьИнформациюЧтоФайлНеБылИзменен = Неопределено Тогда
		ПоказыватьИнформациюЧтоФайлНеБылИзменен = Истина;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"НастройкиПрограммы",
			"ПоказыватьИнформациюЧтоФайлНеБылИзменен",
			ПоказыватьИнформациюЧтоФайлНеБылИзменен);
	КонецЕсли;
	ПерсональныеНастройки.Вставить(
		"ПоказыватьИнформациюЧтоФайлНеБылИзменен",
		ПоказыватьИнформациюЧтоФайлНеБылИзменен);
	// Локализация
	// ПоказыватьПредупреждениеОбОтветственностиПриОтправке.
	ПоказыватьПредупреждениеОбОтветственностиПриОтправке = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиПрограммы", "ПоказыватьПредупреждениеОбОтветственностиПриОтправке");
	
	Если ПоказыватьПредупреждениеОбОтветственностиПриОтправке = Неопределено Тогда
		ПоказыватьПредупреждениеОбОтветственностиПриОтправке = Истина;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"НастройкиПрограммы",
			"ПоказыватьПредупреждениеОбОтветственностиПриОтправке",
			ПоказыватьПредупреждениеОбОтветственностиПриОтправке);
	КонецЕсли;
	ПерсональныеНастройки.Вставить(
		"ПоказыватьПредупреждениеОбОтветственностиПриОтправке",
		ПоказыватьПредупреждениеОбОтветственностиПриОтправке);
		
	// ПоказыватьСообщениеОбОтправкеФайлаВСервисРаспознавания.
	ПоказыватьСообщениеОбОтправкеФайлаВСервисРаспознавания = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиПрограммы", "ПоказыватьСообщениеОбОтправкеФайлаВСервисРаспознавания");
	
	Если ПоказыватьСообщениеОбОтправкеФайлаВСервисРаспознавания = Неопределено Тогда
		ПоказыватьСообщениеОбОтправкеФайлаВСервисРаспознавания = Истина;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"НастройкиПрограммы",
			"ПоказыватьСообщениеОбОтправкеФайлаВСервисРаспознавания",
			ПоказыватьСообщениеОбОтправкеФайлаВСервисРаспознавания);
	КонецЕсли;
	ПерсональныеНастройки.Вставить(
		"ПоказыватьСообщениеОбОтправкеФайлаВСервисРаспознавания",
		ПоказыватьСообщениеОбОтправкеФайлаВСервисРаспознавания);
	// Конец Локализация
	// Настройки открытия файлов.
	ТекстовыеФайлыРасширение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиОткрытияФайлов\ТекстовыеФайлы",
		"Расширение", "TXT XML INI");
	
	ТекстовыеФайлыСпособОткрытия = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиОткрытияФайлов\ТекстовыеФайлы", 
		"СпособОткрытия",
		Перечисления.СпособыОткрытияФайлаНаПросмотр.ВоВстроенномРедакторе);
		
	HtmlФайлыРасширение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиОткрытияФайлов\HtmlФайлы",
		"Расширение", "HTM HTML");
	
	HtmlФайлыСпособОткрытия = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиОткрытияФайлов\HtmlФайлы", 
		"СпособОткрытия",
		Перечисления.СпособыОткрытияФайлаНаПросмотр.ВоВстроенномРедакторе);
	
	ИзображенияРасширение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиОткрытияФайлов\Изображения",
		"Расширение", "BMP GIF JPG JPEG PNG");
	
	ИзображенияСпособОткрытия = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиОткрытияФайлов\Изображения", 
		"СпособОткрытия",
		Перечисления.СпособыОткрытияФайлаНаПросмотр.ВоВстроенномРедакторе);
	
	ГрафическиеСхемыРасширение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиОткрытияФайлов\ГрафическиеСхемы", "Расширение", "GRS");
	
	ГрафическиеСхемыСпособОткрытия = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиОткрытияФайлов\ГрафическиеСхемы",
		"СпособОткрытия",
		Перечисления.СпособыОткрытияФайлаНаПросмотр.ВоВстроенномРедакторе);
	
	ПерсональныеНастройки.Вставить("ТекстовыеФайлыРасширение",       ТекстовыеФайлыРасширение);
	ПерсональныеНастройки.Вставить("ТекстовыеФайлыСпособОткрытия",   ТекстовыеФайлыСпособОткрытия);
	ПерсональныеНастройки.Вставить("ГрафическиеСхемыРасширение",     ГрафическиеСхемыРасширение);
	ПерсональныеНастройки.Вставить("ГрафическиеСхемыСпособОткрытия", ГрафическиеСхемыСпособОткрытия);
	
	ПерсональныеНастройки.Вставить("HtmlФайлыРасширение",       HtmlФайлыРасширение);
	ПерсональныеНастройки.Вставить("HtmlФайлыСпособОткрытия",   HtmlФайлыСпособОткрытия);
	
	ПерсональныеНастройки.Вставить("ИзображенияРасширение",     ИзображенияРасширение);
	ПерсональныеНастройки.Вставить("ИзображенияСпособОткрытия", ИзображенияСпособОткрытия);  
	
	ПерсональныеНастройки.Вставить("ПредельныйРазмерДляПредпросмотра", ФайловыеФункцииПовтИсп.ПредельныйРазмерДляПредпросмотра());
	
КонецПроцедуры

Функция СписокРасширенийСканКопийОригиналов()
	
	УстановитьПривилегированныйРежим(Истина);
	СписокРасширенийСканКопийОригиналов = Константы.СписокРасширенийСканКопийОригиналов.Получить();
	
	Возврат СписокРасширенийСканКопийОригиналов;
	
КонецФункции

Функция СписокЗапрещенныхРасширений()
	
	УстановитьПривилегированныйРежим(Истина);
	СписокРасширений = Константы.СписокЗапрещенныхРасширений.Получить();
	
	Возврат СписокРасширений;
	
КонецФункции

Функция СписокРасширенийФайловOpenDocument()
	
	УстановитьПривилегированныйРежим(Истина);
	СписокРасширенийФайловOpenDocument = Константы.СписокРасширенийФайловOpenDocument.Получить();   

	Если Не ЗначениеЗаполнено(СписокРасширенийФайловOpenDocument) Тогда

		СписокРасширенийФайловOpenDocument =
			"ODT OTT ODP OTP ODS OTS ODC OTC ODF OTF ODM OTH SDW STW SXW STC SXC SDC SDD STI";
		
		ОбновлениеИнформационнойБазыДокументооборот.УстановитьЗначениеКонстанты(
			"СписокРасширенийФайловOpenDocument", СписокРасширенийФайловOpenDocument);
		
	КонецЕсли;
	
	Возврат СписокРасширенийФайловOpenDocument;
	
КонецФункции

Функция СписокРасширенийТекстовыхФайлов()

	УстановитьПривилегированныйРежим(Истина);

	СписокРасширенийТекстовыхФайлов = Константы.СписокРасширенийТекстовыхФайлов.Получить();
	УстановитьПривилегированныйРежим(Ложь);
	Если ПустаяСтрока(СписокРасширенийТекстовыхФайлов) Тогда
		СписокРасширенийТекстовыхФайлов = РаботаСФайламиСлужебный.СписокРасширенийТекстовыхФайлов();
	КонецЕсли;

	Возврат СписокРасширенийТекстовыхФайлов;

КонецФункции

Функция РасширенияФайловДляПроверкиБлокировкиФайлаНаДиске()
	
	УстановитьПривилегированныйРежим(Истина);
	СписокРасширений = Константы.РасширенияФайловДляПроверкиБлокировкиФайлаНаДиске.Получить();
	Возврат СписокРасширений;
	
КонецФункции

// Возвращает признак принадлежности узла к плану обмена РИБ.
//
// Параметры:
//  ПолноеИмяПланаОбмена - Строка - плана обмена, для которого требуется получить значение функции.
//
//  Возвращаемое значение:
//    Булево - Истина - узел принадлежит плану обмена РИБ, иначе Ложь.
//
Функция ЭтоУзелРаспределеннойИнформационнойБазы(ПолноеИмяПланаОбмена) Экспорт

	Возврат ОбщегоНазначения.ОбъектМетаданныхПоПолномуИмени(ПолноеИмяПланаОбмена).РаспределеннаяИнформационнаяБаза;
	
КонецФункции

// Для функции см. ПолныйПутьТома.
// 
// Возвращаемое значение:
//  Булево
//
Функция ПутьКТомуБезУчетаРегиональныхНастроек() Экспорт
	Возврат Константы.ПутьКТомуБезУчетаРегиональныхНастроек.Получить();
КонецФункции

#КонецОбласти
