
#Область ПрограммныйИнтерфейс

// См. ПомощникОтправитьКлиентКОРП.ОбработатьВыборВарианта
// Возвращаемое значение:
//  Булево - Выбор варианта обработан
Функция ОбработатьВыборВарианта(Вариант, Параметры, ФормаВладелец) Экспорт

// Локализация
	Если ПомощникОтправитьКлиентСерверЛокализация.ЭтоВариантОтправкиПоЭДО(Вариант) Тогда
		
		Оповестить("ЗакрытьФормуПомощникаОтправки",, ФормаВладелец);
		ОтправитьПоВариантуОтправитьПоЭДО(Параметры);
		Возврат Истина;
		
	ИначеЕсли ПомощникОтправитьКлиентСерверЛокализация.ЭтоВариантОтправкиЧерез1СShare(Вариант) Тогда
		
		Оповестить("ЗакрытьФормуПомощникаОтправки",, ФормаВладелец);
		ОтправитьПоВариантуОтправитьЧерез1СShare(Параметры);
		Возврат Истина;
	КонецЕсли;
// Локализация Конец

КонецФункции

#КонецОбласти

// Локализация
#Область СлужебныеПроцедурыИФункции

// Инициирует отправку по варианту ОтправитьПоЭДО на клиенте.
//
// Параметры:
//  Параметры - Структура:
//   * Объекты - Массив из СправочникСсылка.ДокументыПредприятия - объекты для отправки.
//
Процедура ОтправитьПоВариантуОтправитьПоЭДО(Параметры) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Источник", ЭтотОбъект);
	ДополнительныеПараметры.Вставить("ОбъектыУчета", Параметры.Объекты);
	ДополнительныеПараметры.Вставить("РежимПодписатьОтправить", Истина);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО",
		ИнтерфейсДокументовЭДОКлиент, ДополнительныеПараметры);
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	
	ЭлектронныеДокументыЭДОКлиент.СформироватьПодписатьОтправитьЭД(Параметры.Объекты,
		ДополнительныеПараметры);
	
КонецПроцедуры

// Инициирует отправку по варианту ОтправитьЧерез1СShare на клиенте.
//
// Параметры:
//  Параметры - Структура:
//   * Объекты - Массив из СправочникСсылка.ДокументыПредприятия - объекты для отправки.
//
Процедура ОтправитьПоВариантуОтправитьЧерез1СShare(Параметры) Экспорт
	
	ИнтеграцияShareКлиент.ПоделитьсяДокументом(Параметры.Объекты[0]);
	
КонецПроцедуры

#КонецОбласти
// Локализация Конец