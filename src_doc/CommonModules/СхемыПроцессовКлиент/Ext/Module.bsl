////////////////////////////////////////////////////////////////////////////////
// Клиентские процедуры и функции по работе со схемами процессов.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область РаботаССхемойВФорме

// Обновляет данные схемы в форме.
// По завершению вызывает внешний обработчик обновления настроек (если он указан).
// Во внешний обработчик передается результат обновления
// (см. СхемыПроцессовКлиентСервер.ОбновитьНастройкиПоСхеме).
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма со схемой.
//  ОбработчикОбновленияНастроек - ОписаниеОповещения
//
Процедура ОбновитьДанныеСхемыВФорме(Форма, ОбработчикОбновленияНастроек = Неопределено) Экспорт
	
	// Не обновляем настройки схемы в веб клиенте, т.к. недоступны некоторые свойства.
	// Данная проверка добавлена на случай, если пользователю все же удастся изменить схему.
	// По умолчанию схема на веб клиенте должна быть доступна только для просмотра.
	#Если ВебКлиент Или МобильныйКлиент Тогда
		Возврат;
	#КонецЕсли	
		
	ОбновитьТекущийЭлементВДанныхСхемыПриФорме(Форма);
	
	РезультатОбновления = СхемыПроцессовКлиентСервер.ОбновитьНастройкиПоСхеме(
		Форма.ДанныеСхемы.НастройкиСхемы, Форма.Схема);
	
	СхемыПроцессовКлиентСервер.ОбновитьПредставлениеЭлементовСхемыПроцесса(
		Форма.Схема, Форма.ДанныеСхемы.НастройкиСхемы);
	
	Форма.ДанныеСхемы.СхемаИзменена = Истина;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Форма", Форма);
	ДопПараметры.Вставить("ОбработчикОбновленияНастроек", ОбработчикОбновленияНастроек);
	ДопПараметры.Вставить("ЭлементДляУточненияТипа", "");
	ДопПараметры.Вставить("РезультатОбновления", РезультатОбновления);
	
	ДоступныеДляВыбораТипыЭлементаОбработка =
		ДоступныеДляВыбораТипыЭлементаГрафическойСхемыОбработка(Форма);
	
	Если РезультатОбновления.ДобавленныеЭлементы.Количество() = 1
		И РезультатОбновления.УдаленныеЭлементы.Количество() = 0
		И ДоступныеДляВыбораТипыЭлементаОбработка.Количество() > 1 Тогда
			
		ЭлементГрафическойСхемы = Форма.Схема.ЭлементыГрафическойСхемы.Найти(
			РезультатОбновления.ДобавленныеЭлементы[0]);		
			
		Если ТипЗнч(ЭлементГрафическойСхемы) = Тип("ЭлементГрафическойСхемыОбработка") Тогда
			
			ДопПараметры.ЭлементДляУточненияТипа = РезультатОбновления.ДобавленныеЭлементы[0];
		КонецЕсли;
		
	КонецЕсли;
	
	Если РезультатОбновления.ДобавленныеЭлементы.Количество() > 0
		Или РезультатОбновления.УдаленныеЭлементы.Количество() > 0 Тогда
		Форма.ДанныеСхемы.ПолучатьДлительностьИзКэш = Ложь;
		Форма.ДанныеСхемы.ПутиИзменены = Истина;
	КонецЕсли;
	
	ОбработчикЗавершенияОбнолвенияДанныхСхемы = Новый ОписаниеОповещения(
		"ЗавершитьОбновлениеДанныхСхемыВФорме",
		ЭтотОбъект,
		ДопПараметры);
	
	Если ЗначениеЗаполнено(ДопПараметры.ЭлементДляУточненияТипа) Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить(
			"ТекущийТипЭлемента",
			Форма.ДанныеСхемы.НастройкиСхемы.ТипыЭлементов[ДопПараметры.ЭлементДляУточненияТипа]);
		ПараметрыФормы.Вставить("ДопустимыеТипы", ДоступныеДляВыбораТипыЭлементаОбработка);

		ОткрытьФорму(
			"ОбщаяФорма.УточнениеТипаДляЭлементаОбработкаГрафическойСхемы",
			ПараметрыФормы,
			Форма,,,,
			ОбработчикЗавершенияОбнолвенияДанныхСхемы);
		
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОбработчикЗавершенияОбнолвенияДанныхСхемы, Неопределено);
		
КонецПроцедуры

// Помещает в данные текущий элемент схемы в данные схемы при форме.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма со схемой.
//
Процедура ОбновитьТекущийЭлементВДанныхСхемыПриФорме(Форма) Экспорт
	
	Если Форма.Элементы.Схема.ТекущийЭлемент <> Неопределено Тогда
		ТекущийЭлементСхемы = Форма.Элементы.Схема.ТекущийЭлемент.Имя;
	Иначе
		ТекущийЭлементСхемы = "";
	КонецЕсли;
	
	Форма.ДанныеСхемы.ТекущийЭлементСхемы = ТекущийЭлементСхемы;
	
КонецПроцедуры

// Восстанавливает текущий элемент в элементе управления графической схемы,
// по данным схемы в форме.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма со схемой.
//
Процедура ВосстановитьТекущийЭлементПоДаннымСхемы(Форма) Экспорт
	
	Если Не ЗначениеЗаполнено(Форма.ДанныеСхемы.ТекущийЭлементСхемы) Тогда
		Возврат;
	КонецЕсли;
	
	Форма.Элементы.Схема.ТекущийЭлемент = Форма.Схема.ЭлементыГрафическойСхемы.Найти(
		Форма.ДанныеСхемы.ТекущийЭлементСхемы);
	
КонецПроцедуры

// Возвращает тип элемента схемы, по данным схемы в форме.
// 
// Параметры:
// 	ИмяЭлемента - Строка - имя элемента схемы.
// 	Форма - ФормаКлиентскогоПриложения - карточка со схемой. 
// 	
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыЭлементовСхемПроцессов
// 	
Функция ТипЭлементаСхемыПоДаннымСхемыВФорме(ИмяЭлемента, Форма) Экспорт
	
	Возврат СхемыПроцессовКлиентСервер.ТипЭлементаСхемыПоДаннымСхемыВФорме(ИмяЭлемента, Форма);
	
КонецФункции

// Показывает результат проверки схемы в форме.
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - карточка со схемой. 
//  
Процедура ПоказатьРезультатПроверкиСхемыВФорме(Форма) Экспорт
	
	Если Не СхемыПроцессовКлиентСервер.ЕстьОшибкиНастройкиСхемы(Форма) Тогда
	 	Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ОбщегоНазначенияКлиент.СообщитьПользователю(
		Форма.ДанныеСхемы.РезультатПроверкиСхемы.ОписаниеОшибки,,
		"Схема");
	
	Если ЗначениеЗаполнено(Форма.ДанныеСхемы.РезультатПроверкиСхемы.ИмяЭлемента) Тогда
		Форма.ДанныеСхемы.ТекущийЭлементСхемы = Форма.ДанныеСхемы.РезультатПроверкиСхемы.ИмяЭлемента;
		ВосстановитьТекущийЭлементПоДаннымСхемы(Форма);
	КонецЕсли;
	
КонецПроцедуры

// Проверяет, что пути схемы были изменены при редактировании формы
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
// 
// Возвращаемое значение:
//  Булево
//
Функция ПутиСхемыИзменены(Форма) Экспорт
	
	Если Форма.ДанныеСхемы.ПутиИзменены Тогда
		Возврат Истина;
	КонецЕсли;
	
	ДанныеСхемы = СхемыПроцессовКлиентСервер.ДанныеСхемыПроцессаИзФормы(Форма);
	
	Возврат СхемыПроцессовВызовСервера.ПутиСхемыИзменены(Форма.Начальныепредшественники, ДанныеСхемы);
		
КонецФункции

#КонецОбласти

#Область ФоноваяПроверка

// Схема проверена фоново, возвращает признак, что была запущена длительная операция проверки схемы
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ВариантПроверки - Строка - Вариант проверки
// 
// Возвращаемое значение:
//  Булево
//
Функция СхемаПроверенаФоново(Форма, ВариантПроверки = "") Экспорт
	
	Если ОбщегоНазначенияКлиент.ИнформационнаяБазаФайловая() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.ЭтоФормаПроцессаСоСхемой(Форма) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РазмерСхемы = Форма.Схема.ЭлементыГрафическойСхемы.Количество();
	КлючеваяОперация = РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.КлючеваяОперацияПроверитьПоРазмеруСхемы(
		РазмерСхемы);
	ОписаниеЗамера = ОценкаПроизводительностиКлиент.НачатьЗамерДлительнойОперации(КлючеваяОперация);
	
	ЗапускПроверки = СхемыПроцессовВызовСервера.ЗапускФоновойПроверкиСхемы(Форма.УникальныйИдентификатор,
		ПоместитьВоВременноеХранилище(Форма.Схема, Форма.УникальныйИдентификатор));
	ПараметрыОжидания  = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.Заголовок = СхемыПроцессовКлиентСервер.ЗаголовокПроверкаСхемы();
	Если ВариантПроверки = ВариантПроверитьДоступностьШаблона() Тогда
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ЗапускПроверки,
			Новый ОписаниеОповещения("ЗавершитьПроверкуДоступностиШаблона", Форма, ОписаниеЗамера),
			ПараметрыОжидания);
	ИначеЕсли ВариантПроверки = ВариантПроверитьИЗакрыть() Тогда
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ЗапускПроверки,
			Новый ОписаниеОповещения("ЗавершитьПроверкуПередЗакрытием", Форма, ОписаниеЗамера), ПараметрыОжидания);
	Иначе	
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ЗапускПроверки,
			Новый ОписаниеОповещения("ОбработатьРезультатПроверкиСхемы", Форма, ОписаниеЗамера), ПараметрыОжидания);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Вариант проверить доступность шаблона.
// 
// Возвращаемое значение:
//  Строка - Вариант проверить доступность шаблона
//
Функция ВариантПроверитьДоступностьШаблона() Экспорт
	
	Возврат "ПроверитьДоступностьШаблона";
	
КонецФункции

// Вариант проверить и закрыть.
// 
// Возвращаемое значение:
//  Строка - Вариант проверить и закрыть
//
Функция ВариантПроверитьИЗакрыть() Экспорт
	
	Возврат "ПроверитьИЗакрыть";

КонецФункции

#КонецОбласти

#Область ФоновоеОбновлениеКэширующихДанныхСхемы

// Кэш схемы рассчитан фоново.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ПараметрыЗаписи - Структура
//  ОжидатьЗавершения - Булево - Ожидать завершения
// 
// Возвращаемое значение:
//  Булево - Кэш схемы рассчитан фоново
//
Функция КэшСхемыРассчитанФоново(Форма, ПараметрыЗаписи, ОжидатьЗавершения = Истина) Экспорт
	
	Если ПараметрыЗаписи.Свойство("КэшСхемыРассчитанФоново") Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	Если ОбщегоНазначенияКлиент.ИнформационнаяБазаФайловая() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЭтоСхемаОбработки = ПараметрыЗаписи.Свойство("ЭтоСхемаОбработки");	
	Если Не (ЭтоСхемаОбработки Или РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.ЭтоФормаПроцессаСоСхемой(Форма))
		Тогда
			Возврат Ложь;
	КонецЕсли;	
	
	ДанныеСхемы = ПараметрыЗаписи.ДанныеСхемы; // см. СхемыПроцессовКлиентСервер.СтруктураДанныхСхемыПроцесса
	Если Не ДанныеСхемы.ПутиИзменены И Не ДанныеСхемы.РассчитатьОтносительныйСрок Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ПараметрыЗаписи.Свойство("ОписаниеЗамера") Тогда
		РазмерСхемы = Форма.Схема.ЭлементыГрафическойСхемы.Количество();
		КлючеваяОперация = РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.КлючеваяОперацияЗаписатьПоРазмеруСхемы(
			РазмерСхемы);
		ОписаниеЗамера = ОценкаПроизводительностиКлиент.НачатьЗамерДлительнойОперации(КлючеваяОперация);
		ПараметрыЗаписи.Вставить("ОписаниеЗамера", ОписаниеЗамера);
	КонецЕсли;
	
	ЗапускОбновления = СхемыПроцессовВызовСервера.ЗапускФоновогоРасчетаКэширующихДанныхСхемы(
		Форма.УникальныйИдентификатор, ПараметрыЗаписи.ДанныеСхемы, ПараметрыЗаписи.ДанныеПараметровСхемы,
		ОжидатьЗавершения);
	
	Если ОжидатьЗавершения Тогда

		ПараметрыОжидания  = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ПараметрыОжидания.Заголовок = НСтр("ru = 'Запись схемы процесса'");
		
		// Если закрываем форму после записи, то к тому моменту кэш будет рассчитан; если не закрываем, то расчет кэша
		// только начнется после записи, нужно дождаться его рассчета
		Если Не ОбщегоНазначенияДокументооборотКлиент.ЗакрытьФормуПослеЗаписи(ПараметрыЗаписи) Тогда
			ПараметрыОжидания.ОтменятьПриЗакрытииФормыВладельца = Ложь;
		КонецЕсли;	

		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Форма", Форма);
		ДополнительныеПараметры.Вставить("ПараметрыЗаписи", ПараметрыЗаписи);

		ДлительныеОперацииКлиент.ОжидатьЗавершение(ЗапускОбновления,
			Новый ОписаниеОповещения("ОбработатьРезультатРасчетаКэшаСхемы", ЭтотОбъект, ДополнительныеПараметры),
			ПараметрыОжидания);
			
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции	

// Обработать результат расчета кэша схемы перед записью схемы, если пути схемы были изменены
// 
// Параметры:
//  Результат - см. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
//  ДополнительныеПараметры - Структура:
//   * Форма - ФормаКлиентскогоПриложения - форма записываемой схемы
//   * ПараметрыЗаписи - Структура
//
Процедура ОбработатьРезультатРасчетаКэшаСхемы(Результат, ДополнительныеПараметры) Экспорт
	
	Если ОбщегоНазначенияДокументооборотКлиент.ДлительнаяОперацияОтмененаВыполненаСОшибкой(Результат) Тогда
		Возврат;
	КонецЕсли;

	Форма = ДополнительныеПараметры.Форма; // ФормаКлиентскогоПриложения
	Форма.ДанныеСхемы.АдресХранилищаКэширующихДанных = Результат.АдресРезультата;
	ПараметрыЗаписи = ДополнительныеПараметры.ПараметрыЗаписи; // Структура
	ПараметрыЗаписи.Вставить("КэшСхемыРассчитанФоново");
	Если ПараметрыЗаписи.Свойство("ЭтоСхемаОбработки") Тогда
		Отказ = Ложь;
		Форма.ЗаписатьСхемуПроцесса(ПараметрыЗаписи, Отказ);
		Если Не Отказ И ОбщегоНазначенияДокументооборотКлиент.ЗакрытьФормуПослеЗаписи(ПараметрыЗаписи) Тогда
			Если  Не Форма.ДиалогПроОшибкиПоказан И Форма.СхемаПроверенаФоново(, Истина) Тогда
				Возврат;
			КонецЕсли;
			Форма.Закрыть(Форма.ДанныеСхемы.Ссылка);
		КонецЕсли;
	ИначеЕсли ОбщегоНазначенияДокументооборотКлиент.ЗакрытьФормуПослеЗаписи(ПараметрыЗаписи) Тогда
		ОбъектЗаписан = Форма.Записать(ПараметрыЗаписи);
		Если ОбъектЗаписан И ОбщегоНазначенияДокументооборотКлиент.ЗакрытьФормуПослеЗаписи(ПараметрыЗаписи) Тогда
			Форма.Закрыть();
		КонецЕсли;
	Иначе
		ДанныеСхемы = ПараметрыЗаписи.ДанныеСхемы; // см. СхемыПроцессовКлиентСервер.СтруктураДанныхСхемыПроцесса
		ДанныеПараметровСхемы = ПараметрыЗаписи.ДанныеПараметровСхемы;
		ЗаполнитьЗначенияСвойств(ДанныеСхемы, Форма.ДанныеСхемы,"АдресХранилищаКэширующихДанных, Ссылка");
		ЗаполнитьЗначенияСвойств(ДанныеПараметровСхемы, Форма.ДанныеПараметровСхемы,"ВерсияДанных, Ссылка,
			|ВладелецСхемы");
		СхемыПроцессовВызовСервера.ЗавершитьРасчетКэширующихДанныхСхемы(ПараметрыЗаписи);
		ЗавершитьЗаписьОбъектаВФорме(Форма, ПараметрыЗаписи);
	КонецЕсли;	
	
КонецПроцедуры	

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаССхемойВФорме

// Завершение ОбновитьДанныеСхемыВФорме.
//
Процедура ЗавершитьОбновлениеДанныхСхемыВФорме(НовыйТип, ДопПараметры) Экспорт
	
	Форма = ДопПараметры.Форма;
	ОбработчикОбновленияНастроек = ДопПараметры.ОбработчикОбновленияНастроек;
	ЭлементДляУточненияТипа = ДопПараметры.ЭлементДляУточненияТипа;
	РезультатОбновления = ДопПараметры.РезультатОбновления;
	
	Если ЗначениеЗаполнено(ЭлементДляУточненияТипа)
		И НовыйТип <> Неопределено
		И Форма.ДанныеСхемы.НастройкиСхемы.ТипыЭлементов[ЭлементДляУточненияТипа] <> НовыйТип Тогда
				
		Форма.ДанныеСхемы.НастройкиСхемы.ТипыЭлементов[ЭлементДляУточненияТипа] = НовыйТип;
			
		СхемыПроцессовКлиентСервер.ОбновитьПредставлениеЭлементовСхемыПроцесса(
			Форма.Схема, Форма.ДанныеСхемы.НастройкиСхемы);
		
	КонецЕсли; 
	
	Если ОбработчикОбновленияНастроек <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОбработчикОбновленияНастроек, РезультатОбновления);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает доступные для выбора типы элемента обработки графической схемы.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма со схемой.
//  
// Возвращаемое значение:
//  Массив
// 	 * см. ПеречислениеСсылка.ТипыЭлементовСхемПроцессов
// 	
Функция ДоступныеДляВыбораТипыЭлементаГрафическойСхемыОбработка(Форма)
	
	ТипыБлокаВыполнения = 
		СхемыПроцессовКлиентСервер.ТипыЭлементовСоответствующиеБлокуВыполненияСхемы();
	
	Индекс = ТипыБлокаВыполнения.Количество() - 1;
	Пока Индекс >= 0 Цикл
		ТипЭлемента = ТипыБлокаВыполнения[Индекс];
		
		Если (СхемыПроцессовКлиентСервер.ЭтоСкриптСхемы(ТипЭлемента)  
			И Не СхемыПроцессовКлиентПовтИсп.ДоступнаНастройкаСкриптовВСхемахПроцессов())
			
			Или Форма.ДанныеСхемы.ПоддерживаемыеТипыЭлементов[ТипЭлемента] <> Истина Тогда
			
			ТипыБлокаВыполнения.Удалить(Индекс);
		КонецЕсли;
		
		Индекс = Индекс - 1;
	КонецЦикла;
		
	Возврат ТипыБлокаВыполнения;
	
КонецФункции

#КонецОбласти

#Область ФоновоеОбновлениеКэширующихДанныхСхемы

// Завершает запись объекта(шаблона или процесса) в форме(если расчета кэша схемы вызван в "После записи").
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
//  ПараметрыЗаписи - Структура:
//   * ДанныеСхемы - см. СхемыПроцессовКлиентСервер.СтруктураДанныхСхемыПроцесса
//   * ДанныеПараметровСхемы - см. РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураДанныхПараметровСхемы
//
Процедура ЗавершитьЗаписьОбъектаВФорме(Форма, ПараметрыЗаписи)
	
	РаботаСКомплекснымиБизнесПроцессамиКлиент.ЗавершитьЗаписьШаблонаВФорме(Форма, ПараметрыЗаписи);
	РаботаСКомплекснымиБизнесПроцессамиКлиент.ЗавершитьЗаписьПроцессаВФорме(Форма, ПараметрыЗаписи);
	Форма.ДанныеСхемы.АдресХранилищаКэширующихДанных = "";
	Форма.ДанныеПараметровСхемы.ВерсияДанных = ПараметрыЗаписи.ДанныеПараметровСхемы.ВерсияДанных;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
