
#Область ПрограммныйИнтерфейс

// Обработчик нажатия поля ПредставлениеHTML в карточках задач
//
Процедура ПредставлениеHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка, Форма) Экспорт
	
	Если ОбзорЗадачКлиентПереопределяемый.ПредставлениеHTMLПриНажатии(
		Элемент, ДанныеСобытия, СтандартнаяОбработка, Форма) Тогда
		
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Лев(ДанныеСобытия.Href, 6) = "v8doc:" Тогда 
		
		НавигационнаяСсылкаПоля = Сред(ДанныеСобытия.Href, 7);
		
		#Если Клиент Тогда
			ПерейтиПоНавигационнойСсылке(НавигационнаяСсылкаПоля);
		#КонецЕсли
	ИначеЕсли Найти(ДанныеСобытия.Href, "ИзменитьСрокВыполнения") Тогда
		
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Задача", Форма.Объект.Ссылка);
		ЗначенияЗаполнения.Вставить("ВидВопроса",
			ПредопределенноеЗначение("Перечисление.ВидыВопросовВыполненияЗадач.ПереносСрока"));
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
		ОткрытьФорму("БизнесПроцесс.РешениеВопросовВыполненияЗадач.ФормаОбъекта",
			ПараметрыФормы);
	ИначеЕсли Найти(ДанныеСобытия.Href, "ПоказатьПричинуПрерыванияПроцесса") Тогда
		КомандыРаботыСБизнесПроцессамиКлиент.ПоказатьПричинуПрерывания(Форма);
	ИначеЕсли Найти(ДанныеСобытия.Href, "ОткрытьКарточкуКонтроля") Тогда
		КонтрольКлиент.ОбработкаКомандыКонтроль(форма.Объект.БизнесПроцесс, Форма);
	ИначеЕсли Найти(ДанныеСобытия.Href, "ПоказатьПричинуОтменыВыполнения") Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Ошибка при выполнении задачи'"));
		ПараметрыФормы.Вставить("ТекстСообщения", Форма.ПричинаОтменыВыполнения);
		ОткрытьФорму("ОбщаяФорма.Сообщение", ПараметрыФормы, Форма);
		
	// Открываем предмет на вкладке Товары
	ИначеЕсли Найти(ДанныеСобытия.Href, "goods") Тогда
		ОткрытьПредметСТоварами(ДанныеСобытия.Href);
		
	ИначеЕсли СтрНайти(ДанныеСобытия.Href, "ShowCorrespondence") Тогда		
		
		ОткрытьПереписку(ДанныеСобытия.Href);
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик нажатия поля ПолноеОписаниеЗадачи в списке Задачи мне
//
Процедура ЗадачиМнеПредставлениеHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка, Форма) Экспорт
	
	Если ОбзорЗадачКлиентПереопределяемый.ЗадачиМнеПредставлениеHTMLПриНажатии(
		Элемент, ДанныеСобытия, СтандартнаяОбработка, Форма) Тогда
		
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Лев(ДанныеСобытия.Href, 6) = "v8doc:" Тогда 
		
		НавигационнаяСсылкаПоля = Сред(ДанныеСобытия.Href, 7);
		
		ПерейтиПоНавигационнойСсылке(НавигационнаяСсылкаПоля);
		
	// Открываем предмет на вкладке Товары
	ИначеЕсли Найти(ДанныеСобытия.Href, "goods") Тогда
		ОткрытьПредметСТоварами(ДанныеСобытия.Href);
		
	ИначеЕсли СтрНайти(ДанныеСобытия.Href, "ShowCorrespondence") Тогда		
		
		ОткрытьПереписку(ДанныеСобытия.Href);
		
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму переписки по документу
//
// Параметры:
//   Href - значение из ДанныеСобытия.Href при клике в ПолеHTMLДокумента
//
Процедура ОткрытьПереписку(Ссылка1СДокументооборота) Экспорт
	
	ПредметПереписки = РаботаС_HTMLВызовСервера.СсылкаПоНавигационной(Ссылка1СДокументооборота);
	Если Не ЗначениеЗаполнено(ПредметПереписки) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДелопроизводствоКлиентСервер.ЭтоВходящийДокумент(ПредметПереписки)
		Или ДелопроизводствоКлиентСервер.ЭтоИсходящийДокумент(ПредметПереписки) Тогда
		
		ПараметрыФормы = Новый Структура("Документ", ПредметПереписки);
		ОткрытьФорму("ОбщаяФорма.ИсторияПереписки",
			ПараметрыФормы,,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли ТипЗнч(ПредметПереписки) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда
		
		ПараметрыФормы = Новый Структура("ПредметПереписки", ПредметПереписки);
		ОткрытьФорму("ОбщаяФорма.ПерепискаПоПредмету",
			ПараметрыФормы,,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму документа на вкладке Товары
//
// Параметры:
//   Href - значение из ДанныеСобытия.Href при клике в ПолеHTMLДокумента
//
Процедура ОткрытьПредметСТоварами(Href) Экспорт
	
	ИмяТегаПереписки = "goods";
	
	ПозицияНачала = СтрНайти(Href, ИмяТегаПереписки);
	Если ПозицияНачала <> 0 Тогда
		
		ПозицияКонца = ПозицияНачала + СтрДлина(ИмяТегаПереписки);
		НавигационнаяСсылка = Сред(Href, ПозицияКонца);
		ПрефиксДок = "ДокументыПредприятия";
			
		Если ПозицияНачала <> 0 Тогда
			СтрокаИД = Сред(НавигационнаяСсылка, 6); // 6 = это длина строки "?ref="
			
			ДокументСсылка = ОбзорЗадачВызовСервера.ПолучитьСсылкуДокументаПоИдентификатору(
				ПрефиксДок, СтрокаИД);
				
			Если ЗначениеЗаполнено(ДокументСсылка) Тогда
				ПараметрыОткрытия = Новый Структура("ОткрытьЗакладкуТовары", Истина);
				ДелопроизводствоКлиент.ОткрытьДокументПредприятия(ДокументСсылка, ПараметрыОткрытия);
			КонецЕсли;
			
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

