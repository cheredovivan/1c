#Область ПрограммныйИнтерфейс

// См. РаботаСКорреспонденцией.УстановитьСвойстваРеквизитовКорреспонденции
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
Процедура УстановитьСвойстваРеквизитовКорреспонденции(Форма) Экспорт

// Локализация
	РаботаСОбращениями.УстановитьВидимостьПолейУчетаОбращенийГраждан(Форма, Ложь);
// Локализация Конец

КонецПроцедуры

// См. РаботаСКорреспонденцией.ПроверкаЗаполненияФормыНаСервере
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  ПроверяемыеРеквизиты - Массив из Строка
Процедура ПроверкаЗаполненияФормыНаСервере(Форма, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	// Локализация
	ВидДокументаКэш = Форма.ВидДокументаКэш;
	Объект = Форма.Объект;
	Если ВидДокументаКэш.ЯвляетсяОбращениемОтГраждан Тогда 
		ПроверяемыеРеквизиты.Добавить("ВидОбращения");
		
		Если ЗначениеЗаполнено(Форма.ПервичноеОбращение) 
			И Форма.ПервичноеОбращение = Объект.Ссылка Тогда 
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Указана ссылка на самого себя'"),,"ПервичноеОбращение",,Отказ);
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(Форма.ОсновноеОбращение) 
			И Форма.ОсновноеОбращение = Объект.Ссылка Тогда 
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Указана ссылка на самого себя'"),,"ОсновноеОбращение",,Отказ);
		КонецЕсли;
		
		Если ПолучитьФункциональнуюОпцию("РегламентированныйУчетОбращений") Тогда
			Если Форма.ВопросыОбращения.Количество() = 0 Тогда 
				ДатаПроверкиНаличияВопросов = ДелопроизводствоСерверПовтИсп.ДатаПроверкиНаличияВопросов();
				
				Если Не ЗначениеЗаполнено(ДатаПроверкиНаличияВопросов)
					Или (ЗначениеЗаполнено(ДатаПроверкиНаличияВопросов) 
						И ЗначениеЗаполнено(Объект.ДатаРегистрации) И Объект.ДатаРегистрации > ДатаПроверкиНаличияВопросов)
					Или (ЗначениеЗаполнено(ДатаПроверкиНаличияВопросов) 
						И Не ЗначениеЗаполнено(Объект.ДатаРегистрации) И Объект.ДатаСоздания > ДатаПроверкиНаличияВопросов) Тогда 
					ОбщегоНазначения.СообщитьПользователю(
						НСтр("ru = 'Не указано ни одного вопроса документа'"),,
							"КодВопроса",, Отказ);
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			//Проверка табличной части ВопросыОбращения на задвоения
			КоличествоВопросыОбращения = Форма.ВопросыОбращения.Количество();
			Если КоличествоВопросыОбращения > 1 Тогда
				Для Инд1 = 0 По КоличествоВопросыОбращения - 2 Цикл
					Для Инд2 = Инд1 + 1 По КоличествоВопросыОбращения - 1 Цикл
						Если Форма.ВопросыОбращения[Инд1].Вопрос = Форма.ВопросыОбращения[Инд2].Вопрос Тогда 
							ТекстСообщения = СтрШаблон(
								НСтр("ru = 'Вопрос ""%1"" указан дважды в списке вопросов обращения'"),
								Форма.ВопросыОбращения[Инд2].КодВопроса);
							
							ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,
								"ВопросыОбращения["+ Формат(Инд2, "ЧН=0; ЧГ=0") +"].КодВопроса",,Отказ);
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	// Локализация Конец
	
КонецПроцедуры

// Вызывается из ОбработкаПроверкиЗаполнения модуля объекта документа Корреспонденция
// Позволяет пропустить проверку заполнения контрагентов.
// 
// Параметры:
//  ДокументОбъект - ДокументОбъект.Корреспонденция
//  Отказ - Булево
//  ПроверяемыеРеквизиты - Массив из Строка
// 
// Возвращаемое значение:
//  Булево - Пропустить проверку заполнения контрагентов
Функция ПропуститьПроверкуЗаполненияКонтрагентов(ДокументОбъект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	// Локализация
	Если ДокументОбъект.ВидОбращения = Перечисления.ВидыОбращенийГраждан.Анонимное Тогда
		Возврат Истина;
	КонецЕсли;
	// Локализация Конец
	Возврат Ложь;
	
КонецФункции

// Вызывается из Документы.Корреспонденция.ЗаполнитьДанныеНаФормеДокумента
// Позволяет дополнить запрос для заполнения формы документа.
// 
// Параметры:
//  Запрос - Запрос
Процедура ДополнитьЗапросДляЗаполненияФормыДокумента(Запрос) Экспорт

// Локализация
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДополнительныеПоля",
		"
		|	Корреспонденция.ВопросыОбращения.(
		|		Ссылка КАК Ссылка,
		|		НомерСтроки КАК НомерСтроки,
		|		Вопрос КАК Вопрос,
		|		ДатаОтвета КАК ДатаОтвета,
		|		КодВопроса КАК КодВопроса,
		|		МнениеАвтораОМерах КАК МнениеАвтораОМерах,
		|		МнениеАвтораОРезультатах КАК МнениеАвтораОРезультатах,
		|		ОрганДляПередачи КАК ОрганДляПередачи,
		|		Раздел КАК Раздел,
		|		РезультатРассмотрения КАК РезультатРассмотрения,
		|		Тема КАК Тема,
		|		Тематика КАК Тематика
		|	) КАК ВопросыОбращения,
		|	Корреспонденция.ВидОбращения КАК ВидОбращения,
		|	Корреспонденция.ОсновноеОбращение КАК ОсновноеОбращение,
		|	Корреспонденция.ПервичноеОбращение КАК ПервичноеОбращение,
		|	Корреспонденция.ГотовоКВыгрузкеССТУ,
		|	Корреспонденция.ЭтоПовторноеОбращение КАК ЭтоПовторноеОбращение,
		|	Корреспонденция.ЭтоДубликатОбращения КАК ЭтоДубликатОбращения");
// Локализация Конец

КонецПроцедуры

// Вызывается Из Документы.Корреспонденция.НовыеПараметрыКорреспонденции()
// Позволяет дополнить структуру реквизитов нового документа Корреспонденция
Процедура ДополнитьНовыеПараметрыКорреспонденции(ПараметрыКорреспонденции) Экспорт

// Локализация
	// Добавляет следующие поля:
	// * ВидОбращения - ПеречислениеСсылка.ВидыОбращенийГраждан -
	// * ВопросыОбращения - Массив Из Структура -
	// 					  - См. Справочник.ДокументыПредприятия.Форма.ФормаЭлемента.ВопросыОбращения
	// * ОсновноеОбращение - СправочникСсылка.ДокументыПредприятия -
	// * ПервичноеОбращение - СправочникСсылка.ДокументыПредприятия -
	// * ЭтоДубликатОбращения - Булево -
	// * ЭтоПовторноеОбращение - Булево -
	// * ГотовоКВыгрузкеССТУ - Булево - Обращение готово к выгрузке в ССТУ
	ПараметрыКорреспонденции.Вставить("ВидОбращения", Перечисления.ВидыОбращенийГраждан.ПустаяСсылка());
	ПараметрыКорреспонденции.Вставить("ВопросыОбращения", Новый Массив());
	ПараметрыКорреспонденции.Вставить("ОсновноеОбращение", Справочники.ДокументыПредприятия.ПустаяСсылка());
	ПараметрыКорреспонденции.Вставить("ПервичноеОбращение", Справочники.ДокументыПредприятия.ПустаяСсылка());
	ПараметрыКорреспонденции.Вставить("ЭтоДубликатОбращения", Ложь);
	ПараметрыКорреспонденции.Вставить("ЭтоПовторноеОбращение", Ложь);
	ПараметрыКорреспонденции.Вставить("ГотовоКВыгрузкеССТУ", Ложь);
// Локализация Конец

КонецПроцедуры

#КонецОбласти