
#Область ПрограммныйИнтерфейс

#Область СобытияФормы

// Код обработчика "ПриОткрытии" форм списков контрагентов.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//
Процедура ПриОткрытии(Форма, Отказ) Экспорт
	
	УстановитьТекущуюСтрокуДереваГрупп(Форма);
	ПереключитьВидПросмотра(Форма);
	
КонецПроцедуры

// Код обработчика "ОбработкаОповещения" форм списков контрагентов с возвратом записанной группы.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ИмяСобытия - Строка
//  Параметр - Произвольный
//  Источник - Произвольный
//
Процедура ОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	НоваяГруппа = Неопределено;
	Если ТипЗнч(Параметр) = Тип("Массив") И Параметр.Количество() <> 0 Тогда
		НоваяГруппа = Параметр[0];
	Иначе
		НоваяГруппа = Параметр;
	КонецЕсли;
	
	Форма.ТекущаяГруппа = НоваяГруппа;
	
КонецПроцедуры

#КонецОбласти

#Область СобытияТаблицыГрупп

// Код обработчика "ПриАктивизацииСтроки" таблицы групп.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Элемент - ТаблицаФормы
//
Процедура ГруппыПриАктивизацииСтроки(Форма, Элемент) Экспорт
	
	ТекущиеДанные = Форма.Элементы.Группы.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма.ТекущаяГруппа = ТекущиеДанные.ГруппаКонтрагентов;
	УстановитьОтборПоГруппе(Форма);
	
КонецПроцедуры

// Код обработчика "НачалоПеретаскивания" таблицы групп.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Элемент - ТаблицаФормы
//  ПараметрыПеретаскивания - ПараметрыПеретаскивания
//  Выполнение - Булево
//
Процедура ГруппыНачалоПеретаскивания(Форма, Элемент, ПараметрыПеретаскивания, Выполнение) Экспорт
	
	МожноНачинатьПеретаскиваниеГруппы = ПроверкаНачалаПеретаскиванияГруппы(Форма);
	Если Не МожноНачинатьПеретаскиваниеГруппы Тогда
		Выполнение = Ложь;
		Возврат;
	КонецЕсли;
	
	ПараметрыПеретаскивания.Значение = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Форма.ТекущаяГруппа);
	
КонецПроцедуры

// Код обработчика "ПроверкаПеретаскивания" таблицы групп.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Элемент - ТаблицаФормы
//  ПараметрыПеретаскивания - ПараметрыПеретаскивания
//  СтандартнаяОбработка - Булево
//  Строка - Строка
//         - Число
//  Поле - ПолеФормы
//
Процедура ГруппыПроверкаПеретаскивания(Форма, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка,
	Поле) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если Строка = Неопределено Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		Возврат;
	КонецЕсли;
	
	СтрокаГруппы = Форма.Группы.НайтиПоИдентификатору(Строка);
	Если СтрокаГруппы = Неопределено
		Или СтрокаГруппы.ГруппаКонтрагентов = РаботаСоСпискамиКонтрагентовКлиентСервер.ИмяВсеГруппы() Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		Возврат;
	КонецЕсли;
	
	ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.Перемещение;
	ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Перемещение;
	
КонецПроцедуры

// Код обработчика "Перетаскивание" таблицы групп с возвратом группы, куда выполнили перетаскивание.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Элемент - ТаблицаФормы
//  ПараметрыПеретаскивания - ПараметрыПеретаскивания
//  СтандартнаяОбработка - Булево
//  Строка - Строка
//         - Число
//  Поле - ПолеФормы
// 
Процедура ГруппыПеретаскивание(Форма, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	МожноПеретаскиватьГруппу = ПроверкаПеретаскиванияГруппы(ПараметрыПеретаскивания, Строка);
	Если Не МожноПеретаскиватьГруппу Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаГруппы = Форма.Группы.НайтиПоИдентификатору(Строка);
	Если СтрокаГруппы = Неопределено
		Или СтрокаГруппы.ГруппаКонтрагентов = РаботаСоСпискамиКонтрагентовКлиентСервер.ИмяВсеГруппы() Тогда
		Возврат;
	КонецЕсли;
	
	НоваяГруппа = ?(СтрокаГруппы.ГруппаКонтрагентов = РаботаСоСпискамиКонтрагентовКлиентСервер.ИмяБезГруппы(),
		ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"),
		СтрокаГруппы.ГруппаКонтрагентов);
	
	ЭтоГруппа = РаботаСоСпискамиКонтрагентовВызовСервера.УстановитьНовуюГруппуКонтрагентов(
		ПараметрыПеретаскивания.Значение, НоваяГруппа);
	
	Если ЭтоГруппа Тогда
		
		ЗаполнитьГруппы(Форма);
		
		ИдентификаторСтроки = 0;
		ОбщегоНазначенияКлиентСервер.ПолучитьИдентификаторСтрокиДереваПоЗначениюПоля("ГруппаКонтрагентов",
			ИдентификаторСтроки,
			Форма.Группы.ПолучитьЭлементы(),
			ПараметрыПеретаскивания.Значение[0],
			Ложь);
		Форма.Элементы.Группы.ТекущаяСтрока = ИдентификаторСтроки;
		
	Иначе
		Форма.Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

// Код обработчика "ПередНачаломДобавления" таблицы групп.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Элемент - ТаблицаФормы
//  Отказ - Булево
//  Копирование - Булево
//  Родитель - СправочникСсылка.Контрагенты
//           - Строка
//  ЭтоГруппа - Булево
//  Параметр - Произвольный
//
Процедура ГруппыПередНачаломДобавления(Форма, Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр) Экспорт
	
	Отказ = Истина;
	
	Если Не Форма.ЕстьПравоИзменения Или Не ЗначениеЗаполнено(Форма.ТекущаяГруппа) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ЭтоГруппа", Истина);
	
	Если Не Копирование Тогда
		
		ПараметрыФормы.Вставить("ЗначенияЗаполнения");
		ЗначениеЗаполнения = Новый Структура("Родитель", Форма.ТекущаяГруппа);
		ПараметрыФормы.ЗначенияЗаполнения = ЗначениеЗаполнения;
		
	ИначеЕсли ТипЗнч(Форма.ТекущаяГруппа) = Тип("Строка") Тогда
		
		Возврат;
		
	Иначе
		
		ПараметрыФормы.Вставить("ЗначениеКопирования", Форма.ТекущаяГруппа);
		
	КонецЕсли;
	
	ОткрытьФорму("Справочник.Контрагенты.ФормаГруппы", ПараметрыФормы, Форма.Элементы.Список);
	
КонецПроцедуры

// Код обработчика "ПередУдалением" таблицы групп.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Элемент - ТаблицаФормы
//  Отказ - Булево
//
Процедура ГруппыПередУдалением(Форма, Элемент, Отказ) Экспорт
	
	Отказ = Истина;
	
	Если Не Форма.ЕстьПравоИзменения
		Или ТипЗнч(Форма.ТекущаяГруппа) <> Тип("СправочникСсылка.Контрагенты")
		Или Не ЗначениеЗаполнено(Форма.ТекущаяГруппа) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура("Форма", Форма);
	ОписаниеОповещения = Новый ОписаниеОповещения("ГруппыПередУдалениемПродолжение", ЭтотОбъект, ПараметрыОповещения);
	
	Если Не Форма.Элементы.Группы.ТекущиеДанные.ПометкаУдаления Тогда
		ТекстВопроса = СтрШаблон(НСтр("ru = 'Пометить ""%1"" на удаление?'"), Форма.ТекущаяГруппа);
	Иначе
		ТекстВопроса = СтрШаблон(НСтр("ru = 'Снять с ""%1"" пометку удаления?'"), Форма.ТекущаяГруппа);
	КонецЕсли;
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0, КодВозвратаДиалога.Да);
	
КонецПроцедуры

// Код обработчика "ПередНачаломИзменения" таблицы групп.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Элемент - ТаблицаФормы
//  Отказ - Булево
//
Процедура ГруппыПередНачаломИзменения(Форма, Элемент, Отказ) Экспорт
	
	Отказ = Истина;
	
	Если Не Форма.ЕстьПравоИзменения
		Или ТипЗнч(Форма.ТекущаяГруппа) <> Тип("СправочникСсылка.Контрагенты")
		Или Не ЗначениеЗаполнено(Форма.ТекущаяГруппа) Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, Форма.ТекущаяГруппа);
	
КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

// Обработчик команды "ПоказыватьУдаленные".
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Команда - КомандаФормы
//
Процедура ПоказыватьУдаленные(Форма, Команда) Экспорт
	
	Форма.ПоказыватьУдаленные = Не Форма.ПоказыватьУдаленные;
	Форма.Элементы.ПоказыватьУдаленные.Пометка = Форма.ПоказыватьУдаленные;
	
КонецПроцедуры

// Обработчик команды "ПоказыватьПодчиненные".
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Команда - КомандаФормы
//
Процедура ПоказыватьПодчиненные(Форма, Команда) Экспорт
	
	Форма.ПоказыватьПодчиненные = Не Форма.ПоказыватьПодчиненные;
	Форма.Элементы.ГруппыКонтекстноеМенюПоказыватьПодчиненные.Пометка = Форма.ПоказыватьПодчиненные;
	
	УстановитьОтборПоГруппе(Форма);
	
КонецПроцедуры

// Обработчик команды "РежимПросмотраСписком".
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Команда - КомандаФормы
//
Процедура РежимПросмотраСписком(Форма, Команда) Экспорт
	
	Форма.ВидПросмотра = ПредопределенноеЗначение("Перечисление.ВидыПросмотраСпискаОбъектов.Списком");
	
	ВидПросмотраПриИзменении(Форма);
	
КонецПроцедуры

// Обработчик команды "РежимПросмотраПоГруппам".
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Команда - КомандаФормы
//
Процедура РежимПросмотраПоГруппам(Форма, Команда) Экспорт
	
	Форма.ВидПросмотра = ПредопределенноеЗначение("Перечисление.ВидыПросмотраСпискаОбъектов.ПоПапкам");
	
	УстановитьТекущуюСтрокуДереваГрупп(Форма);
	ВидПросмотраПриИзменении(Форма);
	
КонецПроцедуры

// Обработчик команды "СвернутьГруппы".
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Команда - КомандаФормы
//
Процедура СвернутьГруппы(Форма, Команда) Экспорт
	
	ЭлементыДереваГрупп = Форма.Группы.ПолучитьЭлементы();
	ЭлементФормы = Форма.Элементы.Группы;
	
	Для Каждого ЭлементДерева Из ЭлементыДереваГрупп Цикл
		СвернутьВеткуДереваГрупп(ЭлементФормы, ЭлементДерева);
		ЭлементФормы.Свернуть(ЭлементДерева.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

// Обработчик команды "СвернутьГруппы".
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Команда - КомандаФормы
//
Процедура РазвернутьГруппы(Форма, Команда) Экспорт
	
	ЭлементФормы = Форма.Элементы.Группы;
	
	РазвернутьВеткуДереваГрупп(ЭлементФормы, Форма.Группы);
	
КонецПроцедуры

// Обработчик команды "ОбновитьГруппы".
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Команда - КомандаФормы
//
Процедура ОбновитьГруппы(Форма, Команда) Экспорт
	
	ЗаполнитьГруппы(Форма);
	
КонецПроцедуры

#КонецОбласти

// Заполняет дерево групп.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//
Процедура ЗаполнитьГруппы(Форма) Экспорт
	
	ТекущиеДанныеДерева = Форма.Группы;
	ЭлементыДерева = ТекущиеДанныеДерева.ПолучитьЭлементы();
	ЭлементыДерева.Очистить();
	
	НовыеДанныеДерева = РаботаСоСпискамиКонтрагентовВызовСервера.СформироватьДеревоГрупп(ТекущиеДанныеДерева,
		Форма.ПоказыватьУдаленные);
	НовыеЭлементыДерева = НовыеДанныеДерева.ПолучитьЭлементы();
	
	ЗаполнитьКоллекциюЭлементовДерева(ЭлементыДерева, НовыеЭлементыДерева);
	
	ИдентификаторСтроки = Неопределено;
	Если Форма.ТекущаяГруппа <> Неопределено Тогда
		ИдентификаторСтроки = РаботаСоСпискамиКонтрагентовКлиентСервер.ИдентификаторСтрокиДереваПоЗначению(Форма.Группы,
			Форма.ТекущаяГруппа);
	КонецЕсли;
	
	Если ИдентификаторСтроки <> Неопределено Тогда
		Форма.Элементы.Группы.ТекущаяСтрока = ИдентификаторСтроки;
	КонецЕсли;
	
	РаботаСоСпискамиКонтрагентовКлиентСервер.ДобавитьСпециальныеГруппы(ЭлементыДерева);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьТекущуюСтрокуДереваГрупп(Форма)
	
	Если Форма.ТекущаяГруппа = Неопределено Тогда
		
		Форма.Элементы.Группы.ТекущаяСтрока = Неопределено;
		
	Иначе
		
		Отбор = Новый Структура("ГруппаКонтрагентов", Форма.ТекущаяГруппа);
		ЭлементДерева = ОбщегоНазначенияДокументооборотКлиентСервер.НайтиСтрокуДерева(Форма.Группы, Отбор);
		
		Если ЭлементДерева <> Неопределено Тогда
			Форма.Элементы.Группы.ТекущаяСтрока = ЭлементДерева.ПолучитьИдентификатор();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПереключитьВидПросмотра(Форма)
	
	Если Форма.ВидПросмотра = ПредопределенноеЗначение("Перечисление.ВидыПросмотраСпискаОбъектов.ПоПапкам") Тогда
		
		Форма.Элементы.ГруппыКонтрагентов.Видимость = Истина;
		Форма.Элементы.РежимПросмотраСписком.Пометка = Ложь;
		Форма.Элементы.РежимПросмотраПоГруппам.Пометка = Истина;
		Форма.Элементы.ГруппыКонтекстноеМенюПоказыватьПодчиненные.Пометка = Форма.ПоказыватьПодчиненные;
		
	Иначе
		
		Форма.Элементы.ГруппыКонтрагентов.Видимость = Ложь;
		Форма.Элементы.РежимПросмотраСписком.Пометка = Истина;
		Форма.Элементы.РежимПросмотраПоГруппам.Пометка = Ложь;
		Форма.ТекущаяГруппа = РаботаСоСпискамиКонтрагентовКлиентСервер.ИмяВсеГруппы();
		
	КонецЕсли;
	
	УстановитьОтборПоГруппе(Форма);
	
КонецПроцедуры

Процедура УстановитьОтборПоГруппе(Форма)
	
	Если Форма.ТекущаяГруппа = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПравоеЗначение = Неопределено;
	Сравнение = ВидСравненияКомпоновкиДанных.Равно;
	Использование = Истина;
	
	ЭтоОтборПоГруппе = ТипЗнч(Форма.ТекущаяГруппа) = Тип("СправочникСсылка.Контрагенты");
	
	Форма.Элементы.ГруппыКонтекстноеМенюСкопировать.Доступность = ЭтоОтборПоГруппе;
	Форма.Элементы.ГруппыКонтекстноеМенюИзменить.Доступность = ЭтоОтборПоГруппе;
	Форма.Элементы.ГруппыКонтекстноеМенюУдалить.Доступность = ЭтоОтборПоГруппе;
	
	Если ЭтоОтборПоГруппе Тогда
		
		Если Форма.ПоказыватьПодчиненные Тогда
			Сравнение = ВидСравненияКомпоновкиДанных.ВИерархии;
		КонецЕсли;
		ПравоеЗначение = Форма.ТекущаяГруппа;
		
	ИначеЕсли Форма.ТекущаяГруппа = РаботаСоСпискамиКонтрагентовКлиентСервер.ИмяВсеГруппы() Тогда
		
		Использование = Ложь;
		
	Иначе
		
		ПравоеЗначение = ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка");
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Форма.Список,
		"Родитель",
		ПравоеЗначение,
		Сравнение,
		,
		Использование);
	
КонецПроцедуры

Процедура ГруппыПередУдалениемПродолжение(Результат, Параметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	НоваяПометкаУдаления = РаботаСоСпискамиКонтрагентовВызовСервера.ИзменитьПометкуУдаленияГруппы(
		Параметры.Форма.ТекущаяГруппа);
	ИдентификаторТекущейСтроки = Параметры.Форма.Элементы.Группы.ТекущиеДанные.ПолучитьИдентификатор();
	ТекущаяСтрокаДерева = Параметры.Форма.Группы.НайтиПоИдентификатору(ИдентификаторТекущейСтроки);
	
	Если Не Параметры.Форма.ПоказыватьУдаленные И НоваяПометкаУдаления = Истина Тогда
		РодительТекущейСтроки = ТекущаяСтрокаДерева.ПолучитьРодителя();
		Если РодительТекущейСтроки <> Неопределено Тогда
			ЭлементыРодителя = РодительТекущейСтроки.ПолучитьЭлементы();
			ЭлементыРодителя.Удалить(ТекущаяСтрокаДерева);
		Иначе
			КоллекцияЭлементовДерева = Параметры.Форма.Группы.ПолучитьЭлементы();
			КоллекцияЭлементовДерева.Удалить(ТекущаяСтрокаДерева);
		КонецЕсли;
	Иначе
		ТекущаяСтрокаДерева.ПометкаУдаления = НоваяПометкаУдаления;
		ИзменитьПиктограммуРекурсивно(ТекущаяСтрокаДерева, НоваяПометкаУдаления);
	КонецЕсли;
	
	Параметры.Форма.Элементы.Список.Обновить();
	
КонецПроцедуры

Процедура ИзменитьПиктограммуРекурсивно(СтрокаДерева, ПометкаУдаления)
	
	СтрокаДерева.ПометкаУдаления = ПометкаУдаления;
	СтрокаДерева.ИндексПиктограммы = ?(ПометкаУдаления, 1, 0);
	
	СтрокиДерева = СтрокаДерева.ПолучитьЭлементы();
	Для Каждого СтрокаПодчиненная Из СтрокиДерева Цикл
		ИзменитьПиктограммуРекурсивно(СтрокаПодчиненная, ПометкаУдаления);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВидПросмотраПриИзменении(Форма)
	
	Если Форма.ПредыдущийВидПросмотра = Форма.ВидПросмотра Тогда
		Возврат;
	КонецЕсли;
	
	Форма.ПредыдущийВидПросмотра = Форма.ВидПросмотра;
	
	ПереключитьВидПросмотра(Форма);
	
КонецПроцедуры

Процедура СвернутьВеткуДереваГрупп(ЭлементФормы, Дерево)
	
	Для Каждого Элемент Из Дерево.ПолучитьЭлементы() Цикл
		ИдентификаторСтроки = Элемент.ПолучитьИдентификатор();
		ЭлементФормы.Свернуть(ИдентификаторСтроки);
		СвернутьВеткуДереваГрупп(ЭлементФормы, Элемент);
	КонецЦикла;
	
КонецПроцедуры

Процедура РазвернутьВеткуДереваГрупп(ЭлементФормы, Дерево)
	
	Для Каждого Элемент Из Дерево.ПолучитьЭлементы() Цикл
		ИдентификаторСтроки = Элемент.ПолучитьИдентификатор();
		ЭлементФормы.Развернуть(ИдентификаторСтроки);
		РазвернутьВеткуДереваГрупп(ЭлементФормы, Элемент);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьКоллекциюЭлементовДерева(ТекущаяКоллекция, НоваяКоллекция)
	
	Для Каждого Строка Из НоваяКоллекция Цикл
		ЭлементДерева = ТекущаяКоллекция.Добавить();
		ЗаполнитьЗначенияСвойств(ЭлементДерева, Строка);
		
		ПодчиненныеЭлементы = Строка.ПолучитьЭлементы();
		
		Если ПодчиненныеЭлементы.Количество() > 0 Тогда
			ЗаполнитьКоллекциюЭлементовДерева(ЭлементДерева.ПолучитьЭлементы(), ПодчиненныеЭлементы);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПроверкаНачалаПеретаскиванияГруппы(Форма)
	
	Результат = Форма.ЕстьПравоИзменения И Не Форма.ТекущаяГруппа = Неопределено
		И Форма.ТекущаяГруппа <> РаботаСоСпискамиКонтрагентовКлиентСервер.ИмяВсеГруппы()
		И Форма.ТекущаяГруппа <> РаботаСоСпискамиКонтрагентовКлиентСервер.ИмяБезГруппы();
	
	Возврат Результат;
	
КонецФункции

Функция ПроверкаПеретаскиванияГруппы(ПараметрыПеретаскивания, Строка)
	
	Результат = ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив")
		И ПараметрыПеретаскивания.Значение.Количество() <> 0
		И ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("СправочникСсылка.Контрагенты")
		И Строка <> Неопределено;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
