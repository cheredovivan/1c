
#Область ПрограммныйИнтерфейс

// См. ОбновлениеИнформационнойБазыДокументооборот.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	// Локализация
	ОбновлениеИнформационнойБазыДокументооборот.ДобавитьСтандартныйОбработчикАрхив(Обработчики, "3_0_10_8");
	ОбновлениеИнформационнойБазыДокументооборот.ДобавитьСтандартныйОбработчикАрхив(Обработчики, "3_0_10_9");
	ДобавитьСтандартныйОтложенныйОбработчик(Обработчики,
		"3_0_11_2",
		"ПерейтиНаВерсию_3_0_11_2",
		"9999e101-a8dd-4515-aa92-2f3cc6aa9999",
		НСтр("ru = 'Обновляет состояние МЭДО в регистре ДанныеДокументовПредприятия для быстрого показа в списке'"));
	
	ДобавитьСтандартныйОтложенныйОбработчик(Обработчики,
		"3_0_11_3",
		"ПерейтиНаВерсию_3_0_11_3",
		"aaaae101-a8dd-4515-aa92-2f3cc6aaaaaa",
		НСтр("ru = 'Создает табличную часть Пакеты в документах ДанныеДокументаМЭДО'"));
	
	ДобавитьСтандартныйОбработчик(Обработчики, "3_0_11_15");
	
	ДобавитьСтандартныйОбработчик(Обработчики, "3_0_12_4");
	
	ДобавитьСтандартныйОбработчик(Обработчики, "3_0_12_8");
	
	ДобавитьСтандартныйОтложенныйОбработчик(Обработчики,
		"3_0_12_11",
		"ПерейтиНаВерсию_3_0_12_11",
		"317cbbd6-0b9d-4f9d-bba6-ce42ea5ec6d6",
		НСтр("ru = 'Проставляет АктуализироватьВерсию = Истина в РС НастройкиКонтрагентовМЭДО'"));
	
	ДобавитьСтандартныйОтложенныйОбработчик(Обработчики,
		"3_0_12_15",
		"ПерейтиНаВерсию_3_0_12_15",
		"68B5BAAC-CFAB-43db-A49A-AF25A95BFE63",
		НСтр("ru = 'Удаляет состояния ЭДО бумажных документов, которые еще не были сформированы'"));
	
	ДобавитьСтандартныйОбработчик(Обработчики, "3_0_13_10");
	ДобавитьСтандартныйОбработчик(Обработчики, "3_0_15_2");
	ДобавитьСтандартныйОбработчик(Обработчики, "3_0_15_9");
	ДобавитьСтандартныйОбработчик(Обработчики, "3_0_15_15");
	ДобавитьСтандартныйОбработчик(Обработчики, "3_0_15_20");
	ДобавитьСтандартныйОтложенныйОбработчик(Обработчики,
		"3_0_15_21",
		"ПерейтиНаВерсию_3_0_15_21",
		"78f2e571-84cb-45e0-b58a-3138eca82b68",
		НСтр("ru = 'Обновление состояний ЭДО в данных документов ДО'"));
	ДобавитьСтандартныйОбработчик(Обработчики, "3_0_15_36");
	ДобавитьСтандартныйОтложенныйОбработчик(Обработчики,
		"3_0_15_38",
		"ПерейтиНаВерсию_3_0_15_38",
		"a6f19fb6-5dd8-4a39-bee0-ae7f85310d44",
		НСтр("ru = 'Устанавливает реквизит НеПредлагатьСохранятьНастройки РС НастройкиПолученияЭлектронныхДокументов'"));
	ДобавитьСтандартныйОбработчик(Обработчики, "3_0_17_27");
	ДобавитьСтандартныйОбработчик(Обработчики, "3_0_18_10");
	// Локализация Конец
	
КонецПроцедуры

// См. ОбновлениеИнформационнойБазыДокументооборот.ОбработчикиПервоначальногоЗаполненияОбщие
// Позволяет дополнить обработчики первого запуска национальной спецификой
Процедура ОбработчикиПервоначальногоЗаполненияОбщие() Экспорт
	// Локализация

	// Внешнее подписание.
	Справочники.СервисыВнешнегоПодписания.ЗаполнитьПредопределенные();
	
	// Локализация Конец
КонецПроцедуры


// См. ОбновлениеИнформационнойБазыДокументооборот.ОбработчикиПервоначальногоЗаполненияЗависящиеОтРедакции
// Позволяет дополнить обработчики первого запуска национальной спецификой.
// Обработчики выполняются только в ЦУ редакции ХОЛД или в редакции КОРП.
Процедура ОбработчикиПервоначальногоЗаполненияЗависящиеОтРедакции() Экспорт

	// Локализация
	Константы.Локализация.Установить(Истина);

	// БЭД
	Константы.ИспользоватьОтложеннуюОтправкуЭлектронныхДокументов.Установить(Ложь);
	Константы.ИспользоватьВнутренниеДокументыЭДО.Установить(Ложь);
	Константы.ОтправлятьВходящиеДокументыНаУтверждение.Установить(Истина);

	МЭДО.ИнициализацияПриПервомЗапуске();
	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда 
		СПАРКРиски.ОбщиеДанныеНачальноеЗаполнениеДанныхСПАРКРиски();
	КонецЕсли;
	// ИнтернетПоддержкаПользователей.СПАРКРиски

	// Заполнение справочника Валюты
	КодыВалют = Новый Массив;
	КодыВалют.Добавить("643");
	КодыВалют.Добавить("840");
	КодыВалют.Добавить("978");
	Обработки.ЗагрузкаКурсовВалют.ДобавитьВалютыПоКоду(КодыВалют);
	
	СправочникСсылка = Справочники.Валюты.НайтиПоКоду("643"); // рубль
	Константы.ОсновнаяВалюта.Установить(СправочникСсылка);
	// Локализация Конец
	
КонецПроцедуры

Процедура ДобавитьСтандартныйОбработчик(Обработчики, Версия) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = СтрЗаменить(Версия, "_", ".");
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыДокументооборотЛокализация.ПерейтиНаВерсию_" 
		+ СтрЗаменить(Версия, ".", "_");
	
КонецПроцедуры

// Параметры:
//  Обработчики		 - ТаблицаЗначений - Таблица с обработчиками обновления
//  Версия			 - Строка - Версия, для которой нужно выполнить обработчик. Указывается в формате "X_X_X_X"
//  ИмяПроцедуры	 - Строка - Имя процедуры, которая будет выполнена при отложенном обновлении.
//                     Если передается только наименование процедуры, то считается, что процедура размещена
//                     в текущем модуле (ОбновлениеИнформационнойБазыДокументооборот).
//  Идентификатор	 - Строка - Уникальный идентификатор обработчика отложенного обновления
//  Комментарий		 - Строка - Описание процедуры, которое отображается в интерфейсе
//
Процедура ДобавитьСтандартныйОтложенныйОбработчик(Обработчики, Версия, ИмяПроцедуры, Идентификатор, Комментарий) Экспорт
	
	СтрокаВерсии = СтрЗаменить(Версия, "_", ".");
	ПараллельноеОтложенноеОбновлениеСВерсии = ОбновлениеИнформационнойБазыДокументооборот.ПараллельноеОтложенноеОбновлениеСВерсии();
	Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СтрокаВерсии, ПараллельноеОтложенноеОбновлениеСВерсии) >= 0 Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Не удалось добавить обработчик для версии %1.
				|С версии %2 включено параллельное отложенное обновление.
				|Метод ДобавитьСтандартныйОтложенныйОбработчик больше вызывать нельзя.'"),
			СтрокаВерсии,
			ПараллельноеОтложенноеОбновлениеСВерсии);
	КонецЕсли;
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = СтрокаВерсии;
	Обработчик.РежимВыполнения = "Отложенно";
	
	Если СтрНайти(ИмяПроцедуры, ".") = 0 Тогда
		Обработчик.Процедура = "ОбновлениеИнформационнойБазыДокументооборотЛокализация." + ИмяПроцедуры;
	Иначе
		Обработчик.Процедура = ИмяПроцедуры;  
	КонецЕсли;
	
	Обработчик.Идентификатор = Новый УникальныйИдентификатор(Идентификатор);
	Обработчик.Комментарий = Комментарий;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Локализация

// Обновляет состояние МЭДО в регистре ДанныеДокументовПредприятия для быстрого показа в списке
// 
// Параметры:
//  Параметры - Структура - Параметры обработчика
Процедура ПерейтиНаВерсию_3_0_11_2(Параметры) Экспорт
	
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(СостоянияСрез.Документ) КАК КолвоДокументов
			|ИЗ
			|	РегистрСведений.СостоянияДокументовМЭДО.СрезПоследних КАК СостоянияСрез
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеДокументовПредприятия КАК ДанныеДокументов
			|		ПО СостоянияСрез.Документ = ДанныеДокументов.Документ
			|ГДЕ
			|	ДанныеДокументов.СостояниеМЭДО = ЗНАЧЕНИЕ(Перечисление.СостоянияДокументовМЭДО.ПустаяСсылка)");
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Параметры.ПрогрессВыполнения.ВсегоОбъектов = Выборка.КолвоДокументов;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбработаноОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	СостоянияСрез.Документ КАК Документ,
		|	СостоянияСрез.Состояние КАК СостояниеМЭДО
		|ИЗ
		|	РегистрСведений.СостоянияДокументовМЭДО.СрезПоследних КАК СостоянияСрез
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеДокументовПредприятия КАК ДанныеДокументов
		|		ПО СостоянияСрез.Документ = ДанныеДокументов.Документ
		|ГДЕ
		|	ДанныеДокументов.СостояниеМЭДО = ЗНАЧЕНИЕ(Перечисление.СостоянияДокументовМЭДО.ПустаяСсылка)");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Попытка
			Делопроизводство.ЗаписатьДанныеДокумента(Выборка.Документ, "СостояниеМЭДО", Выборка.СостояниеМЭДО, Истина);
			ОбработаноОбъектов = ОбработаноОбъектов + 1;
		Исключение
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			СтекОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось заполнить состояние МЭДО для документа %1 по причине:
					|%2'"),
				Выборка.Документ,
				СтекОшибки); 
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Выборка.Документ.Метаданные(),
				Выборка.Документ,
				ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
	Если ОбработаноОбъектов = 0 И ПроблемныхОбъектов > 0 Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Процедуре ПерейтиНаВерсию_3_0_11_2 не удалось записать состояния МЭДО для некоторых документов (пропущены): %1'"),
			ПроблемныхОбъектов);
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = 
		Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ОбработаноОбъектов;
	Параметры.ОбработкаЗавершена = ОбработаноОбъектов = 0;
	
КонецПроцедуры

// Создает табличную часть Пакеты в документах ДанныеДокументаМЭДО
// 
// Параметры:
//  Параметры - Структура - Параметры обработчика
Процедура ПерейтиНаВерсию_3_0_11_3(Параметры) Экспорт
	
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(Шапка.Ссылка) КАК КолвоДокументов
			|ИЗ
			|	Документ.ДанныеДокументаМЭДО КАК Шапка
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДанныеДокументаМЭДО.Пакеты КАК ТЧПакеты
			|		ПО ТЧПакеты.Ссылка = Шапка.Ссылка
			|ГДЕ
			|	ТЧПакеты.Ссылка ЕСТЬ NULL");
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Параметры.ПрогрессВыполнения.ВсегоОбъектов = Выборка.КолвоДокументов;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбработаноОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	Шапка.Ссылка КАК ДанныеМЭДОСсылка
		|ИЗ
		|	Документ.ДанныеДокументаМЭДО КАК Шапка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДанныеДокументаМЭДО.Пакеты КАК ТЧПакеты
		|		ПО ТЧПакеты.Ссылка = Шапка.Ссылка
		|ГДЕ
		|	ТЧПакеты.Ссылка ЕСТЬ NULL");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Попытка
			ДанныеМЭДООбъект = Выборка.ДанныеМЭДОСсылка.ПолучитьОбъект(); // ДокументОбъект.ДанныеДокументаМЭДО
			СтрокаТЧ = ДанныеМЭДООбъект.Пакеты.Добавить();
			СтрокаТЧ.ВерсияМЭДО				= ДанныеМЭДООбъект.УдалитьВерсияМЭДО;
			СтрокаТЧ.ИдентификаторСообщения	= ДанныеМЭДООбъект.УдалитьИдентификаторСообщения;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДанныеМЭДООбъект);
			
			ОбработаноОбъектов = ОбработаноОбъектов + 1;
		Исключение
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			СтекОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось заполнить табличную часть Пакеты для документа %1 по причине:
					|%2'"),
				Выборка.ДанныеМЭДОСсылка,
				СтекОшибки); 
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Выборка.ДанныеМЭДОСсылка.Метаданные(),
				Выборка.ДанныеМЭДОСсылка,
				ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
	Если ОбработаноОбъектов = 0 И ПроблемныхОбъектов > 0 Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Процедуре %1 не удалось заполнить табличную часть Пакеты для некоторых документов (пропущены): %2'"),
			"ПерейтиНаВерсию_3_0_11_3",
			ПроблемныхОбъектов);
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = 
		Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ОбработаноОбъектов;
	Параметры.ОбработкаЗавершена = ОбработаноОбъектов = 0;
	
КонецПроцедуры

// Добавление новых прав на использование СПАРК в полномочие "Ответственные за НСИ"
Процедура ПерейтиНаВерсию_3_0_11_15() Экспорт
	
	Если ОбщегоНазначенияДокументооборот.ЭтоДокументооборотХолдинга()
			И Не ОбщегоНазначенияДокументооборот.ЭтоЦентральныйУзел() Тогда
		Возврат;
	КонецЕсли;
	
	УправлениеДоступомДокументооборот.ДобавитьРольВПолномочия(
		Метаданные.Роли.ПереходВВебВерсиюСПАРК, "7bc7f2da-db69-11de-a544-00179ab398dc");
	УправлениеДоступомДокументооборот.ДобавитьРольВПолномочия(
		Метаданные.Роли.ПросмотрОтчетаДосьеКонтрагента, "7bc7f2da-db69-11de-a544-00179ab398dc");
	УправлениеДоступомДокументооборот.ДобавитьРольВПолномочия(
		Метаданные.Роли.АналитическиеОтчетыСПАРКРиски, "7bc7f2da-db69-11de-a544-00179ab398dc");
	УправлениеДоступомДокументооборот.ДобавитьРольВПолномочия(
		Метаданные.Роли.ЗапросНовойСправкиСПАРКРиски, "7bc7f2da-db69-11de-a544-00179ab398dc");
	
КонецПроцедуры

// Установка новой константы МЭДО в Истина, т.к. старое поведение до этого такое-же, как будто она стояла в Истина
Процедура ПерейтиНаВерсию_3_0_12_4() Экспорт
	
	Константы.АвтоматическиОтправлятьКвитанцииМЭДО.Установить(Истина);
	Константы.ВариантАвтосозданияУведомленийМЭДО.Установить(
		Перечисления.ВариантыАвтосозданияУведомленийМЭДО.НеСоздавать);
	
КонецПроцедуры

Процедура ПерейтиНаВерсию_3_0_12_8() Экспорт
	
	// Регистрация МЧД к обмену.
	УзелАрхив = ПланыОбмена.ОбменНСИСАрхивом.УзелАрхив();
	ПланыОбмена.ЗарегистрироватьИзменения(УзелАрхив,
		Метаданные.Справочники.МашиночитаемыеДоверенностиКонтрагентов);
	ПланыОбмена.ЗарегистрироватьИзменения(УзелАрхив,
		Метаданные.Справочники.МашиночитаемыеДоверенностиОрганизаций);
	
КонецПроцедуры

// Проставляет АктуализироватьВерсию = Истина в РС НастройкиКонтрагентовМЭДО. Т.к. старое поведение такое же, как будто
// это поле равно Истина
// 
// Параметры:
//  Параметры - Структура - Параметры обработчика
Процедура ПерейтиНаВерсию_3_0_12_11(Параметры) Экспорт 
	
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(Настройки.Контрагент) КАК КолвоЗаписей
			|ИЗ
			|	РегистрСведений.НастройкиКонтрагентовМЭДО КАК Настройки
			|ГДЕ
			|	НЕ Настройки.АктуализироватьВерсию");
			
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Параметры.ПрогрессВыполнения.ВсегоОбъектов = Выборка.КолвоЗаписей;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбработаноОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 5000
		|	Настройки.Контрагент
		|ИЗ
		|	РегистрСведений.НастройкиКонтрагентовМЭДО КАК Настройки
		|ГДЕ
		|	НЕ Настройки.АктуализироватьВерсию");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			Набор = РегистрыСведений.НастройкиКонтрагентовМЭДО.СоздатьНаборЗаписей();
			Набор.Отбор.Контрагент.Установить(Выборка.Контрагент);
			Набор.Прочитать();
			Если Набор.Количество() > 0 Тогда
				Запись = Набор[0];
				Запись.АктуализироватьВерсию = Истина;
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
			КонецЕсли;
			
			ОбработаноОбъектов = ОбработаноОбъектов + 1;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			СтекОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось заполнить поле ""%1"" в регистре сведений ""%2"",
					|для контрагента %3, по причине:
					|%4'"),
				"АктуализироватьВерсию", "НастройкиКонтрагентовМЭДО", Выборка.Контрагент, СтекОшибки); 
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Выборка.Контрагент.Метаданные(),
				Выборка.Контрагент,
				ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
	Если ОбработаноОбъектов = 0 И ПроблемныхОбъектов > 0 Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Процедуре %1 не удалось записать РС ""%2"" для некоторых объектов (пропущены): %3'"),
			"ПерейтиНаВерсию_3_0_12_11",
			"НастройкиКонтрагентовМЭДО",
			ПроблемныхОбъектов);
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = 
		Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ОбработаноОбъектов;
	Параметры.ОбработкаЗавершена = ОбработаноОбъектов = 0;
	
КонецПроцедуры

// Устанавливает состояние "Удалено" в РС СостояниеДокументовПоЭДО для бумажных документов, которые еще не сформированы
// 
// Параметры:
//  Параметры - Структура - Параметры обработчика
Процедура ПерейтиНаВерсию_3_0_12_15(Параметры) Экспорт
	
	РегистрыСведений.СостояниеДокументовПоЭДО.УдалитьСостояниеЭДОБумажныхДокументовПриОбновлении(Параметры);
	
КонецПроцедуры

// 1. Заполняет новое поле в РС НастройкиОрганизацийМЭДО
// 2. В настройках пользователей меняет тип настройки для положения рег.штампов и штрихкодов с
// ПеречислениеСсылка.УдалитьВариантыРасположенияШтрихкода на ПеречислениеСсылка.МестаВставкиКартинки
// - обе перезаписи объединены, т.к. это в рамках одной доработки, касающейся регистрационных штампов.
Процедура ПерейтиНаВерсию_3_0_13_10() Экспорт
	
	// п.1:
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Организация
		|ИЗ
		|	РегистрСведений.НастройкиОрганизацийМЭДО КАК Настройки
		|ГДЕ
		|	СтраницаВставкиРегШтампа = &ПустоеЗначение");
	Запрос.УстановитьПараметр("ПустоеЗначение", Перечисления.СтраницаВставкиКартинки.ПустаяСсылка());
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Набор = РегистрыСведений.НастройкиОрганизацийМЭДО.СоздатьНаборЗаписей();
		Набор.Отбор.Организация.Установить(Выборка.Организация);
		Набор.Прочитать();
		Для Каждого Запись Из Набор Цикл
			Запись.СтраницаВставкиРегШтампа = Перечисления.СтраницаВставкиКартинки.Первая;
		КонецЦикла;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
	КонецЦикла;
	
	
	// п.2:
	ПользователиИБ = ПользователиИнформационнойБазы.ПолучитьПользователей();
	Для Каждого ПользовательИБ Из ПользователиИБ Цикл
		Если Не Пользователи.ВходВПрограммуРазрешен(ПользовательИБ) Тогда
			Продолжить;
		КонецЕсли;
		
		// Для рег.номера:
		СохраненноеЗначение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиРегНомера", "ПоложениеНаСтранице", , , ПользовательИБ.Имя);
		НовоеЗначение = НовоеЗначениеПеречисленияПоСтарому(СохраненноеЗначение);
		Если ЗначениеЗаполнено(НовоеЗначение) Тогда
			ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
				"НастройкиРегНомера", "ПоложениеНаСтранице", НовоеЗначение);
		КонецЕсли;
		
		// для штрихкода:
		СохраненноеЗначение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиШтрихкода", "ПоложениеШтрихкодаНаСтранице", , , ПользовательИБ.Имя);
		НовоеЗначение = НовоеЗначениеПеречисленияПоСтарому(СохраненноеЗначение);
		Если ЗначениеЗаполнено(НовоеЗначение) Тогда
			ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
				"НастройкиШтрихкода", "ПоложениеШтрихкодаНаСтранице", НовоеЗначение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПерейтиНаВерсию_3_0_15_2() Экспорт
	
	ОбменЭДОДокументооборот.ИсключитьФайлыИзОбъектовУчетаЭДО();
	
КонецПроцедуры

Процедура ПерейтиНаВерсию_3_0_15_9() Экспорт

	РегистрыСведений.НастройкиОтправкиДокументовПоЭДО.ЗаполнитьИдентификаторыУчетныхЗаписейЭДО();
	РегистрыСведений.НастройкиОтправкиДокументовПоЭДО.ЗаполнитьРеквизитВидаДокументаИспользоватьЭДО();
	
КонецПроцедуры

// Обновляет полномочия для работы с ЭДО
Процедура ПерейтиНаВерсию_3_0_15_15() Экспорт
	
	Если ОбщегоНазначенияДокументооборот.ЭтоДокументооборотХолдинга()
			И Не ОбщегоНазначенияДокументооборот.ЭтоЦентральныйУзел() Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка

		// Создадим новые полномочия для работы с ЭД: чтение, выполнение обмена, настройка, МЧД
		Идентификаторы = Новый Массив;
		Идентификаторы.Добавить("f29fef1f-bac3-460c-a165-07230eac7a6c");
		Идентификаторы.Добавить("bd0ef8cc-f453-4eb0-b77e-398f7990b955");

		Для Каждого Идентификатор Из Идентификаторы Цикл

			Полномочие = Справочники.ПрофилиГруппДоступа.ПоставляемыйПрофильПоИдентификатору(
				Идентификатор);
			Если Полномочие = Неопределено Тогда
				Полномочие = Справочники.ПрофилиГруппДоступа.СоздатьЭлемент();
				Полномочие.ИдентификаторПоставляемыхДанных = Новый УникальныйИдентификатор(Идентификатор);
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(Полномочие);
			КонецЕсли;
			Справочники.ПрофилиГруппДоступа.ЗаполнитьПоставляемыйПрофиль(Полномочие.Ссылка, Ложь);

		КонецЦикла;


		// Удалим старое полномочие "Ответственные за ЭДО" у сотрудников и дадим им новое "Оператор ЭДО"
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ИдентификаторПоставляемыхДанных",
			Новый УникальныйИдентификатор("2be49dd2-337f-4b3a-af9d-17fe9f8325ec"));
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПрофилиГруппДоступа.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ПрофилиГруппДоступа КАК ПрофилиГруппДоступа
			|ГДЕ
			|	ПрофилиГруппДоступа.ИдентификаторПоставляемыхДанных = &ИдентификаторПоставляемыхДанных
			|	И НЕ ПрофилиГруппДоступа.ПометкаУдаления";
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			
			ОтветственныеЗаЭДО = Выборка.Ссылка;

			НаборСтарыхПолномочий = РегистрыСведений.ПолномочияСотрудников.СоздатьНаборЗаписей();
			НаборСтарыхПолномочий.Отбор.Полномочия.Установить(ОтветственныеЗаЭДО);
			НаборСтарыхПолномочий.Прочитать();
			
			Если НаборСтарыхПолномочий.Количество() Тогда
				
				ОператорЭДО = Справочники.ПрофилиГруппДоступа.ПоставляемыйПрофильПоИдентификатору("f29fef1f-bac3-460c-a165-07230eac7a6c"); 
			
				НаборНовыхПолномочий = РегистрыСведений.ПолномочияСотрудников.СоздатьНаборЗаписей();
				НаборНовыхПолномочий.Отбор.Полномочия.Установить(ОператорЭДО);
				Для Каждого Запись Из НаборСтарыхПолномочий Цикл
					НоваяЗапись = НаборНовыхПолномочий.Добавить();
					НоваяЗапись.Полномочия = ОператорЭДО;
					НоваяЗапись.Владелец = Запись.Владелец;
				КонецЦикла;
				
				НаборНовыхПолномочий.Записать();
				
			КонецЕсли;
			
			НаборСтарыхПолномочий.Очистить();
			НаборСтарыхПолномочий.Записать();
			
			ОтветственныеЗаЭДООбъект = ОтветственныеЗаЭДО.ПолучитьОбъект();
			ОтветственныеЗаЭДООбъект.Наименование = СтрШаблон(НСтр("ru='%1 (не используется)'"),
				ОтветственныеЗаЭДООбъект.Наименование);
			ОтветственныеЗаЭДООбъект.Записать();
			ОтветственныеЗаЭДООбъект.УстановитьПометкуУдаления(Истина);
			
		КонецЕсли;
		
		
		// Обновим полномочие "Ответственные за МЭДО"
		УправлениеДоступомДокументооборот.УдалитьРольИзПолномочий("БазовыеПраваЭД",
			"4a3c2c1a-3290-11ed-9ec2-4cedfb9508c3");
		УправлениеДоступомДокументооборот.ДобавитьРольВПолномочия("БазовыеПраваЭДДокументооборот",
			"4a3c2c1a-3290-11ed-9ec2-4cedfb9508c3");
		УправлениеДоступомДокументооборот.УдалитьРольИзПолномочий("УдалитьВыполнениеОбменаЭД",
			"4a3c2c1a-3290-11ed-9ec2-4cedfb9508c3");
		УправлениеДоступомДокументооборот.УдалитьРольИзПолномочий("УдалитьЧтениеЭД",
			"4a3c2c1a-3290-11ed-9ec2-4cedfb9508c3");
		УправлениеДоступомДокументооборот.УдалитьРольИзПолномочий("УдалитьНастройкаПараметровЭД",
			"4a3c2c1a-3290-11ed-9ec2-4cedfb9508c3");
		
		
		// Полномочие "Пользователи"
		УправлениеДоступомДокументооборот.УдалитьРольИзПолномочий("БазовыеПраваЭД",
			"c999acb1-d5f5-11de-a544-00179ab398dc");
		УправлениеДоступомДокументооборот.ДобавитьРольВПолномочия("БазовыеПраваЭДДокументооборот",
			"c999acb1-d5f5-11de-a544-00179ab398dc");
		УправлениеДоступомДокументооборот.ДобавитьРольВПолномочия("ИспользованиеУведомленийЭДО",
			"c999acb1-d5f5-11de-a544-00179ab398dc");
		УправлениеДоступомДокументооборот.ДобавитьРольВПолномочия("ЧтениеЭлектронныхДокументовПоДокументамДО",
			"c999acb1-d5f5-11de-a544-00179ab398dc");
		УправлениеДоступомДокументооборот.ДобавитьРольВПолномочия("ЧтениеМаршрутовПодписанияДокументооборот",
			"c999acb1-d5f5-11de-a544-00179ab398dc");
		УправлениеДоступомДокументооборот.ДобавитьРольВПолномочия("ЧтениеМЧДЭДОДокументооборот",
			"c999acb1-d5f5-11de-a544-00179ab398dc");
		УправлениеДоступомДокументооборот.ДобавитьРольВПолномочия("ЧтениеНастроекОбменаСКонтрагентамиДокументооборот",
			"c999acb1-d5f5-11de-a544-00179ab398dc");
		УправлениеДоступомДокументооборот.ДобавитьРольВПолномочия("ЧтениеНастроекЭлектронногоВзаимодействия",
			"c999acb1-d5f5-11de-a544-00179ab398dc");
		УправлениеДоступомДокументооборот.ДобавитьРольВПолномочия("ЧтениеНоменклатурыКонтрагентовБЭД",
			"c999acb1-d5f5-11de-a544-00179ab398dc");
		
		
		// Определим права по дескрипторам для объектов ЭДО
		ДокументооборотПраваДоступа.ОпределитьПраваОбъектовТаблицы(
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Документы.ЭлектронныйДокументВходящийЭДО));
		ДокументооборотПраваДоступа.ОпределитьПраваОбъектовТаблицы(
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Документы.ЭлектронныйДокументИсходящийЭДО));
		ДокументооборотПраваДоступа.ОпределитьПраваОбъектовТаблицы(
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Документы.ТранспортныйКонтейнерЭДО));

		ЗафиксироватьТранзакцию();
		
	Исключение

		ОтменитьТранзакцию();
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Процедуре ""ПерейтиНаВерсию_3_0_15_15"" не удалось обновить полномочия для работы с ЭД по причине:
			|%1'"), ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

	КонецПопытки;
	
КонецПроцедуры

Процедура ПерейтиНаВерсию_3_0_15_20() Экспорт
	
	Если ОбщегоНазначенияДокументооборот.ЭтоДокументооборотХолдинга()
			И Не ОбщегоНазначенияДокументооборот.ЭтоЦентральныйУзел() Тогда
			
		Возврат;
	КонецЕсли;
	
	ОбменЭДОДокументооборот.ТрансформироватьПравилаУчетаЭДДОВПравилаАвтоматическогоСозданияОбъектов();
	
КонецПроцедуры

Процедура ПерейтиНаВерсию_3_0_15_21(Параметры) Экспорт
	
	ОбменЭДОДокументооборот.ОбновитьСостоянияЭДОВДанныхДокументов(Параметры);
	
КонецПроцедуры

// Заменяет файлы на документы в состояниях объектов учета ЭДО
Процедура ПерейтиНаВерсию_3_0_15_36() Экспорт
	
	ОбменЭДОДокументооборот.ИсключитьФайлыИзВСостоянияхОбъектовУчетаЭДО();
	
КонецПроцедуры

// Устанавливает реквизит НеПредлагатьСохранятьНастройки РС НастройкиПолученияЭлектронныхДокументов Истина
// 
// Параметры:
//  Параметры - Структура - Параметры обработчика:
//   * ПрогрессВыполнения - Структура:
//      ** ОбработаноОбъектов - Число.
//      ** ВсегоОбъектов - Число.
//
Процедура ПерейтиНаВерсию_3_0_15_38(Параметры) Экспорт
	
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(НастройкиПолученияЭлектронныхДокументов.Получатель) КАК ВсегоОбъектов
		|ИЗ
		|	РегистрСведений.НастройкиПолученияЭлектронныхДокументов КАК НастройкиПолученияЭлектронныхДокументов
		|ГДЕ
		|	НастройкиПолученияЭлектронныхДокументов.НеПредлагатьСохранятьНастройки = ЛОЖЬ");
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Параметры.ПрогрессВыполнения.ВсегоОбъектов = Выборка.ВсегоОбъектов;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбработаноОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	НастройкиПолученияЭлектронныхДокументов.Получатель КАК Получатель,
	|	НастройкиПолученияЭлектронныхДокументов.Отправитель КАК Отправитель,
	|	НастройкиПолученияЭлектронныхДокументов.ИдентификаторОтправителя КАК ИдентификаторОтправителя,
	|	НастройкиПолученияЭлектронныхДокументов.ИдентификаторПолучателя КАК ИдентификаторПолучателя,
	|	НастройкиПолученияЭлектронныхДокументов.ВидДокумента КАК ВидДокумента
	|ИЗ
	|	РегистрСведений.НастройкиПолученияЭлектронныхДокументов КАК НастройкиПолученияЭлектронныхДокументов
	|ГДЕ
	|	НастройкиПолученияЭлектронныхДокументов.НеПредлагатьСохранятьНастройки = ЛОЖЬ");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Набор = РегистрыСведений.НастройкиПолученияЭлектронныхДокументов.СоздатьНаборЗаписей();
			Набор.Отбор.Получатель.Установить(Выборка.Получатель);
			Набор.Отбор.Отправитель.Установить(Выборка.Отправитель);        
			Набор.Отбор.ИдентификаторОтправителя.Установить(Выборка.ИдентификаторОтправителя);
			Набор.Отбор.ИдентификаторПолучателя.Установить(Выборка.ИдентификаторПолучателя);
			Набор.Отбор.ВидДокумента.Установить(Выборка.ВидДокумента);
			Набор.Прочитать();
			
			Для Каждого Стр Из Набор Цикл
				Стр.НеПредлагатьСохранятьНастройки = Истина;
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось установить реквизит НеПредлагатьСохранятьНастройки в настройке %1-%2 по причине:
				|%3'"),
				Выборка.Получатель, Выборка.Отправитель,
				ПодробноеПредставлениеОшибки); 
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбработаноОбъектов = 0 И ПроблемныхОбъектов > 0 Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Процедуре ПерейтиНаВерсию_3_0_15_38 не удалось установить реквизит НеПредлагатьСохранятьНастройки: %1'"),
			ПроблемныхОбъектов);
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = 
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ОбработаноОбъектов;
	Параметры.ОбработкаЗавершена = (ОбработаноОбъектов = 0);
	
КонецПроцедуры

Процедура ПерейтиНаВерсию_3_0_17_27() Экспорт
	
	ОбновлениеИнформационнойБазыДокументооборот.УстановитьЗначениеКонстанты("Локализация", Истина);
	
КонецПроцедуры

Функция НовоеЗначениеПеречисленияПоСтарому(СтароеЗначение)
	
	НовоеЗначение = Неопределено;
	Если ТипЗнч(СтароеЗначение) = Тип("ПеречислениеСсылка.УдалитьВариантыРасположенияШтрихкода") Тогда
		Если СтароеЗначение = Перечисления.УдалитьВариантыРасположенияШтрихкода.ЛевыйВерхний Тогда
			НовоеЗначение = Перечисления.МестаВставкиКартинки.ЛевыйВерхний;
		ИначеЕсли СтароеЗначение = Перечисления.УдалитьВариантыРасположенияШтрихкода.ЛевыйНижний Тогда 
			НовоеЗначение = Перечисления.МестаВставкиКартинки.ЛевыйНижний;
		ИначеЕсли СтароеЗначение = Перечисления.УдалитьВариантыРасположенияШтрихкода.ПравыйВерхний Тогда 
			НовоеЗначение = Перечисления.МестаВставкиКартинки.ПравыйВерхний;
		ИначеЕсли СтароеЗначение = Перечисления.УдалитьВариантыРасположенияШтрихкода.ПравыйНижний Тогда 
			НовоеЗначение = Перечисления.МестаВставкиКартинки.ПравыйНижний;
		ИначеЕсли СтароеЗначение = Перечисления.УдалитьВариантыРасположенияШтрихкода.ПроизвольноеПоложение Тогда 
			НовоеЗначение = Перечисления.МестаВставкиКартинки.ПроизвольноеПоложение;
		КонецЕсли;
	КонецЕсли;
	Возврат НовоеЗначение;
	
КонецФункции

Процедура ПерейтиНаВерсию_3_0_18_10() Экспорт

	Если Не МультиязычностьСервер.ИспользуетсяДополнительныйЯзык(1) Тогда
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	КлассификаторЕдиницИзмерения.Ссылка КАК Ссылка,
		|	КлассификаторЕдиницИзмерения.Код КАК Код
		|ИЗ
		|	Справочник.КлассификаторЕдиницИзмерения КАК КлассификаторЕдиницИзмерения
		|ГДЕ
		|	НЕ КлассификаторЕдиницИзмерения.ПометкаУдаления
		|	И КлассификаторЕдиницИзмерения.НаименованиеЯзык1 = """"";
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Пока РезультатЗапроса.Следующий() Цикл
		
		Сведения = Справочники.КлассификаторЕдиницИзмерения.СведенияОЕдиницеИзмеренияИзКлассификатора(
			РезультатЗапроса.Код);
		
		ОбъектСправочника = РезультатЗапроса.Ссылка.ПолучитьОбъект();
		НужноЗаписать = Ложь;
		
		Если МультиязычностьСервер.КодДополнительногоЯзыкаИнформационнойБазы(1) = "en" Тогда
			
			ОбъектСправочника.НаименованиеЯзык1 = ?(ЗначениеЗаполнено(Сведения.МеждународноеНаименование), 
				Сведения.МеждународноеНаименование, ОбъектСправочника.Наименование);
			НужноЗаписать = Истина;
		
		ИначеЕсли МультиязычностьСервер.СведенияОЯзыках().ОсновнойЯзык = "en" Тогда
				
			Если ЗначениеЗаполнено(
				Сведения.МеждународноеНаименование) И ОбъектСправочника.Наименование = Сведения.Наименование Тогда
				ОбъектСправочника.Наименование = Сведения.МеждународноеНаименование;
			КонецЕсли;
			
			ОбъектСправочника.НаименованиеЯзык1 = ?(ЗначениеЗаполнено(Сведения.Наименование),
				Сведения.Наименование, ОбъектСправочника.Наименование);
			НужноЗаписать = Истина;	
		
		КонецЕсли;
		
		Если НужноЗаписать Тогда
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ОбъектСправочника);
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

// Локализация Конец

#КонецОбласти