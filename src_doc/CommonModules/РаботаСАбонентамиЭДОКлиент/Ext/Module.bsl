// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

// Параметры:
//  ИдентификаторЭДО - Строка
//  ОписаниеОповещения - ОписаниеОповещения - обработчик, который должен быть вызван после обновления данных абонента:
//  * РезультатОбновления - Структура:
//  ** Успех - Булево
//  ** КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//
Процедура ОбновитьДанныеАбонентаПоИдентификатору(ИдентификаторЭДО, ОписаниеОповещения) Экспорт
	
	ДлительнаяОперация = РаботаСАбонентамиЭДОВызовСервера.ОбновитьДанныеАбонентаПоИдентификатору(
		ИдентификаторЭДО);
	
	ПараметрыОповещения = Новый Структура("ИдентификаторЭДО", ИдентификаторЭДО);
	ПараметрыОповещения = Новый Структура("ОписаниеОповещения", ОписаниеОповещения);
		
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		ОбновитьДанныеАбонентаПоИдентификаторуПослеОжидания(ДлительнаяОперация, ПараметрыОповещения);
		Возврат;
	КонецЕсли;
	
	ОбработчикЗавершения = Новый ОписаниеОповещения("ОбновитьДанныеАбонентаПоИдентификаторуПослеОжидания",
		РаботаСАбонентамиЭДОКлиент, ПараметрыОповещения);
		
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОбработчикЗавершения, ПараметрыОжидания);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  РезультатДлительнойОперации - См. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
//  ПараметрыОповещения - Структура:
//  * ИдентификаторЭДО - Строка
//  * ОписаниеОповещения - ОписаниеОповещения - обработчик, который должен быть вызван после обновления данных абонента:
//  ** РезультатОбновления - Структура:
//  *** Успех - Булево
//  *** КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  ** ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//
Процедура ОбновитьДанныеАбонентаПоИдентификаторуПослеОжидания(РезультатДлительнойОперации,
	ПараметрыОповещения) Экспорт
	
	КонтекстДиагностики = ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики();
	
	РезультатОбновления = ОбщегоНазначенияБЭДКлиент.РезультатВыполненияФоновогоЗадания(
		НСтр("ru = 'Обновление данных абонента ЭДО'"),
		РезультатДлительнойОперации, КонтекстДиагностики); // См. ПакетыДокументовЭДО.НовыйРезультатДействийСПакетом
		
	ОбработкаНеисправностейБЭДКлиент.ДополнитьКонтекстДиагностики(КонтекстДиагностики,
		РезультатОбновления.КонтекстДиагностики);
	
	ВыполнитьОбработкуОповещения(ПараметрыОповещения.ОписаниеОповещения, РезультатОбновления);
	
КонецПроцедуры

#КонецОбласти
