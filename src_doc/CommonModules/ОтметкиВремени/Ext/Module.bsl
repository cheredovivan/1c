
// @strict-types

#Область ПрограммныйИнтерфейс

// Возвращает текущую отметку времени.
//
// Возвращаемое значение:
//	Число - Текущая отметка времени.
//
Функция Текущая() Экспорт
	
	Возврат ТекущаяУниверсальнаяДатаВМиллисекундах();
	
КонецФункции

// Устанавливает идентификаторы записей набора независимого регистра сведений.
//
// Параметры:
//	Объект - РегистрСведенийНаборЗаписей - Набор записей, котором необходимо установить идентификаторы записей.
//
Процедура ЗаполнитьИдентификаторыНабораЗаписей(Объект) Экспорт
	
	МенеджерОбъекта = ОтметкиВремениПовтИсп.МенеджерОбъектаПоИдентификатору(
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Объект))); // РегистрСведенийМенеджер
	КлючевойНабор = МенеджерОбъекта.СоздатьНаборЗаписей(); // РегистрСведенийНаборЗаписей
	
	Для Каждого Запись Из Объект Цикл // РегистрСведенийЗапись
		Для Каждого Поле Из КлючевойНабор.Отбор Цикл // ЭлементОтбора
			Поле.Установить(Запись[Поле.Имя], Истина);
		КонецЦикла;
		
		Запись.ИдентификаторОтметкиВремени = ИдентификаторНабораЗаписей(КлючевойНабор);
	КонецЦикла;
	
КонецПроцедуры

// Отключает регистрацию отметок для указанного объекта.
// 
// Параметры:
//	Объект - РегистрСведенийНаборЗаписей - Объект, для которого нужно отключить регистрацию отметок времени.
//
Процедура ОтключитьРегистрацию(Объект) Экспорт
	
	Объект.ОбменДанными.Загрузка = Истина;
	Объект.ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов", Истина);
	Объект.ДополнительныеСвойства.Вставить("ПереходОтключитьМеханизмРегистрации", Истина);
	
КонецПроцедуры

// Возвращает вид ключа отметок времени для объекта метаданных.
// 
// Параметры:
//	ИдентификаторОбъекта - СправочникСсылка.ИдентификаторыОбъектовМетаданных -
//						 - СправочникСсылка.ИдентификаторыОбъектовРасширений - Объект-источник отметок времени, тип
//																			   ключа которого необходимо определить.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.ВидыКлючейОтметокВремени - Вид ключа объекта.
//
Функция ВидКлючаОбъекта(ИдентификаторОбъекта) Экспорт
	
	ОбъектМетаданных = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(ИдентификаторОбъекта); // ОбъектМетаданных
	
	Если ОбщегоНазначения.ЭтоКонстанта(ОбъектМетаданных) Тогда
		Возврат Перечисления.ВидыКлючейОтметокВремени.КлючЗначенияКонстанты;
	ИначеЕсли ОбщегоНазначения.ЭтоОбъектСсылочногоТипа(ОбъектМетаданных) Тогда
		Возврат Перечисления.ВидыКлючейОтметокВремени.КлючСсылочногоОбъекта;
	ИначеЕсли ОбщегоНазначения.ЭтоРегистрСведений(ОбъектМетаданных) Тогда
		//@skip-check property-return-type
		Если ОбъектМетаданных.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору Тогда
			Возврат Перечисления.ВидыКлючейОтметокВремени.КлючНабораПодчинениеРегистратору;
		КонецЕсли;
		
		//@skip-check property-return-type
		Измерения = ОбъектМетаданных.Измерения; // КоллекцияОбъектовМетаданных
		//@skip-check property-return-type
		ОтборПоПериоду = ОбъектМетаданных.ОсновнойОтборПоПериоду; // Булево
		
		Если Измерения.Количество() = 0 И ОтборПоПериоду <> Истина Тогда
			Возврат Перечисления.ВидыКлючейОтметокВремени.КлючНабораРегистрБезИзмерений;
		КонецЕсли;
		
		ЭлементСостава =
			Метаданные.ОбщиеРеквизиты.ИдентификаторОтметкиВремени.Состав.Найти(ОбъектМетаданных); // ЭлементСоставаОбщегоРеквизита
		Если ЭлементСостава <> Неопределено
				И ЭлементСостава.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Использовать Тогда
			Возврат Перечисления.ВидыКлючейОтметокВремени.КлючНабораИдентификаторНабораЗаписей;
		КонецЕсли;
		
		//@skip-check invocation-parameter-type-intersect
		//@skip-check property-return-type
		Если Измерения.Количество() > 0 И ЭтоСсылочныйТипЗначения(Измерения[0].Тип) Тогда
			Возврат Перечисления.ВидыКлючейОтметокВремени.КлючНабораПервоеСсылочноеИзмерение;
		КонецЕсли;
	ИначеЕсли ОбщегоНазначения.ЭтоРегистрНакопления(ОбъектМетаданных)
			Или ОбщегоНазначения.ЭтоРегистрБухгалтерии(ОбъектМетаданных)
			Или ОбщегоНазначения.ЭтоРегистрРасчета(ОбъектМетаданных) Тогда
		
		Возврат Перечисления.ВидыКлючейОтметокВремени.КлючНабораПодчинениеРегистратору;
	КонецЕсли;

	ВызватьИсключение СтрШаблон("%1 %2 ""%3""",
		НСтр("ru = 'Не удалось определить вид ключа отметок времени'", ОбщегоНазначения.КодОсновногоЯзыка()),
		НСтр("ru = 'для'", ОбщегоНазначения.КодОсновногоЯзыка()),
		ИдентификаторОбъекта);

КонецФункции

// Возвращает имя поля ключа объекта.
//
// Параметры:
//	ИдентификаторОбъекта - СправочникСсылка.ИдентификаторыОбъектовМетаданных - 
//						 - СправочникСсылка.ИдентификаторыОбъектовРасширений - Объект-источник отметок времени, ключевое
//																			   поле которого необходимо определить.
//
// Возвращаемое значение:
//	Строка - Ключевое поле объекта.
//
Функция ПолеКлючаОбъекта(ИдентификаторОбъекта) Экспорт
	
	ВидКлюча = ОтметкиВремениПовтИсп.ВидКлючаОбъекта(ИдентификаторОбъекта);
	
	Если ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючСсылочногоОбъекта Тогда
		Возврат ИмяПоляХраненияКлючейСсылочныхОбъектов();
	ИначеЕсли ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораПодчинениеРегистратору Тогда
		Возврат ИмяПоляХраненияКлючейНаборовСПодчинениемРегистратору();
	ИначеЕсли ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораПервоеСсылочноеИзмерение Тогда
		Возврат ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(ИдентификаторОбъекта).Измерения[0].Имя; //@skip-check property-return-type
	ИначеЕсли ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораИдентификаторНабораЗаписей Тогда
		Возврат ИмяПоляХраненияКлючейВидаИдентификаторНабораЗаписей();
	ИначеЕсли ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораРегистрБезИзмерений Тогда
		Возврат "*";
	ИначеЕсли ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючЗначенияКонстанты Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат "-";
	
КонецФункции

// Возвращает тип поля ключа объекта.
//
// Параметры:
//	ИдентификаторОбъекта - СправочникСсылка.ИдентификаторыОбъектовМетаданных - 
//						 - СправочникСсылка.ИдентификаторыОбъектовРасширений - Объект-источник отметок времени, тип ключевого
//																			   поля которого необходимо определить.
//
// Возвращаемое значение:
//	ОписаниеТипов - Тип ключа объекта.
//
Функция ТипКлючаОбъекта(ИдентификаторОбъекта) Экспорт
	
	ВидКлюча = ОтметкиВремениПовтИсп.ВидКлючаОбъекта(ИдентификаторОбъекта);
	ОбъектМетаданных = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(ИдентификаторОбъекта); // ОбъектМетаданныхСправочник, ОбъектМетаданныхРегистрСведений, ОбъектМетаданныхКонстанта
	
	Если ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючСсылочногоОбъекта Тогда
		Возврат ОбъектМетаданных.СтандартныеРеквизиты.Ссылка.Тип;
	ИначеЕсли ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораПодчинениеРегистратору Тогда
		Возврат ОбъектМетаданных.СтандартныеРеквизиты.Регистратор.Тип;
	ИначеЕсли ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораПервоеСсылочноеИзмерение Тогда
		Возврат ОбъектМетаданных.Измерения[0].Тип;
	ИначеЕсли ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораИдентификаторНабораЗаписей Тогда
		Возврат Метаданные.ОбщиеРеквизиты.ИдентификаторОтметкиВремени.Тип;
	ИначеЕсли ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючЗначенияКонстанты Тогда
		Возврат ОбъектМетаданных.Тип;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает идентификатор набора записей.
//
// Параметры:
//	Объект - РегистрСведенийНаборЗаписей - Объект, для которого необходимо получить идентификатор.
//
// Возвращаемое значение:
//	УникальныйИдентификатор - Идентификатор набора записей.
//
Функция ИдентификаторНабораЗаписей(Объект) Экспорт
	
	Хеширование = Новый ХешированиеДанных(ХешФункция.MD5);
	Хеширование.Добавить(XMLСтрока(Новый ХранилищеЗначения(Объект.Отбор, Новый СжатиеДанных(0))));
	Хеш = СтрЗаменить(Строка(Хеширование.ХешСумма), " ", "");
	Хеширование = Неопределено;
	
	Возврат Новый УникальныйИдентификатор(СтрШаблон("%1-%2-%3-%4-%5",
		Лев(Хеш, 8), Сред(Хеш, 9, 4), Сред(Хеш, 13, 4), Сред(Хеш, 17, 4), Сред(Хеш, 21)));
	
КонецФункции

// Возвращает описание ключа отметки времени.
//
// Параметры:
//	Объект - ОпределяемыйТип.ОтметкиВремениКонстанты -
//		   - ОпределяемыйТип.ОтметкиВремениСсылочныеОбъекты -
//		   - ОпределяемыйТип.ОтметкиВремениРегистры
//		   - ЛюбаяСсылка - Объект, для которого необходимо получить ключ.
//
// Возвращаемое значение:
//	Структура:
//		* ИдентификаторКлюча - УникальныйИдентификатор - Идентификатор ключа.
//		* ТипКлюча - СправочникСсылка.ИдентификаторыОбъектовМетаданных - Тип ключа отметки времени.
//				   - СправочникСсылка.ИдентификаторыОбъектовРасширений -
//				   - Неопределено -
//		* ВидКлюча - ПеречислениеСсылка.ВидыКлючейОтметокВремени - Вид ключа отметки времени.
//				   - Неопределено
//
Функция ОписаниеКлючаОтметкиВремени(Объект) Экспорт
	
	ИдентификаторОбъекта = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Объект));
	ВидКлюча = ОтметкиВремениПовтИсп.ВидКлючаОбъекта(ИдентификаторОбъекта);
	
	ОписаниеКлюча = Новый Структура(
		"ИдентификаторКлюча, ТипКлюча, ВидКлюча",
		ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор(), Неопределено, ВидКлюча);
		
	Если ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючЗначенияКонстанты
			Или ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораРегистрБезИзмерений Тогда
		
		Возврат ОписаниеКлюча;
	ИначеЕсли ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючСсылочногоОбъекта Тогда
		Если ОбщегоНазначения.ЗначениеСсылочногоТипа(Объект) Тогда
			ОписаниеКлюча.ИдентификаторКлюча = Объект.УникальныйИдентификатор();
			ОписаниеКлюча.ТипКлюча = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Объект));
		Иначе
			ОписаниеКлюча.ИдентификаторКлюча = Объект.Ссылка.УникальныйИдентификатор();
			ОписаниеКлюча.ТипКлюча = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Объект.Ссылка));
		КонецЕсли;
	ИначеЕсли ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораПодчинениеРегистратору Тогда
		ПолеКлюча = ОтметкиВремениПовтИсп.ПолеКлючаОбъекта(ИдентификаторОбъекта);
		Ключ = Объект.Отбор[ПолеКлюча].Значение; // ЛюбаяСсылка
		
		ПроверитьЗначениеКлючаНабораЗаписей(Ключ, ИдентификаторОбъекта, Объект.Отбор, ПолеКлюча);
		
		ОписаниеКлюча.ИдентификаторКлюча = Ключ.УникальныйИдентификатор();
		ОписаниеКлюча.ТипКлюча = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Ключ));
	ИначеЕсли ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораПервоеСсылочноеИзмерение Тогда
		ПолеКлюча = ОтметкиВремениПовтИсп.ПолеКлючаОбъекта(ИдентификаторОбъекта);
		
		Ключ = Неопределено; // ЛюбаяСсылка, Неопределено
		Если Объект.Отбор[ПолеКлюча].Использование Тогда
			Ключ = Объект.Отбор[ПолеКлюча].Значение;
		ИначеЕсли Объект.Количество() = 1 Тогда
			Ключ = Объект[0][ПолеКлюча]; //@skip-check statement-type-change
		ИначеЕсли Объект.Количество() > 1 Тогда
			КлючиИсточника = Объект.Выгрузить(, ПолеКлюча);
			КлючиИсточника.Свернуть(ПолеКлюча);
			
			Ключ = КлючиИсточника[0][0];
		КонецЕсли;
		
		ПроверитьЗначениеКлючаНабораЗаписей(Ключ, ИдентификаторОбъекта, Объект.Отбор, ПолеКлюча);
		
		ОписаниеКлюча.ИдентификаторКлюча = Ключ.УникальныйИдентификатор(); // УникальныйИдентификатор
		ОписаниеКлюча.ТипКлюча = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Ключ));
	ИначеЕсли ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораИдентификаторНабораЗаписей Тогда
		ИдентификаторКлюча = Неопределено; // УникальныйИдентификатор
		Если Объект.Количество() = 0 Тогда
			ИдентификаторКлюча = ИдентификаторНабораЗаписей(Объект);
		Иначе
			Если Не ЗначениеЗаполнено(Объект[0][ИмяПоляХраненияКлючейВидаИдентификаторНабораЗаписей()]) Тогда
				ЗаполнитьИдентификаторыНабораЗаписей(Объект);
			КонецЕсли;
			
			ИдентификаторКлюча = Объект[0][ИмяПоляХраненияКлючейВидаИдентификаторНабораЗаписей()]; // УникальныйИдентификатор
		КонецЕсли;
		
		ОписаниеКлюча.ИдентификаторКлюча = ИдентификаторКлюча;
		ОписаниеКлюча.ТипКлюча = Неопределено;
	Иначе
		ВызватьИсключение СтрШаблон("%1 %2 ""%3""",
			НСтр("ru = 'Не удалось определить вид ключа отметок времени'", ОбщегоНазначения.КодОсновногоЯзыка()),
			НСтр("ru = 'для'", ОбщегоНазначения.КодОсновногоЯзыка()),
			ИдентификаторОбъекта);
	КонецЕсли;
	
	Возврат ОписаниеКлюча;
	
КонецФункции

// Возвращает значение ключа отметки времени по его описанию (для ссылочных объектов).
//
// Параметры:
//	ОписаниеКлюча - см. ОписаниеКлючаОтметкиВремени
//
// Возвращаемое значение:
//	ЛюбаяСсылка, Неопределено - Значение ключа.
//
Функция ЗначениеСсылкиПоОписаниюКлюча(ОписаниеКлюча) Экспорт
	
	Если Не ОписаниеКлюча.ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючСсылочногоОбъекта
			И Не ОписаниеКлюча.ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораПодчинениеРегистратору
			И Не ОписаниеКлюча.ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораПервоеСсылочноеИзмерение Тогда
			
		Возврат Неопределено;
	КонецЕсли;
			
	Возврат ЗначениеСсылкиПоКлючуОтметкиВремени(ОписаниеКлюча.ИдентификаторКлюча, ОписаниеКлюча.ТипКлюча);
	
КонецФункции

// Возвращает значение ключа отметки времени.
//
// Параметры:
//	ИдентификаторКлюча - УникальныйИдентификатор - Идентификатор ключа отметки времени.
//	ТипКлюча - СправочникСсылка.ИдентификаторыОбъектовМетаданных -
//			 - СправочникСсылка.ИдентификаторыОбъектовРасширений - Тип ключа отметки времени.
//
// Возвращаемое значение:
//	ЛюбаяСсылка - Значение ключа.
//
Функция ЗначениеСсылкиПоКлючуОтметкиВремени(ИдентификаторКлюча, ТипКлюча) Экспорт
	
	Возврат ОтметкиВремениПовтИсп.МенеджерОбъектаПоИдентификатору(ТипКлюча).ПолучитьСсылку(ИдентификаторКлюча);
	
КонецФункции

// Преобразует дату в отметку времени.
//
// Параметры:
//	Дата - Дата - Дата, которую необходимо преобразовать.
//	УчитыватьЧасовойПояс - Булево - Признак необходимости учета часового пояса при расчете отметки времени.
//
// Возвращаемое значение:
//	Число - Отметка времени.
//
Функция ДатаВОтметкуВремени(Дата, УчитыватьЧасовойПояс = Ложь) Экспорт
	
	Возврат (?(УчитыватьЧасовойПояс, УниверсальноеВремя(Дата, ЧасовойПоясСеанса()), Дата) - Дата(1, 1, 1)) * 1000;
	
КонецФункции

// Возвращает представление отметки времени, совместимое с определяемым типом ОтметкаВремени.
//
// Параметры:
//	Отметка - Число - Отметка времени, которую необходимо преобразовать.
//
// Возвращаемое значение:
//	Строка - Представление отметки времени.
//
Функция ПредставлениеОтметки(Отметка) Экспорт
	
	ОтметкаДатой = УниверсальнаяДатаПоОтметкеВремени(Отметка); // см. УниверсальнаяДатаПоОтметкеВремени
	Миллисекунд = Отметка % 1000;
	
	Возврат
		Формат(ОтметкаДатой, "ДФ=yyyyMMddHHmmss") + Формат(Миллисекунд, "ЧЦ=3; ЧН=000; ЧВН=; ЧГ=0");
	
КонецФункции

// Возвращает универсальную дату, соответствующую отметке времени.
//
// Параметры:
//	ОтметкаВремени - Число - Отметка времени.
//
// Возвращаемое значение:
//	Дата - Универсальные дата и время.
//
Функция УниверсальнаяДатаПоОтметкеВремени(ОтметкаВремени) Экспорт
	
	Возврат Дата(1, 1, 1) + Цел(ОтметкаВремени / 1000);
	
КонецФункции

// Возвращает дату, соответствующую отметке времени (в часовом поясе сеанса).
//
// Параметры:
//	ОтметкаВремени - Число - Отметка времени.
//
// Возвращаемое значение:
//	Дата - Дата и время в часовом поясе сеанса.
//
Функция ДатаПоОтметкеВремени(ОтметкаВремени) Экспорт
	
	УниверсальнаяДата = УниверсальнаяДатаПоОтметкеВремени(ОтметкаВремени);
	РазностьДат = ТекущаяДатаСеанса() - ТекущаяУниверсальнаяДата();
	Дата = УниверсальнаяДата + РазностьДат;
	
	Возврат Дата;
	
КонецФункции

// Возвращает в виде структуры отбор набора записей, для которого была зарегистрирована отметка времени.
//
// Параметры:
//  Хранилище - ХранилищеЗначения - Хранилище, содержащее отбор.
//
// Возвращаемое значение:
//	Структура, Неопределено - Значение отбора.
//
Функция ЗначенияКлюча(Хранилище) Экспорт
	
	Попытка
		Отбор = Хранилище.Получить(); // Отбор
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	Если Отбор = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Значения = Новый Структура();
	
	Для Каждого Поле Из Отбор Цикл // ЭлементОтбора
		Если Поле.Использование Тогда
			Значения.Вставить(Поле.Имя, Поле.Значение);
		КонецЕсли;
	КонецЦикла;
	Отбор = Неопределено;
	
	Возврат Значения;
	
КонецФункции

// Возвращает менеджер объекта метаданных по его идентификатору.
//
// Параметры:
//	Идентификатор - СправочникСсылка.ИдентификаторыОбъектовМетаданных -
//				  - СправочникСсылка.ИдентификаторыОбъектовРасширений - Идентификатор объекта метаданных.
//
// Возвращаемое значение:
//	СправочникМенеджер
//	ДокументМенеджер
//	БизнесПроцессМенеджер
//	ЗадачаМенеджер
//	ПланВидовХарактеристикМенеджер
//	Неопределено
//
Функция МенеджерОбъектаПоИдентификатору(Идентификатор) Экспорт
	
	Если Не ТипЗнч(Идентификатор) = Тип("СправочникСсылка.ИдентификаторыОбъектовМетаданных")
			И Не ТипЗнч(Идентификатор) = Тип("СправочникСсылка.ИдентификаторыОбъектовРасширений") Тогда
		
		Возврат Неопределено;
	КонецЕсли;
	
	ОбъектМетаданных = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(Идентификатор);
	Если ОбъектМетаданных = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ОбъектМетаданных.ПолноеИмя());
	
КонецФункции

// Проверяет, является ли ключ ключом ссылочного типа.
//
// Параметры:
//	ВидКлюча - ПеречислениеСсылка.ВидыКлючейОтметокВремени - Вид ключа отметки времени.
//
// Возвращаемое значение:
//	Булево - Истина, если это ключ ссылочного типа.
//
Функция ЭтоВидКлючаСсылочногоТипа(ВидКлюча) Экспорт
	
	Возврат ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючСсылочногоОбъекта
				Или ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораПодчинениеРегистратору
				Или ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораПервоеСсылочноеИзмерение;
	
КонецФункции

// Формирует данные отметки времени.
//
// Параметры:
//  Объект - ОпределяемыйТип.ОтметкиВремениРегистры, ЛюбаяСсылка - Объект, для которого необходимо получить данные.
// 
// Возвращаемое значение:
//	Структура - Данные отметки времени:
//		* Отметка  - Число - Отметка времени.
//		* Источник - ОпределяемыйТип.ИсточникиДляОтметокВремени, Неопределено - Источник отметки времени.
//
Функция ДанныеОтметкиВремени(Объект) Экспорт
	
	ДанныеОтметкиВремени = Новый Структура("Отметка, Источник", 0, Неопределено);
		
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Т.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	Т.ТипКлюча КАК Ключ,
		|	Т.Отметка КАК Отметка,
		|	Т.Источник КАК Источник
		|ПОМЕСТИТЬ Очередь
		|ИЗ
		|	РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК Т
		|ГДЕ
		|	Т.ИдентификаторКлюча = &ИдентификаторКлюча
		|		И Т.ТипКлюча = &ТипКлюча
		|		И &ВидКлюча = ЗНАЧЕНИЕ(Перечисление.ВидыКлючейОтметокВремени.КлючСсылочногоОбъекта)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.ИдентификаторКлюча,
		|	Т.ТипКлюча,
		|	Т.Отметка,
		|	Т.Источник
		|ИЗ
		|	РегистрСведений.ОтметкиВремениРегистровКонстант КАК Т
		|ГДЕ
		|	Т.ИдентификаторКлюча = &ИдентификаторКлюча
		|		И Т.ТипКлюча = &ТипКлюча
		|		И Т.Объект = &Объект
		|		И &ВидКлюча <> ЗНАЧЕНИЕ(Перечисление.ВидыКлючейОтметокВремени.КлючСсылочногоОбъекта)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.ИдентификаторКлюча,
		|	Т.ТипКлюча,
		|	Т.Отметка,
		|	Т.Источник
		|ИЗ
		|	РегистрСведений.ОтметкиВремениОчередь1 КАК Т
		|ГДЕ
		|	Т.ИдентификаторКлюча = &ИдентификаторКлюча
		|		И Т.ТипКлюча = &ТипКлюча
		|		И Т.Объект = &Объект
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.ИдентификаторКлюча,
		|	Т.ТипКлюча,
		|	Т.Отметка,
		|	Т.Источник
		|ИЗ
		|	РегистрСведений.ОтметкиВремениОчередь2 КАК Т
		|ГДЕ
		|	Т.ИдентификаторКлюча = &ИдентификаторКлюча
		|		И Т.ТипКлюча = &ТипКлюча
		|		И Т.Объект = &Объект
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.ИдентификаторКлюча,
		|	Т.ТипКлюча,
		|	Т.Отметка,
		|	Т.Источник
		|ИЗ
		|	РегистрСведений.ОтметкиВремениОчередь3 КАК Т
		|ГДЕ
		|	Т.ИдентификаторКлюча = &ИдентификаторКлюча
		|		И Т.ТипКлюча = &ТипКлюча
		|		И Т.Объект = &Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Отметка КАК Отметка,
		|	Т.Источник КАК Источник
		|ИЗ
		|	Очередь КАК Т
		|ГДЕ
		|	(Т.ИдентификаторКлюча, Т.Отметка) В
		|		(ВЫБРАТЬ
		|			Т.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|			МАКСИМУМ(Т.Отметка) КАК Отметка
		|		ИЗ
		|			Очередь КАК Т
		|		СГРУППИРОВАТЬ ПО
		|			Т.ИдентификаторКлюча)");
		
	ОписаниеКлюча = ОписаниеКлючаОтметкиВремени(Объект); // см. ОписаниеКлючаОтметкиВремени
	
	Запрос.УстановитьПараметр("ИдентификаторКлюча", ОписаниеКлюча.ИдентификаторКлюча);
	Запрос.УстановитьПараметр("ТипКлюча", ОписаниеКлюча.ТипКлюча);
	Запрос.УстановитьПараметр("ВидКлюча", ОписаниеКлюча.ВидКлюча);
	Запрос.УстановитьПараметр("Объект", ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Объект)));
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаРезультата = РезультатЗапроса.Выбрать();
	Если ВыборкаРезультата.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ДанныеОтметкиВремени, ВыборкаРезультата);
	КонецЕсли;
	
	Возврат ДанныеОтметкиВремени;
	
КонецФункции

// Возвращает имя поля, в котором хранятся ключи вида идентификатор набора записей.
//
// Возвращаемое значение:
//	Строка
//
Функция ИмяПоляХраненияКлючейВидаИдентификаторНабораЗаписей() Экспорт
	
	Возврат "ИдентификаторОтметкиВремени";
	
КонецФункции

// Возвращает имя поля, в котором хранятся ключи наборов записей с подчинением регистратору.
//
// Возвращаемое значение:
//	Строка
//
Функция ИмяПоляХраненияКлючейНаборовСПодчинениемРегистратору() Экспорт
	
	Возврат "Регистратор";
	
КонецФункции

// Возвращает имя поля, в котором хранятся ключи ссылочных объектов.
//
// Возвращаемое значение:
//	Строка
//
Функция ИмяПоляХраненияКлючейСсылочныхОбъектов() Экспорт
	
	Возврат "Ссылка";
	
КонецФункции

// Проверяет является ли значение ключа набора записей допустимым, если нет - выбрасывает исключение.
//
// Параметры:
//	Ключ - ЛюбаяСсылка, Неопределено - Ключ набора записей.
//	Объект - СправочникСсылка.ИдентификаторыОбъектовМетаданных,
//		   - СправочникСсылка.ИдентификаторыОбъектовРасширений - Идентификатор объекта, которому принадлежит набор.
//	Отбор - Отбор - Отбор набора записей.
//	ПолеКлюча - Строка - Поле, в котором хранится ключ.
//
Процедура ПроверитьЗначениеКлючаНабораЗаписей(Ключ, Объект, Отбор, ПолеКлюча) Экспорт
	
	Если Ключ = Неопределено Тогда
		ВызватьИсключение СтрШаблон("%1 ""%5"" %2 ""%3"" [%4]",
			НСтр("ru = 'Не найдено значение ключевого поля'", ОбщегоНазначения.КодОсновногоЯзыка()),
			НСтр("ru = 'для'", ОбщегоНазначения.КодОсновногоЯзыка()),
			Объект,
			Отбор,
			ПолеКлюча);
	КонецЕсли;
	
	Если Не ОтметкиВремениПовтИсп.ОписаниеТипаКлючиСсылочногоТипа().СодержитТип(ТипЗнч(Ключ)) Тогда
		ВызватьИсключение СтрШаблон("%1 ""%6"" %2 ""%4"" %3 [%5]",
			НСтр("ru = 'Значение ключевого поля'", ОбщегоНазначения.КодОсновногоЯзыка()),
			НСтр("ru = 'объекта'", ОбщегоНазначения.КодОсновногоЯзыка()),
			НСтр("ru = 'не является допустимым'", ОбщегоНазначения.КодОсновногоЯзыка()),
			Объект,
			Отбор,
			ПолеКлюча);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает максимально допустимое количество записей в наборе.
//
// Возвращаемое значение:
//	Число - Количество записей.
//
Функция МаксимальноеКоличествоЗаписейНабораОтметокВремени() Экспорт
	
	Возврат 1000;
	
КонецФункции

// Выполняет запись в информационную базу и очистку набора записей отметок времени.
//
// Параметры:
//	НаборЗаписей - РегистрСведенийНаборЗаписей.ОтметкиВремениОчередь1 - Набор, который должен быть записан.
//				 - РегистрСведенийНаборЗаписей.ОтметкиВремениОчередь2 -
//				 - РегистрСведенийНаборЗаписей.ОтметкиВремениОчередь3 -
//				 - РегистрСведенийНаборЗаписей.ОтметкиВремениСсылочныхОбъектов -
//				 - РегистрСведенийНаборЗаписей.ОтметкиВремениРегистровКонстант -
//	РежимЗамещения - Неопределено, РежимЗамещения - Режим замещения записей набора (по умолчанию Слияние).
//	Немедленно - Булево - Признак необходимости записи без дополнительных проверок.
//
Процедура ЗаписатьНабор(НаборЗаписей, РежимЗамещения = Неопределено, Немедленно = Ложь) Экспорт
	
	КоличествоЗаписей = НаборЗаписей.Количество();
	Если КоличествоЗаписей = 0
			Или (Не Немедленно И КоличествоЗаписей < МаксимальноеКоличествоЗаписейНабораОтметокВремени()) Тогда
		Возврат;
	КонецЕсли;
	
	ОтключитьРегистрацию(НаборЗаписей);
	НаборЗаписей.РасширенныеРежимыЗамещения = Истина;
	НаборЗаписей.Записать(
		?(РежимЗамещения = Неопределено, ОбщегоНазначения.РежимСлиянияНабораЗаписей(), РежимЗамещения));
	НаборЗаписей.Очистить();
	
КонецПроцедуры

#Область ОбработкаОчереди

// Обработчик регламентного задания ОтметкиВремениОбработка.
//
Процедура ОбработкаОчереди() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ОтметкиВремениОбработка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	РазобратьОчередь(Истина);
	
КонецПроцедуры

// Выполняет перенос отметок времени из очереди на постоянное хранение.
//
// Параметры:
//	РежимОжидания - Булево - Признак использования режима ожидания.
//
Процедура РазобратьОчередь(РежимОжидания = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СостояниеОбработки = 2;
	ВремяПерерыва = Неопределено;
	
	Пока СостояниеОбработки >= 1 Цикл
		
		//@skip-check query-in-loop
		ДанныеОчереди = ДанныеОчереди(); // см. ДанныеОчереди
		
		КоличествоЗаписейОчереди = 0;
		Если ДанныеОчереди.Количество > 0 Тогда
			
			ДанныеДляБлокировки = ДанныеДляБлокировки(ДанныеОчереди.МенеджерВременныхТаблиц);
			
			НачатьТранзакцию();
		
			Попытка
				СостояниеОбработки = 2;
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтметкиВремениОчередь1");
				ЭлементБлокировки.ИсточникДанных = ДанныеДляБлокировки.ОтметкиВремениОчередь1;
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ИдентификаторКлюча", "ИдентификаторКлюча");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипКлюча", "ТипКлюча");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Объект", "Объект");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Отметка", "Отметка");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтметкиВремениОчередь2");
				ЭлементБлокировки.ИсточникДанных = ДанныеДляБлокировки.ОтметкиВремениОчередь2;
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ИдентификаторКлюча", "ИдентификаторКлюча");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипКлюча", "ТипКлюча");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Объект", "Объект");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Отметка", "Отметка");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтметкиВремениОчередь3");
				ЭлементБлокировки.ИсточникДанных = ДанныеДляБлокировки.ОтметкиВремениОчередь3;
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ИдентификаторКлюча", "ИдентификаторКлюча");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипКлюча", "ТипКлюча");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Объект", "Объект");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Отметка", "Отметка");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтметкиВремениСсылочныхОбъектов");
				ЭлементБлокировки.ИсточникДанных = ДанныеДляБлокировки.ОтметкиВремениСсылочныхОбъектов;
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ИдентификаторКлюча", "ИдентификаторКлюча");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипКлюча", "ТипКлюча");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтметкиВремениРегистровКонстант");
				ЭлементБлокировки.ИсточникДанных = ДанныеДляБлокировки.ОтметкиВремениРегистровКонстант;
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ИдентификаторКлюча", "ИдентификаторКлюча");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипКлюча", "ТипКлюча");
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Объект", "Объект");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				
				Блокировка.Заблокировать();
				
				// @skip-check query-in-loop
				ДанныеДляОбновления = ДанныеДляОбновления(ДанныеОчереди.МенеджерВременныхТаблиц); // см. ДанныеДляОбновления
				
				Для Каждого ДействияИТаблицы Из ДанныеДляОбновления Цикл // Структура Из КлючИЗначение
					Режим = ДействияИТаблицы.Ключ; // Строка
					ТаблицыИДанные = ДействияИТаблицы.Значение; // Структура Из КлючИЗначение
					
					Для Каждого ТаблицаИДанные Из ТаблицыИДанные Цикл // Структура Из КлючИЗначение
						Таблица = ТаблицаИДанные.Ключ; // Строка
						ДанныеТаблицы = ТаблицаИДанные.Значение; // РезультатЗапроса, Неопределено
						
						Если ДанныеТаблицы = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						НаборЗаписей = РегистрыСведений[Таблица].СоздатьНаборЗаписей(); // РегистрСведенийНаборЗаписей.ОтметкиВремениОчередь1
						
						Выборка = ДанныеТаблицы.Выбрать(); // ВыборкаИзРезультатаЗапроса
						Пока Выборка.Следующий() Цикл
							ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
							ЗаписатьНабор(НаборЗаписей, РежимЗамещения[Режим]);
						КонецЦикла;
						ЗаписатьНабор(НаборЗаписей, РежимЗамещения[Режим], Истина);
						
						НаборЗаписей = Неопределено;
						ДанныеТаблицы = Неопределено;
					КонецЦикла;
				КонецЦикла;
				
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				
				ЗаписатьОшибкуОбработкиОчередиОтметокВремени(
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ВызватьИсключение;
			КонецПопытки;
		КонецЕсли;

		Если ДанныеОчереди.Количество < 100 Тогда
			СостояниеОбработки = СостояниеОбработки - 1;
			Если Не РежимОжидания Тогда
				СостояниеОбработки = СостояниеОбработки - 1;
			КонецЕсли;
		КонецЕсли;
		
		ДанныеОчереди.МенеджерВременныхТаблиц.Закрыть();
		ДанныеДляОбновления = Неопределено;
		ДанныеДляБлокировки = Неопределено;
		ДанныеОчереди = Неопределено;
		
		Если РежимОжидания Тогда
			Если ВремяПерерыва = Неопределено Тогда
				ВремяПерерыва = ТекущаяУниверсальнаяДата() + 300;
			КонецЕсли;
			Если ТекущаяУниверсальнаяДата() >= ВремяПерерыва Тогда
				Прервать;
			КонецЕсли;
			Если СостояниеОбработки = 1 Тогда
				ОбщегоНазначенияБТС.Пауза(3);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Ошибка типизации.
//
// Параметры:
//  ИмяПоля - Строка, Неопределено - Имя поля
//  Значение - Произвольный - Значение
//  Метаданное - ОбъектМетаданных, Произвольный, Неопределено, Null - Метаданное
Процедура ОшибкаТипизации(ИмяПоля, Значение, Метаданное) Экспорт
	
	Причина = СтрШаблон("%1 ""%3"" %2 ""%4"" (%5)",
		НСтр("ru = 'Неактуальный тип значения'"),
		НСтр("ru = 'поле'"),
		ТипЗнч(Значение),
		ИмяПоля, Метаданное.ПолноеИмя());
	ВызватьИсключение Причина;
	
КонецПроцедуры

#КонецОбласти

#Область ВыборкаИзменений

// Возвращает порцию зарегистрированных изменений.
//
// Параметры:
//	Отметка - Число - Отметка, начиная с которой необходимо выбрать изменения.
//	ПараметрыВыборки - см. ПараметрыВыборкиИзменений
//
// Возвращаемое значение:
//	ТаблицаЗначений:
//		* ИдентификаторКлюча - УникальныйИдентификатор - Идентификатор ключа, зарегистрированной отметки времени.
//		* ТипКлюча - СправочникСсылка.ИдентификаторыОбъектовМетаданных -
//				   - СправочникСсылка.ИдентификаторыОбъектовРасширений - Тип ключа, зарегистрированной отметки времени.
//		* Объект - СправочникСсылка.ИдентификаторыОбъектовМетаданных -
//				 - СправочникСсылка.ИдентификаторыОбъектовРасширений - Тип объекта, для которого зарегистрирована отметка.
//		* Ключ - ЛюбаяСсылка, УникальныйИдентификатор - Ключ объекта, для которого зарегистрирована отметка времени.
//		* Отметка - Число - Отметка времени.
//		* Удаление - Булево - Признак удаления данных.
//		* Источник - ОпределяемыйТип.ИсточникиДляОтметокВремени - Источник отметки времени.
//		* ВидКлюча - ПеречислениеСсылка.ВидыКлючейОтметокВремени - Вид ключа отметки времени.
//		* УдаленныеКлючи - Структура Из КлючИЗначение - Структура, содержащая значение отбора исходного набора записей.
//
Функция ВыбратьИзменения(Отметка, ПараметрыВыборки) Экспорт
	
	Изменения = РезультатВыборкиИзменений(Отметка, ПараметрыВыборки);
	
	Изменения.Колонки.Добавить(
		"Ключ", Новый ОписаниеТипов(ОбщегоНазначения.ОписаниеТипаВсеСсылки(), "УникальныйИдентификатор"));
	Изменения.Колонки.Добавить("ПолеКлюча", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(10)));
	Изменения.Колонки.Добавить("ПолеИсточника", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(150)));
	Изменения.Колонки.Добавить("УдаленныеКлючи", Новый ОписаниеТипов("Неопределено"));
	
	// Разыменование ключей.
	Для Каждого Строка Из Изменения Цикл
		Если ОтметкиВремениПовтИсп.ЭтоВидКлючаСсылочногоТипа(Строка.ВидКлюча) Тогда
			Строка.Ключ = ЗначениеСсылкиПоКлючуОтметкиВремени(Строка.ИдентификаторКлюча, Строка.ТипКлюча); //@skip-check property-return-type
		Иначе
			Строка.Ключ = Строка.ИдентификаторКлюча; //@skip-check property-return-type
		КонецЕсли;
		
		Если Строка.ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючЗначенияКонстанты
				Или Строка.ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораРегистрБезИзмерений Тогда
			Строка.ПолеКлюча = "Объект";
		Иначе
			Строка.ПолеКлюча = "Ключ";
		КонецЕсли;
		
		Строка.ПолеИсточника = ПолеКлючаОбъекта(Строка.Объект);
		Если Строка.Удаление
				И Строка.ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораИдентификаторНабораЗаписей Тогда
			Строка.УдаленныеКлючи = ЗначенияКлюча(Строка.ХранилищеКлюча);
		КонецЕсли;
		
		ПараметрыВыборки.ГраницаВыборкиИзменений = Макс(ПараметрыВыборки.ГраницаВыборкиИзменений, Строка.Граница);
	КонецЦикла;
	
	Изменения.Колонки.Удалить("ХранилищеКлюча");
	
	Если ПараметрыВыборки.МенеджерВременныхТаблиц <> Неопределено Тогда
		ТекстЗапроса = Новый Массив; // Массив Из Строка
		
		ГраницаОтметокСдвинута = Отметка <> ПараметрыВыборки.ГраницаВыборкиИзменений;
		
		Для Каждого Таблица Из ПараметрыВыборки.МенеджерВременныхТаблиц.Таблицы Цикл
			Если (ГраницаОтметокСдвинута И Таблица.ПолноеИмя = "Изменённые")
					Или (Не ГраницаОтметокСдвинута И Таблица.ПолноеИмя = "РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ") Тогда
					
				Продолжить;
			КонецЕсли;
			
			ТекстЗапроса.Добавить(СтрШаблон("УНИЧТОЖИТЬ %1", Таблица.ПолноеИмя));
		КонецЦикла;

		Если ГраницаОтметокСдвинута Тогда
			ТекстЗапроса.Добавить(
				"ВЫБРАТЬ
				|	Т.ИдентификаторКлюча КАК ИдентификаторКлюча,
				|	Т.ТипКлюча КАК ТипКлюча,
				|	Т.Объект КАК Объект,
				|	Т.Отметка КАК Отметка
				|ПОМЕСТИТЬ РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ
				|ИЗ
				|	Изменённые КАК Т");
			
			ТекстЗапроса.Добавить("УНИЧТОЖИТЬ Изменённые");
		КонецЕсли;
		
		Запрос = Новый Запрос(СтрСоединить(ТекстЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов()));
		Запрос.МенеджерВременныхТаблиц = ПараметрыВыборки.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
	КонецЕсли;
	
	Возврат Изменения;
	
КонецФункции

// Производит инициализацию параметров выборки изменений.
//
// Параметры:
//  Объект - СправочникСсылка.ИдентификаторыОбъектовРасширений -
//		   - СправочникСсылка.ИдентификаторыОбъектовМетаданных -
//		   - Массив из СправочникСсылка.ИдентификаторыОбъектовРасширений -
//		   - Массив из СправочникСсылка.ИдентификаторыОбъектовМетаданных - Идентификатор (тип) объекта, изменения
//																		   которого необходимо получить.
//		   - Неопределено - Выбирать изменения без отбора по идентификаторам (типам) объектов.
//	Источник - ОпределяемыйТип.ИсточникиДляОтметокВремени - Источник отметок времени.
//	РазмерПорции - Число - Количество выбираемых за один раз изменений.
//
// Возвращаемое значение:
//	см. ПараметрыВыборкиИзменений.
//
Функция ИнициализироватьПараметрыВыборки(Объект = Неопределено, Источник = Неопределено, РазмерПорции = 1000) Экспорт
	
	ПараметрыВыборки = ПараметрыВыборкиИзменений();
	
	Если Объект <> Неопределено Тогда 
		ПараметрыВыборки.Объект = Объект;
	КонецЕсли;
	
	Если Источник <> Неопределено Тогда 
		ПараметрыВыборки.Источник = Источник;
	КонецЕсли;
	
	Если ПараметрыВыборки.РазмерПорции < 2 Тогда
		ПараметрыВыборки.РазмерПорции = 2;
	Иначе
		ПараметрыВыборки.РазмерПорции = РазмерПорции;
	КонецЕсли;
	
	МенеджерВременныхТаблиц(ПараметрыВыборки);
	
	Возврат ПараметрыВыборки;
	
КонецФункции

// Деструктор параметров выборки изменений.
//
// Параметры:
//	ПараметрыВыборки - см. ПараметрыВыборкиИзменений.
//
Процедура ЗавершитьВыборкуИзменений(ПараметрыВыборки) Экспорт
	
	Если ПараметрыВыборки.Объект <> Неопределено Тогда 
		ПараметрыВыборки.Объект = Неопределено;
	КонецЕсли;
	
	Если ПараметрыВыборки.Источник <> Неопределено Тогда 
		ПараметрыВыборки.Источник = Неопределено;
	КонецЕсли;
	
	ЗакрытьМенеджерВременныхТаблиц(ПараметрыВыборки);
	
КонецПроцедуры

// Конструктор параметра МенеджерВременныхТаблиц.
//
// Параметры:
//	ПараметрыВыборки - см. ПараметрыВыборкиИзменений.
//
Процедура МенеджерВременныхТаблиц(ПараметрыВыборки) Экспорт
	
	ПараметрыВыборки.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	&ПустойИдентификатор КАК ИдентификаторКлюча,
		|	НЕОПРЕДЕЛЕНО КАК ТипКлюча,
		|	НЕОПРЕДЕЛЕНО КАК Объект,
		|	НЕОПРЕДЕЛЕНО КАК Отметка
		|ПОМЕСТИТЬ РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ
		|ГДЕ
		|	ЛОЖЬ");
	Запрос.УстановитьПараметр(
		"ПустойИдентификатор", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
	Запрос.МенеджерВременныхТаблиц = ПараметрыВыборки.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры

// Закрывает менеджер временных таблиц, содержащийся в параметрах выборки изменений.
//
// Параметры:
//	ПараметрыВыборки - см. ПараметрыВыборкиИзменений.
//
Процедура ЗакрытьМенеджерВременныхТаблиц(ПараметрыВыборки) Экспорт

	Если ТипЗнч(ПараметрыВыборки.МенеджерВременныхТаблиц) = Тип("МенеджерВременныхТаблиц") Тогда
		ПараметрыВыборки.МенеджерВременныхТаблиц.Закрыть();
		ПараметрыВыборки.МенеджерВременныхТаблиц = Неопределено;
	КонецЕсли;

КонецПроцедуры

// Конструктор параметров выборки изменений.
//
// Возвращаемое значение:
//	Структура:
//		* РазмерПорции - Число - Количество выбираемых за один раз изменений.
//  	* Объект - Неопределено -
//  			 - СправочникСсылка.ИдентификаторыОбъектовРасширений -
//				 - СправочникСсылка.ИдентификаторыОбъектовМетаданных -
//				 - Массив Из СправочникСсылка.ИдентификаторыОбъектовРасширений -
//				 - Массив Из СправочникСсылка.ИдентификаторыОбъектовМетаданных - Идентификатор (тип) объекта, изменения
//				   																 которого необходимо получить. 
//		* Источник - Неопределено, СправочникСсылка.ИдентификаторыОбъектовМетаданных - Источник отметок времени.
//		* МенеджерВременныхТаблиц - МенеджерВременныхТаблиц -
//								  - Неопределено - Менеджер временных таблиц.
//		* ГраницаВыборкиИзменений - Число - Граница начала выборки изменений.
//
Функция ПараметрыВыборкиИзменений() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("РазмерПорции", 1000);
	Параметры.Вставить("Объект", Неопределено);
	Параметры.Вставить("Источник", Неопределено);
	Параметры.Вставить("МенеджерВременныхТаблиц", Неопределено);
	Параметры.Вставить("ГраницаВыборкиИзменений", 0);
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#Область УстаревшиеПроцедурыИФункции

// Устарела. Следует использовать ПолеКлючаОбъекта.
// Возвращает ключевое поле источника.
//
// Параметры:
//	Объект - ОбъектМетаданных -
//		   - СправочникСсылка.ИдентификаторыОбъектовМетаданных -
//		   - СправочникСсылка.ИдентификаторыОбъектовРасширений - Объект-источник отметок времени, ключевое
//																 поле которого необходимо определить.
//	ТипКлюча - Число - Тип ключа объекта-источника отметок времени.
//
// Возвращаемое значение:
//	Строка - Ключевое поле источника.
//
Функция КлючевоеПолеИсточника(Объект, ТипКлюча) Экспорт
	
	Если ТипЗнч(Объект) = Тип("ОбъектМетаданных") Тогда
		Возврат ПолеКлючаОбъекта(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Объект));
	КонецЕсли;
	
	Возврат ПолеКлючаОбъекта(Объект);
	
КонецФункции

// Устарела (использование в новых механизмах недопустимо).
// Возвращает ключ для набора записей регистра сведений.
// 
// Параметры:
//	Источник - ОпределяемыйТип.ОтметкиВремениРегистры - Объект, для которого необходимо определить ключ.
//
// Возвращаемое значение:
//	ЛюбаяСсылка - Ключ набора записей.
//
Функция Ключ(Источник) Экспорт
	
	Ключ = Неопределено;
	
	ОписаниеКлюча = ОписаниеКлючаОтметкиВремени(Источник); // см. ОписаниеКлючаОтметкиВремени
	Если ОтметкиВремениПовтИсп.ЭтоВидКлючаСсылочногоТипа(ОписаниеКлюча.ВидКлюча) Тогда
		Ключ = ЗначениеСсылкиПоКлючуОтметкиВремени(ОписаниеКлюча.ИдентификаторКлюча, ОписаниеКлюча.ТипКлюча);
	ИначеЕсли ОписаниеКлюча.ВидКлюча = Перечисления.ВидыКлючейОтметокВремени.КлючНабораИдентификаторНабораЗаписей Тогда
		Ключ = Справочники.ИдентификаторыОбъектовМетаданных.ПолучитьСсылку(ОписаниеКлюча.ИдентификаторКлюча);
	КонецЕсли;
	
	Возврат Ключ;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет является ли ссылочный тип значения.
// 
// Параметры:
//  ОписаниеТипа - ОписаниеТипов - Описание типа.
// 
// Возвращаемое значение:
//  Булево - Это ссылочный тип значения
//
Функция ЭтоСсылочныйТипЗначения(ОписаниеТипа)
	
	Для Каждого Тип Из ОписаниеТипа.Типы() Цикл
		Если Не ОтметкиВремениПовтИсп.ОписаниеТипаКлючиСсылочногоТипа().СодержитТип(Тип) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#Область ОбработкаОчереди

// Возвращает данные очереди для обновления таблиц отметок времени.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий данные очереди.
//
// Возвращаемое значение:
//	Структура:
//		* Удаление - см. СписокТаблицДляУдаленияОтметокПриОбработкеОчереди.
//		* Добавление - см. СписокТаблицДляДобавленияОтметокПриОбработкеОчереди.
//
Функция ДанныеДляОбновления(МенеджерВременныхТаблиц)
	
	ДанныеДляОбновления = Новый Структура;
	ДанныеДляОбновления.Вставить("Удаление", СписокТаблицДляУдаленияОтметокПриОбработкеОчереди());
	ДанныеДляОбновления.Вставить("Добавление", СписокТаблицДляДобавленияОтметокПриОбработкеОчереди());
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеОчереди.НомерЗаписи КАК НомерЗаписи,
		|	ДанныеОчереди.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ДанныеОчереди.ТипКлюча КАК ТипКлюча,
		|	ДанныеОчереди.Объект КАК Объект,
		|	ДанныеОчереди.ВидКлюча КАК ВидКлюча
		|ПОМЕСТИТЬ ДанныеОчередиАктуальные
		|ИЗ
		|	ДанныеОчереди КАК ДанныеОчереди
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			МАКСИМУМ(ДанныеОчереди.НомерЗаписи) КАК НомерЗаписи,
		|			ДанныеОчереди.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|			ДанныеОчереди.ТипКлюча КАК ТипКлюча,
		|			ДанныеОчереди.Объект КАК Объект
		|		ИЗ
		|			ДанныеОчереди КАК ДанныеОчереди
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ДанныеОчереди.ИдентификаторКлюча,
		|			ДанныеОчереди.ТипКлюча,
		|			ДанныеОчереди.Объект) КАК АктуальныеЗаписи
		|		ПО ДанныеОчереди.НомерЗаписи = АктуальныеЗаписи.НомерЗаписи
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтметкиНаХранении.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ОтметкиНаХранении.ТипКлюча КАК ТипКлюча,
		|	ОтметкиНаХранении.ТипКлюча КАК Объект,
		|	ОтметкиНаХранении.Граница КАК Граница,
		|	ОтметкиНаХранении.Отметка КАК Отметка
		|ПОМЕСТИТЬ ОтметкиВремениСсылочныхОбъектов
		|ИЗ
		|	РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиНаХранении
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОчередиАктуальные КАК ДанныеОчереди
		|		ПО ОтметкиНаХранении.ИдентификаторКлюча = ДанныеОчереди.ИдентификаторКлюча
		|			И ОтметкиНаХранении.ТипКлюча = ДанныеОчереди.ТипКлюча
		|			И ОтметкиНаХранении.ТипКлюча = ДанныеОчереди.Объект
		|			И (ДанныеОчереди.ВидКлюча = ЗНАЧЕНИЕ(Перечисление.ВидыКлючейОтметокВремени.КлючСсылочногоОбъекта))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтметкиНаХранении.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ОтметкиНаХранении.ТипКлюча КАК ТипКлюча,
		|	ОтметкиНаХранении.Объект КАК Объект,
		|	ОтметкиНаХранении.Граница КАК Граница,
		|	ОтметкиНаХранении.Отметка КАК Отметка
		|ПОМЕСТИТЬ ОтметкиВремениРегистровКонстант
		|ИЗ
		|	РегистрСведений.ОтметкиВремениРегистровКонстант КАК ОтметкиНаХранении
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОчередиАктуальные КАК ДанныеОчереди
		|		ПО ОтметкиНаХранении.ИдентификаторКлюча = ДанныеОчереди.ИдентификаторКлюча
		|			И ОтметкиНаХранении.ТипКлюча = ДанныеОчереди.ТипКлюча
		|			И ОтметкиНаХранении.Объект = ДанныеОчереди.Объект
		|			И (ДанныеОчереди.ВидКлюча <> ЗНАЧЕНИЕ(Перечисление.ВидыКлючейОтметокВремени.КлючСсылочногоОбъекта))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	Т.ТипКлюча КАК ТипКлюча,
		|	Т.Объект КАК Объект,
		|	МАКСИМУМ(Т.Отметка) КАК Отметка
		|ПОМЕСТИТЬ ОтметкиВремениСсылочныхОбъектовАктуальные
		|ИЗ
		|	ОтметкиВремениСсылочныхОбъектов КАК Т
		|
		|СГРУППИРОВАТЬ ПО
		|	Т.ИдентификаторКлюча,
		|	Т.ТипКлюча,
		|	Т.Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	Т.ТипКлюча КАК ТипКлюча,
		|	Т.Объект КАК Объект,
		|	МАКСИМУМ(Т.Отметка) КАК Отметка
		|ПОМЕСТИТЬ ОтметкиВремениРегистровКонстантАктуальные
		|ИЗ
		|	ОтметкиВремениРегистровКонстант КАК Т
		|
		|СГРУППИРОВАТЬ ПО
		|	Т.ИдентификаторКлюча,
		|	Т.ТипКлюча,
		|	Т.Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА Границы.Граница >= ДанныеОчереди.Отметка
		|			ТОГДА Границы.Граница + 1
		|		ИНАЧЕ ДанныеОчереди.Отметка
		|	КОНЕЦ КАК Граница,
		|	ДанныеОчереди.Отметка КАК Отметка,
		|	ДанныеОчереди.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ДанныеОчереди.ТипКлюча КАК ТипКлюча,
		|	ДанныеОчереди.Объект КАК Объект,
		|	ДанныеОчереди.Удаление КАК Удаление,
		|	ДанныеОчереди.Источник КАК Источник,
		|	ДанныеОчереди.ВидКлюча КАК ВидКлюча,
		|	ДанныеОчереди.ЗначенияКлюча КАК ЗначенияКлюча
		|ПОМЕСТИТЬ ДобавляемыеОтметкиВремениСсылочныхОбъектов
		|ИЗ
		|	ДанныеОчереди КАК ДанныеОчереди
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОчередиАктуальные КАК ДанныеОчередиАктуальные
		|		ПО ДанныеОчереди.НомерЗаписи = ДанныеОчередиАктуальные.НомерЗаписи
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОтметкиВремениСсылочныхОбъектовАктуальные КАК ОтметкиВремени
		|		ПО ДанныеОчереди.ИдентификаторКлюча = ОтметкиВремени.ИдентификаторКлюча
		|			И ДанныеОчереди.ТипКлюча = ОтметкиВремени.ТипКлюча
		|			И ДанныеОчереди.Объект = ОтметкиВремени.Объект
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Границы КАК Границы
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ДанныеОчереди.ВидКлюча = ЗНАЧЕНИЕ(Перечисление.ВидыКлючейОтметокВремени.КлючСсылочногоОбъекта)
		|	И ДанныеОчереди.Отметка > ЕСТЬNULL(ОтметкиВремени.Отметка, -1)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА Границы.Граница >= ДанныеОчереди.Отметка
		|			ТОГДА Границы.Граница + 1
		|		ИНАЧЕ ДанныеОчереди.Отметка
		|	КОНЕЦ КАК Граница,
		|	ДанныеОчереди.Отметка КАК Отметка,
		|	ДанныеОчереди.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ДанныеОчереди.ТипКлюча КАК ТипКлюча,
		|	ДанныеОчереди.Объект КАК Объект,
		|	ДанныеОчереди.Удаление КАК Удаление,
		|	ДанныеОчереди.Источник КАК Источник,
		|	ДанныеОчереди.ВидКлюча КАК ВидКлюча,
		|	ДанныеОчереди.ЗначенияКлюча КАК ЗначенияКлюча
		|ПОМЕСТИТЬ ДобавляемыеОтметкиВремениРегистровКонстант
		|ИЗ
		|	ДанныеОчереди КАК ДанныеОчереди
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОчередиАктуальные КАК ДанныеОчередиАктуальные
		|		ПО ДанныеОчереди.НомерЗаписи = ДанныеОчередиАктуальные.НомерЗаписи
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОтметкиВремениРегистровКонстантАктуальные КАК ОтметкиВремени
		|		ПО ДанныеОчереди.ИдентификаторКлюча = ОтметкиВремени.ИдентификаторКлюча
		|			И ДанныеОчереди.ТипКлюча = ОтметкиВремени.ТипКлюча
		|			И ДанныеОчереди.Объект = ОтметкиВремени.Объект
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Границы КАК Границы
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ДанныеОчереди.ВидКлюча <> ЗНАЧЕНИЕ(Перечисление.ВидыКлючейОтметокВремени.КлючСсылочногоОбъекта)
		|	И ДанныеОчереди.Отметка > ЕСТЬNULL(ОтметкиВремени.Отметка, -1)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтметкиВремени.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ОтметкиВремени.ТипКлюча КАК ТипКлюча,
		|	ОтметкиВремени.Граница КАК Граница
		|ИЗ
		|	ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремени
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДобавляемыеОтметкиВремениСсылочныхОбъектов КАК ДобавляемыеОтметки
		|		ПО ОтметкиВремени.ИдентификаторКлюча = ДобавляемыеОтметки.ИдентификаторКлюча
		|			И ОтметкиВремени.ТипКлюча = ДобавляемыеОтметки.ТипКлюча
		|			И ОтметкиВремени.Объект = ДобавляемыеОтметки.Объект
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОтметкиВремениСсылочныхОбъектовАктуальные КАК АктуальныеОтметки
		|		ПО ОтметкиВремени.ИдентификаторКлюча = АктуальныеОтметки.ИдентификаторКлюча
		|			И ОтметкиВремени.ТипКлюча = АктуальныеОтметки.ТипКлюча
		|			И ОтметкиВремени.Объект = АктуальныеОтметки.Объект
		|ГДЕ
		|	(НЕ ДобавляемыеОтметки.Отметка ЕСТЬ NULL
		|			ИЛИ ОтметкиВремени.Отметка < АктуальныеОтметки.Отметка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтметкиВремени.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ОтметкиВремени.ТипКлюча КАК ТипКлюча,
		|	ОтметкиВремени.Объект КАК Объект,
		|	ОтметкиВремени.Граница КАК Граница
		|ИЗ
		|	ОтметкиВремениРегистровКонстант КАК ОтметкиВремени
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДобавляемыеОтметкиВремениРегистровКонстант КАК ДобавляемыеОтметки
		|		ПО ОтметкиВремени.ИдентификаторКлюча = ДобавляемыеОтметки.ИдентификаторКлюча
		|			И ОтметкиВремени.ТипКлюча = ДобавляемыеОтметки.ТипКлюча
		|			И ОтметкиВремени.Объект = ДобавляемыеОтметки.Объект
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОтметкиВремениРегистровКонстантАктуальные КАК АктуальныеОтметки
		|		ПО ОтметкиВремени.ИдентификаторКлюча = АктуальныеОтметки.ИдентификаторКлюча
		|			И ОтметкиВремени.ТипКлюча = АктуальныеОтметки.ТипКлюча
		|			И ОтметкиВремени.Объект = АктуальныеОтметки.Объект
		|ГДЕ
		|	(НЕ ДобавляемыеОтметки.Отметка ЕСТЬ NULL
		|			ИЛИ ОтметкиВремени.Отметка < АктуальныеОтметки.Отметка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеОчереди.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ДанныеОчереди.ТипКлюча КАК ТипКлюча,
		|	ДанныеОчереди.Объект КАК Объект,
		|	ДанныеОчереди.Отметка КАК Отметка
		|ИЗ
		|	ДанныеОчереди КАК ДанныеОчереди
		|ГДЕ
		|	ДанныеОчереди.Окно = 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеОчереди.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ДанныеОчереди.ТипКлюча КАК ТипКлюча,
		|	ДанныеОчереди.Объект КАК Объект,
		|	ДанныеОчереди.Отметка КАК Отметка
		|ИЗ
		|	ДанныеОчереди КАК ДанныеОчереди
		|ГДЕ
		|	ДанныеОчереди.Окно = 2
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеОчереди.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ДанныеОчереди.ТипКлюча КАК ТипКлюча,
		|	ДанныеОчереди.Объект КАК Объект,
		|	ДанныеОчереди.Отметка КАК Отметка
		|ИЗ
		|	ДанныеОчереди КАК ДанныеОчереди
		|ГДЕ
		|	ДанныеОчереди.Окно = 3
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДобавляемыеОтметкиВремениСсылочныхОбъектов.Граница КАК Граница,
		|	ДобавляемыеОтметкиВремениСсылочныхОбъектов.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ДобавляемыеОтметкиВремениСсылочныхОбъектов.ТипКлюча КАК ТипКлюча,
		|	ДобавляемыеОтметкиВремениСсылочныхОбъектов.Отметка КАК Отметка,
		|	ДобавляемыеОтметкиВремениСсылочныхОбъектов.Удаление КАК Удаление,
		|	ДобавляемыеОтметкиВремениСсылочныхОбъектов.Источник КАК Источник
		|ИЗ
		|	ДобавляемыеОтметкиВремениСсылочныхОбъектов КАК ДобавляемыеОтметкиВремениСсылочныхОбъектов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДобавляемыеОтметкиВремениРегистровКонстант.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ДобавляемыеОтметкиВремениРегистровКонстант.ТипКлюча КАК ТипКлюча,
		|	ДобавляемыеОтметкиВремениРегистровКонстант.Объект КАК Объект,
		|	ДобавляемыеОтметкиВремениРегистровКонстант.Граница КАК Граница,
		|	ДобавляемыеОтметкиВремениРегистровКонстант.Отметка КАК Отметка,
		|	ДобавляемыеОтметкиВремениРегистровКонстант.Удаление КАК Удаление,
		|	ДобавляемыеОтметкиВремениРегистровКонстант.ВидКлюча КАК ВидКлюча,
		|	ДобавляемыеОтметкиВремениРегистровКонстант.ЗначенияКлюча КАК ЗначенияКлюча,
		|	ДобавляемыеОтметкиВремениРегистровКонстант.Источник КАК Источник
		|ИЗ
		|	ДобавляемыеОтметкиВремениРегистровКонстант КАК ДобавляемыеОтметкиВремениРегистровКонстант";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеДляОбновления.Удаление.ОтметкиВремениОчередь1 = РезультатЗапроса[РезультатЗапроса.ВГраница() - 4];
	ДанныеДляОбновления.Удаление.ОтметкиВремениОчередь2 = РезультатЗапроса[РезультатЗапроса.ВГраница() - 3];
	ДанныеДляОбновления.Удаление.ОтметкиВремениОчередь3 = РезультатЗапроса[РезультатЗапроса.ВГраница() - 2];
	ДанныеДляОбновления.Удаление.ОтметкиВремениСсылочныхОбъектов = РезультатЗапроса[РезультатЗапроса.ВГраница() - 6];
	ДанныеДляОбновления.Удаление.ОтметкиВремениРегистровКонстант = РезультатЗапроса[РезультатЗапроса.ВГраница() - 5];
	ДанныеДляОбновления.Добавление.ОтметкиВремениСсылочныхОбъектов = РезультатЗапроса[РезультатЗапроса.ВГраница() - 1];
	ДанныеДляОбновления.Добавление.ОтметкиВремениРегистровКонстант = РезультатЗапроса[РезультатЗапроса.ВГраница()];
	
	Возврат ДанныеДляОбновления;
	
КонецФункции

// Возвращает данные очереди отметок времени.
//
// Возвращаемое значение:
//	Структура:
//		* МенеджерВременныхТаблиц, Неопределено - Менеджер временных таблиц, содержащий данные очереди. 
//		* Количество - Число - Количество, выбранных из очереди, данных.
//
Функция ДанныеОчереди()
	
	ДанныеОчереди = Новый Структура;
	ДанныеОчереди.Вставить("МенеджерВременныхТаблиц", Неопределено);
	ДанныеОчереди.Вставить("Количество", 0);
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтметкиВремениОчередь1");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтметкиВремениОчередь2");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтметкиВремениОчередь3");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтметкиВремениСсылочныхОбъектов");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтметкиВремениРегистровКонстант");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1000
			|	АВТОНОМЕРЗАПИСИ() КАК НомерЗаписи,
			|	ДанныеОчереди.Окно КАК Окно,
			|	ДанныеОчереди.Отметка КАК Отметка,
			|	ДанныеОчереди.ИдентификаторКлюча КАК ИдентификаторКлюча,
			|	ДанныеОчереди.ТипКлюча КАК ТипКлюча,
			|	ДанныеОчереди.Объект КАК Объект,
			|	ДанныеОчереди.Удаление КАК Удаление,
			|	ДанныеОчереди.Источник КАК Источник,
			|	ДанныеОчереди.ВидКлюча КАК ВидКлюча,
			|	ДанныеОчереди.ЗначенияКлюча КАК ЗначенияКлюча
			|ПОМЕСТИТЬ ДанныеОчереди
			|ИЗ
			|	(ВЫБРАТЬ ПЕРВЫЕ 1000
			|		1 КАК Окно,
			|		Очередь.Отметка КАК Отметка,
			|		Очередь.ИдентификаторКлюча КАК ИдентификаторКлюча,
			|		Очередь.ТипКлюча КАК ТипКлюча,
			|		Очередь.Объект КАК Объект,
			|		Очередь.Удаление КАК Удаление,
			|		Очередь.Источник КАК Источник,
			|		Очередь.ВидКлюча КАК ВидКлюча,
			|		Очередь.ЗначенияКлюча КАК ЗначенияКлюча
			|	ИЗ
			|		РегистрСведений.ОтметкиВремениОчередь1 КАК Очередь
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ ПЕРВЫЕ 1000
			|		2,
			|		Очередь.Отметка,
			|		Очередь.ИдентификаторКлюча,
			|		Очередь.ТипКлюча,
			|		Очередь.Объект,
			|		Очередь.Удаление,
			|		Очередь.Источник,
			|		Очередь.ВидКлюча,
			|		Очередь.ЗначенияКлюча
			|	ИЗ
			|		РегистрСведений.ОтметкиВремениОчередь2 КАК Очередь
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ ПЕРВЫЕ 1000
			|		3,
			|		Очередь.Отметка,
			|		Очередь.ИдентификаторКлюча,
			|		Очередь.ТипКлюча,
			|		Очередь.Объект,
			|		Очередь.Удаление,
			|		Очередь.Источник,
			|		Очередь.ВидКлюча,
			|		Очередь.ЗначенияКлюча
			|	ИЗ
			|		РегистрСведений.ОтметкиВремениОчередь3 КАК Очередь
			|	
			|	УПОРЯДОЧИТЬ ПО
			|		Отметка) КАК ДанныеОчереди
			|
			|УПОРЯДОЧИТЬ ПО
			|	Отметка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЕСТЬNULL(МАКСИМУМ(Т.Граница), -1) КАК Граница
			|ПОМЕСТИТЬ Границы
			|ИЗ
			|	(ВЫБРАТЬ
			|		Т.Граница КАК Граница
			|	ИЗ
			|		(ВЫБРАТЬ ПЕРВЫЕ 1
			|			Т.Граница КАК Граница
			|		ИЗ
			|			РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК Т
			|		
			|		УПОРЯДОЧИТЬ ПО
			|			Граница УБЫВ) КАК Т
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Т.Граница
			|	ИЗ
			|		(ВЫБРАТЬ ПЕРВЫЕ 1
			|			Т.Граница КАК Граница
			|		ИЗ
			|			РегистрСведений.ОтметкиВремениРегистровКонстант КАК Т
			|		
			|		УПОРЯДОЧИТЬ ПО
			|			Т.Граница УБЫВ) КАК Т) КАК Т
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЕСТЬNULL(КОЛИЧЕСТВО(ДанныеОчереди.Отметка), 0) КАК Количество
			|ИЗ
			|	ДанныеОчереди КАК ДанныеОчереди";
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"ВЫБРАТЬ ПЕРВЫЕ 1000",
			СтрШаблон("ВЫБРАТЬ ПЕРВЫЕ %1",
				Формат(МаксимальноеКоличествоЗаписейНабораОтметокВремени(), "ЧДЦ=0; ЧГ=0")));
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ДанныеОчереди.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
			ДанныеОчереди.Количество = Выборка.Количество;
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ЗаписатьОшибкуОбработкиОчередиОтметокВремени(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат ДанныеОчереди;
	
КонецФункции

// Возвращает данные для блокировки таблиц отметок времени.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий данные очереди.
//
// Возвращаемое значение:
//	Структура:
//		* ОтметкиВремениРегистровКонстант - ТаблицаЗначений.
//		* ОтметкиВремениСсылочныхОбъектов - ТаблицаЗначений.
//		* ОтметкиВремениОчередь1 - ТаблицаЗначений.
//		* ОтметкиВремениОчередь2 - ТаблицаЗначений.
//		* ОтметкиВремениОчередь3 - ТаблицаЗначений.
//
Функция ДанныеДляБлокировки(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДанныеОчереди.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ДанныеОчереди.ТипКлюча КАК ТипКлюча,
		|	ДанныеОчереди.Объект КАК Объект,
		|	ДанныеОчереди.Отметка КАК Отметка
		|ИЗ
		|	ДанныеОчереди КАК ДанныеОчереди
		|ГДЕ
		|	ДанныеОчереди.Окно = 3
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеОчереди.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ДанныеОчереди.ТипКлюча КАК ТипКлюча,
		|	ДанныеОчереди.Объект КАК Объект,
		|	ДанныеОчереди.Отметка КАК Отметка
		|ИЗ
		|	ДанныеОчереди КАК ДанныеОчереди
		|ГДЕ
		|	ДанныеОчереди.Окно = 2
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеОчереди.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ДанныеОчереди.ТипКлюча КАК ТипКлюча,
		|	ДанныеОчереди.Объект КАК Объект,
		|	ДанныеОчереди.Отметка КАК Отметка
		|ИЗ
		|	ДанныеОчереди КАК ДанныеОчереди
		|ГДЕ
		|	ДанныеОчереди.Окно = 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеОчереди.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ДанныеОчереди.ТипКлюча КАК ТипКлюча
		|ИЗ
		|	ДанныеОчереди КАК ДанныеОчереди
		|ГДЕ
		|	ДанныеОчереди.ВидКлюча = ЗНАЧЕНИЕ(Перечисление.ВидыКлючейОтметокВремени.КлючСсылочногоОбъекта)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеОчереди.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ДанныеОчереди.ТипКлюча КАК ТипКлюча,
		|	ДанныеОчереди.Объект КАК Объект
		|ИЗ
		|	ДанныеОчереди КАК ДанныеОчереди
		|ГДЕ
		|	ДанныеОчереди.ВидКлюча <> ЗНАЧЕНИЕ(Перечисление.ВидыКлючейОтметокВремени.КлючСсылочногоОбъекта)");
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеДляБлокировки = Новый Структура;
	ДанныеДляБлокировки.Вставить(
		"ОтметкиВремениРегистровКонстант", РезультатЗапроса[РезультатЗапроса.ВГраница()].Выгрузить());
	ДанныеДляБлокировки.Вставить(
		"ОтметкиВремениСсылочныхОбъектов", РезультатЗапроса[РезультатЗапроса.ВГраница() - 1].Выгрузить());
	ДанныеДляБлокировки.Вставить(
		"ОтметкиВремениОчередь1", РезультатЗапроса[РезультатЗапроса.ВГраница() - 2].Выгрузить());
	ДанныеДляБлокировки.Вставить(
		"ОтметкиВремениОчередь2", РезультатЗапроса[РезультатЗапроса.ВГраница() - 3].Выгрузить());
	ДанныеДляБлокировки.Вставить(
		"ОтметкиВремениОчередь3", РезультатЗапроса[РезультатЗапроса.ВГраница() - 4].Выгрузить());
	
	Возврат ДанныеДляБлокировки;
	
КонецФункции

// Возвращает список таблиц, в которых возможно удаление отметок времени, во время обработки очереди.
//
// Возвращаемое значение:
//	Структура:
//		* ОтметкиВремениОчередь1 - РезультатЗапроса - Отметки времени очередь 1.
//								 - Неопределено -
//		* ОтметкиВремениОчередь2 - РезультатЗапроса - Отметки времени очередь 2.
//								 - Неопределено -
//		* ОтметкиВремениОчередь3 - РезультатЗапроса - Отметки времени очередь 3.
//								 - Неопределено -
//		* ОтметкиВремениСсылочныхОбъектов - РезультатЗапроса - Отметки времени ссылочных объектов.
//										  - Неопределено -
//		* ОтметкиВремениРегистровКонстант - РезультатЗапроса - Отметки времени регистров, констант.
//										  - Неопределено -
//
Функция СписокТаблицДляУдаленияОтметокПриОбработкеОчереди()
	
	Таблицы = Новый Структура;
	Таблицы.Вставить("ОтметкиВремениОчередь1", Неопределено);
	Таблицы.Вставить("ОтметкиВремениОчередь2", Неопределено);
	Таблицы.Вставить("ОтметкиВремениОчередь3", Неопределено);
	Таблицы.Вставить("ОтметкиВремениСсылочныхОбъектов", Неопределено);
	Таблицы.Вставить("ОтметкиВремениРегистровКонстант", Неопределено);
	
	Возврат Таблицы;
	
КонецФункции

// Возвращает список таблиц, в которых возможно добавление отметок времени, во время обработки очереди.
//
// Возвращаемое значение:
//	Структура:
//		* ОтметкиВремениСсылочныхОбъектов - РезультатЗапроса - Отметки времени ссылочных объектов.
//										  - Неопределено -
//		* ОтметкиВремениРегистровКонстант - РезультатЗапроса - Отметки времени регистров, констант.
//										  - Неопределено -
//
Функция СписокТаблицДляДобавленияОтметокПриОбработкеОчереди()
	
	Таблицы = Новый Структура;
	Таблицы.Вставить("ОтметкиВремениСсылочныхОбъектов", Неопределено);
	Таблицы.Вставить("ОтметкиВремениРегистровКонстант", Неопределено);
	
	Возврат Таблицы;
	
КонецФункции

// Фиксирует ошибку обработки очрееди отметок времени.
//
// Параметры:
//	ИнформацияОбОшибке - Строка - Информация об ошибке.
//
Процедура ЗаписатьОшибкуОбработкиОчередиОтметокВремени(ИнформацияОбОшибке)
	
	ЗаписьЖурналаРегистрации(
		СобытиеЖурналаРегистрацииОбработкаОчереди(),
		УровеньЖурналаРегистрации.Ошибка,,,
		ИнформацияОбОшибке);
	
КонецПроцедуры

// Возвращает событие журнала регистрации обработки очереди отметок времени.
//
// Возвращаемое значение:
//	Строка - Событие журнала регистрации.
//
Функция СобытиеЖурналаРегистрацииОбработкаОчереди()
	
	Возврат НСтр("ru = 'ОтметкиВремени.ОбработкаОчереди'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

#КонецОбласти

#Область ВыборкаИКонтрольИзменений

// Выбирает порцию изменений, согласно переданным параметрам.
//
// Параметры:
//	Отметка - Число - Отметка, начиная с которой необходимо выбрать изменения.
//	ПараметрыВыборки - см. ПараметрыВыборкиИзменений
//
// Возвращаемое значение:
//	ТаблицаЗначений:
//		* ИдентификаторКлюча - УникальныйИдентификатор - Идентификатор ключа отметки времени.
//		* ТипКлюча - СправочникСсылка.ИдентификаторыОбъектовМетаданных -
//				   - СправочникСсылка.ИдентификаторыОбъектовРасширений - Тип ключа отметки времени.
//		* Объект - СправочникСсылка.ИдентификаторыОбъектовМетаданных -
//				 - СправочникСсылка.ИдентификаторыОбъектовРасширений - Тип объекта, для которого зарегистрирована отметка.
//		* Отметка - Число - Отметка времени.
//		* Граница - Число - Граница отметок времени.
//		* Удаление - Булево - Признак удаления данных.
//		* Источник - ОпределяемыйТип.ИсточникиДляОтметокВремени - Источник отметки времени.
//		* ВидКлюча - ПеречислениеСсылка.ВидыКлючейОтметокВремени - Вид ключа отметки времени.
//		* ХранилищеКлюча - ХранилищеЗначения - Хранилище значений, содержащее отбор исходного набора записей.
//
Функция РезультатВыборкиИзменений(Отметка, ПараметрыВыборки)
	
	ТекстЗапроса = Новый Массив; // Массив Из Строка
	ТекстЗапроса.Добавить(
		"ВЫБРАТЬ
		|	&Граница КАК Нижняя, ЕСТЬNULL(МАКСИМУМ(Т.Граница), 0) КАК Верхняя
		|ПОМЕСТИТЬ Границы
		|ИЗ
		|	(ВЫБРАТЬ ПЕРВЫЕ 500
		|		Т.Граница КАК Граница
		|	ИЗ	РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК Т
		|	ГДЕ
		|		Т.Граница > &Граница
		|		//ОТБОР_ОТМЕТКИ_ХРАНЕНИЕ_ССЫЛОЧНЫЕ_ОБЪЕКТЫ
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 500
		|		Т.Граница КАК Граница
		|	ИЗ	РегистрСведений.ОтметкиВремениРегистровКонстант КАК Т
		|	ГДЕ
		|		Т.Граница > &Граница
		|		//ОТБОР_ОТМЕТКИ_ХРАНЕНИЕ_РЕГИСТРЫ
		|	УПОРЯДОЧИТЬ ПО Граница
		|	) КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	Т.ТипКлюча КАК ТипКлюча,
		|	Т.ТипКлюча КАК Объект,
		|	Т.Отметка КАК Отметка,
		|	Т.Граница КАК Граница,
		|	Т.Удаление КАК Удаление,
		|	Т.Источник КАК Источник,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыКлючейОтметокВремени.КлючСсылочногоОбъекта) КАК ВидКлюча,
		|	NULL КАК ЗначенияКлюча
		|ПОМЕСТИТЬ ВсеИзменения
		|ИЗ
		|	РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК Т
		|	СОЕДИНЕНИЕ Границы КАК Границы
		|	ПО ИСТИНА
		|ГДЕ
		|	Т.Граница >= Границы.Нижняя И Т.Граница <= Границы.Верхняя
		|	//ОТБОР_ОТМЕТКИ_ХРАНЕНИЕ_ССЫЛОЧНЫЕ_ОБЪЕКТЫ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.ИдентификаторКлюча,
		|	Т.ТипКлюча,
		|	Т.Объект,
		|	Т.Отметка,
		|	Т.Граница,
		|	Т.Удаление,
		|	Т.Источник,
		|	Т.ВидКлюча,
		|	Т.ЗначенияКлюча
		|ИЗ
		|	РегистрСведений.ОтметкиВремениРегистровКонстант КАК Т
		|	СОЕДИНЕНИЕ Границы КАК Границы
		|	ПО ИСТИНА
		|ГДЕ
		|	Т.Граница >= Границы.Нижняя И Т.Граница <= Границы.Верхняя
		|	//ОТБОР_ОТМЕТКИ_ХРАНЕНИЕ_РЕГИСТРЫ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.ИдентификаторКлюча,
		|	Т.ТипКлюча,
		|	Т.Объект,
		|	Т.Отметка,
		|	0,
		|	Т.Удаление,
		|	Т.Источник,
		|	Т.ВидКлюча,
		|	Т.ЗначенияКлюча
		|ИЗ
		|	РегистрСведений.ОтметкиВремениОчередь1 КАК Т
		|//ОТБОР_ОТМЕТКИ_ОЧЕРЕДЬ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.ИдентификаторКлюча,
		|	Т.ТипКлюча,
		|	Т.Объект,
		|	Т.Отметка,
		|	0,
		|	Т.Удаление,
		|	Т.Источник,
		|	Т.ВидКлюча,
		|	Т.ЗначенияКлюча
		|ИЗ
		|	РегистрСведений.ОтметкиВремениОчередь2 КАК Т
		|//ОТБОР_ОТМЕТКИ_ОЧЕРЕДЬ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.ИдентификаторКлюча,
		|	Т.ТипКлюча,
		|	Т.Объект,
		|	Т.Отметка,
		|	0,
		|	Т.Удаление,
		|	Т.Источник,
		|	Т.ВидКлюча,
		|	Т.ЗначенияКлюча
		|ИЗ
		|	РегистрСведений.ОтметкиВремениОчередь3 КАК Т
		|//ОТБОР_ОТМЕТКИ_ОЧЕРЕДЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	Т.ТипКлюча КАК ТипКлюча,
		|	Т.Объект КАК Объект,
		|	МАКСИМУМ(Т.Отметка) КАК Отметка
		|ПОМЕСТИТЬ АктуальныеИзменения
		|ИЗ
		|	ВсеИзменения КАК Т
		|
		|СГРУППИРОВАТЬ ПО
		|	Т.ИдентификаторКлюча,
		|	Т.ТипКлюча,
		|	Т.Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	Т.ТипКлюча КАК ТипКлюча,
		|	Т.Объект КАК Объект,
		|	Т.Отметка КАК Отметка,
		|	Т.Граница КАК Граница,
		|	Т.Источник КАК Источник,
		|	Т.Удаление КАК Удаление,
		|	Т.ВидКлюча КАК ВидКлюча
		|ПОМЕСТИТЬ Изменённые
		|ИЗ
		|	ВсеИзменения КАК Т
		|		СОЕДИНЕНИЕ АктуальныеИзменения КАК АктуальныеИзменения
		|		ПО Т.ИдентификаторКлюча = АктуальныеИзменения.ИдентификаторКлюча
		|			И Т.ТипКлюча = АктуальныеИзменения.ТипКлюча
		|			И Т.Объект = АктуальныеИзменения.Объект
		|			И Т.Отметка = АктуальныеИзменения.Отметка
		|		//ОТБОР_РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ
		|//УСЛОВИЯ_ИЗМЕНЕННЫЕ
		|");
		
	Если ПараметрыВыборки.МенеджерВременныхТаблиц = Неопределено Тогда
		
		ТекстЗапроса.Добавить(
			"ВЫБРАТЬ
			|	Т.ИдентификаторКлюча КАК ИдентификаторКлюча,
			|	Т.ТипКлюча КАК ТипКлюча,
			|	Т.Объект КАК Объект,
			|	Т.Отметка КАК Отметка,
			|	Т.Граница КАК Граница,
			|	Т.Источник КАК Источник,
			|	Т.Удаление КАК Удаление,
			|	Т.ВидКлюча КАК ВидКлюча,
			|	ВсеИзменения.ЗначенияКлюча КАК ХранилищеКлюча
			|ИЗ
			|	Изменённые КАК Т
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеИзменения КАК ВсеИзменения
			|		ПО Т.ИдентификаторКлюча = ВсеИзменения.ИдентификаторКлюча
			|			И Т.ТипКлюча = ВсеИзменения.ТипКлюча
			|			И Т.Объект = ВсеИзменения.Объект
			|			И Т.Отметка = ВсеИзменения.Отметка
			|			И Т.ВидКлюча = ЗНАЧЕНИЕ(Перечисление.ВидыКлючейОтметокВремени.КлючНабораИдентификаторНабораЗаписей)
			|			И (Т.Удаление = ИСТИНА)
			|			И (ВсеИзменения.Удаление = ИСТИНА)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Отметка,
			|	Удаление УБЫВ");
		
	Иначе
		
		ТекстЗапроса.Добавить(
			"ВЫБРАТЬ
			|	Т.ИдентификаторКлюча КАК ИдентификаторКлюча,
			|	Т.ТипКлюча КАК ТипКлюча,
			|	Т.Объект КАК Объект,
			|	Т.Отметка КАК Отметка
			|ПОМЕСТИТЬ _РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ
			|ИЗ
			|	РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ КАК Т
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Т.ИдентификаторКлюча,
			|	Т.ТипКлюча,
			|	Т.Объект,
			|	Т.Отметка
			|ИЗ
			|	Изменённые КАК Т");
		
		ТекстЗапроса.Добавить(
			"УНИЧТОЖИТЬ РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ");
		
		ТекстЗапроса.Добавить(
			"ВЫБРАТЬ
			|	Т.ИдентификаторКлюча КАК ИдентификаторКлюча,
			|	Т.ТипКлюча КАК ТипКлюча,
			|	Т.Объект КАК Объект,
			|	Т.Отметка КАК Отметка
			|ПОМЕСТИТЬ РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ
			|ИЗ
			|	_РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ КАК Т"); 
					
		ТекстЗапроса.Добавить(
			"ВЫБРАТЬ
			|	Т.ИдентификаторКлюча КАК ИдентификаторКлюча,
			|	Т.ТипКлюча КАК ТипКлюча,
			|	Т.Объект КАК Объект,
			|	Т.Отметка КАК Отметка,
			|	Т.Граница КАК Граница,
			|	Т.Источник КАК Источник,
			|	Т.Удаление КАК Удаление,
			|	Т.ВидКлюча КАК ВидКлюча,
			|	ВсеИзменения.ЗначенияКлюча КАК ХранилищеКлюча
			|ИЗ
			|	Изменённые КАК Т
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеИзменения КАК ВсеИзменения
			|		ПО Т.ИдентификаторКлюча = ВсеИзменения.ИдентификаторКлюча
			|			И Т.ТипКлюча = ВсеИзменения.ТипКлюча
			|			И Т.Объект = ВсеИзменения.Объект
			|			И Т.Отметка = ВсеИзменения.Отметка
			|			И Т.ВидКлюча = ЗНАЧЕНИЕ(Перечисление.ВидыКлючейОтметокВремени.КлючНабораИдентификаторНабораЗаписей)
			|			И (Т.Удаление = ИСТИНА)
			|			И (ВсеИзменения.Удаление = ИСТИНА)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Отметка,
			|	Удаление УБЫВ");
		
	КонецЕсли;
	
	Запрос =
		Новый Запрос(СтрСоединить(ТекстЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов()));
	Запрос.УстановитьПараметр("Граница", Отметка);
	
	Если ЗначениеЗаполнено(ПараметрыВыборки.Объект) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ОТБОР_ОТМЕТКИ_ХРАНЕНИЕ_ССЫЛОЧНЫЕ_ОБЪЕКТЫ", "И Т.ТипКлюча В (&Объект)");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ОТБОР_ОТМЕТКИ_ХРАНЕНИЕ_РЕГИСТРЫ", "И Т.Объект В (&Объект)");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ОТБОР_ОТМЕТКИ_ОЧЕРЕДЬ", "ГДЕ Т.Объект В (&Объект)");
		
		Запрос.УстановитьПараметр("Объект", ПараметрыВыборки.Объект);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыВыборки.РазмерПорции) И Не ПараметрыВыборки.РазмерПорции = 1000 Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"ПЕРВЫЕ 500",
			СтрШаблон("ПЕРВЫЕ %1", Формат(Цел(ПараметрыВыборки.РазмерПорции / 2), "ЧГ=0")));
			
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"ПЕРВЫЕ 1000",
			СтрШаблон("ПЕРВЫЕ %1", Формат(ПараметрыВыборки.РазмерПорции, "ЧГ=0")));
	КонецЕсли;
	
	УсловияИзменённые = Новый Массив; // Массив Из Строка
	
	Если ПараметрыВыборки.МенеджерВременныхТаблиц <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"//ОТБОР_РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ",
			"ЛЕВОЕ СОЕДИНЕНИЕ РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ КАК _РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ
			|	ПО Т.ИдентификаторКлюча = _РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ.ИдентификаторКлюча
			|		И Т.ТипКлюча = _РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ.ТипКлюча
			|		И Т.Объект = _РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ.Объект
			|		И Т.Отметка = _РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ.Отметка");
		
		УсловияИзменённые.Добавить("_РАНЕЕ_ВЫБРАННЫЕ_ИЗМЕНЕНИЯ.ИдентификаторКлюча ЕСТЬ NULL");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыВыборки.Источник) Тогда
		УсловияИзменённые.Добавить("Т.Источник <> &Источник");
		Запрос.УстановитьПараметр("Источник", ПараметрыВыборки.Источник);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УсловияИзменённые) Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"//УСЛОВИЯ_ИЗМЕНЕННЫЕ",
			СтрШаблон("ГДЕ %1", СтрСоединить(УсловияИзменённые, " И ")));
	КонецЕсли;
	
	Если ПараметрыВыборки.МенеджерВременныхТаблиц <> Неопределено Тогда
		Запрос.МенеджерВременныхТаблиц = ПараметрыВыборки.МенеджерВременныхТаблиц;
	КонецЕсли;
	
	Изменения = Запрос.Выполнить().Выгрузить();
	
	Возврат Изменения;
	
КонецФункции

#КонецОбласти

#КонецОбласти
