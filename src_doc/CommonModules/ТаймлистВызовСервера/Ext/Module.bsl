
#Область ПрограммныйИнтерфейс

// см. Таймлист.ТаймлистИспользуется
//
Функция ТаймлистИспользуется() Экспорт
	
	Возврат Таймлист.ТаймлистИспользуется();
	
КонецФункции

// Возвращает разрешение на добавление больших аудио- и видеофайлов в программу.
// 
// Параметры:
//  ИмяФайла - Строка
//  Размер - Строка - размер файла в мегабайтах.
// 
// Возвращаемое значение:
//  Булево - Истина, если разрешено.
// 
Функция РазрешеноДобавлятьБольшиеФайлы(Знач ИмяФайла, Знач Размер) Экспорт
	
	Если Не ТаймлистПовтИсп.ДоступноРаспознаваниеПоЗапросу() Или Размер > 1024 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Помещает файл Таймлист в очередь обработки.
// 
// Параметры:
//  Файл - СправочникСсылка.Файлы, СправочникСсылка.ВерсииФайлов - обрабатываемый файл.
//  Задание - ПеречислениеСсылка.ТаймлистТипыЗаданий - выполняемое задание.
//  Параметры - Структура - параметры выполнения задания.
//  
// Возвращаемое значение:
//  Булево - Истина, если передаваемый файл был расширения Таймлист.
// 
Функция НачатьОбработкуФайла(Знач Файл, Знач Задание, Знач Параметры = Неопределено) Экспорт
	
	ЭтоФайлТаймлист = Ложь;
	
	Если ТипЗнч(Файл) = Тип("СправочникСсылка.Файлы") Тогда
		
		РеквизитыВерсииФайла = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Файл,
			"ТекущаяВерсия, ТекущаяВерсияРасширение");
		
		Если ТаймлистКлиентСервер.ЭтоРасширениеФайлаТаймлист(РеквизитыВерсииФайла.ТекущаяВерсияРасширение) Тогда
			ЭтоФайлТаймлист = Истина;
			Таймлист.ПоместитьФайлВОчередьОбработки(РеквизитыВерсииФайла.ТекущаяВерсия, Задание, Параметры);
		КонецЕсли;
		
	Иначе
		
		РасширениеВерсииФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Файл, "Расширение");
		
		Если ТаймлистКлиентСервер.ЭтоРасширениеФайлаТаймлист(РасширениеВерсииФайла) Тогда
			ЭтоФайлТаймлист = Истина;
			Таймлист.ПоместитьФайлВОчередьОбработки(Файл, Задание, Параметры);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ЭтоФайлТаймлист;
	
КонецФункции

// Удаляет данные работы сервиса по переданной версии файла, а также выполняет очистку в самом сервисе.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов - обрабатываемый файл.
//  ИдентификаторЗадания - Строка
// 
Процедура УдалитьДанныеФайла(Знач ВерсияФайла, Знач ИдентификаторЗадания = "") Экспорт
	
	Таймлист.УдалитьДанныеФайла(ВерсияФайла, ИдентификаторЗадания);
	
КонецПроцедуры

// Редактирует данные.
// 
// Параметры:
//  Файл - СправочникСсылка.Файлы
// 
// Возвращаемое значение:
//  СправочникСсылка.Пользователи
//
Функция РедактируетДанные(Знач Файл) Экспорт
	
	//@skip-check bsl-legacy-check-string-literal
	ВерсияФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Файл, "ТекущаяВерсия");
	
	Возврат РегистрыСведений.ТаймлистДанныеРаботыСервиса.Данные(ВерсияФайла, "Редактирует").Редактирует;
	
КонецФункции

// Выполняет проверку подключения к сервису Таймлист по указанным логину и паролю.
//
// Параметры:
//  Логин - Строка
//  Пароль - Строка
//  Порт - Число
//  ЗащищенноеСоединение - Булево
//
// Возвращаемое значение:
//  Булево - Истина, если подключение было успешно установлено
// 
Функция СервисПоДаннымАвторизацииПодключен(Знач Логин, Знач Пароль, Знач Порт, Знач ЗащищенноеСоединение) Экспорт
	
	Возврат Таймлист.СервисПоДаннымАвторизацииПодключен(Логин, Пароль, Порт, ЗащищенноеСоединение);
	
КонецФункции

// Возвращает данные файла, необходимые для формирования предпросмотра.
// 
// Параметры:
//  Файл - СправочникСсылка.Файлы
// 
// Возвращаемое значение:
//  Структура:
//   * Ссылка - СправочникСсылка.Файлы
//   * Задание - ПеречислениеСсылка.ТаймлистТипыЗаданий
//   * Статус - ПеречислениеСсылка.ТаймлистСтатусы
//   * ТекущаяВерсия - СправочникСсылка.ВерсииФайлов
//   * ТекущаяВерсияРазмер - Число
//   * Расшифровка - ХранилищеЗначения
//   * Автопротокол - ХранилищеЗначения
//   * СледующиеШаги - Строка
// 
Функция ДанныеФайлаДляОткрытия(Знач Файл) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеФайла = Новый Структура;
	ДанныеФайла.Вставить("Ссылка");
	ДанныеФайла.Вставить("ТекущаяВерсия");
	ДанныеФайла.Вставить("ТекущаяВерсияРазмер");
	ДанныеФайла.Вставить("Задание");
	ДанныеФайла.Вставить("Статус");
	ДанныеФайла.Вставить("Расшифровка");
	ДанныеФайла.Вставить("Автопротокол");
	ДанныеФайла.Вставить("СледующиеШаги");
	ДанныеФайла.Вставить("ИдентификаторЗадания");
	ДанныеФайла.Вставить("ТекстОшибки");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Файлы.Ссылка,
		|	Файлы.ТекущаяВерсия,
		|	Файлы.ТекущаяВерсияРазмер,
		|	ЕСТЬNULL(ТаймлистФайлыВОбработке.Задание,
		|		ЗНАЧЕНИЕ(Перечисление.ТаймлистТипыЗаданий.ПустаяСсылка)) КАК Задание,
		|	ЕСТЬNULL(ТаймлистДанныеРаботыСервиса.Статус,
		|		ЗНАЧЕНИЕ(Перечисление.ТаймлистСтатусы.ПустаяСсылка)) КАК Статус,
		|	ЕСТЬNULL(ТаймлистДанныеРаботыСервиса.Расшифровка, Неопределено) КАК Расшифровка,
		|	ЕСТЬNULL(ТаймлистДанныеРаботыСервиса.РасшифровкаИзмененная, Неопределено) КАК РасшифровкаИзмененная,
		|	ЕСТЬNULL(ТаймлистДанныеРаботыСервиса.Автопротокол, Неопределено) КАК Автопротокол,
		|	ЕСТЬNULL(ТаймлистДанныеРаботыСервиса.АвтопротоколИзмененный, Неопределено) КАК АвтопротоколИзмененный,
		|	ЕСТЬNULL(ТаймлистДанныеРаботыСервиса.СледующиеШаги, """") КАК СледующиеШаги,
		|	ЕСТЬNULL(ТаймлистДанныеРаботыСервиса.ИдентификаторЗадания, """") КАК ИдентификаторЗадания,
		|	ЕСТЬNULL(ТаймлистОшибкиРаботыССервисом.ТекстОшибки, """") КАК ТекстОшибки
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаймлистДанныеРаботыСервиса КАК ТаймлистДанныеРаботыСервиса
		|		ПО (ТаймлистДанныеРаботыСервиса.ВерсияФайла = Файлы.ТекущаяВерсия)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаймлистФайлыВОбработке КАК ТаймлистФайлыВОбработке
		|		ПО (ТаймлистФайлыВОбработке.ВерсияФайла = Файлы.ТекущаяВерсия)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТаймлистОшибкиРаботыССервисом КАК ТаймлистОшибкиРаботыССервисом
		|		ПО (ТаймлистОшибкиРаботыССервисом.ВерсияФайла = Файлы.ТекущаяВерсия)
		|ГДЕ
		|	Файлы.Ссылка = &Файл";
	
	Запрос.УстановитьПараметр("Файл", Файл);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(ДанныеФайла, Выборка,, "Расшифровка, Автопротокол");
		
		Если Выборка.РасшифровкаИзмененная <> Неопределено И Выборка.РасшифровкаИзмененная.Получить() <> Неопределено Тогда
			ДанныеФайла.Расшифровка = Выборка.РасшифровкаИзмененная;
		Иначе
			ДанныеФайла.Расшифровка = Выборка.Расшифровка;
		КонецЕсли;
		
		Если Выборка.АвтопротоколИзмененный <> Неопределено И Выборка.АвтопротоколИзмененный.Получить() <> Неопределено Тогда
			ДанныеФайла.Автопротокол = Выборка.АвтопротоколИзмененный;
		Иначе
			ДанныеФайла.Автопротокол = Выборка.Автопротокол;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДанныеФайла;
	
КонецФункции

// Заполняет данные выбора участников.
// 
// Параметры:
//  Текст - Строка
//  ДанныеВыбора - СписокЗначений из СправочникСсылка.Сотрудники
//  ЗаполнитьСотрудников - Булево
// 
Процедура ЗаполнитьДанныеВыбораУчастников(Знач Текст, ДанныеВыбора, Знач ЗаполнитьСотрудников = Ложь) Экспорт
	
	Если ЗаполнитьСотрудников Тогда
		ДанныеВыбораСотрудников = Сотрудники.СформироватьДанныеВыбора(Текст);
		Для Каждого ЭлементДанные Из ДанныеВыбораСотрудников Цикл
			ДанныеВыбора.Добавить(ЭлементДанные.Значение, ЭлементДанные.Представление);
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыВыбораКонтактныхЛиц = Новый Структура("СтрокаПоиска, Отбор", Текст);
	ДанныеВыбораКонтактныхЛиц = Справочники.КонтактныеЛица.ПолучитьДанныеВыбора(ПараметрыВыбораКонтактныхЛиц);
	Для Каждого ЭлементДанные Из ДанныеВыбораКонтактныхЛиц Цикл
		ДанныеВыбора.Добавить(ЭлементДанные.Значение, ЭлементДанные.Представление);
	КонецЦикла;
	
КонецПроцедуры

// Формирует файл автопротокола.
// 
// Параметры:
//  Автопротокол - ФорматированныйДокумент
//               - Строка - HTML представление
// 
// Возвращаемое значение:
//  Строка - адрес хранилища, куда помещается сформированный файл.
// 
Функция АвтопротоколДокументWord(Автопротокол) Экспорт
	
	АвтопротоколДляСозданияФайла = "";
	
	АвтопротоколБезСлужебныхТегов = Таймлист.HTMLПредставлениеБезСлужебныхТегов(Автопротокол);
	АвтопротоколДляСозданияФайла = ОбщегоНазначенияДокументооборот.ФорматированнаяСтрокаИзHTML(
		АвтопротоколБезСлужебныхТегов);
	
	МакетДокумента = ПолучитьОбщийМакет("ТаймлистАвтопротокол");
	Макет = УправлениеПечатью.ИнициализироватьМакетОфисногоДокумента(МакетДокумента, Неопределено);
	
	ОписаниеОбластей = Новый Структура;
	
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Текст1", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Текст2", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Заголовок", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Текст", "Общая");
	
	ПечатнаяФорма = УправлениеПечатью.ИнициализироватьПечатнуюФорму(Неопределено, Неопределено, Макет);
	
	Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей.Текст1);
	
	Данные = Новый Структура;
	Текст1 = СтрЗаменить(АвтопротоколДляСозданияФайла,"###", "»");
	Текст1 = СтрЗаменить(Текст1,"***", "«");
	Текст1 = СтрЗаменить(Текст1,"«Ключевые моменты «","");
	Текст1 = СтрЗаменить(Текст1,
		"(данные этого раздела используются для заполнения протокола мероприятия и пунктов исполнения)", "");
	ДляТекст2 = Текст1;
	Часть2 = СтрНайти(Текст1, "Следующие шаги", НаправлениеПоиска.СНачала, 1, 1);
	Текст1 = Лев(Текст1, Часть2 - 2);
	Данные.Вставить("Текст1", Текст1);
	
	УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, Данные);
	
	Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей.Текст2);
	
	ДанныеТекст = Новый Структура;
	Текст2Длина = СтрДлина(ДляТекст2);
	Текст2 = Прав(ДляТекст2, Текст2Длина - Часть2 - 16);
	ДанныеТекст.Вставить("Текст2", Текст2);
	
	УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеТекст);

	АдресХранилищаПечатнойФормы = УправлениеПечатью.СформироватьДокумент(ПечатнаяФорма);
	
	УправлениеПечатью.ОчиститьСсылки(ПечатнаяФорма);
	УправлениеПечатью.ОчиститьСсылки(Макет);
	
	Возврат АдресХранилищаПечатнойФормы;
	
КонецФункции

#КонецОбласти
