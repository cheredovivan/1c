
#Область СлужебныйПрограммныйИнтерфейс

// Если документ на распознавании или не пройдена верификация - вернет описание ошибки.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  МассивОшибок - Массив из Строка
Процедура ПроверитьДокументНаРаспознавании(Форма, МассивОшибок) Экспорт
	
	Если ТипЗнч(Форма.Объект.Ссылка) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда
		
		Если МожнозапуститьВОбработку(Форма) = Ложь Тогда
			
			ТекстОшибки = НСтр("ru = 'Не завершен процесс распознавания документа или документ не верифицирован.'", 
				ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
			МассивОшибок.Добавить(ТекстОшибки);
			
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры


// Открывает форму верификации.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
//  Форма - ФормаКлиентскогоПриложения
Процедура ОткрытьФормуВерификации(ДокументСсылка, Форма) Экспорт
	
	ДанныеВерификации = РаспознаваниеДокументовСлужебныйКлиентСервер.ДанныеВерифицируемыхРеквизитов(Форма);
	
	ЗначенияРеквизитов = 
		РаспознаваниеДокументовСлужебныйКлиентСервер.ТекущиеЗначенияВерифицируемыхРеквизитовФормы(Форма);
	
	ТребуетПроверки = РаспознаваниеДокументовСлужебныйКлиентСервер.НужнаВерификация(Форма);
	
	НеРаспознан = РаспознаваниеДокументовСлужебныйКлиентСервер.ДанныеДокументаНеРаспознаны(Форма);
	
	ПараметрыДляПередачи = Новый Структура;
	ПараметрыДляПередачи.Вставить("ДанныеВерификации", ДанныеВерификации);
	ПараметрыДляПередачи.Вставить("ЗначенияРеквизитовФормы", ЗначенияРеквизитов);
	
	АдресПередаваемыхПараметров = ПоместитьВоВременноеХранилище(ПараметрыДляПередачи, Форма.УникальныйИдентификатор);
	
	ПараметрыФормыВерификации = Новый Структура;
	ПараметрыФормыВерификации.Вставить("ДокументСсылка", ДокументСсылка);
	ПараметрыФормыВерификации.Вставить("АдресПараметровЗаполнения", АдресПередаваемыхПараметров);
	ПараметрыФормыВерификации.Вставить("ОткрытиеИзФормыДокумента", Истина);
	
	ОткрытиеВРежимеПросмотраИстории = ТребуетПроверки = Ложь И НеРаспознан = Ложь;
	
	ПараметрыФормыВерификации.Вставить("ОткрытиеВРежимеПросмотраИстории", ОткрытиеВРежимеПросмотраИстории);
	
	Если Форма.СтатусРаспознаванияДокумента = 
		ПредопределенноеЗначение("Перечисление.СтатусыРаспознаванияДокументов.Распознан") Тогда
		
		Если Не ЗначениеЗаполнено(Форма.Объект.ВидДокумента) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Необходимо выбрать вид документа!""'"), , "ВидИТематикаТекст");
			Возврат;
		КонецЕсли;
		
		Если НеРаспознан = Ложь Тогда
			ПараметрыФормыВерификации.Вставить("ВидДокументаКэш", Форма.ВидДокументаКэш);
			
			ОткрытьФорму("Справочник.ДокументыПредприятия.Форма.ФормаВерификацииРаспознанногоДокумента", 
				ПараметрыФормыВерификации, Форма, , , , , РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		КонецЕсли;
			
	Иначе
		
		ОткрытьФорму("Справочник.ДокументыПредприятия.Форма.РаспознаваниеДокумента", 
			ПараметрыФормыВерификации, Форма, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;

КонецПроцедуры

// Возвращает признак возможности запуска документа в обработку.
// 
// Параметры:
//  Форма - Форма - Форма
// 
// Возвращаемое значение:
//  Булево - Можно запустить в обработку
Функция МожноЗапуститьВОбработку(Форма) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "СтатусРаспознаванияДокумента") Тогда
		Если Форма.СтатусРаспознаванияДокумента = 
			ПредопределенноеЗначение("Перечисление.СтатусыРаспознаванияДокументов.НаРаспознавании") 
			ИЛИ РаспознаваниеДокументовСлужебныйКлиентСервер.НужнаВерификация(Форма) Тогда
				Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
		
	Возврат Истина;
		
КонецФункции

// Возвращает признак наличия верифицируемых реквизитов
// 
// Параметры:
//  ФормаДокумента - ФормаКлиентскогоПриложения
//  Шапка - Булево
//  ТаблЧ - Булево
// 
// Возвращаемое значение:
//  Булево
Функция ЕстьВерифицируемыеРеквизиты(ФормаДокумента, Шапка = Истина, ТаблЧ = Истина) Экспорт
		
	Если Шапка = Истина И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(
		ФормаДокумента, "ВерифицируемыеРеквизитыШапки") Тогда
			
		Если ФормаДокумента.ВерифицируемыеРеквизитыШапки.Количество() > 0 Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТаблЧ = Истина 
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаДокумента, "ВерифицируемыеРеквизитыТЧ") Тогда
	
		Если ФормаДокумента.ВерифицируемыеРеквизитыТЧ.Количество() > 0 Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти
