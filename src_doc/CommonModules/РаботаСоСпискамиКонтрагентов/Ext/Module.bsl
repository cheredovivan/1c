
#Область ПрограммныйИнтерфейс

#Область СобытияФормы

// Код обработчика "ПриСозданииНаСервере" форм списков контрагентов.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Форма.ЕстьПравоИзменения = ПравоДоступа("Изменение", Метаданные.Справочники.Контрагенты);
	Форма.ПредыдущийВидПросмотра = Форма.ВидПросмотра;
	Форма.Элементы.ГруппыКонтекстноеМенюДобавить.Видимость = Форма.ЕстьПравоИзменения;
	Форма.Элементы.ГруппыКонтекстноеМенюСкопировать.Видимость = Форма.ЕстьПравоИзменения;
	Форма.Элементы.ГруппыКонтекстноеМенюИзменить.Видимость = Форма.ЕстьПравоИзменения;
	Форма.Элементы.ГруппыКонтекстноеМенюУдалить.Видимость = Форма.ЕстьПравоИзменения;
	
	ЭлементыКоллекцииГрупп = Форма.Группы.ПолучитьЭлементы();
	Если ЭлементыКоллекцииГрупп.Количество() = 0 Тогда
		РаботаСоСпискамиКонтрагентовКлиентСервер.ДобавитьСпециальныеГруппы(ЭлементыКоллекцииГрупп);
	КонецЕсли;
	
	УстановитьУсловноеОформлениеГрупп(Форма);
	
КонецПроцедуры

// Код обработчика "ПриЗагрузкеДанныхИзНастроекНаСервере" форм списков контрагентов.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Настройки - Соответствие Из КлючИЗначение
//
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Форма, Настройки) Экспорт
	
	Если Настройки["ПоказыватьУдаленные"] <> Неопределено Тогда
		Форма.ПоказыватьУдаленные = Настройки["ПоказыватьУдаленные"];
		Форма.Элементы.ПоказыватьУдаленные.Пометка = Форма.ПоказыватьУдаленные;
	КонецЕсли;
	
	Если Настройки["ВидПросмотра"] <> Неопределено Тогда
		Форма.ВидПросмотра = Настройки["ВидПросмотра"];
	КонецЕсли;
	
	Если Настройки["ТекущаяГруппа"] <> Неопределено Тогда
		Форма.ТекущаяГруппа = Настройки["ТекущаяГруппа"];
	КонецЕсли;
	
	Если Настройки["ПоказыватьПодчиненные"] <> Неопределено Тогда
		Форма.ПоказыватьПодчиненные = Настройки["ПоказыватьПодчиненные"];
	КонецЕсли;
	
	УстановитьОтборПомеченныхНаУдаление(Форма);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьГруппы(Форма) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаФормированияДереваГрупп(Форма.ПоказыватьУдаленные);
	
	Дерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	ЗначениеВДанныеФормы(Дерево, Форма.Группы);
	
	ИдентификаторСтроки = Неопределено;
	Если Форма.ТекущаяГруппа <> Неопределено Тогда
		ИдентификаторСтроки = РаботаСоСпискамиКонтрагентовКлиентСервер.ИдентификаторСтрокиДереваПоЗначению(Форма.Группы,
			Форма.ТекущаяГруппа);
	КонецЕсли;
	
	Если ИдентификаторСтроки <> Неопределено Тогда
		Форма.Элементы.Группы.ТекущаяСтрока = ИдентификаторСтроки;
	КонецЕсли;
	
	ЭлементыКоллекции = Форма.Группы.ПолучитьЭлементы();
	РаботаСоСпискамиКонтрагентовКлиентСервер.ДобавитьСпециальныеГруппы(ЭлементыКоллекции);
	
КонецПроцедуры

Функция ТекстЗапросаФормированияДереваГрупп(ПоказыватьУдаленные) Экспорт
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВЫБОР
		|		КОГДА Контрагенты.ПометкаУдаления
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ИндексПиктограммы,
		|	Контрагенты.Ссылка КАК ГруппаКонтрагентов,
		|	ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка) КАК ПредставлениеГруппы,
		|	Контрагенты.ПометкаУдаления КАК ПометкаУдаления
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ЭтоГруппа = ИСТИНА
		|	И &ОтборУдаленные
		|
		|УПОРЯДОЧИТЬ ПО
		|	Контрагенты.Ссылка ИЕРАРХИЯ
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Если Не ПоказыватьУдаленные Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборУдаленные", "Контрагенты.ПометкаУдаления = ЛОЖЬ");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборУдаленные", "ИСТИНА");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьОтборПомеченныхНаУдаление(Форма) Экспорт
	
	Если Не Форма.ПоказыватьУдаленные Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Форма.Список, "ПометкаУдаления", Ложь,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Форма.Список, "ПометкаУдаления");
	КонецЕсли;
	
	ЗаполнитьГруппы(Форма);
	
КонецПроцедуры

Процедура УстановитьУсловноеОформлениеГрупп(Форма)
	
	Элемент = Форма.УсловноеОформление.Элементы.Добавить();
	
	ОформляемоеПоле = Элемент.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ГруппыПредставлениеГруппы");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Группы.ПометкаУдаления");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"Шрифт", Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста, , , , , , Истина));
	
КонецПроцедуры

#КонецОбласти
