#Область СлужебныйПрограммныйИнтерфейс

#Область СозданиеДокументаПоФайлу

// Возвращает пустые параметры создания документа по файлу.
// 
// Возвращаемое значение:
//  См. ОбменСКонтрагентамиКлиент.НовыеПараметрыСозданияЭлектронногоДокументаПоФайлу
Функция НовыеПараметрыСозданияДокументаПоФайлу() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Организация", Неопределено);
	Параметры.Вставить("Контрагент", Неопределено);
	Параметры.Вставить("Договор", Неопределено);
	Параметры.Вставить("ВидДокумента", ПредопределенноеЗначение("Справочник.ВидыДокументовЭДО.ПустаяСсылка"));
	Параметры.Вставить("НомерДокумента", "");
	Параметры.Вставить("ДатаДокумента", Дата(1,1,1));
	Параметры.Вставить("СуммаДокумента", 0);
	Параметры.Вставить("ОбъектыУчета", Новый Массив);
	Параметры.Вставить("Подписанты", Новый Массив);
	Параметры.Вставить("МаршрутПодписания", ПредопределенноеЗначение("Справочник.МаршрутыПодписания.ПустаяСсылка"));
	Параметры.Вставить("СоздатьНомерАвтоматически", Ложь);
	Параметры.Вставить("ПараметрыОбмена", Неопределено);
	Параметры.Вставить("ИдентификаторДокумента", "");
	Параметры.Вставить("СопроводительнаяЗаписка", "");
	Параметры.Вставить("СвязующийОбъектУчета", Неопределено);
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#Область ОбработкаПредупрежденийКомпонентов

// Возвращает структуру данных предупреждения компонента.
// 
// Возвращаемое значение:
// 	Структура - параметры просмотра документооборота:
// 	* Вид - Строка - вид предупреждения.
// 	* ТекстОшибки - Строка - текст сообщения.
// 	* Блокирующее - Булево - признак невозможности дальнейших действий без устранения проблемы.
// 	* ДополнительныеДанные - Структура - дополнительные данные для обработки сообщения.
Функция НовыеДанныеПредупрежденияКомпонента() Экспорт
	
	ПараметрыСообщения = Новый Структура;
	ПараметрыСообщения.Вставить("Вид", "");
	ПараметрыСообщения.Вставить("ТекстОшибки", "");
	ПараметрыСообщения.Вставить("Блокирующее", Ложь);
	ПараметрыСообщения.Вставить("ДополнительныеДанные", Новый Структура);	

	Возврат ПараметрыСообщения;

КонецФункции

// Возвращает структуру данных для открытия формы проблем при обработке документов.
// 
// Возвращаемое значение:
//  Структура - параметры просмотра документооборота:
//  * СписокДокументовКОтправке - Массив из ДокументСсылка - ссылки на обрабатываемые документы.
//  * АдресСведенийОбОшибках - Строка - адрес сведений об ошибках во временном хранилище.
//  * РежимПодписатьОтправить - Булево - режим "Подписать и отправить".
//  * ФормаВладелец - ФормаКлиентскогоПриложения
//
Функция НовыеПараметрыПроблемПриОбработкеДокументов() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("СписокДокументовКОтправке", Новый Массив);
	Параметры.Вставить("АдресСведенийОбОшибках", "");
	Параметры.Вставить("РежимПодписатьОтправить", Ложь);
	Параметры.Вставить("ИсправляемыйДокумент", Неопределено);
	Параметры.Вставить("ФормаВладелец", Неопределено);
	Параметры.Вставить("Предупреждения", Неопределено);
	
	Возврат Параметры;

КонецФункции

#КонецОбласти

#Область ОшибкиПриОбработкеДокументов

Функция ВидОшибкиНетПравДляНастройкиЭДО() Экспорт
	
	ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки();
	ВидОшибки.Идентификатор = "НетПравДляНастройкиЭДО";
	ВидОшибки.ЗаголовокПроблемы = НСтр("ru = 'Не удалось сформировать электронный документ'");
	ВидОшибки.ОписаниеПроблемы = НСтр("ru = 'Нет прав для настройки электронного документооборота'");
	ВидОшибки.ОписаниеРешения = НСтр("ru = 'Обратитесь к администратору'");

	Возврат ВидОшибки;
	
КонецФункции

Процедура ЗаполнитьСостояниеЭДО_ФормаДокумента(Параметры) Экспорт
	
	Форма = Параметры.Форма;
	ОбъектУчета = Параметры.ДокументСсылка;
	КонтроллерСостояниеЭДО = Параметры.КонтроллерСостояниеЭДО;
	ГруппаСостояниеЭДО = Параметры.ГруппаСостояниеЭДО;
	
	Если КонтроллерСостояниеЭДО = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОбъектаУчета = ИнтеграцияЭДОВызовСервера.ОписаниеОбъектаУчета(ОбъектУчета);
	Для каждого СтрокаОписания Из ОписаниеОбъектаУчета Цикл
		Если СтрокаОписания.Направление <> ПредопределенноеЗначение("Перечисление.НаправленияЭДО.Внутренний") Тогда
			Продолжить;
		КонецЕсли;
		
		ПризнакВидимости = ИнтерфейсДокументовЭДОВызовСервера.ИспользуетсяЭДОИВнутреннийЭДО();
		Если ГруппаСостояниеЭДО = Неопределено Тогда
			КонтроллерСостояниеЭДО.Видимость = ПризнакВидимости;
		Иначе
			ГруппаСостояниеЭДО.Видимость = ПризнакВидимости;
		КонецЕсли;
		
		Если Не ПризнакВидимости Тогда
			Возврат;
		КонецЕсли;
		
		Прервать;
	КонецЦикла;
	
	ДанныеСостоянияЭДО = ИнтеграцияЭДОВызовСервера.ДанныеСостоянияЭДОДляФормыОбъектаУчета(ОбъектУчета);
	
	Если ГруппаСостояниеЭДО = Неопределено Тогда
		КонтроллерСостояниеЭДО.Видимость = ДанныеСостоянияЭДО.ИспользуетсяОбменЭлектроннымиДокументами;
	Иначе
		ГруппаСостояниеЭДО.Видимость = ДанныеСостоянияЭДО.ИспользуетсяОбменЭлектроннымиДокументами;
	КонецЕсли;
	
	Если Не ДанныеСостоянияЭДО.ИспользуетсяОбменЭлектроннымиДокументами Тогда
		Возврат;
	КонецЕсли;
	
	СкрыватьСостояниеНеНачатогоЭДО = Ложь;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПараметрыОбменаСКонтрагентами") 
		И Форма.ПараметрыОбменаСКонтрагентами <> Неопределено Тогда
		Форма.ПараметрыОбменаСКонтрагентами.Вставить("ДанныеСостоянияЭДОИзначальные", ДанныеСостоянияЭДО);
		Форма.ПараметрыОбменаСКонтрагентами.Вставить("ДанныеСостоянияЭДОТекущие", ДанныеСостоянияЭДО);
		СкрыватьСостояниеНеНачатогоЭДО = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			Форма.ПараметрыОбменаСКонтрагентами, "СкрыватьСостояниеНеНачатогоЭДО", Ложь);
	КонецЕсли;
	
	Если ПустаяСтрока(ДанныеСостоянияЭДО.ПредставлениеСостояния)
		ИЛИ СкрыватьСостояниеНеНачатогоЭДО И ДанныеСостоянияЭДО.ЭтоСостояниеНеНачатогоЭДО Тогда
		Если ГруппаСостояниеЭДО = Неопределено Тогда
			КонтроллерСостояниеЭДО.Видимость = Ложь;
		Иначе
			ГруппаСостояниеЭДО.Видимость = Ложь;
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(КонтроллерСостояниеЭДО) = Тип("ПолеФормы")
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПараметрыОбменаСКонтрагентами") 
		И Форма.ПараметрыОбменаСКонтрагентами <> Неопределено		 
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.ПараметрыОбменаСКонтрагентами, "ПутьКДаннымКонтроллераСостояния") Тогда
		Форма[Форма.ПараметрыОбменаСКонтрагентами.ПутьКДаннымКонтроллераСостояния] = ДанныеСостоянияЭДО.ПредставлениеСостояния;
	ИначеЕсли ТипЗнч(КонтроллерСостояниеЭДО) = Тип("ДекорацияФормы") Тогда
		КонтроллерСостояниеЭДО.Заголовок = ДанныеСостоянияЭДО.ПредставлениеСостояния;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьСостояниеЭДО_ФормаСправочника(Параметры) Экспорт
	
	Форма = Параметры.Форма;
	ОбъектУчета = Параметры.СправочникСсылка;
	КонтроллерСостояниеЭДО = Параметры.КонтроллерСостояниеЭДО;
	ГруппаСостояниеЭДО = Параметры.ГруппаСостояниеЭДО;
	
	Если КонтроллерСостояниеЭДО = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОбъектаУчета = ИнтеграцияЭДОВызовСервера.ОписаниеОбъектаУчета(ОбъектУчета);
	Для каждого СтрокаОписания Из ОписаниеОбъектаУчета Цикл
		Если СтрокаОписания.Направление <> ПредопределенноеЗначение("Перечисление.НаправленияЭДО.Внутренний") Тогда
			Продолжить;
		КонецЕсли;
		
		ПризнакВидимости = ИнтерфейсДокументовЭДОВызовСервера.ИспользуетсяЭДОИВнутреннийЭДО();
		Если ГруппаСостояниеЭДО = Неопределено Тогда
			КонтроллерСостояниеЭДО.Видимость = ПризнакВидимости;
		Иначе
			ГруппаСостояниеЭДО.Видимость = ПризнакВидимости;
		КонецЕсли;
		
		Если Не ПризнакВидимости Тогда
			Возврат;
		КонецЕсли;
		
		Прервать;
	КонецЦикла;
	
	ДанныеСостоянияЭДО = ИнтеграцияЭДОВызовСервера.ДанныеСостоянияЭДОДляФормыОбъектаУчета(ОбъектУчета);
	
	Если ГруппаСостояниеЭДО = Неопределено Тогда
		КонтроллерСостояниеЭДО.Видимость = ДанныеСостоянияЭДО.ИспользуетсяОбменЭлектроннымиДокументами;
	Иначе
		ГруппаСостояниеЭДО.Видимость = ДанныеСостоянияЭДО.ИспользуетсяОбменЭлектроннымиДокументами;
	КонецЕсли;
	
	Если Не ДанныеСостоянияЭДО.ИспользуетсяОбменЭлектроннымиДокументами Тогда
		Возврат;
	КонецЕсли;
	
	СкрыватьСостояниеНеНачатогоЭДО = Ложь;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПараметрыОбменаСКонтрагентами") 
		И Форма.ПараметрыОбменаСКонтрагентами <> Неопределено Тогда
		Форма.ПараметрыОбменаСКонтрагентами.Вставить("ДанныеСостоянияЭДОИзначальные", ДанныеСостоянияЭДО);
		Форма.ПараметрыОбменаСКонтрагентами.Вставить("ДанныеСостоянияЭДОТекущие", ДанныеСостоянияЭДО);
		СкрыватьСостояниеНеНачатогоЭДО = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			Форма.ПараметрыОбменаСКонтрагентами, "СкрыватьСостояниеНеНачатогоЭДО", Ложь);
	КонецЕсли;
	
	Если ПустаяСтрока(ДанныеСостоянияЭДО.ПредставлениеСостояния)
		ИЛИ СкрыватьСостояниеНеНачатогоЭДО И ДанныеСостоянияЭДО.ЭтоСостояниеНеНачатогоЭДО Тогда
		Если ГруппаСостояниеЭДО = Неопределено Тогда
			КонтроллерСостояниеЭДО.Видимость = Ложь;
		Иначе
			ГруппаСостояниеЭДО.Видимость = Ложь;
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(КонтроллерСостояниеЭДО) = Тип("ПолеФормы")
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПараметрыОбменаСКонтрагентами") 
		И Форма.ПараметрыОбменаСКонтрагентами <> Неопределено		 
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.ПараметрыОбменаСКонтрагентами, "ПутьКДаннымКонтроллераСостояния") Тогда
		Форма[Форма.ПараметрыОбменаСКонтрагентами.ПутьКДаннымКонтроллераСостояния] = ДанныеСостоянияЭДО.ПредставлениеСостояния;
	ИначеЕсли ТипЗнч(КонтроллерСостояниеЭДО) = Тип("ДекорацияФормы") Тогда
		КонтроллерСостояниеЭДО.Заголовок = ДанныеСостоянияЭДО.ПредставлениеСостояния;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ДействияПоЭДО

// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДОКлиентСервер.НовыйИтогВыполненияДействийПоЭДО
Функция НовыйИтогВыполненияДействийПоЭДО() Экспорт
	Возврат ЭлектронныеДокументыЭДОКлиентСервер.НовыйИтогВыполненияДействийПоЭДО();
КонецФункции

// Параметры:
//  Приемник - см. НовыйИтогВыполненияДействийПоЭДО
//  Источник - см. НовыйИтогВыполненияДействийПоЭДО
Процедура ДополнитьИтогВыполненияДействийПоЭДО(Приемник, Источник) Экспорт
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Приемник.ОбработанныеДокументы,
		Источник.ОбработанныеДокументы, Истина);
	
	ОбработаноПоДействиямПриемника = Приемник.ОбработаноПоДействиям;
	
	Для Каждого ОбработаноПоДействиюИсточника Из Источник.ОбработаноПоДействиям Цикл
		Обработано = ОбработаноПоДействиюИсточника.Значение;
		Действие = ОбработаноПоДействиюИсточника.Ключ;
		
		ОбработаноПриемник = ОбработаноПоДействиямПриемника[Действие];
		Если ЗначениеЗаполнено(ОбработаноПриемник) Тогда
			Обработано = Обработано + ОбработаноПриемник;
		КонецЕсли;
		
		ОбработаноПоДействиямПриемника.Вставить(Действие, Обработано);
	КонецЦикла;
	
	ДополнитьРезультатыОтправкиПолучения(Приемник.РезультатыОтправкиПолучения, Источник.РезультатыОтправкиПолучения);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Приемник.ОбработанныеУчетныеЗаписи, Источник.ОбработанныеУчетныеЗаписи, Истина);
	
	Приемник.ПолученоКонтейнеров = Приемник.ПолученоКонтейнеров + Источник.ПолученоКонтейнеров;
	Приемник.ОбработаноКонтейнеров = Приемник.ОбработаноКонтейнеров + Источник.ОбработаноКонтейнеров;
КонецПроцедуры

// Возвращает пустой набор действий по ЭДО.
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - ПеречислениеСсылка.ДействияПоЭДО
//  * Значение - Булево
//
Функция НовыйНаборДействийПоЭДО() Экспорт
	Возврат Новый Соответствие;
КонецФункции

// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДОКлиентСервер.НовыеОбъектыДействийПоЭДО
Функция НовыеОбъектыДействийПоЭДО() Экспорт
	Возврат ЭлектронныеДокументыЭДОКлиентСервер.НовыеОбъектыДействийПоЭДО();
КонецФункции

// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО
Функция НовыеПараметрыВыполненияДействийПоЭДО() Экспорт
	Возврат ЭлектронныеДокументыЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
КонецФункции

// Параметры:
//  НаборДействий - См. НовыйНаборДействийПоЭДО
//  Действие      - ПеречислениеСсылка.ДействияПоЭДО
//
Процедура ДобавитьДействие(НаборДействий, Действие) Экспорт
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, Действие);;
КонецПроцедуры

// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДОКлиентСервер.НовыеДополнительныеПараметрыДействия
Функция НовыеДополнительныеПараметрыДействия() Экспорт
	Параметры = Новый Структура;
	Параметры.Вставить("Комментарий", "");
	Параметры.Вставить("Ответственный", Неопределено);
	Возврат ЭлектронныеДокументыЭДОКлиентСервер.НовыеДополнительныеПараметрыДействия();
КонецФункции

// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДОКлиентСервер.НовоеОписаниеДокументаДляФормированияПоФайлам
Функция НовоеОписаниеДокументаДляФормированияПоФайлам() Экспорт
	Возврат ЭлектронныеДокументыЭДОКлиентСервер.НовоеОписаниеДокументаДляФормированияПоФайлам();
КонецФункции

// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДОКлиентСервер.НовыеФайлыТитулаДокументаДляФормирования
Функция НовыеФайлыТитулаДокументаДляФормирования() Экспорт
	Возврат ЭлектронныеДокументыЭДОКлиентСервер.НовыеФайлыТитулаДокументаДляФормирования();
КонецФункции

// Параметры:
//  Приемник - см. НовыеРезультатыОтправкиПолучения
//  Источник - см. НовыеРезультатыОтправкиПолучения
Процедура ДополнитьРезультатыОтправкиПолучения(Приемник, Источник) Экспорт
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Приемник.Успешные, Источник.Успешные);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Приемник.Неудачные, Источник.Неудачные);
КонецПроцедуры

#КонецОбласти

#Область ПредставлениеДокумента

// Возвращает представление электронного документа по его свойствам.
// 
// Параметры:
//  СвойстваДокумента - Структура:
//  * ВидДокумента   - СправочникСсылка.ВидыДокументовЭДО
//  * НомерДокумента - Строка
//  * ДатаДокумента  - Дата
//  ЭтоНовый - Булево
// Возвращаемое значение:
//  Строка - представление электронного документа.
//
Функция ПредставлениеДокументаПоСвойствам(СвойстваДокумента, ЭтоНовый = Ложь) Экспорт
	
	// В случае если в событии получения формы возникла ошибка, структура будет пустой.
	Если Не ЗначениеЗаполнено(СвойстваДокумента) Тогда
		Возврат "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СвойстваДокумента.НомерДокумента) И ЗначениеЗаполнено(СвойстваДокумента.ДатаДокумента) Тогда
		Представление = СтрШаблон(НСтр("ru = '%1 № %2 от %3'"), СвойстваДокумента.ВидДокумента,
			СвойстваДокумента.НомерДокумента, Формат(СвойстваДокумента.ДатаДокумента, "ДЛФ=D;"));
	ИначеЕсли ЗначениеЗаполнено(СвойстваДокумента.НомерДокумента) Тогда
		Представление = СтрШаблон(НСтр("ru = '%1 № %2'"), СвойстваДокумента.ВидДокумента,
			СвойстваДокумента.НомерДокумента);
	ИначеЕсли ЗначениеЗаполнено(СвойстваДокумента.ДатаДокумента) Тогда
		Представление = СтрШаблон(НСтр("ru = '%1 от %2'"), СвойстваДокумента.ВидДокумента,
			Формат(СвойстваДокумента.ДатаДокумента, "ДЛФ=D;"));
	Иначе
		Представление = Строка(СвойстваДокумента.ВидДокумента);
	КонецЕсли;
	
	Если ЭтоНовый Тогда
		Представление = СтрШаблон(НСтр("ru = '%1 (Создание)'"), Представление);
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

#КонецОбласти

Функция НайтиЭлементСхемыИнформацияПолучателя(ЭлементСхемыРегламенты) Экспорт
	
	Результат = Неопределено;
	
	Если ЭлементСхемыРегламенты.ТипЭлементаРегламента =
		ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя") Тогда
		
		Для Каждого ЭлементСхемы Из ЭлементСхемыРегламенты.ПолучитьЭлементы() Цикл
			Если ЭлементСхемы.ТипЭлементаРегламента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ИнформацияПолучателя") Тогда
				Результат = ЭлементСхемы;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает представления для вариантов формирования дополнительных сведений акта о расхождениях.
// 
// Возвращаемое значение:
//  ФиксированноеСоответствие из КлючИЗначение:
//   * Ключ - Строка - вариант формирования
//   * Значение - Строка - представление варианта
//
Функция ПредставленияВариантовФормированияДополнительныхСведенийАктаОРасхождениях() Экспорт
	
	Варианты = Новый Соответствие();
		
	Варианты.Вставить("1", НСтр("ru = 'К документу дополнительные сведения не формируются'"));
	Варианты.Вставить("2", НСтр("ru = 'К документу формируются дополнительные сведения об оприходовании ценностей покупателем (в том числе на склад), подписанные ответственным лицом покупателя (уполномоченным покупателем лицом)'"));
	Варианты.Вставить("3", НСтр("ru = 'К документу формируются дополнительные сведения о его утверждении, подписанные ответственным лицом покупателя (уполномоченным покупателем лицом)'"));
	Варианты.Вставить("4", НСтр("ru = 'К документу формируются иные дополнительные сведения, подписанные ответственным лицом покупателя (уполномоченным покупателем лицом)'"));
	Варианты.Вставить("5", НСтр("ru = 'К документу формируются дополнительные сведения о согласии (несогласии) с результатами приемки, подписанные ответственным лицом со стороны продавца (уполномоченным продавцом лицом)'"));
	Варианты.Вставить("6", НСтр("ru = 'К документу формируются иные дополнительные сведения, подписанные ответственным лицом со стороны продавца (уполномоченным продавцом лицом)'"));
	Варианты.Вставить("7", НСтр("ru = 'К документу формируются дополнительные сведения о согласии (несогласии) с результатами приемки, подписанные ответственным лицом со стороны перевозчика (уполномоченным перевозчиком лицом)'"));
	Варианты.Вставить("8", НСтр("ru = 'К документу формируются иные дополнительные сведения, подписанные ответственным лицом со стороны перевозчика (уполномоченным перевозчиком лицом)'"));
	
	Возврат Новый ФиксированноеСоответствие(Варианты);
	
КонецФункции

#Область ЕИС

// Возвращает приложение для ЕИС.
// 
// Параметры:
// 	ЭлементСхемыРегламенты - ДанныеФормыЭлементДерева - Элемент для которого нужно найти информацию отправителя.
// 	ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО - тип регламента элемента схемы.
//
// Возвращаемое значение:
//  ДанныеФормыЭлементДерева, Неопределено - Элемент информации отправителя.
//
Функция НайтиЭлементСхемыПриложениеДляЕИС(ЭлементСхемыРегламенты, ТипЭлементаРегламента) Экспорт
	
	Результат = Неопределено;
	
	Если ЭлементСхемыРегламенты.ТипЭлементаРегламента =
		ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя") Тогда
		
		Для Каждого ЭлементСхемы Из ЭлементСхемыРегламенты.ПолучитьЭлементы() Цикл
			Если ЭлементСхемы.ТипЭлементаРегламента = ТипЭлементаРегламента Тогда
				Результат = ЭлементСхемы;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

Процедура ОбновитьФормуПечатиДокументовБСП(Форма) Экспорт
	
	ОбъектыУчета = Форма.Параметры.ПараметрКоманды;
	
	Если Не ЗначениеЗаполнено(ОбъектыУчета) Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ОбъектыУчета) <> Тип("Массив") Тогда
		 ОбъектыУчета = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбъектыУчета);
	КонецЕсли;
	
	ПараметрыЭД = Неопределено;
	ОписаниеОбъектаУчета = ИнтеграцияЭДОВызовСервера.ОписаниеОбъектаУчета(ОбъектыУчета[0]);
	Для каждого СтрокаОписания Из ОписаниеОбъектаУчета Цикл
		Если СтрокаОписания.ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.Внутренний") Тогда
			ПараметрыЭД = СтрокаОписания;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ПараметрыЭД) Тогда 
		Возврат;
	КонецЕсли;
		
	Команда = ИнтерфейсДокументовЭДОВызовСервера.КомандаПечатиОбъекта(ОбъектыУчета[0], Форма.НастройкиПечатныхФорм[0].ИмяМакета);
	Если Не ЗначениеЗаполнено(Команда) Тогда
		Форма.Элементы.ГруппаДополнительнаяИнформация.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ВидВнутреннегоДокумента = ИнтерфейсДокументовЭДОВызовСервера.НайтиСоздатьВидВнутреннегоДокумента(ОбъектыУчета[0], Команда);

	Настройки = ИнтерфейсДокументовЭДОВызовСервера.СуществуюНастройкиВнутреннегоЭДО(ПараметрыЭД.Организация, ВидВнутреннегоДокумента); 
		
	ЭДОНачат = ИнтерфейсДокументовЭДОВызовСервера.ЕстьАктуальныеЭлектронныеДокументы(ОбъектыУчета, ВидВнутреннегоДокумента);
	ТекстСостоянияЭД = ИнтеграцияЭДОВызовСервера.ПредставлениеСостоянияВнутреннегоДокументаОбъектаУчета(ОбъектыУчета, ВидВнутреннегоДокумента);
	
	Форма.Элементы.ГруппаДополнительнаяИнформация.Видимость = Ложь;

	Если Настройки <> Неопределено И Настройки.Формировать Или ЭДОНачат Тогда 
		
		Форма.Элементы.ОтправитьНаПодписьКнопкаФормы.КнопкаПоУмолчанию = Истина;
		
		Если ЗначениеЗаполнено(ТекстСостоянияЭД) И ЭДОНачат Тогда
			
			Форма.Элементы.ПредставлениеПрогрессаПодписанияДекорация.Заголовок = Новый ФорматированнаяСтрока(ТекстСостоянияЭД,,,, "ВнутреннийЭДОПрогрессПодписания");
			
			Форма.Элементы.ЭмблемаСервиса1СЭДОПодписаниеКартинка.Видимость = Истина;
			Форма.Элементы.ПредставлениеПрогрессаПодписанияДекорация.Видимость = Истина;
			Форма.Элементы.ОтправитьНаПодписьКнопкаФормы.Видимость = Ложь;
			
		Иначе

			Форма.Элементы.ОтправитьНаПодписьКнопкаФормы.Видимость = ЗначениеЗаполнено(Форма.ОбъектыПечати);
			
		КонецЕсли;
		
		Форма.Элементы.КнопкаПечатьКоманднаяПанель.ЦветФона = Новый Цвет;
		Форма.Элементы.КнопкаПечатьКоманднаяПанель.Отображение = ОтображениеКнопки.Картинка;
		
	ИначеЕсли ЗначениеЗаполнено(ПараметрыЭД) И ПараметрыЭД.Направление = ПредопределенноеЗначение("Перечисление.НаправленияЭДО.Внутренний") 
		И Настройки = Неопределено Тогда 
		
		ТекстПриглашения = НСтр("ru = 'Хотите сэкономить на бумаге? Оформите документ в электронном виде.'");
		
		Форма.Элементы.ДополнительнаяИнформация.Заголовок = Новый ФорматированнаяСтрока(ТекстПриглашения,,,, "Реклама1СЭДОВнутренний"); 
		Форма.Элементы.ГруппаДополнительнаяИнформация.Видимость = ЗначениеЗаполнено(Форма.ОбъектыПечати);
		Форма.Элементы.КнопкаПечатьКоманднаяПанель.КнопкаПоУмолчанию = Истина;
		Форма.Элементы.КартинкаИнформации.Видимость = Истина;
		Форма.Элементы.КартинкаИнформации.Картинка = БиблиотекаКартинок.ЭмблемаСервиса1СЭДО;

	КонецЕсли;
	
КонецПроцедуры

Процедура РазблокироватьЗаблокированныеЭлементыФормы(Форма, СписокЭлементов) Экспорт

	Для Каждого ЗаблокированныйЭлементыФормы Из СписокЭлементов Цикл

		Форма.Элементы[ЗаблокированныйЭлементыФормы.Значение].Доступность = Истина;

	КонецЦикла;

	СписокЭлементов.Очистить();

КонецПроцедуры

Функция ДанныеТабличногоДокумента(Знач ТабличныйДокумент, Знач ТипФайла) Экспорт
	
	#Если ВебКлиент Тогда
		// Внимание! В веб-клиенте использование данного варианта недоступно.
		Возврат ИнтерфейсДокументовЭДОВызовСервера.ДанныеТабличногоДокумента(ТабличныйДокумент, ТипФайла);
	#КонецЕсли
	
	Поток = Новый ПотокВПамяти();
	ТабличныйДокумент.Записать(Поток, ТипФайла);
	Возврат Поток.ЗакрытьИПолучитьДвоичныеДанные();
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * ВидДокумента - Неопределено
//                 - СправочникСсылка.ВидыДокументовЭДО - уточнение вида документа, когда по объекту учета доступно несколько вариантов.
//  * ДанныеКаталога - Строка - адрес временного хранилища данных каталога.
//  * НовыйДокумент - Булево - при Истина поиск текущего электронного документа не производится.
//  * ЗначенияДополнительныхПолей - Неопределено,Произвольный
//  * ФайлДоговора - Неопределено,
//                 - См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
Функция НовыеПараметрыОткрытияЭлектронногоДокумента() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("ВидДокумента", Неопределено);
	Параметры.Вставить("ДанныеКаталога", "");
	Параметры.Вставить("НовыйДокумент", Ложь);
	Параметры.Вставить("ЗначенияДополнительныхПолей", Неопределено);
	Параметры.Вставить("ФайлДоговора", Неопределено);
	
	Возврат Параметры;
	
КонецФункции

#Область ФормированиеНеформализованныхЭДОИзПечатныхФорм

// Конструктор данных печатной формы для формирования неформализованного ЭДО
// 
// Возвращаемое значение:
//  Структура - Новые данные печатной формы для неформализованного ЭДО:
//    * ТабличныйДокумент - ТабличныйДокумент
//    * ДвоичныеДанныеФайла - Неопределено, ДвоичныеДанные - 
//    * НаименованиеФайла - Строка
//    * Расширение - Строка
//    * Уникальность - Строка
Функция НовыеДанныеПечатнойФормыДляНеформализованногоЭДО() Экспорт
	
	ДанныеПечатнойФормы = Новый Структура;
	ДанныеПечатнойФормы.Вставить("ТабличныйДокумент", Новый ТабличныйДокумент());
	ДанныеПечатнойФормы.Вставить("ДвоичныеДанныеФайла", Неопределено);
	ДанныеПечатнойФормы.Вставить("НаименованиеФайла", "");
	ДанныеПечатнойФормы.Вставить("Расширение", "");
	ДанныеПечатнойФормы.Вставить("Уникальность", "");
	
	Возврат ДанныеПечатнойФормы;
	
КонецФункции

// Новый контекст отправки печатных форм по ЭДО.
// 
// Возвращаемое значение:
//  Структура - Новый контекст отправки печатных форм по ЭДО:
// * ДокументОснование - Неопределено, ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО -
// * ДанныеПечатныхФорм - Массив Из см. НовыеДанныеПечатнойФормыДляНеформализованногоЭДО
Функция НовыйКонтекстОтправкиПечатныхФормПоЭДО() Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("ДокументОснование", Неопределено);
	Контекст.Вставить("ДанныеПечатныхФорм", Новый Массив);
	
	Возврат Контекст;
	
КонецФункции

#КонецОбласти

#Область РучноеФормированиеТитула

#Если Не ВнешнееСоединение Тогда
	
// Формирует структуру данных ручного формирования титула.
// По данным ручного формирования может заполняться описание данных электронного документа.
// 
// Параметры:
//  Форма - См. Обработка.ИнтерфейсДокументовЭДО.Форма.РучноеФормированиеОтветногоТитула
// 
// Возвращаемое значение:
//  - Неопределено
//  См. ДанныеРучногоФормированияОтветногоТитулаОбщаяФорма
Функция ДанныеРучногоФормированияТитула(Форма) Экспорт
	
	Формы = ФормыРучногоФормированияТитула();
	Если Форма.ИмяФормы = Формы.ОбщаяФорма
		Или Форма.ИмяФормы = Формы.ЛегкийКлиентЭДО Тогда
		Возврат ДанныеРучногоФормированияОтветногоТитулаОбщаяФорма(Форма);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецЕсли // ВнешнееСоединение

#КонецОбласти

#Область ДоговорныйДокумент

// Параметры:
//  Формат - Строка
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоФорматДоговорногоДокументаПДФ_А3(Формат) Экспорт
	Возврат Формат = "ON_DOGDOC_1_999_01_01_01_01";
КонецФункции

// Параметры:
//  Формат - Строка
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоФорматДоговорногоДокументаXML(Формат) Экспорт
	Возврат Формат = "ON_SODSD_1_999_02_01_01_01";
КонецФункции

#КонецОбласти

// Ключ объекта настроек интерфейса ЭДО, используется для сохранения настроек интерфейса в хранилище общих настроек.
// 
// Возвращаемое значение:
//  Строка 
Функция КлючОбъектаНастроекИнтерфейсаЭДО() Экспорт
	Возврат "ИнтерфейсЭДО";
КонецФункции

// Возвращает путь к форме текущих дел по ЭДО в зависимости от варианта интерфейса.
// 
// Параметры:
//  ВариантИнтерфейса - Строка - См. ВариантыИнтерфейсаДокументовЭДО
// 
// Возвращаемое значение:
//  Строка
Функция ФормаТекущихДелЭДОПоВариантуИнтерфейса(ВариантИнтерфейса) Экспорт
	
	ФормыТекущихДелЭДОПоВариантамИнтерфейса = ФормыТекущихДелЭДОПоВариантамИнтерфейса();
	Возврат ФормыТекущихДелЭДОПоВариантамИнтерфейса[ВариантИнтерфейса];
	
КонецФункции

// Возвращает путь к форме просмотра электронного документа в зависимости от варианта интерфейса.
// 
// Параметры:
//  ВариантИнтерфейса - Строка - См. ВариантыИнтерфейсаДокументовЭДО
//  ЭтоВходящийЭДО - Булево
// 
// Возвращаемое значение:
//  Строка
Функция ФормаПросмотраЭлектронногоДокументаПоВариантуИнтерфейса(ВариантИнтерфейса, ЭтоВходящийЭДО) Экспорт
	
	ФормыПросмотраЭлектронногоДокумента = ФормыПросмотраЭлектронногоДокументаПоВариантамИнтерфейса(ЭтоВходящийЭДО);
	Возврат ФормыПросмотраЭлектронногоДокумента[ВариантИнтерфейса];
	
КонецФункции

// Форма выбора варианта интерфейса.
// 
// Возвращаемое значение:
//  Строка
Функция ФормаВыбораВариантаИнтерфейса() Экспорт
	Возврат "Обработка.ИнтерфейсДокументовЭДО.Форма.ВыборВариантаИнтерфейсаЭДО";
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * Расширенный - Строка
//  * Легкий - Строка
//  * ЛегкийКлиентЭДО - Строка
Функция ВариантыИнтерфейсаДокументовЭДО() Экспорт
	ВариантыИнтерфейса = Новый Структура;
	ВариантыИнтерфейса.Вставить("Расширенный", НСтр("ru='Расширенный'"));
	ВариантыИнтерфейса.Вставить("Легкий", НСтр("ru='Легкий'"));
	ВариантыИнтерфейса.Вставить("ЛегкийКлиентЭДО", НСтр("ru='Легкий Клиент ЭДО'"));
	Возврат ВариантыИнтерфейса;
КонецФункции

// Параметры:
//  ИмяРеквизита - Строка
// 
// Возвращаемое значение:
//  Строка
Функция ТекстСообщенияОНезаполненностиРеквизитовДокумента(ИмяРеквизита) Экспорт

	ШаблонСообщенияОНезаполненности = НСтр("ru = 'Поле ""%1"" не заполнено. Документ не записан.
		|Заполните его и повторите запись.'");
	Возврат СтрШаблон(ШаблонСообщенияОНезаполненности, ИмяРеквизита);

КонецФункции

// Возвращаемое значение:
//  Строка
Функция ТекстСообщенияОНеобходимостиСнятьПометкуУдаленияДляОткрытияФайла() Экспорт
	Возврат НСтр("ru = 'Для открытия файла, необходимо снять пометку удаления с документа'");
КонецФункции

// Возвращаемое значение:
//  Строка
Функция ТекстСообщенияОбОтсутствииВозможностиЗагрузитьФайлXML() Экспорт
	Возврат НСтр("ru = 'Файл содержит ошибки. Выберите другой файл'");
КонецФункции

// Возвращаемое значение:
//  Строка
Функция ПредставлениеОтсутствующегоФайла() Экспорт
	Возврат НСтр("ru = 'отсутствует'");
КонецФункции

// Возвращаемое значение:
//  Строка
Функция ТекстСообщенияОбОтсутствииФайлаЭлектронногоДокумента() Экспорт
	Возврат НСтр("ru='Файл электронного документа отсутствует или удален'");
КонецФункции

// Структура полных имен форм, используемых для ручного формирования титулов.
// 
// Возвращаемое значение:
//  Структура:
//  * ОбщаяФорма - Строка
//  * ЛегкийКлиентЭДО - Строка
//
Функция ФормыРучногоФормированияТитула() Экспорт
	
	Формы = Новый Структура;
	Формы.Вставить("ОбщаяФорма", "Обработка.ИнтерфейсДокументовЭДО.Форма.РучноеФормированиеОтветногоТитула");
	Формы.Вставить("ЛегкийКлиентЭДО",
		"Обработка.ИнтерфейсДокументовЭДО.Форма.РучноеФормированиеОтветногоТитулаЛегкийКлиентЭДО");
	Возврат Формы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаПредупрежденийКомпонентов

// Возвращает пустые настройки формирования электронного документа по объекту учета.
// 
// Возвращаемое значение:
// 	Структура - Описание:
// * Направление - ПеречислениеСсылка.НаправленияЭДО - направление электронного документа.
// * НастройкиОтправки - Неопределено
//                     - См. НастройкиЭДО.НастройкиОтправки
// * НастройкиВнутреннегоЭДО - Неопределено
//                           - См. ИнтерфейсДокументовЭДО.НастройкиВнутреннегоЭДО
// * ЭтоОблачныйЭДО - Булево
Функция НовыеНастройкиФормированияЭлектронногоДокументаОбъектаУчета() Экспорт
	НастройкиФормирования = Новый Структура;
	НастройкиФормирования.Вставить("Направление", ПредопределенноеЗначение("Перечисление.НаправленияЭДО.ПустаяСсылка")); 
	НастройкиФормирования.Вставить("НастройкиОтправки", Неопределено);
	НастройкиФормирования.Вставить("НастройкиВнутреннегоЭДО", Неопределено);
	НастройкиФормирования.Вставить("ЭтоОблачныйЭДО", Ложь);
	Возврат НастройкиФормирования;
КонецФункции

#КонецОбласти

#Область ПредставлениеДокумента

// Заполняет имена полей для формирования представления электронного документа
// 
// Параметры:
//  Поля - Массив из Строка
//  СтандартнаяОбработка - Булево
//
Процедура ОбработкаПолученияПолейПредставленияДокумента(Поля, СтандартнаяОбработка) Экспорт
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("ВидДокумента");
	Поля.Добавить("НомерДокумента");
	Поля.Добавить("ДатаДокумента");
КонецПроцедуры

// Заполняет представление электронного документа.
// 
// Параметры:
//  Данные - Структура
//  Представление - Строка
//  СтандартнаяОбработка - Булево
//
Процедура ОбработкаПолученияПредставленияДокумента(Данные, Представление, СтандартнаяОбработка) Экспорт
	СтандартнаяОбработка = Ложь;
	Представление = ПредставлениеДокументаПоСвойствам(Данные);
КонецПроцедуры

#КонецОбласти

#Область РучноеФормированиеТитула

#Если Не ВнешнееСоединение Тогда

// Параметры:
//  Форма - См. Обработка.ИнтерфейсДокументовЭДО.Форма.РучноеФормированиеОтветногоТитула
// 
// Возвращаемое значение:
//  См. ДанныеРучногоФормированияОтветногоТитулаУПД_2019
//  См. ДанныеРучногоФормированияДополнительныхСведений
//  См. ДанныеРучногоФормированияАктаСверкиВзаиморасчетов
//  См. ДанныеРучногоФормированияОтветногоТитулаУКД_2020
Функция ДанныеРучногоФормированияОтветногоТитулаОбщаяФорма(Форма)
	Если Форма.ЭтоУПД Тогда
		Возврат ДанныеРучногоФормированияОтветногоТитулаУПД_2019(Форма);
	ИначеЕсли Форма.ЭтоУПД_5_02 Тогда
		Возврат ДанныеРучногоФормированияОтветногоТитулаУПД_5_02(Форма);
	ИначеЕсли Форма.ЭтоДополнительныеСведения Тогда
		Возврат ДанныеРучногоФормированияДополнительныхСведений(Форма);
	ИначеЕсли Форма.ЭтоАктСверкиВзаиморасчетов Тогда
		Возврат ДанныеРучногоФормированияАктаСверкиВзаиморасчетов(Форма);
	Иначе
		Возврат ДанныеРучногоФормированияОтветногоТитулаУКД_2020(Форма);
	КонецЕсли;
КонецФункции

// Служебный метод. Для получения данных см. ДанныеРучногоФормированияТитула
// 
// Параметры:
//  Форма - См. Обработка.ИнтерфейсДокументовЭДО.Форма.РучноеФормированиеОтветногоТитула
// 
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДОКлиентСервер.НовыеДанныеРучногоФормированияОтветногоТитулаУПД_2019
Функция ДанныеРучногоФормированияОтветногоТитулаУПД_2019(Форма)
	Данные = ЭлектронныеДокументыЭДОКлиентСервер.НовыеДанныеРучногоФормированияОтветногоТитулаУПД_2019();
	Данные.Вставить("СоставительДокументаНаименование", Форма.СоставительДокументаНаименование);
	Если Форма.СоставительДокумента <> Форма.Покупатель Тогда
		Данные.Вставить("СоставительДокументаДоверенность", Форма.СоставительДокументаДоверенность);
	КонецЕсли;

	СведенияОПринятииТоваров = Новый Структура;
	СведенияОПринятииТоваров.Вставить("КодИтога", Форма.КодИтога);
	СведенияОПринятииТоваров.Вставить("ДатаПолученияТоваров", Форма.ДатаПолученияТоваров);
	СведенияОПринятииТоваров.Вставить("СодержаниеОперации", Форма.СодержаниеОперации);
	Данные.Вставить("СведенияОПринятииТоваров", СведенияОПринятииТоваров);

	Если Форма.ЕстьДокументОРасхождениях Тогда
		ДокументОРасхождениях = Новый Структура;
		ДокументОРасхождениях.Вставить("Вид", Форма.ДокументОРасхожденияхВид);
		ДокументОРасхождениях.Вставить("Наименование", Форма.ДокументОРасхожденияхНаименование);
		ДокументОРасхождениях.Вставить("Дата", Форма.ДокументОРасхожденияхДата);
		ДокументОРасхождениях.Вставить("Номер", Форма.ДокументОРасхожденияхНомер);
		Данные.Вставить("ДокументОРасхождениях", ДокументОРасхождениях);
	КонецЕсли;

	Если Форма.ЗакупкаДляГосударственныхНужд Тогда
		ЗакупкаДляГосударственныхНуждСтруктура = Новый Структура;
		ЗакупкаДляГосударственныхНуждСтруктура.Вставить("КодЗакупки", Форма.КодЗакупки);
		ЗакупкаДляГосударственныхНуждСтруктура.Вставить("НомерЛицевогоСчетаПокупателя",
			Форма.НомерЛицевогоСчетаПокупателя);
		ЗакупкаДляГосударственныхНуждСтруктура.Вставить("НаименованиеФинансовогоОрганаПокупателя",
			Форма.НаименованиеФинансовогоОрганаПокупателя);
		ЗакупкаДляГосударственныхНуждСтруктура.Вставить("НомерРеестровойЗаписиПокупателя",
			Форма.НомерРеестровойЗаписиПокупателя);
		ЗакупкаДляГосударственныхНуждСтруктура.Вставить("НомерБюджетногоОбязательстваПокупателя",
			Форма.НомерБюджетногоОбязательстваПокупателя);
		ЗакупкаДляГосударственныхНуждСтруктура.Вставить("КазначействоПокупателяКод", Форма.КазначействоПокупателяКод);
		ЗакупкаДляГосударственныхНуждСтруктура.Вставить("КазначействоПокупателяНаименование",
			Форма.КазначействоПокупателяНаименование);
		ЗакупкаДляГосударственныхНуждСтруктура.Вставить("ОКТМОМестаПоставки", Форма.ОКТМОМестаПоставки);
		ЗакупкаДляГосударственныхНуждСтруктура.Вставить("НомерДенежногоОбязательства",
			Форма.НомерДенежногоОбязательства);
		ЗакупкаДляГосударственныхНуждСтруктура.Вставить("ПредельнаяДатаОплаты", Форма.ПредельнаяДатаОплаты);
		ЗакупкаДляГосударственныхНуждСтруктура.Вставить("ОКТМОПокупателя", Форма.ОКТМОПокупателя);
		ЗакупкаДляГосударственныхНуждСтруктура.Вставить("НомерРеестровойЗаписиПокупателя",
			Форма.НомерРеестровойЗаписиПокупателя);
		ЗакупкаДляГосударственныхНуждСтруктура.Вставить("НомерБюджетногоОбязательстваПокупателя", Форма.КодЗакупки);
		ЗакупкаДляГосударственныхНуждСтруктура.Вставить("КазначействоПокупателяКод", Форма.КазначействоПокупателяКод);
		ЗакупкаДляГосударственныхНуждСтруктура.Вставить("КазначействоПокупателяНаименование",
			Форма.КазначействоПокупателяНаименование);
		Данные.Вставить("ЗакупкаДляГосударственныхНужд", ЗакупкаДляГосударственныхНуждСтруктура);

		ДенежныеОбязательства = Новый Массив; // Массив Из Структура
		СтруктураСтроки = Новый ФиксированнаяСтруктура("ВидСредств, КодБюджетнойКлассификации, КодОбъектаФАИП, КодЦели,
			|НомерСтрокиИнформацииПродавца, СуммаАванса");
		Для Каждого СтрокаТаблицы Из Форма.ДенежныеОбязательства Цикл
			ДанныеСтроки = Новый Структура(СтруктураСтроки);
			ЗаполнитьЗначенияСвойств(ДанныеСтроки, СтрокаТаблицы);
			ДенежныеОбязательства.Добавить(ДанныеСтроки);
		КонецЦикла;
		Данные.Вставить("ДенежныеОбязательства", ДенежныеОбязательства);
	КонецЕсли;
	Возврат Данные;
КонецФункции

// Служебный метод. Для получения данных см. ДанныеРучногоФормированияТитула
// 
// Параметры:
//  Форма - См. Обработка.ИнтерфейсДокументовЭДО.Форма.РучноеФормированиеОтветногоТитула
// 
// Возвращаемое значение:
//  Структура:
// * СоставительДокументаНаименование - Строка
// * СодержаниеФХЖ - Структура:
// 	** СведенияОПринятииТоваров - Структура:
// 		*** КодИтога - Строка
// 		*** ДатаПолученияТоваров - Дата
// 		*** СодержаниеОперации - Строка 
// 		*** ДокументОРасхождениях - Структура:
// 			**** НаименованиеДокумента - Строка
// 			**** ДатаДокумента - Дата
// 			**** НомерДокумента - Строка
// * ЗакупкаДляГосударственныхНужд - Структура:
// ** КодЗакупки - Строка
// ** НомерЛицевогоСчетаПокупателя - Строка
// ** НаименованиеФинансовогоОрганаПокупателя - Строка
// ** НомерРеестровойЗаписиПокупателя - Строка
// ** НомерБюджетногоОбязательстваПокупателя - Строка
// ** КазначействоПокупателяКод - Строка
// ** КазначействоПокупателяНаименование - Строка
// ** ОКТМОМестаПоставки - Строка
// ** НомерДенежногоОбязательства - Строка
// ** ПредельнаяДатаОплаты - Дата
// ** ОКТМОПокупателя - Строка
// * ДенежныеОбязательства - Массив Из Структура:
// ** ВидСредств - Строка
// ** КодБюджетнойКлассификации - Строка
// ** КодОбъектаФАИП - Строка
// ** КодЦели - Строка
// ** НомерСтрокиИнформацииПродавца - Число
// ** СуммаАванса - Число
// 
Функция ДанныеРучногоФормированияОтветногоТитулаУПД_5_02(Форма)
	
	Данные = ЭлектронныеДокументыЭДОКлиентСервер.НовыеДанныеРучногоФормированияОтветногоТитулаУПД_5_02();
	
	Данные.Вставить("СоставительДокументаНаименование", Форма.СоставительДокументаНаименование);
	Если Форма.СоставительДокумента <> Форма.Покупатель Тогда
		Данные.Вставить("СоставительДокументаДоверенность", Форма.СоставительДокументаДоверенность);
	КонецЕсли;
	
	СведенияОПринятииТоваров = Новый Структура;
	СведенияОПринятииТоваров.Вставить("КодИтога", Форма.КодИтога);
	СведенияОПринятииТоваров.Вставить("ДатаПолученияТоваров", Форма.ДатаПолученияТоваров);
	СведенияОПринятииТоваров.Вставить("СодержаниеОперации", Форма.СодержаниеОперации);

	Если Форма.ЕстьДокументОРасхождениях Тогда
		ДокументОРасхождениях = Новый Структура("НаименованиеДокумента, НомерДокумента, ДатаДокумента");
		ДокументОРасхождениях.НаименованиеДокумента = Форма.ДокументОРасхожденияхНаименование;
		ДокументОРасхождениях.ДатаДокумента = Форма.ДокументОРасхожденияхДата;
		ДокументОРасхождениях.НомерДокумента = Форма.ДокументОРасхожденияхНомер;
		СведенияОПринятииТоваров.Вставить("ДокументОРасхождениях", ДокументОРасхождениях);
	КонецЕсли;
	
	Данные.Вставить("СодержаниеФХЖ", Новый Структура("СведенияОПринятииТоваров", СведенияОПринятииТоваров));
	
	Если Форма.ЗакупкаДляГосударственныхНужд Тогда
		ЗакупкаДляГосударственныхНужд = Новый Структура;
		
		ЗакупкаДляГосударственныхНужд.Вставить("КодЗакупки",
			Форма.КодЗакупки);
		ЗакупкаДляГосударственныхНужд.Вставить("НомерЛицевогоСчетаПокупателя",
			Форма.НомерЛицевогоСчетаПокупателя);
		ЗакупкаДляГосударственныхНужд.Вставить("НаименованиеФинансовогоОрганаПокупателя",
			Форма.НаименованиеФинансовогоОрганаПокупателя);
		ЗакупкаДляГосударственныхНужд.Вставить("НомерРеестровойЗаписиПокупателя",
			Форма.НомерРеестровойЗаписиПокупателя);
		ЗакупкаДляГосударственныхНужд.Вставить("НомерБюджетногоОбязательстваПокупателя",
			Форма.НомерБюджетногоОбязательстваПокупателя);
		ЗакупкаДляГосударственныхНужд.Вставить("КазначействоПокупателяКод",
			Форма.КазначействоПокупателяКод);
		ЗакупкаДляГосударственныхНужд.Вставить("КазначействоПокупателяНаименование",
			Форма.КазначействоПокупателяНаименование);
		ЗакупкаДляГосударственныхНужд.Вставить("ОКТМОМестаПоставки",
			Форма.ОКТМОМестаПоставки);
		ЗакупкаДляГосударственныхНужд.Вставить("НомерДенежногоОбязательства",
			Форма.НомерДенежногоОбязательства);
		ЗакупкаДляГосударственныхНужд.Вставить("ПредельнаяДатаОплаты",
			Форма.ПредельнаяДатаОплаты);
		ЗакупкаДляГосударственныхНужд.Вставить("ОКТМОПокупателя",
			Форма.ОКТМОПокупателя);
		
		ДенежныеОбязательства = Новый Массив; // Массив Из Структура
		СтруктураСтроки = Новый ФиксированнаяСтруктура("ВидСредств, КодБюджетнойКлассификации, КодОбъектаФАИП, КодЦели,
			|НомерСтрокиИнформацииПродавца, СуммаАванса");
		Для Каждого СтрокаТаблицы Из Форма.ДенежныеОбязательства Цикл
			ДанныеСтроки = Новый Структура(СтруктураСтроки);
			ЗаполнитьЗначенияСвойств(ДанныеСтроки, СтрокаТаблицы);
			ДенежныеОбязательства.Добавить(ДанныеСтроки);
		КонецЦикла;
		
		Данные.Вставить("ДенежныеОбязательства", ДенежныеОбязательства);
		Данные.Вставить("ЗакупкаДляГосударственныхНужд", ЗакупкаДляГосударственныхНужд);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Форма.ОснованиеСоставителяНаименование)
		И ЗначениеЗаполнено(Форма.ОснованиеСоставителяНомер)
		И ЗначениеЗаполнено(Форма.ОснованиеСоставителяДата) Тогда
		
		ОснованиеСоставителяФайла = Новый Структура("НаименованиеДокумента, НомерДокумента, ДатаДокумента");
		ОснованиеСоставителяФайла.НаименованиеДокумента = Форма.ОснованиеСоставителяНаименование;
		ОснованиеСоставителяФайла.НомерДокумента = Форма.ОснованиеСоставителяНомер;
		ОснованиеСоставителяФайла.ДатаДокумента = Форма.ОснованиеСоставителяДата;
		
		Данные.Вставить("ОснованиеСоставителяФайла", ОснованиеСоставителяФайла);
	КонецЕсли;	
		
	Возврат Данные;
	
КонецФункции

// Служебный метод. Для получения данных см. ДанныеРучногоФормированияТитула
// 
// Параметры:
//  Форма - См. Обработка.ИнтерфейсДокументовЭДО.Форма.РучноеФормированиеОтветногоТитула
// 
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДОКлиентСервер.НовыеДанныеРучногоФормированияОтветногоТитулаУКД_2020
Функция ДанныеРучногоФормированияОтветногоТитулаУКД_2020(Форма)
	Данные = ЭлектронныеДокументыЭДОКлиентСервер.НовыеДанныеРучногоФормированияОтветногоТитулаУКД_2020();
	Данные.Вставить("СоставительДокументаНаименование", Форма.СоставительДокументаНаименование);
	Если Форма.СоставительДокумента <> Форма.Покупатель Тогда
		Данные.Вставить("СоставительДокументаДоверенность", Форма.СоставительДокументаДоверенность);
	КонецЕсли;
	Данные.Вставить("ДатаСогласования", Форма.ДатаСогласования);
	Данные.Вставить("СодержаниеОперации", Форма.СодержаниеОперации_УКД);
	Возврат Данные;
КонецФункции

// Служебный метод. Для получения данных см. ДанныеРучногоФормированияТитула
// 
// Параметры:
//  Форма - См. Обработка.ИнтерфейсДокументовЭДО.Форма.РучноеФормированиеОтветногоТитула
// 
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДОКлиентСервер.НовыеДанныеРучногоФормированияДополнительныхСведений
Функция ДанныеРучногоФормированияДополнительныхСведений(Форма)
	Данные = ЭлектронныеДокументыЭДОКлиентСервер.НовыеДанныеРучногоФормированияДополнительныхСведений();
	Данные.Вставить("СодержаниеДополнительныхСведений", Форма.СодержаниеДополнительныхСведений);
	Данные.Вставить("СоставительДокументаНаименование", Форма.СоставительДокументаНаименование);
	Данные.Вставить("ФормированиеДополнительныхСведений", Форма.ФормированиеДополнительныхСведений);
	Если Форма.СоставительДокумента <> Форма.Покупатель Тогда
		Данные.Вставить("СоставительДокументаДоверенность", Форма.СоставительДокументаДоверенность);
	КонецЕсли;
	Возврат Данные;
КонецФункции

// Служебный метод. Для получения данных см. ДанныеРучногоФормированияТитула
// 
// Параметры:
//  Форма - См. Обработка.ИнтерфейсДокументовЭДО.Форма.РучноеФормированиеОтветногоТитула
// 
// Возвращаемое значение:
//  Структура:
// * ЕстьРасхождения - Булево
Функция ДанныеРучногоФормированияАктаСверкиВзаиморасчетов(Форма)
	Данные = Новый Структура;
	Данные.Вставить("ЕстьРасхождения", Форма.ЕстьРасхождения);
	Возврат Данные;
КонецФункции

#КонецЕсли // ВнешнееСоединение

#КонецОбласти

// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//  * Ключ - Строка - См. ВариантыИнтерфейсаДокументовЭДО
//  * Значение - Строка - путь к форме текущих дел ЭДО
Функция ФормыТекущихДелЭДОПоВариантамИнтерфейса()
	
	ВариантыИнтерфейса = ВариантыИнтерфейсаДокументовЭДО();
	
	ТекущиеДелаЭДОПоВариантамИнтерфейса = Новый Соответствие;
	ТекущиеДелаЭДОПоВариантамИнтерфейса.Вставить(ВариантыИнтерфейса.Расширенный, 
		"Обработка.ИнтерфейсДокументовЭДО.Форма.ТекущиеДелаПоЭДО");
	ТекущиеДелаЭДОПоВариантамИнтерфейса.Вставить(ВариантыИнтерфейса.Легкий, 
		"Обработка.ИнтерфейсДокументовЭДО.Форма.ТекущиеДелаПоЭДОЛегкийИнтерфейс");
	ТекущиеДелаЭДОПоВариантамИнтерфейса.Вставить(ВариантыИнтерфейса.ЛегкийКлиентЭДО, 
		"Обработка.ИнтерфейсДокументовЭДО.Форма.ТекущиеДелаПоЭДОЛегкийКлиентЭДО");
	Возврат ТекущиеДелаЭДОПоВариантамИнтерфейса;
	
КонецФункции

// Параметры:
//  ЭтоВходящийЭДО - Булево
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//  * Ключ - Строка - См. ВариантыИнтерфейсаДокументовЭДО
//  * Значение - Строка - путь к форме просмотра электронного документа
Функция ФормыПросмотраЭлектронногоДокументаПоВариантамИнтерфейса(ЭтоВходящийЭДО)
	
	ВариантыИнтерфейса = ВариантыИнтерфейсаДокументовЭДО();
	
	Результат = Новый Соответствие;
	Если ЭтоВходящийЭДО Тогда
		Результат.Вставить(ВариантыИнтерфейса.Расширенный, 
			"Документ.ЭлектронныйДокументВходящийЭДО.Форма.ФормаПросмотра");
		Результат.Вставить(ВариантыИнтерфейса.Легкий, 
			"Документ.ЭлектронныйДокументВходящийЭДО.Форма.ФормаПросмотраЛегкийИнтерфейс");
		Результат.Вставить(ВариантыИнтерфейса.ЛегкийКлиентЭДО, 
			"Документ.ЭлектронныйДокументВходящийЭДО.Форма.ФормаПросмотраЛегкийКлиентЭДО");
	Иначе
		Результат.Вставить(ВариантыИнтерфейса.Расширенный, 
			"Документ.ЭлектронныйДокументИсходящийЭДО.Форма.ФормаПросмотра");
		Результат.Вставить(ВариантыИнтерфейса.Легкий, 
			"Документ.ЭлектронныйДокументИсходящийЭДО.Форма.ФормаПросмотраЛегкийИнтерфейс");
		Результат.Вставить(ВариантыИнтерфейса.ЛегкийКлиентЭДО, 
			"Документ.ЭлектронныйДокументИсходящийЭДО.Форма.ФормаПросмотраЛегкийКлиентЭДО");
	КонецЕсли;
			
	Возврат Результат;
	
КонецФункции

#КонецОбласти