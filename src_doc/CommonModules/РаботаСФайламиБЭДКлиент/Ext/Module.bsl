
#Область СлужебныйПрограммныйИнтерфейс

// Устанавливает путь к каталогу временных файлов в операционной системе Windows.
// 
// Параметры:
// 	Путь - Строка
// Возвращаемое значение:
//  Булево - Истина, если установка прошла успешно
Функция УстановитьПутьККаталогуВременныхФайлов(Путь) Экспорт
	
	Если Не ОбщегоНазначенияКлиент.ЭтоWindowsКлиент() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Установка пути к каталогу временных файлов не поддерживается для данной операционной системы'"));
		Возврат Ложь;
	КонецЕсли;
	
	#Если Не МобильныйКлиент Тогда
		
		Если Не ЗначениеЗаполнено(Путь) Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Не заполнен путь к каталогу временных файлов'"));
			Возврат Ложь;
		КонецЕсли;
		Оболочка = Новый COMОбъект("Wscript.Shell");
		Оболочка.RegWrite("HKEY_CURRENT_USER\Environment\TMP", Путь, "REG_EXPAND_SZ");
		
		Возврат Истина;
	#КонецЕсли
	
	Возврат Ложь;
	
КонецФункции

// Выполняет корректировку имени файла и предлагает пользователю исправить, если в имени были обнаружены некорректные символы.
//
// Параметры:
// 	ОповещениеОЗавершении - ОписаниеОповещения - описание процедуры, которая будет вызвана после проверки имени файла со
// 							следующими параметрами:
// 							 * ИмяФайла - Строка
// 							 			- Неопределено - Если пользователь отказался от ввода имени файла.
// 							 * АдресВХранилище - Строка
// 	ИмяФайла - Строка
//	Кодировка - Строка
//
Процедура СкорректироватьИмяФайлаСУчетомКодировки(ОповещениеОЗавершении, ИмяФайла, Кодировка = "windows-1251") Экспорт 
	
	РезультатПроверки = РаботаСФайламиБЭДКлиентСервер.ПроверитьИмяФайлаСУчетомКодировки(ИмяФайла, Кодировка);
	
	Если РезультатПроверки.ИмеютсяНекорректныеСимволы
		Или РезультатПроверки.ПустоеИмяФайла
		Или РезультатПроверки.ПустоеРасширение Тогда
		
		ПараметрыОповещения = 
			Новый Структура("ОповещениеОЗавершении, Кодировка", ОповещениеОЗавершении, Кодировка);
		
		ОповещениеПослеВводаСтроки = 
			Новый ОписаниеОповещения("СкорректироватьИмяФайлаПослеВводаСтроки", ЭтотОбъект, ПараметрыОповещения); 
			
		ДополнительноеОписание = 
			СтрШаблон(НСтр("ru = 'В имени файла ""%1"" присутствовали недопустимые символы, имя файла было скорректировано системой.
				|Проверьте и при необходимости отредактируйте имя файла.'"), РезультатПроверки.ИмяФайлаДоКорректировки);
	
		Если РезультатПроверки.ПустоеИмяФайла Тогда
			ДополнительноеОписание = 
				СтрШаблон(НСтр("ru = 'В имени файла ""%1"" присутствовали недопустимые символы, имя файла было скорректировано системой.
					|Имя файла не может быть пустым.'"), РезультатПроверки.ИмяФайлаДоКорректировки);
		ИначеЕсли РезультатПроверки.ПустоеРасширение Тогда
			ДополнительноеОписание = 
				СтрШаблон(НСтр("ru = 'В имени файла ""%1"" присутствовали недопустимые символы, имя файла было скорректировано системой.
					|Имя расширения файла не может быть пустым.'"), РезультатПроверки.ИмяФайлаДоКорректировки);
		КонецЕсли;
			
		КомментарийОбязательностиВвода = 
			НСтр("ru = 'Имя файла не может быть пустым. Введите имя файла.'");
			
		ДополнительныеПараметры = ОбщегоНазначенияБЭДКлиент.ПараметрыВводаСтроки();
		ДополнительныеПараметры.ЗаголовокФормы = НСтр("ru = 'Изменение имени файла'");
		ДополнительныеПараметры.Строка = РезультатПроверки.ИмяФайлаПослеКорректировки;
		ДополнительныеПараметры.ДополнительноеОписание = ДополнительноеОписание;
		ДополнительныеПараметры.Обязательность = Истина;
		ДополнительныеПараметры.КомментарийОбязательностиВвода = КомментарийОбязательностиВвода;
		
		ОбщегоНазначенияБЭДКлиент.ПоказатьВводСтрокиБЭД(ОповещениеПослеВводаСтроки, ДополнительныеПараметры);
		
	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, РезультатПроверки.ИмяФайлаПослеКорректировки);
	КонецЕсли;
	
КонецПроцедуры 

// Продолжение процедуры СкорректироватьИмяФайлаСУчетомКодировки после ввода строки пользователем.
//
// Параметры:
//  ИмяФайла - Строка
//  ДополнительныеПараметры - Структура:
//  * ОповещениеОЗавершении - ОписаниеОповещения 
//  * Кодировка - Строка
//
Процедура СкорректироватьИмяФайлаПослеВводаСтроки(ИмяФайла, ДополнительныеПараметры) Экспорт
	Если ИмяФайла = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении, ИмяФайла);
		Возврат;
	КонецЕсли;
	СкорректироватьИмяФайлаСУчетомКодировки(
		ДополнительныеПараметры.ОповещениеОЗавершении, ИмяФайла, ДополнительныеПараметры.Кодировка);
КонецПроцедуры 

// Выполняет преобазование имени файла в латиницу, если это требуется 
//
// Параметры:
//  ИмяФайла - Строка
//  ОтключитьТранслитерацию - Булево - Истина, если не переводить в латиницу.
//  
//  Возвращаемое значение:
//   Строка - Имя файла после преобразования.
//
Функция ПреобразоватьИмяФайлаСУчетомТранслитерации(ИмяФайла, ОтключитьТранслитерацию) Экспорт
	
	Результат = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайла, "_");

	Если Не ОтключитьТранслитерацию Тогда
		Результат = СтроковыеФункцииКлиент.СтрокаЛатиницей(Результат);
		Результат = СтрЗаменить(Результат, " ", "_");
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

// Параметры:
//  ИмяФайла - Строка
//  АдресФайла - Строка - Адрес файла во временном хранилище.
// 
// Возвращаемое значение:
//  Структура:
//  * ОписаниеФайла - Неопределено
//                  - См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
//  * СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//
Функция ОписаниеФайлаПослеПроверкиКорректностиИмениПоИмениИАдресуВоВременномХранилище(ИмяФайла, АдресФайла) Экспорт
	
	Результат = Новый Структура("ОписаниеФайла, СообщенияДляПользователя", Неопределено, Новый Массив);
	
	ДанныеФайловДляКорректировкиИмен = Новый Соответствие; // Соответствие Из КлючИЗначение
	
	НовоеОписаниеФайла = РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла();
		
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(АдресФайла);
	НовоеОписаниеФайла.ИмяФайла = ИмяФайла;
	НовоеОписаниеФайла.ДвоичныеДанные = ДвоичныеДанныеФайла;
	
	ДанныеФайловДляКорректировкиИмен.Вставить(Новый УникальныйИдентификатор(), НовоеОписаниеФайла);
	
	РезультатыКорректировки = СкорректироватьИменаФайловСУчетомКодировкиБезУчастияПользователя(
		ДанныеФайловДляКорректировкиИмен);
		
	ТранслитерацияОтключена = Не ИнтерфейсДокументовЭДОВызовСервера.ВключенаТранслитерацияИменФайлов();
	
	Для Каждого ДанныеФайла Из ДанныеФайловДляКорректировкиИмен Цикл
		
		ОписаниеФайла = ДанныеФайла.Значение;
		РезультатКорректировки = РезультатыКорректировки[ДанныеФайла.Ключ];
		
		Если ЗначениеЗаполнено(РезультатКорректировки.ТекстыСообщенияПользователю) Тогда
			Результат.СообщенияДляПользователя = РезультатКорректировки.ТекстыСообщенияПользователю;
		КонецЕсли;
		
		Если РезультатКорректировки.ЕстьОшибки
			И Не ЗначениеЗаполнено(РезультатКорректировки.ИмяФайлаПослеКорректировки) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РезультатКорректировки.ИмяФайлаПослеКорректировки) Тогда
			ОписаниеФайла.ИмяФайла = РезультатКорректировки.ИмяФайлаПослеКорректировки;
		КонецЕсли;
		
		ОписаниеФайла.ИмяФайла = ПреобразоватьИмяФайлаСУчетомТранслитерации(ОписаниеФайла.ИмяФайла,
			ТранслитерацияОтключена);
		
		Результат.ОписаниеФайла = ОписаниеФайла;
		
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

// Параметры:
//  ВыбранныеФайлы - Массив из Структура:
//                   * Хранение  - Строка - адрес временного хранилища с двоичными данными файла.
//                   * Имя       - Строка
//                   * ПолноеИмя - Строка
//                   * ИмяФайла  - Строка - имя файла с расширением.
// 
// Возвращаемое значение:
//  Структура - Описания файлов после проверки корректности имен:
//  * ОписанияФайлов - Массив Из См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
//  * СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//
Функция ОписанияФайловПослеПроверкиКорректностиИмен(ВыбранныеФайлы) Экспорт
	
	Результат = Новый Структура("ОписанияФайлов, СообщенияДляПользователя", Новый Массив, Новый Массив);
	
	ДанныеФайловДляКорректировкиИмен = Новый Соответствие; // Соответствие Из КлючИЗначение
	Для Каждого ВыбранныйФайл Из ВыбранныеФайлы Цикл
		
		НовоеОписаниеФайла = РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла();
		
		ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ВыбранныйФайл.Хранение);
		УдалитьИзВременногоХранилища(ВыбранныйФайл.Хранение);
		НовоеОписаниеФайла.ИмяФайла = ВыбранныйФайл.ИмяФайла;
		НовоеОписаниеФайла.ДвоичныеДанные = ДвоичныеДанныеФайла;
		
		ДанныеФайловДляКорректировкиИмен.Вставить(ВыбранныйФайл.Имя, НовоеОписаниеФайла);

	КонецЦикла;
	
	РезультатыКорректировки = СкорректироватьИменаФайловСУчетомКодировкиБезУчастияПользователя(
		ДанныеФайловДляКорректировкиИмен);
		
	ТранслитерацияОтключена = Не ИнтерфейсДокументовЭДОВызовСервера.ВключенаТранслитерацияИменФайлов();
	
	ОписанияФайлов = Новый Массив; // Массив Из См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
	СообщенияДляПользователя = Новый Массив; // Массив Из ФорматированнаяСтрока
	
	Для Каждого ДанныеФайла Из ДанныеФайловДляКорректировкиИмен Цикл
		
		ОписаниеФайла = ДанныеФайла.Значение;
		РезультатКорректировки = РезультатыКорректировки[ДанныеФайла.Ключ];
		
		Если ЗначениеЗаполнено(РезультатКорректировки.ТекстыСообщенияПользователю) Тогда
			Для Каждого ТекстСообщенияПользователю Из РезультатКорректировки.ТекстыСообщенияПользователю Цикл
				СообщенияДляПользователя.Добавить(ТекстСообщенияПользователю);
				СообщенияДляПользователя.Добавить(Символы.ПС);
				СообщенияДляПользователя.Добавить(Символы.ПС);
			КонецЦикла;
		КонецЕсли;
		
		Если РезультатКорректировки.ЕстьОшибки
			И Не ЗначениеЗаполнено(РезультатКорректировки.ИмяФайлаПослеКорректировки) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РезультатКорректировки.ИмяФайлаПослеКорректировки) Тогда
			ОписаниеФайла.ИмяФайла = РезультатКорректировки.ИмяФайлаПослеКорректировки;
		КонецЕсли;
		
		ОписаниеФайла.ИмяФайла = ПреобразоватьИмяФайлаСУчетомТранслитерации(ОписаниеФайла.ИмяФайла,
			ТранслитерацияОтключена);
		
		ОписанияФайлов.Добавить(ОписаниеФайла);
		
	КонецЦикла;
	
	Результат.ОписанияФайлов = ОписанияФайлов;
	Результат.СообщенияДляПользователя = СообщенияДляПользователя;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  СообщенияПользователю - Массив Из ФорматированнаяСтрока
//
Процедура ПоказатьСообщенияРезультатаПроверкиИменФайловПользователю(СообщенияПользователю) Экспорт
		
	Если Не ЗначениеЗаполнено(СообщенияПользователю) Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияБЭДКлиент.ПоказатьФорматированнуюСтроку(
		Новый ФорматированнаяСтрока(СообщенияПользователю), 
		НСтр("ru = 'Проверка имен файлов'"), НСтр("ru = 'Понятно'"));

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращаемое значение:
//  Структура:
//  * ЕстьОшибки - Булево
//  * ИмяФайлаПослеКорректировки - Строка
//  * ТекстыСообщенияПользователю - Массив Из ФорматированнаяСтрока
//
Функция НовыйРезультатКорректировкиИмениФайла()

	Результат = Новый Структура;
	Результат.Вставить("ЕстьОшибки", Ложь);
	Результат.Вставить("ИмяФайлаПослеКорректировки", "");
	Результат.Вставить("ТекстыСообщенияПользователю", Новый Массив);
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ДанныеФайлов - Соответствие Из КлючИЗначение:
//  * Ключ - УникальныйИдентификатор - идентификатор файла, требуется, т.к. имена файлов могут совпадать.
//  * Значение - См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
//  Кодировка - Строка
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//  * Ключ - УникальныйИдентификатор - идентификатор файла.
//  * Значение - См. НовыйРезультатКорректировкиИмениФайла
Функция СкорректироватьИменаФайловСУчетомКодировкиБезУчастияПользователя(ДанныеФайлов,
	Кодировка = "windows-1251")
	
	Результат = Новый Соответствие;
	
	Для Каждого ДанныеФайла Из ДанныеФайлов Цикл

		ОписаниеФайла = ДанныеФайла.Значение;
		ИмяФайла = ОписаниеФайла.ИмяФайла;
		
		РезультатПроверки = РаботаСФайламиБЭДКлиентСервер.ПроверитьИмяФайлаСУчетомКодировки(ИмяФайла, Кодировка);
		
		РезультатКорректировки = НовыйРезультатКорректировкиИмениФайла();
		РезультатКорректировки.ЕстьОшибки = РезультатПроверки.ИмеютсяНекорректныеСимволы
			Или РезультатПроверки.ПустоеИмяФайла Или РезультатПроверки.ПустоеРасширение;
		
		Если РезультатПроверки.ИмеютсяНекорректныеСимволы Тогда
			
			РезультатКорректировки.ИмяФайлаПослеКорректировки = РезультатПроверки.ИмяФайлаПослеКорректировки;
			РезультатКорректировки.ТекстыСообщенияПользователю.Добавить(
				ТекстСообщенияИмеютсяНекорректныеСимволы(ИмяФайла));
		
		КонецЕсли;
		
		Если РезультатПроверки.ПустоеИмяФайла Тогда
			
			РезультатКорректировки.ТекстыСообщенияПользователю.Добавить(ТекстСообщенияЗаполнитеИмяФайла(ИмяФайла));
			
		КонецЕсли;
		
		Если РезультатПроверки.ПустоеРасширение Тогда
			
			РасширениеФайла = РаботаСФайламиБЭДВызовСервера.РасширениеФайлаПоСигнатуре(ОписаниеФайла);
			Если ЗначениеЗаполнено(РасширениеФайла) Тогда
				РезультатКорректировки.ИмяФайлаПослеКорректировки = СтрШаблон("%1.%2", 
					РезультатКорректировки.ИмяФайлаПослеКорректировки, РасширениеФайла);
				РезультатКорректировки.ТекстыСообщенияПользователю.Добавить(ТекстСообщенияПустоеРасширение(ИмяФайла));
			Иначе
				РезультатКорректировки.ТекстыСообщенияПользователю.Добавить(
					ТекстСообщенияЗаполнитеРасширение(ИмяФайла));
			КонецЕсли;
			
		КонецЕсли;
		Результат.Вставить(ДанныеФайла.Ключ, РезультатКорректировки);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ИмяФайла - Строка
//
// Возвращаемое значение:
//  ФорматированнаяСтрока
Функция ТекстСообщенияИмеютсяНекорректныеСимволы(ИмяФайла)
	
	ШаблонСтроки = СтрШаблон(
		НСтр("ru = 'В имени файла %1 присутствовали недопустимые символы, имя файла было скорректировано системой.'"),
		ШаблонСтрокиИмяФайла());
	Возврат СтроковыеФункцииКлиент.ФорматированнаяСтрока(ШаблонСтроки, ИмяФайла);
	
КонецФункции

// Параметры:
//  ИмяФайла - Строка
//
// Возвращаемое значение:
//  ФорматированнаяСтрока
Функция ТекстСообщенияЗаполнитеИмяФайла(ИмяФайла)
	
	ШаблонСтроки = СтрШаблон(
		НСтр("ru = 'Файл %1 не добавлен, так как в имени файла заполнено только расширение.
			|Заполните имя файла или выберите другой файл.'"),
		ШаблонСтрокиИмяФайла());
	Возврат СтроковыеФункцииКлиент.ФорматированнаяСтрока(ШаблонСтроки, ИмяФайла);
	
КонецФункции

// Параметры:
//  ИмяФайла - Строка
//
// Возвращаемое значение:
//  ФорматированнаяСтрока
Функция ТекстСообщенияПустоеРасширение(ИмяФайла)
	
	ШаблонСтроки = СтрШаблон(
		НСтр("ru = 'В имени файла %1 не заполнено расширение, имя файла было скорректировано системой.'"),
		ШаблонСтрокиИмяФайла());
	Возврат СтроковыеФункцииКлиент.ФорматированнаяСтрока(ШаблонСтроки, ИмяФайла);
	
КонецФункции

// Параметры:
//  ИмяФайла - Строка
//
// Возвращаемое значение:
//  ФорматированнаяСтрока
Функция ТекстСообщенияЗаполнитеРасширение(ИмяФайла)
	
	ШаблонСтроки = СтрШаблон(
		НСтр("ru = 'Файл %1 не добавлен, так как не заполнено расширение.
			|Заполните расширение в имени файла или выберите другой файл.'"),
		ШаблонСтрокиИмяФайла());
	Возврат СтроковыеФункцииКлиент.ФорматированнаяСтрока(ШаблонСтроки, ИмяФайла);
	
КонецФункции

// Возвращаемое значение:
//  Строка
Функция ШаблонСтрокиИмяФайла()
	
	Возврат "<span style=""color:ЦветГиперссылкиБЭД"">""%1""</span>";
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
