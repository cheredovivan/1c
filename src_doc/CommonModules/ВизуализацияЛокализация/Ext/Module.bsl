

#Область ПрограммныйИнтерфейс

// Возвращает признак использования внешнего сервиса вставки ЭП
//
// Возвращаемое значение:
//   Булево
//
Функция ИспользуетсяВнешнийСервисВставкиШтампаЭП() Экспорт
	
	// Локализация
	УстановитьПривилегированныйРежим(Истина);
	Если Константы.Использовать1СШтамп.Получить() Тогда
		Возврат Истина;
	КонецЕсли;
	// Локализация Конец
	
	Возврат Ложь;
	
КонецФункции

// Возвращает массив грифов доступа для внешнего сервиса вставки ЭП. Если пусто, то любые допустимы.
//
// Возвращаемое значение:
//   Массив из СправочникСсылка.ГрифыДоступа
//
Функция ГрифыДоступаДляВнешнегоСервиса() Экспорт
	
	Результат = Новый Массив;
	// Локализация
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат, ГрифыДоступаДля1СШтамп());
	// Локализация Конец
	Возврат Результат;
	
КонецФункции	

// Возвращает массив видов документов для внешнего сервиса вставки ЭП. Если пусто, то любые допустимы.
//
// Возвращаемое значение:
//   Массив из СправочникСсылка.ВидыДокументов
//
Функция ВидыДокументовДляВнешнегоСервиса() Экспорт
	
	Результат = Новый Массив;
	// Локализация
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат, ВидыДокументовДля1СШтамп());
	// Локализация Конец
	Возврат Результат;
	
КонецФункции

Функция ДляДанногоФайлаМожноИспользоватьВнешнийСервисВставкиЭП(ДанныеФайла, ИтоговыйФорматФайлаСоШтампомЭП) Экспорт
	
	// Локализация
	Возврат ДляДанногоФайлаМожноИспользовать1СШтамп(ДанныеФайла, ИтоговыйФорматФайлаСоШтампомЭП);
	// Локализация Конец
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

// Локализация
#Область СлужебныеПроцедурыИФункции

// Возвращает массив грифов доступа для 1С Штамп. Если пусто , то любые допустимы.
//
// Возвращаемое значение:
//   Массив из СправочникССылка.ГрифыДоступа
//
Функция ГрифыДоступаДля1СШтамп()
	
	УстановитьПривилегированныйРежим(Истина);

	МассивГрифов = Новый Массив;
	Выборка = РегистрыСведений.ГрифыДоступаДля1СШтамп.Выбрать();
	Пока Выборка.Следующий() Цикл
		МассивГрифов.Добавить(Выборка.ГрифДоступа);
	КонецЦикла;
	
	Возврат МассивГрифов;
	
КонецФункции	

// Возвращает массив видов документов для 1С Штамп. Если пусто , то любые допустимы.
//
// Возвращаемое значение:
//   Массив из СправочникССылка.ВидыДокументов
//
Функция ВидыДокументовДля1СШтамп()
	
	УстановитьПривилегированныйРежим(Истина);

	МассивВидов = Новый Массив;
	Выборка = РегистрыСведений.ВидыДокументовДля1СШтамп.Выбрать();
	Пока Выборка.Следующий() Цикл
		МассивВидов.Добавить(Выборка.ВидДокумента);
	КонецЦикла;
	
	Возврат МассивВидов;
	
КонецФункции

Функция ДляДанногоФайлаМожноИспользовать1СШтамп(ДанныеФайла, ИтоговыйФорматФайлаСоШтампомЭП)
	
	ИспользоватьВнешнийСервисВставкиЭП = ВизуализацияПовтИсп.ИспользуетсяВнешнийСервисВставкиШтампаЭП();
	
	Если Не ИспользоватьВнешнийСервисВставкиЭП Тогда // проверим и возможно ИспользоватьВнешнийСервисВставкиЭП поставим Ложь  
		Возврат Ложь;
	КонецЕсли;	
	
	// Смотрим на настройки (на гриф, вид документа, настройка «Итоговый формат», размер файла (превышает ли 5 мб), 
	// есть ли в doc docx файле ВставитьЭП и другие теги
	
	Если ДанныеФайла.Размер > 30000000 Тогда // более 30 мб
		Возврат Ложь;
	КонецЕсли;	  
	
	ГрифыДоступаДля1СШтамп = ВизуализацияПовтИсп.ГрифыДоступаДляВнешнегоСервиса();
	ВидыДокументовДля1СШтамп = ВизуализацияПовтИсп.ВидыДокументовДляВнешнегоСервиса();
	
	Если ГрифыДоступаДля1СШтамп.Количество() <> 0 Или ВидыДокументовДля1СШтамп.Количество() <> 0 Тогда  
		
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеФайла.Владелец, "ВидДокумента, ГрифДоступа");
		
		Если ГрифыДоступаДля1СШтамп.Количество() <> 0 И ГрифыДоступаДля1СШтамп.Найти(РеквизитыДокумента.ГрифДоступа) = Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;	
		
		Если ВидыДокументовДля1СШтамп.Количество() <> 0 Тогда
			
			ВходитВГруппу = Ложь;
			Если ВидыДокументовДля1СШтамп.Найти(РеквизитыДокумента.ВидДокумента) = Неопределено Тогда
				// проверим на вхождение в группы
				
				Для Каждого ВидДокументаШтамп Из ВидыДокументовДля1СШтамп Цикл     
					Если РеквизитыДокумента.ВидДокумента.ПринадлежитЭлементу(ВидДокументаШтамп) Тогда
						ВходитВГруппу = Истина;
						Прервать;
					КонецЕсли;	
				КонецЦикла;	

				Если Не ВходитВГруппу Тогда
					Возврат Ложь;
				КонецЕсли;	
				
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;	
	
	ЭтоМЭДО = МЭДОПереопределяемый.ЭтоДокументМЭДО(ДанныеФайла.Владелец, Перечисления.НаправленияСообщенийМЭДО.Входящее)
		Или МЭДОПереопределяемый.ЭтоДокументМЭДО(ДанныеФайла.Владелец, Перечисления.НаправленияСообщенийМЭДО.Исходящее);
	Если ЭтоМЭДО Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ИспользоватьВнешнийСервисВставкиЭП;
	
КонецФункции

#КонецОбласти
// Локализация Конец
