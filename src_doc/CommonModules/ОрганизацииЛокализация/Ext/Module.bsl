#Область ПрограммныйИнтерфейс

#Область ФормаЭлемента

// Вызывается из "ПриСозданииНаСервере" формы элемента справочника Организации
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
Процедура ПриСозданииНаСервере_ФормаЭлемента(Форма, Отказ, СтандартнаяОбработка) Экспорт

// Локализация
	Элементы = Форма.Элементы;
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыПриСозданииНаСервере = ОбменСКонтрагентами.ПараметрыПриСозданииНаСервере_ФормаСправочника();
	ПараметрыПриСозданииНаСервере.Форма                 = Форма;
	ПараметрыПриСозданииНаСервере.МестоРазмещенияКоманд = Элементы.КомандыЭДО;
	ОбменСКонтрагентами.ПриСозданииНаСервере_ФормаСправочника(ПараметрыПриСозданииНаСервере);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ЭтоНовыйЭлемент = Форма.Параметры.Ключ.Пустая();
	Элементы.ГруппаЗаполнениеПоДаннымЕдиныхГосРеестров.Видимость = ЭтоНовыйЭлемент;
	Элементы.ЗаполнитьПоИНН.Видимость = НЕ ЭтоНовыйЭлемент;
	Элементы.ЗаполнитьПоИНН_ИП.Видимость = Не ЭтоНовыйЭлемент;
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
	
	Элементы.ПолеПоискаИНННаименование.ПодсказкаВвода = СтрШаблон(
		НСтр("ru = 'Введите ИНН или наименование %1'"), РедакцииКонфигурацииКлиентСервер.ОрганизацииРодительный());
	Форма.Команды.ЗаполнитьПоИНН.Подсказка = СтрШаблон(
		НСтр("ru = 'Автоматически заполнить реквизиты %1
		|по ИНН на основании данных ЕГРЮЛ / ЕГРИП'"), РедакцииКонфигурацииКлиентСервер.ОрганизацииРодительный());
	
// Локализация Конец

КонецПроцедуры

#КонецОбласти

#Область ФормаСписка

// Вызывается из "ПриСозданииНаСервере" формы списка справочника Организации
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
Процедура ПриСозданииНаСервере_ФормаСписка(Форма, Отказ, СтандартнаяОбработка) Экспорт

// Локализация
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыПриСозданииНаСервере = ОбменСКонтрагентами.ПараметрыПриСозданииНаСервере_ФормаСписка();
	ПараметрыПриСозданииНаСервере.Форма = Форма;
	ПараметрыПриСозданииНаСервере.МестоРазмещенияКоманд = Форма.Элементы.КомандыЭДО;
	ПараметрыПриСозданииНаСервере.ИсточникКомандЭДО = Форма.Список;
	ОбменСКонтрагентами.ПриСозданииНаСервере_ФормаСписка(ПараметрыПриСозданииНаСервере);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
// Локализация Конец

КонецПроцедуры

// Вызывается из "СписокПриПолученииДанныхНаСервере" формы списка справочника Организации
// 
// Параметры:
//  ИмяЭлемента - Строка - Имя элемента
//  Настройки - НастройкиКомпоновкиДанных - Настройки
//  Строки - СтрокиДинамическогоСписка - Строки
Процедура СписокПриПолученииДанныхНаСервере_ФормаСписка(ИмяЭлемента, Настройки, Строки) Экспорт

	// Локализация
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентами.ПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	// Локализация Конец

КонецПроцедуры

#КонецОбласти

#Область МодульОбъекта

// Вызывается из "ОбработкаПроверкиЗаполнения" модуля объекта справочника Организации
// 
// Параметры:
//  Объект - СправочникСсылка.Организации
//  Отказ - Булево
Процедура ОбработкаПроверкиЗаполнения_МодульОбъекта(Объект, Отказ) Экспорт

// Локализация	

	ТекстСообщения = "";
	
	Если Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда 
	
		Если ЗначениеЗаполнено(Объект.ИНН) 
			И Не РегламентированныеДанныеКлиентСерверЛокализация.ИННСоответствуетТребованиям(
				Объект.ИНН, Истина, ТекстСообщения) Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, "Объект.ИНН",, Отказ);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.КПП) 
			И Не РегламентированныеДанныеКлиентСерверЛокализация.КППСоответствуетТребованиям(
				Объект.КПП, ТекстСообщения) Тогда
 			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, "Объект.КПП",, Отказ);
 		КонецЕсли;
	
		Если ЗначениеЗаполнено(Объект.КодПоОКПО) 
			И Не РегламентированныеДанныеКлиентСерверЛокализация.КодПоОКПОСоответствуетТребованиям(
				Объект.КодПоОКПО, Истина, ТекстСообщения) Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, "Объект.КодПоОКПО",, Отказ);
		КонецЕсли;	
		
	ИначеЕсли Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда 
		
		Если ЗначениеЗаполнено(Объект.ИНН) 
			И Не РегламентированныеДанныеКлиентСерверЛокализация.ИННСоответствуетТребованиям(
				Объект.ИНН, Ложь, ТекстСообщения) Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, "Объект.ИНН",, Отказ);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.КодПоОКПО) 
			И Не РегламентированныеДанныеКлиентСерверЛокализация.КодПоОКПОСоответствуетТребованиям(
				Объект.КодПоОКПО, Ложь, ТекстСообщения) Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, "Объект.КодПоОКПО",, Отказ);
		КонецЕсли;
	Иначе
		// 
	КонецЕсли;	

// Локализация Конец	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти