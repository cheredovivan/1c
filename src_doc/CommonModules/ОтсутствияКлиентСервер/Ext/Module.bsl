////////////////////////////////////////////////////////////////////////////////
// Подсистема "Отсутствия".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает пустое HTML представление.
//
// Возвращаемое значение:
//  Строка - пустое HTML представление.
//
Функция ПолучитьПустоеHTMLПредставление() Экспорт
	
	Возврат "<html><body scroll=auto></body></html>";
	
КонецФункции

// Возвращает структуру данных об исполнителе.
//
// Параметры:
//  Исполнитель - СправочникСсылка.ПолныеРоли, СправочникСсылка.Пользователи - Исполнитель.
//  СрокИсполнения - Дата - Срок исполнения.
//
// Возвращаемое значение:
//  Структура - Данные исполнителя.
//
Функция ПолучитьДанныеИсполнителя(Исполнитель = Неопределено, СрокИсполнения = Неопределено) Экспорт
	
	Если СрокИсполнения = Неопределено Тогда
		СрокИсполнения = Дата(1, 1, 1);
	КонецЕсли;
	
	ДанныеИсполнителя = Новый Структура;
	ДанныеИсполнителя.Вставить("Исполнитель", Исполнитель);
	ДанныеИсполнителя.Вставить("СрокИсполнения", СрокИсполнения);
	
	Возврат ДанныеИсполнителя;
	
КонецФункции

// Формирует представление отсутствия.
//
// Параметры:
//  РеквизитыОтсутствия - Структура - Структура реквизитов отсутствия.
//  ВключатьОписаниеДаты - Булево - Признак того, что следует формировать описание даты.
//
// Возвращаемое значение:
//  Строка - Представление отсутствия.
//
Функция СформироватьПредставлениеОтсутствия(РеквизитыОтсутствия, ВключатьОписаниеДаты) Экспорт
	
	ОписаниеСотрудника = Строка(РеквизитыОтсутствия.Сотрудник);
	ОписаниеВидаОтсутствия = Строка(РеквизитыОтсутствия.ВидОтсутствия);
	ОписаниеОтсутствия = ОписаниеВидаОтсутствия + " - " + ОписаниеСотрудника;
	Если ВключатьОписаниеДаты Тогда
		ОписаниеДаты = СформироватьДатыОтсутствия(
			РеквизитыОтсутствия.ДатаНачала, РеквизитыОтсутствия.ДатаОкончания,
			РеквизитыОтсутствия.ВесьДень, Ложь, Ложь, Ложь);
		ОписаниеОтсутствия = ОписаниеОтсутствия + " - " + ОписаниеДаты;
	КонецЕсли;
	
	Возврат ОписаниеОтсутствия;
	
КонецФункции

// Формирует текстовое представление периода отсутствия.
//
// Параметры:
//  ДатаНачала - Дата - Дата начала отсутствия.
//  ДатаОкончания - Дата - Дата окончания отсутствия.
//  ВесьДень - Булево - Признак того что отсутствие на весь день.
//  ПредставлениеЧасовогоПояса - Строка - Представление часового пояса.
//
// Возвращаемое значение:
//  Строка - Текстовое представление периода отсутствия.
//
Функция ПериодСтрокой(ДатаНачала, ДатаОкончания, ВесьДень, ПредставлениеЧасовогоПояса) Экспорт
	
	ПериодСтрокой = СформироватьДатыОтсутствия(
		ДатаНачала,
		ДатаОкончания,
		ВесьДень,
		Истина,
		Ложь,
		Истина);
	Если ЗначениеЗаполнено(ПредставлениеЧасовогоПояса) Тогда
		ПериодСтрокой = ПериодСтрокой + " " + ПредставлениеЧасовогоПояса;
	КонецЕсли;
	
	Возврат ПериодСтрокой;
	
КонецФункции

// Формирует текстовое представление дат отсутствия.
//
// Параметры:
//  ДатаНачала - Дата - Дата начала отсутствия.
//  ДатаОкончания - Дата - Дата окончания отсутствия.
//  ВесьДень - Булево - Признак того что отсутствие на весь день.
//  ПолнаяДата - Булево - Признак того, что дату следует формировать в виде полной даты.
//  ДатаДо - Булево - Флаг, определяющий следует ли включать только дату окончания в описание даты.
//  КраткоеОписание - Булево - Флаг, определяющий следует ли использовать краткий формат даты.
//
// Возвращаемое значение:
//  Строка - Текстовое представление даты отсутствия.
//
Функция СформироватьДатыОтсутствия(ДатаНачала, ДатаОкончания,
	ВесьДень, ПолнаяДата, ДатаДо, КраткоеОписание) Экспорт
	
	ОписаниеДаты = "";

	Если ДатаДо Тогда
		ОписаниеДатыОкончания = СформироватьТекстовоеОписаниеДаты(ДатаОкончания, ВесьДень,
			ПолнаяДата, КраткоеОписание, Истина);
		ОписаниеДаты = СтрШаблон(НСтр("ru = 'до %1'"), ОписаниеДатыОкончания);	
	ИначеЕсли НачалоДня(ДатаОкончания) - НачалоДня(ДатаНачала) = 0 Тогда
		День = СформироватьТекстовоеОписаниеДаты(ДатаНачала, Истина,
			ПолнаяДата, КраткоеОписание, Истина);
		Если ВесьДень Тогда
			ОписаниеДаты = День;
		Иначе
			ВремяНачала = Формат(ДатаНачала, "ДФ='HH:mm'");
			ВремяОкончания = Формат(ДатаОкончания, "ДФ='HH:mm'");
			
			Если КраткоеОписание Тогда
				ОписаниеДаты = СтрШаблон(НСтр("ru = '%1 %2 - %3'"), День, ВремяНачала, ВремяОкончания);			
			Иначе
				ОписаниеДаты = СтрШаблон(НСтр("ru = '%1 с %2 по %3'"), День, ВремяНачала, ВремяОкончания);
			КонецЕсли;
		КонецЕсли;
	Иначе
		ВключатьОписаниеГода = НачалоГода(ДатаНачала) <> НачалоГода(ДатаОкончания);
		ОписаниеДатыНачала = СформироватьТекстовоеОписаниеДаты(ДатаНачала, ВесьДень,
			ПолнаяДата, КраткоеОписание, ВключатьОписаниеГода);
		ОписаниеДатыОкончания = СформироватьТекстовоеОписаниеДаты(ДатаОкончания, ВесьДень,
			ПолнаяДата, КраткоеОписание, Истина);
		Если КраткоеОписание Тогда
			ОписаниеДаты = СтрШаблон(НСтр("ru = '%1 - %2'"), ОписаниеДатыНачала, ОписаниеДатыОкончания);
		Иначе
			ОписаниеДаты = СтрШаблон(НСтр("ru = 'с %1 по %2'"), ОписаниеДатыНачала, ОписаниеДатыОкончания);
		КонецЕсли;
	КонецЕсли;

	Возврат ОписаниеДаты;
	
КонецФункции

// Формирует структуру настроек проверки отсутствий.
//
// Возвращаемое значение:
//  Структура - Настройки проверки отсутствий.
//
Функция НастройкиПроверкиОтсутствий() Экспорт
	
	НастройкиПроверкиОтсутствий = Новый Структура;
	НастройкиПроверкиОтсутствий.Вставить("УчитыватьФлагБудуРазбиратьЗадачи", Истина);
	НастройкиПроверкиОтсутствий.Вставить("УчитыватьТолькоВидыОтсутствийНеВФонде", Ложь);
	
	Возврат НастройкиПроверкиОтсутствий;
	
КонецФункции

// Формирует новую структуру результата проверки отсутствия.
// 
// Возвращаемое значение:
//  Структура - Результат проверки отсутствия:
// * ЕстьОтсутствия - Булево.
// * Отсутствия - Массив из ДокументСсылка.Отсутствие. 
// 
Функция НовыйРезультатПроверкиОтсутствий() Экспорт
	
	РезультатПроверкиОтсутствий = Новый Структура;
	РезультатПроверкиОтсутствий.Вставить("ЕстьОтсутствия", Ложь);
	РезультатПроверкиОтсутствий.Вставить("Отсутствия", Новый Массив);
	
	Возврат РезультатПроверкиОтсутствий;
	
КонецФункции

// Формирует имя ключевой операции "ОтсутствияОткрытиеОтсутствий".
// 
// Возвращаемое значение:
//  Строка
// 
Функция КлючеваяОперацияОткрытиеОтсутствий() Экспорт
	
	КлючеваяОперация = "ОтсутствияОткрытиеОтсутствий";
	
	Возврат КлючеваяОперация;
	
КонецФункции

// Формирует имя ключевой операции "ОтсутствияОткрытиеОтсутствия".
// 
// Возвращаемое значение:
//  Строка
// 
Функция КлючеваяОперацияОткрытиеОтсутствия() Экспорт
	
	КлючеваяОперация = "ОтсутствияОткрытиеОтсутствия";
	
	Возврат КлючеваяОперация;
	
КонецФункции

// Формирует имя ключевой операции "ОтсутствияСозданиеОтсутствия".
// 
// Возвращаемое значение:
//  Строка
// 
Функция КлючеваяОперацияСозданиеОтсутствия() Экспорт
	
	КлючеваяОперация = "ОтсутствияСозданиеОтсутствия";
	
	Возврат КлючеваяОперация;
	
КонецФункции

// Формирует имя ключевой операции "ОтсутствияЗаписьОтсутствия".
// 
// Возвращаемое значение:
//  Строка
// 
Функция КлючеваяОперацияЗаписьОтсутствия() Экспорт
	
	КлючеваяОперация = "ОтсутствияЗаписьОтсутствия";
	
	Возврат КлючеваяОперация;
	
КонецФункции

// Формирует имя ключевой операции "ОтсутствияПроверкаОтсутствий".
// 
// Возвращаемое значение:
//  Строка
// 
Функция КлючеваяОперацияПроверкаОтсутствий() Экспорт
	
	КлючеваяОперация = "ОтсутствияПроверкаОтсутствий";
	
	Возврат КлючеваяОперация;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует текстовое представление даты.
//
// Параметры:
//  Дата - Дата - Дата, текстовое представление которой необходимо сформировать.
//  ВесьДень - Булево - Признак того, что в дату не нужно включать время.
//  ПолнаяДата - Булево - Признак того, что дату следует формировать в виде полной даты.
//  КраткоеОписание - Булево - Флаг, определяющий следует ли использовать краткий формат даты.
//
// Возвращаемое значение:
//  Строка - Текстовое представление даты.
//
Функция СформироватьТекстовоеОписаниеДаты(Дата, ВесьДень, ПолнаяДата, КраткоеОписание, ВключатьОписаниеГода)

	ФорматДаты = "";
	
	ПериодСледующийГод = Новый СтандартныйПериод(ВариантСтандартногоПериода.СледующийГод);
	НачалоСледующегоГода = ПериодСледующийГод.ДатаНачала;
	ПериодЭтотГод = Новый СтандартныйПериод(ВариантСтандартногоПериода.ЭтотГод);
	НачалоЭтогоГода = ПериодЭтотГод.ДатаНачала;
	ПериодЗавтра = Новый СтандартныйПериод(ВариантСтандартногоПериода.Завтра);
	НачалоЗавтрашнегоДня = ПериодЗавтра.ДатаНачала;
	ПериодСегодня = Новый СтандартныйПериод(ВариантСтандартногоПериода.Сегодня);
	НачалоЭтогоДня = ПериодСегодня.ДатаНачала;
	ПериодВчера = Новый СтандартныйПериод(ВариантСтандартногоПериода.Вчера);
	НачалоВчерашнегоДня = ПериодВчера.ДатаНачала;
	
	ДатаДень = НачалоДня(Дата);
	
	МассивФорматДаты = Новый Массив;
	ПредставлениеДня = "";
	Если Дата >= НачалоСледующегоГода Или Дата < НачалоЭтогоГода Или ПолнаяДата Тогда
		Если КраткоеОписание Тогда
			МассивФорматДаты.Добавить("d MMM");
		Иначе
			МассивФорматДаты.Добавить("d MMMM");
		КонецЕсли;
		Если ВключатьОписаниеГода Тогда
			МассивФорматДаты.Добавить("yyyy");
		КонецЕсли;
	ИначеЕсли ДатаДень = НачалоЗавтрашнегоДня Тогда
		ПредставлениеДня = НСтр("ru = 'Завтра'");
	ИначеЕсли ДатаДень = НачалоЭтогоДня Тогда
		ПредставлениеДня = НСтр("ru = 'Сегодня'");
	ИначеЕсли ДатаДень = НачалоВчерашнегоДня Тогда
		ПредставлениеДня = НСтр("ru = 'Вчера'");
	ИначеЕсли (Дата < НачалоСледующегоГода И Дата >= НачалоЗавтрашнегоДня) Или 
				(Дата < НачалоВчерашнегоДня И Дата >= НачалоЭтогоГода) Тогда
		Если КраткоеОписание Тогда
			МассивФорматДаты.Добавить("d MMM");
		Иначе
			МассивФорматДаты.Добавить("d MMMM");
		КонецЕсли;
	КонецЕсли;
	
	Если Не ВесьДень Тогда
		МассивФорматДаты.Добавить("HH:mm");
	КонецЕсли;
	
	ФорматДаты = СтрСоединить(МассивФорматДаты, " ");
	
	МассивОписаниеДаты = Новый Массив;
	Если ЗначениеЗаполнено(ФорматДаты) Тогда
		ФорматДаты = "ДФ='" + ФорматДаты + "'";
		ПредставлениеПоФормату = Формат(Дата, ФорматДаты);
		МассивОписаниеДаты.Добавить(ПредставлениеПоФормату);
	КонецЕсли;
	Если ЗначениеЗаполнено(ПредставлениеДня) Тогда
		МассивОписаниеДаты.Добавить(ПредставлениеДня);
	КонецЕсли;
	ОписаниеДаты = СтрСоединить(МассивОписаниеДаты, " ");
	ОписаниеДаты = НРег(ОписаниеДаты);
	
	Возврат ОписаниеДаты;
	
КонецФункции

#КонецОбласти