
#Область ПрограммныйИнтерфейс

#Область ФормаЭлемента

// Вызывается из "ПриСозданииНаСервере" формы элемента справочника Контрагенты
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
//  Параметры - Структура
//
Процедура ПриСозданииНаСервереФормаЭлемента(Форма, Отказ, СтандартнаяОбработка, Параметры) Экспорт

// Локализация

	Объект = Форма.Объект;
	Элементы = Форма.Элементы;

	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриСозданииНаСервереКонтрагент(Форма, Параметры);
	Форма.РеквизитыПроверкиКонтрагентов.НеИспользоватьКэш = Истина;
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами

	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыПриСозданииНаСервере = ОбменСКонтрагентами.ПараметрыПриСозданииНаСервере_ФормаСправочника();
	ПараметрыПриСозданииНаСервере.Форма                 = Форма;
	ПараметрыПриСозданииНаСервере.МестоРазмещенияКоманд = Форма.Элементы.КомандыЭДО;
	ОбменСКонтрагентами.ПриСозданииНаСервере_ФормаСправочника(ПараметрыПриСозданииНаСервере);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

	// ИнтернетПоддержкаПользователей.СПАРКРиски
	ПараметрыПроцедуры = Новый Структура("ВариантОтображения", "Многострочный");
	СПАРКРиски.ПриСозданииНаСервере(
		Форма,
		Объект,
		Объект.Ссылка,
		ОбщегоНазначенияДокументооборотКлиентСервер.ВидКонтрагентаСпаркПоВидуКонтрагента(Объект.ЮрФизЛицо),
		ПараметрыПроцедуры);
	 // Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	 
	// Команды1СПАРКРиски
	СПАРКРиски.ДобавитьПодключаемыеКомандыКонтрагента(Форма, Объект, Элементы.Подменю1СПАРКРиски);
	// Конец Команды1СПАРКРиски

// Локализация Конец

	Если НЕ ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.РаботаСКонтрагентами") Тогда
		ИнициализироватьРеквизитыФормыКонтрагент(Форма);			
	КонецЕсли;

КонецПроцедуры

// Вызывается из "ПередЗаписьюНаСервере" формы элемента справочника Контрагенты
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ТекущийОбъект - ДанныеФормыСтруктура
Процедура ПередЗаписьюНаСервереФормаЭлемента(Форма, ТекущийОбъект) Экспорт

// Локализация
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПередЗаписьюНаСервереКонтрагент(Форма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
// Локализация Конец
	
КонецПроцедуры	

// Вызывается из "ПослеЗаписиНаСервере" формы элемента справочника Контрагенты
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ТекущийОбъект - ДанныеФормыСтруктура
//  ПараметрыЗаписи - Структура
Процедура ПослеЗаписиНаСервере(Форма, ТекущийОбъект, ПараметрыЗаписи) Экспорт

// Локализация	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	СПАРКРиски.ПослеЗаписиНаСервере(Форма, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
// Локализация Конец	

КонецПроцедуры	

#КонецОбласти

#Область ФормаСписка

// Вызывается из "ПриСозданииНаСервере" формы списка справочника Контрагенты
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
Процедура ПриСозданииНаСервереФормаСписка(Форма, Отказ, СтандартнаяОбработка) Экспорт

// Локализация

	Список = Форма.Список;

	// Отображение состояния контрагентов БЭД
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД") Тогда
		Форма.Элементы.ЭДО.Видимость = Ложь;
	КонецЕсли;
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыПриСозданииНаСервере = ОбменСКонтрагентами.ПараметрыПриСозданииНаСервере_ФормаСписка();
	ПараметрыПриСозданииНаСервере.Форма = Форма;
	ПараметрыПриСозданииНаСервере.МестоРазмещенияКоманд = Форма.Элементы.КомандыЭДО;
	ПараметрыПриСозданииНаСервере.ИсточникКомандЭДО = Список;
	ОбменСКонтрагентами.ПриСозданииНаСервере_ФормаСписка(ПараметрыПриСозданииНаСервере);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриСозданииНаСервереСписокКонтрагентов(Список);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
	//
// Локализация Конец


КонецПроцедуры

// Вызывается из "НастроитьЭлементыФормыДляМобильногоУстройства" формы списка справочника Контрагенты
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
Процедура НастроитьЭлементыФормыДляМобильногоУстройстваФормаСписка(Форма) Экспорт
	
// Локализация
	Форма.Элементы.ЭДО.Видимость = Ложь;
// Локализация Конец

КонецПроцедуры

#КонецОбласти

#Область ФормаВыбора
// Вызывается из "ПриСозданииНаСервере" формы выбора справочника Контрагенты
// 
// Параметры:
//  Список - ДинамическийСписок
Процедура ПриСозданииНаСервереФормаВыбора(Список) Экспорт

// Локализация
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриСозданииНаСервереСписокКонтрагентов(Список);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами 
// Локализация Конец

КонецПроцедуры

#КонецОбласти

#Область МодульОбъекта

// Вызывается из "ОбработкаПроверкиЗаполнения" модуля объекта справочника Контрагенты
// 
// Параметры:
//  Объект - ФормаКлиентскогоПриложения
//  Отказ - Булево
Процедура ОбработкаПроверкиЗаполнения_МодульОбъекта(Объект, Отказ) Экспорт
	
// Локализация
	Если Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда 
		
		ТекстСообщения = "";
		Если ЗначениеЗаполнено(Объект.КПП) И Не 
			 РегламентированныеДанныеКлиентСерверЛокализация.КППСоответствуетТребованиям(Объект.КПП, 
			 																			 ТекстСообщения) Тогда
 			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, "Объект.КПП",, Отказ);
 		КонецЕсли;
		
	КонецЕсли;
// Локализация Конец

КонецПроцедуры

#КонецОбласти

#Область РаботаСКонтрагентами

// Возвращает сведения о юридическом лице из реестров ЕГРЮЛ, РАФП.
//
// Параметры:
//	ИНН - Строка - ИНН юридического лица.
//
// Возвращаемое значение:
//	- Структура - сведения о юридическом лице:
//	* ИНН - Строка - ИНН юридического лица.
//	* ЕГРЮЛ - Структура, Неопределено - сведения из ЮГРЮЛ.
//	* РАФП - ТаблицаЗначений - сведения из РАФП.
//	* ОписаниеОшибки - Строка - описание возникшей ошибки.
Функция СведенияОЮридическомЛицеПоИНН(Знач ИНН) Экспорт
	
	СведенияОЮрлице = Новый Структура;
	СведенияОЮрлице.Вставить("ИНН", ИНН);
	СведенияОЮрлице.Вставить("ЕГРЮЛ");
	СведенияОЮрлице.Вставить("РАФП");
	СведенияОЮрлице.Вставить("ОписаниеОшибки");
	
// Локализация
	СведенияОЮрлице = РаботаСКонтрагентами.СведенияОЮридическомЛицеПоИНН(ИНН);
// Локализация Конец
	
	Возврат СведенияОЮрлице;
	
КонецФункции

// Возвращает реквизиты индивидуального предпринимателя по данным ЕГРИП - ФИО, свидетельство о регистрации,
// коды и т.д.
//
// Параметры:
//	ИНН - Строка - ИНН индивидуального предпринимателя, реквизиты которого надо получить.
//
// Возвращаемое значение:
//	- Структура - реквизиты индивидуального предпринимателя.
Функция РеквизитыПредпринимателяПоИНН(Знач ИНН) Экспорт
	
	РеквизитыПредпринимателя = Новый Структура;
	
	РеквизитыПредпринимателя.Вставить("ИНН");
	РеквизитыПредпринимателя.Вставить("Наименование");
	РеквизитыПредпринимателя.Вставить("НаименованиеПолное");
	РеквизитыПредпринимателя.Вставить("НаименованиеСокращенное");
	РеквизитыПредпринимателя.Вставить("ВидИП");
	РеквизитыПредпринимателя.Вставить("Фамилия");
	РеквизитыПредпринимателя.Вставить("Имя");
	РеквизитыПредпринимателя.Вставить("Отчество");
	РеквизитыПредпринимателя.Вставить("РегистрационныйНомер");
	
	// Следующие свойства могут содержать Неопределено в случае отсутствия в сервисе данных.
	РеквизитыПредпринимателя.Вставить("Пол");
	РеквизитыПредпринимателя.Вставить("КодСтраныГражданства");
	РеквизитыПредпринимателя.Вставить("РегистрацияВНалоговомОргане");
	РеквизитыПредпринимателя.Вставить("РегистрацияВПенсионномФонде");
	РеквизитыПредпринимателя.Вставить("РегистрацияВФСС");
	РеквизитыПредпринимателя.Вставить("ДатаРегистрации");
	РеквизитыПредпринимателя.Вставить("СвидетельствоОРегистрации");
	РеквизитыПредпринимателя.Вставить("ВидДеятельности");
	РеквизитыПредпринимателя.Вставить("Статус");
	РеквизитыПредпринимателя.Вставить("КодОКВЭД");
	РеквизитыПредпринимателя.Вставить("ЭтоОКВЭД2", Ложь);
	РеквизитыПредпринимателя.Вставить("ИсторияРеквизитов");
	РеквизитыПредпринимателя.Вставить("ОписаниеОшибки");
	
// Локализация
	РеквизитыПредпринимателя = РаботаСКонтрагентами.РеквизитыПредпринимателяПоИНН(ИНН);
// Локализация Конец
	
	Возврат РеквизитыПредпринимателя;
	
КонецФункции

// Создает контрагента в информационной базе по реквизитам.
//
// Параметры:
//   РеквизитыКонтрагента - Структура - реквизиты необходимые для создания контрагента.
//   Контрагент - ОпределяемыйТип.КонтрагентБЭД - ссылка на созданного контрагента.
//   Отказ - Булево
Процедура СоздатьКонтрагентаПоРеквизитам(РеквизитыКонтрагента, Контрагент, Отказ = Ложь) Экспорт

// Локализация
	ОбменСКонтрагентамиПереопределяемый.СоздатьКонтрагентаПоРеквизитам(РеквизитыКонтрагента, Контрагент, Отказ);
// Локализация Конец

КонецПроцедуры	

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Вспомогательный API. Инициализировать реквизиты проверки контрагента в карточке контрагента.
//
// Параметры:
//  Форма	 - ФормаКлиентскогоПриложения - Карточка контрагента, проверка которого выполняется.
Процедура ИнициализироватьРеквизитыФормыКонтрагент(Форма)
	
	НовыеРеквизиты = Новый Структура;
	
	ПроверкаИспользуется = Ложь;
	// Включена ли проверка и есть ли доступ на использование проверки.
	НовыеРеквизиты.Вставить("ПроверкаИспользуется", ПроверкаИспользуется);
	// Служебный реквизит
	НовыеРеквизиты.Вставить("НужноПоказатьПредложениеВключитьПроверкуКонтрагентов", Ложь);
	// Результат проверки контрагента.
	НовыеРеквизиты.Вставить("СостояниеКонтрагента");
	// Служебные реквизиты для работы фонового задания.
	НовыеРеквизиты.Вставить("ИнтервалПроверкиРезультата", 	1);
	НовыеРеквизиты.Вставить("ИдентификаторЗадания", 		Новый УникальныйИдентификатор);
	НовыеРеквизиты.Вставить("ФоновоеЗаданиеЗапущено", 		Ложь);
	НовыеРеквизиты.Вставить("АдресХранилища");
	// Если Истина, то перед запуском фонового задания по проверке контрагентов из
	// карточки контрагента выполняется предварительная проверка ИНН и КПП.
	НовыеРеквизиты.Вставить("ВыполнятьПредварительнуюПроверкуКонтрагента",	Ложь);
	НовыеРеквизиты.Вставить("ЭтоИностранныйКонтрагент", 	Ложь);
	НовыеРеквизиты.Вставить("ОтключитьПроверкуФНС", Ложь);
	НовыеРеквизиты.Вставить("ПричинаОтключенияПроверкиФНС", "");
	НовыеРеквизиты.Вставить("ЭтоЮридическоеЛицо" , Истина);
	// Результат проверки контрагента.
	НовыеРеквизиты.Вставить("ЭтоПроверкаПоКнопке", Ложь);
	
	// При открытии формы элемента справочника контрагентов по умолчанию проверка
	// запускается, если заполнена ссылка (существующий элемент справочника) или
	// заполнено свойство Параметры.ЗначениеКопирования.
	// Если Истина, тогда проверка контрагента будет инициирована при открытии
	// формы любого нового контрагента.
	НовыеРеквизиты.Вставить("ЗапускатьПроверкуПриОткрытииФормыНового", Ложь);
	
	Форма.РеквизитыПроверкиКонтрагентов = НовыеРеквизиты;
	
КонецПроцедуры

#КонецОбласти