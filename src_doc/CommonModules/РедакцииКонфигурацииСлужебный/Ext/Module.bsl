
#Область ПрограммныйИнтерфейс

#Область ПроверкаВключенияФункциональности

// Обработчик события перед записью констант.
//
// Параметры:
//  Источник - КонстантаМенеджерЗначения - Изменяемая константа.
//  Отказ - Булево - Признак отказа от записи.
//
Процедура ФункциональностьРедакцииКонфигурацииПриИзмененииПередЗаписью(Источник, Отказ) Экспорт
	
	НедоступнаяФункциональность = НедоступнаяФункциональностьВРедакции();
	
	Если Источник.Значение = Истина
			И НедоступнаяФункциональность.Найти(Источник.Метаданные().Имя) <> Неопределено Тогда
			
		ВызватьИсключение НСтр("ru = 'Функциональность недоступна для включения'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Показывает является ли текущая конфигурация конфигурацией Документооборот Холдинга.
//
// Возвращаемое значение:
//	Булево - Истина - Если текущая конфигурация является конфигурацией Документооборот Холдинга.
//
Функция ЭтоДокументооборотХолдинга() Экспорт
	
	ИмяРедакцииКонфигурации = Метаданные.Имя;
	
	ЭтоДокументооборотХолдинга =
		ИмяРедакцииКонфигурации = ИмяКонфигурацииДокументооборотХолдинга();
	
	Возврат ЭтоДокументооборотХолдинга;
	
КонецФункции

// Показывает является ли текущая конфигурация конфигурацией Документооборот КОРП.
//
// Возвращаемое значение:
//	Булево - Истина - Если текущая конфигурация является конфигурацией Документооборот КОРП.
//
Функция ЭтоДокументооборотКОРП() Экспорт
	
	ИмяРедакцииКонфигурации = Метаданные.Имя;
	
	ЭтоДокументооборотКОРП =
		ИмяРедакцииКонфигурации = ИмяКонфигурацииДокументооборотКОРП()
			Или ИмяРедакцииКонфигурации = ИмяКонфигурацииДокументооборотКОРПРусскийАнглийскийИнтерфейс();
	
	Возврат ЭтоДокументооборотКОРП;
	
КонецФункции

// Показывает является ли текущая конфигурация конфигурацией Документооборот государственного учреждения.
//
// Возвращаемое значение:
//	Булево - Истина - Если текущая конфигурация является конфигурацией Документооборот государственного учреждения.
//
Функция ЭтоДокументооборотГосударственногоУчреждения() Экспорт
	
	ИмяРедакцииКонфигурации = Метаданные.Имя;

	ЭтоДокументооборотГосударственногоУчреждения =
		ИмяРедакцииКонфигурации = ИмяКонфигурацииДокументооборотГосударственногоУчреждения()
			Или ИмяРедакцииКонфигурации = ИмяКонфигурацииДокументооборотГосударственногоУчрежденияКОРПРегион();
	
	Возврат ЭтоДокументооборотГосударственногоУчреждения;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИменаРедакцийКонфигурации

Функция ИмяКонфигурацииДокументооборотХолдинга()
	
	Возврат "ДокументооборотХолдинга";
	
КонецФункции

Функция ИмяКонфигурацииДокументооборотКОРП()
	
	Возврат "ДокументооборотКОРП";
	
КонецФункции

Функция ИмяКонфигурацииДокументооборотКОРПРусскийАнглийскийИнтерфейс()
	
	Возврат "ДокументооборотКОРПРусскийИАнглийскийИнтерфейс";
	
КонецФункции

Функция ИмяКонфигурацииДокументооборотГосударственногоУчреждения()
	
	Возврат "ДокументооборотГосударственногоУчреждения";
	
КонецФункции

Функция ИмяКонфигурацииДокументооборотГосударственногоУчрежденияКОРПРегион()
	
	Возврат "ДокументооборотГосударственногоУчрежденияКОРПРегион";
	
КонецФункции

#КонецОбласти

#Область СпискиНедоступнойФункциональности

// Возвращает список имен констант, которые недоступны для включения в редакции.
//
// Возвращаемое значение:
//	Массив из Строка - Массив имен констант, которые не должны включаться в редакции.
//
Функция НедоступнаяФункциональностьВРедакции()
	
	Если ЭтоДокументооборотКОРП() Тогда
		Возврат КонстантыНедоступныеДляВключенияВРедакцииКОРП();
	КонецЕсли;

	Если ЭтоДокументооборотГосударственногоУчреждения() Тогда
		Возврат КонстантыНедоступныеДляВключенияВРедакцииДГУ();
	КонецЕсли;
	
	Возврат Новый Массив;

КонецФункции

// Возвращает список имен констант, которые недоступны для включения в редакции КОРП.
//
// Возвращаемое значение:
//	Массив из Строка - Массив имен констант, которые не должны включаться в редакции Холдинг.
//
Функция КонстантыНедоступныеДляВключенияВРедакцииКОРП()
	
	СписокКонстант = Новый Массив;
	
	СписокКонстант.Добавить("ИспользоватьКОД");
	
	Возврат СписокКонстант;
	
КонецФункции

// Возвращает список имен констант, которые недоступны для включения в редакции ДГУ.
//
// Возвращаемое значение:
//	Массив из Строка - Массив имен констант, которые не должны включаться в редакции Холдинг.
//
Функция КонстантыНедоступныеДляВключенияВРедакцииДГУ()
	
	СписокКонстант = Новый Массив;
	
	СписокКонстант.Добавить("ИспользоватьКОД");
	
	Возврат СписокКонстант;
	
КонецФункции

#КонецОбласти

#КонецОбласти
