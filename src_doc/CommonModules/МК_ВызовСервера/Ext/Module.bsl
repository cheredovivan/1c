
#Область ПрограммныйИнтерфейс

// Выполняется при интерактивном начале работы пользователя с областью данных или в локальном режиме.
// Соответствует обработчику ПриНачалеРаботыСистемы.
// Параметры:
//  СведенияОМобильномКлиенте - Структура - Сведения о мобильном клиенте
//
Процедура ПриНачалеРаботыСистемы(СведенияОМобильномКлиенте) Экспорт
	
	ПротоколированиеРаботыСотрудников.ЗаписатьВходВСистему();
	РегистрыСведений.МК_СведенияОМобильныхОнлайнКлиентах.ЗаписатьСведенияОКлиенте(СведенияОМобильномКлиенте);
	
КонецПроцедуры

// Добавляет запись в журнал регистрации с клиента.
//
// Параметры:
//  ИмяСобытия - Строка - Имя события ошибки
//  ТекстСообщения - Строка - Данные для записи;
//
Процедура ЗаписатьОшибкуВЖурналРегистрации(ИмяСобытия, ТекстСообщения) Экспорт

	ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);

КонецПроцедуры

// Получает ссылку на объект указанного типа
//
// Параметры:
//  ТипМобильного		 - Строка - Описатель типа объекта
//  СтрокаИдентификатора - Строка, УникальныйИдентификатор - идентификатор объекта.
// 
// Возвращаемое значение:
//  ЛюбаяСсылка - ссылка на объект
//
Функция СсылкаПоТипуИИД(Знач ТипМобильного, Знач СтрокаИдентификатора) Экспорт

	Если Не ЗначениеЗаполнено(ТипМобильного) Или Не ЗначениеЗаполнено(СтрокаИдентификатора) Тогда
		Возврат Неопределено;
	КонецЕсли;

	Если ТипЗнч(СтрокаИдентификатора) = Тип("Строка") Тогда
		Идентификатор = Новый УникальныйИдентификатор(СтрокаИдентификатора)
	ИначеЕсли ТипЗнч(СтрокаИдентификатора) = Тип("УникальныйИдентификатор") Тогда
		Идентификатор = СтрокаИдентификатора;
	КонецЕсли;

	Если Идентификатор = УникальныйИдентификаторПустой() Тогда
		Возврат Неопределено;
	КонецЕсли;

	Если ТипМобильного = "Role" Тогда
		Возврат Справочники.ПолныеРоли.НайтиСоздатьПолнуюРоль(
			Справочники.РолиИсполнителей.ПолучитьСсылку(Идентификатор),
			Неопределено,
			Неопределено)
	КонецЕсли;
	
	ТипСсылки = ТипСсылкиПоТипуМобильного(ТипМобильного);
	XMLТип = XMLТип(ТипСсылки);
	
	Если XMLТип = Неопределено Или XMLТип.ИмяТипа = "Null" Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КлассИТип = СтрРазделить(XMLТип.ИмяТипа, ".");
	Класс = КлассИТип[0];
	ИмяТипа = КлассИТип[1];
	
	Если СтрСравнить(Класс, "DocumentRef") = 0 Тогда		
		Возврат Документы[ИмяТипа].ПолучитьСсылку(Идентификатор);	
			
	ИначеЕсли СтрСравнить(Класс, "CatalogRef") = 0 Тогда		
		Возврат Справочники[ИмяТипа].ПолучитьСсылку(Идентификатор);	
			
	ИначеЕсли СтрСравнить(Класс, "TaskRef") = 0 Тогда		
		Возврат Задачи[ИмяТипа].ПолучитьСсылку(Идентификатор);		
		
	ИначеЕсли СтрСравнить(Класс, "BusinessProcessRef") = 0 Тогда		
		Возврат БизнесПроцессы[ИмяТипа].ПолучитьСсылку(Идентификатор);
		
	КонецЕсли;
	 
КонецФункции

#Область МобильныйСканер

// Снимает блокировку регистра КэшМобильныхСканов для указанной формы
//
// Параметры:
//  Идентификатор - УникальныйИдентификатор - Идентификатор формы, для которой нужно снять блокировку
//
Процедура ОсвободитьСканер(Идентификатор) Экспорт
	
	РегистрыСведений.КэшМобильныхСканов.ОсвободитьСканер(Идентификатор);
	
КонецПроцедуры

// Делает попытку блокировки регистра КэшМобильныхСканов для указанной формы
// Параметры:
//  Идентификатор - УникальныйИдентификатор - Идентификатор формы, для которой нужно установить блокировку
//
//  Возвращаемое значение:
//   Булево - Истина в случае успеха, Ложь в случае неудачи
//
Функция ЗанятьСканер(Идентификатор) Экспорт
	
	Возврат РегистрыСведений.КэшМобильныхСканов.ЗанятьСканер(Идентификатор);
	
КонецФункции

// Проверяет есть ли запись со сканом в регистре КэшМобильныхСканов
//
//  Возвращаемое значение:
//   Булево - Истина, если скан есть, Ложь если записи нет
//
Функция СканыЗагружены() Экспорт
	
	Возврат РегистрыСведений.КэшМобильныхСканов.СканыЗагружены();
	
КонецФункции

// Создает файл в карточке документа по двоичным данным в регистре КэшМобильныхСканов.
// После этого очищает регистр.
//
// Параметры:
//  Владелец - ЛюбаяСсылка - Ссылка на объект, который будет установлен владельцем файла
//  ИмяФайла - Строка - Будущее имя файла
//  ИдентификаторФормыДобавления - УникальныйИдентификатор - Идентификатор карточки, к которой будет прикреплен файл
//
//  Возвращаемое значение:
//   Структура
//      * ФайлыДобавлены - Булево - признак добавления файлов
//      * Файлы - Массив (СправочникСсылка.Файлы) - массив файлов
//
Функция ОбработатьДанныеСканов(Владелец, ИмяФайла) Экспорт
	
	Возврат РегистрыСведений.КэшМобильныхСканов.ОбработатьДанныеСканов(Владелец, ИмяФайла);
	
КонецФункции

// Удаляет данные сканов из регистра КэшМобильныхСканов для текущего пользователя
//
Процедура УдалитьДанныеСканов() Экспорт
	
	Пользователь = Пользователи.ТекущийПользователь();
	РегистрыСведений.КэшМобильныхСканов.Удалить(Пользователь);

КонецПроцедуры

// Получает двоичные данные всех сканов для текущего пользователя
// Возвращаемое значение:
//  Массив - Массив двоичных файлов
//
Функция ДанныеСканов() Экспорт

	Возврат РегистрыСведений.КэшМобильныхСканов.ДанныеСканов();

КонецФункции

// Загружает сканы с моб. устройства в РС
//  Параметры:
//   ДанныеСнимков - Массив - Полученные снимки
//  Возвращаемое значение:
//   Строка - Описание статуса загрузки строкой
Функция ЗагрузитьСканы(ДанныеСнимков) Экспорт

	Возврат РегистрыСведений.КэшМобильныхСканов.ЗагрузитьСканы(ДанныеСнимков);

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает тип объекта уведомления по его строковому описанию.
//
// Параметры:
//  ТипМобильного - Строка - Строковое описание типа уведомления;
// 
// Возвращаемое значение:
//  Строка - Объект уведомления .
//
Функция ТипСсылкиПоТипуМобильного(ТипМобильного)

	Если ТипМобильного = "IncomingMail" Тогда 	
		Возврат Тип("ДокументСсылка.ВходящееПисьмо");
		
	ИначеЕсли ТипМобильного = "OutgoingMail" Тогда
		Возврат Тип("ДокументСсылка.ИсходящееПисьмо");

	ИначеЕсли ТипМобильного = "CalendarEvent" Тогда
		Возврат Тип("СправочникСсылка.ЗаписиРабочегоКалендаря");

	ИначеЕсли ТипМобильного = "Task" Тогда
		Возврат Тип("ЗадачаСсылка.ЗадачаИсполнителя");

	ИначеЕсли ТипМобильного = "Control" Тогда
		Возврат Тип("СправочникСсылка.Контроль");

	ИначеЕсли ТипМобильного = "ExecutionProcess" Тогда
		Возврат Тип("БизнесПроцессСсылка.Исполнение");

	ИначеЕсли ТипМобильного = "MailAccount"Тогда
		Возврат Тип("СправочникСсылка.УчетныеЗаписиЭлектроннойПочты");

	ИначеЕсли ТипМобильного = "MailFolder" Тогда
		Возврат Тип("СправочникСсылка.ПапкиПисем");

	ИначеЕсли ТипМобильного = "User" Тогда
		Возврат Тип("СправочникСсылка.Пользователи");

	ИначеЕсли ТипМобильного = "Role" Тогда
		Возврат Тип("СправочникСсылка.РолиИсполнителей");

	ИначеЕсли ТипМобильного = "File" Тогда
		Возврат Тип("СправочникСсылка.Файлы");

	ИначеЕсли ТипМобильного = "MailThread" Тогда
		Возврат Тип("СправочникСсылка.ВеткиПереписки");
		
	ИначеЕсли ТипМобильного = "Addressee" Тогда
		Возврат Тип("СправочникСсылка.АдресатыПочтовыхСообщений");
		
	ИначеЕсли ТипМобильного = "MailTextTemplate" Тогда
		Возврат Тип("СправочникСсылка.ШаблоныТекстов");

	ИначеЕсли ТипМобильного = "MyAbsence" Тогда
		Возврат Тип("ДокументСсылка.Отсутствие");
		
	ИначеЕсли ТипМобильного = "MyHealth" Тогда
		Возврат Тип("ДокументСсылка.СамочувствиеСотрудника");
		
	ИначеЕсли ТипМобильного = "Event" Тогда
		Возврат Тип("СправочникСсылка.Мероприятия");
				
	КонецЕсли;

КонецФункции

// Возвращает ссылку из значения строки
//  Параметры:
//	 СсылкаСтрокой - Строка - Значение для конвертации
//  Возвращаемое значение:
//  ЛюбаяСсылка - запрашиваемая ссылка
//
Функция ЗначенияСтрокиВнутрВСсылку(СсылкаСтрокой) Экспорт

	Возврат ЗначениеИзСтрокиВнутр(СсылкаСтрокой);

КонецФункции

//Удаляет служебное сообщение по идентификатору
// Параметры:
//  ИдентификаторСообщения - ИдентификаторСообщенияСистемыВзаимодействия -Идентификатор сообщения
//
Процедура УдалитьСлужебноеСообщение(ИдентификаторСообщения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	СистемаВзаимодействия.УдалитьСообщение(ИдентификаторСообщения);

КонецПроцедуры

#КонецОбласти