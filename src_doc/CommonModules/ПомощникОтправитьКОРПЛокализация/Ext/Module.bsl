#Область ПрограммныйИнтерфейс

// Вызывается из ПомощникОтправитьКОРП.ПриЗаполненииДереваВариантовОтправки при
// заполнении дерева вариантов отправки.
//
// Параметры:
//  Помощник - Структура - см. ПомощникОтправить.Помощник();
//  СтандартнаяОбработка - Булево - при установке Ложь стандартное заполнение дерева вариантов
//                                  будет проигнорировано.
//
Процедура ПриЗаполненииДереваВариантовОтправки(Помощник, СтандартнаяОбработка) Экспорт

// Локализация
	ДобавитьВДеревоВариантОтправитьПоЭДО(Помощник);
	ДобавитьВДеревоВариантОтправкиЧерез1СShare(Помощник);
// Локализация Конец

КонецПроцедуры

// Вызывается из ПомощникОтправитьКОРП.ОписаниеВарианта
// Позволяет переопределить описание варианта, сформированное для текущей редакции
//
// Параметры:
//  Вариант - Произвольный - вариант отправки.
//  ОписаниеВарианта - Строка
//
Процедура ПриОпределенииОписанияВарианта(Вариант, ОписаниеВарианта) Экспорт

// Локализация
	Если ПомощникОтправитьКлиентСерверЛокализация.ЭтоВариантОтправкиПоЭДО(Вариант) Тогда
		ОписаниеВарианта = ОписаниеВариантаОтправкиПоЭДО();
	ИначеЕсли ПомощникОтправитьКлиентСерверЛокализация.ЭтоВариантОтправкиЧерез1СShare(Вариант) Тогда
		ОписаниеВарианта = ОписаниеВариантаОтправкиЧерез1СShare();
	КонецЕсли;
// Локализация Конец

КонецПроцедуры

// Возвращает параметры варианта.
//
// Параметры:
//  Вариант - Произвольный - вариант отправки.
//  Помощник - Структура - см. ПомощникОтправить.Помощник()
//  Параметры - Структура - См. ПомощникОтправитьКОРП.ПараметрыВарианта
//
Процедура ПриОпределенииПараметровВарианта(Вариант, Помощник, Параметры) Экспорт

// Локализация
	Если ПомощникОтправитьКлиентСерверЛокализация.ЭтоВариантОтправкиПоЭДО(Вариант) Тогда
		Параметры = ПараметрыВариантаОтправкиПоЭДО(Помощник);
	ИначеЕсли ПомощникОтправитьКлиентСерверЛокализация.ЭтоВариантОтправкиЧерез1СShare(Вариант) Тогда
		Параметры = ПараметрыВариантаОтправкиЧерез1СShare(Помощник);
	КонецЕсли;
// Локализация Конец

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Локализация
#Область ОтправкаПоЭДО

// Добавить в дерево вариант "Отправить по ЭДО".
// 
// Параметры:
//  Помощник - см. ПомощникОтправить.Помощник
//
Процедура ДобавитьВДеревоВариантОтправитьПоЭДО(Помощник)
	
	ВариантОтправкиПоЭДО = ПомощникОтправитьКлиентСерверЛокализация.ВариантОтправкиПоЭДО();
	
	Если Не ПомощникОтправить.ВозможноДобавлениеВарианта(Помощник, ВариантОтправкиПоЭДО) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПомощникОтправить.ЕстьОбъектыУказанныхТипов(
		Помощник.ОбъектыОтправки,
		ДопустимыеТипыОбъектовДляВариантаОтправитьПоЭДО()) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектыОтправки = ПомощникОтправить.ОбъектыОтправки(Помощник);
	Если ОбъектыОтправки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ВозможнаОтправкаПоЭДО(ОбъектыОтправки[0]) Тогда
		Возврат;
	КонецЕсли;	
	
	СтрокаВарианта = Помощник.ДеревоВариантов.Строки.Добавить();
	СтрокаВарианта.Вариант = ВариантОтправкиПоЭДО;
	СтрокаВарианта.Представление = ПредставлениеВариантаОтправитьПоЭДО();
	
КонецПроцедуры

// Возвращает представление варианта отправки по ЭДО.
//
// Возвращаемое значение:
//  Строка
//
Функция ПредставлениеВариантаОтправитьПоЭДО()
	
	Возврат НСтр("ru = 'ЭДО'");
	
КонецФункции

// Возвращает допустимые типы объектов для варианта ОтправитьПоЭДО.
//
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//    * Ключ - Тип - тип объекта.
//    * Значение - Булево - всегда Истина.
//
Функция ДопустимыеТипыОбъектовДляВариантаОтправитьПоЭДО()
	
	ДопустимыеТипы = Новый Соответствие;	
	ДопустимыеТипы[Тип("СправочникСсылка.ДокументыПредприятия")] = Истина;
	
	Возврат ДопустимыеТипы;
	
КонецФункции

// Проверяет, что документ можно отправить по ЭДО
// 
// Параметры:
//  Объект - СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение:
//  Булево - Истина, если отправка возможна
//  
Функция ВозможнаОтправкаПоЭДО(Объект)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЭДОВДокументообороте") Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект, "ВидДокумента,
		|ФормаДокумента");
	РеквизитыОбъекта.Вставить("Ссылка", Объект);	
	ВидДокументаКэш = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыОбъекта.ВидДокумента,
		"ИспользоватьЭДО");	
	Структура = Новый Структура;
	Структура.Вставить("Объект", РеквизитыОбъекта);
	Структура.Вставить("ВидДокументаКэш", ВидДокументаКэш);
	
	Возврат ОбменЭДОДокументооборот.ЕстьВозможностьСоздатьИсходящийЭлектронныйДокумент(Структура);
	
КонецФункции	

// Возвращает параметры для варианта отправки по ЭДО.
//
// Параметры:
//  Помощник - см. Помощник
//
// Возвращаемое значение:
//  Структура:
//   * Объекты - Массив из СправочникСсылка.ДокументыПредприятия - объекты для отправки по ЭДО.
//
Функция ПараметрыВариантаОтправкиПоЭДО(Помощник)
	
	Параметры = Новый Структура;
	Параметры.Вставить("Объекты", Помощник.ОбъектыОтправки);
	
	Возврат Параметры;
	
КонецФункции

// Возвращает описание варианта отправки по ЭДО.
//
// Возвращаемое значение:
//  Строка
//
Функция ОписаниеВариантаОтправкиПоЭДО()
	
	Описание = НСтр("ru = 'Создание, подписание, отправка исходящего документа ЭДО.'");
	Возврат ПомощникОтправить.HTMLОписаниеВарианта(Описание);	
	
КонецФункции

#КонецОбласти
// Локализация Конец

// Локализация
#Область ОтправкаЧерез1СShare

// Добавить в дерево вариант "Отправить через 1С:Share".
// 
// Параметры:
//  Помощник - см. ПомощникОтправить.Помощник
//
Процедура ДобавитьВДеревоВариантОтправкиЧерез1СShare(Помощник)
	
	ВариантОтправкиЧерез1СShare = ПомощникОтправитьКлиентСерверЛокализация.ВариантОтправкиЧерез1СShare();
	
	Если Не ПомощникОтправить.ВозможноДобавлениеВарианта(Помощник, ВариантОтправкиЧерез1СShare) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПомощникОтправить.ЕстьОбъектыУказанныхТипов(
		Помощник.ОбъектыОтправки,
		ДопустимыеТипыОбъектовДляВариантаОтправитьЧерез1СShare()) Тогда		
		Возврат;
	КонецЕсли;
	
	ОбъектыОтправки = ПомощникОтправить.ОбъектыОтправки(Помощник);
	Если ОбъектыОтправки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ВозможнаОтправкаЧерез1СShare(ОбъектыОтправки[0]) Тогда
		Возврат;
	КонецЕсли;	
	
	СтрокаВарианта = Помощник.ДеревоВариантов.Строки.Добавить();
	СтрокаВарианта.Вариант = ВариантОтправкиЧерез1СShare;
	СтрокаВарианта.Представление = ПредставлениеВариантаОтправитьЧерез1СShare();
	
КонецПроцедуры

// Возвращает представление варианта отправки через 1С:Share.
//
// Возвращаемое значение:
//  Строка
//
Функция ПредставлениеВариантаОтправитьЧерез1СShare()
	
	Возврат НСтр("ru = '1C:Share'");
	
КонецФункции

// Возвращает допустимые типы объектов для варианта ОтправитьЧерез1СShare.
//
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//    * Ключ - Тип - тип объекта.
//    * Значение - Булево - всегда Истина.
//
Функция ДопустимыеТипыОбъектовДляВариантаОтправитьЧерез1СShare()
	
	ДопустимыеТипы = Новый Соответствие;	
	ДопустимыеТипы[Тип("СправочникСсылка.ДокументыПредприятия")] = Истина;
	
	Возврат ДопустимыеТипы;
	
КонецФункции

// Возвращает параметры для варианта отправки через 1С:Share.
//
// Параметры:
//  Помощник - см. Помощник
//
// Возвращаемое значение:
//  Структура:
//   * Объекты - Массив из СправочникСсылка.ДокументыПредприятия - объекты для отправки через 1С:Share.
//
Функция ПараметрыВариантаОтправкиЧерез1СShare(Помощник)
	
	Параметры = Новый Структура;
	Параметры.Вставить("Объекты", Помощник.ОбъектыОтправки);
	
	Возврат Параметры;
	
КонецФункции

// Проверяет, что документ можно отправить через 1С:Share
// 
// Параметры:
//  Объект - СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение:
//  Булево - Истина, если отправка возможна
//  
Функция ВозможнаОтправкаЧерез1СShare(Объект)
	
	Если Не ИнтеграцияShareДокументооборот.ИспользоватьСервис1СShare() Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	ВидДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект, "ВидДокумента");
	ВидДокументаКэш = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидДокумента, "ВестиУчетПоОрганизациям,
		|ВестиУчетСторон");	

	Возврат ВидДокументаКэш.ВестиУчетПоОрганизациям Или ВидДокументаКэш.ВестиУчетСторон;
	
КонецФункции

// Возвращает описание варианта отправки через "1С:Share".
//
// Возвращаемое значение:
//  Строка
//
Функция ОписаниеВариантаОтправкиЧерез1СShare()
	
	СсылкаНаИнструкцию = СтрШаблон(НСтр("ru = '<a href=""%1"">""1С:Share</a>""'"),
		ИнтеграцияShare.СсылкаНаИнструкциюСервиса());
	Описание = НСтр("ru = 'Отправка документа через сервис %1.
		|
		|Будет получена ссылка на выбранные документ и файлы, которую вы сможете отправить по электронной почте или через мессенджеры.'");	
	HTMLСтрока = ПомощникОтправить.HTMLОписаниеВарианта(Описание);
	HTMLСтрока = СтрЗаменить(HTMLСтрока, "%1", СсылкаНаИнструкцию);
	
	Возврат HTMLСтрока;
	
КонецФункции

#КонецОбласти
// Локализация Конец

#КонецОбласти
