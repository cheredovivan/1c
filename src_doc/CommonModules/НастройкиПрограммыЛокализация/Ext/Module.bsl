
#Область ПрограммныйИнтерфейс

#Область ОбменДанными

// Вызывается из ПриСозданииНаСервере формы ОбменДанными обработки НастройкиПрограммы
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
Процедура ПриСозданииНаСервере_ОбменДанными(Форма, Отказ, СтандартнаяОбработка) Экспорт
// Локализация
	Элементы = Форма.Элементы;
	
	Элементы.НастроитьЭДО.Доступность = Форма.ИспользоватьОбменЭД;
	
	СсылкаНаИнструкциюСервиса1СShare = ИнтеграцияShare.СсылкаНаИнструкциюСервиса();
	ЧастиЗаголовка = Новый Массив;
	ЧастиЗаголовка.Добавить(Новый ФорматированнаяСтрока(НСтр(
		"ru = 'Использовать обмен документами через сервис ""1C:Share"".'")));
	ЧастиЗаголовка.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = ' Подробнее.'"),, ЦветаСтиля.ГиперссылкаЦвет,,
		СсылкаНаИнструкциюСервиса1СShare));
	Элементы.ИспользоватьСервис1сShareРасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(ЧастиЗаголовка);
	
	Элементы.ГруппаМЭДОНастройки.Доступность = Форма.ИспользоватьМЭДО;
	
	Форма.ОбменСВнешнимиРесурсамиВ21 = Не Константы.ВнешнийОбменВ21Завершен.Получить();
	Элементы.НастроитьМиграциюИзПредыдущейВерсии.Доступность = Форма.ЗагружатьДанныеИз21;
	
	Если МиграцияДанныхИзВнешнихСистемСервер.ИдетЗагрузкаИзДО21() Тогда
		Элементы.ЗагружатьДанныеИз21.Доступность = Ложь;
	КонецЕсли;
// Локализация Конец
КонецПроцедуры

// Вызывается из ЗаписатьКонстантыСервер формы ОбменДанными обработки НастройкиПрограммы
// Дополняет состав записываемых констант специфичными для национальной редакции
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
Процедура ПриЗаписиНастроек_ОбменДанными(Форма) Экспорт
// Локализация

	Если Форма.ИспользоватьМЭДО Тогда
	
		Если Не Константы.ИспользоватьГрифыДоступа.Получить() Тогда
			Константы.ИспользоватьГрифыДоступа.Установить(Истина);
		КонецЕсли;
		Если Не Константы.АвтовизуализацияЭП.Получить() Тогда
			Константы.АвтовизуализацияЭП.Установить(Истина);
		КонецЕсли;
		Если Не Константы.ИспользоватьЭлектронныеПодписи.Получить() Тогда
			Константы.ИспользоватьЭлектронныеПодписи.Установить(Истина);
		КонецЕсли;
		
		РегламентныеЗаданияСервер.УстановитьИспользованиеРегламентногоЗадания(
			Метаданные.РегламентныеЗадания.ВыполнениеОбменаМЭДО, Истина);
	Иначе
		РегламентныеЗаданияСервер.УстановитьИспользованиеРегламентногоЗадания(
			Метаданные.РегламентныеЗадания.ВыполнениеОбменаМЭДО, Ложь);
	КонецЕсли;

// Локализация Конец
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДополнитьИменаКонстант_ОбменДанными(Имена) Экспорт
// Локализация
	Имена.Добавить("ИспользоватьОбменЭД");
	Имена.Добавить("АвтоматическиОтправлятьКвитанцииМЭДО");
	Имена.Добавить("ВариантАвтосозданияУведомленийМЭДО");
	Имена.Добавить("ВремяХраненияЖурналаСобытийМЭДО");
	Имена.Добавить("РазмерПорцииОбработкиМЭДО");
	Имена.Добавить("ИспользоватьСервис1сShare");
	Имена.Добавить("ИспользоватьМЭДО");
// Локализация Конец
КонецПроцедуры

#КонецОбласти

