////////////////////////////////////////////////////////////////////////////////
// Модуль содержит процедуры и функции для работы с электронными подписями.
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область РаботаСЭлектроннымиПодписями

#Область ПроверкаПодписей

// Проверяет подписи объекта на форме
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма из которой происходит проверка
//  ПараметрыПроверки см. РаботаСЭПКлиент.НовыеПараметрыПроверкиПодписей
//  ОбработчикЗавершения - ОписаниеОповещения - Обработчик, который будет вызван после завершения проверки
Процедура ПроверитьПодписи(Форма, ПараметрыПроверки, ОбработчикЗавершения = Неопределено) Экспорт
	
	Контекст = НовыйКонтекстПроверкиПодписей();
	Контекст.ОбработчикЗавершения = ОбработчикЗавершения;
	Контекст.ПараметрыПроверки = ПараметрыПроверки;
	Контекст.Форма = Форма;
	
	ДанныеПодписейОбъектов = ДанныеПодписейОбъектовДляПроверки(Форма, ПараметрыПроверки);
	ЗаполнитьКонтекстПроверкиПоДаннымПодписей(Контекст, ДанныеПодписейОбъектов);
	
	ВыполнитьДействияДляПроверкиПодписей(Контекст);
	
КонецПроцедуры

// Новые праметры для проверки подписей на форме объекта
// 
// Возвращаемое значение:
//  Структура:
//   * ВыделенныеСтроки - Неопределено,
//                      Массив из Число,
//                                Структура - Массив строк таблицы подписей, которые необходимо проверить
//                                                      Если не указано (Неопределено), тогда проверяются все подписи.
//   * ИмяТаблицыЭП - Строка - Имя таблицы или дерева ЭП
//   * ПерезаполнитьВыделенныеСтроки - Булево - Необходимо ли перезаполнить текущие строки по результатам проверки
//
Функция НовыеПараметрыПроверкиПодписей() Экспорт
	
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("ВыделенныеСтроки", Неопределено);
	ПараметрыПроверки.Вставить("ИмяТаблицыЭП", "ЭлектронныеПодписи");
	ПараметрыПроверки.Вставить("ПерезаполнитьВыделенныеСтроки", Ложь);
	
	Возврат ПараметрыПроверки;
	
КонецФункции

#КонецОбласти

// Открывает форму просмотра ЭП.
//
Процедура ОткрытьПодпись(ТекущиеДанные, УникальныйИдентификатор) Экспорт
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВебКлиентПодMacOS() Тогда
		Возврат;
	КонецЕсли;
	
	РасширениеПодключеноФайл = ПодключитьРасширениеРаботыСФайлами();
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Объект) И 
		ЗначениеЗаполнено(ТекущиеДанные.ДатаПодписи) Тогда
		
		// Локализация
		СтандартнаяОбработка = Истина;
			ОбменЭДОДокументооборотКлиент.ПриОткрытииПодписи(
				ТекущиеДанные.Объект, ТекущиеДанные.УникальныйИдентификатор, СтандартнаяОбработка);
			Если Не СтандартнаяОбработка Тогда
				Возврат;
			КонецЕсли;
		// Конец Локализация
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ДатаПодписи", ТекущиеДанные.ДатаПодписи);
		ПараметрыФормы.Вставить("Объект", ТекущиеДанные.Объект);
		ПараметрыФормы.Вставить("УстановившийПодпись", ТекущиеДанные.УстановившийПодпись);
		ПараметрыФормы.Вставить("УникальныйИдентификатор", ТекущиеДанные.УникальныйИдентификатор);
		ПараметрыФормы.Вставить("ПодписьПроверена", ЗначениеЗаполнено(ТекущиеДанные.ДатаПроверкиПодписи));
		ПараметрыФормы.Вставить("ПодписьВерна", ТекущиеДанные.ПодписьВерна);
		ПараметрыФормы.Вставить("СертификатДействителен", ТекущиеДанные.СертификатДействителен);
		Попытка
			ОткрытьФорму("РегистрСведений.ЭлектронныеПодписи.ФормаЗаписи", ПараметрыФормы,,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
			ПоказатьПредупреждение(Неопределено, ТекстОшибки);
		КонецПопытки;
		
	Иначе
		
		Если ТипЗнч(ТекущиеДанные) = Тип("ДанныеФормыЭлементДерева") Тогда
			ДочерниеЭлементы = ТекущиеДанные.ПолучитьЭлементы();
			Если ДочерниеЭлементы.Количество() > 0 Тогда
				Версия = ДочерниеЭлементы[0].Объект;
				Если ЗначениеЗаполнено(Версия) И ТипЗнч(Версия) = Тип("СправочникСсылка.ВерсииФайлов") Тогда
					ФайлСсылка = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(Версия, "Владелец");
					ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(
						ФайлСсылка,
						Неопределено,
						УникальныйИдентификатор);
					РаботаСФайламиКлиент.Открыть(ДанныеФайла, УникальныйИдентификатор); 
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#Область Подписание

// Новые параметры подписания ЭП
// 
// Возвращаемое значение:
//  Структура - Новые параметры подписания:
//   * ОбработчикЗавершения - Неопределено, ОписаниеОповещения - Обработчик, который будет вызван
//                                                               после выполнения подписания
//   * ИдентификаторФормы - УникальныйИдентификатор - УИД формы, из которой вызывается подписание
//   * Заголовки - Неопределено, Структура - Структура, описывающая заголовки формы подписания, возможные поля:
//     ** Операция - Строка - Описание операции, заголовок формы подписания, например, "Подписание визы согласования"
//     ** ЗаголовокДанных - Строка - Представление подписанных данных, например, "Виза согласования"
//   * ДоступныеСертификаты - Неопределено, Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования -
//                                                    Массив сертификатов, доступных для подписания в данной операции
//   * ДоверенностиСертификатов - Неопределено, Соответствие из КлючИЗначение - Допустимые доверенности сертификатов:
//     ** Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - Сертификат
//     ** Значение - Массив из ОпределяемыйТип.МашиночитаемаяДоверенность - Допустимые доверенности
//
Функция НовыеПараметрыПодписания() Экспорт
	
	ПараметрыПодписания = Новый Структура;
	ПараметрыПодписания.Вставить("ОбработчикЗавершения", Неопределено);
	ПараметрыПодписания.Вставить("ИдентификаторФормы", УникальныйИдентификаторПустой());
	ПараметрыПодписания.Вставить("Заголовки", Неопределено);
	ПараметрыПодписания.Вставить("ДоступныеСертификаты", Неопределено);
	ПараметрыПодписания.Вставить("ДоверенностиСертификатов", Неопределено);
	
	Возврат ПараметрыПодписания;
	
КонецФункции

// Подписывает данные ЭП
// 
// Параметры:
//  Данные - ОпределяемыйТип.ПодписанныйОбъект, Структура, Массив из ОпределяемыйТип.ПодписанныйОбъект, Структура -
//      Данные, которые необходимо подписать.
//      Возможны три варианта:
//        1) ОпределяемыйТип.ПодписанныйОбъект - просто ссылка на объект, который нужно подписать
//        2) Стркутура:
//              * ОбъектПодписания - ОпределяемыйТип.ПодписанныйОбъект - Объект, который нужно подписать
//              * ПараметрыДанных - Структура, неопределено - Параметры данных, необходимые для подписания
//              * Представление - Строка, Неопределено - Представление конкретного объекта
//              * ДополнительныеСвойства - Структура, Неопределено - Доп свойства при получении данных подписания
//        3) Массив из п. 1) или п. 2) - Если необходимо подписать сразу несколько объектов.
//  ПараметрыПодписания см. РаботаСЭПКлиент.НовыеПараметрыПодписания
Процедура Подписать(Данные, ПараметрыПодписания) Экспорт
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ЭлектроннаяПодпись") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Данные) = Тип("Массив") Тогда
		МассивОбъектов = Данные;
	Иначе
		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(Данные);
	КонецЕсли;
	
	НаборДанных = Новый Массив;
	МассивДанныхОбъектов = Новый Массив;
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("ОбработчикЗавершения", ПараметрыПодписания.ОбработчикЗавершения);
	ПараметрыВыполнения.Вставить("ИдентификаторФормы", ПараметрыПодписания.ИдентификаторФормы);
	ПараметрыВыполнения.Вставить("МассивДанныхОбъектов", МассивДанныхОбъектов);
	
	СписокПредставлений = Новый СписокЗначений;
	
	Для Каждого Объект Из МассивОбъектов Цикл
		
		ПараметрыДанных = Неопределено;
		ПредставлениеДанных = Неопределено;
		ДополнительныеСвойства = Неопределено;
		
		Если ТипЗнч(Объект) = Тип("Структура")
			И Объект.Свойство("ОбъектПодписания") Тогда
			
			ОбъектПодписания = Объект.ОбъектПодписания;
			
			Если Объект.Свойство("ПараметрыДанных") Тогда
				ПараметрыДанных = Объект.ПараметрыДанных;
			КонецЕсли;
			
			Если Объект.Свойство("Представление") Тогда
				ПредставлениеДанных = Объект.Представление;
			КонецЕсли;
			
			Если Объект.Свойство("ДополнительныеСвойства") Тогда
				ДополнительныеСвойства = Объект.ДополнительныеСвойства;
			КонецЕсли;
			
		Иначе
			ОбъектПодписания = Объект;
		КонецЕсли;
		
		// У документов сперва подписываем не помеченные на удаление файлы.
		Если ДелопроизводствоКлиентСервер.ЭтоДокумент(ОбъектПодписания) Тогда
			
			МассивПодчиненныхФайлов = Делопроизводство.ПолучитьФайлыДокументаДляПодписания(ОбъектПодписания);
			
			Для Каждого Файл Из МассивПодчиненныхФайлов Цикл
				
				МассивДанныхОбъектов.Добавить(Файл);
				
				ТекущиеПараметрыВыполнения = Новый Структура;
				ТекущиеПараметрыВыполнения.Вставить("ИдентификаторФормы", ПараметрыПодписания.ИдентификаторФормы);
				ТекущиеПараметрыВыполнения.Вставить("ПодписываемыеДанные", Файл);
				
				ПараметрыДанныхФайла = Новый Структура;
				
				ТекущиеПараметрыВыполнения.Вставить("ПараметрыДанных", ПараметрыДанныхФайла);
				
				ЭлементДанных = Новый Структура;
				ЭлементДанных.Вставить("Представление", Файл);
				ЭлементДанных.Вставить("Данные",
					Новый ОписаниеОповещения("ПриЗапросеДвоичныхДанныхОбъекта", ЭтотОбъект, ТекущиеПараметрыВыполнения));
				ЭлементДанных.Вставить("Объект",
					Новый ОписаниеОповещения("ПриПолученииПодписи", ЭтотОбъект, ТекущиеПараметрыВыполнения));
				
				Если ДополнительныеСвойства <> Неопределено Тогда
					ЭлементДанных.Вставить("ДополнительныеСвойства", ДополнительныеСвойства);
				КонецЕсли;
				
				НаборДанных.Добавить(ЭлементДанных);
				
				СписокПредставлений.Добавить(Файл,
					СтрШаблон(НСтр("ru = 'Файл: %1'"), Файл));
				
			КонецЦикла;
			
		КонецЕсли;
		
		МассивДанныхОбъектов.Добавить(ОбъектПодписания);
		
		ТекущиеПараметрыВыполнения = Новый Структура;
		ТекущиеПараметрыВыполнения.Вставить("ИдентификаторФормы", ПараметрыПодписания.ИдентификаторФормы);
		ТекущиеПараметрыВыполнения.Вставить("ПодписываемыеДанные", ОбъектПодписания);
		
		Если ПараметрыДанных <> Неопределено Тогда
			ТекущиеПараметрыВыполнения.Вставить("ПараметрыДанных", ПараметрыДанных);
		КонецЕсли;
		
		ЭлементДанных = Новый Структура;
		ЭлементДанных.Вставить("Представление", ОбъектПодписания);
		Если ПредставлениеДанных <> Неопределено Тогда
			ЭлементДанных.Представление = ПредставлениеДанных;
		КонецЕсли;
		ЭлементДанных.Вставить("Данные",
			Новый ОписаниеОповещения("ПриЗапросеДвоичныхДанныхОбъекта", ЭтотОбъект, ТекущиеПараметрыВыполнения));
		ЭлементДанных.Вставить("Объект",
			Новый ОписаниеОповещения("ПриПолученииПодписи", ЭтотОбъект, ТекущиеПараметрыВыполнения));
		
		Если ДополнительныеСвойства <> Неопределено Тогда
			ЭлементДанных.Вставить("ДополнительныеСвойства", ДополнительныеСвойства);
		КонецЕсли;
		
		НаборДанных.Добавить(ЭлементДанных);
		
		Если ДелопроизводствоКлиентСервер.ЭтоДокумент(ОбъектПодписания) Тогда
			СписокПредставлений.Добавить(ОбъектПодписания, Строка(ОбъектПодписания));
		ИначеЕсли ПредставлениеДанных <> Неопределено Тогда
			СписокПредставлений.Добавить(ПредставлениеДанных);
		ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоФайл(ОбъектПодписания) Тогда
			СписокПредставлений.Добавить(ОбъектПодписания,
					СтрШаблон(НСтр("ru = 'Файл: %1'"), ОбъектПодписания));
		Иначе
			СписокПредставлений.Добавить(ОбъектПодписания);
		КонецЕсли;
		
	КонецЦикла;
	
	Заголовки = ПараметрыПодписания.Заголовки;
	Если Не ЗначениеЗаполнено(Заголовки)
		Или ТипЗнч(Заголовки) <> Тип("Структура") Тогда
		
		Заголовки = Новый Структура;
	КонецЕсли;
	
	Если Заголовки.Свойство("Операция") Тогда
		Операция = Заголовки.Операция;
	Иначе
		Операция = НСтр("ru = 'Подписание'");
		Если ДелопроизводствоКлиентСервер.ЭтоДокумент(Данные) Тогда
			Операция = НСтр("ru = 'Подписание документа'");
		ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоВизаСогласования(Данные) Тогда
			Операция = НСтр("ru = 'Подписание визы согласования'");
		ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоРезолюция(Данные) Тогда
			Операция = НСтр("ru = 'Подписание резолюции'");
		КонецЕсли;
	КонецЕсли;
	
	Если Заголовки.Свойство("ЗаголовокДанных") Тогда
		ЗаголовокДанных = Заголовки.ЗаголовокДанных;
	Иначе
		ЗаголовокДанных = НСтр("ru = 'Объект'");
		Если ДелопроизводствоКлиентСервер.ЭтоДокумент(Данные) Тогда
			ЗаголовокДанных = НСтр("ru = 'Документ'");
		ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоВизаСогласования(Данные) Тогда
			ЗаголовокДанных = НСтр("ru = 'Виза согласования'");
		ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоРезолюция(Данные) Тогда
			ЗаголовокДанных = НСтр("ru = 'Резолюция'");
		КонецЕсли;
	КонецЕсли;
	
	Если Заголовки.Свойство("ЗаголовокДанных") Тогда
		ПредставлениеНабора = Заголовки.ЗаголовокДанных;
	Иначе
		ПредставлениеНабора = НСтр("ru = 'Объекты (%1)'");
		// Выводим название документа, так как при подписании документа будут также подписаны его файлы.
		Если ДелопроизводствоКлиентСервер.ЭтоДокумент(Данные) Тогда
			ПредставлениеНабора = Строка(Данные);
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("ПоказатьКомментарий", Истина);
	ОписаниеДанных.Вставить("ИдентификаторФормы",  ПараметрыПодписания.ИдентификаторФормы);
	ОписаниеДанных.Вставить("Операция",            Операция);
	ОписаниеДанных.Вставить("ЗаголовокДанных",     ЗаголовокДанных);
	ОписаниеДанных.Вставить("НаборДанных",         НаборДанных);
	ОписаниеДанных.Вставить("ПредставлениеНабора", ПредставлениеНабора);
	ОписаниеДанных.Вставить("СообщитьОЗавершении", Ложь);
	ОписаниеДанных.Вставить("КонтекстОперации",    Неопределено);
	
	ДоступныеСертификаты = ПараметрыПодписания.ДоступныеСертификаты;
	Если ЗначениеЗаполнено(ДоступныеСертификаты) Тогда
		ОписаниеДанных.Вставить("ОтборСертификатов", ДоступныеСертификаты);
	КонецЕсли;
	
	ДоверенностиСертификатов = ПараметрыПодписания.ДоверенностиСертификатов;
	Если ЗначениеЗаполнено(ДоверенностиСертификатов)
		И ТипЗнч(ДоверенностиСертификатов) = Тип("Соответствие") Тогда
		
		ОписаниеДанных.Вставить("ДоверенностиСертификатов", ДоверенностиСертификатов);
	КонецЕсли;
	
	Если СписокПредставлений.Количество() > 0 Тогда
		ОписаниеДанных.Вставить("СписокПредставлений", СписокПредставлений);
	КонецЕсли;
	
	ОбработчикПродолжения = Новый ОписаниеОповещения("ПослеПодписанияОбъектов", ЭтотОбъект, ПараметрыВыполнения);
	
	ЭлектроннаяПодписьКлиент.Подписать(ОписаниеДанных, , ОбработчикПродолжения);
	
КонецПроцедуры

// Продолжение процедуры ПодписатьОбъект.
// Вызывается из подсистемы ЭлектроннаяПодпись при запросе данных для подписания.
//
Процедура ПриЗапросеДвоичныхДанныхОбъекта(Параметры, Контекст) Экспорт
	
	РезультатПолученияДанных = Новый Структура;
	РезультатПолученияДанных.Вставить("Данные", Неопределено);
	РезультатПолученияДанных.Вставить("ДанныеИзменены", Ложь);
	РезультатПолученияДанных.Вставить("ПричинаИзмененияДанных", "");
	
	Если ДелопроизводствоКлиентСервер.ЭтоФайл(Контекст.ПодписываемыеДанные) Тогда
		
		РезультатПолученияДанных.Данные =
			РаботаСФайламиВызовСервера.ПолучитьДвоичныеДанныеФайла(Контекст.ПодписываемыеДанные);
			
	Иначе
		
		ДополнительныеПараметры = Новый Структура;
		Если Контекст.Свойство("ПараметрыДанных") Тогда
			ДополнительныеПараметры = Контекст.ПараметрыДанных;
		КонецЕсли;
		
		РезультатПолученияДанных.Данные =
			РаботаСЭПВызовСервера.ПолучитьДвоичныеДанныеОбъекта(Контекст.ПодписываемыеДанные,,
				ДополнительныеПараметры);
		
	КонецЕсли;
	
	РаботаСЭПКлиентЛокализация.ПриЗапросеДвоичныхДанныхОбъекта(Параметры, Контекст, РезультатПолученияДанных);
	
	Если Контекст.Свойство("ПараметрыДанных") Тогда
		ДополнительныеПараметры = Контекст.ПараметрыДанных;
		
		Если РезультатПолученияДанных.ДанныеИзменены Тогда
			ДополнительныеПараметры.Вставить("ДанныеИзменены", Истина);
			ДополнительныеПараметры.Вставить("ИзмененныеДанные",
				РезультатПолученияДанных.Данные);
			ДополнительныеПараметры.Вставить("ПричинаИзмененияДанных",
				РезультатПолученияДанных.ПричинаИзмененияДанных);
			
		КонецЕсли;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.Оповещение, РезультатПолученияДанных);
	
КонецПроцедуры

// Продолжение процедуры ПодписатьОбъект.
// Вызывается из подсистемы ЭлектроннаяПодпись после подписания данных для нестандартного
// способа добавления подписи в объект.
//
Процедура ПриПолученииПодписи(Параметры, Контекст) Экспорт
	
	Если Контекст.Свойство("ПараметрыДанных")
		И Контекст.ПараметрыДанных.Свойство("ДанныеПодписанта") Тогда
		
		СвойстваПодписи = Параметры.ОписаниеДанных.ТекущийЭлементНабораДанных.СвойстваПодписи;
		
		// Если контекст недоступен на клиенте и подписание выполнялось на сервере,
		// то СвойстваПодписи - Адрес временного хранилища, содержащего свойства подписи
		Если ТипЗнч(СвойстваПодписи) = Тип("Строка")
			И ЭтоАдресВременногоХранилища(СвойстваПодписи) Тогда
			
			СвойстваПодписи = ПолучитьИзВременногоХранилища(СвойстваПодписи);
		КонецЕсли;
		
		ДанныеПодписанта = Контекст.ПараметрыДанных.ДанныеПодписанта;
		
		СвойстваПодписи.ДатаПодписи = ДанныеПодписанта.ДатаПодписи;
		СвойстваПодписи.Вставить("Подписал", ДанныеПодписанта.Подписал);
		
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.Оповещение, Новый Структура);
	
КонецПроцедуры

// Завершение процедуры ПодписатьОбъект.
Процедура ПослеПодписанияОбъектов(ОписаниеДанных, ПараметрыВыполнения) Экспорт
	
	Если Не ОписаниеДанных.Успех Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСФайламиСлужебныйКлиент.ВернутьРезультат(ПараметрыВыполнения.ОбработчикЗавершения, ОписаниеДанных);
	
КонецПроцедуры

Процедура ПослеПодписанияОбъекта(ОписаниеДанных, ПараметрыВыполнения) Экспорт
	
	Если Не ОписаниеДанных.Успех Тогда
		Возврат;
	КонецЕсли;
	
	ПодписанныеДанные = Новый Массив;
	Для Каждого Данные Из ОписаниеДанных.НаборДанных Цикл
		Если Не Данные.Свойство("СвойстваПодписи") Тогда
			Возврат;
		КонецЕсли;
		Элемент = Новый Структура;
		Элемент.Вставить("ПодписанныйОбъект", Данные.Представление);
		Элемент.Вставить("СвойстваПодписи", Данные.СвойстваПодписи);
		Элемент.Вставить("ПараметрыВыполнения", ПараметрыВыполнения);
		ПодписанныеДанные.Добавить(Элемент);
	КонецЦикла;
	
	РаботаСЭПВызовСервера.ЗанестиИнформациюОПодписях(ПодписанныеДанные, ОписаниеДанных.ИдентификаторФормы);
	ИнформироватьОПодписании(ОписаниеДанных.НаборДанных, ПараметрыВыполнения.Объект);
	
КонецПроцедуры

// По окончании подписания нотифицирует.
//
Процедура ИнформироватьОПодписании(ПодписанныеДанные, Объект) Экспорт
	
	Для Каждого Данные Из ПодписанныеДанные Цикл
		ДелопроизводствоКлиент.ОповеститьОбИзмененииОбъекта(Данные.Представление);
	КонецЦикла;
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Установлена подпись для ""%1""'"),
		Объект);
	Состояние(ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти

// Отображает результат проверки подписей
// 
// Параметры:
//  РезультатПроверки - см. НовыйРезультатВыполеннияДействийПроверки
//
Процедура ОтобразитьРезультатПроверкиПодписей(РезультатПроверки) Экспорт
	
	Если Не РезультатПроверки.Успех Тогда
		Возврат;
	КонецЕсли;
	
	ПроверенаОднаПодпись = РезультатПроверки.ПроверенныеПодписи.Количество() = 1
		И РезультатПроверки.РезультатПроверкиЭДО.Количество() = 0;
	ПровереныПодписиОдногоСообщенияЭДО = РезультатПроверки.ПроверенныеПодписи.Количество() = 0
		И РезультатПроверки.РезультатПроверкиЭДО.Количество() = 1;
	
	Если РезультатПроверки.ПроверенныеПодписи.Количество() = 0
		И РезультатПроверки.РезультатПроверкиЭДО.Количество() = 0 Тогда
		
		Возврат;
	ИначеЕсли ПроверенаОднаПодпись Тогда
		ОтобразитьРезультатПроверкиОднойПодписи(РезультатПроверки);
	Иначе
		ОтобразитьРезультатПроверкиНесколькихПодписей(РезультатПроверки);
	КонецЕсли;
	
КонецПроцедуры

#Область РаботаСУсовершенствованнымиЭП

// Усовершенствует подписи
// 
// Параметры:
//  ИдентификаторыПодписей - Массив Из УникальныйИдентификатор
//  ПараметрыУсовершенствования - см. НовыеПараметрыУсовершенствованияПодписей
Процедура УсовершенствоватьПодписи(ИдентификаторыПодписей, ПараметрыУсовершенствования) Экспорт
	
	Режим = ПараметрыУсовершенствования.РежимУсовершенствования;
	
	ПодписиКУсовершенствованию = ПодписиКУсовершенствованию(ИдентификаторыПодписей, Режим);
	
	Контекст = НовыйКонтекстУсовершенствованияПодписей();
	Контекст.ИдентификаторыВыбранныхПодписей = ИдентификаторыПодписей;
	Контекст.ОбработчикЗавершения = ПараметрыУсовершенствования.ОбработчикЗавершения;
	Контекст.ДанныеПодписейКУсовершенствованию = ПодписиКУсовершенствованию;
	Контекст.Форма = ПараметрыУсовершенствования.ВызывающаяФорма;
	Контекст.РежимУсовершенствования = ПараметрыУсовершенствования.РежимУсовершенствования;
	
	УсовершенствоватьПодписиВыполнение(Контекст);
	
КонецПроцедуры

// Конструктор параметров усовершенствования подписей
// 
// Возвращаемое значение:
//  Структура - Новые параметры усовершенствования подписей:
//    * ВызывающаяФорма - ФормаКлиентскогоПриложения
//    * ОбработчикЗавершения - Неопределено
//    * РежимУсовершенствования - Строка - см. РаботаСЭПКлиентСервер.РежимыУсовершенствованияПодписей
Функция НовыеПараметрыУсовершенствованияПодписей() Экспорт
	
	РежимыУсовершенствования = РаботаСЭПКлиентСервер.РежимыУсовершенствованияПодписей();
	
	ПараметрыУсовершенствования = Новый Структура;
	ПараметрыУсовершенствования.Вставить("ВызывающаяФорма", Неопределено);
	ПараметрыУсовершенствования.Вставить("ОбработчикЗавершения", Неопределено);
	ПараметрыУсовершенствования.Вставить("РежимУсовершенствования", РежимыУсовершенствования.ДобавлениеМетокВремени);
	
	Возврат ПараметрыУсовершенствования;
	
КонецФункции

#КонецОбласти

// Локализация
#Область РаботаСМЧД

Процедура УказатьДоверенностьЭлектроннойПодписи(ИдентификаторПодписи, ОбработчикЗавершения = Неопределено) Экспорт
	
	ДанныеДляУказания = РаботаСЭПВызовСервера.ДанныеДляУстановкиДоверенностиПодписи(ИдентификаторПодписи);
	
	Если Не ДанныеДляУказания.МожноУказатьДоверенность Тогда
		ПоказатьПредупреждение(, ДанныеДляУказания.ОписаниеПроблемы);
		ВыполнитьОбработкуОповещения(ОбработчикЗавершения, Ложь);
		Возврат;
	КонецЕсли;
	
	СписокВыбораДоверенностей = Новый СписокЗначений;
	
	Для Каждого ДанныеДоверенности Из ДанныеДляУказания.ДоступныеДоверенностиДляСертификата Цикл
		Представление = СтрШаблон(НСтр("ru = '%1 (от имени %2, ИНН - %3)'"),
			ДанныеДоверенности.Доверенность, ДанныеДоверенности.Доверитель, ДанныеДоверенности.ИННДоверителя);
		СписокВыбораДоверенностей.Добавить(ДанныеДоверенности.Доверенность, Представление);
	КонецЦикла;
	
	Представление = НСтр("ru = 'Без доверенности (от имени физического лица)'");
	СписокВыбораДоверенностей.Добавить(Неопределено, Представление);
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОбработчикЗавершения", ОбработчикЗавершения);
	Контекст.Вставить("ИдентификаторПодписи", ИдентификаторПодписи);
	
	ОбработчикВыбора = Новый ОписаниеОповещения("ОбработатьВыборДоверенностиДляУказания", ЭтотОбъект, Контекст);
	
	СписокВыбораДоверенностей.ПоказатьВыборЭлемента(ОбработчикВыбора, НСтр("ru = 'Выберите доверенность'"));
	
КонецПроцедуры

#КонецОбласти
// Конец Локализация

#КонецОбласти

#Область ЗагрузкаЭПИзПисьма

// Выполнит проверку соответствия файлов и ЭП
// 
// Параметры:
//  ДополнительныеПараметры - Структура
//    *СписокФайлов - Массив из Структура
//    *ИндексФайла - Число
//    *Форма - ФормаКлиентскогоПриложения 
//    *ПроверятьЭлектронныеПодписиНаСервере - Булево
// 
Процедура УстановитьСоответствиеФайловИЭП(ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.ИндексФайла < 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИндексЭП = ДополнительныеПараметры.СписокФайлов.Количество() - 1;
	
	ДополнительныеПараметры.Вставить("ИндексЭП", ИндексЭП);
	ДополнительныеПараметры.Вставить("Форма", ДополнительныеПараметры.Форма);
	ДополнительныеПараметры.Вставить("ПроверятьЭлектронныеПодписиНаСервере", 
		ДополнительныеПараметры.ПроверятьЭлектронныеПодписиНаСервере);
	
	УстановитьСоответствиеФайловИЭПЦиклЭПНачало(ДополнительныеПараметры);
	
КонецПроцедуры

// Выполнит проверку ЭП после создания менеджера криптографии
// 
// Параметры:
//  МенеджерКриптографии - МенеджерКриптографии
//  ДополнительныеПараметры - Структура
// 
Процедура ПроверитьЭППослеСозданияМенеджераКриптографии(МенеджерКриптографии, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(МенеджерКриптографии) <> Тип("МенеджерКриптографии") Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("МенеджерКриптографии", МенеджерКриптографии);
	ДополнительныеПараметры.ОбработчикЗавершения.ДополнительныеПараметры.Вставить("МенеджерКриптографии", МенеджерКриптографии);
	
	МодульЭлектроннаяПодписьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ЭлектроннаяПодписьКлиент");
	МодульЭлектроннаяПодписьКлиент.ПроверитьПодпись(
		ДополнительныеПараметры.ОбработчикЗавершения,
		ДополнительныеПараметры.АдресФайла,
		ДополнительныеПараметры.АдресПодписи,
		ДополнительныеПараметры.МенеджерКриптографии);
	
КонецПроцедуры

// Выполнит отметку о том, какому файлу принадлежит ЭП
// 
// Параметры:
//  Результат - Булево, Структура
//  ДополнительныеПараметры - Структура
// 
Процедура УстановитьСоответствиеФайловИЭППослеПроверкиСтроки(Результат, ДополнительныеПараметры)  Экспорт
	
	Если (ТипЗнч(Результат) = Тип("Булево") И Результат)
		ИЛИ (ТипЗнч(Результат) = Тип("Структура") 
			И Результат.Свойство("ТекстОшибкиПроверкиСертификата")) Тогда
		
		ДополнительныеПараметры.Вставить("РезультатПроверки", Результат);
		
		СтрокаФайла = ДополнительныеПараметры.СписокФайлов.Получить(ДополнительныеПараметры.ИндексФайла);
		СтрокаЭП = ДополнительныеПараметры.СписокФайлов.Получить(ДополнительныеПараметры.ИндексЭП);
		ДвоичныеДанныеЭП = ПолучитьДвоичныеДанныеФайла(СтрокаЭП, ДополнительныеПараметры.Форма);
		Сертификат = ПолучитьСертификатИзПодписи(ДвоичныеДанныеЭП, ДополнительныеПараметры.МенеджерКриптографии);
		
		ПеренестиФайлВПодпись(СтрокаФайла, СтрокаЭП, Сертификат, ДополнительныеПараметры.РезультатПроверки,
			ДополнительныеПараметры.Форма);
		
		Если ДополнительныеПараметры.ИндексФайла > ДополнительныеПараметры.ИндексЭП Тогда
			ДополнительныеПараметры.ИндексФайла = ДополнительныеПараметры.ИндексФайла - 1;
		КонецЕсли;
		
	КонецЕсли;
	
	ДополнительныеПараметры.ИндексЭП = ДополнительныеПараметры.ИндексЭП - 1;
	УстановитьСоответствиеФайловИЭПЦиклЭПНачало(ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область СохранениеДоверенностейПриСохраненииЭП

Процедура СохранитьДоверенностиПриСохраненииЭП(Контекст) Экспорт
	
	ДанныеДляСохранения = ДанныеДоверенностейДляСохранения(Контекст);
	
	Если Не ДанныеДляСохранения.Сохранять Тогда
		ЭлектроннаяПодписьСлужебныйКлиент.ПослеСохраненияДоверенностиВместеСПодписью(Контекст);
		Возврат;
	КонецЕсли;
	
	Если Контекст.Подключено Тогда
		Описание = Новый ОписаниеПередаваемогоФайла(ДанныеДляСохранения.ИмяФайла, ДанныеДляСохранения.АдресДанных);
		Контекст.ПолучаемыеФайлы.Добавить(Описание);
		ЭлектроннаяПодписьСлужебныйКлиент.ПослеСохраненияДоверенностиВместеСПодписью(Контекст);
	Иначе
		// Сохранение Файла из базы данных на диск.
		ФайловаяСистемаКлиент.СохранитьФайл(
			Новый ОписаниеОповещения("СохранитьДоверенностьВместеСЭППослеСохраненияФайла", ЭтотОбъект,
				Контекст),
			ДанныеДляСохранения.АдресДанных, ДанныеДляСохранения.ИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроверкаПодписей

Процедура ВыполнитьДействияДляПроверкиПодписей(Контекст) Экспорт
	
	РасшифроватьДанныеКПроверкеПодписей(Контекст);
	
	ПроверитьДействительностьПодписей(Контекст);
	
	ЗавершитьПроверкуПодписейНаСервере(Контекст);
	
	ПерезаполнитьТекущиеСтроки(Контекст);
	
	РаботаСЭПКлиентЛокализация.ВыполнитьДействияДляПроверкиПодписей(Контекст);
	
	ЗакончитьПроверкуПодписей(Контекст);
	
КонецПроцедуры

#Область ПолучениеДанныхПодписей

Функция ДанныеПодписейОбъектовДляПроверки(Форма, ПараметрыПроверки)
	
	ИмяТаблицыЭП = ПараметрыПроверки.ИмяТаблицыЭП;
	ВыделенныеСтроки = ПараметрыПроверки.ВыделенныеСтроки;
	
	Если Не ЗначениеЗаполнено(ИмяТаблицыЭП) Тогда
		
		СообщениеИсключения = НСтр("ru = 'Неверные параметры проверки строк: если не указано имя таблицы электронных подписей, то в качестве выделенных строк должен быть передан массив структур свойств подписей.'");
		
		Если ТипЗнч(ВыделенныеСтроки) <> Тип("Массив") Тогда
			ВызватьИсключение СообщениеИсключения;
		КонецЕсли;
		
		Если ВыделенныеСтроки.Количество() = 0 Тогда
			ВызватьИсключение СообщениеИсключения;
		КонецЕсли;
		
		Если ТипЗнч(ВыделенныеСтроки[0]) <> Тип("Структура") Тогда
			ВызватьИсключение СообщениеИсключения;
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеПодписейОбъектов = Новый Соответствие;
	
	НетВыделенныхСтрок =
		ТипЗнч(ВыделенныеСтроки) <> Тип("Массив")
		Или (ТипЗнч(ВыделенныеСтроки) = Тип("Массив")
			И ВыделенныеСтроки.Количество() = 0);
	
	Если НетВыделенныхСтрок Тогда
		
		ТаблицаЭП = Форма[ИмяТаблицыЭП];
		
		Если ТипЗнч(ТаблицаЭП) = Тип("ДанныеФормыДерево") Тогда
			ДополнитьДанныеПодписейИзКоллекцииДерева(ДанныеПодписейОбъектов, ТаблицаЭП.ПолучитьЭлементы());
		ИначеЕсли ТипЗнч(ТаблицаЭП) = Тип("ДанныеФормыКоллекция") Тогда
			
			Для Каждого Строка Из ТаблицаЭП Цикл
				ДополнитьДанныеПодписейИзСтроки(ДанныеПодписейОбъектов, Строка);
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		
		Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
			
			Если ТипЗнч(ВыделеннаяСтрока) = Тип("Число") Тогда
				
				ДанныеСтроки = Форма[ИмяТаблицыЭП].НайтиПоИдентификатору(ВыделеннаяСтрока);
				ДополнитьДанныеПодписейИзСтроки(ДанныеПодписейОбъектов, ДанныеСтроки);
				
				Если ТипЗнч(ДанныеСтроки) = Тип("ДанныеФормыЭлементДерева") Тогда
					ДополнитьДанныеПодписейИзКоллекцииДерева(ДанныеПодписейОбъектов, ДанныеСтроки.ПолучитьЭлементы());
				КонецЕсли;
				
			ИначеЕсли ТипЗнч(ВыделеннаяСтрока) = Тип("Структура") Тогда
				
				ДополнитьДанныеПодписейИзСтроки(ДанныеПодписейОбъектов, ВыделеннаяСтрока);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ДанныеПодписейОбъектов;
	
КонецФункции

Процедура ДополнитьДанныеПодписейИзКоллекцииДерева(ДанныеПодписей, КоллекцияСтрокДерева)
	
	Для Каждого Строка Из КоллекцияСтрокДерева Цикл
		
		ДополнитьДанныеПодписейИзКоллекцииДерева(ДанныеПодписей, Строка.ПолучитьЭлементы());
		
		ДополнитьДанныеПодписейИзСтроки(ДанныеПодписей, Строка);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьДанныеПодписейИзСтроки(ДанныеПодписей, Строка)
	
	УИДПодписи = Строка.УникальныйИдентификатор;
	Объект = Строка.Объект;
	
	Если Не ЗначениеЗаполнено(УИДПодписи)
		Или Не ЗначениеЗаполнено(Объект) Тогда
		
		Возврат;
	КонецЕсли;
	
	ПодписиОбъекта = ДанныеПодписей[Объект];
	Если ПодписиОбъекта = Неопределено Тогда
		ПодписиОбъекта = Новый Соответствие;
		ДанныеПодписей.Вставить(Объект, ПодписиОбъекта);
	КонецЕсли;
	
	ДанныеПодписи = РаботаСЭПКлиентСервер.НовыеДанныеПодписиДляПроверки();
	ЗаполнитьЗначенияСвойств(ДанныеПодписи, Строка);
	ПодписиОбъекта.Вставить(УИДПодписи, ДанныеПодписи);
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСКонтекстомПроверкиПодписей

Функция НовыйКонтекстПроверкиПодписей()
	
	НовыеПараметрыПроверки = НовыеПараметрыПроверкиПодписей();
	
	Контекст = Новый Структура;
	Контекст.Вставить("Форма", Неопределено);
	Контекст.Вставить("ПараметрыПроверки", НовыеПараметрыПроверки);
	Контекст.Вставить("ОбработчикЗавершения", Неопределено);
	
	Контекст.Вставить("ДанныеПодписейОбъектов", Новый Соответствие);
	Контекст.Вставить("ДанныеОбъектов", Новый Соответствие);
	Контекст.Вставить("РасшифрованныеДанныеОбъектов", Новый Соответствие);
	
	Контекст.Вставить("СообщенияЭДОДляПроверкиПодписей", Новый Массив());
	Контекст.Вставить("ИндексТекущегоСообщенияЭДО", -1);
	Контекст.Вставить("РезультатыПроверкиПодписейСообщенийЭДО", Новый Соответствие());
	
	Контекст.Вставить("ПодписанныеОбъекты", Новый Массив);
	Контекст.Вставить("ИндексТекущегоОбъекта", -1);
	
	Контекст.Вставить("ПодписиТекущегоОбъекта", Новый Массив);
	Контекст.Вставить("ИндексТекущейПодписи", -1);
	
	Контекст.Вставить("ТекущийОбъект", Неопределено);
	Контекст.Вставить("ТекущийИдентификаторПодписи", УникальныйИдентификаторПустой());
	
	Контекст.Вставить("ПроверкаТекущегоОбъектаНеуспешна", Ложь);
	Контекст.Вставить("ИндексДополнительнойПроверки", 0);
	
	Контекст.Вставить("РезультатыПроверкиПодписей", Новый Соответствие);
	Контекст.Вставить("ОбщийРезультатПроверки", Новый Соответствие);
	
	ДействияКВыполнению = Новый Соответствие;
	ДействияКВыполнению.Вставить(ДействияПроверкиПодписей().РасшифроватьДанные, Истина);
	ДействияКВыполнению.Вставить(ДействияПроверкиПодписей().ПроверитьПодписи, Истина);
	ДействияКВыполнению.Вставить(ДействияПроверкиПодписей().ЗакончитьПроверкуНаСервере, Истина);
	ДействияКВыполнению.Вставить(ДействияПроверкиПодписей().ПерезаполнитьСтроки, Истина);
	ДействияКВыполнению.Вставить(ДействияПроверкиПодписей().ПроверитьПодписиФайловЭДО, Истина);
	Контекст.Вставить("ДействияКВыполнению", ДействияКВыполнению);
	
	Контекст.Вставить("ИмеетсяРаботающееДействие", Ложь);
	
	Контекст.Вставить("Отказ", Ложь);
	
	Возврат Контекст;
	
КонецФункции

Функция ДействияПроверкиПодписей() Экспорт
	
	ДействияПроверки = Новый Структура;
	ДействияПроверки.Вставить("РасшифроватьДанные", "РасшифроватьДанные");
	ДействияПроверки.Вставить("ПроверитьПодписи", "ПроверитьПодписи");
	ДействияПроверки.Вставить("ЗакончитьПроверкуНаСервере", "ЗакончитьПроверкуНаСервере");
	ДействияПроверки.Вставить("ПерезаполнитьСтроки", "ПерезаполнитьСтроки");
	// Локализация
	ДействияПроверки.Вставить("ПроверитьПодписиФайловЭДО", "ПроверитьПодписиФайловЭДО");
	// Конец Локализация
	
	Возврат ДействияПроверки;
	
КонецФункции

Функция НеобходимоВыполнитьДействиеПроверкиПодписей(Действие, Контекст) Экспорт
	
	Если Контекст.Отказ Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Контекст.ИмеетсяРаботающееДействие Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Контекст.ДействияКВыполнению[Действие] <> Истина Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура УбратьНеобходимостьВыполненияДействияПроверкиПодписей(Действие, Контекст) Экспорт
	
	Контекст.ДействияКВыполнению[Действие] = Ложь;
	
КонецПроцедуры

Процедура УстановитьЗапускДействияПроверкиПодписей(Контекст) Экспорт
	
	Контекст.ИмеетсяРаботающееДействие = Истина;
	
КонецПроцедуры

Процедура СброситьЗапускДействияПроверкиПодписей(Контекст) Экспорт
	
	Контекст.ИмеетсяРаботающееДействие = Ложь;
	
КонецПроцедуры

Процедура УстановитьОтказПроверкиПодписей(Контекст)
	
	Контекст.Отказ = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьКонтекстПроверкиПоДаннымПодписей(Контекст, ДанныеПодписейОбъектов)
	
	РаботаСЭПКлиентЛокализация.ПередЗаполнениемКонтекстаПроверкиПоДаннымПодписей(Контекст, ДанныеПодписейОбъектов);
	
	Контекст.ДанныеПодписейОбъектов = ДанныеПодписейОбъектов;
	
	ПодписанныеОбъекты = Новый Массив;
	Для Каждого Элемент Из ДанныеПодписейОбъектов Цикл
		Объект = Элемент.Ключ;
		ПодписанныеОбъекты.Добавить(Объект);
	КонецЦикла;
	Контекст.ПодписанныеОбъекты = ПодписанныеОбъекты;
	
	ПараметрыПолучения = РаботаСЭПКлиентСервер.НовыеПараметрыПолученияДанныхОбъектовДляПроверкиПодписей();
	ПараметрыПолучения.ДанныеПодписейОбъектов = ДанныеПодписейОбъектов;
	ПараметрыПолучения.УникальныйИдентификаторФормы = Контекст.Форма.УникальныйИдентификатор;
	
	Контекст.ДанныеОбъектов = РаботаСЭПВызовСервера.ДанныеОбъектовДляПроверкиПодписей(ПараметрыПолучения);
	
КонецПроцедуры

#КонецОбласти

#Область РасшифровкаДанных

Процедура РасшифроватьДанныеКПроверкеПодписей(Контекст)
	
	ДействиеРасшифровки = ДействияПроверкиПодписей().РасшифроватьДанные;
	
	Если Не НеобходимоВыполнитьДействиеПроверкиПодписей(ДействиеРасшифровки, Контекст) Тогда
		Возврат;
	КонецЕсли;
	
	ЗашифрованныеОбъекты = Новый Массив;
	Для Каждого Элемент Из Контекст.ДанныеОбъектов Цикл
		
		Объект = Элемент.Ключ;
		ДанныеОбъекта = Элемент.Значение;
		Если ДанныеОбъекта.Зашифрован Тогда
			ЗашифрованныеОбъекты.Добавить(Объект);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗашифрованныеОбъекты.Количество() = 0 Тогда
		УбратьНеобходимостьВыполненияДействияПроверкиПодписей(ДействиеРасшифровки, Контекст);
		Возврат;
	КонецЕсли;
	
	УстановитьЗапускДействияПроверкиПодписей(Контекст);
	
	КонтекстРасшифровки = НовыйКонтекстРасшифровкиДляПроверкиПодписей();
	КонтекстРасшифровки.КонтекстПроверки = Контекст;
	КонтекстРасшифровки.ЗашифрованныеОбъекты = ЗашифрованныеОбъекты;
	
	НачатьЦиклРасшифровкиДляПроверкиПодписей(КонтекстРасшифровки);
	
КонецПроцедуры

Функция НовыйКонтекстРасшифровкиДляПроверкиПодписей()
	
	Контекст = Новый Структура;
	Контекст.Вставить("КонтекстПроверки", Новый Структура);
	
	Контекст.Вставить("ЗашифрованныеОбъекты", Новый Массив);
	Контекст.Вставить("ИндексТекущегоОбъекта", -1);
	
	Контекст.Вставить("ТекущийОбъект", Неопределено);
	
	Контекст.Вставить("РасшифрованныеДанные", Новый Соответствие);
	
	Возврат Контекст;
	
КонецФункции

Процедура НачатьЦиклРасшифровкиДляПроверкиПодписей(КонтекстРасшифровки)
	
	Если Не ИтерироватьИндексыРасшифровкиДляПроверкиПодписей(КонтекстРасшифровки) Тогда
		ЗакончитьРасшифровкуДанныхКПроверкеПодписей(КонтекстРасшифровки);
		Возврат;
	КонецЕсли;
	
	ОписаниеДанных = ОписаниеДанныхРасшифровкиКПроверкеПодписей(КонтекстРасшифровки);
	
	ОбработчикПродолжения = Новый ОписаниеОповещения("ОбработатьРасшифровкуДанныхКПроверкеПодписей",
		ЭтотОбъект, КонтекстРасшифровки);
	
	ЭлектроннаяПодписьКлиент.Расшифровать(
		ОписаниеДанных, КонтекстРасшифровки.КонтекстПроверки.Форма, ОбработчикПродолжения);
	
КонецПроцедуры

Функция ИтерироватьИндексыРасшифровкиДляПроверкиПодписей(КонтекстРасшифровки)
	
	ИндексОбъекта = КонтекстРасшифровки.ИндексТекущегоОбъекта;
	Объекты = КонтекстРасшифровки.ЗашифрованныеОбъекты;
	КоличествоОбъектов = Объекты.Количество();
	
	Если ИндексОбъекта + 1 >= КоличествоОбъектов Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИндексОбъекта = ИндексОбъекта + 1;
	Объект = Объекты[ИндексОбъекта];
	
	КонтекстРасшифровки.ИндексТекущегоОбъекта = ИндексОбъекта;
	КонтекстРасшифровки.ТекущийОбъект = Объект;
	
	Возврат Истина;
	
КонецФункции

Функция ОписаниеДанныхРасшифровкиКПроверкеПодписей(КонтекстРасшифровки)
	
	КонтекстПроверки = КонтекстРасшифровки.КонтекстПроверки;
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("ИдентификаторФормы", КонтекстПроверки.Форма.УникальныйИдентификатор);
	ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Расшифровка данных к проверке подписей'"));
	ОписаниеДанных.Вставить("ЗаголовокДанных", НСтр("ru = 'Зашифрованный объект'"));
	
	ТекущийОбъект = КонтекстРасшифровки.ТекущийОбъект;
	
	ДанныеОбъекта = КонтекстПроверки.ДанныеОбъектов[ТекущийОбъект];
	Данные = ПолучитьИзВременногоХранилища(ДанныеОбъекта.АдресДанных);
	
	ОписаниеДанных.Вставить("Данные", Данные);
	ОписаниеДанных.Вставить("СертификатыШифрования", ДанныеОбъекта.ОбъектШифрования);
	ОписаниеДанных.Вставить("Представление", ДанныеОбъекта.ОбъектШифрования);
	ОписаниеДанных.Вставить("СообщитьОЗавершении", Ложь);
	
	Возврат ОписаниеДанных;
	
КонецФункции

Процедура ОбработатьРасшифровкуДанныхКПроверкеПодписей(ОписаниеДанных, КонтекстРасшифровки) Экспорт
	
	Если Не ОписаниеДанных.Успех Тогда
		УстановитьОтказПроверкиПодписей(КонтекстРасшифровки.КонтекстПроверки);
		ЗакончитьРасшифровкуДанныхКПроверкеПодписей(КонтекстРасшифровки);
		Возврат;
	КонецЕсли;
	
	УИДФормы = КонтекстРасшифровки.КонтекстПроверки.Форма.УникальныйИдентификатор;
	
	РасшифрованныеДанные = ОписаниеДанных.РасшифрованныеДанные;
	Если ТипЗнч(РасшифрованныеДанные) = Тип("Строка") И ЭтоАдресВременногоХранилища(РасшифрованныеДанные) Тогда
		АдресРасшифрованныхДанных = РасшифрованныеДанные;
	Иначе
		АдресРасшифрованныхДанных = ПоместитьВоВременноеХранилище(
			РасшифрованныеДанные, УИДФормы);
	КонецЕсли;
	
	КонтекстРасшифровки.РасшифрованныеДанные.Вставить(
		КонтекстРасшифровки.ТекущийОбъект,
		АдресРасшифрованныхДанных);
	
	НачатьЦиклРасшифровкиДляПроверкиПодписей(КонтекстРасшифровки);
	
КонецПроцедуры

Процедура ЗакончитьРасшифровкуДанныхКПроверкеПодписей(КонтекстРасшифровки)
	
	КонтекстПроверки = КонтекстРасшифровки.КонтекстПроверки;
	КонтекстПроверки.РасшифрованныеДанныеОбъектов = КонтекстРасшифровки.РасшифрованныеДанные;
	
	Если КонтекстРасшифровки.РасшифрованныеДанные.Количество() > 0 Тогда
		
		ПараметрыПолучения = РаботаСЭПКлиентСервер.НовыеПараметрыПолученияДанныхОбъектовДляПроверкиПодписей();
		ПараметрыПолучения.ДанныеПодписейОбъектов = КонтекстПроверки.ДанныеПодписейОбъектов;
		ПараметрыПолучения.УникальныйИдентификаторФормы = КонтекстПроверки.Форма.УникальныйИдентификатор;
		ПараметрыПолучения.РасшифрованныеДанные = КонтекстРасшифровки.РасшифрованныеДанные;
		
		КонтекстПроверки.ДанныеОбъектов = РаботаСЭПВызовСервера.ДанныеОбъектовДляПроверкиПодписей(ПараметрыПолучения);
		
	КонецЕсли;
	
	ДействиеРасшифровки = ДействияПроверкиПодписей().РасшифроватьДанные;
	УбратьНеобходимостьВыполненияДействияПроверкиПодписей(ДействиеРасшифровки, КонтекстПроверки);
	СброситьЗапускДействияПроверкиПодписей(КонтекстПроверки);
	ВыполнитьДействияДляПроверкиПодписей(КонтекстПроверки);
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаДействительностиПодписей

Процедура ПроверитьДействительностьПодписей(Контекст)
	
	ДействиеПроверки = ДействияПроверкиПодписей().ПроверитьПодписи;
	Если Не НеобходимоВыполнитьДействиеПроверкиПодписей(ДействиеПроверки, Контекст) Тогда
		Возврат;
	КонецЕсли;
	
	ПроверятьЭлектронныеПодписиНаСервере = 
		ЭлектроннаяПодписьКлиент.ОбщиеНастройки().ПроверятьЭлектронныеПодписиНаСервере;
	
	Если ПроверятьЭлектронныеПодписиНаСервере Тогда
		ПроверитьЭлектронныеПодписиНаСервере(Контекст);
		Возврат;
	КонецЕсли;
	
	УстановитьЗапускДействияПроверкиПодписей(Контекст);
	
	НачатьЦиклПроверкиДействительностиПодписей(Контекст);
	
КонецПроцедуры

Процедура ПроверитьЭлектронныеПодписиНаСервере(Контекст)
	
	УстановитьЗапускДействияПроверкиПодписей(Контекст);
	
	Контекст.ОбщийРезультатПроверки =
		РаботаСЭПВызовСервера.ПроверитьЭлектронныеПодписиНаСервере(
			Контекст.ДанныеПодписейОбъектов,
			Контекст.ДанныеОбъектов);
	
	УбратьНеобходимостьВыполненияДействияПроверкиПодписей(
		ДействияПроверкиПодписей().ПроверитьПодписи, Контекст);
	УбратьНеобходимостьВыполненияДействияПроверкиПодписей(
		ДействияПроверкиПодписей().ЗакончитьПроверкуНаСервере, Контекст);
	СброситьЗапускДействияПроверкиПодписей(Контекст);
	
КонецПроцедуры

Процедура НачатьЦиклПроверкиДействительностиПодписей(Контекст)
	
	Если Не ИтерироватьИндексыПодписиДляПроверки(Контекст) Тогда
		ЗакончитьПроверкуДействительностиПодписей(Контекст);
		Возврат;
	КонецЕсли;
	
	ДанныеДляПроверки = ДанныеДляПроверкиДействительностиПодписи(Контекст);
	
	ОбработчикЗавершения = Новый ОписаниеОповещения("ОбработатьПроверкуДействительностиПодписи", ЭтотОбъект, Контекст);
	
	ПараметрыПроверки = ЭлектроннаяПодписьКлиент.ПараметрыПроверкиПодписи();
	ПараметрыПроверки.РезультатВВидеСтруктуры = Истина;
	
	ЭлектроннаяПодписьКлиент.ПроверитьПодпись(
		ОбработчикЗавершения,
		ДанныеДляПроверки.АдресДанных,
		ДанныеДляПроверки.АдресПодписи, , ,
		ПараметрыПроверки);
	
КонецПроцедуры

Функция ИтерироватьИндексыПодписиДляПроверки(Контекст)
	
	Если ИтерироватьИндексыПоДополнительнойПроверке(Контекст) Тогда
		Возврат Истина;
	КонецЕсли;
	
	ИндексПодписи = Контекст.ИндексТекущейПодписи;
	ПодписиОбъекта = Контекст.ПодписиТекущегоОбъекта;
	
	ИндексПодписи = ИндексПодписи + 1;
	
	Если ИндексПодписи < ПодписиОбъекта.Количество() Тогда
		Контекст.ИндексТекущейПодписи = ИндексПодписи;
		Контекст.ТекущийИдентификаторПодписи = ПодписиОбъекта[ИндексПодписи];
		Возврат Истина;
	КонецЕсли;
	
	ИндексПодписи = 0;
	ИндексОбъекта = Контекст.ИндексТекущегоОбъекта;
	
	ИндексОбъекта = ИндексОбъекта + 1;
	Объекты = Контекст.ПодписанныеОбъекты;
	
	Если ИндексОбъекта >= Объекты.Количество() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Объект = Объекты[ИндексОбъекта];
	ДанныеПодписейОбъектов = Контекст.ДанныеПодписейОбъектов[Объект];
	
	ПодписиОбъекта = Новый Массив;
	Для Каждого Элемент Из ДанныеПодписейОбъектов Цикл
		ПодписиОбъекта.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	Контекст.ИндексТекущегоОбъекта = ИндексОбъекта;
	Контекст.ТекущийОбъект = Объект;
	
	Контекст.ПодписиТекущегоОбъекта = ПодписиОбъекта;
	
	Контекст.ИндексТекущейПодписи = ИндексПодписи;
	Контекст.ТекущийИдентификаторПодписи = ПодписиОбъекта[ИндексПодписи];
	
	Возврат Истина;
	
КонецФункции

Функция ИтерироватьИндексыПоДополнительнойПроверке(Контекст)
	
	Если Не Контекст.ПроверкаТекущегоОбъектаНеуспешна Тогда
		Контекст.ИндексДополнительнойПроверки = 0;
		Контекст.ПроверкаТекущегоОбъектаНеуспешна = Ложь;
		Возврат Ложь;
	КонецЕсли;
	
	Объект = Контекст.ТекущийОбъект;
	ДанныеОбъекта = Контекст.ДанныеОбъектов[Объект];
	
	Если Не ДанныеОбъекта.РазныеВерсииПодписей Тогда
		Контекст.ИндексДополнительнойПроверки = 0;
		Контекст.ПроверкаТекущегоОбъектаНеуспешна = Ложь;
		Возврат Ложь;
	КонецЕсли;
	
	ИдентификаторПодписи = Контекст.ТекущийИдентификаторПодписи;
	
	ДанныеПодписи = Контекст.ДанныеПодписейОбъектов[Объект][ИдентификаторПодписи];
	Версия = ДанныеПодписи.Версия;
	
	ВариантыДополнительныхПроверок = ДанныеОбъекта.ДвоичныеДанныеПоВерсиям[Версия];
	
	ИндексПроверки = Контекст.ИндексДополнительнойПроверки;
	
	Если ВариантыДополнительныхПроверок.Количество() <= ИндексПроверки + 1 Тогда
		Контекст.ИндексДополнительнойПроверки = 0;
		Контекст.ПроверкаТекущегоОбъектаНеуспешна = Ложь;
		Возврат Ложь;
	КонецЕсли;
	
	Контекст.ИндексДополнительнойПроверки = ИндексПроверки + 1;
	Возврат Истина;
	
КонецФункции

Функция ДанныеДляПроверкиДействительностиПодписи(Контекст)
	
	ДанныеДляПроверки = Новый Структура;
	ДанныеДляПроверки.Вставить("АдресДанных", "");
	ДанныеДляПроверки.Вставить("АдресПодписи", "");
	
	Объект = Контекст.ТекущийОбъект;
	ИдентификаторПодписи = Контекст.ТекущийИдентификаторПодписи;
	
	ДанныеПодписи = Контекст.ДанныеПодписейОбъектов[Объект][ИдентификаторПодписи];
	
	АдресПодписи = ДанныеПодписи.АдресПодписи;
	
	ДанныеОбъекта = Контекст.ДанныеОбъектов[Объект];
	Версия = ДанныеПодписи.Версия;
	
	АдресДанных = "";
	Если Не ДанныеОбъекта.РазныеВерсииПодписей Тогда
		АдресДанных = ДанныеОбъекта.АдресДанных;
	Иначе
		ВариантыДополнительныхПроверок = ДанныеОбъекта.ДвоичныеДанныеПоВерсиям[Версия];
		АдресДанных = ВариантыДополнительныхПроверок[Контекст.ИндексДополнительнойПроверки];
	КонецЕсли;
	
	ДанныеДляПроверки.АдресПодписи = АдресПодписи;
	ДанныеДляПроверки.АдресДанных = АдресДанных;
	
	Возврат ДанныеДляПроверки;
	
КонецФункции

Процедура ОбработатьПроверкуДействительностиПодписи(Результат, Контекст) Экспорт
	
	РезультатПроверки = РаботаСЭПКлиентСервер.РезультатПроверкиДОПоРезультатуБСП(Результат);
	
	Если Не РезультатПроверки.ПодписьВерна Тогда
		Контекст.ПроверкаТекущегоОбъектаНеуспешна = Истина;
	КонецЕсли;
	
	ИдентификаторПодписи = Контекст.ТекущийИдентификаторПодписи;
	
	РезультатыПроверки = Контекст.РезультатыПроверкиПодписей;
	
	РезультатыПроверки.Вставить(ИдентификаторПодписи, РезультатПроверки);
	
	НачатьЦиклПроверкиДействительностиПодписей(Контекст);
	
КонецПроцедуры

Процедура ЗакончитьПроверкуДействительностиПодписей(Контекст)
	
	ДействиеПроверки = ДействияПроверкиПодписей().ПроверитьПодписи;
	УбратьНеобходимостьВыполненияДействияПроверкиПодписей(ДействиеПроверки, Контекст);
	СброситьЗапускДействияПроверкиПодписей(Контекст);
	ВыполнитьДействияДляПроверкиПодписей(Контекст);
	
КонецПроцедуры

#КонецОбласти

#Область ЗавершениеПроверкиПодписейНаСервере

Процедура ЗавершитьПроверкуПодписейНаСервере(Контекст)
	
	ДействиеЗавершения = ДействияПроверкиПодписей().ЗакончитьПроверкуНаСервере;
	Если Не НеобходимоВыполнитьДействиеПроверкиПодписей(ДействиеЗавершения, Контекст) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьЗапускДействияПроверкиПодписей(Контекст);
	
	РезультатПроверки = РаботаСЭПВызовСервера.ЗавершитьПроверкуПодписейНаСервере(
		Контекст.ДанныеПодписейОбъектов,
		Контекст.РезультатыПроверкиПодписей);
	
	Контекст.ОбщийРезультатПроверки = РезультатПроверки;
	
	УбратьНеобходимостьВыполненияДействияПроверкиПодписей(ДействиеЗавершения, Контекст);
	СброситьЗапускДействияПроверкиПодписей(Контекст);
	
КонецПроцедуры

#КонецОбласти

#Область ПерезаполнениеТекущихСтрок

Процедура ПерезаполнитьТекущиеСтроки(Контекст)
	
	ДействиеПерезаполнения = ДействияПроверкиПодписей().ПерезаполнитьСтроки;
	Если Не НеобходимоВыполнитьДействиеПроверкиПодписей(ДействиеПерезаполнения, Контекст) Тогда
		Возврат;
	КонецЕсли;
	
	Если Контекст.ПараметрыПроверки.ПерезаполнитьВыделенныеСтроки <> Истина Тогда
		УбратьНеобходимостьВыполненияДействияПроверкиПодписей(ДействиеПерезаполнения, Контекст);
		Возврат;
	КонецЕсли;
	
	УстановитьЗапускДействияПроверкиПодписей(Контекст);
	
	ПерезаполнитьДанныеВСтрокахПоРезультатамПроверкиПодписей(Контекст);
	
	УбратьНеобходимостьВыполненияДействияПроверкиПодписей(ДействиеПерезаполнения, Контекст);
	СброситьЗапускДействияПроверкиПодписей(Контекст);
	
КонецПроцедуры

Процедура ПерезаполнитьДанныеВСтрокахПоРезультатамПроверкиПодписей(Контекст)
	
	ПараметрыПроверки = Контекст.ПараметрыПроверки;
	Форма = Контекст.Форма;
	
	ИмяТаблицыЭП = ПараметрыПроверки.ИмяТаблицыЭП;
	ВыделенныеСтроки = ПараметрыПроверки.ВыделенныеСтроки;
	
	РезультатыПроверки = Контекст.ОбщийРезультатПроверки;
	
	НетВыделенныхСтрок =
		ТипЗнч(ВыделенныеСтроки) <> Тип("Массив")
		Или (ТипЗнч(ВыделенныеСтроки) = Тип("Массив")
			И ВыделенныеСтроки.Количество() = 0);
	
	Если НетВыделенныхСтрок Тогда
		
		ТаблицаЭП = Форма[ИмяТаблицыЭП];
		
		Если ТипЗнч(ТаблицаЭП) = Тип("ДанныеФормыДерево") Тогда
			ПерезаполнитьДанныеПоРезультатамПроверкиВКоллекцииДерева(ТаблицаЭП.ПолучитьЭлементы(), РезультатыПроверки);
		ИначеЕсли ТипЗнч(ТаблицаЭП) = Тип("ДанныеФормыКоллекция") Тогда
			
			Для Каждого Строка Из ТаблицаЭП Цикл
				ПерезаполнитьДанныеПоРезультатамПроверкиВСтроке(Строка, РезультатыПроверки);
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		
		Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
			
			Если ТипЗнч(ВыделеннаяСтрока) = Тип("Число") Тогда
				
				ДанныеСтроки = Форма[ИмяТаблицыЭП].НайтиПоИдентификатору(ВыделеннаяСтрока);
				ПерезаполнитьДанныеПоРезультатамПроверкиВСтроке(ДанныеСтроки, РезультатыПроверки);
				
				Если ТипЗнч(ДанныеСтроки) = Тип("ДанныеФормыЭлементДерева") Тогда
					ПерезаполнитьДанныеПоРезультатамПроверкиВКоллекцииДерева(
						ДанныеСтроки.ПолучитьЭлементы(), РезультатыПроверки);
				КонецЕсли;
				
			ИначеЕсли ТипЗнч(ВыделеннаяСтрока) = Тип("Структура") Тогда
				
				ПерезаполнитьДанныеПоРезультатамПроверкиВСтроке(ВыделеннаяСтрока, РезультатыПроверки);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПерезаполнитьДанныеПоРезультатамПроверкиВКоллекцииДерева(КоллекцияСтрокДерева, РезультатыПроверки)
	
	Для Каждого Строка Из КоллекцияСтрокДерева Цикл
		
		ПерезаполнитьДанныеПоРезультатамПроверкиВКоллекцииДерева(Строка.ПолучитьЭлементы(), РезультатыПроверки);
		
		ПерезаполнитьДанныеПоРезультатамПроверкиВСтроке(Строка, РезультатыПроверки);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПерезаполнитьДанныеПоРезультатамПроверкиВСтроке(Строка, РезультатыПроверки)
	
	УИДПодписи = Строка.УникальныйИдентификатор;
	
	РезультатПроверкиСтроки = РезультатыПроверки[УИДПодписи];
	Если РезультатПроверкиСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Строка, РезультатПроверкиСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ОкончаниеПроверки

Процедура ЗакончитьПроверкуПодписей(Контекст)
	
	Если Не МожноЗавершитьПроверкуПодписей(Контекст) Тогда
		Возврат;
	КонецЕсли;
	
	РезультатПроверки = НовыйРезультатВыполеннияДействийПроверки();
	РезультатПроверки.ПроверенныеПодписи = Контекст.ОбщийРезультатПроверки;
	РезультатПроверки.Успех = Не (Контекст.Отказ = Истина);
	РезультатПроверки.РезультатПроверкиЭДО = Контекст.РезультатыПроверкиПодписейСообщенийЭДО;
	
	Если Контекст.ОбработчикЗавершения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(Контекст.ОбработчикЗавершения, РезультатПроверки);
	КонецЕсли;
	
КонецПроцедуры

Функция МожноЗавершитьПроверкуПодписей(Контекст)
	
	Если Контекст.ИмеетсяРаботающееДействие Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Контекст.Отказ Тогда
		Возврат Истина;
	КонецЕсли;
	
	ВозможныеДействия = ДействияПроверкиПодписей();
	
	ДействияПроверки = Новый Массив;
	ДействияПроверки.Добавить(ВозможныеДействия.РасшифроватьДанные);
	ДействияПроверки.Добавить(ВозможныеДействия.ПроверитьПодписи);
	ДействияПроверки.Добавить(ВозможныеДействия.ЗакончитьПроверкуНаСервере);
	ДействияПроверки.Добавить(ВозможныеДействия.ПерезаполнитьСтроки);
	
	Для Каждого Действие Из ДействияПроверки Цикл
		Если Контекст.ДействияКВыполнению[Действие] = Истина Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

// Конструктор результата проверки подписей
// 
// Возвращаемое значение:
//  Структура - Новый результат выполенния действий проверки:
//    * Успех - Булево - Успешно ли выполнена проверка
//    * ПроверенныеПодписи - Соответствие Из КлючИЗначение:
//      ** Ключ - УникальныйИдентификатор - УИД подписи
//      ** Значение - Структура:
//        *** ДатаПроверкиПодписи - Дата
//        *** ДоверенностьВерна - Булево
//        *** ПодписьВерна - Булево
//        *** СертификатДействителен - Булево
//        *** Статус - Строка
//     * РезультатПроверкиЭДО - Соответствие Из КлючИЗначение:
//       ** Ключ - ДокументСсылка.СообщениеЭДО
//       ** Значение - см. ЭлектронныеДокументыЭДО.ПроверитьПодписиСообщения
//
Функция НовыйРезультатВыполеннияДействийПроверки()
	
	Результат = Новый Структура;
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("ПроверенныеПодписи", Новый Соответствие);
	Результат.Вставить("РезультатПроверкиЭДО", Новый Соответствие());
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область РаботаСИнтерфейсом

#Область ОбработкаПроверкиПодписей

Процедура ОтобразитьРезультатПроверкиОднойПодписи(РезультатПроверки)
	
	ИдентификаторПодписи = Неопределено;
	Для Каждого Элемент Из РезультатПроверки.ПроверенныеПодписи Цикл
		ИдентификаторПодписи = Элемент.Ключ;
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ИдентификаторПодписи) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УникальныйИдентификатор", ИдентификаторПодписи);
	Попытка
		ОткрытьФорму("РегистрСведений.ЭлектронныеПодписи.ФормаЗаписи",
			ПараметрыФормы, , , , , ,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ТекстОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		ПоказатьПредупреждение(Неопределено, ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

Процедура ОтобразитьРезультатПроверкиНесколькихПодписей(РезультатПроверки)
	
	ВсегоПодписей = РезультатПроверки.ПроверенныеПодписи.Количество();
	
	Действительных = 0;
	Недействительных = 0;
	СПроблемнымиСертификатами = 0;
	
	Для Каждого Элемент Из РезультатПроверки.ПроверенныеПодписи Цикл
		
		РезультатПроверкиПодписи = Элемент.Значение;
		
		Если РезультатПроверкиПодписи.ПодписьВерна Тогда
			
			Действительных = Действительных + 1;
			
			Если Не РезультатПроверкиПодписи.СертификатДействителен Тогда
				СПроблемнымиСертификатами = СПроблемнымиСертификатами + 1;
			КонецЕсли;
			
		Иначе
			Недействительных = Недействительных + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	СтрокиРезультатаПроверки = Новый Массив();
	
	Если ВсегоПодписей > 0 Тогда
		
		СтрокиРезультатаПроверки.Добавить(
			ПредставлениеКоличестваПроверенныхПодписей(ВсегоПодписей));
		
		СтрокиРезультатаПроверки.Добавить(
			ПредставлениеОбщегоРезультатаПроверки(ВсегоПодписей, Действительных, Недействительных));
		
		Если СПроблемнымиСертификатами > 0 Тогда
			СтрокиРезультатаПроверки.Добавить(
				ПредставлениеПодписейСПроблемнымиСертификатами(Действительных, СПроблемнымиСертификатами));
		КонецЕсли;
		
	КонецЕсли;
	
	ПровереноЭДО = РезультатПроверки.РезультатПроверкиЭДО.Количество();
	УспешныхЭДО = 0;
	НеуспешныхЭДО = 0;
	
	Для Каждого Элемент Из РезультатПроверки.РезультатПроверкиЭДО Цикл
		
		РезультатПроверкиЭДО = Элемент.Значение;
		Если РезультатПроверкиЭДО.Успех Тогда
			УспешныхЭДО = УспешныхЭДО + 1;
		Иначе
			НеуспешныхЭДО = НеуспешныхЭДО + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПровереноЭДО > 0 Тогда
		
		СтрокиРезультатаПроверки.Добавить(
			ПредставлениеКоличестваПроверенныхСообщенийЭДО(ПровереноЭДО));
		СтрокиРезультатаПроверки.Добавить(
			ПредставлениеОбщегоРезультатаПроверкиЭДО(ПровереноЭДО, УспешныхЭДО, НеуспешныхЭДО));
		
	КонецЕсли;
	
	Представление = СтрСоединить(СтрокиРезультатаПроверки, Символы.ПС);
	
	ПоказатьПредупреждение( , Представление, , НСтр("ru = 'Результат проверки подписей'"));
	
КонецПроцедуры

// "Проверена(о) N подпись(си/сей)."
// 
// Параметры:
//  ВсегоПодписей - Число
// 
// Возвращаемое значение:
//  Строка
Функция ПредставлениеКоличестваПроверенныхПодписей(ВсегоПодписей)
	
	Возврат СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
		НСтр("ru = ';Проверена %1 подпись.;;Проверено %1 подписи.;Проверено %1 подписей.;Проверена %1 подпись.'"),
		ВсегоПодписей);
	
КонецФункции

// "Все подписи действительны."
//  Или "Все подписи недействительны."
//  Или "N подпись(си/сей) действительна(о), M - недействительна(о)."
// 
// Параметры:
//  ВсегоПодписей - Число
//  Действительных - Число
//  Недействительных - Число
// 
// Возвращаемое значение:
//  Строка
Функция ПредставлениеОбщегоРезультатаПроверки(ВсегоПодписей, Действительных, Недействительных)
	
	Если ВсегоПодписей = Действительных Тогда
		
		Возврат НСтр("ru = 'Все подписи действительны.'");
		
	ИначеЕсли ВсегоПодписей = Недействительных Тогда
		
		Возврат НСтр("ru = 'Все подписи недействительны.'");
		
	Иначе
		
		ПредставлениеКоличестваДействительных =
			СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = ';%1 подпись действительна;; %1 подписи действительно; %1 подписей действительно; %1 подпись действительна'"),
				Действительных);
		
		ПредставлениеКоличестваНедействительных =
			СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = ';%1 - недействительна;; %1 - недействительно; %1 - недействительно; %1 - недействительна'"),
				Недействительных);
		
		Возврат СтрШаблон(
			НСтр("ru = '%1, %2.'"),
			ПредставлениеКоличестваДействительных,
			ПредставлениеКоличестваНедействительных);
		
	КонецЕсли;
	
КонецФункции

// "" - у всех подписей все хорошо
//  Или "У действительных подписей нет доверия к сертификатам." - у всех подписей все плохо
//  Или "У N действительной(ных) подписи(сей) нет доверия к сертификатам."
// 
// Параметры:
//  Действительных - Число
//  СПроблемнымиСертификатами - Число
// 
// Возвращаемое значение:
//  Строка
Функция ПредставлениеПодписейСПроблемнымиСертификатами(Действительных, СПроблемнымиСертификатами)
	
	Если СПроблемнымиСертификатами = 0 Тогда
		
		Возврат "";
		
	ИначеЕсли Действительных = СПроблемнымиСертификатами Тогда
		
		Возврат НСтр("ru = 'У действительных подписей нет доверия к сертификатам.'");
		
	Иначе
		
		Возврат СтрШаблон(
			НСтр("ru = 'У %1 нет доверия к сертификатам.'"),
			СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = ';%1 действительной подписи;;%1 действительных подписей;%1 действительных подписей;%1 действительных подписей'"),
				СПроблемнымиСертификатами));
		
	КонецЕсли;
	
КонецФункции

// "Проверены подписи N сообщения(й) ЭДО."
// 
// Параметры:
//  ВсегоПодписей - Число
// 
// Возвращаемое значение:
//  Строка
Функция ПредставлениеКоличестваПроверенныхСообщенийЭДО(ВсегоСообщений)
	
	Возврат СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
		НСтр("ru = ';Проверены подписи %1 сообщения ЭДО.;;Проверены подписи %1 сообщений ЭДО.;Проверены подписи %1 сообщений ЭДО.;Проверены подписи %1 сообщений ЭДО.'"),
		ВсегоСообщений);
	
КонецФункции

// "Подписи всех сообщений действительны"
//  Или "У всех сообщений имеются недействительные подписи"
//  Или "Подписи %1 сообщения действительны, У %1 сообщения имеются недействительные подписи"
// 
// Параметры:
//  ВсегоСообщений - Число
//  Успешных - Число
//  Неуспешных - Число
// 
// Возвращаемое значение:
//  Строка
Функция ПредставлениеОбщегоРезультатаПроверкиЭДО(ВсегоСообщений, Успешных, Неуспешных)
	
	Если ВсегоСообщений = Успешных Тогда
		
		Возврат НСтр("ru = 'Подписи всех сообщений действительны.'");
		
	ИначеЕсли ВсегоСообщений = Неуспешных Тогда
		
		Возврат НСтр("ru = 'У всех сообщений имеются недействительные подписи.'");
		
	Иначе
		
		ПредставлениеКоличестваДействительных =
			СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = '; Подписи %1 сообщения действительны;; Подписи %1 сообщений действительны; Подписи %1 сообщений действительны; Подписи %1 сообщений действительны'"),
				Успешных);
		
		ПредставлениеКоличестваНедействительных =
			СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = '; у %1 сообщения имеются недействительные подписи.;; у %1 сообщений имеются недействительные подписи.; у %1 сообщений имеются недействительные подписи.; у %1 сообщений имеются недействительные подписи.'"),
				Неуспешных);
		
		Возврат СтрШаблон(
			НСтр("ru = '%1, %2.'"),
			ПредставлениеКоличестваДействительных,
			ПредставлениеКоличестваНедействительных);
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

// Показывает текст и вызывает обработчик с заданным результатом.
//
Процедура ВернутьРезультатПослеПоказаПредупреждения(ОбработчикРезультата, ТекстПредупреждения, Результат) Экспорт
	Если ТипЗнч(ОбработчикРезультата) = Тип("ОписаниеОповещения") Тогда
		ПараметрыОбработчика = Новый Структура;
		ПараметрыОбработчика.Вставить("ОбработчикРезультата", ОбработчикРезультата);
		ПараметрыОбработчика.Вставить("Результат",             Результат);
		Обработчик = Новый ОписаниеОповещения("ВернутьРезультатПослеЗакрытияПростогоДиалога", ЭтотОбъект, ПараметрыОбработчика);
		ПоказатьПредупреждение(Обработчик, ТекстПредупреждения);
	Иначе
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
КонецПроцедуры

// Обработчик результата работы процедуры ВернутьРезультатПослеПоказаПредупреждения.
//
Процедура ВернутьРезультатПослеЗакрытияПростогоДиалога(Структура) Экспорт
	ВыполнитьОбработкуОповещения(Структура.ОбработчикРезультата, Структура.Результат);
КонецПроцедуры

// Показывает стандартное предупреждение.
//
// Параметры:
//  ОбработчикРезультата - ОписаниеОповещения, Неопределено - Описание процедуры, принимающей результат работы метода.
//  ПредставлениеКоманды - Строка - Необязательный. Имя команды, для выполнения которой необходимо расширение.
//
Процедура ПоказатьПредупреждениеОНеобходимостиРасширенияРаботыСКриптографией(
	ОбработчикРезультата = Неопределено, ПредставлениеКоманды = "") Экспорт
	
	ТекстПредупреждения = НСтр("ru = 'Для выполнения команды ""%1"" необходимо
	                                 |установить расширение работы с криптографией.'");
	Если ЗначениеЗаполнено(ПредставлениеКоманды) Тогда
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%1", ПредставлениеКоманды);
	Иначе
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, " ""%1""", "");
	КонецЕсли;
	ВернутьРезультатПослеПоказаПредупреждения(ОбработчикРезультата, ТекстПредупреждения, Неопределено);
	
КонецПроцедуры

// Локализация
#Область РаботаСМЧД

Процедура ОбработатьВыборДоверенностиДляУказания(Элемент, Контекст) Экспорт
	
	Если Элемент = Неопределено Тогда
		Если Контекст.ОбработчикЗавершения <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(Контекст.ОбработчикЗавершения, Ложь);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	РаботаСЭПВызовСервера.УстановитьДоверенностьДляЭлектроннойПодписи(Контекст.ИдентификаторПодписи, Элемент.Значение);
	
	Если Контекст.ОбработчикЗавершения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(Контекст.ОбработчикЗавершения, Истина);
	КонецЕсли;
	
	Оповестить("ИзменениеЭП");	
КонецПроцедуры

#КонецОбласти
// Конец Локализация

#Область СохранениеДоверенности

Функция ДанныеДоверенностейДляСохранения(Контекст)
	
	ДанныеДляСохранения = Новый Структура;
	ДанныеДляСохранения.Вставить("Сохранять", Ложь);
	ДанныеДляСохранения.Вставить("ИмяФайла", "");
	ДанныеДляСохранения.Вставить("АдресДанных", "");
	
	ОписаниеПодписи = Контекст.ОписаниеПодписи;
	Если Не ЗначениеЗаполнено(ОписаниеПодписи.ДоверенностьСсылка) Тогда
		Возврат ДанныеДляСохранения;
	КонецЕсли;
	
	Доверенность = ОписаниеПодписи.ДоверенностьСсылка;
	
	Если Не Контекст.Свойство("ВыгруженныеДоверенности") Тогда
		Контекст.Вставить("ВыгруженныеДоверенности", Новый Соответствие);
	КонецЕсли;
	
	Если Контекст.ВыгруженныеДоверенности[Доверенность] <> Неопределено Тогда
		Возврат ДанныеДляСохранения;
	КонецЕсли;
	
	АдресДанных = ОписаниеПодписи.АдресДанныхВыгрузкиДоверенности;
	Если ТипЗнч(АдресДанных) <> Тип("Строка")
		Или Не ЭтоАдресВременногоХранилища(АдресДанных) Тогда
		
		Возврат ДанныеДляСохранения;
	КонецЕсли;
	
	ДанныеДляВыгрузки = ПолучитьИзВременногоХранилища(АдресДанных);
	Если ТипЗнч(ДанныеДляВыгрузки) <> Тип("Структура")
		Или ДанныеДляВыгрузки.Выгружена <> Истина Тогда
		
		Возврат ДанныеДляСохранения;
	КонецЕсли;
	
	ДанныеДляСохранения.Сохранять = Истина;
	ДанныеДляСохранения.ИмяФайла = ДанныеДляВыгрузки.ИмяФайла;
	ДанныеДляСохранения.АдресДанных = ДанныеДляВыгрузки.АдресДанных;
	
	Контекст.ВыгруженныеДоверенности.Вставить(Доверенность, Истина);
	
	Возврат ДанныеДляСохранения;
	
КонецФункции

Процедура СохранитьДоверенностьВместеСЭППослеСохраненияФайла(Результат, Контекст) Экспорт
	
	ЭлектроннаяПодписьСлужебныйКлиент.ПослеСохраненияДоверенностиВместеСПодписью(Контекст);
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаЭПИзПисьма

Процедура УстановитьСоответствиеФайловИЭПЦиклЭПНачало(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.ИндексЭП < 0 Тогда
		ДополнительныеПараметры.ИндексФайла = ДополнительныеПараметры.ИндексФайла - 1;
		УстановитьСоответствиеФайловИЭП(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
		
	Если ДополнительныеПараметры.ИндексФайла <> ДополнительныеПараметры.ИндексЭП Тогда
		СтрокаФайла = ДополнительныеПараметры.СписокФайлов.Получить(ДополнительныеПараметры.ИндексФайла);
		СтрокаЭП = ДополнительныеПараметры.СписокФайлов.Получить(ДополнительныеПараметры.ИндексЭП);
		ОбработчикЗавершения = Новый ОписаниеОповещения(
			"УстановитьСоответствиеФайловИЭППослеПроверкиСтроки",
			ЭтотОбъект,
			ДополнительныеПараметры);
		ДополнительныеПараметры.Вставить("ОбработчикЗавершения", ОбработчикЗавершения);
		ПроверитьЭППослеПодготовкиДанных(СтрокаФайла.АдресДляПроверки, СтрокаЭП.АдресДляПроверки, ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.ИндексЭП = ДополнительныеПараметры.ИндексЭП - 1;
	УстановитьСоответствиеФайловИЭПЦиклЭПНачало(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПроверитьЭППослеПодготовкиДанных(АдресФайла, АдресПодписи, ДополнительныеПараметры)
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ЭлектроннаяПодпись") Тогда
		Возврат;
	КонецЕсли;
	МодульЭлектроннаяПодписьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ЭлектроннаяПодписьКлиент");
	
	ПроверятьЭлектронныеПодписиНаСервере = МодульЭлектроннаяПодписьКлиент.ОбщиеНастройки(
		).ПроверятьЭлектронныеПодписиНаСервере;
	
	Если Не ПроверятьЭлектронныеПодписиНаСервере Тогда
		ДополнительныеПараметры.Вставить("АдресФайла", АдресФайла);
		ДополнительныеПараметры.Вставить("АдресПодписи", АдресПодписи);
		ДополнительныеПараметры.Вставить("ДатаПодписи", ТекущаяДата());
		ДополнительныеПараметры.Вставить("МодульЭлектроннаяПодписьКлиент", МодульЭлектроннаяПодписьКлиент);
		МодульЭлектроннаяПодписьКлиент.СоздатьМенеджерКриптографии(
			Новый ОписаниеОповещения("ПроверитьЭППослеСозданияМенеджераКриптографии",
				ЭтотОбъект, ДополнительныеПараметры),
			"ПроверкаПодписи");
		Возврат;
	КонецЕсли;                 
	
	СтрокаЭП = ДополнительныеПараметры.СписокФайлов.Получить(ДополнительныеПараметры.ИндексЭП);
	
	ДанныеСтрок = Новый Массив;
	
	ДанныеСтроки = ЭлектроннаяПодписьКлиентСервер.НовыеСвойстваПодписи();

	ДанныеСтроки.Вставить("АдресПодписи", АдресПодписи);
	ДанныеСтроки.Вставить("АдресСертификата", СтрокаЭП.АдресСертификата);
	ДанныеСтроки.Вставить("ДатаПодписи", ТекущаяДата());
	ДанныеСтроки.Вставить("ДатаПроверкиПодписи", Дата(1, 1, 1));
	ДанныеСтроки.Вставить("Статус", "");
	ДанныеСтроки.Вставить("ПодписьВерна", Истина);
	ДанныеСтроки.Вставить("СертификатДействителен", Истина);
	ДанныеСтроки.Вставить("ТекстОшибкиПроверкиПодписи", "");
	ДанныеСтроки.Вставить("ТекстОшибкиПроверкиСертификата", "");
	
	ДанныеСтроки.СрокДействияПоследнейМеткиВремени = ТекущаяДата();
	
	ДанныеСтрок.Добавить(ДанныеСтроки);

	Если ЗначениеЗаполнено(СтрокаЭП.АдресСертификата) Тогда
		ФайловыеФункцииСлужебныйВызовСервера.ПроверитьПодписи(АдресФайла, ДанныеСтрок);
	Иначе	                        
		ДанныеСтрок[0].ПодписьВерна = Ложь;
	КонецЕсли;
	
	Если ДанныеСтрок[0].ПодписьВерна Тогда
		
		РезультатПроверки = Новый Структура("ПодписьВерна, СертификатДействителен,
			|ТекстОшибкиПроверкиПодписи, ТекстОшибкиПроверкиСертификата");
		
		ЗаполнитьЗначенияСвойств(РезультатПроверки, ДанныеСтрок[0]);
		ДополнительныеПараметры.Вставить("РезультатПроверки", РезультатПроверки);
		
		Если ДополнительныеПараметры.Свойство("МенеджерКриптографии")
			И ТипЗнч(ДополнительныеПараметры.МенеджерКриптографии) = Тип("МенеджерКриптографии") Тогда
			
			ПеренестиФайлВПодписьПослеСозданияМенеджераКриптографии(
				ДополнительныеПараметры.МенеджерКриптографии,
				ДополнительныеПараметры);
		Иначе
			МодульЭлектроннаяПодписьКлиент.СоздатьМенеджерКриптографии(
				Новый ОписаниеОповещения("ПеренестиФайлВПодписьПослеСозданияМенеджераКриптографии",
					ЭтотОбъект, ДополнительныеПараметры),
				"ПолучениеСертификатов");
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.ИндексЭП = ДополнительныеПараметры.ИндексЭП - 1;
	УстановитьСоответствиеФайловИЭПЦиклЭПНачало(ДополнительныеПараметры);
	
КонецПроцедуры

Функция ПолучитьДвоичныеДанныеФайла(Строка, Форма)
	
	Для каждого Элемент Из Форма.ФайлыДвоичныеДанные Цикл
		Если Элемент.Представление = Строка.Адрес Тогда
			Возврат Элемент.Значение;
		КонецЕсли;
	КонецЦикла;
	ВызватьИсключение НСтр("ru = 'Не найдены двоичные данные файла.'");
	
КонецФункции

Функция ПолучитьСертификатИзПодписи(ДвоичныеДанныеЭП, МенеджерКриптографии)
	
	Попытка
		Сертификаты = МенеджерКриптографии.ПолучитьСертификатыИзПодписи(ДвоичныеДанныеЭП);
	Исключение
		// Если произошло исключение, значит файл не является файлом подписи.
		Возврат Неопределено;
	КонецПопытки;
	
	Если Сертификаты.Количество() = 0 Тогда
		Возврат НСтр("ru = 'В подписи отсутствуют сертификаты.'");
	КонецЕсли;
	
	Возврат Сертификаты[0];
	
КонецФункции

Процедура ПеренестиФайлВПодписьПослеСозданияМенеджераКриптографии(МенеджерКриптографии,
		ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(МенеджерКриптографии) <> Тип("МенеджерКриптографии") Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("МенеджерКриптографии", МенеджерКриптографии);
	
	СтрокаФайла = ДополнительныеПараметры.СписокФайлов.Получить(ДополнительныеПараметры.ИндексФайла);
	СтрокаЭП = ДополнительныеПараметры.СписокФайлов.Получить(ДополнительныеПараметры.ИндексЭП);
	ДвоичныеДанныеЭП = ПолучитьДвоичныеДанныеФайла(СтрокаЭП, ДополнительныеПараметры.Форма);
	Сертификат = ПолучитьСертификатИзПодписи(ДвоичныеДанныеЭП, ДополнительныеПараметры.МенеджерКриптографии);
	
	ПеренестиФайлВПодпись(СтрокаФайла, СтрокаЭП, Сертификат, ДополнительныеПараметры.РезультатПроверки,
		ДополнительныеПараметры.Форма);
	
	ДополнительныеПараметры.ИндексЭП = ДополнительныеПараметры.ИндексЭП - 1;
	УстановитьСоответствиеФайловИЭПЦиклЭПНачало(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПеренестиФайлВПодпись(ФайлыСтрока, ЭПСтрока, Сертификат = Неопределено, РезультатПроверки, Форма)
	
	ПодписьВерна = Ложь;
	СертификатДействителен = Ложь;
	ТекстОшибкиПроверкиПодписи = "";
	ТекстОшибкиПроверкиСертификата = "";
	
	Если ТипЗнч(РезультатПроверки) = Тип("Булево") Тогда
		ПодписьВерна = РезультатПроверки;
		СертификатДействителен = РезультатПроверки
	ИначеЕсли ТипЗнч(РезультатПроверки) = Тип("Структура") Тогда
		
		Если РезультатПроверки.Свойство("ТекстОшибкиПроверкиПодписи")
			И ЗначениеЗаполнено(РезультатПроверки.ТекстОшибкиПроверкиПодписи) Тогда
			
			ПодписьВерна = Ложь;
			ТекстОшибкиПроверкиПодписи = РезультатПроверки.ТекстОшибкиПроверкиПодписи;
			
		Иначе
			ПодписьВерна = Истина;
		КонецЕсли;
		
		Если РезультатПроверки.Свойство("ТекстОшибкиПроверкиСертификата")
			И ЗначениеЗаполнено(РезультатПроверки.ТекстОшибкиПроверкиСертификата) Тогда
			
			СертификатДействителен = Ложь;
			ТекстОшибкиПроверкиСертификата = РезультатПроверки.ТекстОшибкиПроверкиСертификата;
			
		Иначе
			СертификатДействителен = Истина;
		КонецЕсли;
		
	КонецЕсли;    
	
	Если ПодписьВерна Тогда

		ФайлыСтрокаЭП = Форма.ФайлыЭП.Добавить();
		
		ЗаполнитьЗначенияСвойств(ФайлыСтрокаЭП, ЭПСтрока);
		ФайлыСтрокаЭП.ПодписьВерна = ПодписьВерна;
		ФайлыСтрокаЭП.СертификатДействителен = СертификатДействителен;
		ФайлыСтрокаЭП.ТекстОшибкиПроверкиПодписи = ТекстОшибкиПроверкиПодписи;
		ФайлыСтрокаЭП.ТекстОшибкиПроверкиСертификата = ТекстОшибкиПроверкиСертификата;
		ФайлыСтрокаЭП.АдресФайла = ФайлыСтрока.Адрес;
		ФайлыСтрокаЭП.Адрес = ЭПСтрока.АдресДляПроверки;
		
		Если Сертификат <> Неопределено Тогда
			ФайлыСтрокаЭП.Отпечаток = Base64Строка(Сертификат.Отпечаток);
			ФайлыСтрокаЭП.КомуВыданСертификат = ЭлектроннаяПодписьКлиент.ПредставлениеСертификата(Сертификат);
			ФайлыСтрокаЭП.АдресСертификата =
				ПоместитьВоВременноеХранилище(
					Сертификат.Выгрузить(),
					Форма.УникальныйИдентификатор);
		КонецЕсли;           
		
		УдалитьИзФайлыДобавленные(ЭПСтрока, Форма);
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура УдалитьИзФайлыДобавленные(ФайлыСтрока, Форма)
	
	Для Каждого СтрФайл Из Форма.ФайлыДобавленные Цикл  
		
		Если СтрФайл.Адрес = ФайлыСтрока.Адрес
			Или СтрФайл.ПолныйПуть = ФайлыСтрока.Адрес Тогда
			Форма.ФайлыДобавленные.Удалить(СтрФайл); 
			
			Форма.КоличествоФайлов = Форма.ФайлыДобавленные.Количество();
			Возврат;
		КонецЕсли;	   
		
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСУсовершенствованнымиЭП

// Возвращает подписи для усовершенствования
// 
// Параметры:
//  ИдентификаторыПодписей - Массив Из УникальныйИдентификатор
//  РежимУсовершенствования - Строка - см. РаботаСЭПКлиентСервер.РежимыУсовершенствованияПодписей
// 
// Возвращаемое значение:
//  Массив Из см. РаботаСЭПКлиентСервер.НовыеДанныеПодписиДляУсовершенствования
//  
Функция ПодписиКУсовершенствованию(ИдентификаторыПодписей, РежимУсовершенствования)
	
	Режимы = РаботаСЭПКлиентСервер.РежимыУсовершенствованияПодписей();
	
	Если Не Режимы.Свойство(РежимУсовершенствования) Тогда
		ВызватьИсключение НСтр("ru = 'Неизвестный режим усовершенствования подписей'");
	КонецЕсли;
	
	ДанныеПодписей = РаботаСЭПВызовСервера.ДанныеПодписейДляУсовершенствованияПоИдентификаторам(ИдентификаторыПодписей);
	
	ДанныеПодписейПоТипам = ПодписиПоТипамДляУсовершенствования(ДанныеПодписей);
	
	НенайденныеПодписи = ДанныеПодписейПоТипам[ТипНенайденныхПодписей()];
	Если ТипЗнч(НенайденныеПодписи) = Тип("Соответствие") И НенайденныеПодписи.Количество() > 0 Тогда
		
		ИдентификаторыНенайденныхПодписей = Новый Массив(); // Массив Из УникальныйИдентификатор
		Для Каждого Элемент Из НенайденныеПодписи Цикл
			ИдентификаторПодписи = Элемент.Ключ;
			ИдентификаторыНенайденныхПодписей.Добавить(ИдентификаторПодписи);
		КонецЦикла;
		
		ШаблонСообщения = НСтр("ru = 'Не найдены подписи по идентификаторам для установки метки времени (%1)'");
		Если РежимУсовершенствования = Режимы.АрхивированиеПодписей Тогда
			ШаблонСообщения = НСтр("ru = 'Не найдены подписи по идентификаторам для архивирования (%1)'");
		КонецЕсли;
		
		Сообщение = СтрШаблон(ШаблонСообщения, СтрСоединить(ИдентификаторыНенайденныхПодписей, ", "));
		
		ВызватьИсключение Сообщение;
		
	КонецЕсли;
	
	ТипыПодписей = РаботаСЭПКлиентСервер.ТипыПодписейДляДобавленияМеткиВремени();
	Если РежимУсовершенствования = Режимы.АрхивированиеПодписей Тогда
		ТипыПодписей = РаботаСЭПКлиентСервер.ТипыПодписейДляАрхивирования();
	КонецЕсли;
	
	Возврат ПодписиВыбранныхТиповИзДанныхПодписейПоТипам(ДанныеПодписейПоТипам, ТипыПодписей);
	
КонецФункции

// Сортирует подписи по типам для усовершенствования
// 
// Параметры:
//  ДанныеПодписей - см. РаботаСЭП.ДанныеПодписейДляУсовершенствованияПоИдентификаторам
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//    * Ключ - ПеречислениеСсылка.ТипыПодписиКриптографии, Строка -
//    * Значение - см. РаботаСЭП.ДанныеПодписейДляУсовершенствованияПоИдентификаторам
Функция ПодписиПоТипамДляУсовершенствования(ДанныеПодписей)
	
	ПодписиПоТипам = Новый Соответствие();
	
	Для Каждого Элемент Из ДанныеПодписей Цикл
		
		ИдентификаторПодписи = Элемент.Ключ;
		ДанныеПодписи = Элемент.Значение;
		
		Если Не ЗначениеЗаполнено(ДанныеПодписи.ПодписанныйОбъект)
			Или Не ЗначениеЗаполнено(ДанныеПодписи.ПорядковыйНомер) Тогда
			
			ТипПодписи = ТипНенайденныхПодписей();
		Иначе
			ТипПодписи = ДанныеПодписи.ТипПодписи;
		КонецЕсли;
		
		ПодписиПоТипу = ПодписиПоТипам[ТипПодписи]; // см. РаботаСЭП.ДанныеПодписейДляУсовершенствованияПоИдентификаторам
		Если ПодписиПоТипу = Неопределено Тогда
			ПодписиПоТипу = Новый Соответствие();
			ПодписиПоТипам[ТипПодписи] = ПодписиПоТипу;
		КонецЕсли;
		
		ПодписиПоТипу[ИдентификаторПодписи] = ДанныеПодписи;
		
	КонецЦикла;
	
	Возврат ПодписиПоТипам;
	
КонецФункции

// Строковая константа означающая тип ненайденных подписей
// 
// Возвращаемое значение:
//  Строка
Функция ТипНенайденныхПодписей()
	
	Возврат "НеНайдена";
	
КонецФункции

// Конструктор данных подписи для усовершенствования
// 
// Возвращаемое значение:
//  Структура:
//    * ПодписанныйОбъект - ОпределяемыйТип.ПодписанныйОбъект
//    * ПорядковыйНомер - Число
//
Функция НовыеДанныеПодписиДляУсовершенствования()
	
	ДанныеПодписи = Новый Структура;
	ДанныеПодписи.Вставить("ПодписанныйОбъект", ПредопределенноеЗначение("Справочник.Файлы.ПустаяСсылка"));
	ДанныеПодписи.Вставить("ПорядковыйНомер", 0);
	
	Возврат ДанныеПодписи;
	
КонецФункции

// Возвращает подписи выбранных типов
// 
// Параметры:
//  ДанныеПодписейПоТипам - см. ПодписиПоТипамДляУсовершенствования
//  ВыбранныеТипыПодписей - Массив Из ПеречислениеСсылка.ТипыПодписиКриптографии
// 
// Возвращаемое значение:
//  Массив Из см. РаботаСЭПКлиентСервер.НовыеДанныеПодписиДляУсовершенствования
Функция ПодписиВыбранныхТиповИзДанныхПодписейПоТипам(ДанныеПодписейПоТипам, ВыбранныеТипыПодписей)
	
	ПодписиВыбранныхТипов = Новый Массив(); // Массив Из см. РаботаСЭПКлиентСервер.НовыеДанныеПодписиДляУсовершенствования
	
	Для Каждого ТипПодписи Из ВыбранныеТипыПодписей Цикл
		
		ПодписиПоТипу = ДанныеПодписейПоТипам[ТипПодписи];
		Если ТипЗнч(ПодписиПоТипу) <> Тип("Соответствие") Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого Элемент Из ПодписиПоТипу Цикл
			ДанныеПодписи = Элемент.Значение;
			ПодписиВыбранныхТипов.Добавить(ДанныеПодписи);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ПодписиВыбранныхТипов;
	
КонецФункции

// Конструктор контекста добавление меток времени
// 
// Возвращаемое значение:
//  Структура:
// * ОбработчикЗавершения - Неопределено, ОписаниеОповещения - 
// * ИдентификаторыВыбранныхПодписей - Массив Из УникальныйИдентификатор
// * ДанныеПодписейКУсовершенствованию - Массив Из см. РаботаСЭПКлиентСервер.НовыеДанныеПодписиДляУсовершенствования
// * РежимУсовершенствования - Строка - см. РаботаСЭПКлиентСервер.РежимыУсовершенствованияПодписей
// * Форма - ФормаКлиентскогоПриложения
//
Функция НовыйКонтекстУсовершенствованияПодписей()
	
	РежимыУсовершенствования = РаботаСЭПКлиентСервер.РежимыУсовершенствованияПодписей();
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОбработчикЗавершения", Неопределено);
	Контекст.Вставить("ИдентификаторыВыбранныхПодписей", Новый Массив());
	Контекст.Вставить("ДанныеПодписейКУсовершенствованию", Новый Массив());
	Контекст.Вставить("РежимУсовершенствования", РежимыУсовершенствования.ДобавлениеМетокВремени);
	Контекст.Вставить("Форма", Неопределено);
	
	Возврат Контекст;
	
КонецФункции

// Выполняет асинхронное усовершенствование подписей
// 
// Параметры:
//  Контекст - см. НовыйКонтекстУсовершенствованияПодписей
Асинх Процедура УсовершенствоватьПодписиВыполнение(Контекст)
	
	ОжиданиеРезультатаВопроса = ЗадатьВопросОУсовершенствованииЧастиПодписей(Контекст);
	Результат = Ждать ОжиданиеРезультатаВопроса;
	
	Если Результат <> РезультатыВопросовПоУсовершенствованию().Продолжить Тогда
		Возврат;
	КонецЕсли;
	
	ОжиданиеРезультатаВопроса = ЗадатьВопросОАрхивированииПодписейПоОбъектамВРаботе(Контекст);
	Результат = Ждать ОжиданиеРезультатаВопроса;
	
	Если Результат <> РезультатыВопросовПоУсовершенствованию().Продолжить Тогда
		Возврат;
	КонецЕсли;
	
	ПодписиКУсовершенствованию = Новый Массив(); // Массив из см. НовыеДанныеПодписиДляУсовершенствования
	Для Каждого ДанныеПодписи Из Контекст.ДанныеПодписейКУсовершенствованию Цикл
		ДанныеПодписиДляБСП = НовыеДанныеПодписиДляУсовершенствования();
		ЗаполнитьЗначенияСвойств(ДанныеПодписиДляБСП, ДанныеПодписи);
		ПодписиКУсовершенствованию.Добавить(ДанныеПодписиДляБСП);
	КонецЦикла;
	
	Режим = Контекст.РежимУсовершенствования;
	Режимы = РаботаСЭПКлиентСервер.РежимыУсовершенствованияПодписей();
	
	ТипПодписи = ПредопределенноеЗначение("Перечисление.ТипыПодписиКриптографии.СМеткойДоверенногоВремениCAdEST");
	ДобавитьАрхивнуюМеткуВремени = Ложь;
	Если Режим = Режимы.АрхивированиеПодписей Тогда
		ТипПодписи = ПредопределенноеЗначение("Перечисление.ТипыПодписиКриптографии.АрхивнаяCAdESAv3");
		ДобавитьАрхивнуюМеткуВремени = Истина;
	КонецЕсли;
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("Подпись", ПодписиКУсовершенствованию);
	ОписаниеДанных.Вставить("ТипПодписи", ТипПодписи);
	ОписаниеДанных.Вставить("ДобавитьАрхивнуюМеткуВремени", ДобавитьАрхивнуюМеткуВремени);
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьУсовершенствованиеПодписей", ЭтотОбъект, Контекст);
	ЭлектроннаяПодписьКлиент.УсовершенствоватьПодпись(ОписаниеДанных, Контекст.Форма, Оповещение, Ложь);
	
КонецПроцедуры

// Перечисления результатов задания вопросов по усовершенствованию.
// 
// Возвращаемое значение:
//  Структура:
// * Продолжить - Строка
// * Остановить - Строка 
Функция РезультатыВопросовПоУсовершенствованию()
	
	Результаты = Новый Структура;
	Результаты.Вставить("Продолжить", "Продолжить");
	Результаты.Вставить("Остановить", "Остановить");
	
	Возврат Результаты;
	
КонецФункции

// Задает вопрос о усовершенствовании части подписей
// 
// Параметры:
//  Контекст - см. НовыйКонтекстУсовершенствованияПодписей
// 
// Возвращаемое значение:
//  Строка - см. РезультатыВопросовПоУсовершенствованию
Асинх Функция ЗадатьВопросОУсовершенствованииЧастиПодписей(Контекст)
	
	Режим = Контекст.РежимУсовершенствования;
	Режимы = РаботаСЭПКлиентСервер.РежимыУсовершенствованияПодписей();
	
	ПодписиКУсовершенствованию = Контекст.ДанныеПодписейКУсовершенствованию;
	ИдентификаторыПодписей = Контекст.ИдентификаторыВыбранныхПодписей;
	
	Если ПодписиКУсовершенствованию.Количество() = 0 Тогда
		
		ТекстПредупреждения = НСтр("ru = 'Среди выбранных подписей нет тех, которым можно добавить метку времени.'");
		Если Режим = Режимы.АрхивированиеПодписей Тогда
			ТекстПредупреждения = НСтр("ru = 'Среди выбранных подписей нет тех, которые можно архивировать.'");
		КонецЕсли;
		
		ОжиданиеЗакрытия = ПредупреждениеАсинх(ТекстПредупреждения);
		Ждать ОжиданиеЗакрытия;
		
		Возврат РезультатыВопросовПоУсовершенствованию().Остановить;
		
	ИначеЕсли ПодписиКУсовершенствованию.Количество() < ИдентификаторыПодписей.Количество() Тогда
		
		ТекстВопроса = СтрШаблон(НСтр("ru = 'Из %1 только к %2 можно добавить метку времени. Продолжить?'"),
			СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = ';%1 выбранной подписи;;%1 выбранных подписей;%1 выбранных подписей;%1 выбранных подписей'"),
				ИдентификаторыПодписей.Количество()),
			СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = ';%1 подписи;;%1 подписям;%1 подписей;%1 подписей'"),
				ПодписиКУсовершенствованию.Количество()));
		
		Если Режим = Режимы.АрхивированиеПодписей Тогда
			ТекстВопроса = СтрШаблон(НСтр("ru = 'Из %1 только %2 можно архивировать. Продолжить?'"),
				СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
					НСтр("ru = ';%1 выбранной подписи;;%1 выбранных подписей;%1 выбранных подписей;%1 выбранных подписей'"),
					ИдентификаторыПодписей.Количество()),
				СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
					НСтр("ru = ';%1 подпись;;%1 подписи;%1 подписей;%1 подписей'"),
					ПодписиКУсовершенствованию.Количество()));
		КонецЕсли;
		
		ОжиданиеОтвета = ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Ответ = Ждать ОжиданиеОтвета;
		
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат РезультатыВопросовПоУсовершенствованию().Остановить;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РезультатыВопросовПоУсовершенствованию().Продолжить;
	
КонецФункции

// Задает вопрос о архивировании подписей по объектам, по которым ведется оперативная работа
// 
// Параметры:
//  Контекст - см. НовыйКонтекстУсовершенствованияПодписей
// 
// Возвращаемое значение:
//  Строка - см. РезультатыВопросовПоУсовершенствованию
Асинх Функция ЗадатьВопросОАрхивированииПодписейПоОбъектамВРаботе(Контекст)
	
	Режим = Контекст.РежимУсовершенствования;
	Режимы = РаботаСЭПКлиентСервер.РежимыУсовершенствованияПодписей();
	
	Если Режим <> Режимы.АрхивированиеПодписей Тогда
		Возврат РезультатыВопросовПоУсовершенствованию().Продолжить;
	КонецЕсли;
	
	ДанныеПодписейКУсовершенствованию = Контекст.ДанныеПодписейКУсовершенствованию;
	
	СтатусыРабот = РаботаСЭПКлиентСервер.СтатусыРаботыСПодписаннымиОбъектами();
	ВсеРаботыЗавершены = Истина;
	Для Каждого ДанныеПодписи Из ДанныеПодписейКУсовершенствованию Цикл
		Если ДанныеПодписи.СтатусРаботы <> СтатусыРабот.РаботаЗавершена Тогда
			ВсеРаботыЗавершены = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ВсеРаботыЗавершены Тогда
		Возврат РезультатыВопросовПоУсовершенствованию().Продолжить;
	КонецЕсли;
	
	ТекстВопроса =
		НСтр("ru = 'Добавление архивной метки времени зафиксирует результат проверки подписи. Это может вызвать проблемы при отзыве сертификата. Выполнение данной операции не рекомендуется, пока ведется оперативная работа по документу. Добавить архивную метку времени?'");
	ОжиданиеОтвета = ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Ответ = Ждать ОжиданиеОтвета;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Возврат РезультатыВопросовПоУсовершенствованию().Продолжить;
	Иначе
		Возврат РезультатыВопросовПоУсовершенствованию().Остановить;
	КонецЕсли;
	
КонецФункции

// Обрабатывает результат усовершенствования подписей БСП
// 
// Параметры:
//  Результат - Структура:
//     * Успех - Булево - Истина, если все прошло успешно. Если Успех = Ложь, то частичное завершение
//               определяется по наличию свойства СвойстваПодписи. Если есть, то шаг выполнен.
//     * ТекстОшибки - Строка
//     * СвойстваПодписей - Массив из Структура:
//                          ** Подпись - ДвоичныеДанные - Подпись, которую надо усовершенствовать.
//                                     - Строка - адрес во временном хранилище.
//                          ** ПодписанныйОбъект - ЛюбаяСсылка - если есть.
//                          ** ПорядковыйНомер - Число - если есть.
//                          ** ТипПодписи - ПеречислениеСсылка.ТипыПодписиКриптографии - тип подписи,
//                                если подпись не удалось усовершенствовать, но удалось определить тип.
//                          ** СрокДействияПоследнейМеткиВремени - Дата - срок действия,
//                                если подпись не удалось усовершенствовать, но удалось определить срок действия.
//                          ** СвойстваПодписи - см. ЭлектроннаяПодписьКлиентСервер.НовыеСвойстваПодписи -
//                             свойства усовершенствованной подписи (заполнены только изменившиеся).
//                          ** Ошибка - Строка - текст ошибки при попытке усовершенствования. Если произошла ошибка,
//                           то структура не будет содержать свойств подписи, которую не удалось усовершенствовать.
//                        - Строка - адрес временного хранилища, содержащего массив указанный выше.
//  Контекст - см. НовыйКонтекстУсовершенствованияПодписей
Процедура ОбработатьУсовершенствованиеПодписей(Результат, Контекст) Экспорт
	
	Оповестить("Запись_Подпись");
	
	Если Контекст.ОбработчикЗавершения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(Контекст.ОбработчикЗавершения, Результат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
