///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ОбщиеПроцедурыИФункции

// Обрабатывает адрес веб-сервиса, добавляет "/" в конце, и удаляет региональные настройки, при необходимости.
//
// Параметры:
//   АдресВебСервиса - Строка - адрес веб-сервиса.
//
Процедура ОбработатьАдресВебСервисаИнтегрированнойСистемы(АдресВебСервиса) Экспорт
	
	// Адрес должен заканчиваться на "/".
	Если ЗначениеЗаполнено(АдресВебСервиса)
			И Прав(АдресВебСервиса, 1) <> "/"
			И Прав(АдресВебСервиса, 1) <> "\" Тогда
		АдресВебСервиса = АдресВебСервиса + "/";
	КонецЕсли;
	
	// Удалим региональные настройки (например "/ru_RU/") из адреса.
	Если Прав(АдресВебСервиса, 4) = "/ru/" Или Прав(АдресВебСервиса, 4) = "/en/" Тогда
		АдресВебСервиса = Лев(АдресВебСервиса, СтрДлина(АдресВебСервиса) - 3);
	КонецЕсли;
	РегиональныеНастройки = Прав(АдресВебСервиса, 7);
	Если Лев(РегиональныеНастройки, 1) = "/" И Прав(РегиональныеНастройки, 1) = "/" Тогда
		РегиональныеНастройки = СтрЗаменить(РегиональныеНастройки, "/", "");
		МассивДляПроверки = СтрРазделить(РегиональныеНастройки, "_");
		Если МассивДляПроверки.Количество() = 2
				И СтрДлина(МассивДляПроверки[0]) = 2
				И НРег(МассивДляПроверки[0]) = МассивДляПроверки[0]
				И СтрДлина(МассивДляПроверки[1]) = 2
				И ВРег(МассивДляПроверки[1]) = МассивДляПроверки[1] Тогда
			АдресВебСервиса = Лев(АдресВебСервиса, СтрДлина(АдресВебСервиса) - 6);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти