////////////////////////////////////////////////////////////////////////////////
// Документооборот права доступа (сервер, повторное использование)
//
////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает Истина, если включено использование прав доступа
Функция ВключеноИспользованиеПравДоступа() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Константы.ДокументооборотИспользоватьОграничениеПравДоступа.Получить();
	
КонецФункции

Функция РазмерПорцииДескрипторовДляРасширенияПравСоставомСубъектов() Экспорт
	
	РазмерПорции = 500;
	
	Возврат РазмерПорции;
	
КонецФункции

// Возвращает состав рабочей группы. Если указано значение Неопределено, то 
// возвращаются все пользователи.
//
Функция ПользователиРабочейГруппы(РабочаяГруппа) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если РабочаяГруппа = ПредопределенноеЗначение("Справочник.РабочиеГруппы.ВсеПользователи") 
		Или РабочаяГруппа = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Пользователи.Ссылка КАК Пользователь
			|ИЗ
			|	Справочник.Пользователи КАК Пользователи
			|ГДЕ
			|	Пользователи.ПометкаУдаления = ЛОЖЬ";
			
		Возврат Запрос.Выполнить().Выгрузить();
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Пользователи.Ссылка КАК Пользователь
			|ИЗ
			|	Справочник.РабочиеГруппы.Состав КАК РабочиеГруппыСостав
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
			|		ПО РабочиеГруппыСостав.Участник = Сотрудники.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|		ПО Сотрудники.Ссылка = СотрудникиПользователей.Сотрудник
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
			|		ПО СотрудникиПользователей.Пользователь = Пользователи.Ссылка
			|ГДЕ
			|	РабочиеГруппыСостав.Ссылка В ИЕРАРХИИ(&Ссылка)
			|	И НЕ РабочиеГруппыСостав.Ссылка.ПометкаУдаления
			|	И НЕ Пользователи.ПометкаУдаления
			|	И НЕ Сотрудники.ПометкаУдаления
			|	И Сотрудники.Действует";
			
		Запрос.УстановитьПараметр("Ссылка", РабочаяГруппа.Ссылка);

		Возврат Запрос.Выполнить().Выгрузить();

	КонецЕсли;
	
КонецФункции

// Функция возвращает Истина, если для указанного вида доступа
// в этой группе установлено "Разрешены все, кроме указанных",
// Ложь, если установлено "Запрещены все, кроме указанных"
// и Неопределено в противном случае
Функция ВидДоступаРазрешен(ГруппаДоступа, ВидДоступа) Экспорт
	
	// Перебор всех видов доступа
	Для Каждого ЭлементДоступа Из ГруппаДоступа.ВидыДоступа Цикл
		
		Если ЭлементДоступа.ВидДоступа = ВидДоступа Тогда
			Возврат ЭлементДоступа.ДоступРазрешен;
		КонецЕсли	
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции	

// Возвращает соответствие типов папок и метаданных хранящихся в них объектах. 
// Ключ - тип папки,
// Значение - массив метаданных хранящихся в ней объектов
//
Функция ТипыПапокИСодержимого() Экспорт
	
	Результат = Новый Соответствие;
	
	// Документы предприятия
	МассивМетаданныхСодержимого = Новый Массив;
	МассивМетаданныхСодержимого.Добавить(Метаданные.Справочники.ДокументыПредприятия);
	Результат.Вставить(Тип("СправочникСсылка.ПапкиДокументов"), МассивМетаданныхСодержимого);
	
	// Файлы
	МассивМетаданныхСодержимого = Новый Массив;
	МассивМетаданныхСодержимого.Добавить(Метаданные.Справочники.Файлы);
	Результат.Вставить(Тип("СправочникСсылка.ПапкиФайлов"), МассивМетаданныхСодержимого);
	
	ДокументооборотПраваДоступаПереопределяемый.ДополнитьТипыПапокИСодержимого(Результат);
	
	Возврат Результат;
	
КонецФункции

// Текст запроса, который возвращает выборку с правами по дескрипторам
// для объектов, которые не имеют собственного алгоритма расчета прав.
// 
// Параметры:
//  
//  УчитыватьНастройкиПравПапок - Булево - Учитывать настройки прав папки, соответствующие переданным дескрипторам.
//  УчитыватьПолитикиДоступа - Булево - Учитывать разрешения политик доступа
//  
// Возвращаемое значение:
//	Строка - текст запроса.
Функция ТекстЗапросаДляСтандартногоРасчетаПрав(
			УчитыватьНастройкиПравПапок,
			УчитыватьПолитикиДоступа) Экспорт
	
	Если Не УчитыватьПолитикиДоступа И Не УчитыватьНастройкиПравПапок Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
		"// 0 /////////////////
		|ВЫБРАТЬ
		|	ТаблицаДескрипторов.Дескриптор,
		|	ТаблицаДескрипторов.ВидДоступа,
		|	ТаблицаДескрипторов.ЗначениеДоступа
		|ПОМЕСТИТЬ Дескрипторы
		|ИЗ
		|	&ТаблицаДескрипторов КАК ТаблицаДескрипторов
		|;
		|
		|// 1 //////////////////
		|ВЫБРАТЬ
		|	СотрудникиВКонтейнерах.Сотрудник КАК Сотрудник,
		|	ИСТИНА КАК Чтение,
		|	МАКСИМУМ(ПраваРолей.Добавление) КАК Добавление,
		|	МАКСИМУМ(ПраваРолей.Изменение) КАК Изменение,
		|	МАКСИМУМ(ПраваРолей.Изменение) КАК Удаление,
		|	МАКСИМУМ(ПраваРолей.ЧтениеБезОграничения) КАК ЧтениеБезОграничения,
		|	МАКСИМУМ(ПраваРолей.ДобавлениеБезОграничения) КАК ДобавлениеБезОграничения,
		|	МАКСИМУМ(ПраваРолей.ИзменениеБезОграничения) КАК ИзменениеБезОграничения
		|ПОМЕСТИТЬ ПраваПоРолям
		|ИЗ
		|	РегистрСведений.ПолномочияСотрудников КАК ПолномочияСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.Роли КАК ПрофилиГруппДоступаРоли
		|		ПО ПолномочияСотрудников.Полномочия = ПрофилиГруппДоступаРоли.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваРолей КАК ПраваРолей
		|		ПО ПрофилиГруппДоступаРоли.Роль = ПраваРолей.Роль
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
		|		ПО ПолномочияСотрудников.Владелец = СотрудникиВКонтейнерах.Контейнер
		|ГДЕ
		|	ПраваРолей.ОбъектМетаданных = &Идентификатор
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудникиВКонтейнерах.Сотрудник
		|;
		|
		|// 2 //////////////////
		|ВЫБРАТЬ
		|	Дескрипторы.Дескриптор КАК Дескриптор,
		|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Дескрипторы1.ЗначениеДоступа), 0) КАК КолРазрезов
		|ПОМЕСТИТЬ КолРазрезовДескрипторов
		|ИЗ
		|	Дескрипторы КАК Дескрипторы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Дескрипторы КАК Дескрипторы1
		|		ПО Дескрипторы.Дескриптор = Дескрипторы1.Дескриптор
		|			И (Дескрипторы1.ЗначениеДоступа <> НЕОПРЕДЕЛЕНО)
		|
		|СГРУППИРОВАТЬ ПО
		|	Дескрипторы.Дескриптор
		|;
		|
		|// 3 //////////////////////
		|ВЫБРАТЬ
		|	КолРазрезовДескрипторов.Дескриптор КАК Дескриптор,
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	НЕОПРЕДЕЛЕНО КАК Коллекция,
		|	0 КАК КолРазрезовДоступа,
		|	ИСТИНА КАК Чтение,
		|	ИСТИНА КАК Изменение,
		|	ИСТИНА КАК Добавление,
		|	ИСТИНА КАК Удаление
		|
		|ПОМЕСТИТЬ РазрешенияДоступа
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники,
		|	КолРазрезовДескрипторов КАК КолРазрезовДескрипторов
		|ГДЕ
		|	КолРазрезовДескрипторов.КолРазрезов = 0
		|	И НЕ Сотрудники.ПометкаУдаления
		|;
		|
		|// 4 ////////////////
		|ВЫБРАТЬ
		|	РазрешенияДоступа.Дескриптор,
		|	РазрешенияДоступа.Сотрудник,
		|	МАКСИМУМ(РазрешенияДоступа.Чтение) КАК Чтение,
		|	МАКСИМУМ(РазрешенияДоступа.Изменение) КАК Изменение,
		|	МАКСИМУМ(РазрешенияДоступа.Добавление) КАК Добавление,
		|	МАКСИМУМ(РазрешенияДоступа.Удаление) КАК Удаление
		|ПОМЕСТИТЬ ПраваПоРазрешениямДоступа
		|ИЗ
		|	РазрешенияДоступа КАК РазрешенияДоступа
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КолРазрезовДескрипторов КАК КолРазрезовДескрипторов
		|		ПО РазрешенияДоступа.Дескриптор = КолРазрезовДескрипторов.Дескриптор
		|			И РазрешенияДоступа.КолРазрезовДоступа = КолРазрезовДескрипторов.КолРазрезов
		|
		|СГРУППИРОВАТЬ ПО
		|	РазрешенияДоступа.Дескриптор,
		|	РазрешенияДоступа.Сотрудник
		|;
		|
		|// 5 //////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Дескриптор,
		|	ВложенныйЗапрос.Сотрудник,
		|	МАКСИМУМ(ВложенныйЗапрос.Чтение) КАК Чтение,
		|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Изменение,
		|	МАКСИМУМ(ВложенныйЗапрос.Добавление) КАК Добавление,
		|	МАКСИМУМ(ВложенныйЗапрос.Удаление) КАК Удаление,
		|	МАКСИМУМ(ВложенныйЗапрос.УправлениеПравами) КАК УправлениеПравами
		|ПОМЕСТИТЬ ПраваПоРолямИРазрешениям
		|ИЗ
		|(
		|	ВЫБРАТЬ
		|		ПраваПоРазрешениямДоступа.Дескриптор КАК Дескриптор,
		|		ПраваПоРазрешениямДоступа.Сотрудник КАК Сотрудник,
		|		ПраваПоРазрешениямДоступа.Чтение КАК Чтение,
		|		ПраваПоРазрешениямДоступа.Изменение
		|			И ПраваПоРолям.Изменение КАК Изменение,
		|		ПраваПоРазрешениямДоступа.Добавление
		|			И ПраваПоРолям.Добавление КАК Добавление,
		|		ПраваПоРазрешениямДоступа.Удаление
		|			И ПраваПоРолям.Удаление КАК Удаление,
		|		ЛОЖЬ КАК УправлениеПравами
		|	ИЗ
		|		ПраваПоРолям КАК ПраваПоРолям
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПраваПоРазрешениямДоступа КАК ПраваПоРазрешениямДоступа
		|			ПО ПраваПоРолям.Сотрудник = ПраваПоРазрешениямДоступа.Сотрудник
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Дескрипторы.Дескриптор,
		|		ПраваПоРолям.Сотрудник,
		|		ПраваПоРолям.ЧтениеБезОграничения,
		|		ПраваПоРолям.ИзменениеБезОграничения,
		|		ПраваПоРолям.ИзменениеБезОграничения,
		|		ПраваПоРолям.ИзменениеБезОграничения,
		|		ЛОЖЬ
		|	ИЗ
		|		Дескрипторы КАК Дескрипторы,
		|		ПраваПоРолям КАК ПраваПоРолям
		|	ГДЕ
		|		ПраваПоРолям.ЧтениеБезОграничения
		|) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Дескриптор,
		|	ВложенныйЗапрос.Сотрудник
		|;
		|
		|// 6 ///////////////
		|ВЫБРАТЬ
		|	ПравилаОбработкиНастроекПапки.Настройка КАК Настройка,
		|	ПравилаОбработкиНастроекПапки.Чтение,
		|	ПравилаОбработкиНастроекПапки.Добавление,
		|	ПравилаОбработкиНастроекПапки.Изменение,
		|	ПравилаОбработкиНастроекПапки.Удаление,
		|	ПравилаОбработкиНастроекПапки.УправлениеПравами
		|ПОМЕСТИТЬ ПравилаОбработкиНастроекПапки
		|ИЗ
		|	&ПравилаОбработкиНастроекПапки КАК ПравилаОбработкиНастроекПапки
		|;
		|
		|// 7 ///////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Дескриптор,
		|	ВложенныйЗапрос.Сотрудник,
		|	ВложенныйЗапрос.Право,
		|	МАКСИМУМ(ВложенныйЗапрос.ПравоЗапрещено) КАК ПравоЗапрещено
		|ПОМЕСТИТЬ НастройкиПапок
		|ИЗ
		|(
		|	ВЫБРАТЬ
		|		ДескрипторыДоступаОбъектов.Ссылка КАК Дескриптор,
		|		СотрудникиВКонтейнерах.Сотрудник КАК Сотрудник,
		|		ДескрипторыВладельцевНастройкаПрав.Право КАК Право,
		|		ДескрипторыВладельцевНастройкаПрав.ПравоЗапрещено КАК ПравоЗапрещено
		|	ИЗ
		|		Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|			ПО ДескрипторыДоступаОбъектов.Папка = ДескрипторыДляОбъектов.Объект
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДескрипторыДоступаОбъектов.НастройкаПрав КАК ДескрипторыВладельцевНастройкаПрав
		|			ПО ДескрипторыДляОбъектов.Дескриптор = ДескрипторыВладельцевНастройкаПрав.Ссылка
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
		|			ПО ДескрипторыВладельцевНастройкаПрав.Пользователь = СотрудникиВКонтейнерах.Контейнер
		|	ГДЕ
		|		ДескрипторыДоступаОбъектов.Ссылка В(&Дескрипторы)
		|		И ДескрипторыВладельцевНастройкаПрав.НаследованиеРазрешено
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДескрипторыДоступаОбъектов.Ссылка,
		|		СотрудникиВКонтейнерах.Сотрудник,
		|		ДескрипторыВладельцевНастройкаПрав.Право,
		|		ДескрипторыВладельцевНастройкаПрав.ПравоЗапрещено
		|	ИЗ
		|		Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|			ПО ДескрипторыДоступаОбъектов.Папка = ДескрипторыДляОбъектов.Объект
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДескрипторыДоступаОбъектов.НастройкаПрав КАК ДескрипторыВладельцевНастройкаПрав
		|			ПО ДескрипторыДляОбъектов.Дескриптор = ДескрипторыВладельцевНастройкаПрав.Ссылка
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
		|			ПО ДескрипторыВладельцевНастройкаПрав.Пользователь = СотрудникиВКонтейнерах.Контейнер
		|	ГДЕ
		|		ДескрипторыДоступаОбъектов.Ссылка В(&Дескрипторы)
		|		И НЕ ДескрипторыВладельцевНастройкаПрав.НаследованиеРазрешено
		|		И ДескрипторыДоступаОбъектов.ПапкаПередаетВсеПрава
		|) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Дескриптор,
		|	ВложенныйЗапрос.Сотрудник,
		|	ВложенныйЗапрос.Право
		|;
		|
		|// 8 /////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВложенныйЗапрос.Дескриптор,
		|	ВложенныйЗапрос.Сотрудник,
		|	МАКСИМУМ(ВложенныйЗапрос.Чтение) КАК Чтение,
		|	МАКСИМУМ(ВложенныйЗапрос.Добавление) КАК Добавление,
		|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Изменение,
		|	МАКСИМУМ(ВложенныйЗапрос.Удаление) КАК Удаление,
		|	МАКСИМУМ(ВложенныйЗапрос.УправлениеПравами) КАК УправлениеПравами
		|ПОМЕСТИТЬ ПраваОтПапок
		|ИЗ
		|(
		|	ВЫБРАТЬ
		|		НастройкиПапок.Дескриптор КАК Дескриптор,
		|		НастройкиПапок.Сотрудник КАК Сотрудник,
		|		ПравилаОбработкиНастроекПапки.Чтение
		|			И НЕ НастройкиПапок.ПравоЗапрещено КАК Чтение,
		|		ПравилаОбработкиНастроекПапки.Добавление
		|			И НЕ НастройкиПапок.ПравоЗапрещено КАК Добавление,
		|		ПравилаОбработкиНастроекПапки.Изменение
		|			И НЕ НастройкиПапок.ПравоЗапрещено КАК Изменение,
		|		ПравилаОбработкиНастроекПапки.Удаление
		|			И НЕ НастройкиПапок.ПравоЗапрещено КАК Удаление,
		|		ПравилаОбработкиНастроекПапки.УправлениеПравами
		|			И НЕ НастройкиПапок.ПравоЗапрещено КАК УправлениеПравами
		|	ИЗ
		|		НастройкиПапок КАК НастройкиПапок
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПравилаОбработкиНастроекПапки КАК ПравилаОбработкиНастроекПапки
		|			ПО НастройкиПапок.Право = ПравилаОбработкиНастроекПапки.Настройка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДескрипторыДоступаОбъектов.Ссылка,
		|		СотрудникиСпр.Ссылка,
		|		ИСТИНА,
		|		ИСТИНА,
		|		ИСТИНА,
		|		ИСТИНА,
		|		ЛОЖЬ
		|	ИЗ
		|		Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДескрипторыДоступаОбъектов.НастройкаПрав КАК ДескрипторыДоступаОбъектовНастройкаПрав
		|			ПО ДескрипторыДоступаОбъектов.Ссылка = ДескрипторыДоступаОбъектовНастройкаПрав.Ссылка,
		|		Справочник.Сотрудники КАК СотрудникиСпр
		|	ГДЕ
		|		&ЭтоПапка = ЛОЖЬ
		|		И ДескрипторыДоступаОбъектов.Ссылка В(&Дескрипторы)
		|		И ДескрипторыДоступаОбъектов.Папка = НЕОПРЕДЕЛЕНО
		|		И ДескрипторыДоступаОбъектовНастройкаПрав.Ссылка ЕСТЬ NULL 
		|		И НЕ СотрудникиСпр.ПометкаУдаления
		|) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Дескриптор,
		|	ВложенныйЗапрос.Сотрудник
		|
		|ИМЕЮЩИЕ
		|	МАКСИМУМ(ВложенныйЗапрос.Чтение) = ИСТИНА
		|;
		|
		|// 9 //////////////////
		|ВЫБРАТЬ
		|	ПраваОтПапок.Дескриптор КАК Дескриптор,
		|	ПраваОтПапок.Сотрудник КАК Сотрудник,
		|	ПраваОтПапок.Чтение
		|		И ПраваПоРолямИРазрешениям.Чтение КАК Чтение,
		|	ПраваОтПапок.Добавление
		|		И ПраваПоРолямИРазрешениям.Добавление КАК Добавление,
		|	ПраваОтПапок.Изменение
		|		И ПраваПоРолямИРазрешениям.Изменение КАК Изменение,
		|	ПраваОтПапок.Удаление
		|		И ПраваПоРолямИРазрешениям.Удаление КАК Удаление,
		|	ПраваОтПапок.УправлениеПравами КАК УправлениеПравами
		|ПОМЕСТИТЬ НовыеПрава
		|ИЗ
		|	ПраваОтПапок КАК ПраваОтПапок
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	ПраваПоРолямИРазрешениям КАК ПраваПоРолямИРазрешениям
		|		ПО ПраваОтПапок.Дескриптор = ПраваПоРолямИРазрешениям.Дескриптор
		|			И ПраваОтПапок.Сотрудник = ПраваПоРолямИРазрешениям.Сотрудник";
		
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	ПодзапросыКУдалению = Новый Массив;
	
	Если Не УчитыватьПолитикиДоступа Тогда
		Для Сч = 0 По 5 Цикл
			ПодзапросыКУдалению.Вставить(0, Сч);
		КонецЦикла;
		ПодзапросыКУдалению.Вставить(0, 9);
	КонецЕсли;
	
	Если Не УчитыватьНастройкиПравПапок Тогда
		Для Сч = 6 По 9 Цикл
			ПодзапросыКУдалению.Вставить(0, Сч);
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ИндексПодзапроса Из ПодзапросыКУдалению Цикл
		СхемаЗапроса.ПакетЗапросов.Удалить(ИндексПодзапроса);
	КонецЦикла;
	
	КоличествоПодзапросов = СхемаЗапроса.ПакетЗапросов.Количество();
	ПодзапросНовыеПрава = СхемаЗапроса.ПакетЗапросов[КоличествоПодзапросов - 1];
	ПодзапросНовыеПрава.ТаблицаДляПомещения = "НовыеПрава";
	
	ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	ДокументооборотПраваДоступа.ДополнитьТекстЗапросаПоПравам(ТекстЗапроса);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Текст запроса для индивидуальных дескрипторов доступа.
// Т.е. для дескрипторов, у которых заполнен реквизит "контейнер пользователей".
// 
// Возвращаемое значение:
//  Строка -
Функция ТекстЗапросаДляРасчетаПравИндивидуальныхДескрипторов() Экспорт
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДескрипторыДоступаОбъектов.Ссылка КАК Дескриптор,
		|	СотрудникиВКонтейнерах.Сотрудник КАК Сотрудник,
		|	ИСТИНА КАК Чтение,
		|	ДескрипторыДоступаОбъектов.Изменение КАК Изменение,
		|	ДескрипторыДоступаОбъектов.Изменение КАК Добавление,
		|	ДескрипторыДоступаОбъектов.Изменение КАК Удаление,
		|	ЛОЖЬ КАК УправлениеПравами
		|ПОМЕСТИТЬ НовыеПрава
		|ИЗ
		|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
		|		ПО ДескрипторыДоступаОбъектов.КонтейнерСотрудников = СотрудникиВКонтейнерах.Контейнер
		|ГДЕ
		|	ДескрипторыДоступаОбъектов.Ссылка В (&Дескрипторы)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДескрипторыДоступаОбъектов.Ссылка,
		|	СотрудникиВКонтейнерах.Сотрудник";
		
	ДокументооборотПраваДоступа.ДополнитьТекстЗапросаПоПравам(ТекстЗапроса);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Текст запроса для дескрипторов локальных администраторов.
// 
// Возвращаемое значение:
//  Строка - Текст запроса.
Функция ТекстЗапросаДляРасчетаПравДляЛокальныхАдминистраторов() Экспорт
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаДескрипторов.Дескриптор,
		|	ТаблицаДескрипторов.ВидДоступа,
		|	ТаблицаДескрипторов.ЗначениеДоступа
		|ПОМЕСТИТЬ Дескрипторы
		|ИЗ
		|	&ТаблицаДескрипторов КАК ТаблицаДескрипторов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиВКонтейнерах.Сотрудник КАК Сотрудник,
		|	ИСТИНА КАК Чтение,
		|	МАКСИМУМ(ПраваРолей.Добавление) КАК Добавление,
		|	МАКСИМУМ(ПраваРолей.Изменение) КАК Изменение,
		|	МАКСИМУМ(ПраваРолей.Изменение) КАК Удаление,
		|	МАКСИМУМ(ПраваРолей.ЧтениеБезОграничения) КАК ЧтениеБезОграничения,
		|	МАКСИМУМ(ПраваРолей.ДобавлениеБезОграничения) КАК ДобавлениеБезОграничения,
		|	МАКСИМУМ(ПраваРолей.ИзменениеБезОграничения) КАК ИзменениеБезОграничения
		|ПОМЕСТИТЬ ПраваПоРолям
		|ИЗ
		|	РегистрСведений.ПолномочияСотрудников КАК ПолномочияСотрудников
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.ПрофилиГруппДоступа.Роли КАК ПрофилиГруппДоступаРоли
		|		ПО ПолномочияСотрудников.Полномочия = ПрофилиГруппДоступаРоли.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ПраваРолей КАК ПраваРолей
		|		ПО ПрофилиГруппДоступаРоли.Роль = ПраваРолей.Роль
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
		|		ПО ПолномочияСотрудников.Владелец = СотрудникиВКонтейнерах.Контейнер
		|ГДЕ
		|	ПраваРолей.ОбъектМетаданных = &Идентификатор
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудникиВКонтейнерах.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Дескрипторы.Дескриптор КАК Дескриптор,
		|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Дескрипторы1.ЗначениеДоступа), 0) КАК КолРазрезов
		|ПОМЕСТИТЬ КолРазрезовДескрипторов
		|ИЗ
		|	Дескрипторы КАК Дескрипторы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Дескрипторы КАК Дескрипторы1
		|		ПО Дескрипторы.Дескриптор = Дескрипторы1.Дескриптор
		|			И (Дескрипторы1.ЗначениеДоступа <> НЕОПРЕДЕЛЕНО)
		|
		|СГРУППИРОВАТЬ ПО
		|	Дескрипторы.Дескриптор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РазрешенияДляЛокальныхАдминистраторов.Дескриптор,
		|	РазрешенияДляЛокальныхАдминистраторов.Сотрудник,
		|	РазрешенияДляЛокальныхАдминистраторов.Коллекция,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РазрешенияДляЛокальныхАдминистраторов.ЗначениеДоступа) КАК КолРазрезовДоступа,
		|	МИНИМУМ(РазрешенияДляЛокальныхАдминистраторов.Чтение) КАК Чтение,
		|	МИНИМУМ(РазрешенияДляЛокальныхАдминистраторов.Изменение) КАК Изменение,
		|	МИНИМУМ(РазрешенияДляЛокальныхАдминистраторов.Добавление) КАК Добавление,
		|	МИНИМУМ(РазрешенияДляЛокальныхАдминистраторов.Удаление) КАК Удаление
		|ПОМЕСТИТЬ РазрешенияДоступа
		|ИЗ
		|(
		|	ВЫБРАТЬ
		|		Дескрипторы.Дескриптор КАК Дескриптор,
		|		СотрудникиВКонтейнерах.Сотрудник КАК Сотрудник,
		|		КоллекцииЗначенийДоступаЗначенияДоступа.Ссылка КАК Коллекция,
		|		Дескрипторы.ЗначениеДоступа КАК ЗначениеДоступа,
		|		МАКСИМУМ(УровниДоступа.ЧтениеБезОграничения) КАК Чтение,
		|		МАКСИМУМ(УровниДоступа.ИзменениеБезОграничения) КАК Изменение,
		|		МАКСИМУМ(УровниДоступа.ИзменениеБезОграничения) КАК Добавление,
		|		МАКСИМУМ(УровниДоступа.ИзменениеБезОграничения) КАК Удаление
		|	ИЗ
		|		Дескрипторы КАК Дескрипторы
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РазрешенияДляРазрезовДоступа КАК РазрешенияДляРазрезовДоступа
		|			ПО Дескрипторы.ЗначениеДоступа = РазрешенияДляРазрезовДоступа.РазрезДоступа
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КоллекцииЗначенийДоступа.ЗначенияДоступа КАК КоллекцииЗначенийДоступаЗначенияДоступа
		|			ПО РазрешенияДляРазрезовДоступа.Разрешение = КоллекцииЗначенийДоступаЗначенияДоступа.Значение
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РазрешенияДляЛокальныхАдминистраторов КАК РазрешенияДляЛокальныхАдминистраторов
		|			ПО КоллекцииЗначенийДоступаЗначенияДоступа.Ссылка = РазрешенияДляЛокальныхАдминистраторов.КоллекцияЗначенийДоступа
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
		|			ПО РазрешенияДляЛокальныхАдминистраторов.Пользователь = СотрудникиВКонтейнерах.Контейнер
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УровниДоступа КАК УровниДоступа
		|			ПО РазрешенияДляЛокальныхАдминистраторов.УровеньДоступа = УровниДоступа.Ссылка
		|	ГДЕ
		|		РазрешенияДляЛокальныхАдминистраторов.Предмет = &ИдентификаторДляПроверкиПолитикДоступа
		|		И УровниДоступа.ЧтениеБезОграничения
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Дескрипторы.Дескриптор,
		|		СотрудникиВКонтейнерах.Сотрудник,
		|		КоллекцииЗначенийДоступаЗначенияДоступа.Ссылка,
		|		Дескрипторы.ЗначениеДоступа
		|) КАК РазрешенияДляЛокальныхАдминистраторов
		|
		|СГРУППИРОВАТЬ ПО
		|	РазрешенияДляЛокальныхАдминистраторов.Дескриптор,
		|	РазрешенияДляЛокальныхАдминистраторов.Сотрудник,
		|	РазрешенияДляЛокальныхАдминистраторов.Коллекция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РазрешенияДоступа.Дескриптор,
		|	РазрешенияДоступа.Сотрудник,
		|	МАКСИМУМ(РазрешенияДоступа.Чтение) КАК Чтение,
		|	МАКСИМУМ(РазрешенияДоступа.Изменение) КАК Изменение,
		|	МАКСИМУМ(РазрешенияДоступа.Добавление) КАК Добавление,
		|	МАКСИМУМ(РазрешенияДоступа.Удаление) КАК Удаление
		|ПОМЕСТИТЬ ПраваПоРазрешениямДоступа
		|ИЗ
		|	РазрешенияДоступа КАК РазрешенияДоступа
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КолРазрезовДескрипторов КАК КолРазрезовДескрипторов
		|		ПО РазрешенияДоступа.Дескриптор = КолРазрезовДескрипторов.Дескриптор
		|			И РазрешенияДоступа.КолРазрезовДоступа = КолРазрезовДескрипторов.КолРазрезов
		|
		|СГРУППИРОВАТЬ ПО
		|	РазрешенияДоступа.Дескриптор,
		|	РазрешенияДоступа.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Дескриптор КАК Дескриптор,
		|	ВложенныйЗапрос.Сотрудник КАК Сотрудник,
		|	МАКСИМУМ(ВложенныйЗапрос.Чтение) КАК Чтение,
		|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Изменение,
		|	МАКСИМУМ(ВложенныйЗапрос.Добавление) КАК Добавление,
		|	МАКСИМУМ(ВложенныйЗапрос.Удаление) КАК Удаление,
		|	МАКСИМУМ(ВложенныйЗапрос.УправлениеПравами) КАК УправлениеПравами
		|ПОМЕСТИТЬ НовыеПрава
		|ИЗ
		|(
		|	ВЫБРАТЬ
		|		ПраваПоРазрешениямДоступа.Дескриптор КАК Дескриптор,
		|		ПраваПоРазрешениямДоступа.Сотрудник КАК Сотрудник,
		|		ПраваПоРазрешениямДоступа.Чтение КАК Чтение,
		|		ПраваПоРазрешениямДоступа.Изменение
		|			И ПраваПоРолям.Изменение КАК Изменение,
		|		ПраваПоРазрешениямДоступа.Добавление
		|			И ПраваПоРолям.Добавление КАК Добавление,
		|		ПраваПоРазрешениямДоступа.Удаление
		|			И ПраваПоРолям.Удаление КАК Удаление,
		|		ЛОЖЬ КАК УправлениеПравами
		|	ИЗ
		|		ПраваПоРолям КАК ПраваПоРолям
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПраваПоРазрешениямДоступа КАК ПраваПоРазрешениямДоступа
		|			ПО ПраваПоРолям.Сотрудник = ПраваПоРазрешениямДоступа.Сотрудник
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Дескрипторы.Дескриптор,
		|		ПраваПоРолям.Сотрудник,
		|		ПраваПоРолям.ЧтениеБезОграничения,
		|		ПраваПоРолям.ИзменениеБезОграничения,
		|		ПраваПоРолям.ИзменениеБезОграничения,
		|		ПраваПоРолям.ИзменениеБезОграничения,
		|		ЛОЖЬ
		|	ИЗ
		|		Дескрипторы КАК Дескрипторы,
		|		ПраваПоРолям КАК ПраваПоРолям
		|	ГДЕ
		|		ПраваПоРолям.ЧтениеБезОграничения
		|) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Дескриптор,
		|	ВложенныйЗапрос.Сотрудник";
		
	ДокументооборотПраваДоступа.ДополнитьТекстЗапросаПоПравам(ТекстЗапроса);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Текст запроса для объектов, которые являются разрезами доступа.
// Выдает права всем, кто входит в общие разрешения по разрезу,
//  или в исключительное разрешение, использующее этот разрез.
// 
// Возвращаемое значение:
//	Строка - Текст запроса.
Функция ТекстЗапросаДляРасчетаПравРазрезаДоступа() Экспорт
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаДескрипторов.Дескриптор,
		|	ТаблицаДескрипторов.ВидДоступа,
		|	ТаблицаДескрипторов.ЗначениеДоступа
		|ПОМЕСТИТЬ Дескрипторы
		|ИЗ
		|	&ТаблицаДескрипторов КАК ТаблицаДескрипторов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиВКонтейнерах.Сотрудник КАК Сотрудник,
		|	ИСТИНА КАК Чтение,
		|	МАКСИМУМ(ПраваРолей.Добавление) КАК Добавление,
		|	МАКСИМУМ(ПраваРолей.Изменение) КАК Изменение,
		|	МАКСИМУМ(ПраваРолей.Изменение) КАК Удаление,
		|	МАКСИМУМ(ПраваРолей.ЧтениеБезОграничения) КАК ЧтениеБезОграничения,
		|	МАКСИМУМ(ПраваРолей.ДобавлениеБезОграничения) КАК ДобавлениеБезОграничения,
		|	МАКСИМУМ(ПраваРолей.ИзменениеБезОграничения) КАК ИзменениеБезОграничения
		|ПОМЕСТИТЬ ПраваПоРолям
		|ИЗ
		|	РегистрСведений.ПолномочияСотрудников КАК ПолномочияСотрудников
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.ПрофилиГруппДоступа.Роли КАК ПрофилиГруппДоступаРоли
		|		ПО ПолномочияСотрудников.Полномочия = ПрофилиГруппДоступаРоли.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ПраваРолей КАК ПраваРолей
		|		ПО ПрофилиГруппДоступаРоли.Роль = ПраваРолей.Роль
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
		|		ПО ПолномочияСотрудников.Владелец = СотрудникиВКонтейнерах.Контейнер
		|ГДЕ
		|	ПраваРолей.ОбъектМетаданных = &Идентификатор
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудникиВКонтейнерах.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Дескрипторы.Дескриптор КАК Дескриптор,
		|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Дескрипторы1.ЗначениеДоступа), 0) КАК КолРазрезов
		|ПОМЕСТИТЬ КолРазрезовДескрипторов
		|ИЗ
		|	Дескрипторы КАК Дескрипторы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Дескрипторы КАК Дескрипторы1
		|		ПО Дескрипторы.Дескриптор = Дескрипторы1.Дескриптор
		|			И (Дескрипторы1.ЗначениеДоступа <> НЕОПРЕДЕЛЕНО)
		|
		|СГРУППИРОВАТЬ ПО
		|	Дескрипторы.Дескриптор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Дескриптор,
		|	ВложенныйЗапрос.Сотрудник,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВложенныйЗапрос.ЗначениеДоступа) КАК КолРазрезовДоступа,
		|	ИСТИНА КАК Чтение,
		|	ИСТИНА КАК Изменение,
		|	ИСТИНА КАК Добавление,
		|	ИСТИНА КАК Удаление
		|ПОМЕСТИТЬ РазрешенияДоступа
		|ИЗ
		|	(ВЫБРАТЬ
		|		Дескрипторы.Дескриптор,
		|		СотрудникиВКонтейнерах.Сотрудник,
		|		Дескрипторы.ЗначениеДоступа
		|	ИЗ
		|		Дескрипторы КАК Дескрипторы
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РазрешенияДляРазрезовДоступа КАК РазрешенияДляРазрезовДоступа
		|			ПО Дескрипторы.ЗначениеДоступа = РазрешенияДляРазрезовДоступа.РазрезДоступа
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КоллекцииЗначенийДоступа.ЗначенияДоступа КАК КоллекцииЗначенийДоступаЗначенияДоступа
		|			ПО РазрешенияДляРазрезовДоступа.Разрешение = КоллекцииЗначенийДоступаЗначенияДоступа.Значение
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РазрешенияДляЛокальныхАдминистраторов КАК РазрешенияДляЛокальныхАдминистраторов
		|			ПО КоллекцииЗначенийДоступаЗначенияДоступа.Ссылка = РазрешенияДляЛокальныхАдминистраторов.КоллекцияЗначенийДоступа
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
		|			ПО РазрешенияДляЛокальныхАдминистраторов.Пользователь = СотрудникиВКонтейнерах.Контейнер
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УровниДоступа КАК УровниДоступа
		|			ПО РазрешенияДляЛокальныхАдминистраторов.УровеньДоступа = УровниДоступа.Ссылка) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Дескриптор,
		|	ВложенныйЗапрос.Сотрудник
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КолРазрезовДескрипторов.Дескриптор,
		|	Сотрудники.Ссылка,
		|	0,
		|	ИСТИНА,
		|	ИСТИНА,
		|	ИСТИНА,
		|	ИСТИНА
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники,
		|	КолРазрезовДескрипторов КАК КолРазрезовДескрипторов
		|ГДЕ
		|	КолРазрезовДескрипторов.КолРазрезов = 0
		|	И НЕ Сотрудники.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РазрешенияДоступа.Дескриптор,
		|	РазрешенияДоступа.Сотрудник,
		|	МАКСИМУМ(РазрешенияДоступа.Чтение) КАК Чтение,
		|	МАКСИМУМ(РазрешенияДоступа.Изменение) КАК Изменение,
		|	МАКСИМУМ(РазрешенияДоступа.Добавление) КАК Добавление,
		|	МАКСИМУМ(РазрешенияДоступа.Удаление) КАК Удаление
		|ПОМЕСТИТЬ ПраваПоРазрешениямДоступа
		|ИЗ
		|	РазрешенияДоступа КАК РазрешенияДоступа
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КолРазрезовДескрипторов КАК КолРазрезовДескрипторов
		|		ПО РазрешенияДоступа.Дескриптор = КолРазрезовДескрипторов.Дескриптор
		|			И РазрешенияДоступа.КолРазрезовДоступа = КолРазрезовДескрипторов.КолРазрезов
		|
		|СГРУППИРОВАТЬ ПО
		|	РазрешенияДоступа.Дескриптор,
		|	РазрешенияДоступа.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Дескриптор,
		|	ВложенныйЗапрос.Сотрудник,
		|	МАКСИМУМ(ВложенныйЗапрос.Чтение) КАК Чтение,
		|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Изменение,
		|	МАКСИМУМ(ВложенныйЗапрос.Добавление) КАК Добавление,
		|	МАКСИМУМ(ВложенныйЗапрос.Удаление) КАК Удаление,
		|	МАКСИМУМ(ВложенныйЗапрос.УправлениеПравами) КАК УправлениеПравами
		|ПОМЕСТИТЬ НовыеПрава
		|ИЗ
		|(
		|	ВЫБРАТЬ
		|		ПраваПоРазрешениямДоступа.Дескриптор КАК Дескриптор,
		|		ПраваПоРазрешениямДоступа.Сотрудник КАК Сотрудник,
		|		ПраваПоРазрешениямДоступа.Чтение КАК Чтение,
		|		ПраваПоРазрешениямДоступа.Изменение
		|			И ПраваПоРолям.Изменение КАК Изменение,
		|		ПраваПоРазрешениямДоступа.Добавление
		|			И ПраваПоРолям.Добавление КАК Добавление,
		|		ПраваПоРазрешениямДоступа.Удаление
		|			И ПраваПоРолям.Удаление КАК Удаление,
		|		ЛОЖЬ КАК УправлениеПравами
		|	ИЗ
		|		ПраваПоРолям КАК ПраваПоРолям
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПраваПоРазрешениямДоступа КАК ПраваПоРазрешениямДоступа
		|			ПО ПраваПоРолям.Сотрудник = ПраваПоРазрешениямДоступа.Сотрудник
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Дескрипторы.Дескриптор,
		|		ПраваПоРолям.Сотрудник,
		|		ПраваПоРолям.ЧтениеБезОграничения,
		|		ПраваПоРолям.ДобавлениеБезОграничения,
		|		ПраваПоРолям.ДобавлениеБезОграничения,
		|		ПраваПоРолям.ДобавлениеБезОграничения,
		|		ЛОЖЬ
		|	ИЗ
		|		Дескрипторы КАК Дескрипторы,
		|		ПраваПоРолям КАК ПраваПоРолям
		|	ГДЕ
		|		ПраваПоРолям.ЧтениеБезОграничения
		|) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Дескриптор,
		|	ВложенныйЗапрос.Сотрудник";
	
	ДокументооборотПраваДоступа.ДополнитьТекстЗапросаПоПравам(ТекстЗапроса);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Текст запроса, который возвращает выборку с правами по дескрипторам.
// Работает аналогично стандартному запросу, но использует другой подзапрос для получения настроек прав.
// 
// Возвращаемое значение - Строка - текст запроса
// 
Функция ТекстЗапросаДляРасчетаПравПапок() Экспорт
	
	ТекстЗапросаКНастройкамПапок =
		"ВЫБРАТЬ
		|	ДескрипторыДоступаОбъектовНастройкаПрав.Ссылка КАК Дескриптор,
		|	СотрудникиВКонтейнерах.Сотрудник КАК Сотрудник,
		|	ДескрипторыДоступаОбъектовНастройкаПрав.Право КАК Право,
		|	МАКСИМУМ(ДескрипторыДоступаОбъектовНастройкаПрав.ПравоЗапрещено) КАК ПравоЗапрещено
		|ПОМЕСТИТЬ НастройкиПапок
		|ИЗ
		|	Справочник.ДескрипторыДоступаОбъектов.НастройкаПрав КАК ДескрипторыДоступаОбъектовНастройкаПрав
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
		|		ПО ДескрипторыДоступаОбъектовНастройкаПрав.Пользователь = СотрудникиВКонтейнерах.Контейнер
		|ГДЕ
		|	ДескрипторыДоступаОбъектовНастройкаПрав.Ссылка В(&Дескрипторы)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДескрипторыДоступаОбъектовНастройкаПрав.Ссылка,
		|	СотрудникиВКонтейнерах.Сотрудник,
		|	ДескрипторыДоступаОбъектовНастройкаПрав.Право";
		
	ТекстСтандартногоЗапроса = ТекстЗапросаДляСтандартногоРасчетаПрав(Истина, Ложь);
	
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстСтандартногоЗапроса);
	ПакетЗапросов = СхемаЗапроса.ПакетЗапросов;
	
	Для Каждого Подзапрос Из ПакетЗапросов Цикл
		Если Подзапрос.ТаблицаДляПомещения = "НастройкиПапок" Тогда
			Подзапрос.УстановитьТекстЗапроса(ТекстЗапросаКНастройкамПапок);
		КонецЕсли;
	КонецЦикла;
	
	ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Запрос, который выбирает значения доступа, соответствующие дескрипторам, по всем актуальным видам.
// 
// Возвращаемое значение:
//  Запрос -
Функция ЗапросПоДескрипторамСВидамиДоступа() Экспорт
	
	Запрос = Новый Запрос;
	
	АктуальныеВидыДоступа = ДокументооборотПраваДоступаПовтИсп.ТаблицаРазрезовДоступа();
	
	Если АктуальныеВидыДоступа.Количество() = 0 Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТаблицаДескрипторов.Ссылка КАК Дескриптор,
			|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыДоступа.ПустаяСсылка) КАК ВидДоступа,
			|	ЕСТЬNULL(ТаблицаДескрипторов1.Ссылка, НЕОПРЕДЕЛЕНО) КАК ЗначениеДоступа
			|ИЗ
			|	Справочник.ДескрипторыДоступаОбъектов КАК ТаблицаДескрипторов
					// Для того, чтобы тип колонки выгруженной таблицы не был пустым,
					// и таблицу можно было использовать в запросе для расчета прав.
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДескрипторыДоступаОбъектов КАК ТаблицаДескрипторов1
			|		ПО ЛОЖЬ
			|ГДЕ
			|	ТаблицаДескрипторов.Ссылка В(&Дескрипторы)";
			
		Возврат Запрос;
			
	КонецЕсли;
	
	ШаблонЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаДескрипторов.Ссылка КАК Дескриптор,
		|	&ПараметрВидДоступа КАК ВидДоступа,
		|	ЕСТЬNULL(ТаблицаЗначенийДоступа.Ссылка, НЕОПРЕДЕЛЕНО) КАК ЗначениеДоступа
		|ИЗ
		|	Справочник.ДескрипторыДоступаОбъектов КАК ТаблицаДескрипторов
		|	СОЕДИНЕНИЕ &СоединениеТЧ КАК ТЧДескрипторов ПО &Условие // Заменяемая строка.
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	&ТаблицаЗначенийДоступа КАК ТаблицаЗначенийДоступа
		|		ПО &ТаблицаСПолемСоединенияСоЗначением = ТаблицаЗначенийДоступа.Ссылка
		|ГДЕ
		|	ТаблицаДескрипторов.Ссылка В (&Дескрипторы)";
	ЗаменяемоеСоединение = "СОЕДИНЕНИЕ &СоединениеТЧ КАК ТЧДескрипторов ПО &Условие";
	ЭтоПерваяСтрока = Истина;
	Для Каждого Стр Из АктуальныеВидыДоступа Цикл
		
		ФрагментТекста = СтрЗаменить(ШаблонЗапроса, "&ПараметрВидДоступа", "&" + Стр.Имя);
		ФрагментТекста = СтрЗаменить(ФрагментТекста, "&ТаблицаЗначенийДоступа", Стр.ИмяТаблицыЗначенийДоступа);
		
		Если ЗначениеЗаполнено(Стр.ИмяТЧДескриптора) Тогда
			ФрагментТекста = СтрЗаменить(
				ФрагментТекста,
				ЗаменяемоеСоединение, 
				СтрШаблон(
					"	ЛЕВОЕ СОЕДИНЕНИЕ
					|	Справочник.ДескрипторыДоступаОбъектов.%1 КАК ТЧДескрипторов
					|		ПО ТаблицаДескрипторов.Ссылка = ТЧДескрипторов.Ссылка", Стр.ИмяТЧДескриптора));
			ФрагментТекста = СтрЗаменить(
				ФрагментТекста,
				"&ТаблицаСПолемСоединенияСоЗначением",
				СтрШаблон("%1.%2", "ТЧДескрипторов", Стр.ИмяПоляТаблицыДескриптора));
		Иначе
			ФрагментТекста = СтрЗаменить(ФрагментТекста, ЗаменяемоеСоединение, "");
			ФрагментТекста = СтрЗаменить(
				ФрагментТекста,
				"&ТаблицаСПолемСоединенияСоЗначением",
				СтрШаблон("%1.%2", "ТаблицаДескрипторов", Стр.ИмяПоляТаблицыДескриптора));
		КонецЕсли;
		
		Запрос.Текст = Запрос.Текст + ?(ЭтоПерваяСтрока, "", Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС);
		Запрос.Текст = Запрос.Текст + ФрагментТекста;
		
		Запрос.УстановитьПараметр(Стр.Имя, Стр.ВидДоступа);
		
		ЭтоПерваяСтрока = Ложь;
		
	КонецЦикла;
	
	Возврат Запрос;
	
КонецФункции

Функция ТаблицаРазрезовДоступа(ТолькоАктуальные = Истина) Экспорт
	
	Возврат ДокументооборотПраваДоступаПереопределяемый.ТаблицаРазрезовДоступа(ТолькоАктуальные);
	
КонецФункции

Функция ДеревоЗначенийДоступа() Экспорт
	
	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("ВидДоступа");
	Дерево.Колонки.Добавить("ЗначениеДоступа");
	Дерево.Колонки.Добавить("Представление");
	Дерево.Колонки.Добавить("ЭтоГруппа");
	
	ШаблонТекстаЗапросаПоЗначениямДоступа =
		"ВЫБРАТЬ
		|	ТаблицаВидаДоступа.Ссылка КАК ЗначениеДоступа,
		|	ТаблицаВидаДоступа.Представление КАК Представление
		|ИЗ
		|	&ИмяТаблицыЗначенийДоступа КАК ТаблицаВидаДоступа
		|ГДЕ
		|	НЕ ПометкаУдаления";
	
	ТаблицаРазрезовДоступа = ДокументооборотПраваДоступаПовтИсп.ТаблицаРазрезовДоступа();
	ТаблицаРазрезовДоступа.Сортировать("Имя");
	Для Каждого СтрВидаДоступа Из ТаблицаРазрезовДоступа Цикл
		
		ИмяТаблицыЗначенийДоступа = СтрВидаДоступа.ИмяТаблицыЗначенийДоступа;
		МетаданныеТаблицы = Метаданные.НайтиПоПолномуИмени(ИмяТаблицыЗначенийДоступа);
		ЭтоИерархическийСправочник = 
			Метаданные.Справочники.Содержит(МетаданныеТаблицы) И МетаданныеТаблицы.Иерархический;
		
		Запрос = Новый Запрос(ШаблонТекстаЗапросаПоЗначениямДоступа);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяТаблицыЗначенийДоступа", ИмяТаблицыЗначенийДоступа);
		Если ЭтоИерархическийСправочник Тогда
			Запрос.Текст = Запрос.Текст + "
				|УПОРЯДОЧИТЬ ПО ТаблицаВидаДоступа.Ссылка ИЕРАРХИЯ";
		КонецЕсли;
		
		//@skip-check query-in-loop - разные запросы к разным таблицам.
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			// Если нет ни одного значения, вид доступа не добавляется.
			Продолжить;
		КонецЕсли;
		
		НоваяСтрокаВида = Дерево.Строки.Добавить();
		НоваяСтрокаВида.ВидДоступа = СтрВидаДоступа.ВидДоступа;
		НоваяСтрокаВида.ЗначениеДоступа = СтрВидаДоступа.ВидДоступа;
		НоваяСтрокаВида.Представление = Строка(СтрВидаДоступа.ВидДоступа);
		
		ДеревоРазрезаДоступа = Результат.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		ДеревоРазрезаДоступа.Строки.Сортировать("Представление", Истина);
		
		ПеренестиСтрокиВДеревоВидовДоступа(НоваяСтрокаВида, ДеревоРазрезаДоступа, СтрВидаДоступа.ВидДоступа);
		
	КонецЦикла;
	
	Возврат Дерево;
	
КонецФункции

// Возвращает массив неиспользуемых разрезов доступа.
//
// Параметры:
//  ОтключенныеПоФункциональнымОпциям - Булево - включать разрезы, отключенные по функциональным опциям.
//  ОтключенныеВручную - Булево - включать разрезы, отключенные вручную.
//
// Возвращаемое значение:
//  Массив - массив отключенных разрезов доступа.
//
Функция ОтключенныеРазрезыДоступа(
			ОтключенныеПоФункциональнымОпциям = Истина,
			ОтключенныеВручную = Истина) Экспорт
	
	Возврат ДокументооборотПраваДоступаПереопределяемый.ОтключенныеРазрезыДоступа(
		ОтключенныеПоФункциональнымОпциям, ОтключенныеВручную);
	
КонецФункции

// Возвращает соответствие разрезов доступа и имен реквизитов Дескриптора.
//
Функция ИменаРеквизитовДескрпитораПоРазрезамДоступа() Экспорт
	
	Возврат ДокументооборотПраваДоступаПереопределяемый.ИменаРеквизитовДескрпитораПоРазрезамДоступа();
	
КонецФункции

// Возвращает массив неиспользуемых реквизитов дескриптора.
// 
// Параметры:
//  ИдентификаторОбъектаМетаданных - Ссылка на ИОМ - значение одноименного реквизита дескриптора.
// 
// Возвращаемое значение:
//  Массив - список имен неиспользуемых реквизитов.
// 
Функция НеиспользуемыеРеквизитыДескриптора(ИдентификаторОбъектаМетаданных) Экспорт
	
	Возврат ДокументооборотПраваДоступаПереопределяемый.НеиспользуемыеРеквизитыДескриптора(
		ИдентификаторОбъектаМетаданных);
	
КонецФункции

// Возвращает массив типов объектов, которые имеют разрезы доступа.
//
Функция ТипыСсылокИспользующиеРазрезыДоступа() Экспорт
	
	Результат = Новый Массив;
	
	ПредметыДоступаСРазрезами = ДокументооборотПраваДоступаПереопределяемый.ПредметыДоступаСРазрезами();
	ИдентификаторыПредметов = ПредметыДоступаСРазрезами.ВыгрузитьКолонку("ОбъектМетаданных");
	ПустыеСсылки = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ИдентификаторыПредметов, "ЗначениеПустойСсылки");
	
	Для Каждого КлючИЗначение Из ПустыеСсылки Цикл
		Результат.Добавить(ТипЗнч(КлючИЗначение.Значение));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Получает индивидуальный дескриптор из базы данных. Если в базе данных его нет, то создаёт его.
// 
// Параметры:
//  ОбъектМетаданных - СправочникСсылка.ИдентификаторыОбъектовМетаданных.
//  КонтейнерПользователей - ОпределяемыйТип.КонтейнерыСотрудников.
//  Изменение - Булево.
// 
// Возвращаемое значение:
//  СправочникСсылка.ДескрипторыДоступаОбъектов - Индивидуальный дескриптор.
//
Функция ПолучитьИндивидуальныйДескриптор(ОбъектМетаданных, КонтейнерПользователей, Изменение) Экспорт
	
	НовыйДескриптор = Справочники.ДескрипторыДоступаОбъектов.СоздатьДескрипторИндивидуальный(
		ОбъектМетаданных, КонтейнерПользователей, Изменение);
	
	Дескриптор = Справочники.ДескрипторыДоступаОбъектов.НайтиДескрипторПоОбразцу(НовыйДескриптор);
	Если Не ЗначениеЗаполнено(Дескриптор) Тогда
		// Если не найден уже существующий дескриптор, то записываем новый:
		НовыйДескриптор.Записать();
		Дескриптор = НовыйДескриптор.Ссылка;
	КонецЕсли;
	
	Возврат Дескриптор;
	
КонецФункции

// Возвращает коэффициент для записи прав поштучно.
// Используется при записи прав по дескрипторам для определения варианта записи: набором записей,\
//  или отдельными записями 
// 
// Возвращаемое значение:
//  Число - Пороговый коэффициент для записи прав поштучно
Функция ПороговыйКоэффициентДляЗаписиПравПоштучно() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Константы.ПороговыйКоэффициентДляЗаписиПравПоштучно.Получить();
	
КонецФункции

// Сопоставление ненужных ролей нужным, некоторые роли из библиотек не работают, присутствуют в конфигурации только
// из-за особенностей встраивания. Их не должно быть ни у кого в полномочиях.
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//	* Ключ - СправочникСсылка.ИдентификаторыОбъектовМетаданных - Ненужная роль, которую надо подменить.
//	* Значение - Структура - На что надо подменить:
//		** РольИОМ - СправочникСсылка.ИдентификаторыОбъектовМетаданных - Нужная роль, на которую надо подменить.
//		** РольСтрока - СправочникСсылка.ИдентификаторыОбъектовМетаданных - Полное имя нужной роли, например
//																			"Роль.БазовыеПраваЭДДокументооборот".
Функция СопоставлениеНенужныхРолейНужным() Экспорт
	
	Подмена = Новый Соответствие();
	// При локализации можно переопределить свою подмену ненужных ролей нужными.
	
	// Локализация
	
	МДР = Метаданные.Роли;
	// Ненужные роли:
	НужныеНенужныеРоли = Новый Массив();
	НужныеНенужныеРоли.Добавить(МДР.БазовыеПраваЭД);
	НужныеНенужныеРоли.Добавить(МДР.ДобавлениеИзменениеМЧДКонтрагентовЭДО);
	НужныеНенужныеРоли.Добавить(МДР.ДобавлениеИзменениеМЧДОрганизацийЭДО);
	НужныеНенужныеРоли.Добавить(МДР.ДобавлениеИзменениеМаршрутовПодписания);
	НужныеНенужныеРоли.Добавить(МДР.ДобавлениеИзменениеНастроекОбменаСКонтрагентами);
	НужныеНенужныеРоли.Добавить(МДР.ДобавлениеИзменениеУчетныхЗаписейСерверовDSS); // Ей замены нет.
	НужныеНенужныеРоли.Добавить(МДР.ДобавлениеИзменениеЭлектронныхДокументов);
	НужныеНенужныеРоли.Добавить(МДР.ЧтениеМЧДЭДО);
	НужныеНенужныеРоли.Добавить(МДР.ЧтениеМаршрутовПодписания);
	НужныеНенужныеРоли.Добавить(МДР.ЧтениеНастроекОбменаСКонтрагентами);
	НужныеНенужныеРоли.Добавить(МДР.ЧтениеЭлектронныхДокументов);
	// Нужные роли:
	НужныеНенужныеРоли.Добавить(МДР.БазовыеПраваЭДДокументооборот);
	НужныеНенужныеРоли.Добавить(МДР.ДобавлениеИзменениеМЧДКонтрагентовЭДОДокументооборот);
	НужныеНенужныеРоли.Добавить(МДР.ДобавлениеИзменениеМЧДОрганизацийЭДОДокументооборот);
	НужныеНенужныеРоли.Добавить(МДР.ДобавлениеИзменениеМаршрутовПодписанияДокументооборот);
	НужныеНенужныеРоли.Добавить(МДР.ДобавлениеИзменениеНастроекОбменаСКонтрагентамиДокументооборот);
	НужныеНенужныеРоли.Добавить(МДР.ДобавлениеИзменениеЭлектронныхДокументовДокументооборот);
	НужныеНенужныеРоли.Добавить(МДР.ЧтениеМЧДЭДОДокументооборот);
	НужныеНенужныеРоли.Добавить(МДР.ЧтениеМаршрутовПодписанияДокументооборот);
	НужныеНенужныеРоли.Добавить(МДР.ЧтениеНастроекОбменаСКонтрагентамиДокументооборот);
	НужныеНенужныеРоли.Добавить(МДР.ЧтениеЭлектронныхДокументовПоОрганизациямДокументооборот);
	
	РолиИОМПоРолямМД = ОбщегоНазначения.ИдентификаторыОбъектовМетаданных(НужныеНенужныеРоли);
	
	ВставитьПодмену(
		Подмена, РолиИОМПоРолямМД,
		МДР.БазовыеПраваЭД,
		МДР.БазовыеПраваЭДДокументооборот);
	ВставитьПодмену(
		Подмена, РолиИОМПоРолямМД,
		МДР.ДобавлениеИзменениеМЧДКонтрагентовЭДО,
		МДР.ДобавлениеИзменениеМЧДКонтрагентовЭДОДокументооборот);
	ВставитьПодмену(
		Подмена, РолиИОМПоРолямМД, 
		МДР.ДобавлениеИзменениеМЧДОрганизацийЭДО,
		МДР.ДобавлениеИзменениеМЧДОрганизацийЭДОДокументооборот);
	ВставитьПодмену(
		Подмена, РолиИОМПоРолямМД, 
		МДР.ДобавлениеИзменениеМаршрутовПодписания,
		МДР.ДобавлениеИзменениеМаршрутовПодписанияДокументооборот);
	ВставитьПодмену(
		Подмена, РолиИОМПоРолямМД, 
		МДР.ДобавлениеИзменениеНастроекОбменаСКонтрагентами,
		МДР.ДобавлениеИзменениеНастроекОбменаСКонтрагентамиДокументооборот);
	ВставитьПодмену(
		Подмена, РолиИОМПоРолямМД, 
		МДР.ДобавлениеИзменениеУчетныхЗаписейСерверовDSS,
		Неопределено); // Неопределено - замены нет, признак, что эту роль просто надо удалить.
	ВставитьПодмену(
		Подмена, РолиИОМПоРолямМД, 
		МДР.ДобавлениеИзменениеЭлектронныхДокументов,
		МДР.ДобавлениеИзменениеЭлектронныхДокументовДокументооборот);
	ВставитьПодмену(
		Подмена, РолиИОМПоРолямМД, 
		МДР.ЧтениеМЧДЭДО,
		МДР.ЧтениеМЧДЭДОДокументооборот);
	ВставитьПодмену(
		Подмена, РолиИОМПоРолямМД, 
		МДР.ЧтениеМаршрутовПодписания,
		МДР.ЧтениеМаршрутовПодписанияДокументооборот);
	ВставитьПодмену(
		Подмена, РолиИОМПоРолямМД, 
		МДР.ЧтениеНастроекОбменаСКонтрагентами,
		МДР.ЧтениеНастроекОбменаСКонтрагентамиДокументооборот);
	ВставитьПодмену(
		Подмена, РолиИОМПоРолямМД, 
		МДР.ЧтениеЭлектронныхДокументов,
		МДР.ЧтениеЭлектронныхДокументовПоОрганизациямДокументооборот);
	
	// Локализация Конец
	
	Возврат Подмена;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает массив идентификаторов объектов, которые используют доступ по дескрипторам.
// 
// Возвращаемое значение:
//  Массив из СправочникСсылка.ИдентификаторыОбъектовМетаданных - Все объекты механизма прав доступа
Функция ВсеОбъектыМеханизмаПравДоступа() Экспорт
	
	Результат = Новый Массив;
	
	ТипыОбъектовМеханизмаПрав =
		ДокументооборотПраваДоступаПовтИсп.ТипыОбъектовИспользующихДоступПоДескрипторам();
	
	Для Каждого ТипОбъекта Из ТипыОбъектовМеханизмаПрав Цикл
		Результат.Добавить(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипОбъекта));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Вычисляет хэш значение по строке
// используется для заполнения реквизита дескриптора Хэш,
// который позволяет быстро находить существующий дескриптор по шаблону
// 
// Параметры:
//  Строка - Строка
// 
// Возвращаемое значение:
//  Число
Функция ВычислитьХэшПоСтроке(Строка) Экспорт
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.CRC32);
	ХешированиеДанных.Добавить(Строка);
	
	Возврат ХешированиеДанных.ХешСумма;
	
КонецФункции

#Область ТипыСЗависимымиПравами

// Если переопределять список типов, то переопределять обе эти процедуры синхронно:

// Возвращает объекты метаданных, чьи права зависят от прав других объектов
// при обновлении прав эти объекты должны обрабатываться в последнюю очередь.
// Плюс, здесь перечислены все объекты, которые не зависят от других, но их тоже можно обрабатывать в последнюю очередь
// и многопоточно.
// 
// Возвращаемое значение:
//  Массив Из СправочникСсылка.ИдентификаторыОбъектовМетаданных
Функция ОбъектыСЗависимымиПравами() Экспорт
	
	ОбъектыМетаданных = Новый Массив();
	ОбъектыМетаданных.Добавить(Метаданные.Справочники.ДокументыПредприятия);
	ОбъектыМетаданных.Добавить(Метаданные.Справочники.ЗаписиРабочегоКалендаря);
	ОбъектыМетаданных.Добавить(Метаданные.Справочники.Мероприятия);
	ОбъектыМетаданных.Добавить(Метаданные.Справочники.Проекты);
	ОбъектыМетаданных.Добавить(Метаданные.Справочники.СообщенияОбсуждений);
	ОбъектыМетаданных.Добавить(Метаданные.Справочники.ТемыОбсуждений);
	ОбъектыМетаданных.Добавить(Метаданные.Справочники.ШаблоныДокументов);
	ОбъектыМетаданных.Добавить(Метаданные.Документы.ВходящееПисьмо);
	ОбъектыМетаданных.Добавить(Метаданные.Документы.ДействиеЗадачи);
	ОбъектыМетаданных.Добавить(Метаданные.Документы.ЕжедневныйОтчет);
	ОбъектыМетаданных.Добавить(Метаданные.Документы.ЕженедельныйОтчет);
	ОбъектыМетаданных.Добавить(Метаданные.Документы.Задача);
	ОбъектыМетаданных.Добавить(Метаданные.Документы.ИсходящееПисьмо);
	
	ДокументооборотПраваДоступаЛокализация.ДополнитьОбъектыСЗависимымиПравами(ОбъектыМетаданных);
	
	// Из массива объектов метаданных делаем массив из элементов справочника ИОМ:
	ЗависимыеОбъекты = Новый Массив();
	ОбъектыСоответствие = ОбщегоНазначения.ИдентификаторыОбъектовМетаданных(ОбъектыМетаданных);
	Для Каждого КлючЗначение Из ОбъектыСоответствие Цикл
		ЗависимыеОбъекты.Добавить(КлючЗначение.Значение);
	КонецЦикла;
	
	Возврат ЗависимыеОбъекты;
	
КонецФункции

// Аналог функции см. ОбъектыСЗависимымиПравами, но возвращает типы, а не ИОМы.
// 
// Возвращаемое значение:
//  Массив Из Тип
Функция ТипыОбъектовСЗависимымиПравами() Экспорт
	
	ИОМы = ОбъектыСЗависимымиПравами();
	
	// Достаем из ИОМов их тип значения:
	ЗависимыеТипы = Новый Массив();
	ЗначенияПустойСсылкиПоИОМам = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ИОМы, "ЗначениеПустойСсылки");
	Для Каждого КлючЗначение Из ЗначенияПустойСсылкиПоИОМам Цикл
		ЗависимыеТипы.Добавить(ТипЗнч(КлючЗначение.Значение));
	КонецЦикла;
	
	Возврат ЗависимыеТипы;
	
КонецФункции

#КонецОбласти // ТипыСЗависимымиПравами

// Возвращает, включено ли отложенное обновление прав доступа
// 
// Возвращаемое значение:
//  Булево
Функция ОтложенноеОбновлениеПравДоступа() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Константы.ДокументооборотИспользоватьОтложенноеОбновлениеПравДоступа.Получить();
	
КонецФункции	

// Кэширование соответствующей константы.
// 
// Возвращаемое значение:
//  Число
Функция КоличествоПотоковОчередиПрав() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Колво = Константы.КоличествоПотоковОчередиПрав.Получить();
	Если Не ЗначениеЗаполнено(Колво) Тогда
		// Константа не заполнена, заполним ее по умолчанию:
		ПоУмолчанию = 10;
		Константы.КоличествоПотоковОчередиПрав.Установить(ПоУмолчанию);
		Возврат ПоУмолчанию;
	КонецЕсли;
	
	Если Колво > 1 И ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		// Принудительно ставим 1 поток. В файловой базе многопоточность зависнет, там одновременно только 1 задание.
		// На продуктивной файловой базе рекомендуется вообще выключить отложенное обновление, использовать только на
		// тестовых файловых базах для тестирования разработки с расчетом на клиент-серверные базы.
		Колво = 1;
		Константы.КоличествоПотоковОчередиПрав.Установить(1);
	КонецЕсли;
	
	Возврат Колво;
	
КонецФункции

// Возвращает размер порции для расчета прав дескрипторов и для потока многопоточной очереди прав. 
// Столько записей очереди будет запускаться разом в каждый поток при разборе очереди прав регламентным заданием.
// 
// Параметры:
//  ИндивидуальныеДескрипторы - Булево
// 
// Возвращаемое значение:
//  Число
Функция РазмерПорцииПриОбработкеОчереди(ИндивидуальныеДескрипторы) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	РазмерПорции = 500;
	Если ИндивидуальныеДескрипторы Тогда
		РазмерПорцииПоУмолчанию = 100;
		РазмерПорции = Константы.ГрупповойРасчетПравДоступаРазмерПорции.Получить();
		Если Не ЗначениеЗаполнено(РазмерПорции) Тогда
			// Константа не заполнена, заполним ее по умолчанию:
			РазмерПорции = РазмерПорцииПоУмолчанию;
			Константы.ГрупповойРасчетПравДоступаРазмерПорции.Установить(РазмерПорции);
		КонецЕсли;
	КонецЕсли;
	
	Возврат РазмерПорции;
	
КонецФункции

// Возвращает массив типов объектов, которые используют доступ по дескрипторам.
// 
// Возвращаемое значение:
//  Массив из Тип
Функция ТипыОбъектовИспользующихДоступПоДескрипторам() Экспорт
	
	Результат = Метаданные.ПодпискиНаСобытия.ДокументооборотПраваДоступаПриЗаписиОбъектаДоступа.Источник.Типы();
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив типов объектов, которые используют доступ по дескрипторам.
// 
// Возвращаемое значение:
//  Массив Из Тип
Функция ТипыСсылокИспользующихДоступПоДескрипторам() Экспорт
	
	Возврат Метаданные.ОпределяемыеТипы.ОбъектДоступа.Тип.Типы();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПеренестиСтрокиВДеревоВидовДоступа(Приемник, Источник, ВидДоступа = Неопределено)
	
	ПарыКОбработке = Новый Массив;
	ПарыКОбработке.Добавить(Новый Структура("Приемник, Источник", Приемник, Источник));
	
	Пока ПарыКОбработке.Количество() > 0 Цикл
		
		ТекущийПриемник = ПарыКОбработке[0].Приемник;
		ТекущийИсточник = ПарыКОбработке[0].Источник;
		
		Для Каждого СтрИсточника Из ТекущийИсточник.Строки Цикл
			
			СтрПриемника = ТекущийПриемник.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрПриемника, СтрИсточника);
			Если ЗначениеЗаполнено(ВидДоступа) Тогда
				СтрПриемника.ВидДоступа = ВидДоступа;
				СтрПриемника.ЭтоГруппа = СтрПриемника.ЗначениеДоступа.ЭтоГруппа;
			КонецЕсли;
			
			ПарыКОбработке.Добавить(Новый Структура("Приемник, Источник", СтрПриемника, СтрИсточника));
			
		КонецЦикла;
		
		ПарыКОбработке.Удалить(0);
		
	КонецЦикла;
	
КонецПроцедуры

// Локализация

// Просто обертка для лаконичности.
Процедура ВставитьПодмену(Подмена, РолиИОМПоРолямМД, НенужнаяРольМД, НужнаяРольМД)
	
	ПолноеИмяНужной = ?(НужнаяРольМД = Неопределено, "", НужнаяРольМД.ПолноеИмя());
	НужнаяРольИОМ = ?(НужнаяРольМД = Неопределено, Неопределено, РолиИОМПоРолямМД[ПолноеИмяНужной]);
	НаЧтоПодменить = Новый Структура("РольИОМ, РольСтрока", НужнаяРольИОМ, ПолноеИмяНужной);
	НенужнаяРольИОМ = РолиИОМПоРолямМД[НенужнаяРольМД.ПолноеИмя()];
	
	Подмена.Вставить(НенужнаяРольИОМ, НаЧтоПодменить);
	
КонецПроцедуры
// Локализация Конец

#КонецОбласти

#КонецЕсли
