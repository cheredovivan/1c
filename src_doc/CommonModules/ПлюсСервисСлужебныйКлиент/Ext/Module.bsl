#Область СлужебныйПрограммныйИнтерфейс

Процедура ОткрытьФормуВзаимодействияССервисом(Параметры = Неопределено) Экспорт
	
	Если Параметры = Неопределено Тогда
		Параметры = Новый Структура;
	КонецЕсли;
	
	Параметры.Вставить("ЗаголовокПрограммы", КлиентскоеПриложение.ПолучитьЗаголовок());
	
	ОткрытьФорму("Обработка.ПлюсСервис.Форма.ВзаимодействиеССервисом", Параметры);
	
КонецПроцедуры

Функция ПолучитьФормуВзаимодействияССервисом(Параметры = Неопределено) Экспорт
	
	Если Параметры = Неопределено Тогда
		Параметры = Новый Структура;
	КонецЕсли;
	
	Параметры.Вставить("ЗаголовокПрограммы", КлиентскоеПриложение.ПолучитьЗаголовок());

	//@skip-check use-non-recommended-method - особенность реализации
	Возврат ПолучитьФорму("Обработка.ПлюсСервис.Форма.ВзаимодействиеССервисом", Параметры);
	
КонецФункции

Процедура ПриПолученииСерверногоОповещения(ИмяОповещения, Результат) Экспорт
	
	Если ИмяОповещения = ПлюсСервисКлиентСервер.ИмяСерверногоОповещенияНеобходимаУстановкаМонопольногоРежима() Тогда
		ОбработатьОбновлениеРасширений(Результат);
	ИначеЕсли ИмяОповещения = ПлюсСервисКлиентСервер.ИмяСерверногоОповещенияОтключеноАвтообновление() Тогда
		ОбработатьОтключениеАвтообновленияРасширений(Результат);
	КонецЕсли;
		
КонецПроцедуры

Процедура ОткрытьФормуУстановкиРасширений(Знач ДанныеУведомления) Экспорт
	
	ОткрытьФормуВзаимодействияССервисом(Новый Структура("ЭтоУстановкаОбновлений", Истина));
	
КонецПроцедуры

Процедура ОбработчикГлобальногоПоиска(СтрокаПоиска, РезультатыПоиска, ДополнительныеПараметры) Экспорт
	
	Если СтрНайти(СтрокаПоиска, ПлюсСервисКлиентСервер.АдресСервиса()) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПлюсСервисКлиент.Используется() Тогда
		
		ТекстОшибки = НСтр("ru = 'Сервис 1С:Плюс недоступен для использования в данной конфигурации'");
		Результат = Новый ЭлементРезультатаГлобальногоПоиска(СтрокаПоиска, ТекстОшибки, ,
			СтандартныйВидГлобальногоПоиска.НавигационнаяСсылка);
		
		РезультатыПоиска.Добавить(Результат);
		Возврат;
	КонецЕсли;
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(СтрокаПоиска);
	НачалоПараметров = СтрНайти(СтруктураURI.ПутьНаСервере, "?");
	Если НачалоПараметров = 0 Тогда
		Возврат;
	КонецЕсли;

	ЗначенияПараметров = ЗначенияПараметровURL(Сред(СтруктураURI.ПутьНаСервере, НачалоПараметров + 1));
	ПараметрыСостояния = ЗначенияПараметров.Получить("state");
	Если Не ЗначениеЗаполнено(ПараметрыСостояния) Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияПараметров = ЗначенияПараметровURL(СтрЗаменить(СтрЗаменить(ПараметрыСостояния, "%3D", "="), "%2B", "&"));
	
	ПубличныйИдентификатор = ЗначенияПараметров.Получить(ПлюсСервисКлиентСервер.ПолеПубличныйИдентификатор());
	ИдентификаторВерсии = ЗначенияПараметров.Получить(ПлюсСервисКлиентСервер.ПолеИдентификаторВерсии());
	
	Если Не ЗначениеЗаполнено(ПубличныйИдентификатор) И Не ЗначениеЗаполнено(ИдентификаторВерсии) Тогда
		Возврат;
	КонецЕсли;
	
	УстановкаРазрешена = ЕстьПравоУстановкиРасширений();

	Если Не УстановкаРазрешена Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("ПубличныйИдентификатор", ПубличныйИдентификатор);
	ПараметрыПоиска.Вставить("ИдентификаторВерсии", ИдентификаторВерсии);

	//@skip-check use-non-recommended-method - Особенность реализации
	ФормаОбработки = ПолучитьФормуВзаимодействияССервисом();
	ИдентификаторСеанса = ФормаОбработки.ИдентификаторСеанса;
	УстановкаРазрешена = ФормаОбработки.УстановкаРазрешена;
	
	Попытка
		ДанныеРасширения = ПлюсСервисВызовСервера.ПолучитьДанныеРасширения(ИдентификаторСеанса, ПараметрыПоиска);
	Исключение
		ТекстОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Результат = Новый ЭлементРезультатаГлобальногоПоиска(СтрокаПоиска, ТекстОшибки, ,
			СтандартныйВидГлобальногоПоиска.НавигационнаяСсылка);
		РезультатыПоиска.Добавить(Результат);
		Возврат;
	КонецПопытки;

	Картинка = ПолучитьИзВременногоХранилища(ДанныеРасширения.АдресКартинки);
	
	Результат = Новый ЭлементРезультатаГлобальногоПоиска(
		ИмяРезультатаПоиска1СПлюс(), 
		СтроковыеФункцииКлиент.ФорматированнаяСтрока(НСтр(
			"ru = '<b>%1 (Дополнение 1С:Плюс)</b>
			|%2'"), 
			ДанныеРасширения.Наименование, 
			ДанныеРасширения.КраткаяИнформация), Картинка);
	
	Если Не ДанныеРасширения.ЭтоРасширение Тогда
		Действие = ДействиеРезультатаГлобальногоПоиска(ПлюсСервисКлиентСервер.КомандаОткрытьНастройки(),
			НСтр("ru = 'Настроить'"), ДанныеРасширения);
		Результат.Действия.Добавить(Действие, Действие.Представление);
	ИначеЕсли ДанныеРасширения.Установлено Тогда
		Действие = ДействиеРезультатаГлобальногоПоиска(ПлюсСервисКлиентСервер.КомандаРасширениеУстановлено(),
			НСтр("ru = 'Расширение уже установлено'"), ДанныеРасширения);
		Результат.Действия.Добавить(Действие, Действие.Представление);
	ИначеЕсли УстановкаРазрешена Тогда
		Действие = ДействиеРезультатаГлобальногоПоиска(ПлюсСервисКлиентСервер.КомандаУстановитьРасширение(), 
			НСтр("ru = 'Установить расширение'"), ДанныеРасширения);
		Результат.Действия.Добавить(Действие, Действие.Представление);
	КонецЕсли;
	
	Действие = ДействиеРезультатаГлобальногоПоиска(ПлюсСервисКлиентСервер.КомандаПосмотретьОписание(),
		НСтр("ru = 'Посмотреть описание'"), ДанныеРасширения);
	Результат.Действия.Добавить(Действие, Действие.Представление);
		
	РезультатыПоиска.Добавить(Результат);

КонецПроцедуры

Процедура ОткрытьСервисБезПодключения(ПубличныйИдентификатор, ИдентификаторВерсии, КаналUTM) Экспорт
	
	АдресСервиса = ПлюсСервисКлиентСервер.АдресСервиса();
	Если ЗначениеЗаполнено(ПубличныйИдентификатор) Тогда
		АдресПерехода = СтрШаблон("%1/?state=%2%3%4", АдресСервиса,
			ПлюсСервисКлиентСервер.ПолеПубличныйИдентификатор(), "%3D", ПубличныйИдентификатор);
	Иначе
		АдресПерехода = СтрШаблон("%1/?state=%2%3%4", АдресСервиса, ПлюсСервисКлиентСервер.ПолеИдентификаторВерсии(),
			"%3D", ИдентификаторВерсии);
	КонецЕсли;
	АдресПерехода = СтрШаблон("%1&utm_source=1Capp&utm_medium=%2", АдресПерехода, КаналUTM);

	ПерейтиПоНавигационнойСсылке(АдресПерехода);

КонецПроцедуры

// Действие результата глобального поиска.
// 
// Параметры:
//  Команда - Строка
//  Представление - Строка
//  ДанныеРасширения - см. ПлюсСервисВызовСервера.ПолучитьДанныеРасширения
//  
// Возвращаемое значение:
//  Структура:
//   * Команда - Строка
//   * Представление - Строка
//   * ДанныеРасширения - см. ПлюсСервисВызовСервера.ПолучитьДанныеРасширения
//	 * КаналUTM - Строка
Функция ДействиеРезультатаГлобальногоПоиска(Команда, Представление, ДанныеРасширения) Экспорт
	
	Действие = Новый Структура;
	Действие.Вставить("Команда", Команда);
	Действие.Вставить("Представление", Представление);
	Действие.Вставить("ДанныеРасширения", ДанныеРасширения);
	Действие.Вставить("КаналUTM", "GlobalSearch");
	
	Возврат Действие;
	
КонецФункции

Функция ЗначенияПараметровURL(СтрокаПараметров) Экспорт
	
	Параметры = СтрРазделить(СтрокаПараметров, "&");
	
	ЗначенияПараметров = Новый Соответствие;
	Для Каждого Параметр Из Параметры Цикл
		ПоложениеПрисвоения = СтрНайти(Параметр, "=");
		Если ПоложениеПрисвоения = 0 Тогда
			ЗначенияПараметров.Вставить(Параметр);
		Иначе
			ЗначенияПараметров.Вставить(Лев(Параметр, ПоложениеПрисвоения - 1), Сред(Параметр, ПоложениеПрисвоения + 1));
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЗначенияПараметров;
	
КонецФункции

Функция ЕстьПравоУстановкиРасширений() Экспорт
	
	Возврат ПользователиКлиент.ЭтоПолноправныйПользователь()
		Или ПользователиКлиент.ЭтоПолноправныйПользователь(Истина);;
	
КонецФункции

Функция ИмяРезультатаПоиска1СПлюс() Экспорт
	
	Возврат "ДополнениеСервиса1СПлюс";
	
КонецФункции

Функция СобытиеОповещения() Экспорт
	
	Возврат "ОповещениеСервиса1СПлюс";
	
КонецФункции

Функция СобытиеЗакрытияОкнаСостояния() Экспорт
	Возврат "ЗакрытиеОкнаСостояния1СПлюс";
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьОбновлениеРасширений(Результат)
	
	ТекстОповещения = НСтр("ru = 'Получены обновления расширений из 1С:Плюс'");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуУстановкиРасширений", ЭтотОбъект);
	ПоказатьОповещениеПользователя(ТекстОповещения, 
		ОписаниеОповещения, 
		СтрСоединить(Результат, Символы.ПС) + Символы.ПС + НСтр("ru = 'Нажмите здесь для завершения установки'"), 
		БиблиотекаКартинок.ДиалогИнформация, 
		СтатусОповещенияПользователя.Важное,
		"ОбновлениеРасширений");
	
КонецПроцедуры

Процедура ОбработатьОтключениеАвтообновленияРасширений(Результат)
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Результат.Свойство("Расширения") Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОткрытьФормуВзаимодействияССервисом",
		ЭтотОбъект,
		Новый Структура("ОжидаетсяОповещение", Истина));
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Отключено автообновление для расширений из 1С:Плюс'"), 
		ОписаниеОповещения, 
		НСтр("ru = 'Нажмите здесь для ознакомления со списком расширений у которых отключено автообновление.'"), 
		БиблиотекаКартинок.ДиалогИнформация, 
		СтатусОповещенияПользователя.Важное,
		"ОтключениеАвтообновленияРасширений");
	
КонецПроцедуры

Процедура НажатиеНаРекомендациюПлюс(ПубличныйИдентификатор) Экспорт
	
	КаналUTM = "UserNotification";
	УстановкаРазрешена = ЕстьПравоУстановкиРасширений();

	Если Не УстановкаРазрешена Тогда
		АдресСервиса = ПлюсСервисКлиентСервер.АдресСервиса();
		АдресПерехода = СтрШаблон("%1/?state=%2%3%4", АдресСервиса,
			ПлюсСервисКлиентСервер.ПолеПубличныйИдентификатор(), "%3D", ПубличныйИдентификатор);
		АдресПерехода = СтрШаблон("%1&utm_source=1Capp&utm_medium=%2", АдресПерехода, КаналUTM);

		ПерейтиПоНавигационнойСсылке(АдресПерехода);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ОжидаетсяОповещение", Истина);
	ФормаОбработки = ПолучитьФормуВзаимодействияССервисом(ПараметрыФормы);
	ИдентификаторСеанса = ФормаОбработки.ИдентификаторСеанса;
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("ПубличныйИдентификатор", ПубличныйИдентификатор);	
	
	Попытка
		ДанныеРасширения = ПлюсСервисВызовСервера.ПолучитьДанныеРасширения(ИдентификаторСеанса, ПараметрыПоиска);
	Исключение
		ТекстОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецПопытки;
	
	Действие = Новый Структура;
	Действие.Вставить("Команда", ПлюсСервисКлиентСервер.КомандаПосмотретьОписание());
	Действие.Вставить("КаналUTM", КаналUTM);
	Действие.Вставить("ДанныеРасширения", ДанныеРасширения);
	
	Если ФормаОбработки.Открыта() Тогда
		ФормаОбработки.Активизировать();
	Иначе
		ОткрытьФорму(ФормаОбработки);
	КонецЕсли;
	Оповестить(СобытиеОповещения(), Действие);
	
КонецПроцедуры

// Параметры:
//  Рекомендация - См. ПлюсСервисКлиентСервер.ПараметрыРекомендацииДляОповещенияПользователя
Процедура ПоказатьРекомендацию(Рекомендация) Экспорт

	Попытка
		Логотип = Новый Картинка(ПолучитьИзВременногоХранилища(Рекомендация.АдресЛоготип));
		Действие = Новый ОписаниеОповещения("НажатиеНаРекомендациюПлюс", ЭтотОбъект, Рекомендация.ИдРасширения);

		ПоказатьОповещениеПользователя(Рекомендация.Заголовок, Действие, Рекомендация.Описание, Логотип,
			СтатусОповещенияПользователя.Важное);

		ДобавитьВИсториюПоказов(Рекомендация);
	Исключение
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ДобавитьВИсториюПоказов(Рекомендация, Ложь, ТекстОшибки);
	КонецПопытки;

КонецПроцедуры

// См. ОбщегоНазначенияКлиентПереопределяемый.ПередПериодическойОтправкойДанныхКлиентаНаСервер
Процедура ПередПериодическойОтправкойДанныхКлиентаНаСервер(Параметры) Экспорт

	Если Не ПлюсСервисКлиент.Используется() Тогда
		Возврат;
	КонецЕсли;

	ПлюсСервисПараметрыПриложения = ПолучитьПараметрыПриложения();

	ИмяПараметра = ПлюсСервисКлиентСервер.ИмяПараметраПриложенияПлюсРекомендации();
	ПлюсРекомендацииПараметрыПриложения = ПлюсСервисПараметрыПриложения[ИмяПараметра];

	ПараметрЛимитИсчерпан = ПлюсСервисКлиентСервер.ИмяПараметраПриложенияЛимитИсчерпан();
	ЛимитИсчерпан = ПлюсРекомендацииПараметрыПриложения[ПараметрЛимитИсчерпан];
	ПараметрИсторияПоказов = ПлюсСервисКлиентСервер.ИмяПараметраПриложенияИсторияПоказов();
	ИсторияПоказов = ПлюсРекомендацииПараметрыПриложения[ПараметрИсторияПоказов];
	
	Если ЛимитИсчерпан И ИсторияПоказов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Параметры.Вставить("ПлюсСервисПараметрыПриложения", ПлюсСервисПараметрыПриложения);

КонецПроцедуры

// См. ОбщегоНазначенияКлиентПереопределяемый.ПослеПериодическогоПолученияДанныхКлиентаНаСервере
Процедура ПослеПериодическогоПолученияДанныхКлиентаНаСервере(Результаты) Экспорт

	Если Результаты = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПлюсСервисПараметрыПриложения = Результаты.Получить("ПлюсСервисПараметрыПриложения");
	Если ПлюсСервисПараметрыПриложения = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ИмяПараметра = ПлюсСервисКлиентСервер.ИмяПараметраПриложенияПлюсРекомендации();
	ОбновлениеПараметровРекомендаций = ПлюсСервисПараметрыПриложения.Получить(ИмяПараметра);
	Если ОбновлениеПараметровРекомендаций <> Неопределено Тогда
		ОбновитьПараметрыПриложения(ОбновлениеПараметровРекомендаций);
	КонецЕсли;

	Рекомендация = Результаты.Получить("Рекомендация");
	Если Рекомендация <> Неопределено Тогда
		ПоказатьРекомендацию(Рекомендация);
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьПараметрыПриложения()

	ПлюсСервисПараметрыПриложения = Новый Соответствие;

	Если ПараметрыПриложения = Неопределено Тогда
		ПараметрыПриложения = Новый Соответствие;
	КонецЕсли;

	ИмяПараметра = ПлюсСервисКлиентСервер.ИмяПараметраПриложенияПлюсРекомендации();

	Если ПараметрыПриложения.Получить(ИмяПараметра) = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПараметра, Новый Соответствие);
		
		ПараметрИсторияОткрытыхФорм = ПлюсСервисКлиентСервер.ИмяПараметраПриложенияИсторияОткрытыхФорм();
		ПараметрЛимитИсчерпан = ПлюсСервисКлиентСервер.ИмяПараметраПриложенияЛимитИсчерпан();
		ПараметрИсторияПоказов = ПлюсСервисКлиентСервер.ИмяПараметраПриложенияИсторияПоказов();
		
		ПараметрыПриложения[ИмяПараметра].Вставить(ПараметрИсторияОткрытыхФорм, Новый Массив);
		ПараметрыПриложения[ИмяПараметра].Вставить(ПараметрЛимитИсчерпан, Ложь);
		ПараметрыПриложения[ИмяПараметра].Вставить(ПараметрИсторияПоказов, Новый Массив);
	КонецЕсли;

	ПлюсСервисПараметрыПриложения.Вставить(ИмяПараметра, ПараметрыПриложения[ИмяПараметра]);

	Возврат ПлюсСервисПараметрыПриложения;

КонецФункции

Процедура ОбновитьПараметрыПриложения(ОбновлениеПараметровРекомендаций)

	ПлюсРекомендацииПараметрыПриложения = ПлюсРекомендацииПараметрыПриложения();

	Для Каждого КлючИЗначение Из ПлюсРекомендацииПараметрыПриложения Цикл

		ОбновленноеЗначение = ОбновлениеПараметровРекомендаций.Получить(КлючИЗначение.Ключ);
		Если ОбновленноеЗначение = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		ПлюсРекомендацииПараметрыПриложения.Вставить(КлючИЗначение.Ключ, ОбновленноеЗначение);

	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьВИсториюПоказов(Рекомендация, Успешно = Истина, ТекстОшибки = "")

	ПлюсРекомендацииПараметрыПриложения = ПлюсРекомендацииПараметрыПриложения();
	ПараметрИсторияПоказов = ПлюсСервисКлиентСервер.ИмяПараметраПриложенияИсторияПоказов();
	ИсторияПоказов = ПлюсРекомендацииПараметрыПриложения[ПараметрИсторияПоказов];

	РезультатаПоказа = ПлюсСервисКлиентСервер.ПараметрыРекомендацииДляИсторииПоказов();
	ЗаполнитьЗначенияСвойств(РезультатаПоказа, Рекомендация);
	Если Успешно Тогда
		РезультатаПоказа.Вставить("ДатаВыполнения", ОбщегоНазначенияКлиент.ДатаСеанса());
		РезультатаПоказа.Вставить("Выполнено", Истина);
	Иначе
		РезультатаПоказа.Вставить("ТекстОшибки", ТекстОшибки);
	КонецЕсли;

	ИсторияПоказов.Добавить(РезультатаПоказа);
	ПлюсРекомендацииПараметрыПриложения.Вставить(ПараметрИсторияПоказов, ИсторияПоказов);
	ОбновитьПараметрыПриложения(ПлюсРекомендацииПараметрыПриложения);

КонецПроцедуры

Функция ПлюсРекомендацииПараметрыПриложения()

	ИмяПараметра = ПлюсСервисКлиентСервер.ИмяПараметраПриложенияПлюсРекомендации();

	ПлюсСервисПараметрыПриложения = ПолучитьПараметрыПриложения();
	ПлюсРекомендацииПараметрыПриложения = ПлюсСервисПараметрыПриложения[ИмяПараметра];

	Возврат ПлюсРекомендацииПараметрыПриложения;

КонецФункции

#КонецОбласти
