// Обработчик подписки на событие ЗаписатьВСписокБизнесПроцессов.
//
Процедура ЗаписатьВСписокБизнесПроцессов(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если МиграцияДанныхИзВнешнихСистемСервер.ЭтоОбъектИзДругойСистемы(Источник.ИсточникДанных)
		И Не ПараметрыСеанса.МиграцияДанныхРазрешеноПрерываниеПроцессов Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотУзел = ОбменДаннымиДокументооборотПовтИсп.ИдентификаторТекущегоУзла();
	Если Источник.УзелОбработки <> ЭтотУзел Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ВидЗаписи")
		И Источник.ДополнительныеСвойства.ВидЗаписи <> "ЗаписьСОбновлениемОбщегоСпискаПроцессов"
		И Источник.ДополнительныеСвойства.ВидЗаписи <> 
			"ЗаписьС_ОбновлениемОбщегоСпискаПроцессовИРезультатовВыполнения_ЗаполнениемУИДИсполнителей"
		И Источник.ДополнительныеСвойства.ВидЗаписи <> 
			"ЗаписьСОбновлением_Предметов_ПредметовЗадач_Проекта_ОбщегоСпискаПроцессов_РабочихГруппПредметов_РабочихГруппПроцессов_ДопРеквизитовПоПредметам" Тогда
		
		Возврат;
	КонецЕсли;
	
	ЗначенияПолей = БизнесПроцессыИЗадачиВызовСервера.ЗначениеПолейПроцессаДляЗаписиВОбщийСписок(Источник);
	БизнесПроцессыИЗадачиВызовСервера.ЗаписатьВСписокБизнесПроцессов(Источник.Ссылка, ЗначенияПолей);

КонецПроцедуры

// Обработчик подписки на событие УстановитьПометкуУдаленияЗадач.
//
Процедура УстановитьПометкуУдаленияЗадач(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда 
        Возврат;  
	КонецЕсли; 
	
	Если МиграцияДанныхИзВнешнихСистемСервер.ЭтоОбъектИзДругойСистемы(Источник.ИсточникДанных) Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ЭтоНовый() Тогда 
        Возврат;  
	КонецЕсли; 
	
	Если Источник.ДополнительныеСвойства.Свойство("ВидЗаписи") Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущаяПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "ПометкаУдаления");
	Если Источник.ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда
		УстановитьПривилегированныйРежим(Истина);
		БизнесПроцессыИЗадачиСервер.УстановитьПометкуУдаленияЗадач(Источник.Ссылка, Источник.ПометкаУдаления);
	КонецЕсли;	
	
КонецПроцедуры

Процедура ЗадачаПриЗаписиПриЗаписи(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	БизнесПроцессыИЗадачиСервер.ПриЗаписиЗадачиСервер(Источник);
	
КонецПроцедуры

Процедура БизнесПроцессПриКопировании(Источник, ОбъектКопирования) Экспорт
	
	Источник.Состояние = Перечисления.СостоянияБизнесПроцессов.Активен;
	
	Мультипредметность.БизнесПроцессПриКопировании(Источник);
	
КонецПроцедуры

