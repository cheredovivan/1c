
#Область ПрограммныйИнтерфейс

#Область ФормаЭлемента

// Вызывается из ПриСозданииНаСервере формы элемента справочника ВидыДокументов
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
Процедура ПриСозданииНаСервере_ФормаЭлемента(Форма, Отказ, СтандартнаяОбработка) Экспорт
// Локализация
	Элементы = Форма.Элементы;
	
	Форма.ИспользоватьЭДОВДокументообороте = ПолучитьФункциональнуюОпцию("ИспользоватьЭДОВДокументообороте");
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЭДОВДокументообороте") Тогда
		Элементы.РолиФайловФайлЭлектронногоДокумента.Видимость = Ложь;
		Элементы.СтраницаНастройкиЭДО.Видимость = Ложь;
	Иначе
		Форма.ИспользоватьОбменЭД = Истина;
	КонецЕсли;
	
	Форма.ЕстьПравоНастройкиЭДО = ОбменЭДОДокументооборот.ЕстьПравоНастройкиОтправкиЭДО();
	Элементы.СтраницаНастройкиЭДО.ТолькоПросмотр = Не Форма.ЕстьПравоНастройкиЭДО;
	
	Если Не ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
		ОбменЭДОДокументооборотКлиентСервер.УстановитьВидимостьПодсказкиФорматаЭДО(Форма, Форма.ФорматДокумента,
			"ФорматДокумента", Форма.ИспользоватьЭДОВДокументообороте);
	КонецЕсли;
// Локализация Конец
КонецПроцедуры

// Вызывается из ОбработкаПроверкиЗаполненияНаСервере формы элемента справочника ВидыДокументов
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  ПроверяемыеРеквизиты - Строка - Проверяемые реквизиты
Процедура ОбработкаПроверкиЗаполненияНаСервере_ФормаЭлемента(Форма, Отказ, ПроверяемыеРеквизиты) Экспорт
// Локализация
	ОбработкаПроверкиЗаполненияНастроекЭДО(Форма, Отказ);
// Локализация Конец
КонецПроцедуры

// Вызывается из ПослеЗаписиНаСервере формы элемента справочника ВидыДокументов
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ТекущийОбъект - СправочникОбъект.ВидыДокументов
//  ПараметрыЗаписи - Структура - Параметры записи
Процедура ПослеЗаписиНаСервере_ФормаЭлемента(Форма, ТекущийОбъект, ПараметрыЗаписи) Экспорт
// Локализация
	Форма.ЗаполнитьНастройкиЭДО(ТекущийОбъект);
// Локализация Конец
КонецПроцедуры

// Вызывается из ПриЧтенииНаСервере формы элемента справочника ВидыДокументов
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения 
//  ТекущийОбъект - СправочникОбъект.ВидыДокументов
Процедура ПриЧтенииНаСервере_ФормаЭлемента(Форма, ТекущийОбъект) Экспорт
// Локализация
	Форма.ЗаполнитьНастройкиЭДО(ТекущийОбъект);
	ОпределитьУчастиеВидаДокументаВОтраженииЭДО(Форма);
// Локализация Конец
КонецПроцедуры

// Вызывается из ПередЗаписьюНаСервере формы элемента справочника ВидыДокументов
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения 
//  Отказ - Булево
//  ТекущийОбъект - СправочникОбъект.ВидыДокументов
//  ПараметрыЗаписи - Структура - Параметры записи
Процедура ПередЗаписьюНаСервере_ФормаЭлемента(Форма, Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт
// Локализация
	ПередатьНастройкиЭДОДляЗаписи(Форма, ТекущийОбъект);
// Локализация Конец
КонецПроцедуры

#КонецОбласти

#Область МодульОбъекта

// Вызывается из ПриЗаписи модуля объекта справочника ВидыДокументов
//
// Параметры:
//  Отказ - Булево
//  Объект - СправочникОбъект.ВидыДокументов
Процедура ПриЗаписи(Отказ, Объект) Экспорт

// Локализация
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД")
		И Объект.ДополнительныеСвойства.Свойство("НастройкиОтправкиЭДО") Тогда
		РегистрыСведений.НастройкиОтправкиДокументовПоЭДО.ЗаписатьНаборНастроек(Объект.Ссылка,
			Объект.ДополнительныеСвойства.НастройкиОтправкиЭДО);
	КонецЕсли;
		
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД") 
		И Не Объект.ЭтоГруппа 
		И Не Объект.ВестиУчетТоваровИУслуг
		И Объект.ДополнительныеСвойства.ПредыдущиеЗначенияРеквизитов.ВестиУчетТоваровИУслуг Тогда
		
		УдалитьСтрокуЗаполненияТоваровВПравилах(Объект.Ссылка);
		
	КонецЕсли;
// Локализация Конец

КонецПроцедуры

#КонецОбласти

#КонецОбласти

// Локализация
#Область СлужебныеПроцедурыИФункции

#Область ЭДО

// Передать настройки ЭДО для записи.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения 
//  ТекущийОбъект - СправочникОбъект.ВидыДокументов 
Процедура ПередатьНастройкиЭДОДляЗаписи(Форма, ТекущийОбъект) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД") Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаДляРегистра = Форма.ТаблицаНастроекЭДО.Выгрузить(, "Отправитель, Получатель, ВидДокументаЭДО,
		| ИдентификаторОтправителя, ИдентификаторПолучателя, Отправлять, ТребуетсяОтветнаяПодпись,
		| ТребуетсяИзвещениеОПолучении, Формат");
	
	Отбор = Новый Структура("Отправлять", Истина);
	Отбор.Вставить("Отправлять", Истина);
	Отбор.Вставить("ВидДокументаЭДО", Справочники.ВидыДокументовЭДО.ПустаяСсылка());
	
	СтрокиКУдалению	= ТаблицаДляРегистра.НайтиСтроки(Отбор);
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		ТаблицаДляРегистра.Удалить(СтрокаКУдалению);
	КонецЦикла;
	
	Для Каждого СтрТЗ Из ТаблицаДляРегистра Цикл
		Если НЕ ЗначениеЗаполнено(СтрТЗ.ВидДокументаЭДО) Тогда
			СтрТЗ.ТребуетсяИзвещениеОПолучении = Ложь;
			СтрТЗ.ТребуетсяОтветнаяПодпись = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	ЕстьОбщиеИзменения = Форма.ВидДокументаЭДО <> Форма.ВидДокументаЭДОНачальноеЗначение
		Или Форма.ФорматДокумента <> Форма.ФорматНачальноеЗначение
		Или Форма.ТребуетсяИзвещениеОПолучении <> Форма.ТребуетсяИзвещениеОПолученииНачальноеЗначение
		Или Форма.ТребуетсяОтветнаяПодпись <> Форма.ТребуетсяОтветнаяПодписьНачальноеЗначение;
	
	ЕстьЧастныеИзменения = Ложь;	
	Если Не ЕстьОбщиеИзменения Тогда
		ЕстьЧастныеИзменения = Не ОбщегоНазначенияДокументооборот.ДанныеСовпадают(ТаблицаДляРегистра,
			Форма.ТаблицаНастроекЭДОНачальныеЗначения.Выгрузить());
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Форма.ВидДокументаЭДО) Тогда
		НоваяНастройка = ТаблицаДляРегистра.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяНастройка, Форма);
		НоваяНастройка.Отправлять = Истина;
		НоваяНастройка.Формат = Форма.ФорматДокумента;
	КонецЕсли;
	
	Если ЕстьОбщиеИзменения Или ЕстьЧастныеИзменения Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("НастройкиОтправкиЭДО", ТаблицаДляРегистра);
	КонецЕсли;
	
КонецПроцедуры

// Обработка проверки заполнения настроек ЭДО.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
//  Отказ - Булево - Отказ
Процедура ОбработкаПроверкиЗаполненияНастроекЭДО(Форма, Отказ) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД") Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьЗаполнениеНастроекЭДО(Форма, Отказ);
	ПроверитьДублированиеНастроекЭДО(Форма, Отказ);
	
КонецПроцедуры

// Проверяет корректность заполнения настроек ЭДО.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
//  Отказ - Булево - принимает значение Истина, если найдены не заполненные поля
Процедура ПроверитьЗаполнениеНастроекЭДО(Форма, Отказ)
	
	Для Каждого СтрокаНастроек Из Форма.ТаблицаНастроекЭДО Цикл
		
		НомерСтроки = СтрокаНастроек.НомерСтроки - 1;
		
		Если (Не СтрокаНастроек.Отправлять Или ЗначениеЗаполнено(СтрокаНастроек.ВидДокументаЭДО))
			И Не ЗначениеЗаполнено(СтрокаНастроек.Отправитель) Тогда
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(НСтр("ru = 'Не заполнено поле ""Отправитель"" в строке %1'"), СтрокаНастроек.НомерСтроки),,
					"ТаблицаНастроекЭДО[" + Формат(НомерСтроки, "ЧН=; ЧГ=") + "].Отправитель",, Отказ);
		КонецЕсли;

		Если (Не СтрокаНастроек.Отправлять Или ЗначениеЗаполнено(СтрокаНастроек.ВидДокументаЭДО))
			И Не ЗначениеЗаполнено(СтрокаНастроек.Получатель) Тогда
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(НСтр("ru = 'Не заполнено поле ""Получатель"" в строке %1'"), СтрокаНастроек.НомерСтроки),,
					"ТаблицаНастроекЭДО[" + Формат(НомерСтроки, "ЧН=; ЧГ=") + "].Получатель",, Отказ);
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

// Проверяет наличие дублей настроек ЭДО.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
//  Отказ - Булево - принимает значение Истина, если найдены дубли настроек
Процедура ПроверитьДублированиеНастроекЭДО(Форма, Отказ)
	
	ТЗ_Настройки = Форма.ТаблицаНастроекЭДО.Выгрузить();
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПоискаДублейНастроекЭДО();
	Запрос.УстановитьПараметр("ТЗ_Настройки", ТЗ_Настройки);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(НСтр(
			"ru = 'По отправителю %1 и получателю %2 есть дубль настройки в строке %3'"), Выборка.Отправитель,
				Выборка.Получатель, Выборка.НомерСтроки), , "ТаблицаНастроекЭДО[" + Формат(Выборка.НомерСтроки - 1,
					"ЧН=; ЧГ=") + "].НомерСтроки",, Отказ);
	КонецЦикла;	
	
КонецПроцедуры

// Возвращает текст запроса поиска дублей настроек ЭДО.
// 
// Возвращаемое значение:
//  Строка - Текст запроса поиска дублей настроек ЭДО
Функция ТекстЗапросаПоискаДублейНастроекЭДО()
	
	Возврат "ВЫБРАТЬ
	|	ТЗ_Настройки.Отправитель,
	|	ТЗ_Настройки.Получатель,
	|	ТЗ_Настройки.ВидДокументаЭДО,
	|	ТЗ_Настройки.Отправлять,
	|	ТЗ_Настройки.НомерСтроки
	|ПОМЕСТИТЬ ВТ_Настройки
	|ИЗ
	|	&ТЗ_Настройки КАК ТЗ_Настройки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Настройки.Отправитель КАК Отправитель,
	|	ВТ_Настройки.Получатель КАК Получатель,
	|	ВТ_Настройки.НомерСтроки
	|ПОМЕСТИТЬ ВТ_ОтборНастроек
	|ИЗ
	|	ВТ_Настройки КАК ВТ_Настройки
	|ГДЕ
	|	ВТ_Настройки.Отправитель <> Значение(Справочник.Организации.ПустаяСсылка)
	|	И НЕ ВТ_Настройки.Получатель В (Значение(Справочник.Организации.ПустаяСсылка),
	|		Значение(Справочник.Контрагенты.ПустаяСсылка), Неопределено)
	|	И ВТ_Настройки.Отправлять
	|	И ВТ_Настройки.ВидДокументаЭДО <> ЗНАЧЕНИЕ(Справочник.ВидыДокументовЭДО.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_Настройки.Отправитель,
	|	ВТ_Настройки.Получатель,
	|	ВТ_Настройки.НомерСтроки
	|ИЗ
	|	ВТ_Настройки КАК ВТ_Настройки
	|ГДЕ
	|	ВТ_Настройки.Отправитель <> Значение(Справочник.Организации.ПустаяСсылка)
	|	И НЕ ВТ_Настройки.Получатель В (Значение(Справочник.Организации.ПустаяСсылка),
	|		Значение(Справочник.Контрагенты.ПустаяСсылка), Неопределено)
	|	И НЕ ВТ_Настройки.Отправлять
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ОтборНастроек.Отправитель,
	|	ВТ_ОтборНастроек.Получатель
	|ПОМЕСТИТЬ ВТ_Дубли
	|ИЗ
	|	ВТ_ОтборНастроек КАК ВТ_ОтборНастроек
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ОтборНастроек.Отправитель,
	|	ВТ_ОтборНастроек.Получатель
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_ОтборНастроек.НомерСтроки) > 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ОтборНастроек.Отправитель,
	|	ВТ_ОтборНастроек.Получатель,
	|	ВТ_ОтборНастроек.НомерСтроки
	|ИЗ
	|	ВТ_Дубли КАК ВТ_Дубли
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОтборНастроек КАК ВТ_ОтборНастроек
	|		ПО ВТ_ОтборНастроек.Отправитель = ВТ_Дубли.Отправитель
	|		И ВТ_ОтборНастроек.Получатель = ВТ_Дубли.Получатель";
		
КонецФункции

// Устанавливает значение реквизита ВидДокументаУчаствуетВОтраженииДокументовЭДО
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
Процедура ОпределитьУчастиеВидаДокументаВОтраженииЭДО(Форма) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ШаблоныДляСозданияДокументовПоВходящимЭДО.Шаблон КАК Шаблон
		|ИЗ
		|	РегистрСведений.ШаблоныДляСозданияДокументовПоВходящимЭДО КАК ШаблоныДляСозданияДокументовПоВходящимЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШаблоныДокументов КАК ШаблоныДокументов
		|		ПО ШаблоныДляСозданияДокументовПоВходящимЭДО.Шаблон = ШаблоныДокументов.Ссылка
		|ГДЕ
		|	ШаблоныДокументов.ВидДокумента = &ВидДокумента";
	
	Запрос.УстановитьПараметр("ВидДокумента", Форма.Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Форма.ВидДокументаУчаствуетВОтраженииДокументовЭДО = ВыборкаДетальныеЗаписи.Следующий();
	
КонецПроцедуры

#КонецОбласти

Процедура УдалитьСтрокуЗаполненияТоваровВПравилах(ВидДокумента)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПравилаЗагрузкиДанныхВДО.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПравилаЗагрузкиДанныхВДО.ПравилаЗаполненияРеквизитовДО КАК ПравилаЗагрузкиДанныхВДОПравилаЗаполненияРеквизитовДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаЗагрузкиДанныхВДО КАК ПравилаЗагрузкиДанныхВДО
		|		ПО ПравилаЗагрузкиДанныхВДОПравилаЗаполненияРеквизитовДО.Ссылка = ПравилаЗагрузкиДанныхВДО.Ссылка
		|ГДЕ
		|	ПравилаЗагрузкиДанныхВДО.ВидДокумента = &ВидДокумента
		|	И ПравилаЗагрузкиДанныхВДОПравилаЗаполненияРеквизитовДО.ИмяРеквизитаОбъектаДО = ""Товары""";
	
	Запрос.УстановитьПараметр("ВидДокумента", ВидДокумента);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОбъектПравил = Выборка.Ссылка.ПолучитьОбъект();
		ПравилаЗаполнения = ОбъектПравил.ПравилаЗаполненияРеквизитовДО;
		СтрокаТовары = ПравилаЗаполнения.Найти("Товары", "ИмяРеквизитаОбъектаДО");
		ПравилаЗаполнения.Удалить(СтрокаТовары);
		ОбъектПравил.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
// Локализация Конец
