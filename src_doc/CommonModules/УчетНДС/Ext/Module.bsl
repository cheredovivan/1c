#Область ПрограммныйИнтерфейс

// Функция возвращает процент НДС.
//
// Параметры:
//  СтавкаНДС - СправочникСсылка.СтавкиНДС
//
// Возвращаемое значение:
//  Число - Процент НДС.
//
Функция ЗначениеСтавкиНДС(СтавкаНДС) Экспорт
	
	Если Не ЗначениеЗаполнено(СтавкаНДС) Тогда
		Возврат 0;
	КонецЕсли;

	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтавкаНДС, "Ставка");
	
КонецФункции

// Проверяет действие ставки на указанную дату.
//
// Параметры:
//  СтавкаНДС - СправочникСсылка.СтавкиНДС - проверяемая ставка НДС.
//  ДатаПроверки - Дата
//
// Возвращаемое значение:
//  Булево - Истина, если ставка НДС действует.
//
Функция СтавкаНДСДействует(СтавкаНДС, ДатаПроверки) Экспорт
	
	РеквизитыПериод = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтавкаНДС, "НачалоПериода, КонецПериода");
	
	Возврат (ДатаПроверки >= РеквизитыПериод.НачалоПериода Или Не ЗначениеЗаполнено(РеквизитыПериод.НачалоПериода))
		И (ДатаПроверки <= РеквизитыПериод.КонецПериода Или Не ЗначениеЗаполнено(РеквизитыПериод.КонецПериода))
	
КонецФункции

// Возвращает строковое представление периода действия ставки НДС.
//
// Параметры:
//  НачалоПериода - Дата
//  КонецПериода - Дата
//
// Возвращаемое значение:
//  Строка - строковое представление периода.
//
Функция ПериодДействияСтавкиНДССтрокой(НачалоПериода, КонецПериода) Экспорт
	
	ВключатьОписаниеГода = ЗначениеЗаполнено(КонецПериода)
		И НачалоГода(НачалоПериода) <> НачалоГода(КонецПериода);
	ОписаниеДатыНачала = ТекстовоеОписаниеДаты(НачалоПериода, ВключатьОписаниеГода);
	
	Если НачалоПериода = КонецПериода Тогда
		ОписаниеПериода = ОписаниеДатыНачала;
	ИначеЕсли ЗначениеЗаполнено(КонецПериода) Тогда
		ОписаниеДатыОкончания = ТекстовоеОписаниеДаты(КонецПериода, ВключатьОписаниеГода);
		ОписаниеПериода = СтрШаблон(НСтр("ru = 'с %1 по %2'"), ОписаниеДатыНачала, ОписаниеДатыОкончания);
	Иначе
		ОписаниеПериода = СтрШаблон(НСтр("ru = 'бессрочно с %1'"), ОписаниеДатыНачала, ОписаниеДатыОкончания);
	КонецЕсли;
	
	Возврат ОписаниеПериода;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Получение элемента справочника СтавкиНДС по числовому значению ставки.
//
// Параметры:
//   Значение - Число - если необходимо получить элемент по конкретному значению.
//   РасчетнаяСтавка -Булево - если необходимо получить расчетную ставку.
//
// Возвращаемое значение:
//   СправочникСсылка.СтавкиНДС - соответствующая перечислению ставка НДС.
//
Функция СтавкаНДСИзЧисла(Значение, РасчетнаяСтавка = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СтавкиНДС.Ссылка
		|ИЗ
		|	Справочник.СтавкиНДС КАК СтавкиНДС
		|ГДЕ
		|	СтавкиНДС.Ставка = &Ставка
		|	И СтавкиНДС.Ссылка <> ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
		|	И СтавкиНДС.РасчетнаяСтавка = &РасчетнаяСтавка
		// Локализация
		|И (СтавкиНДС.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
		|	ИЛИ СтавкиНДС.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка))
		// Локализация Конец
		|";
	
	Запрос.УстановитьПараметр("Ставка", Значение);
	Запрос.УстановитьПараметр("РасчетнаяСтавка", РасчетнаяСтавка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Справочники.СтавкиНДС.ПустаяСсылка();
	
КонецФункции

// Получение элемента справочника СтавкиНДС по одноименнному перечислению.
// Используется только в целях переноса данных.
//
// Параметры:
//  СтавкаНДС - ПеречислениеСсылка.УдалитьСтавкиНДС
// 
// Возвращаемое значение:
//  СправочникСсылка.СтавкиНДС - соответствующая перечислению ставка НДС в одноименном справочнике.
//
Функция СтавкаНДСИзПеречисления(СтавкаНДС) Экспорт
	
	Если СтавкаНДС = Перечисления.УдалитьСтавкиНДС.НДС20 Тогда
		Возврат СтавкаНДСИзЧисла(20);
	ИначеЕсли СтавкаНДС = Перечисления.УдалитьСтавкиНДС.НДС18 Тогда
		Возврат СтавкаНДСИзЧисла(18);
	ИначеЕсли СтавкаНДС = Перечисления.УдалитьСтавкиНДС.НДС10 Тогда
		Возврат СтавкаНДСИзЧисла(10);
	ИначеЕсли СтавкаНДС = Перечисления.УдалитьСтавкиНДС.НДС0 Тогда
		Возврат Справочники.СтавкиНДС.НДС0;
	ИначеЕсли СтавкаНДС = Перечисления.УдалитьСтавкиНДС.БезНДС Тогда
		Возврат Справочники.СтавкиНДС.БезНДС;
	Иначе
		Возврат Справочники.СтавкиНДС.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстовоеОписаниеДаты(Дата, ВключатьОписаниеГода)
	
	ФорматДаты = "d MMMM";
	Если НачалоГода(Дата) <> НачалоГода(ТекущаяДатаСеанса()) Или ВключатьОписаниеГода Тогда
		ФорматДаты = ФорматДаты + " yyyy";
	КонецЕсли;
	
	ФорматДаты = "ДФ='" + ФорматДаты + "'";
	ОписаниеДаты = НРег(Формат(Дата, ФорматДаты));
	
	Возврат ОписаниеДаты;
	
КонецФункции

#КонецОбласти
