
#Область ПрограммныйИнтерфейс

// Включает опции по умолчанию, в зависимости от редакции - ДГУ или КОРП
Процедура ИнициализацияПриПервомЗапуске() Экспорт
	
// Локализация
	Если ЭтоДокументооборотГосударственногоУчреждения() Тогда
		Константы.ВестиУчетОбращенийГраждан.Установить(Истина);
		Константы.РегламентированныйУчетОбращений.Установить(Истина);
		Константы.ДатаПроверкиНаличияВопросов.Установить(ТекущаяДатаСеанса());
		Константы.ИспользоватьУчетПоОрганизациям.Установить(Истина);
	КонецЕсли;
// Локализация Конец
	
КонецПроцедуры

// Меняет настройку условного оформления (а именно Текст) для РС НастройкаШаблоновБизнесПроцессов
//   Чтобы в ДГУ отличалось от КОРП.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения -
Процедура УсловноеОформление_НастройкаШаблоновБизнесПроцессов(Форма) Экспорт
	
	Если Форма.УсловноеОформление.Элементы.Количество() = 1 Тогда	
		Элем = Форма.УсловноеОформление.Элементы[0];
		
		ПарамТекст = Новый ПараметрКомпоновкиДанных("Текст");
		Для Каждого ЭлемОформления Из Элем.Оформление.Элементы Цикл
			
			Если ЭлемОформления.Параметр = ПарамТекст Тогда
				ЭлемОформления.Значение = РедакцииКонфигурацииКлиентСервер.ДляВсехОрганизаций();
			КонецЕсли;	
			
		КонецЦикла;	
		
	КонецЕсли;	
	
КонецПроцедуры

// Проверить/переименовать наименование предопределенных элементов
// ДГУ и КОРП - разное название термина "Организация", приводит наименование к текущей редакции
Процедура ПроверитьПереименоватьНаименованиеПредопределенных() Экспорт
	
	ПроверитьПереименоватьЭлементСправочника(
		Справочники.АвтоподстановкиДляОбъектов.РуководительОрганизации, 
		СтрШаблон(НСтр("ru = 'Руководитель %1'"), РедакцииКонфигурацииКлиентСервер.ОрганизацииРодительный()));
	
	ПроверитьПереименоватьЭлементСправочника(
		Справочники.АвтоподстановкиДляПроцессов.ВсеРуководителиПредприятия, 
		СтрШаблон(НСтр("ru = 'Все руководители %1'"), РедакцииКонфигурацииКлиентСервер.ПредприятияРодительный()));
	
КонецПроцедуры

// Текущая конфигурация - это редакция ДГУ - Документооборот государственного учреждения или ДГУ Регион
// 
// Возвращаемое значение:
//  Булево - Истина, если это ДГУ или ДГУ Регион
Функция ЭтоДокументооборотГосударственногоУчреждения() Экспорт
	
	Возврат РедакцииКонфигурацииСлужебный.ЭтоДокументооборотГосударственногоУчреждения();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьПереименоватьЭлементСправочника(СправочникСсылка, Наименование)
	
	ЭлементСправочника = СправочникСсылка.ПолучитьОбъект();
	Если СокрЛП(ЭлементСправочника.Наименование) = СокрЛП(Наименование) Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементСправочника.Наименование = Наименование;
	ЭлементСправочника.ОбменДанными.Загрузка = Истина;// в некоторых справочниках перед записью модифицируются и другие реквизиты, при начальной настройке это не нужно.
	ЭлементСправочника.Записать();
	
КонецПроцедуры

#КонецОбласти