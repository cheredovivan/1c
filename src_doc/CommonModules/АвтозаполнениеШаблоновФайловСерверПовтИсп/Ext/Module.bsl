
#Область СлужебныйПрограммныйИнтерфейс

// Формирует дерево значений с реквизитами и доп. реквизитами документа указанного типа.
//
// Параметры:
//  ВидВладельцаФайла - СправочникСсылка.ВидыДокументов - 
//						Текстовое описание параметра процедуры (функции).
//  ТипДокумента - Строка - Строковое представление типа документа
//
// Возвращаемое значение:
//  ДеревоЗначений - дерево значений с реквизитами документа
//	 * Наименование - Строка - Имя реквизита.
//	 * Тип - Строка - Имя типа реквизита.
//	 * ОбъектРодитель - Строка - полный путь до элемента дерева реквизитов
Функция ЗаполнитьДеревоРеквизитовДляВыбора(ВидВладельцаФайла, ТипДокумента) Экспорт
	
	ОбязательныеРеквизиты = Новый СписокЗначений;
	ИсключенияРеквизитов = Новый Соответствие;
	
	ИсключенияРеквизитов.Вставить("ИмяПредопределенныхДанных", Истина);
	ИсключенияРеквизитов.Вставить("ПометкаУдаления", Истина);
	ИсключенияРеквизитов.Вставить("Предопределенный", Истина);
	ИсключенияРеквизитов.Вставить("Подписал", Истина);
	ИсключенияРеквизитов.Вставить("ПодписанВсеми", Истина);
	ИсключенияРеквизитов.Вставить("УстановилПодпись", Истина);
	ИсключенияРеквизитов.Вставить("Утвердил", Истина);
	
	ИсключенияРеквизитов.Вставить("Логотип", Истина);
	
	Типы = Новый ОписаниеТипов("Строка");
	
	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("Наименование", Типы);
	Дерево.Колонки.Добавить("Тип", Типы);
	Дерево.Колонки.Добавить("ОбъектРодитель", Типы);
	Дерево.Колонки.Добавить("Описание", Типы);
	Дерево.Колонки.Добавить("Представление", Типы);
	
	НоваяСтрокаВидВладельцаФайла = Дерево.Строки.Добавить();
	НоваяСтрокаВидВладельцаФайла.Наименование = НСтр("ru = 'ВладелецФайла'");
	НоваяСтрокаВидВладельцаФайла.Представление = НСтр("ru = 'Владелец файла'");
	НоваяСтрокаВидВладельцаФайла.Тип = ТипДокумента;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
		ОбязательныеРеквизиты.Добавить("Организация");
	КонецЕсли;
	Если ТипЗнч(ВидВладельцаФайла) = Тип("СправочникСсылка.ВидыДокументов") 
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидВладельцаФайла, "ВестиУчетСторон") Тогда 
		ОбязательныеРеквизиты.Добавить("КонтактноеЛицо");
		ОбязательныеРеквизиты.Добавить("Контрагент");
	КонецЕсли;	
		
	КэшВидаДокумента = Делопроизводство.КэшВидаДокумента(ВидВладельцаФайла);
	ЭтоКорреспонденция = КэшВидаДокумента.ЯвляетсяВходящейКорреспонденцией
		Или КэшВидаДокумента.ЯвляетсяИсходящейКорреспонденцией;
	
	Для Каждого СправочникМетаданные Из Метаданные.Справочники Цикл
		Если СправочникМетаданные.ПредставлениеОбъекта = ТипДокумента Тогда
			ЗаполнитьДеревоРеквизитов(НоваяСтрокаВидВладельцаФайла, СправочникМетаданные.СтандартныеРеквизиты,
				"ВладелецФайла", 1, ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
			ЗаполнитьДеревоРеквизитов(НоваяСтрокаВидВладельцаФайла, СправочникМетаданные.Реквизиты,
				"ВладелецФайла", 1, ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
			Прервать;
		КонецЕсли
	КонецЦикла;
	
	Попытка
		НаборСвойств = УправлениеСвойствамиСлужебный.ПолучитьНаборыСвойствОбъекта(ВидВладельцаФайла);
		НоваяСтрокаДопРеквизиты = НоваяСтрокаВидВладельцаФайла.Строки.Добавить();
		НоваяСтрокаДопРеквизиты.Наименование = НСтр("ru = 'ДопРеквизиты'");
		НоваяСтрокаДопРеквизиты.Представление = НСтр("ru = 'ДопРеквизиты'");
		НоваяСтрокаДопРеквизиты.Тип = "";
		НоваяСтрокаДопРеквизиты.Описание = НСтр("ru = 'Дополнительные реквизиты'");
		
		Для Каждого ЭлементНабораСвойств Из НаборСвойств Цикл
			Для Каждого ДопРеквизит Из ЭлементНабораСвойств.Набор.ДополнительныеРеквизиты Цикл
				Если ДопРеквизит <> Неопределено И ЗначениеЗаполнено(ДопРеквизит.Свойство) Тогда
					НоваяСтрокаДопРеквизит = НоваяСтрокаДопРеквизиты.Строки.Добавить();
					НоваяСтрокаДопРеквизит.Наименование = ДопРеквизит.Свойство.Наименование;
					НоваяСтрокаДопРеквизит.Представление = ДопРеквизит.Свойство.Наименование;
					НоваяСтрокаДопРеквизит.Тип = ДопРеквизит.Свойство.ТипЗначения;
					НоваяСтрокаДопРеквизит.ОбъектРодитель = "ВладелецФайла.ДопРеквизиты";
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	Исключение
		// у владельца файла может не быть доп.реквизитов
	КонецПопытки;
	
	НоваяСтрокаФайл = Дерево.Строки.Добавить();
	НоваяСтрокаФайл.Наименование = НСтр("ru = 'Файл'");
	НоваяСтрокаФайл.Представление = НСтр("ru = 'Файл'");
	НоваяСтрокаФайл.Тип = НСтр("ru = 'Справочник'");
	НоваяСтрокаФайл.Описание = НСтр("ru = 'Файл'");
	
	ЗаполнитьДеревоРеквизитов(НоваяСтрокаФайл, Метаданные.Справочники.Файлы.СтандартныеРеквизиты,
		"Файл", 1, ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
	ЗаполнитьДеревоРеквизитов(НоваяСтрокаФайл, Метаданные.Справочники.Файлы.Реквизиты,
		"Файл", 1, ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
	
	Если ЭтоКорреспонденция Тогда
		
		ИсключенияРеквизитов.Вставить("Создал", Истина);
		ИсключенияРеквизитов.Вставить("ДатаСоздания", Истина);
		ИсключенияРеквизитов.Вставить("Основание", Истина);
		
		Если КэшВидаДокумента.ЯвляетсяИсходящейКорреспонденцией Тогда
			
			ИсключенияРеквизитов.Вставить("Адресат", Истина);
			
			ЗаполнитьДеревоРеквизитов(НоваяСтрокаВидВладельцаФайла, 
				Метаданные.Документы.Корреспонденция.ТабличныеЧасти.Корреспонденты.Реквизиты,
				"Корреспонденция|Корреспонденты", 1, ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
			
			ИсключенияРеквизитов.Вставить("ДатаКонтрагента", Истина);
			ИсключенияРеквизитов.Вставить("НомерКонтрагента", Истина);
			
		ИначеЕсли КэшВидаДокумента.ЯвляетсяВходящейКорреспонденцией Тогда
			
			ЗаполнитьДеревоРеквизитов(НоваяСтрокаВидВладельцаФайла, 
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
					Метаданные.Документы.Корреспонденция.ТабличныеЧасти.Корреспонденты.Реквизиты.Адресат),
				"Корреспонденция|Корреспонденты", 1, ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
			
		КонецЕсли;
		
		ЗаполнитьДеревоРеквизитов(НоваяСтрокаВидВладельцаФайла, Метаданные.Документы.Корреспонденция.Реквизиты,
			"Корреспонденция", 1, ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
		
		// Локализация
		Если КэшВидаДокумента.ЯвляетсяОбращениемОтГраждан Тогда
			НоваяСтрокаВопрос = НоваяСтрокаВидВладельцаФайла.Строки.Добавить();
			НоваяСтрокаВопрос.Наименование = НСтр("ru = 'Вопрос обращения'");
			НоваяСтрокаВопрос.Представление = НСтр("ru = 'Вопрос обращения'");
			НоваяСтрокаВопрос.Тип = "";
			НоваяСтрокаВопрос.Описание = НСтр("ru = 'Вопрос обращения'");
			
			ЗаполнитьДеревоРеквизитов(НоваяСтрокаВопрос,
				Метаданные.Документы.Корреспонденция.ТабличныеЧасти.ВопросыОбращения.Реквизиты,
				"Корреспонденция|ВопросыОбращения", 2, ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
		КонецЕсли;
		// Локализация Конец
			
		СтрокаНомерКонтрагента = НоваяСтрокаВидВладельцаФайла.Строки.Найти("НомерКонтрагента", "Наименование");
		Если КэшВидаДокумента.ЯвляетсяВходящейКорреспонденцией Тогда
			СтрокаНомерКонтрагента.Представление = НСтр("ru = 'Входящий номер'");
			СтрокаНомерКонтрагента.Описание = НСтр("ru = 'Входящий номер'");
		ИначеЕсли КэшВидаДокумента.ЯвляетсяИсходящейКорреспонденцией Тогда
			СтрокаНомерКонтрагента.Представление = НСтр("ru = 'Исходящий номер'");
			СтрокаНомерКонтрагента.Описание = НСтр("ru = 'Исходящий номер'");
		КонецЕсли;	
		
	КонецЕсли;
	
	СтрокаКонтрагент = НоваяСтрокаВидВладельцаФайла.Строки.Найти("Контрагент", "Наименование");
	Если СтрокаКонтрагент <> Неопределено Тогда
		
		Если КэшВидаДокумента.ЯвляетсяВходящейКорреспонденцией Тогда
			СтрокаКонтрагент.Представление = НСтр("ru = 'Отправитель'");
			СтрокаКонтрагент.Описание = НСтр("ru = 'Отправитель'");
		ИначеЕсли КэшВидаДокумента.ЯвляетсяИсходящейКорреспонденцией Тогда
			СтрокаКонтрагент.Представление = НСтр("ru = 'Получатель'");
			СтрокаКонтрагент.Описание = НСтр("ru = 'Получатель'");
		КонецЕсли;
		
		СтрокаОсновнойСчет = СтрокаКонтрагент.Строки.Найти("ОсновнойБанковскийСчет", "Наименование");
		ИсключенияРеквизитов.Вставить("СуммаБезКопеек", Истина);
		ИсключенияРеквизитов.Вставить("МесяцПрописью", Истина);
		РеквизитыСчета = Метаданные.Справочники.БанковскиеСчета.Реквизиты;
		ЗаполнитьДеревоРеквизитов(СтрокаОсновнойСчет, РеквизитыСчета,
			СтрокаОсновнойСчет.ОбъектРодитель+"|ОсновнойБанковскийСчет", 1, 
			ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
			
		СтрокаБанк = СтрокаОсновнойСчет.Строки.Найти("Банк", "Наименование");
		ЗаполнитьДеревоРеквизитов(СтрокаБанк,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
				Метаданные.Справочники.КлассификаторБанков.СтандартныеРеквизиты.Код),
			СтрокаБанк.ОбъектРодитель+"|Банк", 2, 
			ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
			
		СтрокаБанкДляРасчетов = СтрокаОсновнойСчет.Строки.Найти("БанкДляРасчетов", "Наименование");
		ЗаполнитьДеревоРеквизитов(СтрокаБанкДляРасчетов,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
				Метаданные.Справочники.КлассификаторБанков.СтандартныеРеквизиты.Код),
			СтрокаБанкДляРасчетов.ОбъектРодитель+"|БанкДляРасчетов", 2, 
			ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
		
	КонецЕсли;
	
	Строка = НоваяСтрокаВидВладельцаФайла.Строки.Добавить();
	Строка.Наименование = "";
	Строка.Представление = НСтр("ru = 'Подписывающие'");
	Строка.Тип = "Строка";
	Строка.ОбъектРодитель = НСтр("ru = 'Подписывающие'");
	Строка.Описание = "Все подписывающие документа через запятую";
	
	Дерево.Строки.Сортировать("Представление", Истина);
	
	Возврат Дерево;
	
КонецФункции

// Формирует дерево значений с реквизитами и доп. реквизитами документа указанного типа.
//
// Параметры:
//  ВидВладельцаФайла - СправочникСсылка.ВидыДокументов - 
//						Текстовое описание параметра процедуры (функции).
//  ТипДокумента - Тип - тип заполняемого документа.
//  Параметры - Структура - вспомогательные параметры.
//
// Возвращаемое значение:
//  ДеревоЗначений - дерево значений с реквизитами документа
//	 * Наименование - Строка - Имя реквизита.
//	 * Тип - Строка - Имя типа реквизита.
//	 * ОбъектРодитель - Строка - полный путь до элемента дерева реквизитов
Функция ЗаполнитьДеревоРеквизитовДляВыбораКраткойФормы(ВидВладельцаФайла, ТипДокумента, Параметры) Экспорт
	
	ОбязательныеРеквизиты = Новый СписокЗначений;
	ИсключенияРеквизитов = Новый Соответствие;
	
	ИсключенияРеквизитов.Вставить("ИмяПредопределенныхДанных", Истина);
	ИсключенияРеквизитов.Вставить("ПометкаУдаления", Истина);
	ИсключенияРеквизитов.Вставить("Предопределенный", Истина);
	ИсключенияРеквизитов.Вставить("Подписал", Истина);
	ИсключенияРеквизитов.Вставить("ПодписанВсеми", Истина);
	ИсключенияРеквизитов.Вставить("УстановилПодпись", Истина);
	ИсключенияРеквизитов.Вставить("Утвердил", Истина);
	ИсключенияРеквизитов.Вставить("ПредставлениеДолжностиПодразделения", Истина);
	ИсключенияРеквизитов.Вставить("ПредставлениеДолжностиПодразделенияЯзык1", Истина);
	ИсключенияРеквизитов.Вставить("ПредставлениеДолжностиПодразделенияЯзык2", Истина);
	ИсключенияРеквизитов.Вставить("Логотип", Истина);
	ИсключенияРеквизитов.Вставить("УдалитьДолжность", Истина);
	
	Типы = Новый ОписаниеТипов("Строка");
	ТипБулево = Новый ОписаниеТипов("Булево");
	КЧ = Новый КвалификаторыЧисла(10, 0);
	ТипЧисло = Новый ОписаниеТипов("Число",,, КЧ);
	КЧ1 = Новый КвалификаторыЧисла(1, 0);
	ТипЧисло1 = Новый ОписаниеТипов("Число",,, КЧ1);
	
	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("Наименование", Типы);
	Дерево.Колонки.Добавить("НаименованиеДляWord", Типы);
	Дерево.Колонки.Добавить("Тип", Типы);
	Дерево.Колонки.Добавить("ОбъектРодитель", Типы);
	Дерево.Колонки.Добавить("Описание", Типы);
	Дерево.Колонки.Добавить("ОписаниеПоляЗамены", Типы);
	Дерево.Колонки.Добавить("РеквизитРодитель", Типы);
	
	Дерево.Колонки.Добавить("ПолеФайла", Типы);
	Дерево.Колонки.Добавить("СоздатьПоле", ТипБулево);
	Дерево.Колонки.Добавить("РеквизитТабличнойЧасти", ТипБулево);
	Дерево.Колонки.Добавить("ДополнительныйРеквизит", ТипБулево);
	Дерево.Колонки.Добавить("КонтактнаяИнформация", ТипБулево);
	Дерево.Колонки.Добавить("НомерКолонкиТабличнойЧасти", ТипЧисло);
	Дерево.Колонки.Добавить("ФорматСвойства", Типы);
	Дерево.Колонки.Добавить("КоличествоПолей", ТипЧисло1);
	
	Дерево.Колонки.Добавить("ИспользоватьПадеж", ТипБулево);
	Дерево.Колонки.Добавить("Падеж", Типы);
	
	ИсключенияРеквизитов.Вставить("ВидДокумента", Истина);
	ИсключенияРеквизитов.Вставить("ЗапретитьАвтоматическоеДобавлениеУчастниковРабочейГруппы", Истина);
	ИсключенияРеквизитов.Вставить("ПодписанЭП", Истина);
	ИсключенияРеквизитов.Вставить("ЧисловойНомер", Истина);
	ИсключенияРеквизитов.Вставить("Шаблон", Истина);
	ИсключенияРеквизитов.Вставить("УдалитьЗапретитьАвтоматическоеДобавлениеУчастниковРабочейГруппы", Истина);
	ИсключенияРеквизитов.Вставить("РезультатПодписания", Истина);
	ИсключенияРеквизитов.Вставить("УстановилПодпись", Истина);
	ИсключенияРеквизитов.Вставить("Утвердил", Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
		ОбязательныеРеквизиты.Добавить("Организация");
	КонецЕсли;
	
	КэшВидаДокумента = Делопроизводство.КэшВидаДокумента(ВидВладельцаФайла);
	ЭтоКорреспонденция = КэшВидаДокумента.ЯвляетсяВходящейКорреспонденцией
		Или КэшВидаДокумента.ЯвляетсяИсходящейКорреспонденцией;
	
	СправочникМетаданные = Метаданные.Справочники.ДокументыПредприятия;
	ИсключенияРеквизитов.Вставить("Бессрочный", Истина);
	ИсключенияРеквизитов.Вставить("НеДействует", Истина);
	
	Если Не Параметры.УчитыватьНДС Тогда
		ИсключенияРеквизитов.Вставить("СуммаНДС", Истина);
		ИсключенияРеквизитов.Вставить("СуммаНДСПрописью", Истина);
	КонецЕсли;
	
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидВладельцаФайла, "ВестиУчетСторон") Тогда 
		ОбязательныеРеквизиты.Добавить("КонтактноеЛицо");
		ОбязательныеРеквизиты.Добавить("Контрагент");
	КонецЕсли;	
	
	Документ = Справочники.ДокументыПредприятия.СоздатьЭлемент();
	
	ЗаполнитьДеревоРеквизитовКраткойФормы(Дерево, СправочникМетаданные.Реквизиты, "ВладелецФайла", 1,
		ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
		
	Если ЭтоКорреспонденция Тогда
		
		ИсключенияРеквизитов.Вставить("Создал", Истина);
		ИсключенияРеквизитов.Вставить("ДатаСоздания", Истина);
		ИсключенияРеквизитов.Вставить("Основание", Истина);
		ИсключенияРеквизитов.Вставить("СрокИсполненияПервоначальный", Истина);
		ИсключенияРеквизитов.Вставить("Корреспондент", Истина);
		// Локализация
		Если Не КэшВидаДокумента.ЯвляетсяОбращениемОтГраждан Тогда
			ИсключенияРеквизитов.Вставить("ВидОбращения", Истина);
		КонецЕсли;
		// Локализация Конец
		
		Если КэшВидаДокумента.ЯвляетсяИсходящейКорреспонденцией Тогда
			
			ИсключенияРеквизитов.Вставить("Адресат", Истина);
			
			ЗаполнитьДеревоРеквизитовКраткойФормы(Дерево, 
				Метаданные.Документы.Корреспонденция.ТабличныеЧасти.Корреспонденты.Реквизиты,
				"Корреспонденция|Корреспонденты", 1, ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
			
			ИсключенияРеквизитов.Вставить("ДатаКонтрагента", Истина);
			ИсключенияРеквизитов.Вставить("НомерКонтрагента", Истина);
			
		ИначеЕсли КэшВидаДокумента.ЯвляетсяВходящейКорреспонденцией Тогда
			
			ЗаполнитьДеревоРеквизитовКраткойФормы(Дерево, 
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
					Метаданные.Документы.Корреспонденция.ТабличныеЧасти.Корреспонденты.Реквизиты.Адресат),
				"Корреспонденция|Корреспонденты", 1, ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
			
		КонецЕсли;
		
		ЗаполнитьДеревоРеквизитовКраткойФормы(Дерево, Метаданные.Документы.Корреспонденция.Реквизиты,
			"Корреспонденция", 1, ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
		
		// Локализация
		Если КэшВидаДокумента.ЯвляетсяОбращениемОтГраждан Тогда
			СтрокаРеквизитыОбращения = Дерево.Строки.Добавить();
			СтрокаРеквизитыОбращения.Наименование = "ВопросыОбращения";
			СтрокаРеквизитыОбращения.НаименованиеДляWord = "ВопросыОбращения";
			СтрокаРеквизитыОбращения.Описание = НСтр("ru = 'Вопрос обращения'");
			
			ЗаполнитьДеревоРеквизитовКраткойФормы(СтрокаРеквизитыОбращения,
				Метаданные.Документы.Корреспонденция.ТабличныеЧасти.ВопросыОбращения.Реквизиты,
				"Корреспонденция|ВопросыОбращения", 2, ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
		КонецЕсли;
		// Локализация Конец
		
		СтрокаНомерКонтрагента = Дерево.Строки.Найти("НомерКонтрагента", "Наименование");
		Если КэшВидаДокумента.ЯвляетсяВходящейКорреспонденцией Тогда
			СтрокаНомерКонтрагента.Описание = НСтр("ru = 'Входящий номер'");
		ИначеЕсли КэшВидаДокумента.ЯвляетсяИсходящейКорреспонденцией Тогда
			СтрокаНомерКонтрагента.Описание = НСтр("ru = 'Исходящий номер'");
		КонецЕсли;
		
	КонецЕсли;
		
	Если Параметры.Свойство("ВестиУчетТоваровИУслуг") И Параметры.ВестиУчетТоваровИУслуг Тогда 
		
		НоваяСтрокаСписок = Дерево.Строки.Добавить();
		НоваяСтрокаСписок.Наименование = "СписокТоваровИУслуг";
		НоваяСтрокаСписок.НаименованиеДляWord = "СписокТоваровИУслуг";
		НоваяСтрокаСписок.Описание = НСтр("ru = 'Список товаров и услуг'");
		НоваяСтрокаСписок.РеквизитТабличнойЧасти = Истина;
		
		ИсключенияРеквизитов.Вставить("УдалитьСтавкаНДС", Истина);
		
		Если Не Параметры.УчитыватьНДС Тогда 
			ИсключенияРеквизитов.Вставить("СтавкаНДС", Истина);
			ИсключенияРеквизитов.Вставить("СуммаНДС", Истина);
		КонецЕсли;
		
		ЗаполнитьДеревоРеквизитовКраткойФормы(НоваяСтрокаСписок, СправочникМетаданные.ТабличныеЧасти.Товары.Реквизиты, 
			"ВладелецФайла|ТабличнаяЧасть", 1, ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов, , Истина);
	КонецЕсли;
	
	Попытка
		Документ.ВидДокумента = ВидВладельцаФайла;
		НаборСвойств = УправлениеСвойствамиСлужебный.ПолучитьНаборыСвойствОбъекта(Документ);
		
		Если НаборСвойств.Количество() = 0 Тогда 
			Возврат Дерево;
		КонецЕсли;
		
		Для Каждого ЭлементНабораСвойств Из НаборСвойств Цикл
			Для Каждого ДопРеквизит Из ЭлементНабораСвойств.Набор.ДополнительныеРеквизиты Цикл
				Если ДопРеквизит <> Неопределено И ЗначениеЗаполнено(ДопРеквизит.Свойство) Тогда
					РеквизитыСвойства = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДопРеквизит.Свойство,
						"ПометкаУдаления, Заголовок, ТипЗначения, УникальныйКодДляПоля, ФорматСвойства");
					Если ДопРеквизит.ПометкаУдаления Или РеквизитыСвойства.ПометкаУдаления = Истина Тогда 
						Продолжить;
					КонецЕсли;
					
					НоваяСтрокаДопРеквизит = Дерево.Строки.Добавить();
					НоваяСтрокаДопРеквизит.Наименование = РеквизитыСвойства.УникальныйКодДляПоля;
					НоваяСтрокаДопРеквизит.НаименованиеДляWord = РеквизитыСвойства.УникальныйКодДляПоля;
					НоваяСтрокаДопРеквизит.Тип = РеквизитыСвойства.ТипЗначения;
					НоваяСтрокаДопРеквизит.ОбъектРодитель = "ВладелецФайла.ДопРеквизиты";
					НоваяСтрокаДопРеквизит.Описание = РеквизитыСвойства.Заголовок;
					НоваяСтрокаДопРеквизит.ОписаниеПоляЗамены = РеквизитыСвойства.Заголовок;
					НоваяСтрокаДопРеквизит.ДополнительныйРеквизит = Истина;
					НоваяСтрокаДопРеквизит.ФорматСвойства = РеквизитыСвойства.ФорматСвойства;
					
					Если НоваяСтрокаДопРеквизит.Тип <> Строка(Тип("Дата")) 
						И НоваяСтрокаДопРеквизит.Тип <> Строка(Тип("Число"))
						И НоваяСтрокаДопРеквизит.Тип <> Строка(Тип("Булево")) Тогда
						НоваяСтрокаДопРеквизит.ИспользоватьПадеж = Истина;
					КонецЕсли;
					
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	Исключение
		// У владельца файла может не быть доп.реквизитов
	КонецПопытки;
	
	СтрокаКонтрагент = Дерево.Строки.Найти("Контрагент", "Наименование");
	Если СтрокаКонтрагент <> Неопределено Тогда
		
		Если КэшВидаДокумента.ЯвляетсяВходящейКорреспонденцией Тогда
			СтрокаКонтрагент.Описание = НСтр("ru = 'Отправитель'");
		ИначеЕсли КэшВидаДокумента.ЯвляетсяИсходящейКорреспонденцией Тогда
			СтрокаКонтрагент.Описание = НСтр("ru = 'Получатель'");
		КонецЕсли;
		
		СтрокаОсновнойСчет = СтрокаКонтрагент.Строки.Найти("ОсновнойБанковскийСчет", "Наименование");
		ИсключенияРеквизитов.Вставить("СуммаБезКопеек", Истина);
		ИсключенияРеквизитов.Вставить("МесяцПрописью", Истина);
		РеквизитыСчета = Метаданные.Справочники.БанковскиеСчета.Реквизиты;
		ЗаполнитьДеревоРеквизитовКраткойФормы(СтрокаОсновнойСчет, РеквизитыСчета,
			СтрокаОсновнойСчет.ОбъектРодитель+"|ОсновнойБанковскийСчет", 1, 
			ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
			
		СтрокаБанк = СтрокаОсновнойСчет.Строки.Найти("Банк", "Наименование");
		ЗаполнитьДеревоРеквизитовКраткойФормы(СтрокаБанк,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
				Метаданные.Справочники.КлассификаторБанков.СтандартныеРеквизиты.Код),
			СтрокаБанк.ОбъектРодитель+"|Банк", 2, 
			ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
			
		СтрокаБанкДляРасчетов = СтрокаОсновнойСчет.Строки.Найти("БанкДляРасчетов", "Наименование");
		ЗаполнитьДеревоРеквизитовКраткойФормы(СтрокаБанкДляРасчетов,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
				Метаданные.Справочники.КлассификаторБанков.СтандартныеРеквизиты.Код),
			СтрокаБанкДляРасчетов.ОбъектРодитель+"|БанкДляРасчетов", 2, 
			ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
		
	КонецЕсли;
	
	Строка = Дерево.Строки.Добавить();
	Строка.Наименование = "";
	Строка.НаименованиеДляWord = "Подписывающие";
	Строка.ОбъектРодитель = "Подписывающие";
	Строка.Описание = НСтр("ru = 'Подписывающие'");
	Строка.ОписаниеПоляЗамены = НСтр("ru = 'Подписывающие'");
	Строка.Тип = "Строка";

	Дерево.Строки.Сортировать("Описание", Истина);
	
	Возврат Дерево;
	
КонецФункции

// Формирует дерево значений с реквизитами и доп. реквизитами документа указанного типа.
//
// Параметры:
//  ВидДокумента - СправочникСсылка.ВидыДокументов - 
//		Текстовое описание параметра процедуры (функции).
//  ПоказыватьВсеРеквизиты - Булево - показывает или скрывает часть реквизитов объекта.
//
// Возвращаемое значение:
//  ДеревоЗначений - дерево значений с реквизитами документа
//
Функция ЗаполнитьДеревоРеквизитовДляШаблона(ВидДокумента, ПоказыватьВсеРеквизиты = Ложь) Экспорт
	
	ИсключенияРеквизитов = Новый Соответствие;
	ОбязательныеРеквизиты = Новый СписокЗначений;
	
	Типы = Новый ОписаниеТипов("Строка");
	ТипБулево = Новый ОписаниеТипов("Булево");
	
	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("Наименование", Типы);
	Дерево.Колонки.Добавить("Тип", Типы);
	Дерево.Колонки.Добавить("ОбъектРодитель", Типы);
	Дерево.Колонки.Добавить("Описание", Типы);
	Дерево.Колонки.Добавить("ОписаниеПоляЗамены", Типы);
	Дерево.Колонки.Добавить("РеквизитРодитель", Типы);
	Дерево.Колонки.Добавить("ДополнительныйРеквизит", ТипБулево);
	
	ИсключенияРеквизитов.Вставить("Заголовок", Истина);
	ИсключенияРеквизитов.Вставить("ЗапретитьАвтоматическоеДобавлениеУчастниковРабочейГруппы", Истина);
	ИсключенияРеквизитов.Вставить("ПодписанЭП", Истина);
	ИсключенияРеквизитов.Вставить("ЧисловойНомер", Истина);
	ИсключенияРеквизитов.Вставить("Шаблон", Истина);
	ИсключенияРеквизитов.Вставить("КомментарийПодписи", Истина);
	
	Если Не ПоказыватьВсеРеквизиты Тогда 
		ИсключенияРеквизитов.Вставить("ГрифДоступа", Истина);
		ИсключенияРеквизитов.Вставить("КоличествоЛистов", Истина);
		ИсключенияРеквизитов.Вставить("КоличествоПриложений", Истина);
		ИсключенияРеквизитов.Вставить("КоличествоЭкземпляров", Истина);
		ИсключенияРеквизитов.Вставить("ЛистовВПриложениях", Истина);
		ИсключенияРеквизитов.Вставить("Содержание", Истина);
		ИсключенияРеквизитов.Вставить("Комментарий", Истина);
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
		ОбязательныеРеквизиты.Добавить("Организация");
	КонецЕсли;
	
	КэшВидаДокумента = Делопроизводство.КэшВидаДокумента(ВидДокумента);
	ЭтоКорреспонденция = КэшВидаДокумента.ЯвляетсяВходящейКорреспонденцией
		Или КэшВидаДокумента.ЯвляетсяИсходящейКорреспонденцией;

	СправочникМетаданные = Метаданные.Справочники.ДокументыПредприятия;
	// Документы предприятия
	ИсключенияРеквизитов.Вставить("Бессрочный", Истина);
	ИсключенияРеквизитов.Вставить("НеДействует", Истина);
	ИсключенияРеквизитов.Вставить("УстановилПодпись", Истина);
	ИсключенияРеквизитов.Вставить("РезультатПодписания", Истина);
	ИсключенияРеквизитов.Вставить("Утвердил", Истина);
	Если Не ПоказыватьВсеРеквизиты Тогда 
		ИсключенияРеквизитов.Вставить("ПорядокПродления", Истина);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидДокумента) И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидДокумента, "ВестиУчетСторон") Тогда 
		ОбязательныеРеквизиты.Добавить("КонтактноеЛицо");
		ОбязательныеРеквизиты.Добавить("Контрагент");
	КонецЕсли;	
	
	Если ЭтоКорреспонденция Тогда
		
		ИсключенияРеквизитов.Вставить("Создал", Истина);
		ИсключенияРеквизитов.Вставить("ВидКорреспонденции", Истина);
		ИсключенияРеквизитов.Вставить("СрокИсполненияПервоначальный", Истина);
		ИсключенияРеквизитов.Вставить("ДатаСоздания", Истина);
		ИсключенияРеквизитов.Вставить("Основание", Истина);
		// Локализация
		Если Не КэшВидаДокумента.ЯвляетсяОбращениемОтГраждан Тогда
			ИсключенияРеквизитов.Вставить("ВидОбращения", Истина);
		КонецЕсли;
		// Локализация Конец
		
		Если КэшВидаДокумента.ЯвляетсяИсходящейКорреспонденцией Тогда
			
			ИсключенияРеквизитов.Вставить("Адресат", Истина);
			ИсключенияРеквизитов.Вставить("Корреспондент", Истина);
			ИсключенияРеквизитов.Вставить("ПодписалОтКонтрагента", Истина);
			ИсключенияРеквизитов.Вставить("КонтактноеЛицо", Истина);
			ИсключенияРеквизитов.Вставить("Контрагент", Истина);
		
			ЗаполнитьРеквизитыДляШаблона(Дерево, 
				Метаданные.Документы.Корреспонденция.ТабличныеЧасти.Корреспонденты.Реквизиты,
				"Корреспонденция|Корреспонденты", 1, ВидДокумента, ОбязательныеРеквизиты, ИсключенияРеквизитов,,
				ПоказыватьВсеРеквизиты);
			
			ИсключенияРеквизитов.Вставить("ДатаКонтрагента", Истина);
			ИсключенияРеквизитов.Вставить("НомерКонтрагента", Истина);
			ИсключенияРеквизитов.Вставить("КонтактноеЛицоПереадресовавшего", Истина);
			ИсключенияРеквизитов.Вставить("Переадресовавший", Истина);
			ИсключенияРеквизитов.Вставить("СпособПолучения", Истина);
			ИсключенияРеквизитов.Вставить("СрокИсполненияПервоначальный", Истина);
			ИсключенияРеквизитов.Вставить("СрокОтветаПереадресовавшему", Истина);
			
		ИначеЕсли КэшВидаДокумента.ЯвляетсяВходящейКорреспонденцией Тогда
			
			ЗаполнитьРеквизитыДляШаблона(Дерево, 
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
					Метаданные.Документы.Корреспонденция.ТабличныеЧасти.Корреспонденты.Реквизиты.Адресат),
				"Корреспонденция|Корреспонденты", 1, ВидДокумента, ОбязательныеРеквизиты, ИсключенияРеквизитов,,
				ПоказыватьВсеРеквизиты);
			
		КонецЕсли;
		
		ЗаполнитьРеквизитыДляШаблона(Дерево, Метаданные.Документы.Корреспонденция.Реквизиты,
			"Корреспонденция", 1, ВидДокумента, ОбязательныеРеквизиты, ИсключенияРеквизитов,,
				ПоказыватьВсеРеквизиты);
		
		// Локализация
		Если КэшВидаДокумента.ЯвляетсяОбращениемОтГраждан Тогда
			СтрокаРеквизитыОбращения = Дерево.Строки.Добавить();
			СтрокаРеквизитыОбращения.Наименование = "ВопросыОбращения";
			СтрокаРеквизитыОбращения.Описание = НСтр("ru = 'Вопрос обращения'");
			
			ЗаполнитьРеквизитыДляШаблона(СтрокаРеквизитыОбращения,
				Метаданные.Документы.Корреспонденция.ТабличныеЧасти.ВопросыОбращения.Реквизиты,
				"Корреспонденция|ВопросыОбращения", 2, ВидДокумента, ОбязательныеРеквизиты, ИсключенияРеквизитов,,
				ПоказыватьВсеРеквизиты);
		КонецЕсли;
		// Локализация Конец
		
		СтрокаНомерКонтрагента = Дерево.Строки.Найти("НомерКонтрагента", "Наименование");
		Если КэшВидаДокумента.ЯвляетсяВходящейКорреспонденцией Тогда
			СтрокаНомерКонтрагента.Описание = НСтр("ru = 'Входящий номер'");
		ИначеЕсли КэшВидаДокумента.ЯвляетсяИсходящейКорреспонденцией Тогда
			СтрокаНомерКонтрагента.Описание = НСтр("ru = 'Исходящий номер'");
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьРеквизитыДляШаблона(Дерево, СправочникМетаданные.Реквизиты, "", 1, 
		ВидДокумента, ОбязательныеРеквизиты, ИсключенияРеквизитов, , ПоказыватьВсеРеквизиты);
		
	Если КэшВидаДокумента.ЯвляетсяИсходящейКорреспонденцией Тогда 
		ДобавитьСтрокуДляШаблона(Дерево, "", "Получатель", НСтр("ru = 'Получатель'"),
			Тип("Строка"));
	КонецЕсли;
	
	
	Документ = Справочники.ДокументыПредприятия.СоздатьЭлемент();
	
	Попытка
		Документ.ВидДокумента = ВидДокумента;
		НаборСвойств = УправлениеСвойствамиСлужебный.ПолучитьНаборыСвойствОбъекта(Документ);
		
		Если НаборСвойств.Количество() = 0 Тогда
			Возврат Дерево;
		КонецЕсли;
		
		Массив = Новый Массив;
		Массив.Добавить(Тип("Булево"));
		ОписаниеБулева = Новый ОписаниеТипов(Массив);
		Для Каждого ЭлементНабораСвойств Из НаборСвойств Цикл
			Для Каждого ДопРеквизит Из ЭлементНабораСвойств.Набор.ДополнительныеРеквизиты Цикл
				Если ДопРеквизит <> Неопределено И ЗначениеЗаполнено(ДопРеквизит.Свойство) Тогда
					РеквизитыСвойства = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДопРеквизит.Свойство,
						"ПометкаУдаления, Заголовок, ТипЗначения, ФорматСвойства");
					Если ДопРеквизит.ПометкаУдаления Или РеквизитыСвойства.ПометкаУдаления = Истина Тогда 
						Продолжить;
					КонецЕсли;
					
					Если Не ПоказыватьВсеРеквизиты И РеквизитыСвойства.ТипЗначения = ОписаниеБулева Тогда 
						Продолжить;
					КонецЕсли;
					
					НоваяСтрокаДопРеквизит = Дерево.Строки.Добавить();
					НоваяСтрокаДопРеквизит.Наименование = РеквизитыСвойства.Заголовок;
					НоваяСтрокаДопРеквизит.Тип = РеквизитыСвойства.ТипЗначения;
					НоваяСтрокаДопРеквизит.ОбъектРодитель = "ДопРеквизиты";
					НоваяСтрокаДопРеквизит.Описание = РеквизитыСвойства.Заголовок;
					НоваяСтрокаДопРеквизит.ОписаниеПоляЗамены = РеквизитыСвойства.Заголовок;
					НоваяСтрокаДопРеквизит.ДополнительныйРеквизит = Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	Исключение
		// У владельца файла может не быть доп.реквизитов
	КонецПопытки;
	
	Дерево.Строки.Сортировать("Описание", Истина);
	
	Возврат Дерево;
	
КонецФункции

// Формирует структуру с описаниями функциональных опций, в состав которых входит объект
//	метаданных, указанный в параметре функции.
//
// Параметры:
//  ОбъектМетаданных - Строка - Строка с полным именем объекта метаданных конфигурации. Например:
//	 Справочник.Организации. Необязательный.
//
// Возвращаемое значение:
//  Структура, с двумя полями-описаниями типа Структура.
//	 * Ключ - Строка - варианты: "Независимые" или "Параметрические".
//	 * Значение - Структура - Описание функциональной опции.
//	Структура описания функциональной опции:
//	 * Ключ - Строка - имя функциональной опции
//	 * Значение - Структура с полями: 
//		"Хранение" - Структура - поля: Тип - Строка - тип объекта хранения ФО (например, "Справочник"),
//		Вид - Строка - вид объекта хранения ФО (например, "Организации"), ПолноеИмя - Строка - полное
//		имя объекта хранения (например: Справочник.Организации)
//		"Параметры" - Неопределено или Структура - описание см. функцию ПолучитьПараметрыФункциональнойОпции
//
Функция ПолучитьСписокФункциональныхОпций(ИмяОбъектаМетаданных = "") Экспорт
	
	СписокДоступныхОпций = Новый Структура("Независимые, Параметрические", Новый Структура, Новый Структура);
	
	ОбъектМетаданных = Неопределено;
	Если ЗначениеЗаполнено(ИмяОбъектаМетаданных) Тогда
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяОбъектаМетаданных);
		Если ОбъектМетаданных = Неопределено Тогда
			Возврат СписокДоступныхОпций;	
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого ФункциональнаяОпция Из Метаданные.ФункциональныеОпции Цикл
		Если Не ФункциональнаяОпция.Состав.Содержит(ОбъектМетаданных) Тогда
			Продолжить;
		КонецЕсли;
		
		МассивСтрок = СтрРазделить(ФункциональнаяОпция.Хранение.ПолноеИмя(), ".");
		Если МассивСтрок.Количество() < 2 Тогда				
			Продолжить;
		КонецЕсли;
		
		ОписаниеХранения = Новый Структура;
		ОписаниеХранения.Вставить("Тип", МассивСтрок[0]);
		ОписаниеХранения.Вставить("Вид", МассивСтрок[1]);
		ОписаниеХранения.Вставить("ПолноеИмя", СтрШаблон("%1.%2", МассивСтрок[0], МассивСтрок[1]));
		
		Если СтрСравнить(ОписаниеХранения.Тип, "Константа") = 0 Тогда
			ОписаниеОпции = Новый Структура;
			ОписаниеОпции.Вставить("Хранение", ОписаниеХранения);
			ОписаниеОпции.Вставить("Параметры", Неопределено);
			
			СписокДоступныхОпций.Независимые.Вставить(ФункциональнаяОпция.Имя, ОписаниеОпции);
		Иначе
			// Получим подходящие для функциональной опции параметры
			ПараметрыОпции = АвтозаполнениеШаблоновФайловСерверПовтИсп.ПолучитьПараметрыФункциональнойОпции(ОписаниеХранения.ПолноеИмя);
			
			Если ПараметрыОпции.Количество() Тогда
				ОписаниеОпции = Новый Структура;
				ОписаниеОпции.Вставить("Хранение", ОписаниеХранения);
				ОписаниеОпции.Вставить("Параметры", ПараметрыОпции);
				
				СписокДоступныхОпций.Параметрические.Вставить(ФункциональнаяОпция.Имя, ОписаниеОпции);
			КонецЕсли;
		КонецЕсли;		
	КонецЦикла;
	
	Возврат СписокДоступныхОпций;
	
КонецФункции

// Формирует структуру со списком параметров функциональной опции
//
// Параметры:
//  ОбъектХраненияЗначения - Строка - Полное имя объекта метаданных конфигурации (например:
//	"Справочник.Организации").
//
// Возвращаемое значение:
//  Структура
//	 * Ключ - Строка - имя параметра ФО
//	 * Значение - Структура - описание параметра ФО
//  Структура описания параметра ФО:
//	Поля структуры описания параметра функциональной опции:
//	 * Поле - Строка - Имя измерения регистра сведений или "Ссылка".
//	 * Тип - Описание типов - описание типов поля
//
Функция ПолучитьПараметрыФункциональнойОпции(ОбъектХраненияЗначения) Экспорт
	
	СписокПараметров = Новый Структура;
	
	Для Каждого Параметр Из Метаданные.ПараметрыФункциональныхОпций Цикл
		ОписаниеХранения = Новый Структура;		
		//@skip-warning
		Для Каждого Использование Из Параметр.Использование Цикл
			МассивСтрок = СтрРазделить(Использование.ПолноеИмя(), ".");			
			Если МассивСтрок.Количество() < 2 Тогда
				Продолжить;
			КонецЕсли;
						
			Если СтрСравнить(СтрШаблон("%1.%2", МассивСтрок[0], МассивСтрок[1]), ОбъектХраненияЗначения) = 0 Тогда
				Если СтрСравнить(МассивСтрок[0], "Справочник") = 0 Тогда
					ОписаниеХранения.Вставить("Поле", "Ссылка");
					ОписаниеХранения.Вставить("Тип", Новый ОписаниеТипов(СтрШаблон("%1Ссылка.%2", МассивСтрок[0], МассивСтрок[1])));
				Иначе
					ОписаниеХранения.Вставить("Поле", МассивСтрок[3]);
					ОписаниеХранения.Вставить("Тип", Использование.Тип);
				КонецЕсли;
				
				Прервать;
			КонецЕсли;			
		КонецЦикла;
		
		Если ОписаниеХранения.Количество() Тогда
			СписокПараметров.Вставить(Параметр.Имя, ОписаниеХранения);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СписокПараметров;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьДеревоРеквизитов(Родитель, НаборРеквизитов, Путь, УровеньВложенности, ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов)
	
	Если УровеньВложенности > 2 Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеТипаСотрудникиПользователи = 
		Новый ОписаниеТипов("СправочникСсылка.Сотрудники, СправочникСсылка.Пользователи");
		
	Для Каждого Реквизит Из НаборРеквизитов Цикл
		
		Если ТипЗнч(Реквизит) <> Тип("ОписаниеСтандартногоРеквизита") Тогда
			Если ОбязательныеРеквизиты.НайтиПоЗначению(Реквизит.Имя) = Неопределено Тогда
				РеквизитВключен = Истина;
				РеквизитВходитВФункциональныеОпции = Ложь;
				
				ДоступныеОпции = АвтозаполнениеШаблоновФайловСерверПовтИсп.ПолучитьСписокФункциональныхОпций(Реквизит.ПолноеИмя());
				РеквизитВходитВФункциональныеОпции = ДоступныеОпции.Независимые.Количество() 
					Или ДоступныеОпции.Параметрические.Количество();
				РеквизитВключен = РеквизитДоступенПоФункциональнымОпциям(ДоступныеОпции, ВидВладельцаФайла);
				
				Если РеквизитВходитВФункциональныеОпции И НЕ РеквизитВключен
					Или ИсключенияРеквизитов[Реквизит.Имя] <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Строка = Родитель.Строки.Добавить();
		Строка.Наименование = Реквизит.Имя;
		Строка.Представление = Реквизит.Имя;
		Строка.Тип = Реквизит.Тип;
		Строка.ОбъектРодитель = Путь;
		Строка.Описание = Реквизит.Синоним;
		
		Если Реквизит.Имя = "ПодписанУтвержден" Тогда
			Строка.Представление = НСтр("ru = 'Статус подписания и утверждения'");
			Строка.Описание = НСтр("ru = 'Статус подписания и утверждения'");
		КонецЕсли;
		
		Если Реквизит.Тип.СодержитТип(Тип("СправочникСсылка.Сотрудники")) Тогда
			
			СтрокаФИО = Строка.Строки.Добавить();
			СтрокаФИО.Наименование = "ФИО";
			СтрокаФИО.Представление = НСтр("ru = 'Полное имя'");
			СтрокаФИО.Тип = "Строка";
			СтрокаФИО.ОбъектРодитель = Путь + "|" + Строка.Наименование;
			СтрокаФИО.Описание = НСтр("ru = 'Полное имя'");
			
			СтрокаФамилияИнициалы = Строка.Строки.Добавить();
			СтрокаФамилияИнициалы.Наименование = "ФамилияИнициалы";
			СтрокаФамилияИнициалы.Представление = НСтр("ru = 'Фамилия и инициалы'");
			СтрокаФамилияИнициалы.Тип = "Строка";
			СтрокаФамилияИнициалы.ОбъектРодитель = Путь + "|" + Строка.Наименование;
			СтрокаФамилияИнициалы.Описание = НСтр("ru = 'Фамилия и инициалы'");
			
		КонецЕсли;
		
		// Индивидуальная проверка для контрагентов и организаций.
		// виртуальные реквизиты КПП на дату, Наименование на дату:
		Если (Строка.ОбъектРодитель = "ВладелецФайла|Контрагент" Или Строка.ОбъектРодитель = "ВладелецФайла|Организация")
				И (
					Строка.Наименование = "КПП" Или Строка.Наименование = "НаименованиеПолное"
				) Тогда
			
			СтрокаНаДату = Родитель.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаНаДату, Строка);
			СтрокаНаДату.Наименование			= Строка.Наименование + "НаДату";
			СтрокаНаДату.Тип					= "СтрокаПолученнаяДинамически";
			СтрокаНаДату.Описание				= СтрШаблон("%1 %2", Строка.Описание, Текст_НаДату());
			СтрокаНаДату.Представление			= СтрокаНаДату.Наименование;
			
			Если Строка.Наименование = "НаименованиеПолное" Тогда
				// Кроме полного нужно добавить еще обычное наименование на дату:
				СтрокаНаДату2 = Родитель.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаНаДату2, СтрокаНаДату);
				СтрокаНаДату2.Наименование			= "НаименованиеНаДату";
				СтрокаНаДату2.Тип					= "СтрокаПолученнаяДинамически";
				СтрокаНадату2.Описание				= Текст_НаименованиеНаДату();
				СтрокаНаДату2.Представление			= СтрокаНаДату2.Наименование;
			КонецЕсли;
		КонецЕсли;
		
		
		Для Каждого СправочникМетаданные Из Метаданные.Справочники Цикл
			Если СправочникМетаданные.ПредставлениеОбъекта <> Строка(Реквизит.Тип) Тогда
				Если ОписаниеТипаСотрудникиПользователи <> Реквизит.Тип Или 
					СправочникМетаданные <> Метаданные.Справочники.Сотрудники Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			ЗаполнитьДеревоРеквизитов(Строка, СправочникМетаданные.СтандартныеРеквизиты,
				Путь + "|" + Строка.Наименование, УровеньВложенности + 1,
				ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
				
			ЗаполнитьДеревоРеквизитов(Строка, СправочникМетаданные.Реквизиты,
				Путь + "|" + Строка.Наименование, УровеньВложенности + 1,
				ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов);
				
			Попытка
				Если СправочникМетаданные.ТабличныеЧасти.Найти("ДополнительныеРеквизиты") <> Неопределено Тогда
					ЗаполнитьСписокДопРеквизитов(Строка, СправочникМетаданные, Путь + "|" + Реквизит.Имя, ВидВладельцаФайла);
				КонецЕсли;
			Исключение
				// у справочника может не быть ни одного настроенного доп.реквизита
			КонецПопытки;
			Если СправочникМетаданные.ТабличныеЧасти.Найти("КонтактнаяИнформация") <> Неопределено Тогда
				ЗаполнитьСписокКонтактнойИнформации(Строка, СправочникМетаданные, Путь + "|" + Реквизит.Имя);
			КонецЕсли;
			Прервать;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСписокДопРеквизитов(Строка, СправочникМетаданные, Путь, ВидВладельцаФайла)
	
	УстановитьПривилегированныйРежим(Истина);
	Объект = Справочники[СправочникМетаданные.Имя].СоздатьЭлемент();
	Если ДелопроизводствоКлиентСервер.ЭтоДокумент(Объект) Тогда
		Объект.ВидДокумента = ВидВладельцаФайла;
	КонецЕсли;
	НаборСвойств = УправлениеСвойствамиСлужебный.ПолучитьНаборыСвойствОбъекта(Объект);
	КоличествоНаборов = НаборСвойств.Количество();
	
	Если КоличествоНаборов > 0 Тогда
		НоваяСтрокаДопРеквизиты = Строка.Строки.Добавить();
		НоваяСтрокаДопРеквизиты.Наименование = НСтр("ru = 'ДопРеквизиты'");
		НоваяСтрокаДопРеквизиты.Представление = НСтр("ru = 'ДопРеквизиты'");
		НоваяСтрокаДопРеквизиты.Тип = "";
		НоваяСтрокаДопРеквизиты.Описание = НСтр("ru = 'Дополнительные реквизиты'");
		
		Для Каждого ВложенныйНаборСвойств Из НаборСвойств Цикл
			НоваяСтрокаВложенныеДопРеквизиты = НоваяСтрокаДопРеквизиты.Строки.Добавить();
			НоваяСтрокаВложенныеДопРеквизиты.Наименование = ВложенныйНаборСвойств.Набор.Наименование;
			НоваяСтрокаВложенныеДопРеквизиты.Представление = ВложенныйНаборСвойств.Набор.Наименование;
			НоваяСтрокаВложенныеДопРеквизиты.Тип = "";
			Для Каждого ДопРеквизит Из ВложенныйНаборСвойств.Набор.ДополнительныеРеквизиты Цикл
				НоваяСтрокаДопРеквизит = НоваяСтрокаВложенныеДопРеквизиты.Строки.Добавить();
				НоваяСтрокаДопРеквизит.Наименование = ДопРеквизит.Свойство.Наименование;
				НоваяСтрокаДопРеквизит.Представление = ДопРеквизит.Свойство.Наименование;
				НоваяСтрокаДопРеквизит.Тип = ДопРеквизит.Свойство.ТипЗначения;
				
				Если КоличествоНаборов = 1 Тогда 
					НаборНаименование = "";
				Иначе 
					НаборНаименование = ВложенныйНаборСвойств.Набор.Наименование;
				КонецЕсли;
				
				НоваяСтрокаДопРеквизит.ОбъектРодитель = Путь + "|" + НоваяСтрокаДопРеквизиты.Наименование 
					+ ?(ЗначениеЗаполнено(НаборНаименование), "|" + НаборНаименование, "");
				НоваяСтрокаДопРеквизит.ОбъектРодитель = СтрЗаменить(НоваяСтрокаДопРеквизит.ОбъектРодитель, 
					НСтр("ru = 'Доп. свойства'"), НСтр("ru = 'ДопСвойства'"));
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ЗаполнитьСписокКонтактнойИнформации(Строка, СправочникМетаданные, Путь)
	
	//Получение набора контактной информации для данного справочника
	// Получим список видов КИ
	ИмяСправочника = СправочникМетаданные.Имя;
	ГруппаВидовКИ = Справочники.ВидыКонтактнойИнформации["Справочник" + ИмяСправочника];
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыКонтактнойИнформации.Ссылка КАК Вид,
	|	ВидыКонтактнойИнформации.Наименование КАК Наименование,
	|	ВидыКонтактнойИнформации.Тип КАК Тип,
	|	ВидыКонтактнойИнформации.ПометкаУдаления КАК ПометкаУдаления,
	|	ИСТИНА КАК Использовать
	|ИЗ
	|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	|ГДЕ
	|	ВидыКонтактнойИнформации.Родитель = &ГруппаВидовКИ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПометкаУдаления,
	|	ВидыКонтактнойИнформации.РеквизитДопУпорядочивания";
	
	Запрос.УстановитьПараметр("ГруппаВидовКИ", ГруппаВидовКИ);
	УстановитьПривилегированныйРежим(Истина);
	КонтактнаяИнформация = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	Если КонтактнаяИнформация.Количество() > 0 Тогда
		НоваяСтрокаКонтактнаяИнформация = Строка.Строки.Добавить();
		НоваяСтрокаКонтактнаяИнформация.Наименование = НСтр("ru = 'Контактная информация'");
		НоваяСтрокаКонтактнаяИнформация.Представление = НСтр("ru = 'Контактная информация'");
		НоваяСтрокаКонтактнаяИнформация.Тип = "";
		НоваяСтрокаКонтактнаяИнформация.Описание = НСтр("ru = 'Контактная информация'");

		Для Каждого Запись Из КонтактнаяИнформация Цикл
			НоваяСтрока = НоваяСтрокаКонтактнаяИнформация.Строки.Добавить();
			НоваяСтрока.Наименование = Запись.Наименование;
			НоваяСтрока.Представление = Запись.Наименование;
			НоваяСтрока.Тип = Запись.Тип;
			НоваяСтрока.ОбъектРодитель = Путь 
				+ "|" 
				+ СтрЗаменить(
					НоваяСтрокаКонтактнаяИнформация.Наименование, 
					НСтр("ru = 'Контактная информация'"), 
					НСтр("ru = 'КонтактнаяИнформация'"));
		КонецЦикла;
	КонецЕсли;	
	
КонецПроцедуры

Процедура ЗаполнитьДеревоРеквизитовКраткойФормы(
	Родитель, 
	НаборРеквизитов, 
	Путь, 
	УровеньВложенности, 
	ВидВладельцаФайла,
	ОбязательныеРеквизиты,
	ИсключенияРеквизитов,
	Знач ТочечныеИсключенияРеквизитов = Неопределено,
	РеквизитТабличнойЧасти = Ложь)
	
	Если УровеньВложенности > 2 Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеТипаСотрудникиПользователи = 
		Новый ОписаниеТипов("СправочникСсылка.Сотрудники, СправочникСсылка.Пользователи");
	
	Для Каждого Реквизит Из НаборРеквизитов Цикл
		
		Если ТипЗнч(Реквизит) <> Тип("ОписаниеСтандартногоРеквизита") Тогда			
			Если ОбязательныеРеквизиты.НайтиПоЗначению(Реквизит.Имя) = Неопределено Тогда
				Если ИсключенияРеквизитов[Реквизит.Имя] <> Неопределено 
					Или (ТочечныеИсключенияРеквизитов <> Неопределено И 
						ТочечныеИсключенияРеквизитов.НайтиПоЗначению(Реквизит.Имя) <> Неопределено) Тогда 
						Продолжить;
				КонецЕсли;
				
				РеквизитВключен = Истина;
				РеквизитВходитВФункциональныеОпции = Ложь;
				
				ДоступныеОпции = АвтозаполнениеШаблоновФайловСерверПовтИсп.ПолучитьСписокФункциональныхОпций(Реквизит.ПолноеИмя());
				РеквизитВходитВФункциональныеОпции = ДоступныеОпции.Независимые.Количество() 
					Или ДоступныеОпции.Параметрические.Количество();
				РеквизитВключен = РеквизитДоступенПоФункциональнымОпциям(ДоступныеОпции, ВидВладельцаФайла);
				
				Если РеквизитВходитВФункциональныеОпции И Не РеквизитВключен Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Строка = Родитель.Строки.Добавить();
		Строка.Наименование = Реквизит.Имя;
		
		Если Реквизит.Имя = "ПредставлениеВПереписке" Тогда 
			Строка.НаименованиеДляWord = "ВПереписке";
		ИначеЕсли Реквизит.Имя = "ПредставлениеВПерепискеСРангом" Тогда 
			Строка.НаименованиеДляWord = "ВПерепискеСРангом";
		Иначе 
			Строка.НаименованиеДляWord = Реквизит.Имя;
		КонецЕсли;
		
		Строка.Тип = Реквизит.Тип;
		
		Если Строка.Тип <> Строка(Тип("Дата"))
			И Строка.Тип <> Строка(Тип("Число"))
			И Строка.Тип <> Строка(Тип("Булево")) Тогда
			Строка.ИспользоватьПадеж = Истина;
		КонецЕсли;
		
		Строка.ОбъектРодитель = Путь;
		Строка.РеквизитТабличнойЧасти = РеквизитТабличнойЧасти;
		
		Если Не РеквизитТабличнойЧасти Тогда 
			РеквизитРодитель = СтрЗаменить(Путь,"ВладелецФайла", "");
			РеквизитРодитель = СтрЗаменить(РеквизитРодитель, "|", "_");
			Строка.РеквизитРодитель = РеквизитРодитель;
			Если Лев(Строка.РеквизитРодитель, 1) = "_" Тогда 
				Строка.РеквизитРодитель = Прав(Строка.РеквизитРодитель, СтрДлина(Строка.РеквизитРодитель) - 1);
			КонецЕсли;
		Иначе 
			Строка.НомерКолонкиТабличнойЧасти = 10000;
			Строка.РеквизитРодитель = "ТЧ";
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Реквизит.Синоним) Тогда 
			Строка.Описание = Реквизит.Имя;
			Строка.ОписаниеПоляЗамены = Реквизит.Имя;
		Иначе 
			Строка.Описание = Реквизит.Синоним;
			Строка.ОписаниеПоляЗамены = Реквизит.Синоним;
		КонецЕсли;
		
		Если Реквизит.Имя = "АдресМеждународный" Тогда 
			Строка.Описание = НСтр("ru = 'Адрес международный'");
		ИначеЕсли Реквизит.Имя = "ГородМеждународный" Тогда 
			Строка.Описание = НСтр("ru = 'Город международный'");
		КонецЕсли;
		
		Если Реквизит.Имя = "ПодписанУтвержден" Тогда
			Строка.Описание = НСтр("ru = 'Статус подписания и утверждения'");
		КонецЕсли;
		
		
		// Индивидуальная проверка для контрагентов и организаций.
		// виртуальные реквизиты КПП на дату, Наименование на дату:
		Если (Строка.ОбъектРодитель = "ВладелецФайла|Контрагент" Или Строка.ОбъектРодитель = "ВладелецФайла|Организация")
				И (
					Строка.Наименование = "КПП" Или Строка.Наименование = "НаименованиеПолное"
				) Тогда
			
			ПредставлениеСправочникаЮрлиц = ПредставлениеСправочникаЮрлиц(Строка.ОбъектРодитель);
			
			СтрокаНаДату = Родитель.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаНаДату, Строка);
			СтрокаНаДату.Наименование			= Строка.Наименование + "НаДату";
			СтрокаНаДату.НаименованиеДляWord	= СтрокаНаДату.Наименование;
			СтрокаНаДату.Тип					= "СтрокаПолученнаяДинамически";
			СтрокаНаДату.Описание				= СтрШаблон("%1 %2", Строка.Описание, Текст_НаДату());
			СтрокаНаДату.ОписаниеПоляЗамены		= СтрШаблон("%1 %2 %3", ПредставлениеСправочникаЮрлиц, Строка.Описание, Текст_НаДату());
			
			Если Строка.Наименование = "НаименованиеПолное" Тогда
				// Кроме полного нужно добавить еще обычное наименование на дату:
				СтрокаНаДату2 = Родитель.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаНаДату2, СтрокаНаДату);
				СтрокаНаДату2.Наименование			= "НаименованиеНаДату";
				СтрокаНаДату2.НаименованиеДляWord	= СтрокаНаДату2.Наименование;
				СтрокаНаДату2.Тип					= "СтрокаПолученнаяДинамически";
				СтрокаНадату2.Описание				= Текст_НаименованиеНаДату();
				СтрокаНаДату2.ОписаниеПоляЗамены	= СтрШаблон("%1 %2", ПредставлениеСправочникаЮрлиц, Текст_НаименованиеНаДату());
			КонецЕсли;
		КонецЕсли;
		
		Если Реквизит.Тип.СодержитТип(Тип("СправочникСсылка.Сотрудники")) Тогда
			
			СтрокаФИО = Строка.Строки.Добавить();
			СтрокаФИО.Наименование = "ФИО";
			СтрокаФИО.НаименованиеДляWord = "ФИО";
			СтрокаФИО.Тип = "Строка";
			СтрокаФИО.РеквизитРодитель =
				?(ЗначениеЗаполнено(Строка.РеквизитРодитель), Строка.РеквизитРодитель + "_" + Строка.Наименование,
				Строка.Наименование);
			СтрокаФИО.ОбъектРодитель = Путь + "|" + Строка.Наименование;
			СтрокаФИО.Описание = НСтр("ru = 'Полное имя'");
			СтрокаФИО.ОписаниеПоляЗамены = Строка.РеквизитРодитель + Строка.Наименование + НСтр("ru = 'ФИО'");
			СтрокаФИО.ИспользоватьПадеж = Истина;
			
			СтрокаФамилияИнициалы = Строка.Строки.Добавить();
			СтрокаФамилияИнициалы.Наименование = "ФамилияИнициалы";
			СтрокаФамилияИнициалы.НаименованиеДляWord = "ФамилияИнициалы";
			СтрокаФамилияИнициалы.Тип = "Строка";
			СтрокаФамилияИнициалы.РеквизитРодитель =
				?(ЗначениеЗаполнено(Строка.РеквизитРодитель), Строка.РеквизитРодитель + "_" + Строка.Наименование,
				Строка.Наименование);
			СтрокаФамилияИнициалы.ОбъектРодитель = Путь + "|" + Строка.Наименование;
			СтрокаФамилияИнициалы.Описание = НСтр("ru = 'Фамилия и инициалы'");
			СтрокаФамилияИнициалы.ОписаниеПоляЗамены = Строка.РеквизитРодитель + Строка.Наименование + НСтр("ru = 'ФамилияИнициалы'");
			СтрокаФамилияИнициалы.ИспользоватьПадеж = Истина;
			
		КонецЕсли;
		
		Для Каждого СправочникМетаданные Из Метаданные.Справочники Цикл
			
			Если СправочникМетаданные.ПредставлениеОбъекта <> Строка(Реквизит.Тип) Тогда
				Если ОписаниеТипаСотрудникиПользователи <> Реквизит.Тип Или 
					СправочникМетаданные <> Метаданные.Справочники.Сотрудники Тогда
					Продолжить;
				КонецЕсли;
			ИначеЕсли СправочникМетаданные.Имя = "ВидыДокументов"
				Или СправочникМетаданные.Имя = "Валюты" 
				Или СправочникМетаданные.Имя = "ГрифыДоступа" Тогда
				Продолжить;
				
			ИначеЕсли СправочникМетаданные.Имя = "Пользователи" Тогда 
				ТочечныеИсключения = Новый СписокЗначений;
				ТочечныеИсключения.Добавить("Недействителен");
				ТочечныеИсключения.Добавить("Комментарий");
				ТочечныеИсключения.Добавить("Служебный");
				ТочечныеИсключения.Добавить("Подготовлен");
				ТочечныеИсключения.Добавить("ИдентификаторПользователяИБ");
				ТочечныеИсключения.Добавить("РазрешенныеВебСерверы");
				ТочечныеИсключения.Добавить("СвойстваПользователяИБ");
				ТочечныеИсключения.Добавить("ИдентификаторПользователяСервиса");
				ТочечныеИсключения.Добавить("КодЯзыка");
				ТочечныеИсключения.Добавить("Подразделение");
				ТочечныеИсключения.Добавить("ФизическоеЛицо");
				
			ИначеЕсли СправочникМетаданные.Имя = "Номенклатура" Тогда
				
				ТочечныеИсключения = Новый СписокЗначений;
				ТочечныеИсключения.Добавить("Цена");
				ТочечныеИсключения.Добавить("ЕдиницаИзмерения");
				ТочечныеИсключения.Добавить("СтавкаНДС");
				ТочечныеИсключения.Добавить("УдалитьСтавкаНДС");
				
			ИначеЕсли СправочникМетаданные.Имя = "СтавкиНДС" Тогда
				
				ТочечныеИсключения = Новый СписокЗначений;
				ТочечныеИсключения.Добавить("НачалоПериода");
				ТочечныеИсключения.Добавить("КонецПериода");
				ТочечныеИсключения.Добавить("РасчетнаяСтавка");
				ТочечныеИсключения.Добавить("Ставка");
				ТочечныеИсключения.Добавить("Страна");
				
			ИначеЕсли СправочникМетаданные.Имя = "Контрагенты" Тогда 
				
				ТочечныеИсключения = Новый СписокЗначений;
				ТочечныеИсключения.Добавить("ГруппаДоступа");
				ТочечныеИсключения.Добавить("Комментарий");
				ТочечныеИсключения.Добавить("ДействуетНаОсновании");
				ТочечныеИсключения.Добавить("ЮрФизЛицо");
								
			КонецЕсли;
			
			ЗаполнитьДеревоРеквизитовКраткойФормы(Строка, СправочникМетаданные.Реквизиты, Путь + "|" + Строка.Наименование,
				УровеньВложенности + 1, ВидВладельцаФайла, ОбязательныеРеквизиты, ИсключенияРеквизитов, ТочечныеИсключения, 
				РеквизитТабличнойЧасти);
			ТочечныеИсключения = Новый СписокЗначений;
			Попытка
				Если СправочникМетаданные.ТабличныеЧасти.Найти("ДополнительныеРеквизиты") <> Неопределено Тогда
					Если СправочникМетаданные.Имя = "Номенклатура" Тогда
						ЗаполнитьСписокДопРеквизитовКраткойФормы(Родитель, СправочникМетаданные, Путь + "|" + Реквизит.Имя, 
							ВидВладельцаФайла, РеквизитТабличнойЧасти);
					Иначе 
						ЗаполнитьСписокДопРеквизитовКраткойФормы(Строка, СправочникМетаданные, Путь + "|" + Реквизит.Имя, 
							ВидВладельцаФайла, РеквизитТабличнойЧасти);
					КонецЕсли;
				КонецЕсли;
			Исключение
				// У справочника может не быть ни одного настроенного доп.реквизита
			КонецПопытки;
			
			Если СправочникМетаданные.ТабличныеЧасти.Найти("КонтактнаяИнформация") <> Неопределено Тогда
				ЗаполнитьСписокКонтактнойИнформацииКраткойФормы(Строка, СправочникМетаданные, Путь + "|" + Реквизит.Имя);
			КонецЕсли;
			
			Прервать;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСписокДопРеквизитовКраткойФормы(Строка, СправочникМетаданные, Путь, ВидВладельцаФайла, РеквизитТабличнойЧасти, ТипКонтрагента = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	Объект = Справочники[СправочникМетаданные.Имя].СоздатьЭлемент();
	
	Если ЗначениеЗаполнено(ТипКонтрагента) Тогда 
		Объект.ЮридическоеФизическоеЛицо = ТипКонтрагента;
	КонецЕсли;
	
	НаборСвойств = УправлениеСвойствамиСлужебный.ПолучитьНаборыСвойствОбъекта(Объект);
	КоличествоНаборов = НаборСвойств.Количество();
	
	Если КоличествоНаборов > 0 Тогда
		Для Каждого ВложенныйНаборСвойств Из НаборСвойств Цикл
			Для Каждого ДопРеквизит Из ВложенныйНаборСвойств.Набор.ДополнительныеРеквизиты Цикл
				РеквизитыСвойства = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДопРеквизит.Свойство,
					"ПометкаУдаления, Заголовок, Наименование, ТипЗначения, Комментарий, УникальныйКодДляПоля, ФорматСвойства");
				Если ДопРеквизит.ПометкаУдаления Или РеквизитыСвойства.ПометкаУдаления = Истина Тогда 
					Продолжить;
				КонецЕсли;
				
				НоваяСтрокаДопРеквизит = Строка.Строки.Добавить();
				НоваяСтрокаДопРеквизит.Наименование = РеквизитыСвойства.УникальныйКодДляПоля;
				НоваяСтрокаДопРеквизит.НаименованиеДляWord = РеквизитыСвойства.УникальныйКодДляПоля;
				НоваяСтрокаДопРеквизит.Тип = РеквизитыСвойства.ТипЗначения;
				НоваяСтрокаДопРеквизит.Описание = РеквизитыСвойства.Заголовок;
				НоваяСтрокаДопРеквизит.ДополнительныйРеквизит = Истина;
				НоваяСтрокаДопРеквизит.ФорматСвойства = РеквизитыСвойства.ФорматСвойства;
				
				Если КоличествоНаборов = 1 Тогда 
					НаборНаименование = "";
				Иначе 
					НаборНаименование = ВложенныйНаборСвойств.Набор.Наименование;
				КонецЕсли;
				
				НоваяСтрокаДопРеквизит.ОбъектРодитель = Путь + "|" + НСтр("ru = 'ДопРеквизиты'") 
					+ ?(ЗначениеЗаполнено(НаборНаименование), "|" + НаборНаименование, "");
				НоваяСтрокаДопРеквизит.ОбъектРодитель = СтрЗаменить(НоваяСтрокаДопРеквизит.ОбъектРодитель, 
					НСтр("ru = 'Доп. свойства'"), НСтр("ru = 'ДопСвойства'"));
					
					
				НоваяСтрокаДопРеквизит.РеквизитТабличнойЧасти = РеквизитТабличнойЧасти;
				
				Если Не РеквизитТабличнойЧасти Тогда 
					РеквизитРодитель = СтрЗаменить(НоваяСтрокаДопРеквизит.ОбъектРодитель,"ВладелецФайла", "");
					РеквизитРодитель = СтрЗаменить(РеквизитРодитель, "|", "_");
					НоваяСтрокаДопРеквизит.РеквизитРодитель = РеквизитРодитель;
					Если Лев(НоваяСтрокаДопРеквизит.РеквизитРодитель, 1) = "_" Тогда 
						НоваяСтрокаДопРеквизит.РеквизитРодитель = Прав(НоваяСтрокаДопРеквизит.РеквизитРодитель, 
							СтрДлина(НоваяСтрокаДопРеквизит.РеквизитРодитель) - 1);
					КонецЕсли;
				Иначе 
					НоваяСтрокаДопРеквизит.НомерКолонкиТабличнойЧасти = 10000;
					НоваяСтрокаДопРеквизит.РеквизитРодитель = "ТЧ";
				КонецЕсли;
				
				Если ЗначениеЗаполнено(РеквизитыСвойства.Комментарий) Тогда 
					НоваяСтрокаДопРеквизит.ОписаниеПоляЗамены = РеквизитыСвойства.Комментарий;
				Иначе 
					НоваяСтрокаДопРеквизит.ОписаниеПоляЗамены = РеквизитыСвойства.Заголовок;
				КонецЕсли;
				
				Если НоваяСтрокаДопРеквизит.Тип <> ТипЗнч("Дата") 
					И НоваяСтрокаДопРеквизит.Тип <> ТипЗнч("Число")
					И НоваяСтрокаДопРеквизит.Тип <> ТипЗнч("Булево") Тогда
					НоваяСтрокаДопРеквизит.ИспользоватьПадеж = Истина;
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ЗаполнитьСписокКонтактнойИнформацииКраткойФормы(Строка, СправочникМетаданные, Путь)
	
	//Получение набора контактной информации для данного справочника
	// Получим список видов КИ
	ИмяСправочника = СправочникМетаданные.Имя;
	ГруппаВидовКИ = Справочники.ВидыКонтактнойИнформации["Справочник" + ИмяСправочника];
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыКонтактнойИнформации.Ссылка КАК Вид,
	|	ВидыКонтактнойИнформации.Ссылка.ИмяПредопределенныхДанных Как УникальноеИмя,
	|	ВидыКонтактнойИнформации.Наименование,
	|	ВидыКонтактнойИнформации.Тип,
	|	ВидыКонтактнойИнформации.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	|ГДЕ
	|	ВидыКонтактнойИнформации.Родитель = &ГруппаВидовКИ
	|	И ВидыКонтактнойИнформации.Тип <> ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПометкаУдаления,
	|	ВидыКонтактнойИнформации.РеквизитДопУпорядочивания";
	
	Запрос.УстановитьПараметр("ГруппаВидовКИ", ГруппаВидовКИ);
	УстановитьПривилегированныйРежим(Истина);
	КонтактнаяИнформация = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	Если КонтактнаяИнформация.Количество() > 0 Тогда
		Для Каждого Запись Из КонтактнаяИнформация Цикл
			Если Не Запись.ПометкаУдаления Тогда 
				НоваяСтрока = Строка.Строки.Добавить();
				Если ЗначениеЗаполнено(Запись.УникальноеИмя) Тогда 
					ИмяРеквизита = Запись.УникальноеИмя;
				Иначе 
					ИмяРеквизита = Запись.Наименование;
				КонецЕсли;
				
				НоваяСтрока.Наименование = ИмяРеквизита;
				НоваяСтрока.НаименованиеДляWord = ИмяРеквизита;
				НоваяСтрока.Тип = Запись.Тип;
				НоваяСтрока.ОбъектРодитель = Путь 
					+ "|КонтактнаяИнформация";
				НоваяСтрока.Описание = Запись.Наименование;
				НоваяСтрока.ОписаниеПоляЗамены = ИмяРеквизита;
				
				РеквизитРодитель = СтрЗаменить(НоваяСтрока.ОбъектРодитель,"ВладелецФайла", "");
				РеквизитРодитель = СтрЗаменить(РеквизитРодитель, "|", "_");
				НоваяСтрока.РеквизитРодитель = РеквизитРодитель;
				Если Лев(НоваяСтрока.РеквизитРодитель, 1) = "_" Тогда 
					НоваяСтрока.РеквизитРодитель = Прав(НоваяСтрока.РеквизитРодитель, 
						СтрДлина(НоваяСтрока.РеквизитРодитель) - 1);
				КонецЕсли;
				НоваяСтрока.КонтактнаяИнформация = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыДляШаблона(
	Родитель, 
	НаборРеквизитов, 
	Путь, 
	УровеньВложенности, 
	ВидВладельца,
	ОбязательныеРеквизиты,
	ИсключенияРеквизитов,
	Знач ТочечныеИсключенияРеквизитов = Неопределено,
	ПоказыватьВсеРеквизиты)
	
	Если УровеньВложенности > 2 Тогда
		Возврат;
	КонецЕсли;
	
	Массив = Новый Массив;
	Массив.Добавить(Тип("Булево"));
	ОписаниеБулева = Новый ОписаниеТипов(Массив);
	Для Каждого Реквизит Из НаборРеквизитов Цикл
		
		Если ТипЗнч(Реквизит) <> Тип("ОписаниеСтандартногоРеквизита") Тогда
			Если ОбязательныеРеквизиты.НайтиПоЗначению(Реквизит.Имя) = Неопределено Тогда
				Если ИсключенияРеквизитов[Реквизит.Имя] <> Неопределено 
					Или (ТочечныеИсключенияРеквизитов <> Неопределено И 
						ТочечныеИсключенияРеквизитов.НайтиПоЗначению(Реквизит.Имя) <> Неопределено)
					Или (Не ПоказыватьВсеРеквизиты И Реквизит.Тип = ОписаниеБулева) Тогда 
					Продолжить;
				КонецЕсли;

				РеквизитВключен = Истина;
				РеквизитВходитВФункциональныеОпции = Ложь;
				
				ДоступныеОпции = АвтозаполнениеШаблоновФайловСерверПовтИсп.ПолучитьСписокФункциональныхОпций(Реквизит.ПолноеИмя());
				РеквизитВходитВФункциональныеОпции = ДоступныеОпции.Независимые.Количество() 
					Или ДоступныеОпции.Параметрические.Количество();
				РеквизитВключен = РеквизитДоступенПоФункциональнымОпциям(ДоступныеОпции, ВидВладельца);
				
				Если РеквизитВходитВФункциональныеОпции И НЕ РеквизитВключен Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Строка = Родитель.Строки.Добавить();
		Строка.Наименование = Реквизит.Имя;
		Строка.Тип = Реквизит.Тип;
		Строка.ОбъектРодитель = Путь;
		
		РеквизитРодитель = СтрЗаменить(Путь, "|", "_");
		Строка.РеквизитРодитель = РеквизитРодитель;
		Если Лев(Строка.РеквизитРодитель, 1) = "_" Тогда 
			Строка.РеквизитРодитель = Прав(Строка.РеквизитРодитель, СтрДлина(Строка.РеквизитРодитель) - 1);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Реквизит.Синоним) Тогда 
			Строка.Описание = Реквизит.Имя;
			Строка.ОписаниеПоляЗамены = Реквизит.Имя;
		Иначе 
			Строка.Описание = Реквизит.Синоним;
			Строка.ОписаниеПоляЗамены = Реквизит.Синоним;
		КонецЕсли;
		
		
		// Индивидуальная проверка для контрагентов и организаций.
		// виртуальные реквизиты КПП на дату, Наименование на дату:
		Если (Строка.ОбъектРодитель = "Контрагент" Или Строка.ОбъектРодитель = "Организация")
				И (
					Строка.Наименование = "КПП" Или Строка.Наименование = "НаименованиеПолное"
				) Тогда
			
			СтрокаНаДату = Родитель.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаНаДату, Строка);
			СтрокаНаДату.Наименование			= Строка.Наименование + "НаДату";
			СтрокаНаДату.Тип					= "СтрокаПолученнаяДинамически";
			СтрокаНаДату.Описание				= СтрШаблон("%1 %2", Строка.Описание, Текст_НаДату());
			СтрокаНаДату.ОписаниеПоляЗамены		= СтрокаНаДату.Описание;
			
			Если Строка.Наименование = "НаименованиеПолное" Тогда
				// Кроме полного нужно добавить еще обычное наименование на дату:
				СтрокаНаДату2 = Родитель.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаНаДату2, СтрокаНаДату);
				СтрокаНаДату2.Наименование			= "НаименованиеНаДату";
				СтрокаНаДату2.Тип					= "СтрокаПолученнаяДинамически";
				СтрокаНадату2.Описание				= Текст_НаименованиеНаДату();
				СтрокаНаДату2.ОписаниеПоляЗамены	= СтрокаНаДату2.Описание;
			КонецЕсли;
		КонецЕсли;
		
		Если Реквизит.Тип.СодержитТип(Тип("СправочникСсылка.Сотрудники")) Тогда
			
			СтрокаПредставлениеВДокументах = Строка.Строки.Добавить();
			СтрокаПредставлениеВДокументах.Наименование = "ПредставлениеВДокументах";
			СтрокаПредставлениеВДокументах.Тип = "Строка";
			СтрокаПредставлениеВДокументах.ОбъектРодитель = Строка.Наименование;
			СтрокаПредставлениеВДокументах.Описание = НСтр("ru = 'Представление в документах'");
			СтрокаПредставлениеВДокументах.ОписаниеПоляЗамены = НСтр("ru = 'Представление в документах'");
			
			СтрокаПредставлениеВПереписке = Строка.Строки.Добавить();
			СтрокаПредставлениеВПереписке.Наименование = "ПредставлениеВПереписке";
			СтрокаПредставлениеВПереписке.Тип = "Строка";
			СтрокаПредставлениеВПереписке.ОбъектРодитель = Строка.Наименование;
			СтрокаПредставлениеВПереписке.Описание = НСтр("ru = 'Представление в переписке'");
			СтрокаПредставлениеВПереписке.ОписаниеПоляЗамены = НСтр("ru = 'Представление в переписке'");
			
			СтрокаФИО = Строка.Строки.Добавить();
			СтрокаФИО.Наименование = "ФИО";
			СтрокаФИО.Тип = "Строка";
			СтрокаФИО.ОбъектРодитель = Строка.Наименование;
			СтрокаФИО.Описание = НСтр("ru = 'Полное имя'");
			СтрокаФИО.ОписаниеПоляЗамены = НСтр("ru = 'Полное имя'");
			
			СтрокаФамилияИнициалы = Строка.Строки.Добавить();
			СтрокаФамилияИнициалы.Наименование = "ФамилияИнициалы";
			СтрокаФамилияИнициалы.Тип = "Строка";
			СтрокаФамилияИнициалы.ОбъектРодитель = Строка.Наименование;
			СтрокаФамилияИнициалы.Описание = НСтр("ru = 'Фамилия и инициалы'");
			СтрокаФамилияИнициалы.ОписаниеПоляЗамены = НСтр("ru = 'Фамилия и инициалы'");
			
		КонецЕсли;
		
		Для Каждого СправочникМетаданные Из Метаданные.Справочники Цикл
			
			Если СправочникМетаданные.ПредставлениеОбъекта <> Строка(Реквизит.Тип) Тогда
				Продолжить;
			ИначеЕсли СправочникМетаданные.Имя = "Валюты" 
				Или СправочникМетаданные.Имя = "ГрифыДоступа" Тогда
				Продолжить;
				
			ИначеЕсли СправочникМетаданные.Имя = "Пользователи" Тогда 
				ТочечныеИсключения = Новый СписокЗначений;
				ТочечныеИсключения.Добавить("Недействителен");
				ТочечныеИсключения.Добавить("Комментарий");
				ТочечныеИсключения.Добавить("Служебный");
				ТочечныеИсключения.Добавить("Подготовлен");
				ТочечныеИсключения.Добавить("ИдентификаторПользователяИБ");
				ТочечныеИсключения.Добавить("РазрешенныеВебСерверы");
				ТочечныеИсключения.Добавить("СвойстваПользователяИБ");
				ТочечныеИсключения.Добавить("ИдентификаторПользователяСервиса");
				ТочечныеИсключения.Добавить("КодЯзыка");
				ТочечныеИсключения.Добавить("Подразделение");
				ТочечныеИсключения.Добавить("ФизическоеЛицо");
				
			ИначеЕсли СправочникМетаданные.Имя = "Контрагенты" Тогда 
				
				ТочечныеИсключения = Новый СписокЗначений;
				ТочечныеИсключения.Добавить("ГруппаДоступа");
				ТочечныеИсключения.Добавить("Комментарий");
				ТочечныеИсключения.Добавить("ЮрФизЛицо");
				
			ИначеЕсли СправочникМетаданные.Имя = "ВидыДокументов" Тогда 
				
				ТочечныеИсключения = Новый СписокЗначений;
				ТочечныеИсключения.Добавить("НаборСвойств");
				ТочечныеИсключения.Добавить("СрокИсполнения");
				ТочечныеИсключения.Добавить("Комментарий");
					
			КонецЕсли;
			
			НовыйПуть = ?(ЗначениеЗаполнено(Путь), Путь + "|" + Строка.Наименование, Строка.Наименование);
			ЗаполнитьРеквизитыДляШаблона(Строка, СправочникМетаданные.Реквизиты, НовыйПуть,
				УровеньВложенности + 1, ВидВладельца, ОбязательныеРеквизиты, ИсключенияРеквизитов, ТочечныеИсключения, ПоказыватьВсеРеквизиты);
			ТочечныеИсключения = Новый СписокЗначений;
			Попытка
				НовыйПуть = ?(ЗначениеЗаполнено(Путь), Путь + "|" + Реквизит.Имя, Реквизит.Имя);
				Если СправочникМетаданные.ТабличныеЧасти.Найти("ДополнительныеРеквизиты") <> Неопределено Тогда
					ЗаполнитьСписокДопРеквизитовДляШаблона(Строка, СправочникМетаданные, НовыйПуть, 
						ВидВладельца, ПоказыватьВсеРеквизиты);
				КонецЕсли;
			Исключение
				// У справочника может не быть ни одного настроенного доп.реквизита
			КонецПопытки;
			
			Прервать;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСписокДопРеквизитовДляШаблона(Строка, СправочникМетаданные, Путь, ВидВладельцаФайла, ПоказыватьВсеРеквизиты)
	
	УстановитьПривилегированныйРежим(Истина);
	Объект = Справочники[СправочникМетаданные.Имя].СоздатьЭлемент();
	
	НаборСвойств = УправлениеСвойствамиСлужебный.ПолучитьНаборыСвойствОбъекта(Объект);
	КоличествоНаборов = НаборСвойств.Количество();
	
	Если КоличествоНаборов > 0 Тогда
		Массив = Новый Массив;
		Массив.Добавить(Тип("Булево"));
		ОписаниеБулева = Новый ОписаниеТипов(Массив);
		
		Для Каждого ВложенныйНаборСвойств Из НаборСвойств Цикл
			Для Каждого ДопРеквизит Из ВложенныйНаборСвойств.Набор.ДополнительныеРеквизиты Цикл
				РеквизитыСвойства = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДопРеквизит.Свойство,
					"ПометкаУдаления, Заголовок, Наименование, ТипЗначения, Комментарий, ФорматСвойства");
				Если ДопРеквизит.ПометкаУдаления Или РеквизитыСвойства.ПометкаУдаления = Истина Тогда 
					Продолжить;
				КонецЕсли;
				
				Если Не ПоказыватьВсеРеквизиты И РеквизитыСвойства.ТипЗначения = ОписаниеБулева Тогда 
					Продолжить;
				КонецЕсли;
				
				НоваяСтрокаДопРеквизит = Строка.Строки.Добавить();
				НоваяСтрокаДопРеквизит.Наименование = РеквизитыСвойства.Заголовок;
				НоваяСтрокаДопРеквизит.Тип = РеквизитыСвойства.ТипЗначения;
				НоваяСтрокаДопРеквизит.Описание = РеквизитыСвойства.Заголовок;
				НоваяСтрокаДопРеквизит.ДополнительныйРеквизит = Истина;
				
				Если КоличествоНаборов = 1 Тогда 
					НаборНаименование = "";
				Иначе 
					НаборНаименование = ВложенныйНаборСвойств.Набор.Наименование;
				КонецЕсли;
				
				НоваяСтрокаДопРеквизит.ОбъектРодитель = Путь + "|" + НСтр("ru = 'ДопРеквизиты'") 
					+ ?(ЗначениеЗаполнено(НаборНаименование), "|" + НаборНаименование, "");
				НоваяСтрокаДопРеквизит.ОбъектРодитель = СтрЗаменить(НоваяСтрокаДопРеквизит.ОбъектРодитель, 
					НСтр("ru = 'Доп. свойства'"), НСтр("ru = 'ДопСвойства'"));
			
				РеквизитРодитель = СтрЗаменить(Путь, "|", "_");
				НоваяСтрокаДопРеквизит.РеквизитРодитель = РеквизитРодитель;
				Если Лев(НоваяСтрокаДопРеквизит.РеквизитРодитель, 1) = "_" Тогда 
					НоваяСтрокаДопРеквизит.РеквизитРодитель = Прав(НоваяСтрокаДопРеквизит.РеквизитРодитель, 
						СтрДлина(НоваяСтрокаДопРеквизит.РеквизитРодитель) - 1);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(РеквизитыСвойства.Комментарий) Тогда 
					НоваяСтрокаДопРеквизит.ОписаниеПоляЗамены = РеквизитыСвойства.Комментарий;
				Иначе 
					НоваяСтрокаДопРеквизит.ОписаниеПоляЗамены = РеквизитыСвойства.Заголовок;
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ДобавитьСтрокуДляШаблона(Строка, Путь, Наименование, Описание, Тип, ОписаниеПоляЗамены = "")
	
	НоваяСтрока = Строка.Строки.Добавить();
	
	НоваяСтрока.Наименование = Наименование;
	НоваяСтрока.Тип = Тип;
	НоваяСтрока.Описание = Описание;
	НоваяСтрока.ОбъектРодитель = Путь;
	
	РеквизитРодитель = СтрЗаменить(НоваяСтрока.ОбъектРодитель, "|", "_");
	НоваяСтрока.ОбъектРодитель = РеквизитРодитель;
	Если Лев(НоваяСтрока.ОбъектРодитель, 1) = "_" Тогда 
		НоваяСтрока.ОбъектРодитель = Прав(НоваяСтрока.ОбъектРодитель, СтрДлина(НоваяСтрока.ОбъектРодитель) - 1);
	КонецЕсли;
	
	Если ОписаниеПоляЗамены = "" Тогда 
		НоваяСтрока.ОписаниеПоляЗамены = Описание;
	Иначе 
		НоваяСтрока.ОписаниеПоляЗамены = ОписаниеПоляЗамены;
	КонецЕсли;
	
КонецПроцедуры

Функция РеквизитДоступенПоФункциональнымОпциям(ДоступныеОпции, ЗначениеПараметра)
	
	РеквизитДоступен = Ложь;
	
	Если ДоступныеОпции.Независимые.Количество() Тогда
		Для Каждого Опция Из ДоступныеОпции.Независимые Цикл
			РеквизитДоступен = ПолучитьФункциональнуюОпцию(Опция.Ключ);
			Если РеквизитДоступен Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	
		Если Не РеквизитДоступен Тогда
			Возврат РеквизитДоступен;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого Опция Из ДоступныеОпции.Параметрические Цикл
		ПараметрыОпции = Новый Структура;
		Для Каждого Параметр Из Опция.Значение.Параметры Цикл
			Если (Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(ЗначениеПараметра))
					И СтрСравнить(Опция.Значение.Хранение.ПолноеИмя, ЗначениеПараметра.Метаданные().ПолноеИмя()) = 0)
					Или (СтрСравнить(Опция.Значение.Хранение.Тип, "РегистрСведений") = 0 
						И Параметр.Значение.Тип.СодержитТип(ТипЗнч(ЗначениеПараметра))) Тогда
				
				ПараметрыОпции.Вставить(Параметр.Ключ, ЗначениеПараметра);
						
			КонецЕсли;
		КонецЦикла;
			
		Если ПараметрыОпции.Количество() Тогда
			РеквизитДоступен = ПолучитьФункциональнуюОпцию(Опция.Ключ, ПараметрыОпции);
			Если РеквизитДоступен Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат РеквизитДоступен;
	
КонецФункции

Функция Текст_НаДату()
	Возврат НСтр("ru = 'на дату'");
КонецФункции

Функция Текст_НаименованиеНаДату()
	Возврат НСтр("ru = 'Наименование на дату'");
КонецФункции

Функция ПредставлениеСправочникаЮрлиц(ОбъектРодитель)
	
	ПредставлениеРодителя = Неопределено;
	Если СтрНайти(ОбъектРодитель, "Контрагент") > 0 Тогда
		ПредставлениеРодителя = НСтр("ru = 'Контрагент'");
	ИначеЕсли СтрНайти(ОбъектРодитель, "Организация") > 0 Тогда
		ПредставлениеРодителя = РедакцииКонфигурацииКлиентСервер.Организация();;
	КонецЕсли;
	
	Возврат ПредставлениеРодителя;
	
КонецФункции

#КонецОбласти
