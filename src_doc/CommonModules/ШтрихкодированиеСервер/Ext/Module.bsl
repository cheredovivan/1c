#Область ПрограммныйИнтерфейс

//Формирует очередное значение штрихкода
//Параметры:	ПрефиксВнутреннегоШтрихкода - код распределенной базы
//Возвращает:   Значение штрихкода
//Штрихкод формируется по формату ПрефиксВнутреннегоШтрихкода_ПорядковыйНомер_КонтрольныйСимвол
Функция СформироватьШтрихКод() Экспорт

	ПрефиксВнутреннегоШтрихкода = Константы.ПрефиксИБвШтрихкоде.Получить();
	ИспользоватьОбмен = Константы.ИспользоватьСинхронизациюДанных.Получить();
	ИспользоватьКОД = Ложь;

	Если НЕ ЗначениеЗаполнено(ПрефиксВнутреннегоШтрихкода)
		ИЛИ НЕ (ИспользоватьОбмен Или ИспользоватьКОД) Тогда
		ПрефиксВнутреннегоШтрихкода = "000";
	КонецЕсли;
	
	ПрефиксВнутреннегоШтрихкода = Формат(ПрефиксВнутреннегоШтрихкода, "ЧЦ=3; ЧН=; ЧВН=");

	ТекКод = ОчереднойНомер();

	Штрихкод = ПрефиксВнутреннегоШтрихкода + Формат(ТекКод, "ЧЦ=9; ЧВН=; ЧГ=");
    Штрихкод = Штрихкод + КонтрольныйСимволEAN(Штрихкод, 13);  
	Возврат Штрихкод;

КонецФункции // СформироватьШтрихКод()

//Получает очередной порядковый номер для значения штрихкода
Функция ОчереднойНомер() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДокументОбъект = Документы.НомерШтрихкода.СоздатьДокумент();
	ДокументОбъект.Дата = ТекущаяДатаСеанса();
	ДокументОбъект.Записать();
	Возврат ДокументОбъект.Номер;
	
КонецФункции

// Создает экземпляр документа НомерШтрихкода с новым максимальным номером (если ТекущийНомер выше, чем максимальный номер в базе)
//
//Параметры:	
// ТекущийНомер - Число
Процедура УстановитьМаксНомер(ТекущийНомер) Экспорт

	УстановитьПривилегированныйРежим(Истина);   
	
	МаксНомер = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(МАКСИМУМ(НомерШтрихкода.Номер), 0) КАК МаксНомер
	               |ИЗ
	               |	Документ.НомерШтрихкода КАК НомерШтрихкода";
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		МаксНомер = Выборка.МаксНомер;
		
	КонецЕсли;
	
	Если ТекущийНомер > МаксНомер Тогда
	
		ДокументОбъект = Документы.НомерШтрихкода.СоздатьДокумент();
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		ДокументОбъект.Номер = ТекущийНомер;
		ДокументОбъект.Записать();
		
	КонецЕсли;
	
КонецПроцедуры	

//Формирует контрольный символ для штрихкодов EAN-8 и -13
//Параметры: ШтрихКод - строка со штрихкодом без контрольного символа
//			 Тип - тип штрихкода, 8 или 13
//Возвращает: Контрольный символ, от "0" до "9"
Функция КонтрольныйСимволEAN(ШтрихКод, Тип)

	Четн   = 0;
	Нечетн = 0;

	КоличествоИтераций = ?(Тип = 13, 6, 4);

	Для Индекс = 1 По КоличествоИтераций Цикл
		Если (Тип = 8) и (Индекс = КоличествоИтераций) Тогда
		Иначе
			Четн   = Четн   + Сред(ШтрихКод, 2 * Индекс, 1);
		КонецЕсли;
		Нечетн = Нечетн + Сред(ШтрихКод, 2 * Индекс - 1, 1);
	КонецЦикла;

	Если Тип = 13 Тогда
		Четн = Четн * 3;
	Иначе
		Нечетн = Нечетн * 3;
	КонецЕсли;

	КонтЦифра = 10 - (Четн + Нечетн) % 10;

	Возврат ?(КонтЦифра = 10, "0", Строка(КонтЦифра));

КонецФункции // КонтрольныйСимволEAN()

//Присваивает штрихкод объекту
//Параметры: Ссылка - ссылка на Внутренний, Входящий, Исходящий документ или Файл
//			 Код - значение штрихкода
//			 ВнутреннийШтрихкод - флаг, показывающий, что создается штрихкод данной организации
Процедура ПрисвоитьШтрихКод(Ссылка, Код, ВнутреннийШтрихкод = Истина) Экспорт
	
	Если Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ВнутреннийШтрихкод Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Штрихкоды.Владелец,
			|	Штрихкоды.Код
			|ИЗ
			|	РегистрСведений.Штрихкоды КАК Штрихкоды
			|ГДЕ
			|	Штрихкоды.Владелец = &Владелец
			|	И Штрихкоды.ВнутреннийШтрихкод = &ВнутреннийШтрихкод
			|	И Штрихкоды.Код <> &Код";
		Запрос.УстановитьПараметр("Владелец", Ссылка);
		Запрос.УстановитьПараметр("ВнутреннийШтрихкод", Истина);
		Запрос.УстановитьПараметр("Код", Код);
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка присвоения штрихкода %3. У объекта ""%1"" уже есть присвоенный ранее штрихкод %2'"),
				Строка(Ссылка),
				Строка(Выборка.Код),
				Строка(Код));
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Присвоение штрихкода'"),
				УровеньЖурналаРегистрации.Ошибка,
				Ссылка.Метаданные(),
				Ссылка,
				ТекстОшибки);
			Возврат;	
		КонецЕсли;
	КонецЕсли;
	
	ТекЗапись = РегистрыСведений.Штрихкоды.СоздатьМенеджерЗаписи();
	ТекЗапись.Владелец = Ссылка;
	ТекЗапись.ВнутреннийШтрихкод = ВнутреннийШтрихкод;
	ТекЗапись.Код = Код;
	Попытка
		ТекЗапись.Записать();
	Исключение
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Произошла ошибка при записи штрихкода у объекта ""%1"":
				|%2'"),
			Ссылка.Наименование,
			ИнформацияОбОшибке().Описание);
		ВызватьИсключение(ТекстОшибки);
	КонецПопытки;

КонецПроцедуры //ПрисвоитьШтрихКод()

//Получает значение штрихкода указанного объекта
//Параметры:	Ссылка - ссылка на Внутренний, Входящий, Исходящий документ или Файл
//Возвращает:	Значение штрихкода, если штрихкод найден
//				Неопределено, если штрихкод не найден
Функция ПолучитьШтрихКод(Ссылка) Экспорт
	
	НаборЗаписей = РегистрыСведений.Штрихкоды.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Владелец.Установить(Ссылка);
	НаборЗаписей.Прочитать();
	Для Каждого Запись Из НаборЗаписей Цикл
		Если Запись.ВнутреннийШтрихкод Тогда
			Возврат Запись.Код;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
	
КонецФункции //ПолучитьШтрихКод()

// Получает из персональных настроек параметры расположения штрихкода на странице
//
// Возвращаемое значение:
// 	См. ШтрихкодированиеКлиентСервер.НовыеНастройкиШтрихкода
Функция ПолучитьПерсональныеНастройкиПоложенияШтрихкодаНаСтранице() Экспорт
	
	ПоказыватьФормуНастройки = ХранилищеОбщихНастроек.Загрузить("НастройкиШтрихкода", "ПоказыватьФормуНастройкиШтрихкода");
	ПоложениеНаСтранице = ХранилищеОбщихНастроек.Загрузить("НастройкиШтрихкода", "ПоложениеШтрихкодаНаСтранице");
	СмещениеПоГоризонтали = ХранилищеОбщихНастроек.Загрузить("НастройкиШтрихкода", "СмещениеПоГоризонтали");
	СмещениеПоВертикали = ХранилищеОбщихНастроек.Загрузить("НастройкиШтрихкода", "СмещениеПоВертикали");
	ШиринаНаклейки = ХранилищеОбщихНастроек.Загрузить("НастройкиШтрихкода", "ШиринаНаклейки");
	ВысотаНаклейки = ХранилищеОбщихНастроек.Загрузить("НастройкиШтрихкода", "ВысотаНаклейки");	
    ВысотаШК = ХранилищеОбщихНастроек.Загрузить("НастройкиШтрихкода", "ВысотаШтрихкодаПриВставкеВФайл");
	
	Если ПоказыватьФормуНастройки = Неопределено Тогда
		ПоказыватьФормуНастройки = Истина;
	КонецЕсли;	

	Если НЕ ЗначениеЗаполнено(ШиринаНаклейки) Тогда
		ШиринаНаклейки = 50;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ВысотаНаклейки) Тогда
		ВысотаНаклейки = 10;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВысотаШК) Тогда
		ВысотаШК = 10;
	КонецЕсли;
	ПоказыватьЦифры = ХранилищеОбщихНастроек.Загрузить("НастройкиШтрихкода", "ВставлятьЦифрыВШК");
	ПоказыватьЦифры = ?(ПоказыватьЦифры = Неопределено,Ложь,ПоказыватьЦифры);
	
	Если Не ЗначениеЗаполнено(ПоложениеНаСтранице) Тогда
		ПоложениеНаСтранице = Перечисления.МестаВставкиКартинки.ПравыйВерхний;
	КонецЕсли;
	
	НастройкиШтрихкода = Новый Структура;
	НастройкиШтрихкода.Вставить("ПоказыватьФормуНастройки", ПоказыватьФормуНастройки);
	НастройкиШтрихкода.Вставить("ПоложениеНаСтранице", ПоложениеНаСтранице);
	НастройкиШтрихкода.Вставить("СмещениеПоГоризонтали", СмещениеПоГоризонтали);
	НастройкиШтрихкода.Вставить("СмещениеПоВертикали", СмещениеПоВертикали);
	НастройкиШтрихкода.Вставить("ШиринаНаклейки", ШиринаНаклейки);
	НастройкиШтрихкода.Вставить("ВысотаНаклейки", ВысотаНаклейки);
    НастройкиШтрихкода.Вставить("ВысотаШК", ВысотаШК);
	НастройкиШтрихкода.Вставить("ПоказыватьЦифры", ПоказыватьЦифры);
	
	Возврат НастройкиШтрихкода; //@skip-check constructor-function-return-section
	
КонецФункции //ПолучитьПерсональныеНастройкиПоложенияШтрихкодаНаСтранице()

// Записывает в персональные настройки параметры расположения штрихкода на странице
// Параметры:
//  Настройки - См. ШтрихкодированиеКлиентСервер.НовыеНастройкиШтрихкода
Процедура ЗаписатьПерсональныеНастройкиОкнаСвойствШтрихкода(Настройки) Экспорт
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НастройкиШтрихкода", "ПоказыватьФормуНастройкиШтрихкода", Настройки.ПоказыватьФормуНастройки);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НастройкиШтрихкода", "ПоложениеШтрихкодаНаСтранице", Настройки.ПоложениеНаСтранице);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НастройкиШтрихкода", "СмещениеПоГоризонтали", Настройки.СмещениеПоГоризонтали);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НастройкиШтрихкода", "СмещениеПоВертикали", Настройки.СмещениеПоВертикали);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НастройкиШтрихкода", "ВысотаШтрихкодаПриВставкеВФайл", Настройки.ВысотаШК);	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НастройкиШтрихкода", "ВставлятьЦифрыВШК", Настройки.ПоказыватьЦифры);
	
КонецПроцедуры //ЗаписатьПерсональныеНастройкиОкнаСвойствШтрихкода()

// Персональные настройки положения рег номера.
// 
// Возвращаемое значение:
//  См. ШтрихкодированиеКлиентСервер.НовыеНастройкиРегНомера
Функция ПерсональныеНастройкиПоложенияРегНомера() Экспорт
	
	НастройкиРегНомера = ШтрихкодированиеКлиентСервер.НовыеНастройкиРегНомера();
	НастройкиРегНомера.ПоложениеНаСтранице = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиРегНомера", "ПоложениеНаСтранице", Перечисления.МестаВставкиКартинки.ЛевыйВерхний);
	НастройкиРегНомера.СмещениеПоГоризонтали = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиРегНомера", "СмещениеПоГоризонтали");
	НастройкиРегНомера.СмещениеПоВертикали = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиРегНомера", "СмещениеПоВертикали");
	НастройкиРегНомера.СтраницаВставки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиРегНомера", "СтраницаВставки", Перечисления.СтраницаВставкиКартинки.Первая);

	НастройкиРегНомера.ПоказыватьПриВставке = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиРегНомера", "ПоказыватьПриВставке", Истина);
	
	Возврат НастройкиРегНомера; //@skip-check constructor-function-return-section
	
КонецФункции

// Записать персональные настройки положения рег номера.
// 
// Параметры:
//  Настройки - См. ШтрихкодированиеКлиентСервер.НовыеНастройкиРегНомера
Процедура ЗаписатьПерсональныеНастройкиПоложенияРегНомера(Настройки) Экспорт
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"НастройкиРегНомера", "ПоложениеНаСтранице", Настройки.ПоложениеНаСтранице);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"НастройкиРегНомера", "СмещениеПоГоризонтали", Настройки.СмещениеПоГоризонтали);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"НастройкиРегНомера", "СмещениеПоВертикали", Настройки.СмещениеПоВертикали);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"НастройкиРегНомера", "СтраницаВставки", Настройки.СтраницаВставки);

	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"НастройкиРегНомера", "ПоказыватьПриВставке", Настройки.ПоказыватьПриВставке);
	
КонецПроцедуры

//Получает данные о штрихкоде объекта. Если у объекта нет штрихкода, он присваивается
//Параметры: 
//			Объект - ссылка на объект, данные о штрихкоде которого собираются
//			ВысотаКартинкиВПроцентах - высота изображения штрихкода в процентах
//			ПоказыватьЦифры - если Неопределено, берем из настроек, иначе из этого параметра
//Возвращает:
//			структура
//				Штрихкод - значение штрихкода
//				ДвоичныеДанныеИзображения - двоичные данные изображения штрихкода
//				ДвоичныеДанныеФайла - двоичные данные файла, если Объект - это файл
//				НеобходимостьВставкиВместоТэга - влаг, показывающий, что штрихкод только сформирован для файла 
//					и что необходимо выполнить попытку вставить изображение штрихкода вместо тэга в файле
//				ИзменениеФайловMSWordТолькоНаСервере - флаг, показывающий, что вставка изображения в файл MS Word выполняется на сервере
//				НастройкиШтрихкода - структура из персональных настроек
//					ПоказыватьФормуНастройки - показывать форму настройки положения штрихкода
//					ПоложениеНаСтранице - ПеречислениеСсылка.МестаВставкиКартинки -
//					СмещениеПоГоризонтали - расстояние от левого края страницы
//					СмещениеПоВертикали - расстояние от верха страницы	
//
//			Неопределено, если объект - это файл в папке ШаблоныФайлов
Функция ПолучитьДанныеДляВставкиШтрихкодаВОбъект(Знач Объект,
	Знач НеобходимоПередаватьСодержимоеФайла = Истина, 
	Знач ВызовСКлиента = Ложь,
	Знач ВставкаВPdf = Ложь,
	Знач ПоказыватьЦифры = Неопределено) Экспорт
	
	Ссылка = Объект.Ссылка;
	ДанныеШК = Новый Структура;
	
	//Получаем и вставляем персональные настройки окна положения ШК
	НастройкиШтрихкода = ШтрихкодированиеСервер.ПолучитьПерсональныеНастройкиПоложенияШтрихкодаНаСтранице();
	
	Если ПоказыватьЦифры <> Неопределено Тогда 
		НастройкиШтрихкода.ПоказыватьЦифры = ПоказыватьЦифры;
	КонецЕсли;	
    
    ШиринаШтрихкода = 0;
    Если ВставкаВPdf Тогда
    	
    	НастройкиШтрихкода.ВысотаШК = 20;
    	Если НастройкиШтрихкода.ПоказыватьЦифры Тогда
    		НастройкиШтрихкода.ВысотаШК = 30;
    	КонецЕсли;	
    	ШиринаШтрихкода = 200;
    	
		СпособВставкиШтампаЭПВPDF = РаботаСФайламиВызовСервера.ПолучитьСпособВставкиШтампаЭПВPDF();
		Если СпособВставкиШтампаЭПВPDF = Перечисления.СпособыВставкиШтампаЭПВPDF.Встроенный Тогда
			
	    	НастройкиШтрихкода.ВысотаШК = 40;
	    	Если НастройкиШтрихкода.ПоказыватьЦифры Тогда
	    		НастройкиШтрихкода.ВысотаШК = 50;
	    	КонецЕсли;	
	    	
	    	ШиринаШтрихкода = 400;
			
		КонецЕсли;	
	    	
    КонецЕсли;	

	ДанныеШК.Вставить("НастройкиШтрихкода", НастройкиШтрихкода);
    ДанныеШК.Вставить("ДвоичныеДанныеИзображения", Неопределено);
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.Файлы") Тогда
		
		ТекущаяВерсияРасширение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ТекущаяВерсияРасширение");
		
		Если НЕ (АвтозаполнениеШаблоновФайловКлиентСервер.ФорматMSWord(ТекущаяВерсияРасширение) ИЛИ ТекущаяВерсияРасширение = "odt")
			И НеобходимоПередаватьСодержимоеФайла Тогда
			ТекстОшибки = НСтр("ru = 'Вставка штрихкодов осуществляется только в файлы Microsoft Word или Open Office Writer.'");
			ДанныеШК.Вставить("СообщениеОбОшибке", ТекстОшибки);	
			Возврат ДанныеШК;
		КонецЕсли;
		ДанныеШК.Вставить("ФайлРедактируется", ЗначениеЗаполнено(Ссылка.Редактирует));
		//Получаем и вставляем значения констант, определяющих, где выполняется работа с файлами при вставке ШК
		ИзменениеФайловMSWordТолькоНаСервере = Константы.ИзменениеФайловMSWordТолькоНаСервере.Получить();
		ДанныеШК.Вставить("ИзменениеФайловMSWordТолькоНаСервере", ИзменениеФайловMSWordТолькоНаСервере);
		
		ДанныеШК.Вставить("Расширение", ТекущаяВерсияРасширение);
		
		//Получаем и вставляем, если необходимо, двоичные данные файла - для обработки на стороне клиента
		ДвоичныеДанныеФайла = АвтозаполнениеШаблоновФайловВызовСервера.ПолучитьДвоичныеДанныеФайла(Ссылка);
		Если НеобходимоПередаватьСодержимоеФайла И ТекущаяВерсияРасширение = "doc"
			ИЛИ НРег(ТекущаяВерсияРасширение) <> "doc" И НЕ ВызовСКлиента Тогда
			ДанныеШК.Вставить("ДвоичныеДанныеФайла", ДвоичныеДанныеФайла);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьШтрихкоды") Тогда	
		Возврат ДанныеШК;
	КонецЕсли;  
	
	ТекущаяВерсияРасширение = "";
	
	НеобходимостьВставкиВместоТэга = Ложь;
	Штрихкод = Неопределено;	
	//Пытаемся найти штрихкод у объекта
	ЗначениеШтрихкода = ШтрихкодированиеСервер.ПолучитьШтрихКод(Ссылка);
	Если ЗначениеШтрихкода = Неопределено
		ИЛИ ПустаяСтрока(ЗначениеШтрихкода) Тогда
		//если штрихкод не найден и Объект - это файл
		Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.Файлы") И НЕ Ссылка.ТекущаяВерсия.Пустая() Тогда
			Если ТипЗнч(Ссылка.ВладелецФайла) = Тип("СправочникСсылка.ПапкиФайлов")
				И АвтозаполнениеШаблоновФайловВызовСервера.ФайлНаходитсяВИерархииПапокШаблонов(Ссылка) Тогда				
				ТекстОшибки = НСтр("ru = 'Для файлов, находящихся в папке ""Шаблоны файлов"", штрихкод не формируется.'");
				ДанныеШК.Вставить("СообщениеОбОшибке", ТекстОшибки);
				Возврат ДанныеШК;
			ИначеЕсли ТипЗнч(Ссылка.ВладелецФайла) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда
				//если файл хранится в документе, то ему присваивается штрихкод документа
				Штрихкод = ШтрихкодированиеСервер.ПолучитьШтрихКод(Ссылка.ВладелецФайла);
			ИначеЕсли Не ВстроеннаяПочтаКлиентСервер.ЭтоПисьмо(Ссылка.ВладелецФайла) Тогда
				Штрихкод = ШтрихкодированиеСервер.СформироватьШтрихКод();
				//сформированный штрихкод присваивается Объекту
				ШтрихкодированиеСервер.ПрисвоитьШтрихКод(Ссылка, Штрихкод);
			КонецЕсли;
			
			//т.к. штрихкод только что сформирован, необходимо выполнить попытку вставить его изображение вместо тэга в файле
			НеобходимостьВставкиВместоТэга = Истина;
			
		ИначеЕсли  ТипЗнч(Ссылка) = Тип("СправочникСсылка.ДокументыПредприятия")
			И ЗначениеЗаполнено(Ссылка) Тогда
			//если Объект - это документ, то ему присваивается персональный штрихкод
			Штрихкод = ШтрихкодированиеСервер.СформироватьШтрихКод();
			ШтрихкодированиеСервер.ПрисвоитьШтрихКод(Ссылка, Штрихкод);
		КонецЕсли;
	Иначе
		Штрихкод = ЗначениеШтрихкода;
	КонецЕсли;
	Если НЕ Штрихкод = Неопределено Тогда
		
		//Вставим значение ШК
		ДанныеШК.Вставить("Штрихкод", Штрихкод);
		
		Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.Файлы") Тогда
			ТекущаяВерсияРасширение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ТекущаяВерсияРасширение");
		КонецЕсли;	
		
		//Если Идет вызов с клиента, но штрихкодирование выполняется только на сервере, то возвращаем только часть данных
		Если ВызовСКлиента И ИзменениеФайловMSWordТолькоНаСервере И ТекущаяВерсияРасширение = "doc" Тогда
			Возврат ДанныеШК;
		КонецЕсли;
		
		//получаем изображение штрихкода
		ДвоичныеДанныеИзображения = ПолучитьКартинкуШтрихкода(Штрихкод,, НастройкиШтрихкода.ВысотаШК, 
			НастройкиШтрихкода.ПоказыватьЦифры, ШиринаШтрихкода).ПолучитьДвоичныеДанные();
		ДанныеШК.ДвоичныеДанныеИзображения = ДвоичныеДанныеИзображения;
		
		Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.Файлы") Тогда						
		    ДанныеШК.Вставить("НеобходимостьВставкиВместоТэга", НеобходимостьВставкиВместоТэга);
		Конецесли;
				
		Возврат ДанныеШК;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
		
КонецФункции

//Возвращает расширение текущей версии файла
//Параметры:
//			Ссылка - ссылка на объект типа Файл
Функция ПолучитьРасширениеФайла(Ссылка) Экспорт
	
	ТекущаяВерсияРасширение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ТекущаяВерсияРасширение");
	Возврат ТекущаяВерсияРасширение;
	
КонецФункции

// Найти объекты по штрихкоду.
// 
// Параметры:
//  МассивШтрихкодов - Массив из Строка - массив строк, содержащих значение штрихкода.
//  ИскатьЗадачиПоОбъектам - Булево - Искать задачи по объектам
// 
// Возвращаемое значение:
//  Массив из Структура
Функция НайтиОбъектыПоШтрихкоду(МассивШтрихкодов, ИскатьЗадачиПоОбъектам = Ложь) Экспорт
	
    МассивНайденных = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Штрихкоды.Владелец КАК Владелец,
		|	Штрихкоды.Код КАК Код
		|ИЗ
		|	РегистрСведений.Штрихкоды КАК Штрихкоды
		|ГДЕ
		|	Штрихкоды.Код В(&МассивШтрихкодов)";

	Запрос.УстановитьПараметр("МассивШтрихкодов", МассивШтрихкодов);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Если ВыборкаДетальныеЗаписи.Количество() = 0 Тогда
		Возврат МассивНайденных;
	КонецЕсли;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Владелец = ВыборкаДетальныеЗаписи.Владелец;
		
		Если НЕ ЗначениеЗаполнено(Владелец) Тогда
			Возврат МассивНайденных;
		КонецЕсли;
		
		//проверка на доступность владельца для текущего пользователя
		Если Не ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(Владелец).Чтение Тогда
			Возврат МассивНайденных;		
		КонецЕсли;
				
		//если объект-владелец штрихкода доступен для текущего пользователя, продолжаем обработку
		ДанныеВозврата = Новый Структура;
		
		ВладелецФайла = Неопределено;                              
		Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Файлы") Тогда
			ВладелецФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "ВладелецФайла");
		КонецЕсли;	
		
		Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Файлы") 
			И ТипЗнч(ВладелецФайла) <> Тип("СправочникСсылка.ПапкиФайлов") Тогда
			
			ДанныеВозврата.Вставить("Ключ", ВладелецФайла);
			ДанныеВозврата.Вставить("Метаданные", ВладелецФайла.Метаданные().Имя);
			ДанныеВозврата.Вставить("ПредставлениеОбъекта", ВладелецФайла.Метаданные().ПредставлениеОбъекта);
			
		Иначе
			
			ДанныеВозврата.Вставить("Ключ", Владелец);
			ДанныеВозврата.Вставить("Метаданные", Владелец.Метаданные().Имя);
			ДанныеВозврата.Вставить("ПредставлениеОбъекта", Владелец.Метаданные().ПредставлениеОбъекта);
			
		КонецЕсли;  
		
		ТакойОбъектУжеЕсть = Ложь;
		Для Каждого ЭлементМассива Из МассивНайденных Цикл
			Если ЭлементМассива.Ключ = ДанныеВозврата.Ключ Тогда
				ТакойОбъектУжеЕсть = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если НЕ ТакойОбъектУжеЕсть Тогда
			МассивНайденных.Добавить(ДанныеВозврата);
		КонецЕсли;
	КонецЦикла;	                
	
	Если НЕ ИскатьЗадачиПоОбъектам ИЛИ МассивНайденных.Количество() > 1 ИЛИ МассивНайденных.Количество() = 0 Тогда
		Возврат МассивНайденных;
	ИначеЕсли ИскатьЗадачиПоОбъектам И МассивНайденных.Количество() = 1 Тогда 
		
		//поиск задач по найденному документу или файлу           
		
		Приложение = МассивНайденных[0].Ключ;
		
		ЗадачиПоПриложению = Документы.Задача.НайтиПоПриложениям(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Приложение));
			
		Если ЗадачиПоПриложению = 0 Тогда
				
			Возврат МассивНайденных;	
			
		Иначе	
			
			РеестрЗадачиМне = Справочники.РеестрыЗадач.РеестрЗадачиМне(Пользователи.ТекущийПользователь());
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 20
			|	ДействияЗадач.ДействиеЗадачи КАК ДействиеЗадачи,
			|	ДействияЗадач.Заголовок КАК Заголовок
			|ИЗ
			|	РегистрСведений.РеестрЗадачПоИсполнителям КАК ДействияЗадач
			|ГДЕ
			|	ДействияЗадач.ОжидаетВыполнения
			|	И ДействияЗадач.Задача В(&Задачи)
			|	И ДействияЗадач.РеестрЗадач = &РеестрЗадач";
			
			
			Запрос.Параметры.Вставить("Задачи", ЗадачиПоПриложению);
			Запрос.Параметры.Вставить("РеестрЗадач", РеестрЗадачиМне);
			
			Выборка = Запрос.Выполнить().Выбрать();
			КоличествоАктивныхЗадачПользователя = Выборка.Количество();
			Если КоличествоАктивныхЗадачПользователя = 0 Тогда
				Возврат МассивНайденных;	
			Иначе          
				
				МассивЗадач = Новый Массив;
				
				Пока Выборка.Следующий() Цикл 
					МассивЗадач.Добавить(Выборка.ДействиеЗадачи);
				КонецЦикла;
				
				Если МассивЗадач.Количество() > 1 Тогда
					Возврат МассивНайденных; // вернем просто документ
				Иначе	
					
					МассивНайденных.Очистить();

					ДействиеЗадачи = МассивЗадач[0];
					
					ДанныеВозврата = Новый Структура;
					ДанныеВозврата.Вставить("Ключ", ДействиеЗадачи);
					ДанныеВозврата.Вставить("Метаданные", ДействиеЗадачи.Метаданные().Имя);
					ДанныеВозврата.Вставить("ПредставлениеОбъекта", Строка(ДействиеЗадачи));
					ДанныеВозврата.Вставить("ПредметБизнесПроцесса", Приложение);
					МассивНайденных.Добавить(ДанныеВозврата);
					
					Возврат МассивНайденных;
					
				КонецЕсли;	
				
			КонецЕсли;		
			
		КонецЕсли;	
			
		
	КонецЕсли;
	
	Возврат МассивНайденных;
	
КонецФункции 
	
// Загрузка параметров подключения драйвера сканера из хранилища настроек. 
// 
// Параметры: 
//  ТипОС           - Строка - тип операционной системы.  (IN)
// 
// Возвращаемое значение: 
//  Структура, содержащая параметры подключения сканера
Функция ЗагрузитьПараметрыПодключенияСканера(ТипОС) Экспорт

	Если ТипОС = "Windows" Тогда
		
		Возврат ХранилищеОбщихНастроек.Загрузить("ТекущиеНастройкиСканераWindows");
		
	ИначеЕсли ТипОС = "Linux" Тогда
	
		Возврат ХранилищеОбщихНастроек.Загрузить("ТекущиеНастройкиСканераLinux");
		
	КонецЕсли;

КонецФункции

//Получает значение функциональной опции "Использовать штрихкоды"
Функция ПолучитьИспользованиеШтрихкодов() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьШтрихкоды");
	
КонецФункции

// Получает двоичные данные файла и данные для вставки штрихкода
Функция ПолучитьДвоичныеДанныеФайлаИДанныеДляВставкиШтрихкодаВОбъект(
	АдресВременногоХранилищаФайла, 
	знач Объект, 
	НеобходимоПередаватьСодержимоеФайла = Истина, 
	ВызовСКлиента = Ложь) Экспорт
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВременногоХранилищаФайла);
	ДанныеОШтрихкоде = ШтрихкодированиеСервер.ПолучитьДанныеДляВставкиШтрихкодаВОбъект(Объект, НеобходимоПередаватьСодержимоеФайла, ВызовСКлиента);
	
	Возврат Новый Структура("ДвоичныеДанные, ДанныеОШтрихкоде", ДвоичныеДанные, ДанныеОШтрихкоде);
	
КонецФункции

//Получает изображение штрихкода
//Параметры:
//			Код - значение штрихкода
//			ТипКода - формат штрихкода. По умолчанию EAN-13
//			ВысотаВМиллиметрах - высота штрихкода в процентах
//			ВставлятьЦифры - Булево
//			ШиринаШтрихкода  - Число
//Возвращает:
//			Картинка с изображением штрихкода
Функция ПолучитьКартинкуШтрихкода(Код, ТипКода = 1, ВысотаВМиллиметрах, ВставлятьЦифры,
	ШиринаШтрихкода = 0) Экспорт  
	
	ПараметрыШтрихкода = ГенерацияШтрихкода.ПараметрыГенерацииШтрихкода();
	
	// Зададим размер картинки
	Если НЕ ЗначениеЗаполнено(ВысотаВМиллиметрах) Тогда
		ВысотаВМиллиметрах = 10;
	КонецЕсли;
	ПараметрыШтрихкода.Высота = Число(Формат((ВысотаВМиллиметрах/35.3) * 100, "ЧДЦ=0"));

	ПараметрыШтрихкода.Ширина = 114;
	Если ШиринаШтрихкода <> 0 Тогда
		ПараметрыШтрихкода.Ширина = ШиринаШтрихкода;
	КонецЕсли;	 
	
	ПараметрыШтрихкода.ТипКода = ТипКода; // 1 - EAN13
	ПараметрыШтрихкода.Штрихкод = Код;
	ПараметрыШтрихкода.ОтображатьТекст = ВставлятьЦифры;   
	ПараметрыШтрихкода.ПрозрачныйФон = Ложь;
	
	РезультатШтрихкод = ГенерацияШтрихкода.ИзображениеШтрихкода(ПараметрыШтрихкода);
	
	Если РезультатШтрихкод = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Неизвестная ошибка штрихкодирования. Необходимо обратиться к администратору.'");
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Штрихкодирование'"), 
			УровеньЖурналаРегистрации.Ошибка,
			, , 
			ТекстСообщения);
		ВызватьИсключение(ТекстСообщения);         
	КонецЕсли;
	
	Возврат РезультатШтрихкод.Картинка;

КонецФункции

// Вставляет регистрационный штамп в файле DocX 
Функция ВставитьРегистрационныйШтампВФайлDocxСУказаниемПоложения(
	Расширение, 
	ТекстНадписи, 
	ДвоичныеДанныеФайла, 
	ДвоичныеДанныеКартинки, 
	ДанныеОПоложении, 
	ВставлятьШтрихкод) Экспорт
	
	Если ДвоичныеДанныеФайла.Размер() = 0 Тогда
		Возврат ДвоичныеДанныеФайла;
	КонецЕсли;
	
	СтарыйПутьКФайлу = ПолучитьИмяВременногоФайла(Расширение);
	ДвоичныеДанныеФайла.Записать(СтарыйПутьКФайлу);
	НовыйПутьКФайлу = ПолучитьИмяВременногоФайла(Расширение);
	
	КопироватьФайл(СтарыйПутьКФайлу, СтрЗаменить(СтарыйПутьКФайлу, Расширение, "zip"));
	ИмяФайлаСПутемZIP = СтрЗаменить(СтарыйПутьКФайлу, Расширение, "zip");

	ВременнаяПапкаДляРазархивирования = ПолучитьИмяВременногоФайла("");
	ВременныйZIPФайл = ПолучитьИмяВременногоФайла("zip"); 

	Архив = Новый ЧтениеZipФайла();
	Архив.Открыть(ИмяФайлаСПутемZIP);
	Архив.ИзвлечьВсе(ВременнаяПапкаДляРазархивирования, РежимВосстановленияПутейФайловZIP.Восстанавливать);
	Архив.Закрыть();

	//Заполнение полей в теле документа
	ЧтениеXML = Новый ЧтениеXML();
	ЧтениеXML.ОткрытьФайл(ВременнаяПапкаДляРазархивирования + "/word/document.xml");
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ВременнаяПапкаДляРазархивирования + "/word/document_update.xml");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	//получение макета для вставки регштампа
	Если ЗначениеЗаполнено(ТекстНадписи) Тогда
		Если ПолучитьФункциональнуюОпцию("ИспользоватьШтрихкоды") Тогда
			МакетДляВставки = ПолучитьОбщийМакет("ВставкаРегистрационногоШтампаВDocx");
		Иначе
			МакетДляВставки = ПолучитьОбщийМакет("ВставкаРегистрационногоШтампаВDocxБезШтрихкода");
	    КонецЕсли;
	Иначе
		МакетДляВставки = ПолучитьОбщийМакет("ВставкаШтрихкодаСУказаниемПоложенияDocx");	
	КонецЕсли;
	ТекстДляВставкиШтампа = МакетДляВставки.ПолучитьТекст();
		
	//установка положения и прочих параметров
	СмещениеПоГоризонтали = 0;
	СмещениеПоВертикали = 0;
	ГоризонтальноеВыравнивание = "";
	ОтносительночегоСчитатьГоризонтальноеВыравнивание = "margin";
	ВертикальноеВыравнивание = "";
	ОтносительноЧегоСчитатьВертикальноеВыравнивание = "margin";
	
	Если ЗначениеЗаполнено(ТекстНадписи) Тогда
		ИмяКомпании = ТекстНадписи.НазваниеОрганизации;
		РегистрационныйНомер = ТекстНадписи.РегНомер;
		ДатаРегистрации = ТекстНадписи.Регдата;
	КонецЕсли;
	ТекстДляВставкиШтампа = СтрЗаменить(ТекстДляВставкиШтампа, "_ИмяКомпании_", ИмяКомпании);
	ТекстДляВставкиШтампа = СтрЗаменить(ТекстДляВставкиШтампа, "_РегистрационныйНомер_", РегистрационныйНомер);
	ТекстДляВставкиШтампа = СтрЗаменить(ТекстДляВставкиШтампа, "_ДатаРегистрации_", ДатаРегистрации);

	//Высота штрихкода в EMU (English Metrick Units)
	ВысотаШтрихкода = (ДанныеОПоложении.ВысотаШК / 25.4) * 914400;
	
	ПоложениеНаСтранице = ДанныеОПоложении.ПоложениеНаСтранице;
	Если ПоложениеНаСтранице = ПредопределенноеЗначение("Перечисление.МестаВставкиКартинки.ПравыйНижний") Тогда
		СмещениеПоГоризонтали = "MAX";
		СмещениеПоВертикали = "MAX";
	ИначеЕсли ПоложениеНаСтранице = ПредопределенноеЗначение("Перечисление.МестаВставкиКартинки.ПравыйВерхний") Тогда
		СмещениеПоГоризонтали = "MAX";
		СмещениеПоВертикали = "MIN";
	ИначеЕсли ПоложениеНаСтранице = ПредопределенноеЗначение("Перечисление.МестаВставкиКартинки.ЛевыйВерхний") Тогда
		СмещениеПоГоризонтали = "MIN";
		СмещениеПоВертикали = "MIN";
	ИначеЕсли ПоложениеНаСтранице = ПредопределенноеЗначение("Перечисление.МестаВставкиКартинки.ЛевыйНижний") Тогда
		СмещениеПоГоризонтали = "MIN";
		СмещениеПоВертикали = "MAX";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстНадписи) Тогда
		Если СмещениеПоГоризонтали = "MAX" Тогда
			//выравнивание к правому краю
			ГоризонтальноеВыравнивание = "mso-position-horizontal:right";
		ИначеЕсли СмещениеПоГоризонтали = "MIN" Тогда
			//выравнивание к левому краю
			ГоризонтальноеВыравнивание = "mso-position-horizontal:left";
		Иначе
			//если задано произвольное расположение, то переведем смещение по горизонтали из миллиметров в Point'ы
			//1 д = 1/72"
			ОтносительночегоСчитатьГоризонтальноеВыравнивание = "page";
			СмещениеПоГоризонтали = (ДанныеОПоложении.СмещениеПоГоризонтали / 25.4) * 72;
		КонецЕсли;
		
		Если СмещениеПоВертикали = "MAX" Тогда
			//выравнивание по нижнему краю
			ВертикальноеВыравнивание = "mso-position-vertical:bottom";
		ИначеЕсли СмещениеПоВертикали = "MIN" Тогда
			//выравнивание по верхнему краю
			ВертикальноеВыравнивание = "mso-position-vertical:top";
		Иначе
			//если задано произвольное расположение, то переведем смещение по горизонтали из миллиметров в Point'ы
			ОтносительноЧегоСчитатьВертикальноеВыравнивание = "page";
			СмещениеПоВертикали =(ДанныеОПоложении.СмещениеПоВертикали / 25.4) * 72;
		КонецЕсли;

		ТекстДляВставкиШтампа = СтрЗаменить(ТекстДляВставкиШтампа, "_СмещениеПоГоризонтали_", Формат(СмещениеПоГоризонтали,"ЧДЦ=0; ЧГ=0") + "pt");
		ТекстДляВставкиШтампа = СтрЗаменить(ТекстДляВставкиШтампа, "_СмещениеПоВертикали_", Формат(СмещениеПоВертикали,"ЧДЦ=0; ЧГ=0") + "pt");
		ТекстДляВставкиШтампа = СтрЗаменить(ТекстДляВставкиШтампа, "_ГоризонтальноеВыравнивание_", ГоризонтальноеВыравнивание);
		ТекстДляВставкиШтампа = СтрЗаменить(ТекстДляВставкиШтампа, "_ВертикальноеВыравнивание_", ВертикальноеВыравнивание);
	Иначе
		
		Если СмещениеПоГоризонтали = "MAX" Тогда
			//выравнивание к правому краю
			СмещениеПоГоризонтали = "<wp:align>right</wp:align>";
		ИначеЕсли СмещениеПоГоризонтали = "MIN" Тогда
			//выравнивание к левому краю
			СмещениеПоГоризонтали = "<wp:align>left</wp:align>";
		Иначе
			//если задано произвольное расположение, то переведем смещение по горизонтали из миллиметров в EMU
			//1 д = 1/72"
			ОтносительночегоСчитатьГоризонтальноеВыравнивание = "page";
			СмещениеПоГоризонталиЗначение = (ДанныеОПоложении.СмещениеПоГоризонтали / 25.4) * 914400;
			СмещениеПоГоризонтали = "<wp:posOffset>" + Формат(СмещениеПоГоризонталиЗначение,"ЧДЦ=0; ЧГ=0") + "</wp:posOffset>";
		КонецЕсли;
		
		Если СмещениеПоВертикали = "MAX" Тогда
			//выравнивание по нижнему краю
			СмещениеПоВертикали = "<wp:align>bottom</wp:align>";
		ИначеЕсли СмещениеПоВертикали = "MIN" Тогда
			//выравнивание по верхнему краю
			СмещениеПоВертикали = "<wp:align>top</wp:align>";
		Иначе
			//если задано произвольное расположение, то переведем смещение по горизонтали из миллиметров в EMU
			ОтносительноЧегоСчитатьВертикальноеВыравнивание = "page";
			СмещениеПоВертикалиЗначение =(ДанныеОПоложении.СмещениеПоВертикали / 25.4) * 914400;
			СмещениеПоВертикали = "<wp:posOffset>" + Формат(СмещениеПоВертикалиЗначение,"ЧДЦ=0; ЧГ=0") + "</wp:posOffset>";
		КонецЕсли;

		ТекстДляВставкиШтампа = СтрЗаменить(ТекстДляВставкиШтампа, "_СмещениеПоГоризонтали_", Формат(СмещениеПоГоризонтали,"ЧДЦ=0; ЧГ=0"));
		ТекстДляВставкиШтампа = СтрЗаменить(ТекстДляВставкиШтампа, "_СмещениеПоВертикали_", Формат(СмещениеПоВертикали,"ЧДЦ=0; ЧГ=0"));
		
	КонецЕсли;
	
	ТекстДляВставкиШтампа = СтрЗаменить(ТекстДляВставкиШтампа, "_ОтносительноЧегоСчитатьВертикальноеСмещение_", ОтносительноЧегоСчитатьВертикальноеВыравнивание);
	ТекстДляВставкиШтампа = СтрЗаменить(ТекстДляВставкиШтампа, "_ОтносительноЧегоСчитатьГоризонтальноеСмещение_", ОтносительночегоСчитатьГоризонтальноеВыравнивание);
	
	ИДкартинки = 0;
	Если ВставлятьШтрихкод Тогда
		СчетчикСвязей = 0;
		СохранитьИзображениеВоВнутреннейСтруктуреDocx(ВременнаяПапкаДляРазархивирования, ДвоичныеДанныеКартинки, ИДкартинки, СчетчикСвязей);
		ТекстДляВставкиШтампа = СтрЗаменить(ТекстДляВставкиШтампа, "_ИДКартинкиВоВнутреннейПапке_", "rId" + Строка(СчетчикСвязей + 1));
		ТекстДляВставкиШтампа = СтрЗаменить(ТекстДляВставкиШтампа, "_ВысотаШтрихкода_", Формат(ВысотаШтрихкода,"ЧДЦ=0; ЧГ=0"));
	КонецЕсли;
	
	// Обязательно нужно ставить Ложь, иначе будут пропадать пробелы
	ЧтениеXML.ИгнорироватьПробелы = Ложь;
	
	//вставка разметки для регштампа в файл
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ВставитьШтамп = Ложь;
			Если ЧтениеXML.Имя = "w:body" Тогда
				ВставитьШтамп = Истина;
			КонецЕсли;
			ЗаписьXML.ЗаписатьНачалоЭлемента(ЧтениеXML.Имя);
			Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
				ЗаписьXML.ЗаписатьАтрибут(ЧтениеXML.Имя,ЧтениеXML.Значение); 
			КонецЦикла;
			Если ВставитьШтамп Тогда
				ЗаписьXML.ЗаписатьБезОбработки(ТекстДляВставкиШтампа);
				ВставитьШтамп = Ложь;
			КонецЕсли;
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
			ЗаписьXML.ЗаписатьТекст(ЧтениеXML.Значение);
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			
			ЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
	КонецЦикла;
	
	ЧтениеXML.Закрыть();
	ЗаписьXML.Закрыть();     
	
	ИмяФайлаДокументаНовый = ВременнаяПапкаДляРазархивирования + "/word/document_update.xml";
	
	ЧтениеТекста = Новый ЧтениеТекста(
		ИмяФайлаДокументаНовый,
		КодировкаТекста.UTF8);
	ТекстXml = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	
	// проверим наличие namespace
	ПозицияNamespace = СтрНайти(ТекстXml, "http://schemas.openxmlformats.org/drawingml/2006/wordprocessingDrawing");
	ПозицияNamespaceWP = СтрНайти(ТекстXml, "xmlns:wp=");
	Если ПозицияNamespace = 0 И ПозицияNamespaceWP = 0 Тогда // вставим его
	
		СтрокаДок = "<w:document";
		ПозицияДок = СтрНайти(ТекстXml, СтрокаДок);
		
		Если ПозицияДок <> 0 Тогда // вставим его
		
			ПозицияКонцаДок = ПозицияДок + СтрДлина(СтрокаДок);
	
			ТекстXml = Лев(ТекстXml, ПозицияКонцаДок - 1)
			 + " xmlns:wp=""http://schemas.openxmlformats.org/drawingml/2006/wordprocessingDrawing"" "
			  + Сред(ТекстXml, ПозицияКонцаДок);
			  
		КонецЕсли;  
		
	КонецЕсли;	

	ЗаписьТекста = Новый ЗаписьТекста(
		ИмяФайлаДокументаНовый,
		КодировкаТекста.UTF8);
	ЗаписьТекста.Записать(ТекстXml);
	ЗаписьТекста.Закрыть();
	
	
	АвтозаполнениеШаблоновФайловКлиентСервер.ЗаменитьПространствоИменR(
		ВременнаяПапкаДляРазархивирования + "/word/document_update.xml");
	
	ПереместитьФайл(ВременнаяПапкаДляРазархивирования + "/word/document_update.xml", ВременнаяПапкаДляРазархивирования + "/word/document.xml");
	УдалитьФайлы(ВременнаяПапкаДляРазархивирования + "/word/document_update.xml");
	
	Архиватор = Новый ЗаписьZipФайла(ВременныйZIPФайл, "", "");
	Архиватор.Добавить(ВременнаяПапкаДляРазархивирования + "\*.*", РежимСохраненияПутейZIP.СохранятьОтносительныеПути, РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
	Архиватор.Записать();

	ПереместитьФайл(ВременныйZIPФайл, НовыйПутьКФайлу);
	УдалитьФайлы(ВременнаяПапкаДляРазархивирования);	
	УдалитьФайлы(СтарыйПутьКФайлу);
	УдалитьФайлы(ВременныйZIPФайл);
	
	ДвоичныеДанныеЗаполненногоФайла = Новый ДвоичныеДанные(НовыйПутьКФайлу);
	УдалитьФайлы(НовыйПутьКФайлу);
	Возврат ДвоичныеДанныеЗаполненногоФайла;
	
КонецФункции

// Вставляет штрихкод вместо тэга в файле DocX
Функция ВставитьШтрихкодВместоТэгаВФайлDocx(
	Тэг, 
	ДвоичныеДанныеИзображения, 
	ДвоичныеДанныеФайла, 
	Расширение, 
	ВысотаШтрихкода) Экспорт
	
	Если ДвоичныеДанныеФайла.Размер() = 0 Тогда
		Возврат ДвоичныеДанныеФайла;
	КонецЕсли;
	
	СтарыйПутьКФайлу = ПолучитьИмяВременногоФайла(Расширение);
	ДвоичныеДанныеФайла.Записать(СтарыйПутьКФайлу);
	НовыйПутьКФайлу = ПолучитьИмяВременногоФайла(Расширение);
	
	КопироватьФайл(СтарыйПутьКФайлу, СтрЗаменить(СтарыйПутьКФайлу, Расширение, "zip"));
	ИмяФайлаСПутемZIP = СтрЗаменить(СтарыйПутьКФайлу, Расширение, "zip");

	ВременнаяПапкаДляРазархивирования = ПолучитьИмяВременногоФайла("");
	ВременныйZIPФайл = ПолучитьИмяВременногоФайла("zip"); 

	Архив = Новый ЧтениеZipФайла();
	Архив.Открыть(ИмяФайлаСПутемZIP);
	Архив.ИзвлечьВсе(ВременнаяПапкаДляРазархивирования, РежимВосстановленияПутейФайловZIP.Восстанавливать);
	Архив.Закрыть();
	
	ИДкартинки = 0;
	СчетчикСвязей = 0;
	СохранитьИзображениеВоВнутреннейСтруктуреDocx(ВременнаяПапкаДляРазархивирования, ДвоичныеДанныеИзображения, ИДкартинки, СчетчикСвязей);
		
	ЧтениеXML = Новый ЧтениеXML();
	ЧтениеXML.ОткрытьФайл(ВременнаяПапкаДляРазархивирования + "/word/document.xml");
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.Отступ = Ложь;
	ЗаписьXML.ОткрытьФайл(ВременнаяПапкаДляРазархивирования + "/word/document_update.xml");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	// Обязательно нужно ставить Ложь, иначе будут пропадать пробелы
	ЧтениеXML.ИгнорироватьПробелы = Ложь; 
	
	ВнутриТегRun = Ложь;
	
	КартинкаШтрихкода = Ложь;
	Пока ЧтениеXML.Прочитать() Цикл	
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ЗаписьXML.ЗаписатьНачалоЭлемента(ЧтениеXML.Имя); 
			
			Если ЧтениеXML.Имя = "w:r" Тогда
				ВнутриТегRun = Истина;
			КонецЕсли;	
			
			Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
				Если ЧтениеXML.Имя = "name" И ЧтениеXML.Значение = Тэг Тогда
					КартинкаШтрихкода = Истина;
					ЗаписьXML.ЗаписатьАтрибут(ЧтениеXML.Имя,ЧтениеXML.Значение); 
				ИначеЕсли КартинкаШтрихкода И ЧтениеXML.Имя = "r:embed" Тогда
					КартинкаШтрихкода = Ложь;
					ЗаписьXML.ЗаписатьАтрибут(ЧтениеXML.Имя,"rId" + Строка(СчетчикСвязей + 1));
				Иначе
					ЗаписьXML.ЗаписатьАтрибут(ЧтениеXML.Имя,ЧтениеXML.Значение);
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
			Если Найти(ЧтениеXML.Значение, Тэг) > 0 Тогда
				НовоеЗначение = СтрЗаменить(ЧтениеXML.Значение, "&lt;&lt;"+Тэг+"&gt;&gt;" , "_");
				НовоеЗначение = СтрЗаменить(НовоеЗначение, "&lt;"+Тэг+"&gt;" , "_");
				НовоеЗначение = СтрЗаменить(НовоеЗначение, "<<"+Тэг+">>" , "_");
				НовоеЗначение = СтрЗаменить(НовоеЗначение, "<"+Тэг+">" , "_");
				НовоеЗначение = СтрЗаменить(НовоеЗначение, Тэг, "_");
				МакетДляВставкиШтрихкода = ПолучитьОбщийМакет("ВставкаШтрихкодаВместоТегаВDocx");
				ТекстДляВставки = МакетДляВставкиШтрихкода.ПолучитьТекст();
				ТекстДляВставки = "</w:t>" + ТекстДляВставки + "<w:t>";
				
				ВысотаШтрихкодаЗначение = (ВысотаШтрихкода / 25.4) * 914400;
				ТекстДляВставки = СтрЗаменить(ТекстДляВставки, "_ВысотаШтрихкода_", Формат(ВысотаШтрихкодаЗначение,"ЧДЦ=0; ЧГ=0"));
				ТекстДляВставки = СтрЗаменить(ТекстДляВставки, "_ИДКартинкиВоВнутреннейПапке_", "rId" + Строка(СчетчикСвязей + 1));
				
				НовоеЗначение = СтрЗаменить(НовоеЗначение, "_", ТекстДляВставки);  
				
				Если ВнутриТегRun Тогда
					
					НовоеЗначение = СтрЗаменить(НовоеЗначение, "<w:r>", "");  
					НовоеЗначение = СтрЗаменить(НовоеЗначение, "</w:r>", "");  
					
				КонецЕсли;	
				
				ЗаписьXML.ЗаписатьБезОбработки(НовоеЗначение);
			Иначе
				ЗаписьXML.ЗаписатьТекст(ЧтениеXML.Значение);
			КонецЕсли;
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			ЗаписьXML.ЗаписатьКонецЭлемента(); 
			
			Если ЧтениеXML.Имя = "w:r" Тогда
				ВнутриТегRun = Ложь;
			КонецЕсли;	
			
		КонецЕсли;
	КонецЦикла;

	ЧтениеXML.Закрыть();
	ЗаписьXML.Закрыть();
	
	АвтозаполнениеШаблоновФайловКлиентСервер.ЗаменитьПространствоИменR(
		ВременнаяПапкаДляРазархивирования + "/word/document_update.xml");
	
	ПереместитьФайл(ВременнаяПапкаДляРазархивирования + "/word/document_update.xml", ВременнаяПапкаДляРазархивирования + "/word/document.xml");
	УдалитьФайлы(ВременнаяПапкаДляРазархивирования + "/word/document_update.xml");
	
	Архиватор = Новый ЗаписьZipФайла(ВременныйZIPФайл, "", "");
	Архиватор.Добавить(ВременнаяПапкаДляРазархивирования + "\*.*", РежимСохраненияПутейZIP.СохранятьОтносительныеПути, РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
	Архиватор.Записать();

	ПереместитьФайл(ВременныйZIPФайл, НовыйПутьКФайлу);
	УдалитьФайлы(ВременнаяПапкаДляРазархивирования);	
	УдалитьФайлы(СтарыйПутьКФайлу);
	УдалитьФайлы(ВременныйZIPФайл);
	
	ДвоичныеДанныеЗаполненногоФайла = Новый ДвоичныеДанные(НовыйПутьКФайлу);
	УдалитьФайлы(НовыйПутьКФайлу);
	Возврат ДвоичныеДанныеЗаполненногоФайла;
	
КонецФункции

Процедура СохранитьИзображениеВоВнутреннейСтруктуреDocx(
	ВременнаяПапкаДляРазархивирования, 
	ДвоичныеДанныеКартинки, 
	ИДкартинки, 
	СчетчикСвязей)
	
	//сохранение картинки штрихкода во внутренней структуре файла docx
	СоздатьКаталог(ВременнаяПапкаДляРазархивирования + "/word/media");
	
	МассивФайлов  = НайтиФайлы(ВременнаяПапкаДляРазархивирования + "/word/media", "Image*.*", Истина);
	МассивФайлов2 = НайтиФайлы(ВременнаяПапкаДляРазархивирования + "/word/media", "image*.*", Истина);
	ВсеФайлы = Новый Соответствие;
	Для Каждого НайденныйФайл Из МассивФайлов Цикл
		ВсеФайлы.Вставить(НайденныйФайл.ПолноеИмя, 1);
	Конеццикла;
	Для Каждого НайденныйФайл Из МассивФайлов2 Цикл
		ВсеФайлы.Вставить(НайденныйФайл.ПолноеИмя, 1);
	Конеццикла;
	
	ИДкартинки = ИДкартинки + ВсеФайлы.Количество();
	
	КартинкаШК = Новый Картинка(ДвоичныеДанныеКартинки);
	КартинкаШК.Записать(ВременнаяПапкаДляРазархивирования + "/word/media/Image" + Строка(ИДкартинки + 1) + ".jpeg");
	ЧтениеФайлаСвязей = Новый ЧтениеXML();
	ЧтениеФайлаСвязей.ОткрытьФайл(ВременнаяПапкаДляРазархивирования + "/word/_rels/document.xml.rels");
	ЗаписьФайлаСвязей = Новый ЗаписьXML();
	ЗаписьФайлаСвязей.ОткрытьФайл(ВременнаяПапкаДляРазархивирования + "/word/_rels/document_update.xml.rels");
	ЗаписьФайлаСвязей.ЗаписатьОбъявлениеXML();
	Пока ЧтениеФайлаСвязей.Прочитать() Цикл
		Если ЧтениеФайлаСвязей.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ЗаписьФайлаСвязей.ЗаписатьНачалоЭлемента(ЧтениеФайлаСвязей.Имя);
			Пока ЧтениеФайлаСвязей.ПрочитатьАтрибут() Цикл
				ЗаписьФайлаСвязей.ЗаписатьАтрибут(ЧтениеФайлаСвязей.Имя,ЧтениеФайлаСвязей.Значение); 
				Если ЧтениеФайлаСвязей.Имя = "Id" Тогда
					СчетчикСвязей = СчетчикСвязей + 1;
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли ЧтениеФайлаСвязей.ТипУзла = ТипУзлаXML.Текст Тогда
			ЗаписьФайлаСвязей.ЗаписатьТекст(ЧтениеФайлаСвязей.Значение);
		ИначеЕсли ЧтениеФайлаСвязей.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			Если ЧтениеФайлаСвязей.Имя = "Relationships" Тогда
				НоваяСтрока = "<Relationship Target=""media/Image" + Строка(ИДкартинки + 1) + ".jpeg"" Type=""http://schemas.openxmlformats.org/officeDocument/2006/relationships/image"" Id=""rId" + Строка(СчетчикСвязей + 1) + """/>";
				ЗаписьФайлаСвязей.ЗаписатьБезОбработки(НоваяСтрока);
			КонецЕсли;
			ЗаписьФайлаСвязей.ЗаписатьКонецЭлемента();
		КонецЕсли;
	КонецЦикла;
    ЧтениеФайлаСвязей.Закрыть();
	ЗаписьФайлаСвязей.Закрыть();
	ПереместитьФайл(ВременнаяПапкаДляРазархивирования + "/word/_rels/document_update.xml.rels", ВременнаяПапкаДляРазархивирования + "/word/_rels/document.xml.rels");
	УдалитьФайлы(ВременнаяПапкаДляРазархивирования + "/word/_rels/document_update.xml.rels");
	
	//расширение опсиания типов содержимого в файле docx
	ЧтениеФайлаТипов = Новый ЧтениеXML();
	ЧтениеФайлаТипов.ОткрытьФайл(ВременнаяПапкаДляРазархивирования + "/[Content_Types].xml");
	ЗаписьФайлаТипов = Новый ЗаписьXML();
	ЗаписьФайлаТипов.ОткрытьФайл(ВременнаяПапкаДляРазархивирования + "/[Content_Types]_update.xml");		
	ЗаписьФайлаТипов.ЗаписатьОбъявлениеXML();
	АтрибутJPEGНайден = Ложь;
	Пока ЧтениеФайлаТипов.Прочитать() Цикл
		Если ЧтениеФайлаТипов.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ЗаписьФайлаТипов.ЗаписатьНачалоЭлемента(ЧтениеФайлаТипов.Имя);
			Пока ЧтениеФайлаТипов.ПрочитатьАтрибут() Цикл
				ЗаписьФайлаТипов.ЗаписатьАтрибут(ЧтениеФайлаТипов.Имя,ЧтениеФайлаТипов.Значение); 
				Если ЧтениеФайлаТипов.Имя = "Extension" И ЧтениеФайлаТипов.Значение = "jpeg" Тогда
					АтрибутJPEGНайден = Истина;
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли ЧтениеФайлаТипов.ТипУзла = ТипУзлаXML.Текст Тогда
			ЗаписьФайлаТипов.ЗаписатьТекст(ЧтениеФайлаСвязей.Значение);
		ИначеЕсли ЧтениеФайлаТипов.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			Если ЧтениеФайлаТипов.Имя = "Types" И НЕ АтрибутJPEGНайден Тогда
				НоваяСтрока = "<Default Extension=""jpeg"" ContentType=""image/jpeg""/>";
				ЗаписьФайлаТипов.ЗаписатьБезОбработки(НоваяСтрока);
			КонецЕсли;
			ЗаписьФайлаТипов.ЗаписатьКонецЭлемента();
		КонецЕсли;
	КонецЦикла;
	ЧтениеФайлаТипов.Закрыть();
	ЗаписьФайлаТипов.Закрыть();
	ПереместитьФайл(ВременнаяПапкаДляРазархивирования + "/[Content_Types]_update.xml", ВременнаяПапкаДляРазархивирования + "/[Content_Types].xml");
	УдалитьФайлы(ВременнаяПапкаДляРазархивирования + "/[Content_Types]_update.xml");

КонецПроцедуры

Функция ВставитьШтрихкод(ТекущийФайл, ДанныеОШтрихкодеФайла) Экспорт
	
	Если ДанныеОШтрихкодеФайла <> Неопределено И Не ДанныеОШтрихкодеФайла.Свойство("ДвоичныеДанныеФайла") Тогда
		ДанныеОШтрихкодеФайла = ШтрихкодированиеСервер.ПолучитьДанныеДляВставкиШтрихкодаВОбъект(ТекущийФайл);
	КонецЕсли;
	
	Возврат ШтрихкодированиеКлиентСервер.ВставитьШтрихкодСИспользованиемНастроек(ТекущийФайл, 
		Неопределено, 
		Ложь, 
		ДанныеОШтрихкодеФайла.ДвоичныеДанныеИзображения, 
		ДанныеОШтрихкодеФайла.ДвоичныеДанныеФайла,
		ДанныеОШтрихкодеФайла.Расширение,
		ДанныеОШтрихкодеФайла.ФайлРедактируется, 
		ДанныеОШтрихкодеФайла.ИзменениеФайловMSWordТолькоНаСервере);
	
КонецФункции

Функция ВставитьРегистрационныйШтамп(ТекущийФайл, ДанныеОШтрихкодеФайла, ТекстНадписи) Экспорт 
	
	Если ДанныеОШтрихкодеФайла <> Неопределено 
		И НЕ ДанныеОШтрихкодеФайла.Свойство("ДвоичныеДанныеФайла") Тогда
		ДанныеОШтрихкодеФайла = ШтрихкодированиеСервер.ПолучитьДанныеДляВставкиШтрихкодаВОбъект(ТекущийФайл);
	КонецЕсли;
	Возврат ШтрихкодированиеКлиентСервер.ВставитьРегистрационныйШтампСИспользованиемНастроек(ТекущийФайл, 
		Неопределено, 
		Ложь, 
		ТекстНадписи, 
		ДанныеОШтрихкодеФайла.ДвоичныеДанныеФайла,
		ДанныеОШтрихкодеФайла.ДвоичныеДанныеИзображения,
		ДанныеОШтрихкодеФайла.Расширение,
		ДанныеОШтрихкодеФайла.ФайлРедактируется, 
		ДанныеОШтрихкодеФайла.ИзменениеФайловMSWordТолькоНаСервере);
	
КонецФункции

Функция ШтрихкодированиеВключено() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьШтрихкоды");
	
КонецФункции

#КонецОбласти
