#Область СлужебныйПрограммныйИнтерфейс

// Получить данные расширения.
// 
// Параметры:
//  ИдентификаторСеанса - Строка
//  ПараметрыПоиска - Структура:
// * ПубличныйИдентификатор - Строка
// * ИдентификаторВерсии - Строка
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
//   * Наименование - Строка 
//   * КраткаяИнформация - Строка
//   * ИдентификаторВерсии - Строка
//   * ИдентификаторРасширения - Строка
//   * ИдентификаторСеанса - Строка
//   * УстановкаРазрешена - Булево
//   * Установлено - Булево
//   * АдресКартинки - Строка
//   * ЭтоРасширение - Булево
//   * ИнтерфейсНастроек - Строка
//   * ПубличныйИдентификатор - Строка
Функция ПолучитьДанныеРасширения(ИдентификаторСеанса, ПараметрыПоиска) Экспорт
	
	УстановкаРазрешена = Пользователи.ЭтоПолноправныйПользователь() 
		Или Пользователи.ЭтоПолноправныйПользователь(, Истина);
		
	Если ОбщегоНазначения.РазделениеВключено() 
		И Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		УстановкаРазрешена = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыПоиска.ПубличныйИдентификатор) Тогда
		ДанныеРасширения = ПлюсСервис.ДанныеРасширенияПоПубличномуИдентификатору(
			ИдентификаторСеанса, ПараметрыПоиска.ПубличныйИдентификатор);
	Иначе
		ДанныеРасширения = ПлюсСервис.ДанныеРасширенияПоИдентификаторуВерсии(
			ИдентификаторСеанса, ПараметрыПоиска.ИдентификаторВерсии);
	КонецЕсли;
	
	Картинка = Новый Картинка(ДанныеРасширения.ДанныеКартинки);
	
	Результат = Новый Структура;
	Результат.Вставить("Наименование", ДанныеРасширения.Наименование);
	Результат.Вставить("КраткаяИнформация", ДанныеРасширения.КраткаяИнформация); 
	Результат.Вставить("ИдентификаторВерсии", ДанныеРасширения.ИдентификаторВерсии);
	Результат.Вставить("ИдентификаторРасширения", ДанныеРасширения.ИдентификаторРасширения);
	Результат.Вставить("ИдентификаторСеанса", ИдентификаторСеанса);
	Результат.Вставить("УстановкаРазрешена", УстановкаРазрешена);
	Результат.Вставить("Установлено", ДанныеРасширения.Установлено);
	Результат.Вставить("АдресКартинки", ПоместитьВоВременноеХранилище(Картинка));
	Результат.Вставить("ЭтоРасширение", ДанныеРасширения.ЭтоРасширение);
	Результат.Вставить("ИнтерфейсНастроек", ДанныеРасширения.ИнтерфейсНастроек);
	Результат.Вставить("ПубличныйИдентификатор", 
		?(ДанныеРасширения.ПубличныеИдентификаторы.Количество(), ДанныеРасширения.ПубличныеИдентификаторы[0], ""));
	
	Возврат Новый ФиксированнаяСтруктура(Результат);

КонецФункции

// См. ПлюсСервис.Используется
// См. ПлюсСервисКлиент.Используется
Функция Используется() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ПлюсСервисИспользуется");

КонецФункции

Процедура ДобавитьФормуВИсториюОткрытых(ТипИсточника, ТипЗначенияИсточника, ИмяФормы) Экспорт

	ПлюсСервис.ДобавитьФормуВИсториюОткрытых(ТипИсточника, ТипЗначенияИсточника, ИмяФормы);
	
КонецПроцедуры

Процедура СнятьМонопольныйРежим() Экспорт
	
	ПлюсСервис.СнятьМонопольныйРежим();
	
КонецПроцедуры

#КонецОбласти
