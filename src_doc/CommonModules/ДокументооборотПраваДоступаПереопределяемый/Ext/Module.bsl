#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Таблице значений больше этого размера надо добавить индексы для быстрой работы.
// 
// Возвращаемое значение:
//  Число -
Функция РазмерТаблицыЗначенийДляИндексации() Экспорт
	
	Возврат 500;
	
КонецФункции

#КонецОбласти // ДляВызоваИзДругихПодсистем

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает пороговое число объектов в таблице для расчета прав в одном сеансе фоновым заданием
// 
// Возвращаемое значение:
//  Число - Количество объектов
Функция КоличествоОбъектовДляРасчетаПравВОдномСеансе() Экспорт

	Возврат 10000;
	
КонецФункции

// Размер порции для записи РС "Состав субъектов прав доступа", чтобы возникало меньше блокировок.
// Слишком большой размер - ускоряет запись, но увеличивает шанс блокировок. Слишком маленький - наоборот.
// 
// Возвращаемое значение:
//  Число
Функция РазмерПорцииДляЗаписиССПД() Экспорт
	
	Возврат 50000;
	
КонецФункции

// Размер порции для записи больших регистров. Они бывают настолько большими, что не хватает памяти за один раз
// их очистить или перезаписать. (При очистке всего регистра без отбора, неявно происходит его считывание в память).
// Слишком большой размер - ускоряет, но увеличивает шанс, что закончится память. Слишком маленький - наоборот.
// 
// Возвращаемое значение:
//  Число
Функция РазмерПорцииДляЗаписиБольшихРегистров() Экспорт
	
	Возврат 200000;
	
КонецФункции

// Если в НаборЗаписей изменено (удалено + добавлено) более этого числа (в процентах), делаем полную перезапись
// 
// Возвращаемое значение:
//  Число -
Функция ПроцентИзмененийДляПолнойПерезаписи() Экспорт
	
	Возврат 5;
	
КонецФункции

// Если в НаборЗаписей изменено (удалено + добавлено) более этого числа, делаем полную перезапись
// 
// Возвращаемое значение:
//  Число -
Функция ЧислоЗаписейДляПолнойПерезаписи() Экспорт
	
	Возврат 100;
	
КонецФункции

// Переопределяемая часть функции См. ДокументооборотПраваДоступа.ПраваПравообладателейПоОднотипнымОбъектам
// Сюда можно добавлять запросы по своим объектам.
// Еще одна альтернатива - в модуле менеджера своего объекта создать метод ПраваСотрудниковПоОбъектам.
// 
// Параметры:
//  ОбъектыДоступа - Массив из ЛюбаяСсылка -
//  ТипОбъекта - Тип - Тип первого объекта доступа.
//  МетаданныеОбъекта - ОбъектМетаданных - Метаданные первого объекта.
//  ЗаменяемыеПодстроки - Соответствие Из КлючИЗначение - Заменяемые подстроки в запросе:
//  * Ключ - Строка - Что заменить.
//  * Значение - Строка - На что заменить.
//  СотрудникиОтбор - Массив Из СправочникСсылка.Сотрудники - Сотрудники для отбора.
//  				- Неопределено -
//  ПользователиОтбор - Массив Из СправочникСсылка.Пользователи - Пользователи для отбора.
//					  - Неопределено -
// 
// Возвращаемое значение:
//  Запрос - Запрос на права по объекту.
Функция ЗапросНаПраваПоОбъекту(
			ОбъектыДоступа,
			ТипОбъекта,
			МетаданныеОбъекта,
			ЗаменяемыеПодстроки,
			СотрудникиОтбор = Неопределено,
			ПользователиОтбор = Неопределено) Экспорт
	
	ПервыйОбъектДоступа = ОбъектыДоступа[0];
	Запрос = Новый Запрос;
	
	Если ОбщегоНазначения.ЭтоБизнесПроцесс(МетаданныеОбъекта) Тогда
		
		#Область Запрос_БизнесПроцесс
		// Права на чтение имеют все участники процесса, права на изменение - только автор.
		// Также учитываются делегаты и руководители.
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВложенныйЗапрос.ОбъектДоступа КАК ОбъектДоступа,
			|	ВложенныйЗапрос.Сотрудник КАК Сотрудник,
			|	ВложенныйЗапрос.Пользователь КАК Пользователь,
			|	МАКСИМУМ(ВложенныйЗапрос.Чтение) КАК Чтение,
			|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Добавление,
			|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Изменение,
			|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|(
			|	ВЫБРАТЬ
			|		ТаблицаПроцесса.Ссылка КАК ОбъектДоступа,
			|		СоставСубъектов.Сотрудник КАК Сотрудник,
			|		СоставСубъектов.Пользователь КАК Пользователь,
			|		ИСТИНА КАК Чтение,
			|		ЛОЖЬ КАК Изменение
			|	ИЗ
			|		&ТаблицаПроцесса КАК ТаблицаПроцесса
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		РегистрСведений.УчастникиПроцессов КАК УчастникиПроцессов
			|			ПО ТаблицаПроцесса.Ссылка = УчастникиПроцессов.Процесс
			|		ЛЕВОЕ СОЕДИНЕНИЕ
			|		РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|			ПО УчастникиПроцессов.Участник = СоставСубъектов.Субъект
			|	ГДЕ
			|		ТаблицаПроцесса.Ссылка В (&ОбъектыДоступа)
			|		И (СоставСубъектов.ИмяОбластиДелегирования В (&ИменаОбластейЗамещения))
			|		И &СоставСубъектов_ОтборПравообладателей
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ТаблицаПроцесса.Ссылка,
			|		СоставСубъектов.Сотрудник,
			|		СоставСубъектов.Пользователь,
			|		ИСТИНА,
			|		ИСТИНА
			|	ИЗ
			|		&ТаблицаПроцесса КАК ТаблицаПроцесса
			|		ЛЕВОЕ СОЕДИНЕНИЕ
			|		РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|			ПО ТаблицаПроцесса.Автор = СоставСубъектов.Субъект
			|	ГДЕ
			|		ТаблицаПроцесса.Ссылка В(&ОбъектыДоступа)
			|		И (СоставСубъектов.ИмяОбластиДелегирования В (&ИменаОбластейЗамещения))
			|		И &СоставСубъектов_ОтборПравообладателей
			|) КАК ВложенныйЗапрос
			|
			|СГРУППИРОВАТЬ ПО
			|	ВложенныйЗапрос.ОбъектДоступа,
			|	ВложенныйЗапрос.Сотрудник,
			|	ВложенныйЗапрос.Пользователь";
		ЗаменяемыеПодстроки.Вставить("&ТаблицаПроцесса", МетаданныеОбъекта.ПолноеИмя());
		ДокументооборотПраваДоступа.ПодменитьОтборПравообладателейВЗапросе(
			Запрос, "&СоставСубъектов_ОтборПравообладателей", "СоставСубъектов", СотрудникиОтбор, ПользователиОтбор);
		#КонецОбласти
		
	ИначеЕсли ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(ПервыйОбъектДоступа) Тогда
		
		#Область Запрос_ШаблонБизнесПроцесса
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВложенныйЗапрос.ОбъектДоступа,
			|	ВложенныйЗапрос.Сотрудник,
			|	ВложенныйЗапрос.Пользователь,
			|	МАКСИМУМ(ВложенныйЗапрос.Чтение) КАК Чтение,
			|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Изменение,
			|	МАКСИМУМ(ВложенныйЗапрос.Добавление) КАК Добавление,
			|	МАКСИМУМ(ВложенныйЗапрос.Удаление) КАК Удаление,
			|	МАКСИМУМ(ВложенныйЗапрос.УправлениеПравами) КАК УправлениеПравами
			|ИЗ
			|(
			|	ВЫБРАТЬ
			|		ТаблицаШаблона.Ссылка КАК ОбъектДоступа,
			|		СоставСубъектов.Сотрудник,
			|		СоставСубъектов.Пользователь КАК Пользователь,
			|		ИСТИНА КАК Чтение,
			|		ЛОЖЬ КАК Изменение,
			|		ЛОЖЬ КАК Добавление,
			|		ЛОЖЬ КАК Удаление,
			|		ЛОЖЬ КАК УправлениеПравами
			|	ИЗ
			|		&ТаблицаШаблона КАК ТаблицаШаблона
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		РегистрСведений.УчастникиПроцессов КАК УчастникиПроцессов
			|			ПО ТаблицаШаблона.КомплексныйПроцесс = УчастникиПроцессов.Процесс
			|		ЛЕВОЕ СОЕДИНЕНИЕ
			|		РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|			ПО УчастникиПроцессов.Участник = СоставСубъектов.Субъект
			|	ГДЕ
			|		ТаблицаШаблона.Ссылка В (&ОбъектыДоступа)
			|		И СоставСубъектов.ИмяОбластиДелегирования В (&ИменаОбластейЗамещения)
			|		И &СоставСубъектов_ОтборПравообладателей
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ТаблицаШаблона.Ссылка,
			|		СоставСубъектов.Сотрудник,
			|		СоставСубъектов.Пользователь КАК Пользователь,
			|		ИСТИНА,
			|		ИСТИНА,
			|		ИСТИНА,
			|		ИСТИНА,
			|		ЛОЖЬ
			|	ИЗ
			|		&ТаблицаШаблона КАК ТаблицаШаблона
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		БизнесПроцесс.КомплексныйПроцесс КАК КомплексныйПроцессБП
			|			ПО ТаблицаШаблона.КомплексныйПроцесс = КомплексныйПроцессБП.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ
			|		РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|			ПО КомплексныйПроцессБП.Автор = СоставСубъектов.Субъект
			|	ГДЕ
			|		ТаблицаШаблона.Ссылка В (&ОбъектыДоступа)
			|		И СоставСубъектов.ИмяОбластиДелегирования В (&ИменаОбластейЗамещения)
			|		И &СоставСубъектов_ОтборПравообладателей
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ТаблицаШаблона.Ссылка,
			|		ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|		ПраваПоДескрипторамДоступаОбъектов.Пользователь,
			|		ПраваПоДескрипторамДоступаОбъектов.Чтение,
			|		ПраваПоДескрипторамДоступаОбъектов.Изменение,
			|		ПраваПоДескрипторамДоступаОбъектов.Добавление,
			|		ПраваПоДескрипторамДоступаОбъектов.Удаление,
			|		ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами
			|	ИЗ
			|		&ТаблицаШаблона КАК ТаблицаШаблона
			|		ЛЕВОЕ СОЕДИНЕНИЕ
			|		РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|			ПО ТаблицаШаблона.Ссылка = ДескрипторыДляОбъектов.Объект
			|		ЛЕВОЕ СОЕДИНЕНИЕ
			|		РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|			ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|	ГДЕ
			|		ДескрипторыДляОбъектов.Объект В (&ОбъектыДоступа)
			|		И &ПраваПоДескрипторам_ОтборПравообладателей
			|
			|) КАК ВложенныйЗапрос
			|
			|СГРУППИРОВАТЬ ПО
			|	ВложенныйЗапрос.ОбъектДоступа,
			|	ВложенныйЗапрос.Сотрудник,
			|	ВложенныйЗапрос.Пользователь";
		ЗаменяемыеПодстроки.Вставить("&ТаблицаШаблона", ОбъектыДоступа[0].Метаданные().ПолноеИмя());
		ДокументооборотПраваДоступа.ПодменитьОтборПравообладателейВЗапросе(
			Запрос, "&СоставСубъектов_ОтборПравообладателей", "СоставСубъектов", СотрудникиОтбор, ПользователиОтбор);
		#КонецОбласти
		
	ИначеЕсли ДействияКлиентСервер.ЭтоДействие(ПервыйОбъектДоступа) Тогда
		
		#Область Запрос_ДействияОбработокОбъектов
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Действия.Ссылка КАК ОбъектДоступа,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь,
			|	ИСТИНА КАК Чтение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Добавление) КАК Добавление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Изменение) КАК Изменение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Удаление) КАК Удаление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами) КАК УправлениеПравами
			|ИЗ
			|	&ТаблицаСправочникаДействия КАК Действия
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|		ПО Действия.Предмет = ДескрипторыДляОбъектов.Объект
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|		ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|ГДЕ
			|	Действия.Ссылка В (&ОбъектыДоступа)
			|	И &ДопУсловия
			|	И &ПраваПоДескрипторам_ОтборПравообладателей
			|
			|СГРУППИРОВАТЬ ПО
			|	Действия.Ссылка,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь";
		ТипДействия = "";
		Если ТипОбъекта = Тип("СправочникСсылка.ДействияИсполнения") Тогда
			ТипДействия = "ДействияИсполнения";
		ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ДействияОзнакомления") Тогда
			ТипДействия = "ДействияОзнакомления";
		ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ДействияПодписания") Тогда
			ТипДействия = "ДействияПодписания";
		ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ДействияРегистрации") Тогда
			ТипДействия = "ДействияРегистрации";
		ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ДействияСогласования") Тогда
			ТипДействия = "ДействияСогласования";
		ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ДействияУтверждения") Тогда
			ТипДействия = "ДействияУтверждения";	
		КонецЕсли;
		ЗаменяемыеПодстроки.Вставить("&ТаблицаСправочникаДействия", СтрШаблон("Справочник.%1", ТипДействия)); 
		
		ДопНастройки = ДокументооборотПраваДоступа.ПолучитьДопНастройкиДляПолученияПравПоОбъекту(ТипОбъекта);
		ДопУсловия = "ИСТИНА";
		Если Не ДопНастройки.ДобавлятьРуководителейИДелегатов Тогда
			ДопУсловия = "ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование = Неопределено";
		КонецЕсли;
		ЗаменяемыеПодстроки.Вставить("&ДопУсловия", ДопУсловия);
		#КонецОбласти
		
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.Задача") Тогда
		
		#Область Запрос_ДокументЗадача
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВложенныйЗапрос.ОбъектДоступа,
			|	ВложенныйЗапрос.Сотрудник,
			|	ВложенныйЗапрос.Пользователь КАК Пользователь,
			|	МАКСИМУМ(ВложенныйЗапрос.Чтение) КАК Чтение,
			|	МАКСИМУМ(ВложенныйЗапрос.Добавление) КАК Добавление,
			|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Изменение,
			|	МАКСИМУМ(ВложенныйЗапрос.Удаление) КАК Удаление,
			|	МАКСИМУМ(ВложенныйЗапрос.УправлениеПравами) КАК УправлениеПравами
			|ИЗ
			|(
			|	ВЫБРАТЬ
			|		ДескрипторыДляОбъектов.Объект КАК ОбъектДоступа,
			|		ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|		ПраваПоДескрипторамДоступаОбъектов.Пользователь,
			|		ИСТИНА КАК Чтение,
			|		ПраваПоДескрипторамДоступаОбъектов.Добавление КАК Добавление,
			|		ПраваПоДескрипторамДоступаОбъектов.Изменение КАК Изменение,
			|		ПраваПоДескрипторамДоступаОбъектов.Удаление КАК Удаление,
			|		ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами КАК УправлениеПравами
			|	ИЗ
			|		РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|			ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|	ГДЕ
			|		ДескрипторыДляОбъектов.Объект В (&ОбъектыДоступа)
			|		И &ПраваПоДескрипторам_ОтборПравообладателей
			|
			|	ОБЪЕДИНИТЬ ВСЕ
			|
			|	ВЫБРАТЬ
			|		ЗадачаДок.Ссылка,
			|		ЗамещающиеИПомощники.Замещающий,
			|		ЕСТЬNULL(СотрудникиПользователей.Пользователь, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) КАК Пользователь,
			|		ИСТИНА,
			|		ИСТИНА,
			|		ИСТИНА,
			|		ИСТИНА,
			|		ЛОЖЬ
			|	ИЗ
			|		Документ.Задача КАК ЗадачаДок
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
			|			ПО ЗадачаДок.Автор = СотрудникиВКонтейнерах.Контейнер
			|		ЛЕВОЕ СОЕДИНЕНИЕ
			|		Справочник.ЗамещающиеИПомощники КАК ЗамещающиеИПомощники
			|			ПО СотрудникиВКонтейнерах.Сотрудник = ЗамещающиеИПомощники.Сотрудник
			|		ЛЕВОЕ СОЕДИНЕНИЕ
			|		Справочник.ЗамещающиеИПомощники.ВопросыЗамещения КАК ВопросыЗамещенияТЧ
			|			ПО ЗамещающиеИПомощники.Ссылка = ВопросыЗамещенияТЧ.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ
			|		РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|			ПО СотрудникиПользователей.Сотрудник = ЗамещающиеИПомощники.Замещающий
			|	ГДЕ
			|		ЗадачаДок.Ссылка В (&ОбъектыДоступа)
			|		И ЗадачаДок.Источник = НЕОПРЕДЕЛЕНО
			|		И ЗадачаДок.ВышестоящаяЗадача = ЗНАЧЕНИЕ(Документ.Задача.ПустаяСсылка)
			|		И ЗамещающиеИПомощники.Действует
			|		И ВопросыЗамещенияТЧ.Область В (&ОбластиЗамещения)
			|		И &Замещающие_ОтборПравообладателей
			|) КАК ВложенныйЗапрос
			|
			|СГРУППИРОВАТЬ ПО
			|	ВложенныйЗапрос.ОбъектДоступа,
			|	ВложенныйЗапрос.Сотрудник,
			|	ВложенныйЗапрос.Пользователь";
		ОбластиЗамещения = Новый Массив;
		ОбластиЗамещения.Добавить(Справочники.ОбластиЗамещения.ЗадачиПостановкаПоручений);
		ОбластиЗамещения.Добавить(Справочники.ОбластиЗамещения.Задачи);
		ОбластиЗамещения.Добавить(Справочники.ОбластиЗамещения.ВсеОбласти);
		Запрос.УстановитьПараметр("ОбластиЗамещения", ОбластиЗамещения);
		
		Если ПользователиОтбор <> Неопределено Тогда
			ОбщегоНазначенияДокументооборот.ПодменитьОтборПоПолюВЗапросе(
				Запрос, "&Замещающие_ОтборПравообладателей", "СотрудникиПользователей.Пользователь",
				"ПользователиОтбор", ПользователиОтбор);
		ИначеЕсли СотрудникиОтбор <> Неопределено Тогда
			ОбщегоНазначенияДокументооборот.ПодменитьОтборПоПолюВЗапросе(
				Запрос, "&Замещающие_ОтборПравообладателей", "ЗамещающиеИПомощники.Замещающий",
				"СотрудникиОтбор", СотрудникиОтбор);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Замещающие_ОтборПравообладателей", "ИСТИНА");
		КонецЕсли;
		#КонецОбласти
		
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.ДействиеЗадачи") Тогда
		
		#Область Запрос_ДействиеЗадачи
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ДескрипторыДляОбъектов.Объект КАК Объект,
			|	ДескрипторыДляОбъектов.Дескриптор КАК Дескриптор
			|ПОМЕСТИТЬ Дескрипторы
			|ИЗ
			|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|ГДЕ
			|	ДескрипторыДляОбъектов.Объект В (&ОбъектыДоступа)
			|	И ДескрипторыДляОбъектов.ТипДескриптора = &ТипИндивидуальный
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДействиеЗадачи.Ссылка,
			|	ДескрипторыДляОбъектов.Дескриптор
			|ИЗ
			|	Документ.ДействиеЗадачи КАК ДействиеЗадачи
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|		ПО (ДескрипторыДляОбъектов.Объект = ДействиеЗадачи.Задача)
			|ГДЕ
			|	ДействиеЗадачи.Ссылка В (&ОбъектыДоступа)
			|	И ДескрипторыДляОбъектов.ТипДескриптора = &ТипИндивидуальный
			|
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДескрипторыДляОбъектов.Объект КАК ОбъектДоступа,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник КАК Сотрудник,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь КАК Пользователь,
			|	ИСТИНА КАК Чтение,
			|	ПраваПоДескрипторамДоступаОбъектов.Добавление КАК Добавление,
			|	ПраваПоДескрипторамДоступаОбъектов.Изменение КАК Изменение,
			|	ПраваПоДескрипторамДоступаОбъектов.Удаление КАК Удаление,
			|	ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами КАК УправлениеПравами
			|ПОМЕСТИТЬ ПраваПоОбъектам
			|ИЗ
			|	Дескрипторы КАК ДескрипторыДляОбъектов
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|		ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|ГДЕ
			|	НЕ ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование ССЫЛКА Справочник.ЗамещающиеИПомощники
			|	И &ПраваПоДескрипторам_ОтборПравообладателей
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДескрипторыДляОбъектов.Объект,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь,
			|	ИСТИНА,
			|	ПраваПоДескрипторамДоступаОбъектов.Добавление,
			|	ПраваПоДескрипторамДоступаОбъектов.Изменение,
			|	ПраваПоДескрипторамДоступаОбъектов.Удаление,
			|	ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами
			|ИЗ
			|	Дескрипторы КАК ДескрипторыДляОбъектов
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|		ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	Справочник.ЗамещающиеИПомощники.ВопросыЗамещения КАК ЗамещающиеИПомощникиВопросыЗамещения
			|		ПО ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование = ЗамещающиеИПомощникиВопросыЗамещения.Ссылка
			|ГДЕ
			|	ЗамещающиеИПомощникиВопросыЗамещения.Область В (&ОбластиЗамещения)
			|	И &ПраваПоДескрипторам_ОтборПравообладателей
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДействиеЗадачи.Ссылка,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь,
			|	ИСТИНА,
			|	ПраваПоДескрипторамДоступаОбъектов.Добавление,
			|	ПраваПоДескрипторамДоступаОбъектов.Изменение,
			|	ПраваПоДескрипторамДоступаОбъектов.Удаление,
			|	ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами
			|ИЗ
			|	Документ.ДействиеЗадачи КАК ДействиеЗадачи
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	Дескрипторы КАК ДескрипторыДляОбъектов
			|		ПО (ДескрипторыДляОбъектов.Объект = ДействиеЗадачи.Задача)
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|		ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|		
			|ГДЕ
			|	ДействиеЗадачи.Ссылка В (&ОбъектыДоступа)
			|	И НЕ ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование ССЫЛКА Справочник.ЗамещающиеИПомощники
			|	И &ПраваПоДескрипторам_ОтборПравообладателей
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПраваПоОбъектам.ОбъектДоступа КАК ОбъектДоступа,
			|	ПраваПоОбъектам.Сотрудник КАК Сотрудник,
			|	ПраваПоОбъектам.Пользователь КАК Пользователь,
			|	МАКСИМУМ(ПраваПоОбъектам.Чтение) КАК Чтение,
			|	МАКСИМУМ(ПраваПоОбъектам.Добавление) КАК Добавление,
			|	МАКСИМУМ(ПраваПоОбъектам.Изменение) КАК Изменение,
			|	МАКСИМУМ(ПраваПоОбъектам.Удаление) КАК Удаление,
			|	МАКСИМУМ(ПраваПоОбъектам.УправлениеПравами) КАК УправлениеПравами
			|ИЗ
			|	ПраваПоОбъектам КАК ПраваПоОбъектам
			|СГРУППИРОВАТЬ ПО
			|	ПраваПоОбъектам.ОбъектДоступа,
			|	ПраваПоОбъектам.Сотрудник,
			|	ПраваПоОбъектам.Пользователь";
		ОбластиЗамещения = Новый Массив;
		ОбластиЗамещения.Добавить(Справочники.ОбластиЗамещения.ЗадачиВыполнениеПрочихПоручений);
		ОбластиЗамещения.Добавить(Справочники.ОбластиЗамещения.Задачи);
		ОбластиЗамещения.Добавить(Справочники.ОбластиЗамещения.ВсеОбласти);
		Запрос.УстановитьПараметр("ОбластиЗамещения", ОбластиЗамещения);
		Запрос.УстановитьПараметр("ТипИндивидуальный", РегистрыСведений.ДескрипторыДляОбъектов.ТипИндивидуальный());
		#КонецОбласти
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.БанковскиеСчета") Тогда
		
		#Область Запрос_БанковскиеСчета
		Запрос.Текст =
			"ВЫБРАТЬ
			|	БанковскиеСчета.Ссылка КАК ОбъектДоступа,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь,
			|	ИСТИНА КАК Чтение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Добавление) КАК Добавление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Изменение) КАК Изменение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Удаление) КАК Удаление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами) КАК УправлениеПравами
			|ИЗ
			|	Справочник.БанковскиеСчета КАК БанковскиеСчета
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|		ПО БанковскиеСчета.Владелец = ДескрипторыДляОбъектов.Объект
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|		ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|ГДЕ
			|	БанковскиеСчета.Ссылка В (&ОбъектыДоступа)
			|	И &ДопУсловия
			|	И &ПраваПоДескрипторам_ОтборПравообладателей
			|
			|СГРУППИРОВАТЬ ПО
			|	БанковскиеСчета.Ссылка,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь";
		ДопНастройки = ДокументооборотПраваДоступа.ПолучитьДопНастройкиДляПолученияПравПоОбъекту(ТипОбъекта);
		ДопУсловия = "ИСТИНА";
		Если Не ДопНастройки.ДобавлятьРуководителейИДелегатов Тогда
			ДопУсловия = "ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование = Неопределено";
		КонецЕсли;
		ЗаменяемыеПодстроки.Вставить("&ДопУсловия", ДопУсловия);
		#КонецОбласти
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ЗаметкиДокументооборота") Тогда
		
		#Область Запрос_ЗаметкиДокументооборота
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗаметкиДокументооборота.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Сотрудник КАК Сотрудник,
			|	СоставСубъектов.Пользователь КАК Пользователь,
			|	ВЫБОР
			|		КОГДА ЗаметкиДокументооборота.Автор = СоставСубъектов.Сотрудник
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК Чтение,
			|	ИСТИНА КАК Добавление,
			|	ИСТИНА КАК Изменение,
			|	ЛОЖЬ КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Справочник.ЗаметкиДокументооборота КАК ЗаметкиДокументооборота
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО ЗаметкиДокументооборота.Автор = СоставСубъектов.Субъект
			|ГДЕ
			|	ЗаметкиДокументооборота.Ссылка В (&ОбъектыДоступа)
			|	И &СоставСубъектов_ОтборПравообладателей";
		ДокументооборотПраваДоступа.ПодменитьОтборПравообладателейВЗапросе(
			Запрос, "&СоставСубъектов_ОтборПравообладателей", "СоставСубъектов", СотрудникиОтбор, ПользователиОтбор);
		#КонецОбласти
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ЗамещающиеИПомощники") Тогда
		
		#Область Запрос_ЗамещающиеИПомощники
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ЗамещающиеИПомощники.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Сотрудник,
			|	СоставСубъектов.Пользователь,
			|	ИСТИНА КАК Чтение,
			|	ИСТИНА КАК Добавление,
			|	ИСТИНА КАК Изменение,
			|	ИСТИНА КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Справочник.ЗамещающиеИПомощники КАК ЗамещающиеИПомощники
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО ЗамещающиеИПомощники.Сотрудник = СоставСубъектов.Субъект
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектовЗам
			|		ПО ЗамещающиеИПомощники.Замещающий = СоставСубъектовЗам.Субъект
			|ГДЕ
			|	ЗамещающиеИПомощники.Ссылка В (&ОбъектыДоступа)
			|	И СоставСубъектов.ИмяОбластиДелегирования = """"
			|	И &СоставСубъектов_ОтборПравообладателей
			|	И СоставСубъектов.Сотрудник = СоставСубъектовЗам.Сотрудник
			|	И СоставСубъектовЗам.ИмяОбластиДелегирования = """"";
		ДокументооборотПраваДоступа.ПодменитьОтборПравообладателейВЗапросе(
			Запрос, "&СоставСубъектов_ОтборПравообладателей", "СоставСубъектов", СотрудникиОтбор, ПользователиОтбор);
		#КонецОбласти
	
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.КаналыОбсуждений") Тогда
		
		#Область Запрос_КаналыОбсуждений
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	КаналыОбсужденийУчастники.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Сотрудник КАК Сотрудник,
			|	СоставСубъектов.Пользователь КАК Пользователь,
			|	ИСТИНА КАК Чтение,
			|	КаналыОбсужденийУчастники.Роль = ЗНАЧЕНИЕ(Перечисление.РолиУчастниковКаналовОбсуждений.Администратор) КАК Добавление,
			|	КаналыОбсужденийУчастники.Роль = ЗНАЧЕНИЕ(Перечисление.РолиУчастниковКаналовОбсуждений.Администратор) КАК Изменение,
			|	КаналыОбсужденийУчастники.Роль = ЗНАЧЕНИЕ(Перечисление.РолиУчастниковКаналовОбсуждений.Администратор) КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Справочник.КаналыОбсуждений.Участники КАК КаналыОбсужденийУчастники
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
			|		ПО КаналыОбсужденийУчастники.Участник = СотрудникиВКонтейнерах.Контейнер
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО СотрудникиВКонтейнерах.Сотрудник = СоставСубъектов.Субъект
			|ГДЕ
			|	КаналыОбсужденийУчастники.Ссылка В (&ОбъектыДоступа)
			|	И СоставСубъектов.ИмяОбластиДелегирования В (&ИменаОбластейЗамещения)
			|	И &СоставСубъектов_ОтборПравообладателей";
		ДокументооборотПраваДоступа.ПодменитьОтборПравообладателейВЗапросе(
			Запрос, "&СоставСубъектов_ОтборПравообладателей", "СоставСубъектов", СотрудникиОтбор, ПользователиОтбор);
		#КонецОбласти
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.Контроль") Тогда	
		
		#Область Запрос_Контроль
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Контроль.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Сотрудник КАК Сотрудник,
			|	СоставСубъектов.Пользователь КАК Пользователь,
			|	ИСТИНА КАК Чтение,
			|	Контроль.Автор = СоставСубъектов.Сотрудник КАК Добавление,
			|	ИСТИНА КАК Изменение,
			|	ИСТИНА КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Справочник.Контроль КАК Контроль
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО (Контроль.Контролер = СоставСубъектов.Субъект
			|			ИЛИ Контроль.Автор = СоставСубъектов.Субъект)
			|ГДЕ
			|	Контроль.Ссылка В (&ОбъектыДоступа)
			|	И СоставСубъектов.ИмяОбластиДелегирования В ("""", ""Контроль"")
			|	И &СоставСубъектов_ОтборПравообладателей";
		ДокументооборотПраваДоступа.ПодменитьОтборПравообладателейВЗапросе(
			Запрос, "&СоставСубъектов_ОтборПравообладателей", "СоставСубъектов", СотрудникиОтбор, ПользователиОтбор);
		#КонецОбласти
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ОбработкиОбъектов") Тогда
		
		#Область Запрос_ОбработкиОбъектов
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ОбработкиОбъектов.Ссылка КАК ОбъектДоступа,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь,
			|	ИСТИНА КАК Чтение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Добавление) КАК Добавление,
			|	ИСТИНА КАК Изменение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Удаление) КАК Удаление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами) КАК УправлениеПравами
			|ИЗ
			|	Справочник.ОбработкиОбъектов КАК ОбработкиОбъектов
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|		ПО ОбработкиОбъектов.Владелец = ДескрипторыДляОбъектов.Объект
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|		ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|ГДЕ
			|	ОбработкиОбъектов.Ссылка В (&ОбъектыДоступа)
			|	И &ПраваПоДескрипторам_ОтборПравообладателей
			|СГРУППИРОВАТЬ ПО
			|	ОбработкиОбъектов.Ссылка,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь";
		#КонецОбласти
		
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.Отсутствие") Тогда
		
		#Область Запрос_Отсутствие
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Отсутствие.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Сотрудник КАК Сотрудник,
			|	СоставСубъектов.Пользователь КАК Пользователь,
			|	ИСТИНА КАК Чтение,
			|	ВЫБОР
			|		КОГДА Отсутствие.Автор = СоставСубъектов.Сотрудник
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК Добавление,
			|	ВЫБОР
			|		КОГДА СоставСубъектов.Субъект ЕСТЬ NULL 
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ КАК Изменение,
			|	ЛОЖЬ КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Документ.Отсутствие КАК Отсутствие
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО (Отсутствие.Сотрудник = СоставСубъектов.Субъект
			|				ИЛИ Отсутствие.Автор = СоставСубъектов.Субъект)
			|ГДЕ
			|	Отсутствие.Ссылка В (&ОбъектыДоступа)
			|	И СоставСубъектов.ИмяОбластиДелегирования В(&ИменаОбластейЗамещения)
			|	И &СоставСубъектов_ОтборПравообладателей";
		ДокументооборотПраваДоступа.ПодменитьОтборПравообладателейВЗапросе(
			Запрос, "&СоставСубъектов_ОтборПравообладателей", "СоставСубъектов", СотрудникиОтбор, ПользователиОтбор);
		#КонецОбласти
	
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.СамочувствиеСотрудника") Тогда
		
		#Область Запрос_СамочувствиеСотрудника
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СамочувствиеСотрудника.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Сотрудник КАК Сотрудник,
			|	СоставСубъектов.Пользователь КАК Пользователь,
			|	ИСТИНА КАК Чтение,
			|	ВЫБОР
			|		КОГДА СамочувствиеСотрудника.Сотрудник = СоставСубъектов.Сотрудник
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК Добавление,
			|	ВЫБОР
			|		КОГДА СоставСубъектов.Субъект ЕСТЬ NULL 
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ КАК Изменение,
			|	ЛОЖЬ КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Документ.СамочувствиеСотрудника КАК СамочувствиеСотрудника
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО (СамочувствиеСотрудника.Сотрудник = СоставСубъектов.Субъект)
			|ГДЕ
			|	СамочувствиеСотрудника.Ссылка В (&ОбъектыДоступа)
			|	И СоставСубъектов.ИмяОбластиДелегирования В (&ИменаОбластейЗамещения)
			|	И &СоставСубъектов_ОтборПравообладателей";
		ДокументооборотПраваДоступа.ПодменитьОтборПравообладателейВЗапросе(
			Запрос, "&СоставСубъектов_ОтборПравообладателей", "СоставСубъектов", СотрудникиОтбор, ПользователиОтбор);
		#КонецОбласти
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ТерриторииИПомещения") Тогда
		
		#Область Запрос_ТерриторииИПомещения
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТерриторииИПомещения.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Сотрудник КАК Сотрудник,
			|	СоставСубъектов.Пользователь КАК Пользователь,
			|	ИСТИНА КАК Чтение,
			|	ЛОЖЬ КАК Добавление,
			|	ВЫБОР
			|		КОГДА СоставСубъектов.Субъект ЕСТЬ NULL 
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ КАК Изменение,
			|	ЛОЖЬ КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Справочник.ТерриторииИПомещения КАК ТерриторииИПомещения
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО ТерриторииИПомещения.Ответственный = СоставСубъектов.Субъект
			|ГДЕ
			|	ТерриторииИПомещения.Ссылка В (&ОбъектыДоступа)
			|	И СоставСубъектов.ИмяОбластиДелегирования В (&ИменаОбластейЗамещения)
			|	И &СоставСубъектов_ОтборПравообладателей";
		ДокументооборотПраваДоступа.ПодменитьОтборПравообладателейВЗапросе(
			Запрос, "&СоставСубъектов_ОтборПравообладателей", "СоставСубъектов", СотрудникиОтбор, ПользователиОтбор);
		#КонецОбласти
		
	ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.Бронь") Тогда
		
		#Область Запрос_Бронь
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Бронь.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Сотрудник КАК Сотрудник,
			|	СоставСубъектов.Пользователь КАК Пользователь,
			|	ИСТИНА КАК Чтение,
			|	Бронь.Автор = СоставСубъектов.Сотрудник КАК Добавление,
			|	НЕ СоставСубъектов.Субъект ЕСТЬ NULL КАК Изменение,
			|	ЛОЖЬ КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Документ.Бронь КАК Бронь
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО Бронь.Автор = СоставСубъектов.Субъект
			|ГДЕ
			|	Бронь.Ссылка В (&ОбъектыДоступа)
			|	И СоставСубъектов.ИмяОбластиДелегирования В(&ИменаОбластейЗамещения)
			|	И &СоставСубъектов_ОтборПравообладателей";
		ДокументооборотПраваДоступа.ПодменитьОтборПравообладателейВЗапросе(
			Запрос, "&СоставСубъектов_ОтборПравообладателей", "СоставСубъектов", СотрудникиОтбор, ПользователиОтбор);
		#КонецОбласти
		
	ИначеЕсли ДокументооборотПраваДоступаЛокализация.ЗапросПравОднотипныхОбъектовПодлежитЛокализации(
		ПервыйОбъектДоступа, ТипОбъекта) Тогда
		
		ДокументооборотПраваДоступаЛокализация.ЗаполнитьЗапросПравОднотипныхОбъектов(ПервыйОбъектДоступа,
			Запрос, МетаданныеОбъекта, ТипОбъекта);
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ПоказателиПроцессов") Тогда
		
		#Область Запрос_ПоказателиПроцессов
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПоказателиПроцессов.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Сотрудник КАК Сотрудник,
			|	СоставСубъектов.Пользователь КАК Пользователь,
			|	ИСТИНА КАК Чтение,
			|	ПоказателиПроцессов.Автор = СоставСубъектов.Сотрудник КАК Добавление,
			|	НЕ СоставСубъектов.Субъект ЕСТЬ NULL КАК Изменение,
			|	ЛОЖЬ КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ИЗ
			|	Справочник.ПоказателиПроцессов КАК ПоказателиПроцессов
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО ПоказателиПроцессов.Автор = СоставСубъектов.Субъект
			|ГДЕ
			|	ПоказателиПроцессов.Ссылка В (&ОбъектыДоступа)
			|	И СоставСубъектов.ИмяОбластиДелегирования В(&ИменаОбластейЗамещения)
			|	И &СоставСубъектов_ОтборПравообладателей";
		ДокументооборотПраваДоступа.ПодменитьОтборПравообладателейВЗапросе(
			Запрос, "&СоставСубъектов_ОтборПравообладателей", "СоставСубъектов", СотрудникиОтбор, ПользователиОтбор);
		#КонецОбласти
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда
		
		#Область Запрос_ПроектныеЗадачи
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПроектныеЗадачи.Ссылка КАК ОбъектДоступа,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь,
			|	ИСТИНА КАК Чтение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Добавление) КАК Добавление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Изменение) КАК Изменение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Удаление) КАК Удаление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами) КАК УправлениеПравами
			|ИЗ
			|	Справочник.ПроектныеЗадачи КАК ПроектныеЗадачи
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|		ПО ПроектныеЗадачи.Владелец = ДескрипторыДляОбъектов.Объект
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|		ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|ГДЕ
			|	ПроектныеЗадачи.Ссылка В (&ОбъектыДоступа)
			|	И &ДопУсловия
			|	И &ПраваПоДескрипторам_ОтборПравообладателей
			|
			|СГРУППИРОВАТЬ ПО
			|	ПроектныеЗадачи.Ссылка,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь";
		
		ДопНастройки = ДокументооборотПраваДоступа.ПолучитьДопНастройкиДляПолученияПравПоОбъекту(ТипОбъекта);
		ДопУсловия = "ИСТИНА";
		Если Не ДопНастройки.ДобавлятьРуководителейИДелегатов Тогда
			ДопУсловия = "ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование = Неопределено";
		КонецЕсли;
		ЗаменяемыеПодстроки.Вставить("&ДопУсловия", ДопУсловия);
		#КонецОбласти
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ПротоколыМероприятий") Тогда
		
		#Область Запрос_ПротоколыМероприятий
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПротоколыМероприятий.Ссылка КАК ОбъектДоступа,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь,
			|	ИСТИНА КАК Чтение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Добавление) КАК Добавление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Изменение) КАК Изменение,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Удаление) КАК Удаление,
			|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами) КАК УправлениеПравами
			|ИЗ
			|	Справочник.ПротоколыМероприятий КАК ПротоколыМероприятий
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|		ПО ПротоколыМероприятий.Владелец = ДескрипторыДляОбъектов.Объект
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|		ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|ГДЕ
			|	ПротоколыМероприятий.Ссылка В (&ОбъектыДоступа)
			|	И &ДопУсловия
			|	И &ПраваПоДескрипторам_ОтборПравообладателей
			|
			|СГРУППИРОВАТЬ ПО
			|	ПротоколыМероприятий.Ссылка,
			|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
			|	ПраваПоДескрипторамДоступаОбъектов.Пользователь";
		
		ДопНастройки = ДокументооборотПраваДоступа.ПолучитьДопНастройкиДляПолученияПравПоОбъекту(ТипОбъекта);
		ДопУсловия = "ИСТИНА";
		Если Не ДопНастройки.ДобавлятьРуководителейИДелегатов Тогда
			ДопУсловия = "ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование = Неопределено";
		КонецЕсли;
		ЗаменяемыеПодстроки.Вставить("&ДопУсловия", ДопУсловия);
		#КонецОбласти
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.МестаХраненияДел") Тогда
		
		#Область Запрос_МестаХраненияДел
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВложенныйЗапрос.ОбъектДоступа,
			|	ВложенныйЗапрос.Сотрудник,
			|	ВложенныйЗапрос.Пользователь,
			|	МАКСИМУМ(ВложенныйЗапрос.Чтение) КАК Чтение,
			|	МАКСИМУМ(ВложенныйЗапрос.Добавление) КАК Добавление,
			|	МАКСИМУМ(ВложенныйЗапрос.Изменение) КАК Изменение,
			|	МАКСИМУМ(ВложенныйЗапрос.Удаление) КАК Удаление,
			|	МАКСИМУМ(ВложенныйЗапрос.УправлениеПравами) КАК УправлениеПравами
			|ИЗ
			|(
			|	ВЫБРАТЬ
			|		МестаХраненияДел.Ссылка КАК ОбъектДоступа,
			|		ПраваПоДескрипторамДоступаОбъектов.Сотрудник КАК Сотрудник,
			|		ПраваПоДескрипторамДоступаОбъектов.Пользователь КАК Пользователь,
			|		ПраваПоДескрипторамДоступаОбъектов.Чтение КАК Чтение,
			|		ПраваПоДескрипторамДоступаОбъектов.Добавление КАК Добавление,
			|		ПраваПоДескрипторамДоступаОбъектов.Изменение КАК Изменение,
			|		ПраваПоДескрипторамДоступаОбъектов.Удаление КАК Удаление,
			|		ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами КАК УправлениеПравами
			|	ИЗ
			|		Справочник.МестаХраненияДел КАК МестаХраненияДел
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|			ПО МестаХраненияДел.Организация = ДескрипторыДляОбъектов.Объект
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
			|			ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
			|			
			|	ГДЕ
			|		МестаХраненияДел.Ссылка В (&ОбъектыДоступа)
			|		И &ПраваПоДескрипторам_ОтборПравообладателей
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		МестаХраненияДел.Ссылка,
			|		СотрудникиВКонтейнерах.Сотрудник,
			|		СотрудникиПользователей.Пользователь,
			|		ИСТИНА,
			|		ПраваРолей.Добавление,
			|		ПраваРолей.Изменение,
			|		ПраваРолей.Изменение,
			|		ЛОЖЬ
			|	ИЗ
			|		Справочник.МестаХраненияДел КАК МестаХраненияДел,
			|		РегистрСведений.ПолномочияСотрудников КАК ПолномочияСотрудников
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.Роли КАК ПрофилиГруппДоступаРоли
			|			ПО ПолномочияСотрудников.Полномочия = ПрофилиГруппДоступаРоли.Ссылка
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваРолей КАК ПраваРолей
			|			ПО ПрофилиГруппДоступаРоли.Роль = ПраваРолей.Роль
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
			|			ПО ПолномочияСотрудников.Владелец = СотрудникиВКонтейнерах.Контейнер
			|			ЛЕВОЕ СОЕДИНЕНИЕ
			|			РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|			ПО СотрудникиПользователей.Сотрудник = СотрудникиВКонтейнерах.Сотрудник
			|	ГДЕ
			|		ПраваРолей.ОбъектМетаданных = &ИдентификаторОМ
			|		И (МестаХраненияДел.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
			|				ИЛИ ЛОЖЬ В
			|					(ВЫБРАТЬ
			|						ИспользоватьУчетПоОрганизациям.Значение
			|					ИЗ
			|						Константа.ИспользоватьУчетПоОрганизациям КАК ИспользоватьУчетПоОрганизациям))
			|		И &КонтейнерыИлиСотрудникиПользователей_ОтборПравообладателей
			|) КАК ВложенныйЗапрос
			|
			|СГРУППИРОВАТЬ ПО
			|	ВложенныйЗапрос.ОбъектДоступа,
			|	ВложенныйЗапрос.Сотрудник,
			|	ВложенныйЗапрос.Пользователь";
		Если СотрудникиОтбор <> Неопределено Тогда
			ОбщегоНазначенияДокументооборот.ПодменитьОтборПоПолюВЗапросе(
				Запрос, "&КонтейнерыИлиСотрудникиПользователей_ОтборПравообладателей",
				"СотрудникиВКонтейнерах.Сотрудник", "СотрудникиОтбор", СотрудникиОтбор);
		ИначеЕсли ПользователиОтбор <> Неопределено Тогда
			ОбщегоНазначенияДокументооборот.ПодменитьОтборПоПолюВЗапросе(
				Запрос, "&КонтейнерыИлиСотрудникиПользователей_ОтборПравообладателей",
				"СотрудникиПользователей.Пользователь", "ПользователиОтбор", ПользователиОтбор);
		Иначе
			Запрос.Текст = СтрЗаменить(
				Запрос.Текст, "&КонтейнерыИлиСотрудникиПользователей_ОтборПравообладателей", "ИСТИНА");
		КонецЕсли;
		#КонецОбласти
		
	КонецЕсли;
	
	Возврат Запрос;
	
КонецФункции

// Переопределяемая часть обработчика подписки ДокументооборотПередЗаписьюПравообразующихОбъектов
Функция ДокументооборотПередЗаписьюПравообразующихОбъектов(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Проверка на то, что изменились правообразующие реквизиты
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.ЗамещающиеИПомощники") Тогда
		
		Если Источник.ЭтоНовый() Тогда
			Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
		Иначе
			
			РеквизитыСсылки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				Источник.Ссылка, "Сотрудник, Замещающий, ПометкаУдаления, Действует");
			
			СтарыеОбластиДелегирования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				Источник.Ссылка, "ВопросыЗамещения").Выгрузить();
			
			Если Источник.Сотрудник <> РеквизитыСсылки.Сотрудник
			 Или Источник.Замещающий <> РеквизитыСсылки.Замещающий
			 Или Источник.ПометкаУдаления <> РеквизитыСсылки.ПометкаУдаления 
			 Или Источник.Действует <> РеквизитыСсылки.Действует 
			 Или ОбщегоНазначенияДокументооборотКлиентСервер.ЕстьОтличияВТаблицах(
				 Источник.ВопросыЗамещения, СтарыеОбластиДелегирования, "Область") Тогда 
				
				Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
				
				Если Источник.Сотрудник <> РеквизитыСсылки.Сотрудник Тогда
					Источник.ДополнительныеСвойства.Вставить("СтарыйСотрудник", РеквизитыСсылки.Сотрудник);
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		Возврат Истина;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникОбъект.ОбластиЗамещения") Тогда
		
		Если Источник.ЭтоНовый()
		 Или Источник.ПометкаУдаления <> Источник.Ссылка.ПометкаУдаления Тогда 
		 
		 	Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
			
		Иначе	
			
			Если Источник.Ссылка.Состав.Количество() <> Источник.Состав.Количество() Тогда 
				Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
			Иначе	
				Для Каждого Строка Из Источник.Ссылка.Состав Цикл
					СтруктураОтбора = Новый Структура("ОбъектМетаданных", Строка.ОбъектМетаданных);
					НайденныеСтроки = Источник.Состав.НайтиСтроки(СтруктураОтбора);
					Если НайденныеСтроки.Количество() = 0 Тогда 
						Источник.ДополнительныеСвойства.Вставить("ЗначениеИзменено", Истина);
						Прервать;
					КонецЕсли;
				КонецЦикла;	
			КонецЕсли;	
			
		КонецЕсли;
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь; // Не обработано
	
КонецФункции

// Переопределяемая часть обработчика подписки ДокументооборотПередЗаписьюПравообразующихОбъектов
Функция ДокументооборотПриЗаписиПравообразующихОбъектов(Источник, Отказ) Экспорт
	
	Возврат Ложь; // Не обработано
	
КонецФункции

Функция ОбновитьПромежуточныеДанныеДляРасчетаПрав(Источник, ТипИсточника) Экспорт
	
	Если ТипИсточника = Тип("СправочникОбъект.Проекты") Тогда
		
		РегистрыСведений.СоставСубъектовПравДоступа.ЗаписьПроекта(Источник);
		
		Возврат Истина;
		
	ИначеЕсли ТипИсточника = Тип("СправочникОбъект.ОбластиЗамещения") Тогда
		
		РегистрыСведений.СоставСубъектовПравДоступа.ЗаписьОбластиЗамещения(Источник);
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь; // Не обработано
	
КонецФункции

// Переопределяемая часть обработчика подписки ДокументооборотПраваДоступаПриЗаписиПравообразующихКонстант
Функция ДокументооборотПраваДоступаПриЗаписиПравообразующихКонстант(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипЗнч(Источник) = Тип("КонстантаМенеджерЗначения.ВестиУчетПоПроектам") Тогда
		
		// Обработка всех дескрипторов, у которых установлен реквизит Проект
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДескрипторыДоступаОбъектов.Ссылка
			|ИЗ
			|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
			|ГДЕ
			|	ДескрипторыДоступаОбъектов.Проект <> &ПустойПроект";
		
		Запрос.УстановитьПараметр("ПустойПроект", Справочники.Проекты.ПустаяСсылка());
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Дескриптор = Выборка.Ссылка;
			Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Дескриптор);
		КонецЦикла;
		
		Возврат Истина;
		
	КонецЕсли;	
	
	Возврат Ложь;
	
КонецФункции

// Переопределяемая часть метода  ДокументооборотПраваДоступа.ОбновитьПраваСвязанныхДескрипторовПоОбъекту
Процедура ОбновитьПраваСвязанныхДескрипторовПоОбъекту(Объект) Экспорт
	
КонецПроцедуры

Функция ДокументооборотПриЗаписиПравообразующихРегистров(Источник, Отказ) Экспорт 
	
	Если ТипЗнч(Источник) = Тип("РегистрСведенийНаборЗаписей.УчастникиМероприятия") Тогда
		
		Если Источник.ДополнительныеСвойства.Свойство("ПропуститьОпределениеЗависимыхПрав")
			И Источник.ДополнительныеСвойства.ПропуститьОпределениеЗависимыхПрав Тогда
			Возврат Истина;
		КонецЕсли;
		
		Мероприятие = Источник.Отбор.Мероприятие.Значение;
		Если ЗначениеЗаполнено(Мероприятие) Тогда
			МероприятиеОбъект = Мероприятие.ПолучитьОбъект();
			Если МероприятиеОбъект <> Неопределено Тогда // Проверка на удаление мероприятия
				ДокументооборотПраваДоступа.ОпределитьДескрипторыОбъекта(МероприятиеОбъект);
			КонецЕсли;
		КонецЕсли;
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#Область ТипыСЗависимымиПравами

// Если переопределять список типов, то переопределять обе эти процедуры синхронно:

// См. ДокументооборотПраваДоступаПовтИсп.ОбъектыСЗависимымиПравами
Функция ОбъектыСЗависимымиПравами() Экспорт
	
	Возврат ДокументооборотПраваДоступаПовтИсп.ОбъектыСЗависимымиПравами();
	
КонецФункции

// См. ДокументооборотПраваДоступаПовтИсп.ТипыОбъектовСЗависимымиПравами
Функция ТипыОбъектовСЗависимымиПравами() Экспорт
	
	Возврат ДокументооборотПраваДоступаПовтИсп.ТипыОбъектовСЗависимымиПравами();
	
КонецФункции

#КонецОбласти // ТипыСЗависимымиПравами

// Переопределяемая часть функции ДокументооборотПраваДоступаПовтИсп.ТипыПапокИСодержимого
//
Процедура ДополнитьТипыПапокИСодержимого(Результат) Экспорт
	
	// Проекты
	МассивМетаданныхСодержимого = Новый Массив;
	МассивМетаданныхСодержимого.Добавить(Метаданные.Справочники.Проекты);
	Результат.Вставить(Тип("СправочникСсылка.ПапкиПроектов"), МассивМетаданныхСодержимого);
	
	// Мероприятия
	МассивМетаданныхСодержимого = Новый Массив;
	МассивМетаданныхСодержимого.Добавить(Метаданные.Справочники.Мероприятия);
	Результат.Вставить(Тип("СправочникСсылка.ПапкиМероприятий"), МассивМетаданныхСодержимого);
	
	// Письма
	МассивМетаданныхСодержимого = Новый Массив;
	МассивМетаданныхСодержимого.Добавить(Метаданные.Документы.ВходящееПисьмо);
	МассивМетаданныхСодержимого.Добавить(Метаданные.Документы.ИсходящееПисьмо);
	Результат.Вставить(Тип("СправочникСсылка.ПапкиПисем"), МассивМетаданныхСодержимого);
	
	// Темы обсуждений
	МассивМетаданныхСодержимого = Новый Массив;
	МассивМетаданныхСодержимого.Добавить(Метаданные.Справочники.ТемыОбсуждений);
	Результат.Вставить(Тип("СправочникСсылка.ПапкиФорума"), МассивМетаданныхСодержимого);
	
КонецПроцедуры

// Переопределяемая часть метода
//
Процедура ОбновитьПраваЗависящиеОтСоставаУчастниковПроцесса(МассивПроцессов) Экспорт
	
КонецПроцедуры

// Обновляет права объектов, которые зависят от прав на файл.
// 
// Параметры:
// 	ФайлСсылка - СправочникСсылка - ссылка на файл.
// 
Процедура ОбновитьПраваЗависящиеОтФайла(ФайлСсылка) Экспорт
	
	// Права на темы форума, созданные на основании файла.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ТемыОбсуждений.Ссылка
		|ИЗ
		|	Справочник.ТемыОбсуждений КАК ТемыОбсуждений
		|ГДЕ
		|	ТемыОбсуждений.Документ = &Файл");
		
	Запрос.УстановитьПараметр("Файл", ФайлСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДокументооборотПраваДоступа.ОпределитьДескрипторыОбъекта(Выборка.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

// Заполняет структуру настроек, которые будут использоваться при получении прав объекта
//
// Параметры:
//	ДопНастройки - Структура - структура настроек
//  ТипОбъекта - Тип - тип объекта доступа
//  ОбъектДоступа - Ссылка - объект доступа
//  
Процедура ЗаполнитьДопНастройкиДляПолученияПравПоОбъекту(ДопНастройки, ТипОбъекта) Экспорт
	
	// Исключения
	
	Если ТипОбъекта = Тип("СправочникСсылка.Инструкции") Тогда
		ДопНастройки.ДобавлятьРуководителейИДелегатов = Ложь;
	КонецЕсли;
	
	Если ТипОбъекта = Тип("СправочникСсылка.ГруппыКонтактовПользователей") Тогда
		ДопНастройки.ДобавлятьРуководителейИДелегатов = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Для внутреннего использования
//
Процедура ДополнитьОписанияТаблицСоСсылкамиНаКонтрагентов(ОписанияТаблиц) Экспорт
	
	ОписанияТаблиц.Удалить("Справочник.ДокументыПредприятия");
	ОписанияТаблиц.Удалить("Справочник.ШаблоныДокументов");
	
	ОписанияТаблиц.Вставить("Справочник.ДокументыПредприятия.Контрагенты", "Контрагент");
	ОписанияТаблиц.Вставить("Справочник.ШаблоныДокументов.Контрагенты", "Контрагент");
	
КонецПроцедуры

// Возвращает таблицу видов доступа, в соответствии с текущими настройками.
// 
// Параметры:
//  ТолькоАктуальные - Булево - если Истина, то в результат будут включены только те виды доступа,
//   которые используются для расчета прав и не отключены по функциональным опциям.
// 
// Возвращаемое значение: Таблица значений.
// 
Функция ТаблицаРазрезовДоступа(ТолькоАктуальные = Истина) Экспорт
	
	ТаблицаРазрезовДоступа = Новый ТаблицаЗначений;
	ТаблицаРазрезовДоступа.Колонки.Добавить("Имя");
	ТаблицаРазрезовДоступа.Колонки.Добавить("ВидДоступа");
	ТаблицаРазрезовДоступа.Колонки.Добавить("ИмяТаблицыЗначенийДоступа");
	ТаблицаРазрезовДоступа.Колонки.Добавить("ИмяТЧДескриптора");
	ТаблицаРазрезовДоступа.Колонки.Добавить("ИмяПоляТаблицыДескриптора");
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "ВидыДокументов";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.ВидыДокументов;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.ВидыДокументов.ПолноеИмя();
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "ВидыМероприятий";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.ВидыМероприятий;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.ВидыМероприятий.ПолноеИмя();
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "ВопросыДеятельности";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.ВопросыДеятельности;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.ВопросыДеятельности.ПолноеИмя();
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "ГрифыДоступа";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.ГрифыДоступа;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.ГрифыДоступа.ПолноеИмя();
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "Организации";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.Организации;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.Организации.ПолноеИмя();
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "Подразделения";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.Подразделения;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.СтруктураПредприятия.ПолноеИмя();
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "ГруппыДоступаКонтрагентов";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.ГруппыДоступаКонтрагентов;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.ГруппыДоступаКонтрагентов.ПолноеИмя();
	
	Стр = ТаблицаРазрезовДоступа.Добавить();
	Стр.Имя = "ГруппыДоступаФизическихЛиц";
	Стр.ВидДоступа = ПланыВидовХарактеристик.ВидыДоступа.ГруппыДоступаФизическихЛиц;
	Стр.ИмяТаблицыЗначенийДоступа = Метаданные.Справочники.ГруппыДоступаФизическихЛиц.ПолноеИмя();
	
	Если ТолькоАктуальные Тогда
		ОтключенныеРазрезы = ДокументооборотПраваДоступаПовтИсп.ОтключенныеРазрезыДоступа();
		Для Каждого Разрез Из ОтключенныеРазрезы Цикл
			Стр = ТаблицаРазрезовДоступа.Найти(Разрез, "ВидДоступа");
			Если Стр <> Неопределено Тогда
				ТаблицаРазрезовДоступа.Удалить(Стр);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ИменаРеквизитов = ДокументооборотПраваДоступаПовтИсп.ИменаРеквизитовДескрпитораПоРазрезамДоступа();
	Для Каждого Стр Из ТаблицаРазрезовДоступа Цикл
		ИмяРеквизитаДескриптора = ИменаРеквизитов.Получить(Стр.ВидДоступа);
 		НайденнаяПозиция = СтрНайти(ИмяРеквизитаДескриптора, ".");
		Если НайденнаяПозиция = 0 Тогда
			Стр.ИмяПоляТаблицыДескриптора = ИмяРеквизитаДескриптора;
		Иначе
			Стр.ИмяТЧДескриптора = Лев(ИмяРеквизитаДескриптора, НайденнаяПозиция - 1);
			Стр.ИмяПоляТаблицыДескриптора = Сред(ИмяРеквизитаДескриптора, НайденнаяПозиция + 1);
		КонецЕсли;
 	КонецЦикла;
	
	Возврат ТаблицаРазрезовДоступа;
	
КонецФункции

// Возвращает соответствие разрезов доступа и имен реквизитов Дескриптора.
//
Функция ИменаРеквизитовДескрпитораПоРазрезамДоступа() Экспорт
	
	Результат = Новый Соответствие;
	МенеджерВидовДоступа = ПланыВидовХарактеристик.ВидыДоступа;
	
	Результат.Вставить(МенеджерВидовДоступа.ВидыДокументов, "ВидОбъекта");
	Результат.Вставить(МенеджерВидовДоступа.ВидыМероприятий, "ВидОбъекта");
	Результат.Вставить(МенеджерВидовДоступа.ВопросыДеятельности, "ВопросДеятельности");
	Результат.Вставить(МенеджерВидовДоступа.ГрифыДоступа, "ГрифДоступа");
	Результат.Вставить(МенеджерВидовДоступа.Организации, "Организация");
	Результат.Вставить(МенеджерВидовДоступа.Подразделения, "Подразделение");
	
	Результат.Вставить(МенеджерВидовДоступа.ГруппыДоступаКонтрагентов, "Контрагенты.ГруппаДоступа");
	Результат.Вставить(МенеджерВидовДоступа.ГруппыДоступаФизическихЛиц, "ФизическиеЛица.ГруппаДоступа");
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив неиспользуемых реквизитов дескриптора.
// 
// Параметры:
//  ИдентификаторОбъектаМетаданных - СправочникСсылка.ИдентификаторыОбъектовМетаданных - Значение одноименного реквизита
//  																					 дескриптора.
// 
// Возвращаемое значение:
//  Массив Из Строка - список имен неиспользуемых реквизитов.
// 
Функция НеиспользуемыеРеквизитыДескриптора(ИдентификаторОбъектаМетаданных) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Массив;
	
	ОтключенныеРазрезы = ДокументооборотПраваДоступаПовтИсп.ОтключенныеРазрезыДоступа(Ложь);
	ИменаРеквизитов = ДокументооборотПраваДоступаПовтИсп.ИменаРеквизитовДескрпитораПоРазрезамДоступа();
	
	Для Каждого Разрез Из ОтключенныеРазрезы Цикл
		
		ИмяРеквизитаДескриптора = ИменаРеквизитов.Получить(Разрез);
		Если ИмяРеквизитаДескриптора = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИмяРеквизитаДескриптора = "ВидОбъекта" Тогда
			// Проверяется, что виды отключены именно для текущего объекта.
			ИдентификаторыОбъектовСОтключеннымВидом = Новый Массив;
			Если Разрез = ПланыВидовХарактеристик.ВидыДоступа.ВидыДокументов Тогда
				ИдентификаторыОбъектовСОтключеннымВидом.Добавить(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.ДокументыПредприятия));
				ИдентификаторыОбъектовСОтключеннымВидом.Добавить(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.ВидыДокументов));
				ИдентификаторыОбъектовСОтключеннымВидом.Добавить(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.ШаблоныДокументов));
			ИначеЕсли Разрез = ПланыВидовХарактеристик.ВидыДоступа.ВидыМероприятий Тогда
				ИдентификаторыОбъектовСОтключеннымВидом.Добавить(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.Мероприятия));
				ИдентификаторыОбъектовСОтключеннымВидом.Добавить(
					ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.ВидыМероприятий));
			КонецЕсли;
			Если ИдентификаторыОбъектовСОтключеннымВидом.Найти(ИдентификаторОбъектаМетаданных) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		// Если получен реквизит ТЧ, добавляется только имя ТЧ.
		НайденнаяПозиция = СтрНайти(ИмяРеквизитаДескриптора, ".");
		Если НайденнаяПозиция > 0 Тогда
			ИмяРеквизитаДескриптора = Лев(ИмяРеквизитаДескриптора, НайденнаяПозиция - 1);
		КонецЕсли;
		
		Результат.Добавить(ИмяРеквизитаДескриптора);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Определяет поле, по которому упорядочивается таблица данного объекта метаданных.
// На внедрении, в случае создания сущности, где потенциально может быть очень много объектов - есть смысл добавлять
// свои типы и их поля сюда.
// 
// Параметры:
//  ПолноеИмяОМ - Строка - Например "Справочник.ДокументыПредприятия", т.е. как задано в справочнике
//  					   ИдентификаторыОбъектовМетаданных.
// 
// Возвращаемое значение:
//  Строка - Имя поля. Пустая строка, если не нужно упорядочивать.
Функция ПолеУпорядочиванияОбъектаМетаданных(ПолноеИмяОМ) Экспорт
	
	Если СтрНачинаетсяС(ПолноеИмяОМ, "Документ") Тогда
		
		Возврат "Дата"; // Во всех документах есть дата.
		
	ИначеЕсли ПолноеИмяОМ = "Справочник.ДокументыПредприятия"
		Или ПолноеИмяОМ = "Справочник.СообщенияОбсуждений"
		Или ПолноеИмяОМ = "Справочник.ТемыОбсуждений"
		Или ПолноеИмяОМ = "Справочник.ПапкиФайлов"
		Или ПолноеИмяОМ = "Справочник.ПапкиПроектов"
		Или ПолноеИмяОМ = "Справочник.ПапкиДокументов"
		Или ПолноеИмяОМ = "Справочник.ПапкиМероприятий"
		Или ПолноеИмяОМ = "Справочник.Мероприятия"
		Или ПолноеИмяОМ = "Справочник.КонтрольныеТочки"
		Или ПолноеИмяОМ = "Справочник.ПапкиФорума"
		Или ПолноеИмяОМ = "Справочник.КатегорииДанных" Тогда
		
		Возврат "ДатаСоздания";
		
	ИначеЕсли ПолноеИмяОМ = "Справочник.ЗаписиРабочегоКалендаря" Тогда
		
		Возврат "ДатаНачала";
		
	ИначеЕсли ПолноеИмяОМ = "Справочник.Проекты" Тогда
		
		Возврат "ТекущийПланНачало";
		
	ИначеЕсли ПолноеИмяОМ = "Справочник.ШаблоныЗадач" Тогда
		
		Возврат "ДатаИзменения";
		
	ИначеЕсли ПолноеИмяОМ = "Справочник.ФизическиеЛица" Тогда
		
		Возврат "ДатаРождения";
		
	КонецЕсли;
	
	Возврат ""; // Признак, что упорядочивать таблицу не надо.
	
КонецФункции

// Возвращает таблицу объектов, которые могут иметь отдельные разрешения доступа.
// 
// Возвращаемое значение: ТаблицаЗначений
//   * ОбъектМетаданных - СправочникСсылка.ИдентификаторыОбъектовМетаданных - идентификатор объекта.
//   * ВидыДоступа - СписокЗначений - виды доступа, которые влияют на объект.
//   * НеЯвляетсяСамостоятельнымПредметомДоступа - Булево - признак того, что 
//     для объекта нет смысла использовать исключения из общих разрешений доступа.
//
Функция ПредметыДоступаСРазрезами() Экспорт
	
	ТаблицаСвойств = Новый ТаблицаЗначений;
	ТаблицаСвойств.Колонки.Добавить("ОбъектМетаданных", Новый ОписаниеТипов("СправочникСсылка.ИдентификаторыОбъектовМетаданных"));
	ТаблицаСвойств.Колонки.Добавить("ВидыДоступа", Новый ОписаниеТипов("СписокЗначений"));
	ТаблицаСвойств.Колонки.Добавить("НеЯвляетсяСамостоятельнымПредметомДоступа", Новый ОписаниеТипов("Булево"));
	
	// Документы предприятия
	Стр = ТаблицаСвойств.Добавить();
	Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Справочники.ДокументыПредприятия);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВидыДокументов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВопросыДеятельности);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГрифыДоступа);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГруппыДоступаКонтрагентов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Организации);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Подразделения);
	
	// Контрагенты
	Стр = ТаблицаСвойств.Добавить();
	Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Справочники.Контрагенты);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГруппыДоступаКонтрагентов);
	Стр.НеЯвляетсяСамостоятельнымПредметомДоступа = Истина;
	
	// Мероприятия
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеМероприятиями") Тогда
		Стр = ТаблицаСвойств.Добавить();
		Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
			Метаданные.Справочники.Мероприятия);
		Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВидыМероприятий);
		Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГрифыДоступа);
		Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Организации);
		Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Подразделения);
	КонецЕсли;
	
	// Проекты
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда
		Стр = ТаблицаСвойств.Добавить();
		Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
			Метаданные.Справочники.Проекты);
		Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГрифыДоступа);
		Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Организации);
		Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Подразделения);
	КонецЕсли;
	
	// Физические лица
	Стр = ТаблицаСвойств.Добавить();
	Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Справочники.ФизическиеЛица);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГруппыДоступаФизическихЛиц);
	Стр.НеЯвляетсяСамостоятельнымПредметомДоступа = Истина;
	
	// Шаблоны документов
	Стр = ТаблицаСвойств.Добавить();
	Стр.ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Справочники.ШаблоныДокументов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВидыДокументов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ВопросыДеятельности);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГрифыДоступа);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.ГруппыДоступаКонтрагентов);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Организации);
	Стр.ВидыДоступа.Добавить(ПланыВидовХарактеристик.ВидыДоступа.Подразделения);
	Стр.НеЯвляетсяСамостоятельнымПредметомДоступа = Истина;
	
	Возврат ТаблицаСвойств;
	
КонецФункции

// Возвращает идентификатор объекта,
// по которому нужно искать исключительные разрешения при расчете прав переданного объекта.
// 
Функция ИдентификаторДляПроверкиПолитикДоступа(ИдОбъекта, МетаданныеОбъекта) Экспорт
	
	Результат = ИдОбъекта;
	
	// Исключения
	Если МетаданныеОбъекта = Метаданные.Справочники.ШаблоныДокументов Тогда
		Результат = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.ДокументыПредприятия);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив неиспользуемых разрезов доступа.
//
// Параметры:
//  ОтключенныеПоФункциональнымОпциям - Булево - включать разрезы, отключенные по функциональным опциям.
//  ОтключенныеВручную - Булево - включать разрезы, отключенные вручную.
//
// Возвращаемое значение:
//  Массив - массив отключенных разрезов доступа.
//
Функция ОтключенныеРазрезыДоступа(ОтключенныеПоФункциональнымОпциям, ОтключенныеВручную) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Массив;
	МенеджерРазрезов = ПланыВидовХарактеристик.ВидыДоступа;
	СписокРазрезов = Константы.ИспользованиеРазрезовДоступа.Получить().Получить();
	
	Если СписокРазрезов = Неопределено Тогда
		МенеджерЗначения = Константы.ИспользованиеРазрезовДоступа.СоздатьМенеджерЗначения();
		МенеджерЗначения.НачальноеЗаполнение();
		СписокРазрезов = Константы.ИспользованиеРазрезовДоступа.Получить().Получить();
	КонецЕсли;
	
	Для Каждого Разрез Из СписокРазрезов Цикл
		
		Если ОтключенныеВручную Тогда
			Если Не Разрез.Пометка Тогда
				Результат.Добавить(Разрез.Значение);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если ОтключенныеПоФункциональнымОпциям Тогда
			
			Если Разрез.Значение = МенеджерРазрезов.ВопросыДеятельности
					И Не ПолучитьФункциональнуюОпцию("ИспользоватьВопросыДеятельности")
				Или Разрез.Значение = МенеджерРазрезов.ГрифыДоступа
					И Не ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа")
				Или Разрез.Значение = МенеджерРазрезов.Организации
					И Не ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям")
				Или Разрез.Значение = МенеджерРазрезов.ВидыМероприятий
					И Не ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеМероприятиями") Тогда
				
				Результат.Добавить(Разрез.Значение);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
