
#Область СлужебныеПроцедурыИФункции

#Область КлючиПриглашений

Функция КлючПриглашения() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторОрганизации", "");
	Результат.Вставить("ИдентификаторКонтрагента", "");
	Результат.Вставить("ИНН", "");
	Результат.Вставить("КПП", "");
	
	Возврат Результат;
	
КонецФункции

Функция КлючПриглашенияНаИдентификатор() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Тип", ПредопределенноеЗначение("Перечисление.ТипыПриглашений.НаИдентификатор"));
	Результат.Вставить("ИдентификаторОрганизации", "");
	Результат.Вставить("ИдентификаторКонтрагента", "");
	
	Возврат Результат;
	
КонецФункции

Функция КлючШироковещательногоПриглашения() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Тип", ПредопределенноеЗначение("Перечисление.ТипыПриглашений.Широковещательное"));
	Результат.Вставить("ИдентификаторОрганизации", "");
	Результат.Вставить("ИНН", "");
	Результат.Вставить("КПП", "");
	
	Возврат Результат;
	
КонецФункции

Функция КлючЗаявкиНаРоуминг() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Тип", ПредопределенноеЗначение("Перечисление.ТипыПриглашений.ЗаявкаНаРоуминг"));
	Результат.Вставить("ИдентификаторОрганизации", "");
	Результат.Вставить("ИдентификаторОператора", "");
	Результат.Вставить("ИНН", "");
	Результат.Вставить("КПП", "");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

Функция ПодсказкаЭлектроннаяПочтаДляУведомленияОСтатусеЗаявкиНаРоуминг() Экспорт
	
	Возврат НСтр("ru = 'На данный адрес электронной почты будут отправляться оповещения о статусе выполнения заявки по настройке роуминга'");
	
КонецФункции

Функция ВидПриглашенияИсходящее() Экспорт
	
	Возврат "Исходящее";
	
КонецФункции

Функция ЭтоВходящееПриглашение(Приглашение) Экспорт
	
	Возврат Приглашение.Вид = ВидПриглашенияВходящее();
	
КонецФункции

Функция ВидПриглашенияВходящее() Экспорт
	
	Возврат "Входящее";
	
КонецФункции

Функция ТипыПриглашений() Экспорт

	Результат = Новый Структура;
	Результат.Вставить("НаИдентификатор", ПредопределенноеЗначение("Перечисление.ТипыПриглашений.НаИдентификатор"));	
	Результат.Вставить("Широковещательное", ПредопределенноеЗначение("Перечисление.ТипыПриглашений.Широковещательное"));	
	Результат.Вставить("ЗаявкаНаРоуминг", ПредопределенноеЗначение("Перечисление.ТипыПриглашений.ЗаявкаНаРоуминг"));	

	Возврат Новый ФиксированнаяСтруктура(Результат);
		
КонецФункции

#Область ОбработкаНеисправностей

// Возвращает вид ошибки, описывающий ситуацию, когда операция не может быть выполнена из-за того, что
// не настроена связь с контрагентом.
// 
// Параметры:
// 	СтатусПриглашения - ПеречислениеСсылка.СтатусыПриглашений
// 	ВидОперации - Строка - вид операции, при выполнении которой произошла ошибка, может 
// 	принимать значения "Распаковка", "Отправка".
// Возвращаемое значение:
//  См. ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки
Функция ВидОшибкиНеНастроенаСвязьСКонтрагентом(СтатусПриглашения, ВидОперации) Экспорт
	
	Если СтатусПриглашения = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.ТребуетсяСогласие") Тогда
		Идентификатор = "НеПринятоПриглашение" + ВидОперации;
		ОписаниеПроблемы = НСтр("ru = 'Не принято приглашение к обмену электронными документами'");
		Если ВидОперации = "Распаковка" Тогда
			ШаблонОписаниеРешения = НСтр("ru = 'Необходимо <a href = ""%1"">принять</a> приглашение.
				|После того как оно будет принято, транспортный контейнер можно будет распаковать.'");
			ОписаниеРешения = СтрШаблон(ШаблонОписаниеРешения, "принять");
		Иначе
			ШаблонОписаниеРешения = НСтр("ru = 'Необходимо <a href = ""%1"">принять</a> приглашение.
				|После того как оно будет принято, документ можно будет отправить.'");
			ОписаниеРешения = СтрШаблон(ШаблонОписаниеРешения, "принять");
		КонецЕсли;
	ИначеЕсли СтатусПриглашения = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.ОжидаемСогласия") Тогда 
		Идентификатор = "ОжидаетсяСогласиеНаПриглашение" + ВидОперации;
		ОписаниеПроблемы = НСтр("ru = 'Приглашение к обмену электронными документами находится в статусе ""Ожидаем согласие""'");
		ШаблонОписаниеРешения = НСтр("ru = 'Уточните у контрагента, принял ли он <a href = ""%1"">приглашение</a>.
					   |Если приглашение принято - <a href = ""%2"">обратитесь</a> в тех. поддержку'");
		ОписаниеРешения = СтрШаблон(ШаблонОписаниеРешения, "приглашение", "обратитесь");
	ИначеЕсли СтатусПриглашения = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.Отклонено")
		Или СтатусПриглашения = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.ТребуетсяОтправить") Тогда 
		Идентификатор = "ТребуетсяОтправкаПриглашения" + ВидОперации;
		ОписаниеПроблемы = НСтр("ru = 'Приглашение к обмену электронными документами еще не было отправлено'");
		Если ВидОперации = "Распаковка" Тогда
			ШаблонОписаниеРешения = НСтр("ru = 'Чтобы продолжить обмен электронными документами с контрагентом, отправьте <a href = ""%1"">приглашение</a>.
					   |После того как оно будет принято, транспортный контейнер можно будет распаковать.'");
			ОписаниеРешения = СтрШаблон(ШаблонОписаниеРешения, "приглашение");
		Иначе
			ШаблонОписаниеРешения = НСтр("ru = 'Чтобы продолжить обмен электронными документами с контрагентом, отправьте <a href = ""%1"">приглашение</a>.
					   |После того как оно будет принято, документ можно будет отправить.'");
			ОписаниеРешения = СтрШаблон(ШаблонОписаниеРешения, "приглашение");
		КонецЕсли;
	ИначеЕсли СтатусПриглашения = ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.Ошибка") Тогда 
		Идентификатор = "ОшибкаПриглашения" + ВидОперации;
		ОписаниеПроблемы = НСтр("ru = 'Приглашение к обмену электронными документами находится в статусе ""Ошибка""'");
		ОписаниеРешения = СтрШаблон(НСтр("ru = '<a href = ""%1"">Обратитесь</a> в тех. поддержку'"), "Обратитесь");
	ИначеЕсли СтатусПриглашения = Неопределено Тогда 
		Идентификатор = "НетПриглашения" + ВидОперации;
		ОписаниеПроблемы = НСтр("ru = 'Не настроена связь с контрагентом'");
		ОписаниеРешения = НСтр("ru = 'Не настроена связь с контрагентом'")
	Иначе
		Возврат ОбработкаНеисправностейБЭДКлиентСервер.ВидОшибкиНеизвестнаяОшибка();
	КонецЕсли;
	
	Если ВидОперации = "Распаковка" Тогда
		ЗаголовокПроблемы = НСтр("ru = 'Не удалось распаковать транспортный контейнер ЭДО'");
	Иначе
		ЗаголовокПроблемы = НСтр("ru = 'Не удалось отправить электронный документ'");
	КонецЕсли;
	
	ОбработчикОткрытияПриглашения = "ПриглашенияЭДОКлиент.ОткрытьПриглашения";
	ОбработчикОткрытияФормыОбращенияВТехподдержку = 
		ОбработкаНеисправностейБЭДКлиентСервер.ОбработчикОткрытияФормыОбращенияВТехподдержку();
	ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки();
	ВидОшибки.Идентификатор = Идентификатор;
	ВидОшибки.ЗаголовокПроблемы = ЗаголовокПроблемы;
	ВидОшибки.ОписаниеПроблемы = ОписаниеПроблемы;
	ВидОшибки.ОписаниеРешения = ОписаниеРешения;
	ВидОшибки.ОбработчикиНажатия.Вставить("принять", ОбработчикОткрытияПриглашения);
	ВидОшибки.ОбработчикиНажатия.Вставить("приглашение", ОбработчикОткрытияПриглашения);
	ВидОшибки.ОбработчикиНажатия.Вставить("Обратитесь",	ОбработчикОткрытияФормыОбращенияВТехподдержку);
	ВидОшибки.ОбработчикиНажатия.Вставить("обратитесь", ОбработчикОткрытияФормыОбращенияВТехподдержку);
	ВидОшибки.ПараметрыОбработчиков.Вставить("принять", Новый Структура("Группировать", Истина));
	ВидОшибки.ПараметрыОбработчиков.Вставить("приглашение", Новый Структура("Группировать", Истина));
	
	Возврат ВидОшибки;
	
КонецФункции

Функция ВидОшибкиНеУдалосьНайтиКонтрагентаПоПриглашению() Экспорт
	
	ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки();
	ВидОшибки.Идентификатор = "НеУдалосьНайтиКонтрагентаПоРеквизитамИзПриглашения";
	ВидОшибки.ЗаголовокПроблемы = НСтр("ru = 'Не удалось найти контрагента по реквизитам из приглашения'");
	ОписаниеРешения = 
		НСтр("ru = '<a href = ""%1"">Укажите</a> контрагентов в приглашениях.
			 |Для того, чтобы контрагенты создавались автоматически, <a href = ""%2"">включите опцию</a>.'");
	ВидОшибки.ОписаниеРешения = СтрШаблон(ОписаниеРешения, "Укажите", "ВключитеОпцию");
	ВидОшибки.ОбработчикиНажатия.Вставить("Укажите", "ПриглашенияЭДОКлиент.ОткрытьПриглашения");
	ВидОшибки.ОбработчикиНажатия.Вставить("ВключитеОпцию", "ПриглашенияЭДОКлиент.ВключитьОпциюАвтоматическогоСозданияКонтрагентов");
	ВидОшибки.Статус = ОбработкаНеисправностейБЭДКлиентСервер.СтатусыОшибок().Обычная;
	ВидОшибки.ВыводитьСсылкуНаСписокОшибок = Ложь;
	
	Возврат ВидОшибки;
	
КонецФункции

#КонецОбласти

#КонецОбласти