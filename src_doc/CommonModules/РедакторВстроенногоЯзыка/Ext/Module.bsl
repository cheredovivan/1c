///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытий

// Добавляет в форму данные, необходимые для работы редактора.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма с редактором, из которой вызывается событие:
//     * УникальныйИдентификатор - УникальныйИдентификатор - идентификатор вызвавшей формы.
//   ИмяРеквизитаРедактор - Строка - имя реквизита формы, который выбран как путь к данным редактора.
//     Допускается перечисление реквизитов через запятую, если на форму выводится несколько редакторов.
//       Примечание. Имя элементов формы "РедакторСтраницы" и "СтраницаРедактор" должны соответствовать шаблону:
//       "%1Страницы" и "Страница%1", где %1 - это ИмяРеквизитаРедактор.
//   Контекст - Соответствие из КлючИЗначение - контекст выражения на встроенном языке:
//     * Ключ - Строка - имя переменной контекста. Например: Результат, Параметры.
//     * Значение - Структура - передается если у переменной контекста должны быть подчиненные реквизиты.
//         Например: Параметры.Источник, Параметры.Приемник:
//           ** Ключ - Строка - имя подчиненного реквизита.
//           ** Значение - Структура - передается если у подчиненного реквизита есть свои подчиненные реквизиты.
//                Например: Параметры.Источник.Наименование, Параметры.Приемник.Код.
//
Процедура ПриСозданииНаСервере(Форма, ИмяРеквизитаРедактор, Контекст = Неопределено) Экспорт
	
#Если Не ВнешнееСоединение Тогда
	РеквизитыФормы = Форма.ПолучитьРеквизиты();
	
	ДобавляемыеРеквизиты = Новый Массив;
	Если РеквизитФормыПоИмени(РеквизитыФормы, "ДанныеРедактора") = Неопределено Тогда
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ДанныеРедактора", Новый ОписаниеТипов()));
	КонецЕсли;
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Форма.ДанныеРедактора = ДанныеРедактора(Форма.УникальныйИдентификатор, ИмяРеквизитаРедактор, Контекст);
#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

// Выполняет обработку результата выполнения выражения на встроенном языке.
// Получает описание переменных для вывода в редактор.
// При возникновении исключения - добавляет информацию об ошибке.
//
// Параметры:
//   РезультатВыполнения - см. РедакторВстроенногоЯзыка.ПодготовитьВыражениеКВыполнению
//   Форма - ФормаКлиентскогоПриложения - форма с редактором, из которой вызывается событие:
//     * ДанныеРедактора - см. РедакторВстроенногоЯзыка.ДанныеРедактора
//     * УникальныйИдентификатор - УникальныйИдентификатор - идентификатор вызвавшей формы.
//
Процедура ОбработатьРезультатВыполнения(РезультатВыполнения, Форма) Экспорт
	
	УказанКонтекст = (ТипЗнч(Форма.ДанныеРедактора.Контекст) = Тип("Соответствие"));
	
	Если РезультатВыполнения.ИнформацияОбОшибке = Неопределено Тогда
		РезультатВыполнения.Успешно = Истина;
		ЗначенияПеременных = Неопределено;
		Если ТипЗнч(РезультатВыполнения.ПараметрыВыражения) = Тип("Структура")
				И РезультатВыполнения.ПараметрыВыражения.Свойство(ИмяСтруктурыПросмотраПеременных()) Тогда
			ЗначенияПеременных = РезультатВыполнения.ПараметрыВыражения[ИмяСтруктурыПросмотраПеременных()];
			Если ЗначенияПеременных.Свойство(ИмяСтруктурыПросмотраПеременных()) Тогда
				ЗначенияПеременных.Удалить(ИмяСтруктурыПросмотраПеременных());
			КонецЕсли;
			Для Каждого Элемент Из ЗначенияПеременных Цикл
				ЗначениеЭтоСтруктура = (ТипЗнч(Элемент.Значение) = Тип("Структура"));
				Если ЗначениеЭтоСтруктура И Элемент.Значение.Свойство(ИмяСтруктурыПросмотраПеременных()) Тогда
					Элемент.Значение.Удалить(ИмяСтруктурыПросмотраПеременных());
				КонецЕсли;
				Если ЗначениеЭтоСтруктура И УказанКонтекст Тогда
					УбратьРеквизитыВнеКонтекста(Форма.ДанныеРедактора.Контекст[Элемент.Ключ], Элемент.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Если ЗначенияПеременных <> Неопределено Тогда
#Если Не ВнешнееСоединение Тогда
			Попытка
				РезультатВыполнения.ОписаниеПеременных = ОписаниеПеременныхДляВыводаВРедактор(
					ЗначенияПеременных,
					Форма.ДанныеРедактора.АдресХраненияПеременных,
					Форма.УникальныйИдентификатор);
			Исключение
				ДобавитьИнформациюОбОшибке(РезультатВыполнения, ИнформацияОбОшибке());
			КонецПопытки;
#КонецЕсли
		КонецЕсли;
	Иначе
		Если ТипЗнч(РезультатВыполнения.ПараметрыВыражения) = Тип("Структура")
				И РезультатВыполнения.ПараметрыВыражения.Свойство("ИнформацияОбОшибке") Тогда
			ДобавитьИнформациюОбОшибке(РезультатВыполнения, РезультатВыполнения.ПараметрыВыражения.ИнформацияОбОшибке);
		Иначе
			ДобавитьИнформациюОбОшибке(РезультатВыполнения, РезультатВыполнения.ИнформацияОбОшибке);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(РезультатВыполнения.ПараметрыВыражения) = Тип("Структура") Тогда
		РезультатВыполнения.Удалить("ПараметрыВыражения");
	КонецЕсли;
	
КонецПроцедуры

// Добавляет код просмотра переменных в выражение на встроенном языке, и возвращает структуру результата выполнения.
//
// Параметры:
//   ВычисляемоеВыражение - Строка - исходное выражение на встроенном языке.
//   Форма - ФормаКлиентскогоПриложения - форма с редактором, из которой вызывается событие:
//     * ДанныеРедактора - см. РедакторВстроенногоЯзыка.ДанныеРедактора
//   ИмяПеременнойРезультатВыполнения - Строка - параметр нужно заполнять только когда для выполнения выражения
//     используется метод Выполнить (и не используется метод ОбщегоНазначения.ВыполнитьВБезопасномРежиме).
//     Содержит имя переменной, где хранится структура результата выполнения, присутствующая в месте
//     вызова метода Выполнить. В структуру будут помещены промежуточные значения переменных.
//
// Возвращаемое значение:
//   Структура:
//     * Успешно - Булево
//     * ПараметрыВыражения - Структура
//     * ОписаниеПеременных - Неопределено
//     * ИнформацияОбОшибке - Неопределено
//     * ИсходнаяСтрока - Строка
//     * ОписаниеОшибки - Строка
//     * СмещениеНомераСтрокиОшибки - Число - требуется заполнить если выполняемое выражение отличается от выражения,
//         показываемого в редакторе.
//
Функция ПодготовитьВыражениеКВыполнению(ВычисляемоеВыражение, Форма,
		ИмяПеременнойРезультатВыполнения = "") Экспорт
	
	РезультатВыполнения = Новый Структура;
	РезультатВыполнения.Вставить("Успешно", Ложь);
	ПараметрыВыражения = Новый Структура(ИмяСтруктурыПросмотраПеременных(), Новый Структура);
	РезультатВыполнения.Вставить("ПараметрыВыражения", ПараметрыВыражения);
	РезультатВыполнения.Вставить("ОписаниеПеременных", Неопределено);
	РезультатВыполнения.Вставить("ИнформацияОбОшибке", Неопределено);
	РезультатВыполнения.Вставить("ИсходнаяСтрока", "");
	РезультатВыполнения.Вставить("ОписаниеОшибки", "");
	РезультатВыполнения.Вставить("СмещениеНомераСтрокиОшибки", 0);
	
	Если Форма.ДанныеРедактора.Контекст <> Неопределено Тогда
		Если ПустаяСтрока(ИмяПеременнойРезультатВыполнения) Тогда
			ИмяСтруктурыПараметры = "Параметры";
		Иначе
			ИмяСтруктурыПараметры = СтрШаблон("%1.ПараметрыВыражения", ИмяПеременнойРезультатВыполнения);
		КонецЕсли;
		ДобавитьКодДляПросмотраПеременных(ВычисляемоеВыражение, Форма.ДанныеРедактора.Контекст, ИмяСтруктурыПараметры);
	КонецЕсли;
	
	Возврат РезультатВыполнения;
	
КонецФункции

// Возвращает связь реквизита с объектом метеданных редактора.
// Используется при формировании контекста редактора.
//
// Параметры:
//   Реквизит - ОбъектМетаданныхРеквизит - реквизит объекта.
//   Связи - Соответствие из КлючИЗначение - кеширует полученные связи:
//     * Ключ - Тип
//     * Значение - Строка
//
// Возвращаемое значение:
//   Строка
//
Функция СвязьСОбъектомМетаданных(Реквизит, Связи) Экспорт
	
	Связь = Неопределено;
	Типы = Реквизит.Тип.Типы();
	Индекс = 0;
	
	Пока Индекс < Типы.Количество() И Не ЗначениеЗаполнено(Связь) Цикл
		Тип = Типы[Индекс];
		Связь = Связи[Тип];
		Если Связь = Неопределено Тогда
			ИмяКоллекцииМетаданных = ИмяКоллекцииМетаданныхПоТипу(Тип);
			ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип);
			Если ИмяКоллекцииМетаданных <> Неопределено И ОбъектМетаданных <> Неопределено Тогда
				Связь = СтрШаблон("%1.%2", ИмяКоллекцииМетаданных, ОбъектМетаданных.Имя);
			КонецЕсли;
			Связи[Тип] = Связь;
		КонецЕсли;
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Возврат Связь;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает адрес макета с двоичными данными архива глобальных модулей во временном хранилище.
//
// Параметры:
//   УникальныйИдентификаторФормы - УникальныйИдентификатор - идентификатор вызвавшей формы.
//
// Возвращаемое значение:
//   Строка
//
Функция АдресМакетаГлобальныеМодули(УникальныйИдентификаторФормы) Экспорт
	
	Возврат ПоместитьВоВременноеХранилище(
		ПолучитьОбщийМакет("РедакторВстроенногоЯзыкаГлобальныеМодули"), УникальныйИдентификаторФормы);
	
КонецФункции

// Возвращает адрес макета с двоичными данными архива модулей менеджеров объектов во временном хранилище.
//
// Параметры:
//   УникальныйИдентификаторФормы - УникальныйИдентификатор - идентификатор вызвавшей формы.
//
// Возвращаемое значение:
//   Строка
//
Функция АдресМакетаМодулиМенеджеров(УникальныйИдентификаторФормы) Экспорт
	
	Возврат ПоместитьВоВременноеХранилище(
		ПолучитьОбщийМакет("РедакторВстроенногоЯзыкаМодулиМенеджеров"), УникальныйИдентификаторФормы);
	
КонецФункции

// Возвращает адрес макета с двоичными данными архива модулей объектов во временном хранилище.
//
// Параметры:
//   УникальныйИдентификаторФормы - УникальныйИдентификатор - идентификатор вызвавшей формы.
//
// Возвращаемое значение:
//   Строка
//
Функция АдресМакетаМодулиОбъектов(УникальныйИдентификаторФормы) Экспорт
	
	Возврат ПоместитьВоВременноеХранилище(
		ПолучитьОбщийМакет("РедакторВстроенногоЯзыкаМодулиОбъектов"), УникальныйИдентификаторФормы);
	
КонецФункции

// Возвращает адрес макета с двоичными данными архива общих модулей во временном хранилище.
//
// Параметры:
//   УникальныйИдентификаторФормы - УникальныйИдентификатор - идентификатор вызвавшей формы.
//
// Возвращаемое значение:
//   Строка
//
Функция АдресМакетаОбщиеМодули(УникальныйИдентификаторФормы) Экспорт
	
	Возврат ПоместитьВоВременноеХранилище(
		ПолучитьОбщийМакет("РедакторВстроенногоЯзыкаОбщиеМодули"), УникальныйИдентификаторФормы);
	
КонецФункции

// Формирует данные, необходимые для работы редактора встроенного языка.
//
// Параметры:
//   УникальныйИдентификаторФормы - УникальныйИдентификатор - идентификатор вызвавшей формы.
//   ИмяРеквизитаРедактор - Строка - имя реквизита формы, который выбран как путь к данным редактора.
//     Допускается перечисление реквизитов через запятую, если на форму выводится несколько редакторов.
//       Примечание. Имя элементов формы "РедакторСтраницы" и "СтраницаРедактор" должны соответствовать шаблону:
//       "%1Страницы" и "Страница%1", где %1 - это ИмяРеквизитаРедактор.
//   Контекст - Соответствие из КлючИЗначение - контекст выражения на встроенном языке:
//     * Ключ - Строка - имя переменной контекста. Например: Результат, Параметры.
//     * Значение - Структура - передается если у переменной контекста должны быть подчиненные реквизиты.
//         Например: Параметры.Источник, Параметры.Приемник:
//           ** Ключ - Строка - имя подчиненного реквизита.
//           ** Значение - Структура - передается если у подчиненного реквизита есть свои подчиненные реквизиты.
//                Например: Параметры.Источник.Наименование, Параметры.Приемник.Код.
//
// Возвращаемое значение:
//   Структура:
//     * АдресХраненияПеременных - Строка - адрес хранения значений переменных выражения на встроенном языке.
//     * ИмяРеквизитаРедактор - Строка - имя реквизита формы, который выбран как путь к данным редактора.
//     * ИспользоватьРедактор - Булево - хранит значение функциональной опции ИспользоватьРедакторВстроенногоЯзыка.
//     * КоллекцияОбщихМодулей - Строка - адрес коллекции общих модулей во временном хранилище.
//     * Контекст - Соответствие из КлючИЗначение - контекст выражения на встроенном языке.
//                - Неопределено - если контекст не задан.
//     * ОригинальныйКод - Строка - текст, который будет показан при открытии редактора.
//
Функция ДанныеРедактора(УникальныйИдентификаторФормы, ИмяРеквизитаРедактор, Контекст) Экспорт
	
	ИспользоватьРедактор = ПолучитьФункциональнуюОпцию("ИспользоватьРедакторВстроенногоЯзыка");
	
	ДанныеРедактора = Новый Структура;
	ДанныеРедактора.Вставить("АдресХраненияПеременных", "");
	ДанныеРедактора.Вставить("ИмяРеквизитаРедактор", ИмяРеквизитаРедактор);
	ДанныеРедактора.Вставить("ИспользоватьРедактор", ИспользоватьРедактор);
	ДанныеРедактора.Вставить("КоллекцияОбщихМодулей", "");
	ДанныеРедактора.Вставить("Контекст", Контекст);
	ДанныеРедактора.Вставить("ОригинальныйКод", "");
	
	Если ИспользоватьРедактор Тогда
		ДанныеРедактора.КоллекцияОбщихМодулей = КоллекцияОбщихМодулей(УникальныйИдентификаторФормы);
	КонецЕсли;
	
	Возврат ДанныеРедактора;
	
КонецФункции

// Возвращает имя коллекции метаданных редактора.
//
// Параметры:
//   ТипОбъектов - Строка, Тип - тип объекта 1С:Предприятия.
//
// Возвращаемое значение:
//   Строка
//   Неопределено
//
Функция ИмяКоллекцииМетаданныхПоТипу(Знач ТипОбъектов) Экспорт
	
	СоответствиеИмен = Новый Соответствие;
	СоответствиеИмен.Вставить("справочник"                  , "catalogs");
	СоответствиеИмен.Вставить("справочники"                 , "catalogs");
	СоответствиеИмен.Вставить("catalogs"                    , "catalogs");
	СоответствиеИмен.Вставить("документ"                    , "documents");
	СоответствиеИмен.Вставить("документы"                   , "documents");
	СоответствиеИмен.Вставить("documents"                   , "documents");
	СоответствиеИмен.Вставить("регистрсведений"             , "infoRegs");
	СоответствиеИмен.Вставить("регистрысведений"            , "infoRegs");
	СоответствиеИмен.Вставить("informationregisters"        , "infoRegs");
	СоответствиеИмен.Вставить("регистрнакопления"           , "accumRegs");
	СоответствиеИмен.Вставить("регистрынакопления"          , "accumRegs");
	СоответствиеИмен.Вставить("accumulationregisters"       , "accumRegs");
	СоответствиеИмен.Вставить("регистрбухгалтерии"          , "accountRegs");
	СоответствиеИмен.Вставить("регистрыбухгалтерии"         , "accountRegs");
	СоответствиеИмен.Вставить("accountingregisters"         , "accountRegs");
	СоответствиеИмен.Вставить("регистррасчета"              , "calcRegs");
	СоответствиеИмен.Вставить("регистрырасчета"             , "calcRegs");
	СоответствиеИмен.Вставить("calculationregisters"        , "calcRegs");
	СоответствиеИмен.Вставить("обработка"                   , "dataProc");
	СоответствиеИмен.Вставить("обработки"                   , "dataProc");
	СоответствиеИмен.Вставить("dataprocessors"              , "dataProc");
	СоответствиеИмен.Вставить("отчет"                       , "reports");
	СоответствиеИмен.Вставить("отчеты"                      , "reports");
	СоответствиеИмен.Вставить("reports"                     , "reports");
	СоответствиеИмен.Вставить("перечисление"                , "enums");
	СоответствиеИмен.Вставить("перечисления"                , "enums");
	СоответствиеИмен.Вставить("enums"                       , "enums");
	СоответствиеИмен.Вставить("плансчетов"                  , "сhartsOfAccounts");
	СоответствиеИмен.Вставить("планысчетов"                 , "сhartsOfAccounts");
	СоответствиеИмен.Вставить("chartsofaccounts"            , "сhartsOfAccounts");
	СоответствиеИмен.Вставить("бизнеспроцесс"               , "businessProcesses");
	СоответствиеИмен.Вставить("бизнеспроцессы"              , "businessProcesses");
	СоответствиеИмен.Вставить("businessprocesses"           , "businessProcesses");
	СоответствиеИмен.Вставить("задача"                      , "tasks");
	СоответствиеИмен.Вставить("задачи"                      , "tasks");
	СоответствиеИмен.Вставить("tasks"                       , "tasks");
	СоответствиеИмен.Вставить("планобмена"                  , "exchangePlans");
	СоответствиеИмен.Вставить("планыобмена"                 , "exchangePlans");
	СоответствиеИмен.Вставить("exchangeplans"               , "exchangePlans");
	СоответствиеИмен.Вставить("планвидовхарактеристик"      , "chartsOfCharacteristicTypes");
	СоответствиеИмен.Вставить("планывидовхарактеристик"     , "chartsOfCharacteristicTypes");
	СоответствиеИмен.Вставить("chartsofcharacteristictypes" , "chartsOfCharacteristicTypes");
	СоответствиеИмен.Вставить("планвидоврасчета"            , "chartsOfCalculationTypes");
	СоответствиеИмен.Вставить("планывидоврасчета"           , "chartsOfCalculationTypes");
	СоответствиеИмен.Вставить("chartsofcalculationtypes"    , "chartsOfCalculationTypes");
	СоответствиеИмен.Вставить("константа"                   , "constants");
	СоответствиеИмен.Вставить("константы"                   , "constants");
	СоответствиеИмен.Вставить("constants"                   , "constants");
	СоответствиеИмен.Вставить("внешнийисточникданных"       , "externalDataSources");
	СоответствиеИмен.Вставить("внешниеисточникиданных"      , "externalDataSources");
	СоответствиеИмен.Вставить("externaldatasources"         , "externalDataSources");
	
	Если ТипЗнч(ТипОбъектов) = Тип("Тип") Тогда
		ЧастиТипа = СтрРазделить(ОбщегоНазначения.СтроковоеПредставлениеТипа(ТипОбъектов), ".");
		Если ЧастиТипа.Количество() = 2 Тогда
			ТипОбъектов = СтрЗаменить(ЧастиТипа[0], "Ссылка", "");
		Иначе
			ТипОбъектов = "";
		КонецЕсли;
	КонецЕсли;
	
	ТипОбъектов = НРег(ТипОбъектов);
	
	Возврат СоответствиеИмен[ТипОбъектов];
	
КонецФункции

// Формирует список метаданных для загрузки в редактор.
//
// Параметры:
//   ТипОбъектов - Строка - тип объектов метаданных, которые требуется загрузить.
//
// Возвращаемое значение:
//   Структура:
//     * АдресОбновления - Строка - адрес пространства имен редактора.
//     * СписокОбъектов - Структура - список объектов метаданных:
//         ** Ключ - Строка - имя объекта метаданных.
//         ** Значение - Структура - реквизиты объекта метаданных.
//
Функция МетаданныеДляОбновления(ТипОбъектов) Экспорт
	
	Результат = Новый Структура("АдресОбновления, СписокОбъектов", "", Новый Структура);
	
	ИмяКоллекции = ИмяКоллекцииМетаданныхПоТипу(ТипОбъектов);
	Если ИмяКоллекции <> Неопределено Тогда
		Результат.АдресОбновления = СтрШаблон("%1.items", ИмяКоллекции);
		Для Каждого ОбъектМетаданных Из Метаданные[ТипОбъектов] Цикл
			Результат.СписокОбъектов.Вставить(ОбъектМетаданных.Имя, Новый Структура);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Формирует описание объекта метаданных для загрузки в редактор.
//
// Параметры:
//   ТипОбъекта - Строка - тип объекта метаданных, описание которого требуется загрузить.
//
// Возвращаемое значение:
//   Структура:
//     * АдресОбновления - Строка - адрес пространства имен редактора.
//     * Свойства - Структура - список свойств объекта метаданных:
//         ** Ключ - Строка - имя реквизита.
//         ** Значение - Строка - тип реквизита.
//
Функция ОписаниеОбъектаМетаданных(ТипОбъекта) Экспорт
	
	Описание = Новый Структура("АдресОбновления, Свойства", "", Новый Структура);
	
	Части = СтрРазделить(ТипОбъекта, ".");
	
	ИмяКоллекции = ИмяКоллекцииМетаданныхПоТипу(Части[0]);
	Если ИмяКоллекции <> Неопределено Тогда
		ОбъектМетаданных = Метаданные[Части[0]][Части[1]];
		ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
		Описание.АдресОбновления = СтрШаблон("%1.items.%2", ИмяКоллекции, ОбъектМетаданных.Имя);
		Если ИмяМетаданных(ПолноеИмя) = "ВнешнийИсточникДанных" Тогда
			Для Каждого Обход Из СтруктураВнешнегоИсточникаДанных(ОбъектМетаданных) Цикл
				Описание.Свойства.Вставить(Обход.Ключ, Обход.Значение);
			КонецЦикла;
		Иначе
			ЗаполнитьСтруктуруОбъектаМетаданных(Описание.Свойства, ОбъектМетаданных, ПолноеИмя);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Описание;
	
КонецФункции

// Формирует описание переменной для показа в редакторе.
//
// Параметры:
//   ИдентификаторПеременной - Строка - идентификатор переменной.
//   ИмяПеременной - Строка - имя переменной.
//   ПутьКДанным - Строка - путь к данным.
//   АдресХраненияПеременных - Строка - адрес хранения переменных.
//
// Возвращаемое значение:
//   Структура - состоит из одного элемента,
//     где Ключ это идентификатор переменной, а Значение это структура с её описанием.
//
Функция ОписаниеПеременной(ИдентификаторПеременной, ИмяПеременной, ПутьКДанным, АдресХраненияПеременных) Экспорт
	
	Данные = ПолучитьИзВременногоХранилища(АдресХраненияПеременных);
	КешСсылок = Данные.КэшСсылок;
	
	Путь = ?(ЗначениеЗаполнено(ПутьКДанным), ПутьКДанным, ИдентификаторПеременной);
	Путь = СтрЗаменить(Путь, "&quote;", """");
	Параметры = Новый Структура("ЗначениеПеременной, ХранилищеПеременных", Неопределено, Данные.ХранилищеПеременных);
	ОбщегоНазначения.ВыполнитьВБезопасномРежиме(
		СтрШаблон("Параметры.ЗначениеПеременной = Параметры.ХранилищеПеременных.%1", Путь), Параметры);
	Путь = СтрЗаменить(Путь, """", "&quote;");
	ТипЗнч = ТипЗнч(Параметры.ЗначениеПеременной);
	
	Если ЭтоКоллекция(Параметры.ЗначениеПеременной) Тогда
		ЭтоДерево = (ТипЗнч = Тип("ДеревоЗначений"));
		Описание = ОписаниеКоллекции(Параметры.ЗначениеПеременной, ИмяПеременной, Путь, КешСсылок, ЭтоДерево);
		
	ИначеЕсли ТипЗнч = Тип("СтрокаТаблицыЗначений") Тогда
		Описание = ОписаниеСтрокиТаблицыЗначений(Параметры.ЗначениеПеременной, ИмяПеременной, Путь, КешСсылок);
		
	ИначеЕсли ТипЗнч = Тип("СтрокаДереваЗначений") Тогда
		Описание = ОписаниеСтрокиДереваЗначений(Параметры.ЗначениеПеременной, ИмяПеременной, Путь, КешСсылок);
		
	ИначеЕсли ТипЗнч = Тип("Соответствие") Или ТипЗнч = Тип("Структура") Тогда
		Описание = ОписаниеСоответствияСтруктуры(Параметры.ЗначениеПеременной, ИмяПеременной, Путь, КешСсылок);
		
	ИначеЕсли ТипЗнч = Тип("КлючИЗначение") Тогда
		Описание = ОписаниеКлючаИЗначения(Параметры.ЗначениеПеременной, ИмяПеременной, Путь, КешСсылок);
		
	ИначеЕсли ТипЗнч = Тип("ЭлементСпискаЗначений") Тогда
		Описание = ОписаниеЭлементаСпискаЗначений(Параметры.ЗначениеПеременной, ИмяПеременной, Путь, КешСсылок);
		
	ИначеЕсли ТипЗнч = Тип("Запрос") Тогда
		Описание = ОписаниеЗапроса(Параметры.ЗначениеПеременной, ИмяПеременной, Путь, КешСсылок);
		
	ИначеЕсли ТипЗнч = Тип("ХранилищеЗначения") Тогда
		Описание = ОписаниеХранилищаЗначения(Параметры.ЗначениеПеременной, ИмяПеременной, Путь, КешСсылок);
		
	ИначеЕсли ЭтоСсылка(Параметры.ЗначениеПеременной) Тогда
		Описание = ОписаниеСсылочнойПеременной(Параметры.ЗначениеПеременной, ИмяПеременной, Путь, КешСсылок);
		
	ИначеЕсли ЭтоОбъект(Параметры.ЗначениеПеременной) Тогда
		Описание = ОписаниеСсылочнойПеременной(Параметры.ЗначениеПеременной, ИмяПеременной, Путь, КешСсылок, Истина);
		
	ИначеЕсли ЭтоСтрокаТабличнойЧасти(Параметры.ЗначениеПеременной) Тогда
		Описание = ОписаниеСтрокиТабличнойЧасти(Параметры.ЗначениеПеременной, ИмяПеременной, Путь, КешСсылок);
		
	ИначеЕсли ЭтоЗаписьНабораЗаписей(Параметры.ЗначениеПеременной) Тогда
		Описание = ОписаниеЗаписиНабораЗаписей(Параметры.ЗначениеПеременной, ИмяПеременной, Путь, КешСсылок);
		
	ИначеЕсли ЭтоМенеджерЗаписиРегистра(Параметры.ЗначениеПеременной) Тогда
		Описание = ОписаниеМенеджераЗаписейРегистра(Параметры.ЗначениеПеременной, ИмяПеременной, Путь, КешСсылок);
		
	Иначе
		Описание = Неопределено;
		
	КонецЕсли;
	
	Данные.КэшСсылок = КешСсылок;
	ПоместитьВоВременноеХранилище(Данные, АдресХраненияПеременных);
	
	Если Описание <> Неопределено Тогда
		Возврат Новый Структура(ИдентификаторПеременной, Описание);
	Иначе
		ВызватьИсключение("Неизвестный тип переменной");
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВыполнениеКода

Процедура ДобавитьИнформациюОбОшибке(РезультатВыполнения, ИнформацияОбОшибке)
	
	РезультатВыполнения.ИсходнаяСтрока = ИнформацияОбОшибке.ИсходнаяСтрока;
	Если ИнформацияОбОшибке.Причина <> Неопределено Тогда
		РезультатВыполнения.ОписаниеОшибки = ИнформацияОбОшибке.Причина.Описание;
	Иначе
		РезультатВыполнения.ОписаниеОшибки = ИнформацияОбОшибке.Описание;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьКодДляПросмотраПеременных(ВычисляемоеВыражение, Контекст, ИмяСтруктурыПараметры)
	
	Код = Новый Массив;
	Код.Добавить(ВычисляемоеВыражение);
	
	Для Каждого Элемент Из Контекст Цикл
		Код.Добавить(
			СтрШаблон("Попытка %1.%2.Вставить(""%3"", %3); Исключение КонецПопытки;",
				ИмяСтруктурыПараметры,
				ИмяСтруктурыПросмотраПеременных(),
				Элемент.Ключ));
	КонецЦикла;
	
	ВычисляемоеВыражение = СтрСоединить(Код, Символы.ПС);
	
КонецПроцедуры

Функция ЗаполнитьПараметрыКоллекций(Представление, Картинка, Класс, Значение, ТипЗначения)
	
	КоличествоЭлементов = Неопределено;
	
	ЭтоМассив = Ложь;
	ЭтоДерево = Ложь;
	
	Если ЭтоСписок(ТипЗначения) Тогда
		Представление = ПредставлениеКоличестваЭлементовКоллекции(Строка(ТипЗначения), Значение);
		ЭтоМассив = Истина;
		КоличествоЭлементов = Значение.Количество();
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("КоллекцияДвижений") Тогда
		Представление = ПредставлениеКоличестваЭлементовКоллекции(НСтр("ru = 'Движения'"), Значение);
		КоличествоЭлементов = Значение.Количество();
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ЭтоТабличнаяЧасть(Значение) Тогда
		Представление = ПредставлениеКоличестваЭлементовКоллекции(НСтр("ru = 'Табличная часть'"), Значение);
		КоличествоЭлементов = Значение.Количество();
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ЭтоНаборЗаписейРегистра(Значение) Тогда
		Представление = ПредставлениеКоличестваЭлементовКоллекции(НСтр("ru = 'Набор записей'"), Значение);
		КоличествоЭлементов = Значение.Количество();
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("ТаблицаЗначений") Тогда
		Представление = ПредставлениеКоличестваЭлементовКоллекции(Строка(ТипЗначения), Значение);
		КоличествоЭлементов = Значение.Количество();
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("СтрокаТаблицыЗначений")
			Или ЭтоСтрокаТабличнойЧасти(Значение)
			Или ЭтоЗаписьНабораЗаписей(Значение) Тогда
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("ДеревоЗначений") Тогда
		Представление = ПредставлениеКоличестваЭлементовКоллекции(Строка(ТипЗначения), Значение.Строки);
		ЭтоДерево = Истина;
		КоличествоЭлементов = Значение.Строки.Количество();
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("КоллекцияСтрокДереваЗначений") Тогда
		Представление = ПредставлениеКоличестваЭлементовКоллекции(НСтр("ru = 'Строки дерева значений'"), Значение);
		ЭтоДерево = Истина;
		КоличествоЭлементов = Значение.Количество();
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("СтрокаДереваЗначений") Тогда
		ЭтоДерево = Истина;
		ЗаполнениеВыполнено = Истина;
	Иначе
		ЗаполнениеВыполнено = Ложь;
	КонецЕсли;
	
	Если ЗаполнениеВыполнено Тогда
		Если КоличествоЭлементов = 0 Тогда
			Класс = "final";
		КонецЕсли;
		Если ЭтоМассив Тогда
			Картинка = "array";
		ИначеЕсли ЭтоДерево Тогда
			Картинка = "tree";
		Иначе
			Картинка = "table";
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗаполнениеВыполнено;
	
КонецФункции

Функция ЗаполнитьПараметрыРаскрываемыхТипов(Представление, Картинка, Класс, Значение, КешСсылок, ТипЗначения)
	
	Если ТипЗначения = Тип("ХранилищеЗначения") Тогда
		Картинка = "storage";
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("Запрос") Тогда
		Представление = Строка(ТипЗначения);
		Картинка = "query";
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ЭтоМенеджерЗаписиРегистра(Значение) Тогда
		Картинка = "structure";
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("КлючИЗначение") Или ТипЗначения = Тип("ЭлементСпискаЗначений") Тогда
		Представление = Строка(ТипЗначения);
		Картинка = "keyvalue";
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ЭтоСсылка(Значение) Тогда
		Представление = ПредставлениеСсылки(Значение, КешСсылок, Истина);
		Если СтрНайти(Представление, "</a>") Тогда
			Представление = СтрЗаменить(Представление, """", "");
			Представление = СтрЗаменить(Представление, Символы.Таб, "");
		КонецЕсли;
		Картинка = КартинкаДляПредставленияСсылки(Значение);
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ЭтоОбъект(Значение) Тогда
		Представление = ПредставлениеСсылки(Значение.Ссылка, КешСсылок, Истина);
		Если СтрНайти(Представление, "</a>") Тогда
			Представление = СтрЗаменить(Представление, """", "");
			Представление = СтрЗаменить(Представление, Символы.Таб, "");
		КонецЕсли;
		Картинка = КартинкаДляПредставленияСсылки(Значение.Ссылка);
		ЗаполнениеВыполнено = Истина;
	Иначе
		ЗаполнениеВыполнено = Ложь;
	КонецЕсли;
	
	Возврат ЗаполнениеВыполнено;
	
КонецФункции

Функция ЗаполнитьПараметрыФинальныхТипов(Представление, Картинка, Класс, Значение, ТипЗначения)
	
	Если ТипЗначения = Тип("Число") Тогда
		Представление = Строка(Значение);
		Картинка = "int";
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("Строка") Тогда
		Представление = Строка(Значение);
		Картинка = "string";
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("Булево") Тогда
		Представление = Строка(Значение);
		Картинка = "boolean";
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("Дата") Тогда
		Картинка = "date";
		Представление = Строка(Значение);
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("УникальныйИдентификатор") Тогда
		Представление = Строка(Значение);
		Картинка = "uuid";
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ЭтоПеречисление(Значение) Тогда
		Представление = Строка(Значение);
		Картинка = "enum";
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли Значение = Неопределено Тогда
		Представление = НСтр("ru = 'Неопределено'");
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("Null") Тогда
		Картинка = "null";
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("ДвоичныеДанные") Тогда
		Картинка = "binary";
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("Картинка") Тогда
		Картинка = "picture";
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("ТабличныйДокумент") Тогда
		Картинка = "tabular";
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ЭтоТекст(ТипЗначения) Тогда
		Картинка = "text";
		ЗаполнениеВыполнено = Истина;
	ИначеЕсли ТипЗначения = Тип("МенеджерВременныхТаблиц") Тогда
		ЗаполнениеВыполнено = Истина;
	Иначе
		ЗаполнениеВыполнено = Ложь;
	КонецЕсли;
	
	Если ЗаполнениеВыполнено Тогда
		Класс = "final";
	КонецЕсли;
	
	Возврат ЗаполнениеВыполнено;
	
КонецФункции

Функция ИмяОбъектаМетаданных(Ссылка)
	
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(Ссылка));
	
	Если ОбъектМетаданных <> Неопределено Тогда
		Возврат СтрРазделить(ОбъектМетаданных.ПолноеИмя(), ".")[0];
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

Функция КартинкаДляПредставленияСсылки(Ссылка)
	
	ИмяОбъекта = ИмяОбъектаМетаданных(Ссылка);
	
	Имена = Новый Соответствие;
	Имена.Вставить("Справочник", "catalog");
	Имена.Вставить("Catalog", "catalog");
	Имена.Вставить("Документ", "document");
	Имена.Вставить("Document", "document");
	Имена.Вставить("БизнесПроцесс", "bprocess");
	Имена.Вставить("BusinessProcess", "bprocess");
	Имена.Вставить("Задача", "task");
	Имена.Вставить("Task", "task");
	
	Картинка = Имена[ИмяОбъекта];
	
	Если Картинка = Неопределено Тогда
		Картинка = "structure";
	КонецЕсли;
	
	Возврат Картинка;
	
КонецФункции

Функция КраткаяСтруктураПеременной(Значение, Имя, ПутьКДанным, КешСсылок)
	
	ТипЗначения = ТипЗнч(Значение);
	
	Представление = "";
	Картинка = "";
	Класс = "";
	
	Если Не ЗаполнитьПараметрыКоллекций(Представление, Картинка, Класс, Значение, ТипЗначения)
			И Не ЗаполнитьПараметрыРаскрываемыхТипов(Представление, Картинка, Класс, Значение, КешСсылок, ТипЗначения)
			И Не ЗаполнитьПараметрыФинальныхТипов(Представление, Картинка, Класс, Значение, ТипЗначения) Тогда
		Попытка
			Представление = Строка(Значение);
		Исключение
			Представление = Строка(ТипЗначения);
		КонецПопытки;
		Класс = "final";
	КонецЕсли;
	
	Переменная = Новый Структура;
	Переменная.Вставить("label", Имя);
	Переменная.Вставить("value", Представление);
	Переменная.Вставить("type" , Строка(ТипЗначения));
	Переменная.Вставить("path" , ПутьКДанным);
	Переменная.Вставить("class", Класс);
	
	Если ЗначениеЗаполнено(Картинка) Тогда
		Переменная.Вставить("icon", СтрШаблон("%1.png", Картинка));
	КонецЕсли;
	
	Возврат Переменная;
	
КонецФункции

Функция НовыйИдентификаторПеременной()
	
	Возврат СтрШаблон("var_%1", СтрЗаменить(Новый УникальныйИдентификатор(), "-", ""));
	
КонецФункции

Функция ОписаниеПеременныхДляВыводаВРедактор(ЗначенияПеременных, АдресХраненияПеременных,
		УникальныйИдентификаторФормы)
	
	ОписаниеПеременных = Новый Структура;
	КешСсылок = Новый Соответствие;
	ХранилищеПеременных = Новый Структура;
	
	Для Каждого Обход Из ЗначенияПеременных Цикл
		ИдентификаторПеременной = НовыйИдентификаторПеременной();
		Переменная = КраткаяСтруктураПеременной(Обход.Значение, Обход.Ключ, "", КешСсылок);
		ОписаниеПеременных.Вставить(ИдентификаторПеременной, Переменная);
		ХранилищеПеременных.Вставить(ИдентификаторПеременной, Обход.Значение);
	КонецЦикла;
	
	Данные = Новый Структура;
	Данные.Вставить("ХранилищеПеременных", ХранилищеПеременных);
	Данные.Вставить("КэшСсылок", КешСсылок);
	
	АдресХраненияПеременных = ПоместитьВоВременноеХранилище(Данные, УникальныйИдентификаторФормы);
	
	Возврат ОписаниеПеременных;
	
КонецФункции

Функция ПредставлениеКоличестваЭлементовКоллекции(ПредставлениеТипаКоллекции, Коллекция)
	
	Возврат СтрШаблон("%1 (%2)", ПредставлениеТипаКоллекции, Коллекция.Количество());
	
КонецФункции

Функция ПредставлениеСсылки(Значение, КешСсылок, ЭтоСсылка)
	
	Представление = КешСсылок[Значение];
	
	Если Представление = Неопределено Тогда
		Попытка
			Если Не Значение.Пустая() Тогда
				Ссылка = ПолучитьНавигационнуюСсылку(Значение);
				Если ЭтоСсылка Тогда
					Представление = СтрШаблон("<a href='%1'>%2</a>", Ссылка, Строка(Значение));
				Иначе
					Представление = СтрШаблон("[%1](%2)", Строка(Значение), Ссылка);
				КонецЕсли;
			Иначе
				Представление = "";
			КонецЕсли;
		Исключение
			Представление = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		КонецПопытки;
		КешСсылок[Значение] = Представление;
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

Процедура УбратьРеквизитыВнеКонтекста(СтруктураКонтекста, СтруктураЗначения)
	
	Если Не ТипЗнч(СтруктураКонтекста) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	КлючиКУдалению = Новый Массив;
	Для Каждого Элемент Из СтруктураЗначения Цикл
		Если Не СтруктураКонтекста.Свойство(Элемент.Ключ) Тогда
			КлючиКУдалению.Добавить(Элемент.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого КлючКУдалению Из КлючиКУдалению Цикл
		СтруктураЗначения.Удалить(КлючКУдалению);
	КонецЦикла;
	
КонецПроцедуры

Функция ЭтоЗаписьНабораЗаписей(Ссылка)
	
	ЭтоЗаписьНабора = Ложь;
	ТипЗначения = ТипЗнч(Ссылка);
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗначения);
	
	Если ОбъектМетаданных <> Неопределено Тогда
		ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
		Если (СтрНайти(ПолноеИмя, "Регистр") > 0) Или (СтрНайти(ПолноеИмя, "Register") > 0) Тогда
			ЧастиИмени = СтрРазделить(ПолноеИмя, ".");
			Имя1 = ЧастиИмени.Получить(0);
			Имя2 = ЧастиИмени.Получить(1);
			ИмяТипа = СтрШаблон("%1Запись.%2", Имя1, Имя2);
			Попытка
				ЭтоЗаписьНабора = (ТипЗначения = Тип(ИмяТипа));
			Исключение
				ЭтоЗаписьНабора = Ложь;
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЭтоЗаписьНабора;
	
КонецФункции

Функция ЭтоМенеджерЗаписиРегистра(Значение)
	
	ЭтоМенеджерЗаписи = Ложь;
	ТипЗначения = ТипЗнч(Значение);
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗначения);
	
	Если ОбъектМетаданных <> Неопределено Тогда
		ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
		Если СтрНайти(ПолноеИмя, "РегистрСведений") = 1 И Метаданные.РегистрыСведений.Содержит(ОбъектМетаданных) Тогда
			ИмяТипа = СтрШаблон("РегистрСведенийМенеджерЗаписи.%1", ОбъектМетаданных.Имя);
			Попытка
				ЭтоМенеджерЗаписи = (ТипЗначения = Тип(ИмяТипа));
			Исключение
				ЭтоМенеджерЗаписи = Ложь;
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЭтоМенеджерЗаписи;
	
КонецФункции

Функция ЭтоНаборЗаписейРегистра(Значение)
	
	ЭтоНаборЗаписей = Ложь;
	ТипЗначения = ТипЗнч(Значение);
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗначения);
	
	Если ОбъектМетаданных <> Неопределено Тогда
		ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
		Если СтрНайти(ПолноеИмя, "РегистрСведений") = 1 И Метаданные.РегистрыСведений.Содержит(ОбъектМетаданных) Тогда
			ИмяТипа = СтрШаблон("РегистрСведенийНаборЗаписей.%1", ОбъектМетаданных.Имя);
		ИначеЕсли СтрНайти(ПолноеИмя, "РегистрНакопления") = 1
				И Метаданные.РегистрыНакопления.Содержит(ОбъектМетаданных) Тогда
			ИмяТипа = СтрШаблон("РегистрНакопленияНаборЗаписей.%1", ОбъектМетаданных.Имя);
		ИначеЕсли СтрНайти(ПолноеИмя, "РегистрРасчета") = 1
				И Метаданные.РегистрыРасчета.Содержит(ОбъектМетаданных) Тогда
			ИмяТипа = СтрШаблон("РегистрРасчетаНаборЗаписей.%1", ОбъектМетаданных.Имя);
		ИначеЕсли СтрНайти(ПолноеИмя, "РегистрБухгалтери") = 1
				И Метаданные.РегистрыБухгалтерии.Содержит(ОбъектМетаданных) Тогда
			ИмяТипа = СтрШаблон("РегистрБухгалтерииНаборЗаписей.%1", ОбъектМетаданных.Имя);
		Иначе
			ИмяТипа = "";
		КонецЕсли;
		Если ЗначениеЗаполнено(ИмяТипа) Тогда
			Попытка
				ЭтоНаборЗаписей = (ТипЗначения = Тип(ИмяТипа));
			Исключение
				ЭтоНаборЗаписей = Ложь;
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЭтоНаборЗаписей;
	
КонецФункции

Функция ЭтоОбъект(Значение)
	
	ЭтоОбъект = Ложь;
	ТипЗначения = ТипЗнч(Значение);
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗначения);
	
	Если ОбъектМетаданных <> Неопределено И Не ОбщегоНазначения.ЭтоПеречисление(ОбъектМетаданных) Тогда
		ЧастиПолногоИмени = СтрРазделить(ОбъектМетаданных.ПолноеИмя(), ".");
		ИмяТипаОбъект = СтрШаблон("%1Объект.%2", ЧастиПолногоИмени[0], ЧастиПолногоИмени[1]);
		Попытка
			ЭтоОбъект = (ТипЗначения = Тип(ИмяТипаОбъект));
		Исключение
			ЭтоОбъект = Ложь;
		КонецПопытки;
	КонецЕсли;
	
	Возврат ЭтоОбъект;
	
КонецФункции

Функция ЭтоПеречисление(Значение)
	
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(Значение));
	Возврат (ОбъектМетаданных <> Неопределено) И (СтрНайти(ОбъектМетаданных.ПолноеИмя(), "Перечисление") = 1);
	
КонецФункции

Функция ЭтоСписок(ТипЗначения)
	
	Возврат ТипЗначения = Тип("Структура")
		Или ТипЗначения = Тип("Соответствие")
		Или ТипЗначения = Тип("Массив")
		Или ТипЗначения = Тип("СписокЗначений");
	
КонецФункции

Функция ЭтоСсылка(Значение)
	
	ЭтоСсылка = Ложь;
	ТипЗначения = ТипЗнч(Значение);
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗначения);
	
	Если ОбъектМетаданных <> Неопределено И Не ОбщегоНазначения.ЭтоПеречисление(ОбъектМетаданных) Тогда
		ЧастиПолногоИмени = СтрРазделить(ОбъектМетаданных.ПолноеИмя(), ".");
		ИмяТипаСсылка = СтрШаблон("%1Ссылка.%2", ЧастиПолногоИмени[0], ЧастиПолногоИмени[1]);
		Попытка
			ЭтоСсылка = (ТипЗначения = Тип(ИмяТипаСсылка));
		Исключение
			ЭтоСсылка = Ложь;
		КонецПопытки;
	КонецЕсли;
	
	Возврат ЭтоСсылка;
	
КонецФункции

Функция ЭтоСтрокаТабличнойЧасти(Ссылка)
	
	ЭтоСтрокаТабличнойЧасти = Ложь;
	ТипЗначения = ТипЗнч(Ссылка);
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗначения);
	
	Если ОбъектМетаданных <> Неопределено Тогда
		Родитель = ОбъектМетаданных.Родитель();
		ИмяРодителя = Родитель.ПолноеИмя();
		Если ОбъектМетаданныхИмеетТЧ(ИмяРодителя) Тогда
			ЧастиИмени = СтрРазделить(ИмяРодителя, ".");
			Имя1 = ЧастиИмени.Получить(0);
			Имя2 = ЧастиИмени.Получить(1);
			ТабличнаяЧасть = Родитель.ТабличныеЧасти.Найти(ОбъектМетаданных.Имя);
			Если ТабличнаяЧасть <> Неопределено Тогда
				ИмяТипа = СтрШаблон("%1ТабличнаяЧастьСтрока.%2.%3", Имя1, Имя2, ТабличнаяЧасть.Имя);
				Попытка
					ЭтоСтрокаТабличнойЧасти = (ТипЗначения = Тип(ИмяТипа));
				Исключение
					ЭтоСтрокаТабличнойЧасти = Ложь;
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЭтоСтрокаТабличнойЧасти;
	
КонецФункции

Функция ЭтоТабличнаяЧасть(Ссылка)
	
	ЭтоТабличнаяЧасть = Ложь;
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(Ссылка));
	
	Если ОбъектМетаданных <> Неопределено Тогда
		Родитель = ОбъектМетаданных.Родитель();
		Если ОбъектМетаданныхИмеетТЧ(Родитель.ПолноеИмя()) Тогда
			ТабличнаяЧасть = Родитель.ТабличныеЧасти.Найти(ОбъектМетаданных.Имя);
			Если ТабличнаяЧасть <> Неопределено Тогда
				ЭтоТабличнаяЧасть = Не ЭтоСтрокаТабличнойЧасти(Ссылка);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЭтоТабличнаяЧасть;
	
КонецФункции

Функция ЭтоТекст(ТипЗначения)
	
	Возврат ТипЗначения = Тип("ТекстовыйДокумент")
		Или ТипЗначения = Тип("ЧтениеТекста")
		Или ТипЗначения = Тип("ЧтениеXML")
		Или ТипЗначения = Тип("ЧтениеJSON")
		Или ТипЗначения = Тип("ЧтениеHTML")
		Или ТипЗначения = Тип("ЗаписьJSON")
		Или ТипЗначения = Тип("ЗаписьXML")
		Или ТипЗначения = Тип("ЗаписьТекста")
		Или ТипЗначения = Тип("ЧтениеZipФайла")
		Или ТипЗначения = Тип("Файл");
	
КонецФункции

#КонецОбласти

#Область ОбновлениеМетаданных

Процедура ДобавитьОписаниеРеквизита(ОписаниеРеквизитов, Реквизит, Связи)
	
	Связь = ?(Связи <> Неопределено, СвязьСОбъектомМетаданных(Реквизит, Связи), "");
	
	ОписаниеРеквизита = Новый Структура("name", Реквизит.Синоним);
	
	Если ЗначениеЗаполнено(Связь) Тогда
		ОписаниеРеквизита.Вставить("ref", Связь);
	КонецЕсли;
	
	ОписаниеРеквизитов.Вставить(Реквизит.Имя, ОписаниеРеквизита);
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеИзмеренийРесурсов(ОбъектМетаданных, ПолноеИмя, ОписаниеРеквизитов, ОписаниеРесурсов,
		ДополнительныеСвойства, Связи)
	
	Если Не ОбъектМетаданныхИмеетИзмерения(ПолноеИмя) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Реквизит Из ОбъектМетаданных.Измерения Цикл
		ДобавитьОписаниеРеквизита(ОписаниеРеквизитов, Реквизит, Связи);
	КонецЦикла;
	Для Каждого Реквизит Из ОбъектМетаданных.Ресурсы Цикл
		ДобавитьОписаниеРеквизита(ОписаниеРесурсов, Реквизит, Связи);
	КонецЦикла;
	ЗаполнитьТипРегистра(ДополнительныеСвойства, ОбъектМетаданных, ПолноеИмя);
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеПредопределенныхЭлементов(ОбъектМетаданных, ПолноеИмя, ОписаниеПредопределенных)
	
	Если Не ОбъектМетаданныхИмеетПредопределенные(ПолноеИмя) Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяМетаданных(ПолноеИмя) = "ПланСчетов" Тогда
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ТаблицаПланаСчетов.Код КАК Код,
			|	ТаблицаПланаСчетов.ИмяПредопределенныхДанных КАК Имя
			|ИЗ
			|	&Таблица КАК ТаблицаПланаСчетов
			|ГДЕ
			|	ТаблицаПланаСчетов.Предопределенный");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Таблица", ПолноеИмя);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ОписаниеПредопределенных.Вставить(Выборка.Имя, СтрШаблон("%1 (%2)", Выборка.Имя, Выборка.Код));
		КонецЦикла;
	Иначе
		Предопределенные = ОбъектМетаданных.ПолучитьИменаПредопределенных();
		Для Каждого Имя Из Предопределенные Цикл
			ОписаниеПредопределенных.Вставить(Имя, "");
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеТабличныхЧастей(ОбъектМетаданных, ПолноеИмя, ОписаниеРеквизитов, ОписаниеТабличныхЧастей,
		Связи)
	
	Если Не ОбъектМетаданныхИмеетТЧ(ПолноеИмя) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТабличнаяЧасть Из ОбъектМетаданных.ТабличныеЧасти Цикл
		ПредставлениеТЧ = СтрШаблон("ТЧ: %1", ТабличнаяЧасть.Синоним);
		ОписаниеРеквизитов.Вставить(ТабличнаяЧасть.Имя, Новый Структура("name", ПредставлениеТЧ));
		ОписаниеТабличнойЧасти = Новый Структура;
		Для Каждого РеквизитТЧ Из ТабличнаяЧасть.СтандартныеРеквизиты Цикл
			ОписаниеТабличнойЧасти.Вставить(РеквизитТЧ.Имя, РеквизитТЧ.Синоним);
		КонецЦикла;
		Для Каждого РеквизитТЧ Из ТабличнаяЧасть.Реквизиты Цикл
			ДобавитьОписаниеРеквизита(ОписаниеТабличнойЧасти, РеквизитТЧ, Связи);
		КонецЦикла;
		СтруктураТабличнойЧасти = Новый Структура("properties", ОписаниеТабличнойЧасти);
		Если ОписаниеТабличнойЧасти.Количество() > 0 Тогда
			ОписаниеТабличныхЧастей.Вставить(ТабличнаяЧасть.Имя, СтруктураТабличнойЧасти);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСтруктуруОбъектаМетаданных(СтруктураОбъекта, ОбъектМетаданных, ПолноеИмя)
	
	ОписаниеРеквизитов = Новый Структура;
	ОписаниеРесурсов = Новый Структура;
	ОписаниеПредопределенных = Новый Структура;
	ОписаниеТабличныхЧастей = Новый Структура;
	ДополнительныеСвойства = Новый Структура;
	
	Связи = Новый Соответствие;
	
	Если ИмяМетаданных(ПолноеИмя) = "Перечисление" Тогда
		Для Каждого Реквизит Из ОбъектМетаданных.ЗначенияПеречисления Цикл
			ОписаниеРеквизитов.Вставить(Реквизит.Имя, Новый Структура("name", Реквизит.Синоним));
		КонецЦикла;
	Иначе
		Если ОбъектМетаданныхИмеетСтандартныеРеквизиты(ПолноеИмя) Тогда
			Для Каждого Реквизит Из ОбъектМетаданных.СтандартныеРеквизиты Цикл
				ДобавитьОписаниеРеквизита(ОписаниеРеквизитов, Реквизит, Связи);
			КонецЦикла;
		КонецЕсли;
		Для Каждого Реквизит Из ОбъектМетаданных.Реквизиты Цикл
			ДобавитьОписаниеРеквизита(ОписаниеРеквизитов, Реквизит, Связи);
		КонецЦикла;
		
		ЗаполнитьОписаниеПредопределенныхЭлементов(ОбъектМетаданных, ПолноеИмя, ОписаниеПредопределенных);
		ЗаполнитьОписаниеИзмеренийРесурсов(
			ОбъектМетаданных, ПолноеИмя, ОписаниеРеквизитов, ОписаниеРесурсов, ДополнительныеСвойства, Связи);
		ЗаполнитьОписаниеТабличныхЧастей(
			ОбъектМетаданных, ПолноеИмя, ОписаниеРеквизитов, ОписаниеТабличныхЧастей, Связи);
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("properties", ОписаниеРеквизитов);
	
	Для Каждого Обход Из ДополнительныеСвойства Цикл
		СтруктураОбъекта.Вставить(Обход.Ключ, Обход.Значение);
	КонецЦикла;
	
	Если ОписаниеРесурсов.Количество() > 0 Тогда
		СтруктураОбъекта.Вставить("resources", ОписаниеРесурсов);
	КонецЕсли;
	
	Если ОписаниеПредопределенных.Количество() > 0 Тогда
		СтруктураОбъекта.Вставить("predefined", ОписаниеПредопределенных);
	КонецЕсли;
	
	Если ОписаниеТабличныхЧастей.Количество() > 0 Тогда
		СтруктураОбъекта.Вставить("tabulars", ОписаниеТабличныхЧастей);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТипРегистра(ДополнительныеСвойства, ОбъектМетаданных, ПолноеИмя)
	
	ТипРегистра = "";
	
	Если ИмяМетаданных(ПолноеИмя) = "РегистрСведений" Тогда
		Непериодический = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический;
		Если ОбъектМетаданных.ПериодичностьРегистраСведений = Непериодический Тогда
			ТипРегистра = "nonperiodical";
		Иначе
			ТипРегистра = "periodical";
		КонецЕсли;
	ИначеЕсли ИмяМетаданных(ПолноеИмя) = "РегистрНакопления" Тогда
		Если ОбъектМетаданных.ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
			ТипРегистра = "balance";
		Иначе
			ТипРегистра = "turnovers";
		КонецЕсли;
	Иначе
		ТипРегистра = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТипРегистра) Тогда
		ДополнительныеСвойства.Вставить("type", ТипРегистра);
	КонецЕсли;
	
КонецПроцедуры

Функция ИмяМетаданных(ПолноеИмя)
	
	Возврат СтрРазделить(ПолноеИмя, ".")[0];
	
КонецФункции

Функция КоллекцияОбщихМодулей(УникальныйИдентификаторФормы)
	
	КоллекцияОбщихМодулей = РедакторВстроенногоЯзыкаПовтИсп.КоллекцияОбщихМодулей();
	
	Возврат ПоместитьВоВременноеХранилище(КоллекцияОбщихМодулей, УникальныйИдентификаторФормы);
	
КонецФункции

Функция ОбъектМетаданныхИмеетИзмерения(ПолноеИмя)
	
	Объекты = Новый Массив;
	Объекты.Добавить("РегистрСведений");
	Объекты.Добавить("РегистрНакопления");
	Объекты.Добавить("РегистрБухгалтерии");
	Объекты.Добавить("РегистрРасчета");
	
	Возврат Объекты.Найти(ИмяМетаданных(ПолноеИмя)) <> Неопределено;
	
КонецФункции

Функция ОбъектМетаданныхИмеетПредопределенные(ПолноеИмя)
	
	Объекты = Новый Массив;
	Объекты.Добавить("Справочник");
	Объекты.Добавить("ПланСчетов");
	Объекты.Добавить("ПланВидовХарактеристик");
	Объекты.Добавить("ПланВидовРасчета");
	
	Возврат Объекты.Найти(ИмяМетаданных(ПолноеИмя)) <> Неопределено;
	
КонецФункции

Функция ОбъектМетаданныхИмеетСтандартныеРеквизиты(ПолноеИмя)
	
	Объекты = Новый Массив;
	Объекты.Добавить("Справочник");
	Объекты.Добавить("Документ");
	Объекты.Добавить("БизнесПроцесс");
	Объекты.Добавить("Задача");
	
	Возврат Объекты.Найти(ИмяМетаданных(ПолноеИмя)) <> Неопределено;
	
КонецФункции

Функция ОбъектМетаданныхИмеетТЧ(ПолноеИмя)
	
	Объекты = Новый Массив;
	Объекты.Добавить("Справочник");
	Объекты.Добавить("Документ");
	Объекты.Добавить("Отчет");
	Объекты.Добавить("Обработка");
	Объекты.Добавить("БизнесПроцесс");
	Объекты.Добавить("Задача");
	
	Возврат Объекты.Найти(ИмяМетаданных(ПолноеИмя)) <> Неопределено;
	
КонецФункции

Функция СтруктураВнешнегоИсточникаДанных(ОбъектМетаданных)
	
	СтруктураИсточника = Новый Структура;
	
	ОписаниеТаблиц = Новый Структура;
	
	Для Каждого Таблица Из ОбъектМетаданных.Таблицы Цикл
		
		ОбъектныеДанные = Метаданные.СвойстваОбъектов.ТипДанныхТаблицыВнешнегоИсточникаДанных.ОбъектныеДанные;
		ТипТаблицы = ?(Таблица.ТипДанныхТаблицы = ОбъектныеДанные, "ObjectData", "NonobjectData");
		
		СтруктураТаблицы = Новый Структура;
		СтруктураТаблицы.Вставить("tableDataType", ТипТаблицы);
		
		ПоляТаблицы = Новый Структура;
		Для Каждого Поле Из Таблица.Поля Цикл
			ПоляТаблицы.Вставить(Поле.Имя, Новый Структура("name", Поле.Синоним));
		КонецЦикла;
		
		СтруктураТаблицы.Вставить("properties", ПоляТаблицы);
		
		ОписаниеТаблиц.Вставить(Таблица.Имя, СтруктураТаблицы);
		
	КонецЦикла;
	
	СтруктураИсточника.Вставить("tables", Новый Структура("items", ОписаниеТаблиц));
	
	Возврат СтруктураИсточника;
	
КонецФункции

#КонецОбласти

#Область ОписаниеДанных

Процедура ДополнитьДанныеПеременной(ДанныеПеременной, НаборЗначений, МассивКолонок, ПутьКДанным, КешСсылок,
		КолонкаИсключение = "")
	
	Для Каждого Колонка Из МассивКолонок Цикл
		Если Не ПустаяСтрока(КолонкаИсключение) И Колонка.Имя = КолонкаИсключение Тогда
			Продолжить;
		КонецЕсли;
		ЗначениеКолонки = НаборЗначений[Колонка.Имя];
		ДанныеКолонки = КраткаяСтруктураПеременной(
			ЗначениеКолонки, Колонка.Имя, ПутьРеквизита(ПутьКДанным, Колонка), КешСсылок);
		ДанныеПеременной.Вставить(НовыйИдентификаторПеременной(), ДанныеКолонки);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьДанныеПеременнойПоИмени(ДанныеПеременной, Имя, Значение, ПутьКДанным, КешСсылок)
	
	Путь = СтрШаблон("%1.%2", ПутьКДанным, Имя);
	ДанныеРеквизита = КраткаяСтруктураПеременной(Значение, Имя, Путь, КешСсылок);
	ДанныеПеременной.Вставить(НовыйИдентификаторПеременной(), ДанныеРеквизита);
	
КонецПроцедуры

Функция ИмяСтроки(Индекс)
	
	Возврат СтрШаблон("Строка %1", ИндексВСтроку(Индекс));
	
КонецФункции

Функция ИндексВСтроку(Индекс)
	
	Возврат Формат(Индекс, "ЧН=0; ЧГ=0;");
	
КонецФункции

Функция ОписаниеЗаписиНабораЗаписей(Запись, ИмяПеременной, ПутьКДанным, КешСсылок)

	Переменная = КраткаяСтруктураПеременной(Запись, ИмяПеременной, ПутьКДанным, КешСсылок);
	ДанныеПеременной = Новый Структура;
	ДополнитьДанныеПеременной(ДанныеПеременной, Запись, РеквизитыНабораЗаписей(Запись), ПутьКДанным, КешСсылок);
	Переменная.Вставить("children", ДанныеПеременной);
	
	Возврат Переменная;
	
КонецФункции

Функция ОписаниеЗапроса(Запрос, ИмяПеременной, ПутьКДанным, КешСсылок)

	Переменная = КраткаяСтруктураПеременной(Запрос, ИмяПеременной, ПутьКДанным, КешСсылок);
	
	ДанныеПеременной = Новый Структура;
	ДополнитьДанныеПеременнойПоИмени(ДанныеПеременной, "Текст", Запрос.Текст, ПутьКДанным, КешСсылок);
	ДополнитьДанныеПеременнойПоИмени(ДанныеПеременной, "Параметры", Запрос.Параметры, ПутьКДанным, КешСсылок);
	ДополнитьДанныеПеременнойПоИмени(
		ДанныеПеременной, "МенеджерВременныхТаблиц", Запрос.МенеджерВременныхТаблиц, ПутьКДанным, КешСсылок);
	Переменная.Вставить("children", ДанныеПеременной);
	
	Возврат Переменная;
	
КонецФункции

Функция ОписаниеКлючаИЗначения(КлючЗначение, ИмяПеременной, ПутьКДанным, КешСсылок)

	Переменная = КраткаяСтруктураПеременной(КлючЗначение, ИмяПеременной, ПутьКДанным, КешСсылок);
	
	ДанныеПеременной = Новый Структура;
	ДополнитьДанныеПеременнойПоИмени(ДанныеПеременной, "Ключ", КлючЗначение.Ключ, ПутьКДанным, КешСсылок);
	ДополнитьДанныеПеременнойПоИмени(ДанныеПеременной, "Значение", КлючЗначение.Значение, ПутьКДанным, КешСсылок);
	Переменная.Вставить("children", ДанныеПеременной);
	
	Возврат Переменная;
	
КонецФункции

Функция ОписаниеКоллекции(Знач Коллекция, ИмяПеременной, ПутьКДанным, КешСсылок, ЭтоДерево = Ложь)
	
	Переменная = КраткаяСтруктураПеременной(Коллекция, ИмяПеременной, ПутьКДанным, КешСсылок);
	
	ДанныеПеременной = Новый Структура;
	Индекс = 0;
	Если ЭтоДерево Тогда
		Коллекция = Коллекция.Строки;
	КонецЕсли;
	Для Каждого ЭлементКоллекции Из Коллекция Цикл
		Если ЭтоДерево Тогда
			ПутьЗначения = ПутьЗначенияВДереве(ПутьКДанным, Индекс);
		Иначе
			ПутьЗначения = ПутьЗначения(ПутьКДанным, Индекс);
		КонецЕсли;
		ДанныеЗначения = КраткаяСтруктураПеременной(
			ЭлементКоллекции, ИмяСтроки(Индекс), ПутьЗначения, КешСсылок);
		ДанныеПеременной.Вставить(НовыйИдентификаторПеременной(), ДанныеЗначения);
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Переменная.Вставить("children", ДанныеПеременной);
	
	Возврат Переменная;
	
КонецФункции

Функция ОписаниеМенеджераЗаписейРегистра(Запись, ИмяПеременной, ПутьКДанным, КешСсылок)
	
	Переменная = КраткаяСтруктураПеременной(Запись, ИмяПеременной, ПутьКДанным, КешСсылок);
	ДанныеПеременной = Новый Структура;
	ДополнитьДанныеПеременной(ДанныеПеременной, Запись, РеквизитыНабораЗаписей(Запись), ПутьКДанным, КешСсылок);
	Переменная.Вставить("children", ДанныеПеременной);
	
	Возврат Переменная;
	
КонецФункции

Функция ОписаниеСоответствияСтруктуры(Соответствие, ИмяПеременной, ПутьКДанным, КешСсылок)

	Переменная = КраткаяСтруктураПеременной(Соответствие, ИмяПеременной, ПутьКДанным, КешСсылок);
	ЭтоСоответствие = (ТипЗнч(Соответствие) = Тип("Соответствие"));
	ДанныеПеременной = Новый Структура;
	Для Каждого Обход Из Соответствие Цикл
		Если ЭтоСоответствие Тогда
			ПутьЗначения = СтрШаблон("%1[&quote;%2&quote;]", ПутьКДанным, Обход.Ключ);
		Иначе
			ПутьЗначения = СтрШаблон("%1.%2", ПутьКДанным, Обход.Ключ);
		КонецЕсли;
		ДанныеКолонки = КраткаяСтруктураПеременной(Обход.Значение, Обход.Ключ, ПутьЗначения, КешСсылок);
		ДанныеПеременной.Вставить(НовыйИдентификаторПеременной(), ДанныеКолонки);
	КонецЦикла;
	Переменная.Вставить("children", ДанныеПеременной);
	
	Возврат Переменная;
	
КонецФункции

Функция ОписаниеСсылочнойПеременной(Значение, ИмяПеременной, ПутьКДанным, КешСсылок, ЭтоОбъект = Ложь)
	
	Переменная = КраткаяСтруктураПеременной(Значение, ИмяПеременной, ПутьКДанным, КешСсылок);
	
	Если ЭтоОбъект Тогда
		Ссылка = Значение.Ссылка;
	Иначе
		Ссылка = Значение;
	КонецЕсли;
	
	ДанныеПеременной = Новый Структура;
	ДополнитьДанныеПеременнойПоИмени(
		ДанныеПеременной, "УникальныйИдентификатор", Ссылка.УникальныйИдентификатор(), ПутьКДанным, КешСсылок);
	ОбъектМетаданных = Ссылка.Метаданные();
	ПолноеИмяОбъектаМетаданных = ОбъектМетаданных.ПолноеИмя();
	Если ОбъектМетаданныхИмеетСтандартныеРеквизиты(ПолноеИмяОбъектаМетаданных) Тогда
		ДополнитьДанныеПеременной(
			ДанныеПеременной, Ссылка, ОбъектМетаданных.СтандартныеРеквизиты, ПутьКДанным, КешСсылок, "Ссылка");
	КонецЕсли;
	ДополнитьДанныеПеременной(ДанныеПеременной, Ссылка, ОбъектМетаданных.Реквизиты, ПутьКДанным, КешСсылок);
	Если ОбъектМетаданныхИмеетТЧ(ПолноеИмяОбъектаМетаданных) Тогда
		ДополнитьДанныеПеременной(ДанныеПеременной, Ссылка, ОбъектМетаданных.ТабличныеЧасти, ПутьКДанным, КешСсылок);
	КонецЕсли;
	Переменная.Вставить("children", ДанныеПеременной);
	
	Возврат Переменная;
	
КонецФункции

Функция ОписаниеСтрокиДереваЗначений(СтрокаДерева, ИмяПеременной, ПутьКДанным, КешСсылок)

	Переменная = КраткаяСтруктураПеременной(СтрокаДерева, ИмяПеременной, ПутьКДанным, КешСсылок);
	ДанныеПеременной = Новый Структура;
	ДополнитьДанныеПеременной(ДанныеПеременной, СтрокаДерева, СтрокаДерева.Владелец().Колонки, ПутьКДанным, КешСсылок);
	Если СтрокаДерева.Строки.Количество() > 0 Тогда
		ДополнитьДанныеПеременнойПоИмени(ДанныеПеременной, "Строки", СтрокаДерева.Строки, ПутьКДанным, КешСсылок);
	КонецЕсли;
	Переменная.Вставить("children", ДанныеПеременной);
	
	Возврат Переменная;
	
КонецФункции

Функция ОписаниеСтрокиТаблицыЗначений(СтрокаТаблицы, ИмяПеременной, ПутьКДанным, КешСсылок)
	
	Переменная = КраткаяСтруктураПеременной(СтрокаТаблицы, ИмяПеременной, ПутьКДанным, КешСсылок);
	ДанныеПеременной = Новый Структура;
	ДополнитьДанныеПеременной(ДанныеПеременной, СтрокаТаблицы, СтрокаТаблицы.Владелец().Колонки, ПутьКДанным, КешСсылок);
	Переменная.Вставить("children", ДанныеПеременной);
	
	Возврат Переменная;
	
КонецФункции

Функция ОписаниеСтрокиТабличнойЧасти(СтрокаТЧ, ИмяПеременной, ПутьКДанным, КешСсылок)

	Переменная = КраткаяСтруктураПеременной(СтрокаТЧ, ИмяПеременной, ПутьКДанным, КешСсылок);
	ДанныеПеременной = Новый Структура;
	ДополнитьДанныеПеременной(ДанныеПеременной, СтрокаТЧ, РеквизитыСтрокиТЧ(СтрокаТЧ), ПутьКДанным, КешСсылок);
	Переменная.Вставить("children", ДанныеПеременной);
	
	Возврат Переменная;
	
КонецФункции

Функция ОписаниеХранилищаЗначения(ХранилишеЗначения, ИмяПеременной, ПутьКДанным, КешСсылок)
	
	Переменная = КраткаяСтруктураПеременной(ХранилишеЗначения, ИмяПеременной, ПутьКДанным, КешСсылок);
	Данные = ХранилишеЗначения.Получить();
	ДанныеЗначения = КраткаяСтруктураПеременной(Данные, "Значение", СтрШаблон("%1.Получить()", ПутьКДанным), КешСсылок);
	ДанныеХранилища = Новый Структура(НовыйИдентификаторПеременной(), ДанныеЗначения);
	Переменная.Вставить("children", ДанныеХранилища);
	
	Возврат Переменная;
	
КонецФункции

Функция ОписаниеЭлементаСпискаЗначений(ЭлементСписка, ИмяПеременной, ПутьКДанным, КешСсылок)

	Переменная = КраткаяСтруктураПеременной(ЭлементСписка, ИмяПеременной, ПутьКДанным, КешСсылок);
	
	ДанныеПеременной = Новый Структура;
	ДополнитьДанныеПеременнойПоИмени(ДанныеПеременной, "Значение", ЭлементСписка.Значение, ПутьКДанным, КешСсылок);
	ДополнитьДанныеПеременнойПоИмени(ДанныеПеременной, "Пометка", ЭлементСписка.Пометка, ПутьКДанным, КешСсылок);
	ДополнитьДанныеПеременнойПоИмени(
		ДанныеПеременной, "Представление", ЭлементСписка.Представление, ПутьКДанным, КешСсылок);
	Переменная.Вставить("children", ДанныеПеременной);
	
	Возврат Переменная;
	
КонецФункции

Функция ПутьЗначения(ПутьКДанным, Индекс)
	
	Возврат СтрШаблон("%1[%2]", ПутьКДанным, ИндексВСтроку(Индекс));
	
КонецФункции

Функция ПутьЗначенияВДереве(ПутьКДанным, Индекс)
	
	Возврат СтрШаблон("%1.Строки[%2]", ПутьКДанным, ИндексВСтроку(Индекс));
	
КонецФункции

Функция ПутьРеквизита(ПутьКДанным, Реквизит)
	
	Возврат СтрШаблон("%1.%2", ПутьКДанным, Реквизит.Имя);
	
КонецФункции

Функция РеквизитыНабораЗаписей(Запись)
	
	Реквизиты = Новый Массив;
	
	Результат = Метаданные.НайтиПоТипу(ТипЗнч(Запись));
	Если Результат <> Неопределено Тогда
		Для Каждого Измерение Из Результат.Измерения Цикл
			Реквизиты.Добавить(Измерение);
		КонецЦикла;
		Для Каждого Ресурс Из Результат.Ресурсы Цикл
			Реквизиты.Добавить(Ресурс);
		КонецЦикла;
		Для Каждого Реквизит Из Результат.Реквизиты Цикл
			Реквизиты.Добавить(Реквизит);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Реквизиты;
	
КонецФункции

Функция РеквизитыСтрокиТЧ(Строка)
	
	Реквизиты = Новый Массив;
	
	Результат = Метаданные.НайтиПоТипу(ТипЗнч(Строка));
	Если Результат <> Неопределено Тогда
		Родитель = Результат.Родитель();
		Если ОбъектМетаданныхИмеетТЧ(Родитель.ПолноеИмя()) Тогда
			ТабличнаяЧасть = Родитель.ТабличныеЧасти.Найти(Результат.Имя);
			Реквизиты.Добавить(Новый Структура("Имя", "НомерСтроки"));
			Для Каждого Реквизит Из ТабличнаяЧасть.Реквизиты Цикл
				Реквизиты.Добавить(Реквизит);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Реквизиты;
	
КонецФункции

Функция ЭтоКоллекция(Значение)
	
	ТипЗнч = ТипЗнч(Значение);
	Возврат ТипЗнч = Тип("ТаблицаЗначений")
		Или ТипЗнч = Тип("ДеревоЗначений")
		Или ТипЗнч = Тип("КоллекцияСтрокДереваЗначений")
		Или ТипЗнч = Тип("Массив")
		Или ТипЗнч = Тип("СписокЗначений")
		Или ТипЗнч = Тип("КоллекцияДвижений")
		Или ЭтоТабличнаяЧасть(Значение)
		Или ЭтоНаборЗаписейРегистра(Значение);
	
КонецФункции

#КонецОбласти

// Возвращает имя структуры, хранящей значений переменных выражения на встроенном языке, для показа пользователю.
//
// Возвращаемое значение:
//   Строка
//
Функция ИмяСтруктурыПросмотраПеременных()
	
	Возврат "ЗначенияПеременных";
	
КонецФункции

// Ищет и возвращает реквизит формы по имени.
//
// Параметры:
//   РеквизитыФормы - Массив из РеквизитФормы - список всех реквизитов формы.
//   ИмяРеквизита - Строка - имя искомого реквизита.
//
// Возвращаемое значение:
//   РеквизитФормы - в случае если реквизит был найден.
//   Неопределено - если реквизит на форме отсутствует.
//
Функция РеквизитФормыПоИмени(РеквизитыФормы, ИмяРеквизита)
	
#Если Не ВнешнееСоединение Тогда
	Для Каждого Реквизит Из РеквизитыФормы Цикл
		Если Реквизит.Имя = ИмяРеквизита Тогда
			Возврат Реквизит;
		КонецЕсли;
	КонецЦикла;
#КонецЕсли
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти