
#Область СлужебныйПрограммныйИнтерфейс

// Продолжение процедуры ПодписатьОбъект.
// Вызывается из подсистемы ЭлектроннаяПодпись при запросе данных для подписания.
//
Процедура ПриЗапросеДвоичныхДанныхОбъекта(Параметры, Контекст, РезультатПолученияДанных) Экспорт

КонецПроцедуры

Процедура ПередЗаполнениемКонтекстаПроверкиПоДаннымПодписей(Контекст, ДанныеПодписейОбъектов) Экспорт
	
	// Локализация
	ФайлыЭДО = Новый Массив(); // Массив Из СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы
	Для Каждого Элемент Из ДанныеПодписейОбъектов Цикл
		Объект = Элемент.Ключ;
		Если ТипЗнч(Объект) = Тип("СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы") Тогда
			ФайлыЭДО.Добавить(Объект);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ФайлЭДО Из ФайлыЭДО Цикл
		ДанныеПодписейОбъектов.Удалить(ФайлЭДО);
	КонецЦикла;
	
	Контекст.СообщенияЭДОДляПроверкиПодписей = ОбменЭДОДокументооборотВызовСервера.СообщенияЭДОПоФайламЭДО(ФайлыЭДО);
	// Конец Локализация
	
КонецПроцедуры

Процедура ВыполнитьДействияДляПроверкиПодписей(Контекст) Экспорт

// Локализация
	ПроверитьПодписиФайловЭДО(Контекст);
// Конец Локализация

КонецПроцедуры

#КонецОбласти

// Локализация
#Область СлужебныеПроцедурыИФункции

#Область ПроверкаПодписейЭДО

// Выполняет проверку подписей файлов ЭДО
// 
// Параметры:
//  Контекст - см. НовыйКонтекстПроверкиПодписей
Процедура ПроверитьПодписиФайловЭДО(Контекст)
	
	ДействиеПроверки = РаботаСЭПКлиент.ДействияПроверкиПодписей().ПроверитьПодписиФайловЭДО;
	Если Не РаботаСЭПКлиент.НеобходимоВыполнитьДействиеПроверкиПодписей(ДействиеПроверки, Контекст) Тогда
		Возврат;
	КонецЕсли;
	
	Если Контекст.СообщенияЭДОДляПроверкиПодписей.Количество() = 0 Тогда
		РаботаСЭПКлиент.УбратьНеобходимостьВыполненияДействияПроверкиПодписей(ДействиеПроверки, Контекст);
		Возврат;
	КонецЕсли;
	
	РаботаСЭПКлиент.УстановитьЗапускДействияПроверкиПодписей(Контекст);
	
	НачатьЦиклПроверкиПодписейФайловЭДО(Контекст);
	
КонецПроцедуры

Процедура НачатьЦиклПроверкиПодписейФайловЭДО(Контекст)
	
	Если Не ИтерироватьЦиклПроверкиПодписейФайловЭДО(Контекст) Тогда
		ЗакончитьПроверкуПодписейФайловЭДО(Контекст);
		Возврат;
	КонецЕсли;
	
	ТекущееСообщениеЭДО = Контекст.СообщенияЭДОДляПроверкиПодписей[Контекст.ИндексТекущегоСообщенияЭДО];
	
	ОбработчикПроверки = Новый ОписаниеОповещения("ОбработатьПроверкуПодписиФайлаЭДО", ЭтотОбъект, Контекст);
	КонтекстДиагностики = ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики();
	ЭлектронныеДокументыЭДОКлиент.ПроверитьПодписиСообщения(ОбработчикПроверки, ТекущееСообщениеЭДО, КонтекстДиагностики);
	
КонецПроцедуры

Функция ИтерироватьЦиклПроверкиПодписейФайловЭДО(Контекст)
	
	ТекущийИндекс = Контекст.ИндексТекущегоСообщенияЭДО;
	
	ТекущийИндекс = ТекущийИндекс + 1;
	Если ТекущийИндекс > Контекст.СообщенияЭДОДляПроверкиПодписей.ВГраница() Тогда
		Возврат Ложь;
	Иначе
		Контекст.ИндексТекущегоСообщенияЭДО = ТекущийИндекс;
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

Процедура ОбработатьПроверкуПодписиФайлаЭДО(Результат, Контекст) Экспорт
	
	ТекущееСообщениеЭДО = Контекст.СообщенияЭДОДляПроверкиПодписей[Контекст.ИндексТекущегоСообщенияЭДО];
	Контекст.РезультатыПроверкиПодписейСообщенийЭДО[ТекущееСообщениеЭДО] = Результат;
	
	НачатьЦиклПроверкиПодписейФайловЭДО(Контекст);
	
КонецПроцедуры

Процедура ЗакончитьПроверкуПодписейФайловЭДО(Контекст)
	
	ДействиеПроверки = РаботаСЭПКлиент.ДействияПроверкиПодписей().ПроверитьПодписиФайловЭДО;
	РаботаСЭПКлиент.УбратьНеобходимостьВыполненияДействияПроверкиПодписей(ДействиеПроверки, Контекст);
	РаботаСЭПКлиент.СброситьЗапускДействияПроверкиПодписей(Контекст);
	РаботаСЭПКлиент.ВыполнитьДействияДляПроверкиПодписей(Контекст);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
// Конец Локализация
