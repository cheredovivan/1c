///////////////////////////////////////////////////////////////////////////////////////////////////////
// Обработка запросов XDTO, локализация
// Реализует функционал веб-сервиса DMService, имеющий локальную специфику
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ОбщиеПроцедурыИФункции

// Добавляет в структуру дополнительные поля, описывающие параметры корреспонденции документа предприятия,
// которые имеют локальную специфику.
//
// Параметры:
//   ПараметрыКорреспонденции - см. ОбработкаЗапросовXDTOДокументы.ПараметрыКорреспонденцииДокумента
//
Процедура ДополнитьПоляПараметровКорреспонденцииДокумента(ПараметрыКорреспонденции) Экспорт
	
// Локализация
	ПараметрыКорреспонденции.Вставить("ВидОбращения", Неопределено);
	ПараметрыКорреспонденции.Вставить("ЭтоПовторноеОбращение", Неопределено);
	ПараметрыКорреспонденции.Вставить("ПервичноеОбращение", Неопределено);
	ПараметрыКорреспонденции.Вставить("ЭтоДубликатОбращения", Неопределено);
	ПараметрыКорреспонденции.Вставить("ОсновноеОбращение", Неопределено);
	ПараметрыКорреспонденции.Вставить("ГотовоКВыгрузкеССТУ", Ложь);
	
	ПараметрыКорреспонденции.Вставить("ВопросыОбращения", Новый ТаблицаЗначений);
	ПараметрыКорреспонденции.ВопросыОбращения.Колонки.Добавить("Вопрос");
	ПараметрыКорреспонденции.ВопросыОбращения.Колонки.Добавить("ДатаОтвета");
	ПараметрыКорреспонденции.ВопросыОбращения.Колонки.Добавить("Документ");
	ПараметрыКорреспонденции.ВопросыОбращения.Колонки.Добавить("КодВопроса");
	ПараметрыКорреспонденции.ВопросыОбращения.Колонки.Добавить("МнениеАвтораОМерах");
	ПараметрыКорреспонденции.ВопросыОбращения.Колонки.Добавить("МнениеАвтораОРезультатах");
	ПараметрыКорреспонденции.ВопросыОбращения.Колонки.Добавить("ОрганДляПередачи");
	ПараметрыКорреспонденции.ВопросыОбращения.Колонки.Добавить("ОтветныйДокумент");
	ПараметрыКорреспонденции.ВопросыОбращения.Колонки.Добавить("ОтветныйДокументСтрока");
	ПараметрыКорреспонденции.ВопросыОбращения.Колонки.Добавить("Раздел");
	ПараметрыКорреспонденции.ВопросыОбращения.Колонки.Добавить("РезультатРассмотрения");
	ПараметрыКорреспонденции.ВопросыОбращения.Колонки.Добавить("СопроводительныйДокумент");
	ПараметрыКорреспонденции.ВопросыОбращения.Колонки.Добавить("СопроводительныйДокументСтрока");
	ПараметрыКорреспонденции.ВопросыОбращения.Колонки.Добавить("Тема");
	ПараметрыКорреспонденции.ВопросыОбращения.Колонки.Добавить("Тематика");
// Локализация Конец
	
КонецПроцедуры

// Заполняет дополнительные поля, имеющие локальную специфику, в структуре параметров корреспонденции
// по данным из документа предприятия.
//
// Параметры:
//   Документ - СправочникОбъект.ДокументыПредприятия - заполняемый объект.
//   ПараметрыКорреспонденции - см. ОбработкаЗапросовXDTOДокументы.ПараметрыКорреспонденцииДокумента
//
Процедура ЗаполнитьДополнительныеПоляКорреспонденцииИзДокумента(Документ, ПараметрыКорреспонденции) Экспорт
	
// Локализация
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Корреспонденция.ВидОбращения КАК ВидОбращения,
		|	Корреспонденция.ЭтоПовторноеОбращение КАК ЭтоПовторноеОбращение,
		|	Корреспонденция.ПервичноеОбращение КАК ПервичноеОбращение,
		|	Корреспонденция.ЭтоДубликатОбращения КАК ЭтоДубликатОбращения,
		|	Корреспонденция.ОсновноеОбращение КАК ОсновноеОбращение,
		|	Корреспонденция.ГотовоКВыгрузкеССТУ КАК ГотовоКВыгрузкеССТУ,
		|	Корреспонденция.ВопросыОбращения КАК ВопросыОбращения
		|ИЗ
		|	Документ.Корреспонденция КАК Корреспонденция
		|ГДЕ
		|	Корреспонденция.Основание = &Основание");
	Запрос.Параметры.Вставить("Основание", Документ.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ПараметрыКорреспонденции, Выборка,, "ВопросыОбращения");
		ВыборкаВопросыОбращения = Выборка.ВопросыОбращения.Выбрать();
		ПараметрыКорреспонденции.ВопросыОбращения.Очистить();
		Пока ВыборкаВопросыОбращения.Следующий() Цикл
			НовСтрока = ПараметрыКорреспонденции.ВопросыОбращения.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, ВыборкаВопросыОбращения);
			НовСтрока.КодВопроса = РаботаСОбращениямиВызовСервера.СформироватьКодВопроса(
				НовСтрока.Раздел,
				НовСтрока.Тематика,
				НовСтрока.Тема,
				НовСтрока.Вопрос);
		КонецЦикла;
	КонецЦикла;
// Локализация Конец
	
КонецПроцедуры

#КонецОбласти

// Локализация
#Область ДокументЭДО

// Заполняет объект XDTO по данным документа ЭДО.
//
// Параметры:
//   ДокументЭДО - ДокументОбъект.ЭлектронныйДокументВходящийЭДО
//               - ДокументОбъект.ЭлектронныйДокументИсходящийЭДО
//   ОбъектXDTO - ОбъектXDTO - объект XDTO типа DMIncomingElectronicDocument или DMOutgoingElectronicDocument.
//   НаборКолонок - Массив из Строка - массив имен реквизитов к заполнению.
//                - Неопределено - требование заполнить все реквизиты.
//
Процедура ПолучитьДанныеДокументаЭДО(ДокументЭДО, ОбъектXDTO, НаборКолонок = Неопределено) Экспорт
	
	ДанныеДокументаЭДО = Новый Структура;
	
	ОбработкаЗапросовXDTO.ЗаполнитьОбъектXDTOИзДанныхОбъектаДО(
		ДокументЭДО,
		ДанныеДокументаЭДО,
		ОбъектXDTO,
		НаборКолонок,
		Ложь);
	
	ОсновнойФайлДокументаЭДО = ОбменЭДОДокументооборотВызовСервера.ОсновнойФайлДокументаЭДО(ДокументЭДО.Ссылка);
	Если ЗначениеЗаполнено(ОсновнойФайлДокументаЭДО.ПрисоединенныйФайл) Тогда
		ОбъектXDTO.mainFile = ОбработкаЗапросовXDTO.ПолучитьОбъектXDTOПоСуществующимДаннымДО(
			ОсновнойФайлДокументаЭДО.ПрисоединенныйФайл.ПолучитьОбъект(),
			Неопределено);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет документ ЭДО по данным объекта XDTO.
//
// Параметры:
//   ДокументЭДО - ДокументОбъект.ЭлектронныйДокументВходящийЭДО
//               - ДокументОбъект.ЭлектронныйДокументИсходящийЭДО - заполняемый объект.
//   ОбъектXDTO - ОбъектXDTO - объект XDTO типа DMIncomingElectronicDocument или DMOutgoingElectronicDocument.
//     Источник данных.
//
Процедура ЗаполнитьДанныеДокументаЭДО(ДокументЭДО, ОбъектXDTO) Экспорт
	
	
	
КонецПроцедуры

// Ищет подходящий документ ЭДО по объекту XDTO.
//
// Параметры:
//   ОбъектXDTO - ОбъектXDTO - объект XDTO типа DMIncomingElectronicDocument или DMOutgoingElectronicDocument.
//
// Возвращаемое значение:
//   ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//   ДокументСсылка.ЭлектронныйДокументИсходящийЭДО - найденный документ ЭДО.
//   Неопределено - в случае, если документ ЭДО не был найден.
//
Функция НайтиДокументЭДО(ОбъектXDTO) Экспорт
	
	
	
КонецФункции

#КонецОбласти

#Область ПрисоединенныйФайлСообщенияЭДО

// Заполняет объект XDTO по данным присоединенного файла сообщения ЭДО.
//
// Параметры:
//   ПрисоединенныйФайлСообщенияЭДО - СправочникОбъект.СообщениеЭДОПрисоединенныеФайлы
//   ОбъектXDTO - ОбъектXDTO - объект XDTO типа DMEDIMessageAttachedFile.
//   НаборКолонок - Массив из Строка - массив имен реквизитов к заполнению.
//                - Неопределено - требование заполнить все реквизиты.
//
Процедура ПолучитьДанныеПрисоединенногоФайлаСообщенияЭДО(ПрисоединенныйФайлСообщенияЭДО, ОбъектXDTO,
		НаборКолонок = Неопределено) Экспорт
	
	ДанныеФайла = Новый Структура;
	ДанныеФайла.Вставить("author", ПрисоединенныйФайлСообщенияЭДО.Автор);
	ДанныеФайла.Вставить("binaryData", РаботаСФайлами.ДвоичныеДанныеФайла(ПрисоединенныйФайлСообщенияЭДО.Ссылка));
	ДанныеФайла.Вставить("creationDate", ПрисоединенныйФайлСообщенияЭДО.ДатаСоздания);
	ДанныеФайла.Вставить("description", ПрисоединенныйФайлСообщенияЭДО.Описание);
	ДанныеФайла.Вставить("editing", ЗначениеЗаполнено(ПрисоединенныйФайлСообщенияЭДО.Редактирует));
	ДанныеФайла.Вставить("editingUser", ПрисоединенныйФайлСообщенияЭДО.Редактирует);
	ДанныеФайла.Вставить("encrypted", ПрисоединенныйФайлСообщенияЭДО.Зашифрован);
	ДанныеФайла.Вставить("extension", ПрисоединенныйФайлСообщенияЭДО.Расширение);
	ДанныеФайла.Вставить("lockDate", ПрисоединенныйФайлСообщенияЭДО.ДатаЗаема);
	ДанныеФайла.Вставить("modificatedBy", ПрисоединенныйФайлСообщенияЭДО.Автор);
	ДанныеФайла.Вставить("modificationDateUniversal", ПрисоединенныйФайлСообщенияЭДО.ДатаМодификацииУниверсальная);
	ДанныеФайла.Вставить("signed", ПрисоединенныйФайлСообщенияЭДО.ПодписанЭП);
	ДанныеФайла.Вставить("size", ПрисоединенныйФайлСообщенияЭДО.Размер);
	ДанныеФайла.Вставить("storeVersions", ПрисоединенныйФайлСообщенияЭДО.ХранитьВерсии);
	
	ОбработкаЗапросовXDTO.ЗаполнитьОбъектXDTOИзДанныхОбъектаДО(
		ПрисоединенныйФайлСообщенияЭДО,
		ДанныеФайла,
		ОбъектXDTO,
		НаборКолонок,
		Ложь);
	
КонецПроцедуры

// Заполняет присоединенный файл сообщения ЭДО по данным объекта XDTO.
//
// Параметры:
//   ПрисоединенныйФайлСообщенияЭДО - СправочникОбъект.СообщениеЭДОПрисоединенныеФайлы - заполняемый объект.
//   ОбъектXDTO - ОбъектXDTO - объект XDTO типа DMEDIMessageAttachedFile. Источник данных.
//
Процедура ЗаполнитьДанныеПрисоединенногоФайлаСообщенияЭДО(ПрисоединенныйФайлСообщенияЭДО, ОбъектXDTO) Экспорт
	
	
	
КонецПроцедуры

// Ищет присоединенный файл сообщения ЭДО по объекту XDTO.
//
// Параметры:
//   ОбъектXDTO - ОбъектXDTO - объект XDTO типа DMEDIMessageAttachedFile.
//
// Возвращаемое значение:
//   СправочникОбъект.СообщениеЭДОПрисоединенныеФайлы - найденный присоединенный файл сообщения ЭДО.
//   Неопределено - в случае, если присоединенный файл сообщения ЭДО не был найден.
//
Функция НайтиПрисоединенныйФайлСообщенияЭДО(ОбъектXDTO) Экспорт
	
	
	
КонецФункции

#КонецОбласти

// Локализация Конец

#КонецОбласти