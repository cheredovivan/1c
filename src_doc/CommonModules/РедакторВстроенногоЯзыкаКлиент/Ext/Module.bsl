///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытий

// Обработчик события ДокументСформирован поля HTML документа, в котором выводится редактор.
//
// Параметры:
//   ЭлементРедактор - РасширениеПоляФормыДляПоляHTMLДокумента - поле HTML документа, в котором выводится редактор.
//   Форма - ФормаКлиентскогоПриложения - форма с редактором, из которой вызывается событие:
//     * ДанныеРедактора - см. РедакторВстроенногоЯзыка.ДанныеРедактора
//     * Модифицированность - Булево - признак изменения (модифицированности) данных в форме.
//     * УникальныйИдентификатор - УникальныйИдентификатор - идентификатор вызвавшей формы.
//   ОригинальныйКод - Строка - текст, который будет показан при открытии редактора.
//   ТолькоПросмотр - Булево - если Истина, то редактор будет открыт в режиме только просмотра.
//
// Возвращаемое значение:
//   Булево - Истина, если редактор успешно инициализирован.
//
Функция ДокументСформирован(ЭлементРедактор, Форма, ОригинальныйКод = "", ТолькоПросмотр = Ложь) Экспорт
	
	Если Не ИспользоватьРедактор(Форма) Или Не РедакторДоступен(ЭлементРедактор) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Редактор(ЭлементРедактор).hideScrollX();
	Если ТолькоПросмотр Или (ЭлементРедактор.Документ.body.clientHeight > 340) Тогда
		Редактор(ЭлементРедактор).hideScrollY();
	КонецЕсли;
	
	СистемнаяИнформация = Новый СистемнаяИнформация();
	Редактор(ЭлементРедактор).init(СистемнаяИнформация.ВерсияПриложения);
	
	Редактор(ЭлементРедактор).setOption("autoResizeEditorLayout", Истина);
	Редактор(ЭлементРедактор).setOption("renderQueryDelimiters", Истина);
	Если Не ТолькоПросмотр Тогда
		Редактор(ЭлементРедактор).setOption("generateModificationEvent", Истина);
	КонецЕсли;
	Редактор(ЭлементРедактор).minimap(Ложь);
	Редактор(ЭлементРедактор).enableQuickSuggestions(Истина);
	
	Если Не ПустаяСтрока(ОригинальныйКод) Тогда
		Форма.ДанныеРедактора.ОригинальныйКод = ОригинальныйКод;
		УстановитьТекст(ЭлементРедактор, ОригинальныйКод);
		УстановитьОригинальныйКод(ЭлементРедактор, ОригинальныйКод);
		Форма.Модифицированность = Ложь;
	КонецЕсли;
	
	Редактор(ЭлементРедактор).setReadOnly(ТолькоПросмотр);
	
	Возврат Истина;
	
КонецФункции

// Обработчик события ПриНажатии поля HTML документа, в котором выводится редактор.
//
// Параметры:
//   ЭлементРедактор - РасширениеПоляФормыДляПоляHTMLДокумента - поле HTML документа, в котором выводится редактор.
//   ДанныеСобытия - ФиксированнаяСтруктура - передает данные события.
//   СтандартнаяОбработка - Булево - в данный параметр передается признак выполнения стандартной обработки события.
//   Форма - ФормаКлиентскогоПриложения - форма с редактором, из которой вызывается событие:
//     * ДанныеРедактора - см. РедакторВстроенногоЯзыка.ДанныеРедактора
//     * Модифицированность - Булево - признак изменения (модифицированности) данных в форме.
//     * УникальныйИдентификатор - УникальныйИдентификатор - идентификатор вызвавшей формы.
//   НажатаКнопкаEsc - Булево - неявно возвращаемое значение, принимает значение Истина
//     при нажатии клавиши Esc на клавиатуре.
//
Процедура ПриНажатии(ЭлементРедактор, ДанныеСобытия, СтандартнаяОбработка, Форма, НажатаКнопкаEsc = Ложь) Экспорт
	
	ОбработатьСобытиеРедактора(
		ЭлементРедактор,
		Форма.ДанныеРедактора,
		ДанныеСобытия.Event.eventData1C,
		Форма.Модифицированность,
		Форма.УникальныйИдентификатор,
		НажатаКнопкаEsc);
	
КонецПроцедуры

// Инициирует редактор встроенного языка на форме. Устанавливает видимость элементов формы.
//
// Параметры:
//   ЭлементРедактор - РасширениеПоляФормыДляПоляHTMLДокумента - поле HTML документа, в котором будет показан редактор.
//                   - Массив из РасширениеПоляФормыДляПоляHTMLДокумента - если на форму выведено несколько редакторов.
//   ЭлементТекстовыйДокумент - РасширениеПоляФормыДляПоляТекстовогоДокумента - поле текстового документа,
//     которое будет показано в случае, если редактор недоступен или выключен.
//                            - Массив из РасширениеПоляФормыДляПоляТекстовогоДокумента - если на форму выведено
//     несколько редакторов.
//   Форма - ФормаКлиентскогоПриложения - форма с редактором, из которой вызывается событие:
//     * ДанныеРедактора - см. РедакторВстроенногоЯзыка.ДанныеРедактора
//
Процедура ПриОткрытии(ЭлементРедактор, ЭлементТекстовыйДокумент, Форма) Экспорт
	
	Если ТипЗнч(ЭлементРедактор) = Тип("Массив") Тогда
		МассивЭлементовРедактор = ЭлементРедактор;
	Иначе
		МассивЭлементовРедактор = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭлементРедактор);
	КонецЕсли;
	Если ТипЗнч(ЭлементТекстовыйДокумент) = Тип("Массив") Тогда
		МассивЭлементовТекстовыйДокумент = ЭлементТекстовыйДокумент;
	Иначе
		МассивЭлементовТекстовыйДокумент = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭлементТекстовыйДокумент);
	КонецЕсли;
	
	ИспользоватьРедактор = ИспользоватьРедактор(Форма);
	
	Для Каждого РеквизитРедактор Из РеквизитыРедактор(Форма) Цикл
		Если Форма.Элементы.Найти(ИмяЭлементаСтраницыРедактора(РеквизитРедактор)) <> Неопределено Тогда
			Форма.Элементы[ИмяЭлементаСтраницыРедактора(РеквизитРедактор)].Видимость = ИспользоватьРедактор;
		КонецЕсли;
	КонецЦикла;
	Для Каждого Элемент Из МассивЭлементовРедактор Цикл
		Элемент.Видимость = ИспользоватьРедактор;
	КонецЦикла;
	Для Каждого Элемент Из МассивЭлементовТекстовыйДокумент Цикл
		Элемент.Видимость = Не ИспользоватьРедактор;
	КонецЦикла;
	
	Если ИспользоватьРедактор Тогда
		ИзвлечьРедактор(Форма);
#Если ВебКлиент Тогда
		// В веб-клиенте нужно сразу переключиться на страницу редактора,
		// иначе не сработает событие ДокументСформирован.
		ПереключитьНаСтраницуРедактора(Форма);
#КонецЕсли
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Проверяет, используется ли редактор встроенного языка.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма с редактором, из которой вызывается событие:
//     * ДанныеРедактора - см. РедакторВстроенногоЯзыка.ДанныеРедактора
//
// Возвращаемое значение:
//   Булево
//
Функция ИспользоватьРедактор(Форма) Экспорт
	
#Если МобильныйКлиент Тогда
	Возврат Ложь;
#Иначе
	ИспользоватьРедактор = Форма.ДанныеРедактора.ИспользоватьРедактор И ОбщегоНазначенияКлиент.ЭтоWindowsКлиент();
	
	// Если редактор встроен на форму не корректно - выключим его использование.
	Для Каждого РеквизитРедактор Из РеквизитыРедактор(Форма) Цикл
		ИспользоватьРедактор = ИспользоватьРедактор
			И (Форма.Элементы.Найти(ИмяЭлементаСтраницыРедактора(РеквизитРедактор)) <> Неопределено)
			И (Форма.Элементы.Найти(ИмяЭлементаСтраницаРедактор(РеквизитРедактор)) <> Неопределено);
	КонецЦикла;
	
	Возврат ИспользоватьРедактор;
#КонецЕсли
	
КонецФункции

// Выполняет обработку результата выполнения выражения на встроенном языке.
// Показывает значения переменных в случае успешного выполнения.
// В случае возникновения ошибки - подсвечивает соответствующую строку.
//
// Параметры:
//   ЭлементРедактор - РасширениеПоляФормыДляПоляHTMLДокумента - поле HTML документа, в котором выводится редактор.
//   РезультатВыполнения - см. РедакторВстроенногоЯзыка.ПодготовитьВыражениеКВыполнению
//
Процедура ОбработатьРезультатВыполнения(ЭлементРедактор, РезультатВыполнения) Экспорт
	
	Если Не РезультатВыполнения.Успешно Тогда
		Ошибка = СтруктураОшибки(РезультатВыполнения.ОписаниеОшибки);
		ПоказатьПредупреждение(, Ошибка.ТекстОшибки);
	КонецЕсли;
	
	Если Не ЭлементРедактор.Видимость Или Не РедакторДоступен(ЭлементРедактор) Тогда
		Возврат;
	КонецЕсли;
	
	Редактор(ЭлементРедактор).setCustomCodeLenses("[]");
	
#Если Не ВебКлиент Тогда
	// В веб-клиенте не нужно выводить окно с значениями переменных, т.к. из-за того что событие получения
	// переменной не может быть обработано - пользователь не сможет развернуть строку дерева переменных.
	Если ЗначениеЗаполнено(РезультатВыполнения.ОписаниеПеременных) Тогда
		Редактор(ЭлементРедактор).showVariablesDescription(ЗначениеВJSON(РезультатВыполнения.ОписаниеПеременных));
	Иначе
		Редактор(ЭлементРедактор).showVariablesDescription("");
	КонецЕсли;
#КонецЕсли
	
	Если Не РезультатВыполнения.Успешно Тогда
		Если Ошибка.НомерСтроки = 0 Тогда
			Ошибка.НомерСтроки = ИзвлечьНомерСтрокиИзОписанияОшибки(РезультатВыполнения.ОписаниеОшибки);
		КонецЕсли;
		Если Ошибка.НомерСтроки = 0 Тогда
			Ошибка.НомерСтроки = НайтиИсходнуюСтрокуВКоде(ЭлементРедактор, РезультатВыполнения.ИсходнаяСтрока);
		КонецЕсли;
		Если Ошибка.НомерСтроки = 0 Тогда
			Ошибка.НомерСтроки = НайтиСтрокуИзОписанияОшибки(ЭлементРедактор, РезультатВыполнения.ОписаниеОшибки);
		КонецЕсли;
		Если Ошибка.НомерСтроки > 0 Тогда
			Редактор(ЭлементРедактор).markError(
				Ошибка.НомерСтроки + РезультатВыполнения.СмещениеНомераСтрокиОшибки,
				Ошибка.НомерКолонки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Очищает текст в редакторе.
//
// Параметры:
//   ЭлементРедактор - РасширениеПоляФормыДляПоляHTMLДокумента - поле HTML документа, в котором выводится редактор.
//   ТекстВнеРедактора - Строка - текст, который будет очищен, в случае если редактор не используется.
//
Процедура ОчиститьТекст(ЭлементРедактор, ТекстВнеРедактора = "") Экспорт
	
	Если ЭлементРедактор.Видимость И РедакторДоступен(ЭлементРедактор) Тогда
		Редактор(ЭлементРедактор).eraseText();
	Иначе
		ТекстВнеРедактора = "";
	КонецЕсли;
	
КонецПроцедуры

// Получает текст, находящийся в редакторе.
//
// Параметры:
//   ЭлементРедактор - РасширениеПоляФормыДляПоляHTMLДокумента - поле HTML документа, в котором выводится редактор.
//   ТекстВнеРедактора - Строка - текст, который будет возвращен, в случае если редактор не используется.
//
// Возвращаемое значение:
//   Строка
//
Функция ПолучитьТекст(ЭлементРедактор, ТекстВнеРедактора = "") Экспорт
	
	Если ЭлементРедактор.Видимость  И РедакторДоступен(ЭлементРедактор) Тогда
		Возврат Редактор(ЭлементРедактор).getText();
	Иначе
		Возврат ТекстВнеРедактора;
	КонецЕсли;
	
КонецФункции

// Процедура вызывается через подключаемый обработчик после события ДокументСформирован.
// Переключает страницу загрузки на страницу редактора, и запускает обновление метаданных и контекста редактора.
//
// Параметры:
//   ЭлементРедактор - РасширениеПоляФормыДляПоляHTMLДокумента - поле HTML документа, в котором выводится редактор.
//   Форма - ФормаКлиентскогоПриложения - форма с редактором, из которой вызывается событие:
//     * ДанныеРедактора - см. РедакторВстроенногоЯзыка.ДанныеРедактора
//     * Модифицированность - Булево - признак изменения (модифицированности) данных в форме.
//     * УникальныйИдентификатор - УникальныйИдентификатор - идентификатор вызвавшей формы.
//
Процедура ПослеЗагрузкиРедактора(ЭлементРедактор, Форма) Экспорт
	
	ПереключитьНаСтраницуРедактора(Форма);
	
	ОбновитьМетаданные(ЭлементРедактор, Форма.ДанныеРедактора, Форма.УникальныйИдентификатор);
	
	Если Форма.ДанныеРедактора.Контекст <> Неопределено Тогда
		ОбновитьКонтекстВРедакторе(ЭлементРедактор, Форма.ДанныеРедактора.Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает в редакторе контекст выражения на встроенном языке.
//
// Параметры:
//   ЭлементРедактор - РасширениеПоляФормыДляПоляHTMLДокумента - поле HTML документа, в котором выводится редактор.
//   Форма - ФормаКлиентскогоПриложения - форма с редактором, из которой вызывается событие:
//     * ДанныеРедактора - см. РедакторВстроенногоЯзыка.ДанныеРедактора
//   Контекст - Соответствие из КлючИЗначение:
//     * Ключ - Строка - имя переменной контекста. Например: Результат, Параметры.
//     * Значение - Структура - передается если у переменной контекста должны быть подчиненные реквизиты.
//         Например: Параметры.Источник, Параметры.Приемник:
//           ** Ключ - Строка - имя подчиненного реквизита.
//           ** Значение - Структура - передается если у подчиненного реквизита есть свои подчиненные реквизиты.
//                Например: Параметры.Источник.Наименование, Параметры.Приемник.Код.
//
Процедура УстановитьКонтекст(ЭлементРедактор, Форма, Контекст) Экспорт
	
	Если Не ЭлементРедактор.Видимость Или Не РедакторДоступен(ЭлементРедактор) Тогда
		Возврат;
	КонецЕсли;
	
	Форма.ДанныеРедактора.Контекст = Контекст;
	
	ОбновитьКонтекстВРедакторе(ЭлементРедактор, Контекст);
	
КонецПроцедуры

// Устанавливает в редакторе текст из переданной строки.
// Текст, содержащийся в редакторе до исполнения метода, удаляется.
//
// Параметры:
//   ЭлементРедактор - РасширениеПоляФормыДляПоляHTMLДокумента - поле HTML документа, в котором выводится редактор.
//   Текст - Строка - текст, который требуется установить.
//   ПозицияТекста - Число - позиция текста.
//   УчитыватьОтступПервойСтроки - Булево - учитывать отступ.
//
Процедура УстановитьТекст(ЭлементРедактор, Текст, ПозицияТекста = Неопределено,
		УчитыватьОтступПервойСтроки = Ложь) Экспорт
	
	Если Не ЭлементРедактор.Видимость Или Не РедакторДоступен(ЭлементРедактор) Тогда
		Возврат;
	КонецЕсли;
	
	Редактор(ЭлементРедактор).setText(Текст, ПозицияТекста, УчитыватьОтступПервойСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Конструктор структуры, содержащей имена коллекций метаданных.
//
// Возвращаемое значение:
//   Структура - имена коллекций метаданных:
//     * ГлобальныеМодули - Строка
//     * ОбщиеМодули - Строка
//     * МодулиМенеджеров - Строка
//     * МодулиОбъектов - Строка
//
Функция ВидМетаданных() Экспорт
	
	ВидМетаданных = Новый Структура;
	ВидМетаданных.Вставить("ГлобальныеМодули", "GlobalModules");
	ВидМетаданных.Вставить("ОбщиеМодули", "CommonModules");
	ВидМетаданных.Вставить("МодулиМенеджеров", "ManagerModules");
	ВидМетаданных.Вставить("МодулиОбъектов", "ObjectModules");
	
	Возврат ВидМетаданных;
	
КонецФункции

// Извлекает архив с данными редактора на диск.
//
// Параметры:
//   АдресМакета - Строка - адрес макета редактора во временном хранилище.
//   Подкаталог - Строка - подкаталог для извлечения вспомогательных данных.
//   ВерсияИБ - Строка - версия ИБ, которая будет записана в файл для контроля
//     необходимости обновления данных редактора.
//
// Возвращаемое значение:
//   Булево - Истина, если архив был успешно извлечен.
//
Функция ИзвлечьАрхив(АдресМакета, Подкаталог = "", ВерсияИБ = "") Экспорт
	
	Результат = Ложь;
	
#Если Не ВебКлиент Тогда
	// В веб-клиенте не поддерживается работа с файлами.
	// Содержимое модулей и контектсная подсказа к методам будут недоступны.
	Если ЗначениеЗаполнено(АдресМакета) Тогда
		ИмяАрхиваМассив = Новый Массив;
		ИмяАрхиваМассив.Добавить(КаталогИсходников());
		ИмяАрхиваМассив.Добавить(?(ПустаяСтрока(Подкаталог), "BSLEditor", Подкаталог));
		ИмяАрхиваМассив.Добавить(".zip");
		ИмяАрхива = СтрСоединить(ИмяАрхиваМассив);
		ДанныеМакета = ПолучитьИзВременногоХранилища(АдресМакета); // ДвоичныеДанные
		ДанныеМакета.Записать(ИмяАрхива);
		
		ФайлАрхива = Новый ЧтениеФайлаАрхива(ИмяАрхива);
		ПутьККаталогуМассив = Новый Массив;
		ПутьККаталогуМассив.Добавить(КаталогИсходников());
		Если Не ПустаяСтрока(Подкаталог) Тогда
			ПутьККаталогуМассив.Добавить(Подкаталог);
		КонецЕсли;
		ФайлАрхива.ИзвлечьВсе(СтрСоединить(ПутьККаталогуМассив));
		ФайлАрхива.Закрыть();
		
		Если Не ПустаяСтрока(ВерсияИБ) Тогда
			ФайлВерсии = Новый ЗаписьТекста(КаталогИсходников() + "ВерсияИБ.ver");
			ФайлВерсии.ЗаписатьСтроку(ВерсияИБ);
			ФайлВерсии.Закрыть();
		КонецЕсли;
		
		НачатьУдалениеФайлов(,ИмяАрхива);
		
		Результат = Истина;
	КонецЕсли;
#КонецЕсли
	
	Возврат Результат;
	
КонецФункции

// Возвращает путь к каталогу исходников редактора.
//
// Возвращаемое значение:
//   Строка
//
Функция КаталогИсходников() Экспорт
	
	КаталогИсходников = Новый Массив;
	КаталогИсходников.Добавить(КаталогВременныхФайлов());
	КаталогИсходников.Добавить("BSLEditor");
	КаталогИсходников.Добавить(ПолучитьРазделительПути());
	
	Возврат СтрСоединить(КаталогИсходников);
	
КонецФункции

// Возвращает путь к данным редактора встроенного языка.
//
// Возвращаемое значение:
//   Строка - данную строку нужно поместить в свойство ПутьКДанным поля HTML документа на форме.
//
Функция ПутьКДаннымРедактора() Экспорт
	
#Если ВебКлиент Тогда
	// В веб-клиенте в свойство ПутьКДанным будем помещать весь текст HTML документа целиком.
	ВоВременномХранилище = Ложь;
#Иначе
	ВоВременномХранилище = Истина;
#КонецЕсли
	
	Возврат РедакторВстроенногоЯзыкаВызовСервера.ПутьКДаннымРедактора(ВоВременномХранилище);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВыполнениеКода

Функция ИзвлечьНомерСтрокиИзОписанияОшибки(ОписаниеОшибки)
	
	НомерСтроки = 0;
	ОткрывающаяСкобка = СтрНайти(ОписаниеОшибки, "(");
	
	Если ОткрывающаяСкобка > 0 Тогда
		ЗакрывающаяСкобка = СтрНайти(ОписаниеОшибки, ")", НаправлениеПоиска.СНачала, ОткрывающаяСкобка);
		Если ЗакрывающаяСкобка > 0 Тогда
			Позиция = Сред(ОписаниеОшибки, ОткрывающаяСкобка + 1, ЗакрывающаяСкобка - ОткрывающаяСкобка - 1);
			Подстроки = СтрРазделить(Позиция, ",");
			Попытка
				НомерСтроки = Число(Подстроки[0]);
			Исключение
				НомерСтроки = 0;
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НомерСтроки;
	
КонецФункции

Функция НайтиИсходнуюСтрокуВКоде(ЭлементРедактор, ИсходнаяСтрока)
	
	НомерСтроки = Редактор(ЭлементРедактор).findText(ИсходнаяСтрока);
	
	Возврат НомерСтроки;
	
КонецФункции

Функция НайтиСтрокуИзОписанияОшибки(ЭлементРедактор, ОписаниеОшибки)
	
	НомерСтроки = 0;
	ОткрывающаяСкобка = СтрНайти(ОписаниеОшибки, "(", НаправлениеПоиска.СКонца);
	
	Если ОткрывающаяСкобка > 0 Тогда
		ЗакрывающаяСкобка = СтрНайти(ОписаниеОшибки, ")", НаправлениеПоиска.СНачала, ОткрывающаяСкобка);
		Если ЗакрывающаяСкобка > 0 Тогда
			ТекстДляПоиска = Сред(ОписаниеОшибки, ОткрывающаяСкобка + 1, ЗакрывающаяСкобка - ОткрывающаяСкобка - 1);
			НомерСтроки = Редактор(ЭлементРедактор).findText(ТекстДляПоиска);
		КонецЕсли;
	КонецЕсли;
	
	Возврат НомерСтроки;
	
КонецФункции

Процедура ОбработкаСобытияПолученияПеременной(ЭлементРедактор, ДанныеРедактора, Событие)
	
	ИдентификаторПеременной = Событие.variableId;
	ИмяПеременной = Событие.variableName;
	ПутьКДанным = СтрЗаменить(Событие.variablePath, "undefined", "");
	
	ОписаниеПеременной = РедакторВстроенногоЯзыкаВызовСервера.ОписаниеПеременной(
		ИдентификаторПеременной,
		ИмяПеременной,
		ПутьКДанным,
		ДанныеРедактора.АдресХраненияПеременных);
	
	Если ЗначениеЗаполнено(ОписаниеПеременной) Тогда
		Редактор(ЭлементРедактор).updateVariableDescription(ИдентификаторПеременной, ЗначениеВJSON(ОписаниеПеременной));
	КонецЕсли;
	
КонецПроцедуры

Функция СтруктураОшибки(ОписаниеОшибки)
	
	СтруктураОшибки = Новый Структура("ТекстОшибки, НомерСтроки, НомерКолонки", ОписаниеОшибки, 0, 1);
	
	ПозицияРазделителя = СтрНайти(ОписаниеОшибки, ":");
	Если ПозицияРазделителя > 0 Тогда
		СтруктураОшибки.ТекстОшибки = СокрЛП(Сред(ОписаниеОшибки, ПозицияРазделителя + 1));
		
		СтрокаОшибки = Лев(ОписаниеОшибки, ПозицияРазделителя - 1);
		СтрокаОшибки = СтрЗаменить(СтрокаОшибки, "{", "");
		СтрокаОшибки = СтрЗаменить(СтрокаОшибки, "(", "");
		СтрокаОшибки = СтрЗаменить(СтрокаОшибки, ")", "");
		СтрокаОшибки = СтрЗаменить(СтрокаОшибки, "}", "");
		
		Подстроки = СтрЗаменить(СтрокаОшибки, ",", Символы.ПС);
		
		СтруктураОшибки.НомерСтроки = СтрокаВЧисло(СтрПолучитьСтроку(Подстроки, 1), 0);
		СтруктураОшибки.НомерКолонки = СтрокаВЧисло(СтрПолучитьСтроку(Подстроки, 2), 1);
	КонецЕсли;
	
	СтруктураОшибки.ТекстОшибки = СтрШаблон(
		НСтр("ru = 'Найдена ошибка в выражении на встроенном языке:
			|%1'"),
		СтруктураОшибки.ТекстОшибки);
	
	Возврат СтруктураОшибки;
	
КонецФункции

#КонецОбласти

#Область КонструкторЗапросов

Асинх Процедура ВызватьКонструкторЗапроса(Знач ЭлементРедактор, Знач ПараметрыЗапроса)
	
	Если ПараметрыЗапроса = Неопределено Тогда
		ТекстВопроса = НСтр("ru = 'Не найден текст запроса.
			|Создать новый запрос?'");
		Ответ = Ждать ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНетАсинх(
			ТекстВопроса,
			ОбщегоНазначенияДокументооборотКлиент.ПараметрыВопросаДаНет(КодВозвратаДиалога.Да));
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ОткрытьКонструкторЗапроса(ЭлементРедактор, "", Неопределено);
		КонецЕсли;
	Иначе
		ТекстЗапроса = ПодготовитьТекстЗапроса(ПараметрыЗапроса.text);
		ОткрытьКонструкторЗапроса(ЭлементРедактор, ТекстЗапроса, ПараметрыЗапроса.range);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьКонструкторЗапроса(ЭлементРедактор, Текст, ПозицияТекста)
	
#Если Не ВебКлиент И Не МобильныйКлиент Тогда
	// В веб-клиенте не может быть обработано событие вызова конструктора запроса.
	Конструктор = Новый КонструкторЗапроса();
	
	Если ЗначениеЗаполнено(Текст) Тогда
		Попытка
			Конструктор.Текст = Текст;
		Исключение
			Инфо = ИнформацияОбОшибке();
			ПоказатьПредупреждение(,СтрШаблон(
				НСтр("ru = 'Ошибка в тексте запроса:
					|%1'"), Инфо.Причина.Описание));
			Возврат;
		КонецПопытки;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура("ЭлементРедактор, ПозицияТекста", ЭлементРедактор, ПозицияТекста);
	Оповещение = Новый ОписаниеОповещения("ПриЗакрытииКонструктораЗапросов", ЭтотОбъект, ПараметрыОповещения);
	Конструктор.Показать(Оповещение);
#КонецЕсли
	
КонецПроцедуры

Функция ПодготовитьТекстЗапроса(Текст)
	
	ТекстЗапроса = СтрЗаменить(Текст, "|", "");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """""", "$");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """", "");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "$", """");
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ПриЗакрытииКонструктораЗапросов(Текст, ПараметрыОповещения) Экспорт
	
	Если Текст <> Неопределено Тогда
		
		Если Не Редактор(ПараметрыОповещения.ЭлементРедактор).queryMode Тогда
			Текст = СтрЗаменить(Текст, Символы.ПС, Символы.ПС + "|");
			Текст = СтрЗаменить(Текст, """", """""");
			Текст = """" + Текст + """";
		КонецЕсли;
		
		УстановитьТекст(ПараметрыОповещения.ЭлементРедактор, Текст, ПараметрыОповещения.ПозицияТекста, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область КонструкторФорматнойСтроки

Асинх Процедура ВызватьКонструкторФорматнойСтроки(Знач ЭлементРедактор, Знач ПараметрыСтроки)
	
	Если ПараметрыСтроки = Неопределено Тогда
		ТекстВопроса = НСтр("ru = 'Форматная строка не найдена.
			|Создать новую форматную строку?'");
		Ответ = Ждать ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНетАсинх(
			ТекстВопроса,
			ОбщегоНазначенияДокументооборотКлиент.ПараметрыВопросаДаНет(КодВозвратаДиалога.Да));
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ОткрытьКонструкторФорматнойСтроки(ЭлементРедактор, "", Неопределено);
		КонецЕсли;
	Иначе
		ФорматнаяСтрока = СтрЗаменить(СтрЗаменить(ПараметрыСтроки.text, "|", ""), """", "");
		ОткрытьКонструкторФорматнойСтроки(ЭлементРедактор, ФорматнаяСтрока, ПараметрыСтроки.range);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьКонструкторФорматнойСтроки(ЭлементРедактор, ФорматнаяСтрока, ПозицияТекста)
	
#Если Не ВебКлиент И Не МобильныйКлиент Тогда
	// В веб-клиенте не может быть обработано событие вызова конструктора форматной строки.
	Конструктор = Новый КонструкторФорматнойСтроки();
	Попытка
		Конструктор.Текст = ФорматнаяСтрока;
	Исключение
		Инфо = ИнформацияОбОшибке();
		ПоказатьПредупреждение(,СтрШаблон(
			НСтр("ru = 'Ошибка в тексте форматной строки:
				|%1'"), Инфо.Причина.Описание));
		Возврат;
	КонецПопытки;
	
	ПараметрыОповещения = Новый Структура("ЭлементРедактор, ПозицияТекста", ЭлементРедактор, ПозицияТекста);
	Оповещение = Новый ОписаниеОповещения("ПриЗакрытииКонструктораФорматнойСтроки", ЭтотОбъект, ПараметрыОповещения);
	Конструктор.Показать(Оповещение);
#КонецЕсли
	
КонецПроцедуры

Процедура ПриЗакрытииКонструктораФорматнойСтроки(ФорматнаяСтрока, ПараметрыОповещения) Экспорт
	
	Если ФорматнаяСтрока <> Неопределено Тогда
		ФорматнаяСтрока = СтрЗаменить(ФорматнаяСтрока, "'", "");
		ФорматнаяСтрока = """" + ФорматнаяСтрока + """";
		УстановитьТекст(ПараметрыОповещения.ЭлементРедактор, ФорматнаяСтрока, ПараметрыОповещения.ПозицияТекста, Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Метаданные

Функция ЗагрузитьМодульМенеджераИлиОбъектаПоИмени(ЭлементРедактор, СтруктураВыгрузки)
	
	ЗагруженоФункций = 0;
	
#Если Не ВебКлиент Тогда
	// В веб-клиенте не поддерживается работа с файлами.
	// Содержимое модулей и контектсная подсказа к методам будут недоступны.
	Если СтруктураВыгрузки.ТипМодуля = "manager" Тогда
		Подкаталог = ВидМетаданных().МодулиМенеджеров;
	Иначе
		Подкаталог = ВидМетаданных().МодулиОбъектов;
	КонецЕсли;
	ИмяФайла = Новый Массив;
	ИмяФайла.Добавить(КаталогИсходников());
	ИмяФайла.Добавить(Подкаталог);
	ИмяФайла.Добавить(ПолучитьРазделительПути());
	ИмяФайла.Добавить(СтрШаблон("%1_%2.bsl", СтруктураВыгрузки.ТипМетаданных, СтруктураВыгрузки.ОбъектМетаданных));
	
	ФайлНаДиске = Новый Файл(СтрСоединить(ИмяФайла));
	
	Если ФайлНаДиске.Существует() Тогда
		Модуль = Новый ТекстовыйДокумент;
		Модуль.Прочитать(ФайлНаДиске.ПолноеИмя);
		АдресЗагрузки = РедакторВстроенногоЯзыкаВызовСервера.АдресЗагрузкиМодуляВРедактор(СтруктураВыгрузки);
		ЗагруженоФункций = Редактор(ЭлементРедактор).parseMetadataModule(Модуль.ПолучитьТекст(), АдресЗагрузки);
	КонецЕсли;
#КонецЕсли
	
	Возврат ЗагруженоФункций;
	
КонецФункции

Функция ЗагрузитьОбщийМодульПоИмени(ЭлементРедактор, ИмяМодуля, Глобальный)
	
	ЗагруженоФункций = 0;
	
#Если Не ВебКлиент Тогда
	// В веб-клиенте не поддерживается работа с файлами.
	// Содержимое модулей и контектсная подсказа к методам будут недоступны.
	ИмяФайла = Новый Массив;
	ИмяФайла.Добавить(КаталогИсходников());
	Если Глобальный Тогда
		ИмяФайла.Добавить(ВидМетаданных().ГлобальныеМодули);
	Иначе
		ИмяФайла.Добавить(ВидМетаданных().ОбщиеМодули);
	КонецЕсли;
	ИмяФайла.Добавить(ПолучитьРазделительПути());
	ИмяФайла.Добавить(СтрШаблон("%1.bsl", ИмяМодуля));
	
	ФайлНаДиске = Новый Файл(СтрСоединить(ИмяФайла));
	
	Если ФайлНаДиске.Существует() Тогда
		ОбщийМодуль = Новый ТекстовыйДокумент;
		ОбщийМодуль.Прочитать(ФайлНаДиске.ПолноеИмя);
		ЗагруженоФункций = Редактор(ЭлементРедактор).parseCommonModule(ИмяМодуля, ОбщийМодуль.ПолучитьТекст(), Глобальный);
	КонецЕсли;
#КонецЕсли
	
	Возврат ЗагруженоФункций;
	
КонецФункции

Функция ЗагрузитьОбъектМетаданныхПоЗапросу(ЭлементРедактор, ПараметрыЗапроса)
	
	ОписаниеОбъектаМетаданных = РедакторВстроенногоЯзыкаВызовСервера.ОписаниеОбъектаМетаданных(ПараметрыЗапроса);
	Результат = Редактор(ЭлементРедактор).updateMetadata(
		ЗначениеВJSON(ОписаниеОбъектаМетаданных.Свойства),
		ОписаниеОбъектаМетаданных.АдресОбновления);
	
	Возврат (Результат = Истина);
	
КонецФункции

Функция ЗагрузитьСписокМетаданныхПоЗапросу(ЭлементРедактор, ПараметрыЗапроса)
	
	МетаданныеДляОбновления = РедакторВстроенногоЯзыкаВызовСервера.МетаданныеДляОбновления(ПараметрыЗапроса);
	Результат = Редактор(ЭлементРедактор).updateMetadata(
		ЗначениеВJSON(МетаданныеДляОбновления.СписокОбъектов),
		МетаданныеДляОбновления.АдресОбновления);
	
	Возврат (Результат = Истина);
	
КонецФункции

Процедура ЗагрузитьСтруктуруМодуляМенеджераИлиОбъектаПоЗапросу(Результат, ПараметрыОповещения) Экспорт
	
	ДанныеОбновлены = Ложь;
	
	Если Результат = Истина И ПараметрыОповещения.ЧастиЗапроса.Количество() = 4 Тогда
		СтруктураВыгрузки = Новый Структура;
		СтруктураВыгрузки.Вставить("ТипМодуля", ПараметрыОповещения.ЧастиЗапроса[1]);
		СтруктураВыгрузки.Вставить("ТипМетаданных" , ПараметрыОповещения.ЧастиЗапроса[2]);
		СтруктураВыгрузки.Вставить("ОбъектМетаданных" , ПараметрыОповещения.ЧастиЗапроса[3]);
		ЗагруженоФункций = ЗагрузитьМодульМенеджераИлиОбъектаПоИмени(
			ПараметрыОповещения.ЭлементРедактор, СтруктураВыгрузки);
		ДанныеОбновлены = (ЗагруженоФункций > 0);
	КонецЕсли;
	
	ПослеПолученияМетаданных(ПараметрыОповещения.ЭлементРедактор, ПараметрыОповещения.Событие, ДанныеОбновлены);
	
КонецПроцедуры

Процедура ЗагрузитьСтруктуруОбщегоМодуляПоЗапросу(Результат, ПараметрыОповещения) Экспорт
	
	ДанныеОбновлены = Ложь;
	
	Если Результат = Истина И ПараметрыОповещения.ЧастиЗапроса.Количество() = 2 Тогда
		ИмяМодуля = ПараметрыОповещения.ЧастиЗапроса[1];
		ЗагруженоФункций = ЗагрузитьОбщийМодульПоИмени(ПараметрыОповещения.ЭлементРедактор, ИмяМодуля, Ложь);
		ДанныеОбновлены = (ЗагруженоФункций > 0);
	КонецЕсли;
	
	ПослеПолученияМетаданных(ПараметрыОповещения.ЭлементРедактор, ПараметрыОповещения.Событие, ДанныеОбновлены);
	
КонецПроцедуры

Процедура ОбновитьМетаданные(ЭлементРедактор, ДанныеРедактора, УникальныйИдентификатор)
	
	Редактор(ЭлементРедактор).clearMetadata();
	
	КоллекцияОбщихМодулей = ПолучитьИзВременногоХранилища(ДанныеРедактора.КоллекцияОбщихМодулей);
	Если КоллекцияОбщихМодулей <> Неопределено Тогда
		Редактор(ЭлементРедактор).updateMetadata(
			ЗначениеВJSON(КоллекцияОбщихМодулей.Общие),
			"commonModules.items");
		ИзвлечьГлобальныеМодули(УникальныйИдентификатор);
		Для Каждого ГлобальныйМодуль Из КоллекцияОбщихМодулей.Глобальные Цикл
			ЗагрузитьОбщийМодульПоИмени(ЭлементРедактор, ГлобальныйМодуль.Ключ, Истина);
		КонецЦикла;
	КонецЕсли;
	
#Если ВебКлиент Тогда
	// Из-за того что в веб-клиенте не может быть обработано событие получения метаданных - сразу
	// загрузим списки основных метаданных, без детализации.
	ЗагрузитьСписокМетаданныхПоЗапросу(ЭлементРедактор, "catalogs");
	ЗагрузитьСписокМетаданныхПоЗапросу(ЭлементРедактор, "documents");
	ЗагрузитьСписокМетаданныхПоЗапросу(ЭлементРедактор, "infoRegs");
	ЗагрузитьСписокМетаданныхПоЗапросу(ЭлементРедактор, "accumRegs");
	ЗагрузитьСписокМетаданныхПоЗапросу(ЭлементРедактор, "dataProc");
	ЗагрузитьСписокМетаданныхПоЗапросу(ЭлементРедактор, "enums");
	ЗагрузитьСписокМетаданныхПоЗапросу(ЭлементРедактор, "businessProcesses");
	ЗагрузитьСписокМетаданныхПоЗапросу(ЭлементРедактор, "tasks");
	ЗагрузитьСписокМетаданныхПоЗапросу(ЭлементРедактор, "exchangePlans");
	ЗагрузитьСписокМетаданныхПоЗапросу(ЭлементРедактор, "chartsOfCharacteristicTypes");
	ЗагрузитьСписокМетаданныхПоЗапросу(ЭлементРедактор, "constants");
#КонецЕсли
	
КонецПроцедуры

Процедура ОбработкаСобытияПолученияМетаданных(ЭлементРедактор, Событие, УникальныйИдентификатор)
	
	ПараметрыЗапроса = НРег(Событие.metadata);
	ДанныеОбновлены = Ложь;
	ЧастиЗапроса = СтрРазделить(ПараметрыЗапроса, ".");
	
	Если ЧастиЗапроса.Количество() > 1 Тогда
		Если ЧастиЗапроса[0] = "module" Тогда
			Если ЧастиЗапроса.Количество() = 2 Тогда
				ПараметрыОповещения = Новый Структура("ЭлементРедактор, Событие, ЧастиЗапроса",
					ЭлементРедактор, Событие, ЧастиЗапроса);
				ПослеЗагрузки = Новый ОписаниеОповещения("ЗагрузитьСтруктуруОбщегоМодуляПоЗапросу",
					ЭтотОбъект, ПараметрыОповещения);
				ИзвлечьОбщиеМодули(ЭлементРедактор, УникальныйИдентификатор, ПослеЗагрузки);
			Иначе
				ПараметрыОповещения = Новый Структура("ЭлементРедактор, Событие, ЧастиЗапроса",
					ЭлементРедактор, Событие, ЧастиЗапроса);
				ПослеЗагрузки = Новый ОписаниеОповещения("ЗагрузитьСтруктуруМодуляМенеджераИлиОбъектаПоЗапросу",
					ЭтотОбъект, ПараметрыОповещения);
				ИзвлечьМодулиМенеджераИлиОбъекта(ЭлементРедактор, УникальныйИдентификатор, ЧастиЗапроса, ПослеЗагрузки);
			КонецЕсли;
			Возврат;
		Иначе
			ДанныеОбновлены = ЗагрузитьОбъектМетаданныхПоЗапросу(ЭлементРедактор, ПараметрыЗапроса);
		КонецЕсли;
	Иначе
		ДанныеОбновлены = ЗагрузитьСписокМетаданныхПоЗапросу(ЭлементРедактор, ПараметрыЗапроса);
	КонецЕсли;
	
	ПослеПолученияМетаданных(ЭлементРедактор, Событие, ДанныеОбновлены);
	
КонецПроцедуры

Процедура ПослеПолученияМетаданных(ЭлементРедактор, Событие, ДанныеОбновлены)
	
	Если Событие.trigger = "suggestion" И ДанныеОбновлены Тогда
		Редактор(ЭлементРедактор).triggerSuggestions();
	КонецЕсли;
	
	Если Событие.trigger = "snippet" Тогда
		Редактор(ЭлементРедактор).updateSnippetByGUID(Событие.snippet_guid);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Функция ДанныеРеквизита(Реквизит)
	
	ДанныеРеквизита = Новый Структура("name", Реквизит.Ключ);
	
	Если ТипЗнч(Реквизит.Значение) = Тип("Строка") И СтрНайти(Реквизит.Значение, ".") > 0 Тогда
		ДанныеРеквизита.Вставить("ref", Реквизит.Значение);
	КонецЕсли;
	
	Если ТипЗнч(Реквизит.Значение) = Тип("Структура") Тогда
		ПодчиненныеРеквизиты = Новый Структура;
		Для Каждого ПодчиненныйРеквизит Из Реквизит.Значение Цикл
			ПодчиненныеРеквизиты.Вставить(ПодчиненныйРеквизит.Ключ, ДанныеРеквизита(ПодчиненныйРеквизит));
		КонецЦикла;
		ДанныеРеквизита.Вставить("properties", ПодчиненныеРеквизиты);
	КонецЕсли;
	
	Возврат ДанныеРеквизита;
	
КонецФункции

Функция ЗначениеВJSON(Знач Значение)
	
#Если ВебКлиент Тогда
	// ЗаписьJSON недоступна в веб-клиенте.
	Возврат РедакторВстроенногоЯзыкаВызовСервера.ЗначениеВJSON(Значение);
#Иначе
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Значение);
	Возврат ЗаписьJSON.Закрыть();
#КонецЕсли
	
КонецФункции

Процедура ИзвлечьГлобальныеМодули(УникальныйИдентификатор)
	
#Если Не ВебКлиент Тогда
	// В веб-клиенте не поддерживается работа с файлами.
	// Содержимое модулей и контектсная подсказа к методам будут недоступны.
	Подкаталог = ВидМетаданных().ГлобальныеМодули;
	КаталогМодулей = Новый Файл(КаталогИсходников() + Подкаталог);
	Если Не КаталогМодулей.Существует() Тогда
		АдресМакета = РедакторВстроенногоЯзыкаВызовСервера.АдресМакетаГлобальныеМодули(УникальныйИдентификатор);
		ВерсияИБ = РедакторВстроенногоЯзыкаВызовСервера.ВерсияИБ();
		ИзвлечьАрхив(АдресМакета, Подкаталог, ВерсияИБ);
	КонецЕсли;
#КонецЕсли
	
КонецПроцедуры

Процедура ИзвлечьМодулиМенеджераИлиОбъекта(ЭлементРедактор, УникальныйИдентификатор, ЧастиЗапроса, ПослеЗагрузки)
	
#Если Не ВебКлиент Тогда
	// В веб-клиенте не поддерживается работа с файлами.
	// Содержимое модулей и контектсная подсказа к методам будут недоступны.
	ТипМодуля = ЧастиЗапроса[1];
	Если ТипМодуля = "manager" Тогда
		ПараметрыФормы = Новый Структура("ИзвлечьМодулиМенеджеров", Истина);
		Подкаталог = ВидМетаданных().МодулиМенеджеров;
	Иначе
		ПараметрыФормы = Новый Структура("ИзвлечьМодулиОбъектов", Истина);
		Подкаталог = ВидМетаданных().МодулиОбъектов;
	КонецЕсли;
	КаталогМодулей = Новый Файл(КаталогИсходников() + Подкаталог);
	Если КаталогМодулей.Существует() Тогда
		ВыполнитьОбработкуОповещения(ПослеЗагрузки, Истина);
	Иначе
		ОткрытьФорму("ОбщаяФорма.РаспаковкаРедактораВстроенногоЯзыка", ПараметрыФормы,,,,, ПослеЗагрузки);
	КонецЕсли;
#КонецЕсли
	
КонецПроцедуры

Процедура ИзвлечьОбщиеМодули(ЭлементРедактор, УникальныйИдентификатор, ПослеЗагрузки)
	
#Если Не ВебКлиент Тогда
	// В веб-клиенте не поддерживается работа с файлами.
	// Содержимое модулей и контектсная подсказа к методам будут недоступны.
	Подкаталог = ВидМетаданных().ОбщиеМодули;
	КаталогМодулей = Новый Файл(КаталогИсходников() + Подкаталог);
	Если КаталогМодулей.Существует() Тогда
		ВыполнитьОбработкуОповещения(ПослеЗагрузки, Истина);
	Иначе
		ПараметрыФормы = Новый Структура("ИзвлечьОбщиеМодули", Истина);
		ОткрытьФорму("ОбщаяФорма.РаспаковкаРедактораВстроенногоЯзыка", ПараметрыФормы,,,,, ПослеЗагрузки);
	КонецЕсли;
#КонецЕсли
	
КонецПроцедуры

Процедура ИзвлечьРедактор(Форма)
	
#Если Не ВебКлиент Тогда
	// Файл "ВерсияИБ.ver" необходим для отслеживания необходимости обновления временных файлов,
	// хранящих содержимое модулей конфигурации.
	// В веб-клиенте этот функционал не поддерживается.
	ВерсияИБ = РедакторВстроенногоЯзыкаВызовСервера.ВерсияИБ();
	ВерсияИБРедактора = "";
	КаталогИсходников = Новый Файл(КаталогИсходников());
	Если КаталогИсходников.Существует() Тогда
		ФайлВерсии = Новый Файл(КаталогИсходников() + "ВерсияИБ.ver");
		Если ФайлВерсии.Существует() Тогда
			ТекстовыйДокумент = Новый ТекстовыйДокумент;
			ТекстовыйДокумент.Прочитать(ФайлВерсии.ПолноеИмя);
			ВерсияИБРедактора = ТекстовыйДокумент.ПолучитьСтроку(1);
		КонецЕсли;
		Если ВерсияИБ <> ВерсияИБРедактора Тогда
			УдалитьФайлы(КаталогИсходников());
		КонецЕсли;
	КонецЕсли;
#КонецЕсли
	
	Для Каждого РеквизитРедактор Из РеквизитыРедактор(Форма) Цикл
		Форма[СокрЛП(РеквизитРедактор)] = ПутьКДаннымРедактора();
	КонецЦикла;
	
КонецПроцедуры

Функция ИмяЭлементаСтраницаРедактор(РеквизитРедактор)
	
	Возврат СтрШаблон("Страница%1", РеквизитРедактор);
	
КонецФункции

Функция ИмяЭлементаСтраницыРедактора(РеквизитРедактор)
	
	Возврат СтрШаблон("%1Страницы", РеквизитРедактор);
	
КонецФункции

Процедура ОбновитьКонтекстВРедакторе(ЭлементРедактор, Контекст)
	
	Объекты = Новый Соответствие;
	Для Каждого ЭлементКонтекста Из Контекст Цикл
		Объекты.Вставить(ЭлементКонтекста.Ключ, ДанныеРеквизита(ЭлементКонтекста));
	КонецЦикла;
	Редактор(ЭлементРедактор).updateMetadata(ЗначениеВJSON(Новый Структура("customObjects", Объекты)));
	
КонецПроцедуры

Процедура ОбработатьСобытиеРедактора(ЭлементРедактор, ДанныеРедактора, Событие, Модифицированность,
		УникальныйИдентификатор, НажатаКнопкаEsc)
	
	Если Событие = Неопределено Тогда
#Если ВебКлиент Тогда
		// В веб-клиенте нет возможности обрабатывать события редактора.
		// Параметр "Событие" будет всегда равен "Неопределено".
		// Будем отслеживать только необходимость установки признака модифицированности формы.
		Если Не Модифицированность И ОригинальныйКодИзменен(ЭлементРедактор, ДанныеРедактора) Тогда
			Модифицированность = Истина;
		КонецЕсли;
#КонецЕсли
		Возврат;
	КонецЕсли;
	
	ИмяСобытия = Событие.event;
	Если ИмяСобытия = "EVENT_CONTENT_CHANGED" Тогда
		Модифицированность = Истина;
		
	ИначеЕсли ИмяСобытия = "EVENT_ON_KEY_ESC" Тогда
		НажатаКнопкаEsc = Истина;
		
	ИначеЕсли ИмяСобытия = "EVENT_GET_METADATA" Тогда
		ОбработкаСобытияПолученияМетаданных(ЭлементРедактор, Событие.params, УникальныйИдентификатор);
		
	ИначеЕсли ИмяСобытия = "EVENT_GET_VARIABLE_DATA" Тогда
		ОбработкаСобытияПолученияПеременной(ЭлементРедактор, ДанныеРедактора, Событие.params);
		
	ИначеЕсли ИмяСобытия = "EVENT_QUERY_CONSTRUCT" Тогда
		ВызватьКонструкторЗапроса(ЭлементРедактор, Событие.params);
		
	ИначеЕсли ИмяСобытия = "EVENT_FORMAT_CONSTRUCT" Тогда
		ВызватьКонструкторФорматнойСтроки(ЭлементРедактор, Событие.params);
		
	ИначеЕсли ИмяСобытия = "EVENT_ON_LINK_CLICK" И СтрНайти(Событие.params.href, "e1cib") > 0 Тогда
		ПерейтиПоНавигационнойСсылке(Событие.params.href);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ОригинальныйКодИзменен(ЭлементРедактор, ДанныеРедактора)
	
	МассивОригинальныйКод = СтрРазделить(ДанныеРедактора.ОригинальныйКод, Символы.ПС);
	МассивТекущийКод = СтрРазделить(ПолучитьТекст(ЭлементРедактор), Символы.ПС);
	
	Если МассивОригинальныйКод.Количество() <> МассивТекущийКод.Количество() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Для Индекс = 0 По МассивОригинальныйКод.ВГраница() Цикл
		Если СокрЛП(МассивОригинальныйКод[Индекс]) <> СокрЛП(МассивТекущийКод[Индекс]) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Процедура ПереключитьНаСтраницуРедактора(Форма)
	
	Для Каждого РеквизитРедактор Из РеквизитыРедактор(Форма) Цикл
		Форма.Элементы[ИмяЭлементаСтраницыРедактора(РеквизитРедактор)].ТекущаяСтраница =
			Форма.Элементы[ИмяЭлементаСтраницаРедактор(РеквизитРедактор)];
	КонецЦикла;
	
КонецПроцедуры

Функция Редактор(ЭлементРедактор)
	
	Возврат ЭлементРедактор.Документ.defaultView;
	
КонецФункции

Функция РедакторДоступен(ЭлементРедактор)
	
	Если ЭлементРедактор.Документ <> Неопределено И ЭлементРедактор.Документ.defaultView <> Неопределено Тогда
		Попытка
			Редактор(ЭлементРедактор).getReadOnly();
			Возврат Истина;
		Исключение
			Возврат Ложь;
		КонецПопытки;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция РеквизитыРедактор(Форма)
	
	Возврат СтрРазделить(Форма.ДанныеРедактора.ИмяРеквизитаРедактор, ",", Ложь);
	
КонецФункции

Функция СтрокаВЧисло(Значение, ЗначениеПоУмолчанию = 0)
	
	СтрокаБезНулей = СтрЗаменить(Значение, "0", "");
	Если ПустаяСтрока(СтрокаБезНулей) Или СтрокаБезНулей = "-" Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	ТипЧисло = Новый ОписаниеТипов("Число");
	Результат = ТипЧисло.ПривестиЗначение(Значение);
	
	Возврат ?(Результат <> 0 И Не ПустаяСтрока(СтрокаБезНулей), Результат, ЗначениеПоУмолчанию);
	
КонецФункции

Процедура УстановитьОригинальныйКод(ЭлементРедактор, ОригинальныйКод)
	
	Редактор(ЭлементРедактор).setOriginalText(ОригинальныйКод);
	
КонецПроцедуры

#КонецОбласти
