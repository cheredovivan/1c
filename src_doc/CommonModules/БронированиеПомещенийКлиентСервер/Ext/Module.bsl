////////////////////////////////////////////////////////////////////////////////
// Бронирование помещений
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Формирует текстовое представление дат брони.
//
// Параметры:
//  ДатаНачала - Дата - Дата начала отсутствия.
//  ДатаОкончания - Дата - Дата окончания отсутствия.
//  ВесьДень - Булево - Признак того что отсутствие на весь день.
//
// Возвращаемое значение:
//  Строка - Текстовое представление даты отсутствия.
//
Функция СформироватьДатыБрони(ДатаНачала, ДатаОкончания, ВесьДень) Экспорт
	
	ОписаниеДаты = "";
	
	Если НачалоДня(ДатаОкончания) - НачалоДня(ДатаНачала) = 0 Тогда
		День = СформироватьТекстовоеОписаниеДаты(ДатаНачала, Истина);
		Если ВесьДень Тогда
			ОписаниеДаты = День;
		Иначе
			ВремяНачала = Формат(ДатаНачала, "ДФ='HH:mm'");
			ВремяОкончания = Формат(ДатаОкончания, "ДФ='HH:mm'");
			ОписаниеДаты = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 с %2 по %3'"), День, ВремяНачала, ВремяОкончания);
		КонецЕсли;
	Иначе
		ОписаниеДатыНачала = СформироватьТекстовоеОписаниеДаты(ДатаНачала, ВесьДень);
		ОписаниеДатыОкончания = СформироватьТекстовоеОписаниеДаты(ДатаОкончания, ВесьДень);
		ОписаниеДаты = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'с %1 по %2'"), ОписаниеДатыНачала, ОписаниеДатыОкончания);
	КонецЕсли;
	
	Возврат ОписаниеДаты;
	
КонецФункции

// Формирует текстовое представление даты.
//
// Параметры:
//  Дата - Дата - Дата, текстовое представление которой необходимо сформировать.
//  ВесьДень - Булево - Признак того, что в дату не нужно включать время.
//
// Возвращаемое значение:
//  Строка - Текстовое представление даты.
//
Функция СформироватьТекстовоеОписаниеДаты(Дата, ВесьДень) Экспорт
	
	ФорматДаты = "d MMMM yyyy";
	Если Не ВесьДень Тогда
		ФорматДаты = ФорматДаты + " HH:mm";
	КонецЕсли;
	
	ФорматДаты = "ДФ='" + ФорматДаты + "'";
	ОписаниеДаты = Формат(Дата, ФорматДаты);
	
	Возврат ОписаниеДаты;
	
КонецФункции

// Формирует структуру параметров брони.
//
// Возвращаемое значение:
//  Структура - Структура параметров брони.
//
Функция ПолучитьПараметрыБрони() Экспорт
	
	ПараметрыБрони = Новый Структура;
	
	// Заполняемые
	ПараметрыБрони.Вставить("Дата");
	ПараметрыБрони.Вставить("ВремяНачала");
	ПараметрыБрони.Вставить("ВремяОкончания");
	ПараметрыБрони.Вставить("Вместимость");
	ПараметрыБрони.Вставить("Расположение");
	
	Возврат ПараметрыБрони;
	
КонецФункции

// Формирует структуру общего результата броней.
// 
// Возвращаемое значение:
//  Структура -- Общий результат броней::
//   * РезультатыБроней - Массив из Структура - Результаты каждой брони::
//      ** Бронь - ДокументСсылка.Бронь - Бронь.
//      ** ОжидаетПодтверждения - Булево - Бронь ожидает подтверждения.
//   * ОтмененныеБрони - Массив из ДокументСсылка.Бронь - Брони, для которых была выполнена отмена.
//   * ДлительнаяОперацияПодтверждения - Структура - Длительная операция оперативного подтверждения брони. См. ДлительныеОперации.ВыполнитьВФоне.
//                                       Неопределено - Длительная операция не запущена.
//
Функция РезультатБронирования() Экспорт
	
	РезультатБронирования = Новый Структура("РезультатыБроней, ОтмененныеБрони, ДлительнаяОперацияПодтверждения");
	РезультатБронирования.РезультатыБроней = Новый Массив;
	РезультатБронирования.ОтмененныеБрони = Новый Массив;
	
	Возврат РезультатБронирования;
	
КонецФункции

// Возвращает событие журнала регистрации подсистемы "Бронирование помещений".
// 
// Возвращаемое значение:
//  Строка - Событие журнала регистрации подсистемы "Бронирование помещений".
//
Функция СобытиеЖурналаРегистрации() Экспорт
	
	Возврат НСтр("ru = 'Бронирование помещений'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	
КонецФункции

// Дополняет общий результат броней.
//
// Параметры:
//  РезультатБронированияПриемник - Структура - Общий результат броней, которые дополняется.
//  РезультатБронированияИсточник - Структура - Общий результат броней, из которого происходит дополнение.
//
Процедура ДополнитьРезультатБронирования(РезультатБронированияПриемник, РезультатБронированияИсточник) Экспорт
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		РезультатБронированияПриемник.РезультатыБроней,
		РезультатБронированияИсточник.РезультатыБроней);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		РезультатБронированияПриемник.ОтмененныеБрони,
		РезультатБронированияИсточник.ОтмененныеБрони);
	
КонецПроцедуры

// Формирует имя ключевой операции "БронированиеОткрытиеБронирования".
// 
// Возвращаемое значение:
//  Строка
// 
Функция КлючеваяОперацияОткрытиеБронирования() Экспорт
	
	КлючеваяОперация = "БронированиеОткрытиеБронирования";
	
	Возврат КлючеваяОперация;
	
КонецФункции

// Формирует имя ключевой операции "БронированиеОбновлениеПланировщика".
// 
// Возвращаемое значение:
//  Строка
// 
Функция КлючеваяОперацияОбновлениеПланировщика() Экспорт
	
	КлючеваяОперация = "БронированиеОбновлениеПланировщика";
	
	Возврат КлючеваяОперация;
	
КонецФункции

// Формирует имя ключевой операции "БронированиеОткрытиеБрони".
// 
// Возвращаемое значение:
//  Строка
// 
Функция КлючеваяОперацияОткрытиеБрони() Экспорт
	
	КлючеваяОперация = "БронированиеОткрытиеБрони";
	
	Возврат КлючеваяОперация;
	
КонецФункции

// Формирует имя ключевой операции "БронированиеСозданиеБрони".
// 
// Возвращаемое значение:
//  Строка
// 
Функция КлючеваяОперацияСозданиеБрони() Экспорт
	
	КлючеваяОперация = "БронированиеСозданиеБрони";
	
	Возврат КлючеваяОперация;
	
КонецФункции

// Формирует имя ключевой операции "БронированиеЗаписьБрони".
// 
// Возвращаемое значение:
//  Строка
// 
Функция КлючеваяОперацияЗаписьБрони() Экспорт
	
	КлючеваяОперация = "БронированиеЗаписьБрони";
	
	Возврат КлючеваяОперация;
	
КонецФункции

#КонецОбласти