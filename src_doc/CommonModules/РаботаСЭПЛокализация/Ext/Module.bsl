
#Область ПрограммныйИнтерфейс

// См. РаботаСЭП.ОписаниеЭПДляОтметки
// Позволяет дополнить описание ЭП для отметки
// Параметры:
//  ЭП - РегистрСведенийЗапись.ЭлектронныеПодписи - электронная подпись.
//  ОписаниеЭП - Структура
Процедура ДополнитьОписаниеЭПДляОтметки(ЭП, ОписаниеЭП) Экспорт
// Локализация
	ОписаниеЭП.ДанныеДоверенности = ОписаниеДоверенностиЭПДляОтметки(ЭП.ИдентификаторПодписи);
// Конец Локализация
КонецПроцедуры

// См. РаботаСЭП.НовоеОписаниеОтметкиЭП
// Позволяет дополнить описание отметки электронной подписи
// 
// Параметры:
//  ОписаниеЭП - Структура
Процедура ДополнитьНовоеОписаниеОтметкиЭП(ОписаниеЭП) Экспорт
	// Локализация
	НовыеДанныеДоверенности = НовоеОписаниеДоверенностиЭПДляОтметки();
	ОписаниеЭП.Вставить("ДанныеДоверенности", НовыеДанныеДоверенности);
	// Конец Локализация
КонецПроцедуры

// Дополняет параметры формирования набора важных полей для версии.
//
// Параметры:
//  ПараметрыФормирования - Структура - Параметры формирования набора важных полей, дополняемые в процедуре.
//  ДополнительныеПараметры - Структура - Дополнительные параметры
//
Процедура ДополнитьПараметрыФормированияНабораВажныхПолей(ПараметрыФормирования, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Создает новое описание файла.
//
// Возвращаемое значение:
//  Структура - Описание файла, содержащее следующие ключи:
//   * ИмяФайла - Строка - Имя файла.
//   * ДвоичныеДанные - Неопределено - Двоичные данные файла.
//
Функция НовоеОписаниеФайла() Экспорт

	ОписаниеФайла = Новый Структура();
	ОписаниеФайла.Вставить("ИмяФайла", "");
	ОписаниеФайла.Вставить("ДвоичныеДанные", Неопределено);
// Локализация
	ОписаниеФайла = РаботаСФайламиБЭД.НовоеОписаниеФайла();
// Конец Локализация

	Возврат ОписаниеФайла;

КонецФункции

// Дополняет возможные варианты проверок для объекта
//
// Параметры:
//  Объект - СправочникСсылка.ДокументыПредприятия - проверяемый объект.
//  Версия - Число - версия объекта.
//  ВозможныеВариантыПроверок - Массив - массив возможных вариантов проверок.
//  ИменаПараметров - Массив - массив имен параметров.
//
Процедура ДополнитьВозможныеВариантыПроверок(Объект, Версия, ВозможныеВариантыПроверок, ИменаПараметров) Экспорт
	
	// Локализация
	// Повторная проверка 5-й версии подписи для внутренних документов, отправленных по ЭДО.
		Если ДелопроизводствоКлиентСервер.ЭтоДокументПредприятия(Объект) И Версия = 5 Тогда
			ДобавитьПроверкиДляИсходящихЭДО(Объект, ВозможныеВариантыПроверок, ИменаПараметров);
		КонецЕсли;
	// Конец Локализация
	
КонецПроцедуры

// Дополняет статусы работы с подписанными объектами
// 
// Параметры:
//  ПодписанныеОбъекты - Массив из ОпределяемыйТип.ПодписанныйОбъект - 
//  СтатусыПоОбъектам - Соответствие из КлючИЗначение:
// * Ключ - ОпределяемыйТип.ПодписанныйОбъект - 
// * Значение - Строка - См. РаботаСЭПКлиентСервер.СтатусыРаботыСПодписаннымиОбъектами
// 
Процедура ПриОпределенииСтатусовРаботыПоПодписаннымОбъектам(ПодписанныеОбъекты, СтатусыПоОбъектам) Экспорт
	
	// Локализация
	Статусы = РаботаСЭПКлиентСервер.СтатусыРаботыСПодписаннымиОбъектами();
	
	СтатусыПоОбъектам = Новый Соответствие();
	Для Каждого Объект Из ПодписанныеОбъекты Цикл
		СтатусыПоОбъектам[Объект] = Статусы.НеОпределен;
	КонецЦикла;
	
	ОбъектыРаботыПоПодписаннымОбъектам = РаботаСЭП.ОбъектыРаботыПодписанныхОбъектов(ПодписанныеОбъекты);
	
	ДокументыЭДО = Новый Массив();
	
	Для Каждого Элемент Из ОбъектыРаботыПоПодписаннымОбъектам Цикл
		
		ОбъектРаботы = Элемент.Значение;
		
		Если Не ЗначениеЗаполнено(ОбъектРаботы) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ОбъектРаботы) = Тип("ДокументСсылка.ЭлектронныйДокументВходящийЭДО")
			Или ТипЗнч(ОбъектРаботы) = Тип("ДокументСсылка.ЭлектронныйДокументИсходящийЭДО") Тогда
			
			ДокументыЭДО.Добавить(ОбъектРаботы);
		КонецЕсли;
		
	КонецЦикла;
	
	СтатусыДокументовЭДО = СтатусыРаботыПоДокументамЭДО(ДокументыЭДО);
	
	Для Каждого Объект Из ПодписанныеОбъекты Цикл
		
		ОбъектРаботы = ОбъектыРаботыПоПодписаннымОбъектам[Объект];
		Если Не ЗначениеЗаполнено(ОбъектРаботы) Тогда
			Продолжить;
		КонецЕсли;
		
		Статус = Неопределено;
		Если ТипЗнч(ОбъектРаботы) = Тип("ДокументСсылка.ЭлектронныйДокументВходящийЭДО")
			Или ТипЗнч(ОбъектРаботы) = Тип("ДокументСсылка.ЭлектронныйДокументИсходящийЭДО") Тогда
			
			Статус = СтатусыДокументовЭДО[ОбъектРаботы];
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Статус) Тогда
			СтатусыПоОбъектам[Объект] = Статус;
		КонецЕсли;
		
	КонецЦикла;
	// Конец Локализация
	
КонецПроцедуры

// Дополняет соответствие объектов с которыми может вестись работа по подписанным объектам
// 
// Параметры:
//  ПодписанныеОбъекты - Массив Из ОпределяемыйТип.ПодписанныйОбъект
//  ОбъектыРаботы - Соответствие Из КлючИЗначение:
//    * Ключ - ОпределяемыйТип.ПодписанныйОбъект
//    * Значение - СправочникСсылка.ДокументыПредприятия -
// Локализация
//               - ДокументСсылка.ЭлектронныйДокументВходящийЭДО -
//               - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО -
// Конец Локализация
//               - Неопределено -
//
Процедура ДополнитьОбъектыРаботыПодписанныхОбъектов(ПодписанныеОбъекты, ОбъектыРаботы) Экспорт
	
	// Локализация
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СообщениеЭДОПрисоединенныеФайлы.Ссылка КАК ФайлСообщенияЭДО,
		|	СообщениеЭДОПрисоединенныеФайлы.ВладелецФайла КАК Сообщение
		|ПОМЕСТИТЬ ПодписанныеФайлыСообщенияЭДО
		|ИЗ
		|	Справочник.СообщениеЭДОПрисоединенныеФайлы КАК СообщениеЭДОПрисоединенныеФайлы
		|ГДЕ
		|	СообщениеЭДОПрисоединенныеФайлы.Ссылка В (&ПодписанныеОбъекты)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПодписанныеФайлыСообщенияЭДО.ФайлСообщенияЭДО КАК ПодписанныйОбъект,
		|	СообщениеЭДО.ЭлектронныйДокумент КАК ДокументЭДО
		|ИЗ
		|	ПодписанныеФайлыСообщенияЭДО КАК ПодписанныеФайлыСообщенияЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СообщениеЭДО КАК СообщениеЭДО
		|		ПО ПодписанныеФайлыСообщенияЭДО.Сообщение = СообщениеЭДО.Ссылка";
	Запрос.УстановитьПараметр("ПодписанныеОбъекты", ПодписанныеОбъекты);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаПоДокументамЭДО = РезультатыЗапроса[РезультатыЗапроса.ВГраница()].Выбрать();
	
	Пока ВыборкаПоДокументамЭДО.Следующий() Цикл
		ОбъектыРаботы[ВыборкаПоДокументамЭДО.ПодписанныйОбъект] = ВыборкаПоДокументамЭДО.ДокументЭДО;
	КонецЦикла;
	// Конец Локализация
	
КонецПроцедуры

// Заполняет поля по умолчанию для настройки штампа ЭП
// 
// Параметры:
//  Форма - УправляемаяФорма
Процедура ЗаполнитьШтампЭППоУмолчанию(Форма) Экспорт
	
	// Локализация
	Форма.РазмерШтампа = 1;// средний
	Форма.ОсновнаяНадписьКЭП = НСтр("ru = 'ДОКУМЕНТ ПОДПИСАН ЭЛЕКТРОННОЙ ПОДПИСЬЮ'");
	Форма.ОсновнаяНадписьПЭП = НСтр("ru = 'ДОКУМЕНТ ПОДПИСАН ПРОСТОЙ ЭЛЕКТРОННОЙ ПОДПИСЬЮ'");
	Форма.Поле1 = "[Сертификат]";
	Форма.Поле2 = "[Владелец]";
	Форма.Поле3 = "[Подписант]";
	Форма.Поле4 = "[СрокДействия]";
	Форма.Поле5 = "[Доверитель]";
	Форма.Поле6 = "[Доверенность]";
	Форма.Поле7 = "";
	Форма.СкругленныеУглы = Ложь;
	Форма.Логотип = "";
	
	Форма.Цвет = Новый Цвет(10, 100, 220);
	Форма.ШрифтИмя = "Arial, Helvetica, sans-serif";
	Форма.ШрифтРазмер = 26; 
	
	Форма.ПоУмолчанию = Истина;
	// Конец Локализация
	
КонецПроцедуры

// См. РаботаСЭП.ПолучитьЭлектронныеПодписи
// Дополняет текст запроса локальным функционалом, добавляет проверку внешнего подписания
// Параметры:
//  ТекстЗапроса - Строка - Текст запроса.
Процедура ДополнитьЗапрос_ПолучитьЭП(ТекстЗапроса) Экспорт

// Локализация	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЭП.ИдентификаторПодписи КАК УникальныйИдентификатор,
		|	ЭП.ИдентификаторПодписи КАК ИдентификаторПодписи,
		|	ЭП.ДатаПодписи КАК ДатаПодписи,
		|	ЭП.ПодписанныйОбъект КАК Объект,
		|	ЭП.ПодписанныйОбъект КАК ПодписанныйОбъект,
		|	ЭП.ПорядковыйНомер КАК ПорядковыйНомер,
		|	ЭП.УстановившийПодпись КАК УстановившийПодпись,
		|	ЭП.Версия КАК Версия,
		|	ЭП.ДатаПроверкиПодписи КАК ДатаПроверкиПодписи,
		|	ЭП.СрокДействияПоследнейМеткиВремени КАК СрокДействияПоследнейМеткиВремени,
		|	ЭП.ТипПодписи КАК ТипПодписи,
		|	ЭП.Комментарий КАК Комментарий,
		|	ЭП.КомуВыданСертификат КАК КомуВыданСертификат,
		|	ЭП.Отпечаток КАК Отпечаток,
		|	ЭП.Подпись КАК Подпись,
		|	ЭП.ПодписьВерна КАК ПодписьВерна,
		|	ЭП.СертификатДействителен КАК СертификатДействителен,
		|	ЭП.ТекстОшибкиПроверкиПодписи КАК ТекстОшибкиПроверкиПодписи,
		|	ЭП.ТекстОшибкиПроверкиСертификата КАК ТекстОшибкиПроверкиСертификата,
		|	ЕСТЬNULL(ВнешниеПодписи.Сведения, ЗНАЧЕНИЕ(Документ.СведенияОВнешнемПодписании.ПустаяСсылка)) КАК ВнешнееПодписание,
		|	ВЫБОР
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписиНет)
		|			ТОГДА 0
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписьНеПроверена)
		|			ТОГДА 1
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписьДействительна)
		|			ТОГДА 2
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписьНедействительна)
		|			ТОГДА 3
		|		ИНАЧЕ ВЫБОР
		|			КОГДА ЭП.ПодписанныйОбъект.ПодписанЭП
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	КОНЕЦ КАК СтатусПроверкиЭП
		|ИЗ
		|	РегистрСведений.ЭлектронныеПодписи КАК ЭП
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КешИнформацииОбОбъектах КАК КешИнформацииОбОбъектах
		|		ПО ЭП.ПодписанныйОбъект = КешИнформацииОбОбъектах.Объект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОВнешнемПодписанииЭлектронныхПодписей КАК ВнешниеПодписи
		|		ПО ВнешниеПодписи.УникальныйИдентификатор = ЭП.ИдентификаторПодписи
		|ГДЕ
		|	ЭП.ПодписанныйОбъект = &ОбъектСсылка";
// Конец Локализация	
	
КонецПроцедуры	

// Дополняет текст запроса локальным функционалом, добавляет проверку внешнего подписания
// Параметры:
//  ТекстЗапроса - Строка - Текст запроса.
Процедура ДополнитьЗапрос_ВыборкаПодписейОбъектов(ТекстЗапроса) Экспорт

// Локализация	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЭП.ИдентификаторПодписи КАК УникальныйИдентификатор,
		|	ЭП.ИдентификаторПодписи КАК ИдентификаторПодписи,
		|	ЭП.ДатаПодписи КАК ДатаПодписи,
		|	ЭП.ПодписанныйОбъект КАК Объект,
		|	ЭП.ПодписанныйОбъект КАК ПодписанныйОбъект,
		|	ЭП.ПорядковыйНомер КАК ПорядковыйНомер,
		|	ЭП.УстановившийПодпись КАК УстановившийПодпись,
		|	ЭП.Версия КАК Версия,
		|	ЭП.ДатаПроверкиПодписи КАК ДатаПроверкиПодписи,
		|	ЭП.СрокДействияПоследнейМеткиВремени КАК СрокДействияПоследнейМеткиВремени,
		|	ЭП.ТипПодписи КАК ТипПодписи,
		|	ЭП.Комментарий КАК Комментарий,
		|	ЭП.КомуВыданСертификат КАК КомуВыданСертификат,
		|	ЭП.Отпечаток КАК Отпечаток,
		|	ЭП.Подпись КАК Подпись,
		|	ЭП.ПодписьВерна КАК ПодписьВерна,
		|	ЭП.СертификатДействителен КАК СертификатДействителен,
		|	ЭП.ТекстОшибкиПроверкиПодписи КАК ТекстОшибкиПроверкиПодписи,
		|	ЭП.ТекстОшибкиПроверкиСертификата КАК ТекстОшибкиПроверкиСертификата,
		|	ЕСТЬNULL(ВнешниеПодписи.Сведения, ЗНАЧЕНИЕ(Документ.СведенияОВнешнемПодписании.ПустаяСсылка)) КАК ВнешнееПодписание,
		|	ВЫБОР
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписиНет)
		|			ТОГДА 0
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписьНеПроверена)
		|			ТОГДА 1
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписьДействительна)
		|			ТОГДА 2
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписьНедействительна)
		|			ТОГДА 3
		|		ИНАЧЕ ВЫБОР
		|			КОГДА ЭП.ПодписанныйОбъект.ПодписанЭП
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	КОНЕЦ КАК СтатусПроверкиЭП
		|ИЗ
		|	РегистрСведений.ЭлектронныеПодписи КАК ЭП
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КешИнформацииОбОбъектах КАК КешИнформацииОбОбъектах
		|		ПО ЭП.ПодписанныйОбъект = КешИнформацииОбОбъектах.Объект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОВнешнемПодписанииЭлектронныхПодписей КАК ВнешниеПодписи
		|		ПО ВнешниеПодписи.УникальныйИдентификатор = ЭП.ИдентификаторПодписи
		|ГДЕ
		|	ЭП.ПодписанныйОбъект В (&ПодписанныеОбъекты)";
// Конец Локализация	

КонецПроцедуры

#Область РаботаСМЧД

// Проверяет доверенности электронных подписей
// 
// Параметры:
//  ДанныеДляПроверки - Массив из Структура - см РаботаСЭПКлиентСервер.НовыеДанныеДляПроверкиДоверенностиПодписи
// 
// Возвращаемое значение:
//  Структура - Проверить доверенности электронных подписей:
//   * РезультатыПроверкиДоверенностей - Соответствие из КлючИЗначение:
//     ** Ключ - УникальныйИдентификатор - Уникальный идентификатор подписи
//     ** Значение - Структура - Протокол проверки доверенности см МашиночитаемыеДоверенности.НовыйПротоколПроверкиПодписи
//   * ОшибкиПриПроверке - Соответствие из КлючИЗначение:
//     ** Ключ - УникальныйИдентификатор - Уникальный идентификатор подписи
//     ** Значение - Строка - Описание проблемы при проверке подписи
//
Функция ПроверитьДоверенностиЭлектронныхПодписей(ДанныеДляПроверки) Экспорт
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("РезультатыПроверкиДоверенностей", Новый Соответствие);
	РезультатПроверки.Вставить("ОшибкиПриПроверке", Новый Соответствие);
	
	// Локализация
	ДанныеДоверенностей = ДанныеМЧДВДанныхДляПроверкиДоверенностей(ДанныеДляПроверки);
	
	Для Каждого Данные Из ДанныеДляПроверки Цикл
		
		ИдентификаторПодписи = Данные.ДанныеПодписи.УникальныйИдентификатор;
		Доверенность = Данные.Доверенность;
		
		Если Не ЗначениеЗаполнено(Доверенность) Тогда
			СообщениеОшибки = СтрШаблон(НСтр("ru = 'Для подписи %1 к объекту %2 не указана доверенность'"),
				ИдентификаторПодписи, Данные.ДанныеПодписи.Объект);
			РезультатПроверки.ОшибкиПриПроверке.Вставить(ИдентификаторПодписи, СообщениеОшибки);
			Продолжить;
		КонецЕсли;
		
		ДанныеДоверенности = ДанныеДоверенностей[Доверенность];
		Если ДанныеДоверенности = Неопределено Тогда
			СообщениеОшибки = СтрШаблон(НСтр("ru = 'Не удалось получить данные доверенности %1'"),
				Доверенность);
			РезультатПроверки.ОшибкиПриПроверке.Вставить(ИдентификаторПодписи, СообщениеОшибки);
			Продолжить;
		КонецЕсли;
		
		ПараметрыПроверки = ПараметрыПроверкиДоверенностиЭП(Данные, ДанныеДоверенности);
		Результат = МашиночитаемыеДоверенности.ПроверитьПодпись(ПараметрыПроверки);
		
		РезультатПроверки.РезультатыПроверкиДоверенностей.Вставить(ИдентификаторПодписи, Результат.ПротоколПроверки);
		
	КонецЦикла;
	// Конец Локализация
	
	Возврат РезультатПроверки;
	
КонецФункции

// Локализация
Процедура ОбновитьСтатусыПроверкиДоверенностей(РезультатыПроверки, ДатаПроверки = Неопределено) Экспорт
	
	ДатаПроверкиДоверенности = ТекущаяДатаСеанса();
	Если ДатаПроверки <> Неопределено Тогда
		ДатаПроверкиДоверенности = ДатаПроверки;
	КонецЕсли;
	
	Для Каждого Элемент Из РезультатыПроверки Цикл
		
		ИдентификаторПодписи = Элемент.Ключ;
		ПротоколПроверки = Элемент.Значение;
		
		НаборЗаписей = РегистрыСведений.ДоверенностиЭлектронныхПодписей.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.УникальныйИдентификаторПодписи.Установить(ИдентификаторПодписи);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() = 0 Тогда
			СообщениеПользователю = СтрШаблон(
				НСтр("ru = 'Не удалось обновить статус проверки доверенности подписи %1: запись о доверенности не найдена'"),
				ИдентификаторПодписи);
			ВызватьИсключение СообщениеПользователю;
		КонецЕсли;
		
		Запись = НаборЗаписей[0];
		
		РезультатПроверки = РаботаСЭПКлиентСервер.РезультатПроверкиДоверенностиПоПротоколу(ПротоколПроверки);
		
		ПротоколПроверкиJSON = ОбщегоНазначенияБЭД.JSONСтрока(ПротоколПроверки);
		
		Запись.ПротоколПроверки = Новый ХранилищеЗначения(ПротоколПроверкиJSON, Новый СжатиеДанных(9));
		Запись.ДатаПроверки = ДатаПроверкиДоверенности;
		Запись.ДоверенностьВерна = РезультатПроверки.ДоверенностьДействительна;
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры
// Конец Локализация

Процедура ЗанестиИнформациюОДоверенностиЭП(ИдентификаторПодписи, Доверенность, Знач СвойстваПодписи) Экспорт
	
	// Локализация
	СвойстваПодписи.Вставить("УникальныйИдентификатор", ИдентификаторПодписи);
	
	ДанныеДляПроверкиДоверенности = РаботаСЭПКлиентСервер.НовыеДанныеДляПроверкиДоверенностиПодписи();
	ЗаполнитьЗначенияСвойств(ДанныеДляПроверкиДоверенности.ДанныеПодписи, СвойстваПодписи);
	
	Если СвойстваПодписи.Свойство("Подпись") И ТипЗнч(СвойстваПодписи.Подпись) = Тип("ДвоичныеДанные") Тогда
		ДанныеДляПроверкиДоверенности.ДанныеПодписи.АдресПодписи = ПоместитьВоВременноеХранилище(
			СвойстваПодписи.Подпись);
	КонецЕсли;
	
	Если СвойстваПодписи.Свойство("Сертификат") И ТипЗнч(СвойстваПодписи.Сертификат) = Тип("ДвоичныеДанные") Тогда
		ДанныеДляПроверкиДоверенности.ДанныеПодписи.АдресСертификата = ПоместитьВоВременноеХранилище(
			СвойстваПодписи.Сертификат);
	КонецЕсли;
	
	ДанныеДляПроверкиДоверенности.Доверенность = Доверенность;
	
	ДанныеДляПроверки = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеДляПроверкиДоверенности);
	РезультатПроверки = ПроверитьДоверенностиЭлектронныхПодписей(ДанныеДляПроверки);
	ПротоколПроверки = РезультатПроверки.РезультатыПроверкиДоверенностей[ИдентификаторПодписи];
	ДоверенностьВерна =
		РаботаСЭПКлиентСервер.РезультатПроверкиДоверенностиПоПротоколу(ПротоколПроверки).ДоверенностьДействительна;
	
	ПротоколПроверкиJSON = ОбщегоНазначенияБЭД.JSONСтрока(ПротоколПроверки);
	
	МенеджерЗаписи = РегистрыСведений.ДоверенностиЭлектронныхПодписей.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.УникальныйИдентификаторПодписи = ИдентификаторПодписи;
	МенеджерЗаписи.Доверенность = Доверенность;
	МенеджерЗаписи.ДоверенностьВерна = ДоверенностьВерна;
	МенеджерЗаписи.ДатаПроверки = ТекущаяДатаСеанса();
	МенеджерЗаписи.ПротоколПроверки = Новый ХранилищеЗначения(ПротоколПроверкиJSON, Новый СжатиеДанных(9));
	
	МенеджерЗаписи.Записать();
	// Конец Локализация
	
КонецПроцедуры

Процедура УдалитьЗаписьОДоверенностиЭП(ИдентификаторПодписи) Экспорт
	// Локализация
	НаборЗаписей = РегистрыСведений.ДоверенностиЭлектронныхПодписей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.УникальныйИдентификаторПодписи.Установить(ИдентификаторПодписи);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() > 0 Тогда
		НаборЗаписей.Очистить();
		НаборЗаписей.Записать();
	КонецЕсли;
	// Конец Локализация
КонецПроцедуры

// Доступные МЧД для подписания сертификатом.
// 
// Параметры:
//  Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// 
// Возвращаемое значение:
//  Массив Из ОпределяемыйТип.МашиночитаемаяДоверенность
//
Функция ДоступныеМЧДДляПодписанияСертификатом(Сертификат) Экспорт
	
	ДоверенностиДляПодписания = Новый Массив();

	// Локализация
	СвойстваСубъекта = КриптографияБЭД.СвойстваСубъектаСертификатаПоСсылке(Сертификат);
	
	ДанныеПредставителя = РаботаСМЧДДокументооборот.НовыеДанныеПредставителяМЧД();
	ДанныеПредставителя.ИНН = СвойстваСубъекта.ИНН;
	
	ДоверенностиПоПредставителям = РаботаСМЧДДокументооборот.МЧДПоДаннымПредставителей(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеПредставителя));
	
	ВсеМЧД = Новый Массив();
	Для Каждого Элемент Из ДоверенностиПоПредставителям Цикл
		ДоверенностиПоКлючу = Элемент.Значение;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеМЧД, ДоверенностиПоКлючу);
	КонецЦикла;
	ВсеМЧД = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ВсеМЧД);
	
	ДанныеМЧД = РаботаСМЧДДокументооборот.ДанныеМЧД(ВсеМЧД);
	ДанныеДоверителей = РаботаСМЧДДокументооборот.ДанныеДоверителейМЧД(ВсеМЧД);
	
	СтатусМЧДДоступенДляПодписания = Новый Соответствие();
	СтатусМЧДДоступенДляПодписания[
		Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Зарегистрировано] = Истина;
	СтатусМЧДДоступенДляПодписания[
		Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ДатаНачалаДействияНеНаступила] = Истина;
	СтатусМЧДДоступенДляПодписания[
		Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ИстекСрокДействия] = Истина;
	СтатусМЧДДоступенДляПодписания[
		Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ПустаяСсылка()] = Истина;
	
	ТекущаяДата = ТекущаяДатаСеанса();
	

	Для Каждого Доверенность Из ВсеМЧД Цикл
		
		ДанныеДоверенности = ДанныеМЧД[Доверенность]; // см. РаботаСМЧДДокументооборот.НовыеДанныеМЧД
		ДоверителиМЧД = ДанныеДоверителей[Доверенность]; // Массив Из см. РаботаСМЧДДокументооборот.НовыеДанныеДоверителяМЧД
		
		ДействуетПоСроку = (ТекущаяДата > ДанныеДоверенности.ДатаВыдачи)
			И (ДанныеДоверенности.ДатаОкончания > ТекущаяДата);
		
		ДоверителиДопустимыДляПодписания = Истина;
		Для Каждого ДанныеДоверителя Из ДоверителиМЧД Цикл
			Если Не ЗначениеЗаполнено(ДанныеДоверителя.Ссылка)
				Или ТипЗнч(ДанныеДоверителя.Ссылка) <> Тип("СправочникСсылка.Организации") Тогда
				
				ДоверителиДопустимыДляПодписания = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		Если ДанныеДоверенности.ПометкаУдаления
			Или Не ДанныеДоверенности.Верна
			Или Не ДействуетПоСроку
			Или ДанныеДоверенности.Отозвана
			Или СтатусМЧДДоступенДляПодписания[ДанныеДоверенности.СтатусВРеестреФНС] <> Истина
			Или Не ДоверителиДопустимыДляПодписания Тогда
			
			Продолжить;
		КонецЕсли;
		
		ДоверенностиДляПодписания.Добавить(Доверенность);
		
	КонецЦикла;
	
	// Конец Локализация
	
	Возврат ДоверенностиДляПодписания;
	
КонецФункции

Функция СертификатуНужнаДоверенность(Сертификат) Экспорт
	
	// Локализация
	Если ТипЗнч(Сертификат) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
		СвойстваСубъекта = КриптографияБЭД.СвойстваСубъектаСертификатаПоСсылке(Сертификат);
		СвойстваИздателя = КриптографияБЭД.СвойстваИздателяСертификатаПоСсылке(Сертификат);
	Иначе
		СвойстваСубъекта = КриптографияБЭД.СвойстваСубъектаСертификата(Сертификат);
		СвойстваИздателя = КриптографияБЭД.СвойстваИздателяСертификата(Сертификат);
	КонецЕсли;
	
	ЭтоСертификатИП = (СвойстваСубъекта.ОГРНИП <> Неопределено);
	ЭтоСертификатФизЛица = КриптографияБЭД.ЭтоВидСертификатаФизическогоЛица(
		СвойстваСубъекта, СвойстваИздателя);
	
	Возврат ЭтоСертификатФизЛица И Не ЭтоСертификатИП;
	// Конец Локализация
	
	Возврат Ложь;
	
КонецФункции

Функция ДанныеДоверенностейПодписей(ИдентификаторыПодписей) Экспорт
	
	ДанныеДоверенностей = Новый Соответствие;
	
	// Локализация
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоверенностиЭлектронныхПодписей.УникальныйИдентификаторПодписи,
		|	ДоверенностиЭлектронныхПодписей.ДатаПроверки КАК ДатаПроверкиДоверенности,
		|	ДоверенностиЭлектронныхПодписей.Доверенность КАК Доверенность,
		|	ДоверенностиЭлектронныхПодписей.ДоверенностьВерна КАК ДоверенностьВерна,
		|	ДоверенностиЭлектронныхПодписей.ПротоколПроверки КАК ПротоколПроверкиДоверенности
		|ИЗ
		|	РегистрСведений.ДоверенностиЭлектронныхПодписей КАК ДоверенностиЭлектронныхПодписей
		|ГДЕ
		|	ДоверенностиЭлектронныхПодписей.УникальныйИдентификаторПодписи В (&ИдентификаторыПодписей)";
	Запрос.УстановитьПараметр("ИдентификаторыПодписей", ИдентификаторыПодписей);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДанныеДоверенности = НовыеДанныеДоверенности();
		ЗаполнитьЗначенияСвойств(ДанныеДоверенности, Выборка, "Доверенность, ДатаПроверкиДоверенности, ДоверенностьВерна");
		
		ПротоколПроверкиJSON = Выборка.ПротоколПроверкиДоверенности.Получить();
		Если ПротоколПроверкиJSON <> Неопределено Тогда
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(ПротоколПроверкиJSON);
			ДанныеДоверенности.ПротоколПроверкиДоверенности = ПрочитатьJSON(ЧтениеJSON,,"ДатаПроверки");
			ЧтениеJSON.Закрыть();
		КонецЕсли;
		
		ДанныеДоверенностей.Вставить(Выборка.УникальныйИдентификаторПодписи, ДанныеДоверенности);
		
	КонецЦикла;
	// Конец Локализация
	
	Возврат ДанныеДоверенностей;
	
КонецФункции

Функция ДоступноУказаниеДоверенностиПодписи(ИдентификаторПопдиси, Знач Пользователь = Неопределено) Экспорт
	
	// Локализация
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Если Пользователи.ЭтоПолноправныйПользователь(Пользователь) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Идентификаторы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторПопдиси);
	ПодписиПоИдентификаторам = РаботаСЭП.УстановленныеПодписиПоИдентификаторам(Идентификаторы);
	СвойстваПодписи = ПодписиПоИдентификаторам[ИдентификаторПопдиси];
	
	Если ТипЗнч(СвойстваПодписи) = Тип("Структура")
		И СвойстваПодписи.УстановившийПодпись = Пользователь Тогда
		
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	// Конец Локализация
	
	Возврат Ложь;
	
КонецФункции

Функция ДанныеДоверенностейДляВыгрузки(Доверенности, ИдентификаторФормы) Экспорт
	
	ДанныеДоверенностей = Новый Соответствие;
	
	// Локализация
	Для Каждого Доверенность Из Доверенности Цикл
		
		ДанныеДоверенности = ДанныеДоверенностей[Доверенность];
		Если ДанныеДоверенности <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеДоверенности = НовыеДанныеДоверенностиДляВыгрузки();
		
		Если ТипЗнч(Доверенность) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций")
			Или ТипЗнч(Доверенность) = Тип("СправочникСсылка.МЧД003") Тогда
			РезультатВыгрузки = МашиночитаемыеДоверенности.ВыгрузитьДанныеДоверенности(Доверенность);
		Иначе
			РезультатВыгрузки = ВыгрузитьДанныеДоверенностиКонтрагента(Доверенность);
		КонецЕсли;
		
		Если РезультатВыгрузки.Ошибка Тогда
			ДанныеДоверенности.ОшибкаВыгрузки = РезультатВыгрузки.ТекстОшибки;
		Иначе
			ДанныеДоверенности.Выгружена = Истина;
			ДанныеДоверенности.АдресДанных = ПоместитьВоВременноеХранилище(
				РезультатВыгрузки.ОписаниеФайла.ДвоичныеДанные, ИдентификаторФормы);
			ДанныеДоверенности.ИмяФайла = РезультатВыгрузки.ОписаниеФайла.ИмяФайла;
		КонецЕсли;
		
		ДанныеДоверенностей.Вставить(Доверенность, ДанныеДоверенности);
		
	КонецЦикла;
	// Конец Локализация
	
	Возврат ДанныеДоверенностей;
	
КонецФункции

// Получает массив структур - zip файлов МЧД (1 файл на каждый МЧД)
// 
// Параметры:
// ВерсияФайла - СправочникССылка.ВерсииФайлов
// ИдентификаторФормы - УникальныйИдентификатор - Идентификатор формы для помещения данных во временное хранилище
// ИспользованныеМЧД - Соответствие
// 
// Возвращаемое значение:
//  Массив из Структура
Функция ФайлыДоверенностей(ВерсияФайла, ИдентификаторФормы, ИспользованныеМЧД) Экспорт
	
	МассивФайлов = Новый Массив;
	
	// Локализация
	КоллекцияПодписей = ЭлектроннаяПодпись.УстановленныеПодписи(ВерсияФайла);
	ДанныеДоверенностей = ДанныеДоверенностейКоллекцииПодписей(КоллекцияПодписей, ИдентификаторФормы);
	
	Для Каждого КлючИЗначение Из ДанныеДоверенностей Цикл
		
		Значение = КлючИЗначение.Значение;
		
		Если ИспользованныеМЧД.Получить(Значение.ДоверенностьСсылка) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Значение.АдресДанныхВыгрузкиДоверенности) Тогда
			Продолжить;
		КонецЕсли;

		МЧДДанные = ПолучитьИзВременногоХранилища(Значение.АдресДанныхВыгрузкиДоверенности);
		
		СтруктураФайла = Новый Структура("Адрес, Представление, Размер",
			МЧДДанные.АдресДанных, МЧДДанные.ИмяФайла);
			
		ДвДанные = ПолучитьИзВременногоХранилища(МЧДДанные.АдресДанных);	
		СтруктураФайла.Размер = ДвДанные.Размер();
			
		МассивФайлов.Добавить(СтруктураФайла);
		ИспользованныеМЧД.Вставить(Значение.ДоверенностьСсылка, 1);
		
	КонецЦикла;
	// Конец Локализация
	
	Возврат МассивФайлов;
	
КонецФункции	

// Представления МЧД при подписании.
// 
// Параметры:
//  МЧД - Массив Из ОпределяемыйТип.МашиночитаемаяДоверенность
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//    * Ключ - ОпределяемыйТип.МашиночитаемаяДоверенность
//    * Значение - Строка
Функция ПредставленияМЧДПриПодписании(МЧД) Экспорт
	
	ПредставленияДоверенностей = Новый Соответствие();
	
	// Локализация
	ДанныеДоверителей = РаботаСМЧДДокументооборот.ДанныеДоверителейМЧД(МЧД);
	Для Каждого Доверенность Из МЧД Цикл
		
		ДанныеДоверителейДоверенности = ДанныеДоверителей[Доверенность];
		ПредставлениеДоверителя = РаботаСМЧДДокументооборот.ПредставлениеДоверителейМЧД(ДанныеДоверителейДоверенности);
		
		ПредставленияДоверенностей[Доверенность] = СтрШаблон(НСтр("ru = 'По доверенности %1 (от имени %2)'"),
			Доверенность, ПредставлениеДоверителя);
		
	КонецЦикла;
	// Конец Локализация
	
	Возврат ПредставленияДоверенностей;
	
КонецФункции

Функция НовыеДанныеПодписиСУчетомДоверенности() Экспорт
	
	СвойстваПодписи = Новый Структура;
	СвойстваПодписи.Вставить("УникальныйИдентификатор", УникальныйИдентификаторПустой());
	СвойстваПодписи.Вставить("ДатаПодписи", Дата(1, 1, 1));
	СвойстваПодписи.Вставить("Объект", Неопределено);
	СвойстваПодписи.Вставить("УстановившийПодпись", Справочники.Пользователи.ПустаяСсылка());
	СвойстваПодписи.Вставить("Версия", "");
	СвойстваПодписи.Вставить("ДатаПроверкиПодписи", Дата(1, 1, 1));
	СвойстваПодписи.Вставить("СрокДействияПоследнейМеткиВремени", Дата(1, 1, 1));
	СвойстваПодписи.Вставить("ТипПодписи", Перечисления.ТипыПодписиКриптографии.ПустаяСсылка());
	СвойстваПодписи.Вставить("Комментарий", "");
	СвойстваПодписи.Вставить("КомуВыданСертификат", "");
	СвойстваПодписи.Вставить("Отпечаток", "");
	СвойстваПодписи.Вставить("АдресПодписи", Неопределено);
	СвойстваПодписи.Вставить("ПодписьВерна", Ложь);
	СвойстваПодписи.Вставить("АдресСертификата", Неопределено);
	СвойстваПодписи.Вставить("СертификатДействителен", Ложь);
	СвойстваПодписи.Вставить("ТекстОшибкиПроверкиПодписи", "");
	СвойстваПодписи.Вставить("ТекстОшибкиПроверкиСертификата", "");
	СвойстваПодписи.Вставить("СтатусПроверкиЭП", 0);

	СвойстваПодписи.Вставить("АвторПодписи", "");
	СвойстваПодписи.Вставить("Статус", "");
	СвойстваПодписи.Вставить("ПротоколПроверки", Неопределено);	
	СвойстваПодписи.Вставить("ДатаПроверки", Дата(1, 1, 1));

	// Локализация
	СвойстваПодписи.Вставить("Доверенность", Неопределено);
	СвойстваПодписи.Вставить("ДоверенностьВерна", Ложь);
	СвойстваПодписи.Вставить("ВнешнееПодписание", Неопределено);
	// Конец Локализация
	
	Возврат СвойстваПодписи;
	
КонецФункции

Процедура ЗаполнитьСтатусыПодписейИДоверенностей(ДанныеПодписей) Экспорт
	
	// Локализация
	Доверенности = Новый Массив;
	Для Каждого ДанныеПодписи Из ДанныеПодписей Цикл
		Если ЗначениеЗаполнено(ДанныеПодписи.Доверенность) Тогда
			Доверенности.Добавить(ДанныеПодписи.Доверенность);
		КонецЕсли;
	КонецЦикла;
	
	ДоверителиПоДоверенностям = РаботаСМЧДДокументооборот.ДанныеДоверителейМЧД(Доверенности);
	
	Для Каждого ДанныеПодписи Из ДанныеПодписей Цикл
		
		ДанныеДляСтатуса = РаботаСЭПКлиентСервер.НовыеДанныеДляПолученияСтатусаПодписи();
		ДанныеДляСтатуса.ПодписьВерна = ДанныеПодписи.ПодписьВерна;
		ДанныеДляСтатуса.СертификатДействителен = ДанныеПодписи.СертификатДействителен;
		ДанныеДляСтатуса.ТекстОшибкиПроверкиПодписи = ДанныеПодписи.ТекстОшибкиПроверкиПодписи;
		ДанныеДляСтатуса.ТекстОшибкиПроверкиСертификата = ДанныеПодписи.ТекстОшибкиПроверкиСертификата;
		
		Если Не ЗначениеЗаполнено(ДанныеПодписи.Доверенность) Тогда
			ДанныеДляСтатуса.ПодписьПоДоверенности = Ложь;
		Иначе
			ДанныеДляСтатуса.ПодписьПоДоверенности = Истина;
			ДанныеДляСтатуса.ДоверенностьДействительна = ДанныеПодписи.ДоверенностьВерна;
		КонецЕсли;
		
		ДанныеДляСтатуса.ДатаПроверки = ДанныеПодписи.ДатаПроверкиПодписи;
		
		Если ЗначениеЗаполнено(ДанныеПодписи.АдресСертификата) Тогда
			ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(ДанныеПодписи.АдресСертификата);
			
			ДанныеДляСтатуса.СрокПроверкиСертификата = РаботаСЭП.СрокПроверкиДействияСертификата(
				ДвоичныеДанныеСертификата, ДанныеПодписи.СрокДействияПоследнейМеткиВремени);
		Иначе
			ДанныеДляСтатуса.СрокПроверкиСертификата = ТекущаяДатаСеанса();
		КонецЕсли;
		
		ДанныеПодписи.Статус = РаботаСЭПКлиентСервер.ОбщийСтатусПроверкиПодписи(ДанныеДляСтатуса);
		Если ЗначениеЗаполнено(ДанныеПодписи.Доверенность) Тогда
			ПредставлениеДоверителей = РаботаСМЧДДокументооборот.ПредставлениеДоверителейМЧД(
				ДоверителиПоДоверенностям[ДанныеПодписи.Доверенность]);
			
			ДанныеПодписи.АвторПодписи = СтрШаблон(
				НСтр("ru = '%1, от имени %2 по доверенности %3'"),
				ДанныеПодписи.КомуВыданСертификат,
				ПредставлениеДоверителей,
				ДанныеПодписи.Доверенность);
		Иначе
			ДанныеПодписи.АвторПодписи = ДанныеПодписи.КомуВыданСертификат;
		КонецЕсли;
		
	КонецЦикла;
	// Конец Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ВнешнееПодписание

// Сохраняет информацию о ЭП.
// Вызывается из ЗанестиИнформациюОПодписи общего модуля РаботаСЭП
Процедура ПриЗанесенииИнформацииОПодписи(ИдентификаторПодписи, ВнешнееПодписание, СвойстваПодписи) Экспорт

// Локализация
	РаботаСВнешнимПодписанием.ЗанестиИнформациюОВнешнемПодписании(ИдентификаторПодписи, ВнешнееПодписание,
		СвойстваПодписи);
// Конец Локализация

КонецПроцедуры	

// Вызывается из ДанныеОбъектовДляПроверкиПодписей общего модуля РаботаСЭП
Процедура ДанныеОбъектовДляПроверкиПодписей(ДанныеОбъектов, ПараметрыПолучения) Экспорт

// Локализация
	РаботаСВнешнимПодписанием.ПриПолученииДанныхОбъектовДляПроверкиПодписей(ДанныеОбъектов, ПараметрыПолучения);
// Конец Локализация

КонецПроцедуры
	
#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Доверенности

Функция ДанныеДоверенностейКоллекцииПодписей(КоллекцияПодписей, ИдентификаторФормы)
	
	ДанныеДоверенностейКоллекции = Новый Соответствие;
	
	// Локализация
	ИдентификаторыПодписей = Новый Массив;
	Для Каждого СвойстваПодписи Из КоллекцияПодписей Цикл
		ИдентификаторыПодписей.Добавить(СвойстваПодписи.ИдентификаторПодписи);
	КонецЦикла;
	
	ДанныеДоверенностей = ДанныеДоверенностейПодписей(ИдентификаторыПодписей);
	
	Доверенности = Новый Массив;
	Для Каждого Элемент Из ДанныеДоверенностей Цикл
		ДанныеДоверенности = Элемент.Значение;
		Доверенности.Добавить(ДанныеДоверенности.Доверенность);
	КонецЦикла;
	
	Доверенности = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Доверенности);
	
	ДанныеДляВыгрузки = ДанныеДоверенностейДляВыгрузки(Доверенности, ИдентификаторФормы);
	
	Для Каждого СвойстваПодписи Из КоллекцияПодписей Цикл
		
		ИдентификаторПопдиси = СвойстваПодписи.ИдентификаторПодписи;
		
		ДанныеДоверенностиКоллекции = Новый Структура;
		ДанныеДоверенностиКоллекции.Вставить("ДоверенностьСсылка", Неопределено);
		ДанныеДоверенностиКоллекции.Вставить("ДоверенностьПредставление", "");
		ДанныеДоверенностиКоллекции.Вставить("АдресДанныхВыгрузкиДоверенности", "");
		
		ДанныеДоверенности = ДанныеДоверенностей[ИдентификаторПопдиси];
		Если ДанныеДоверенности <> Неопределено Тогда
			
			Доверенность = ДанныеДоверенности.Доверенность;
			
			ДанныеДоверенностиКоллекции.ДоверенностьСсылка = Доверенность;
			ДанныеДоверенностиКоллекции.ДоверенностьПредставление = Строка(Доверенность);
			
			ДанныеВыгрузки = ДанныеДляВыгрузки[Доверенность];
			ДанныеДоверенностиКоллекции.АдресДанныхВыгрузкиДоверенности = ПоместитьВоВременноеХранилище(
				ДанныеВыгрузки, ИдентификаторФормы);
			
		КонецЕсли;
		
		ДанныеДоверенностейКоллекции.Вставить(ИдентификаторПопдиси, ДанныеДоверенностиКоллекции);
		
	КонецЦикла;
	// Конец Локализация
	
	Возврат ДанныеДоверенностейКоллекции;
	
КонецФункции

Функция НовыеДанныеДоверенности()
	
	ДанныеДоверенности = Новый Структура;
	ДанныеДоверенности.Вставить("Доверенность", Неопределено);
	ДанныеДоверенности.Вставить("ДатаПроверкиДоверенности", Дата(1, 1, 1));
	ДанныеДоверенности.Вставить("ДоверенностьВерна", Ложь);
	ДанныеДоверенности.Вставить("ПротоколПроверкиДоверенности", Неопределено);
	
	Возврат ДанныеДоверенности;
	
КонецФункции

Функция ДанныеМЧДВДанныхДляПроверкиДоверенностей(ДанныеДляПроверки)
	
	Доверенности = Новый Массив(); // Массив Из ОпределяемыйТип.МашиночитаемаяДоверенность
	
	// Локализация
	Для Каждого ЭлементДанных Из ДанныеДляПроверки Цикл
		
		Доверенность = ЭлементДанных.Доверенность;
		Если Не ЗначениеЗаполнено(Доверенность) Тогда
			Продолжить;
		КонецЕсли;
		
		Доверенности.Добавить(Доверенность);
	КонецЦикла;
	// Конец Локализация
	
	ДанныеДоверенностей = ДанныеМЧДДляПроверки(Доверенности);
	Возврат ДанныеДоверенностей;
	
КонецФункции

Функция ДанныеМЧДДляПроверки(Доверенности)
	
	ДанныеДоверенностей = Новый Соответствие;
	
	// Локализация
	РеквизитыМЧД = РаботаСМЧДДокументооборот.ДанныеМЧД(Доверенности);
	ДанныеДоверителей = РаботаСМЧДДокументооборот.ДанныеДоверителейМЧД(Доверенности);
	
	Для Каждого Доверенность Из Доверенности Цикл
		
		ДанныеДоверенности = НовыеДанныеДоверенностиДляПроверки();
		
		РеквизитыДоверенности = РеквизитыМЧД[Доверенность];
		ДанныеДоверителейДоверенности = ДанныеДоверителей[Доверенность];
		
		Если ДанныеДоверителейДоверенности.Количество() > 0 Тогда
			ИННДоверителя = ДанныеДоверителейДоверенности[0].ИНН;
		Иначе
			ИННДоверителя = "";
		КонецЕсли;
		
		НомерДоверенности = РеквизитыДоверенности.Номер;
		
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Доверенность);
		СведенияМЧД = МенеджерОбъекта.СведенияМЧД(Доверенность);
		
		ДанныеДоверенности.ИННДоверителя = ИННДоверителя;
		ДанныеДоверенности.НомерДоверенности = НомерДоверенности;
		ДанныеДоверенности.Сведения = СведенияМЧД;
		
		ДанныеДоверенностей.Вставить(Доверенность, ДанныеДоверенности);
		
	КонецЦикла;
	// Конец Локализация
	
	Возврат ДанныеДоверенностей;
	
КонецФункции

Функция НовоеОписаниеДоверенностиЭПДляОтметки()
	
	ДанныеДляОтметки = Новый Структура;
	ДанныеДляОтметки.Вставить("ПодписьПоДоверенности", Ложь);
	ДанныеДляОтметки.Вставить("НомерДоверенности", "");
	ДанныеДляОтметки.Вставить("ДоверительПредставление", "");
	ДанныеДляОтметки.Вставить("ДатаВыдачи", Дата(1, 1, 1));
	
	Возврат ДанныеДляОтметки;
	
КонецФункции

Функция ОписаниеДоверенностиЭПДляОтметки(ИдентификаторПодписи)
	
	ДанныеДляОтметки = НовоеОписаниеДоверенностиЭПДляОтметки();
	
	// Локализация
	ИдентификаторыПодписей = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторПодписи);
	ДанныеДоверенностей = ДанныеДоверенностейПодписей(ИдентификаторыПодписей);
	ДанныеДоверенности = ДанныеДоверенностей[ИдентификаторПодписи];
	
	Если ДанныеДоверенности = Неопределено Тогда
		Возврат ДанныеДляОтметки;
	КонецЕсли;
	
	Доверенность = ДанныеДоверенности.Доверенность;
	
	Если Не ЗначениеЗаполнено(Доверенность) Тогда
		Возврат ДанныеДляОтметки;
	КонецЕсли;
	
	ДанныеМЧД = РаботаСМЧДДокументооборот.ДанныеМЧД(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Доверенность));
	ДанныеДоверителей = РаботаСМЧДДокументооборот.ДанныеДоверителейМЧД(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Доверенность));
	
	РеквизитыДоверенности = ДанныеМЧД[Доверенность];
	ДоверителиДоверенности = ДанныеДоверителей[Доверенность];
	
	ДанныеДляОтметки.ПодписьПоДоверенности = Истина;
	ДанныеДляОтметки.НомерДоверенности = РеквизитыДоверенности.Номер;
	ДанныеДляОтметки.ДоверительПредставление =
		РаботаСМЧДДокументооборот.ПредставлениеДоверителейМЧД(ДоверителиДоверенности);
	ДанныеДляОтметки.ДатаВыдачи = РеквизитыДоверенности.ДатаВыдачи;
	// Конец Локализация
	
	Возврат ДанныеДляОтметки;
	
КонецФункции

Функция НовыеДанныеДоверенностиДляПроверки()
	
	ДанныеДоверенности = Новый Структура;
	ДанныеДоверенности.Вставить("ИННДоверителя", "");
	ДанныеДоверенности.Вставить("НомерДоверенности", "");
	ДанныеДоверенности.Вставить("Сведения", Новый Структура);
	
	Возврат ДанныеДоверенности;
	
КонецФункции

// Локализация
Функция ПараметрыПроверкиДоверенностиЭП(ДанныеПроверки, ДанныеДоверенности)
	
	СвойстваПодписи = КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи();
	СвойстваПодписи.Подпись = ПолучитьИзВременногоХранилища(ДанныеПроверки.ДанныеПодписи.АдресПодписи);
	СвойстваПодписи.Сертификат = ПолучитьИзВременногоХранилища(ДанныеПроверки.ДанныеПодписи.АдресСертификата);
	СвойстваПодписи.Отпечаток = ДанныеПроверки.ДанныеПодписи.Отпечаток;
	СвойстваПодписи.КомуВыданСертификат = ДанныеПроверки.ДанныеПодписи.КомуВыданСертификат;
	СвойстваПодписи.ДатаПодписи = ДанныеПроверки.ДанныеПодписи.ДатаПодписи;
	СвойстваПодписи.ПодписьВерна = ДанныеПроверки.ДанныеПодписи.ПодписьВерна;
	СвойстваПодписи.ДатаПроверкиПодписи = ДанныеПроверки.ДанныеПодписи.ДатаПроверкиПодписи;
	СвойстваПодписи.УстановившийПодпись = ДанныеПроверки.ДанныеПодписи.УстановившийПодпись;
	СвойстваПодписи.Комментарий = ДанныеПроверки.ДанныеПодписи.Комментарий;
	СвойстваПодписи.ТипПодписи = ДанныеПроверки.ДанныеПодписи.ТипПодписи;
	СвойстваПодписи.СрокДействияПоследнейМеткиВремени = ДанныеПроверки.ДанныеПодписи.СрокДействияПоследнейМеткиВремени;
	СвойстваПодписи.ПодписанныйОбъект = ДанныеПроверки.ДанныеПодписи.Объект;
	
	Если Не ЗначениеЗаполнено(СвойстваПодписи.Сертификат) Тогда
		СвойстваПодписи.Сертификат = ПолучитьИзВременногоХранилища(ДанныеПроверки.ДанныеПодписи.АдресСертификата);
	КонецЕсли;
	
	ПараметрыПроверки = МашиночитаемыеДоверенности.НовыеПараметрыПроверкиПодписи();
	ПараметрыПроверки.СвойстваПодписи = СвойстваПодписи;
	ПараметрыПроверки.ИННДоверителя = ДанныеДоверенности.ИННДоверителя;
	ПараметрыПроверки.СведенияМЧД = ДанныеДоверенности.Сведения;
	
	СертификатКриптографии = Новый СертификатКриптографии(СвойстваПодписи.Сертификат);
	ИННПредставителя = КриптографияБЭД.ИННСубъектаСертификата(СертификатКриптографии);
	ПараметрыПроверки.ИННСубъектаСертификата = ИННПредставителя;
	
	Возврат ПараметрыПроверки;
	
КонецФункции

Функция НовыеДанныеДоверенностиДляВыгрузки()
	
	ДанныеДоверенности = Новый Структура;
	ДанныеДоверенности.Вставить("Выгружена", Ложь);
	ДанныеДоверенности.Вставить("ОшибкаВыгрузки", "");
	ДанныеДоверенности.Вставить("ИмяФайла", "");
	ДанныеДоверенности.Вставить("АдресДанных", "");
	
	Возврат ДанныеДоверенности;
	
КонецФункции

Функция РезультатВыгрузкиНеподписаннойДоверенности(РезультатВыгрузки)
	
	РезультатВыгрузки.Ошибка = Истина;
	РезультатВыгрузки.ТекстОшибки = НСтр("ru = 'Выгружать в файл можно только подписанные доверенности. Подпишите и повторите выгрузку.'");
	Возврат РезультатВыгрузки;
	
КонецФункции

Функция ИмяФайлаМЧДКонтрагента(Ссылка)
	
	Реквизиты = "ДатаВыдачи, НомерДоверенности";
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, Реквизиты);

	ЭлементыИмениФайла = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФорматыЭДО_ФНС.ПространствоИмен_МЧД());
	ДатаФайла = ?(ЗначениеЗаполнено(ЗначенияРеквизитов.ДатаВыдачи), ЗначенияРеквизитов.ДатаВыдачи,
		ТекущаяДатаСеанса());
	ЭлементыИмениФайла.Добавить(Формат(ДатаФайла, "ДФ=ггггММдд;"));
	ЭлементыИмениФайла.Добавить(ЗначенияРеквизитов.НомерДоверенности);

	Возврат СтрСоединить(ЭлементыИмениФайла, "_");
	
КонецФункции

Функция ВыгрузитьДанныеДоверенностиКонтрагента(Ссылка)
	
	РезультатВыгрузки = Новый Структура;
	РезультатВыгрузки.Вставить("ОписаниеФайла", Новый Структура("ИмяФайла, ДвоичныеДанные"));
	РезультатВыгрузки.Вставить("Ошибка", Ложь);
	РезультатВыгрузки.Вставить("ТекстОшибки", "");
	
	РезультатВыгрузки.Вставить("ОписаниеФайла", РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла());
	
	Если Ссылка.Пустая() Тогда
		Возврат РезультатВыгрузкиНеподписаннойДоверенности(РезультатВыгрузки);
	КонецЕсли;
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ЭлектроннаяПодпись, XMLфайлМЧД");
	Подпись = ЗначенияРеквизитов.ЭлектроннаяПодпись.Получить();
	Если Подпись = Неопределено Тогда
		Возврат РезультатВыгрузкиНеподписаннойДоверенности(РезультатВыгрузки);
	КонецЕсли;
	
	ДвоичныеДанныеДоверенности = ЗначенияРеквизитов.XMLфайлМЧД.Получить();
	Если ДвоичныеДанныеДоверенности = Неопределено Тогда
		Возврат РезультатВыгрузкиНеподписаннойДоверенности(РезультатВыгрузки);
	КонецЕсли;
	
	Файлы = Новый Массив;
	
	ИмяВременногоКаталога = РаботаСФайламиБЭД.ВременныйКаталог();
	ИмяФайлаДоверенностьБезРасширения = ИмяФайлаМЧДКонтрагента(Ссылка);
	ИмяФайлаДоверенность = ИмяВременногоКаталога + ИмяФайлаДоверенностьБезРасширения + ".xml";
	ИмяФайлаПодпись = ИмяВременногоКаталога + ИмяФайлаДоверенностьБезРасширения + ".p7s";
	
	ДвоичныеДанныеДоверенности.Записать(ИмяФайлаДоверенность);
	Файлы.Добавить(ИмяФайлаДоверенность);
	
	Подпись.Записать(ИмяФайлаПодпись);
	Файлы.Добавить(ИмяФайлаПодпись);
		
	ИмяФайлаВизуализация = ИмяВременногоКаталога + ИмяФайлаДоверенностьБезРасширения + ".pdf";
	РезультатФормирования = МашиночитаемыеДоверенности.ТабличныйДокументМЧД(ДвоичныеДанныеДоверенности);
	
	ТабличныйДокумент = РезультатФормирования.ПредставлениеДокумента;
	Если ТабличныйДокумент <> Неопределено Тогда
		ТабличныйДокумент.Записать(ИмяФайлаВизуализация, ТипФайлаТабличногоДокумента.PDF);
		Файлы.Добавить(ИмяФайлаВизуализация);
	КонецЕсли;
	
	Архив = РаботаСФайламиБЭД.СформироватьАрхивФайлов(Файлы);
	
	УдалитьФайлы(ИмяВременногоКаталога);
	
	РезультатВыгрузки.ОписаниеФайла.ИмяФайла = ИмяФайлаДоверенностьБезРасширения + ".zip";
	РезультатВыгрузки.ОписаниеФайла.ДвоичныеДанные = Архив;
	
	Возврат РезультатВыгрузки;
	
КонецФункции
// Конец Локализация

#КонецОбласти

// Локализация

// Добавляет проверки для исходящих электронных документов, обмен по которым завершен.
//
// Параметры:
//  Объект - СправочникСсылка.ДокументыПредприятия - Документ, для которого добавляются проверки.
//  ВозможныеВариантыПроверок - Структура - Структура, содержащая возможные варианты проверок.
//  ИменаПараметров - Массив - Массив, содержащий имена параметров проверок.
//
Процедура ДобавитьПроверкиДляИсходящихЭДО(Объект, ВозможныеВариантыПроверок, ИменаПараметров)
	
	ДанныеСостояния = ОбменСКонтрагентамиДОСлужебный.СостояниеДокумента(Объект);
	
	Если Не (ДанныеСостояния.Направление = Перечисления.НаправленияЭДО.Исходящий
		И ДанныеСостояния.Состояние = Перечисления.СостоянияЭДОДокументооборот.ОбменЗавершен) Тогда
		
		Возврат;
	КонецЕсли;
	
	// Если это исходящий документ и обмен завершен, то при подписании в таблице сторон со стороны контрагента
	// не было данных о подписании. Это необходимо указать в доп проверках
	ПустоеКонтактноеЛицо = ПредопределенноеЗначение("Справочник.КонтактныеЛица.ПустаяСсылка");
	
	ВозможныеЗначенияПараметра = Новый Массив;
	ВозможныеЗначенияПараметра.Добавить(Новый Массив);
	
	ДанныеПодписанияКонтрагентаДоОбмена = Новый Структура;
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("НомерСтроки", 1);
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("Подписал", ПустоеКонтактноеЛицо);
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("Подписан", Ложь);
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("ДатаПодписи", Дата(1, 1, 1));
	ДанныеПодписанияДоОбмена =
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеПодписанияКонтрагентаДоОбмена);
		
	ВозможныеЗначенияПараметра.Добавить(ДанныеПодписанияДоОбмена);
	
	ДанныеПодписанияКонтрагентаДоОбмена = Новый Структура;
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("НомерСтроки", 1);
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("Подписал", "");
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("Подписан", Ложь);
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("ДатаПодписи", Дата(1, 1, 1));
	ДанныеПодписанияДоОбмена =
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеПодписанияКонтрагентаДоОбмена);
		
	ВозможныеЗначенияПараметра.Добавить(ДанныеПодписанияДоОбмена);
	ДанныеПодписанияКонтрагентаДоОбмена = Новый Структура;
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("НомерСтроки", 1);
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("Подписал", "");
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("Подписан", Ложь);
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("ДатаПодписи", Дата(1, 1, 1));
	ДанныеПодписанияДоОбмена =
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеПодписанияКонтрагентаДоОбмена);
		
	ВозможныеЗначенияПараметра.Добавить(ДанныеПодписанияДоОбмена);
	
	ИмяПараметра = "ОсобыеДанныеПодписания";
	
	ВозможныеВариантыПроверок.Вставить(ИмяПараметра, ВозможныеЗначенияПараметра);
	
	ИменаПараметров.Добавить(ИмяПараметра);
	
КонецПроцедуры

// Возвращает статусы работы по документам ДО
// 
// Параметры:
//  ДокументыЭДО - Массив Из ДокументСсылка.ЭлектронныйДокументВходящийЭДО, ДокументСсылка.ЭлектронныйДокументИсходящийЭДО -
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//    * Ключ - ДокументСсылка.ЭлектронныйДокументВходящийЭДО, ДокументСсылка.ЭлектронныйДокументИсходящийЭДО -
//    * Значение - Строка - см. РаботаСЭПКлиентСервер.СтатусыРаботыСПодписаннымиОбъектами
Функция СтатусыРаботыПоДокументамЭДО(ДокументыЭДО)
	
	СостоянияПоДокументам = ЭлектронныеДокументыЭДО.СостоянияДокументов(ДокументыЭДО);
	
	СтатусыДокументов = Новый Соответствие();
	Для Каждого Документ Из ДокументыЭДО Цикл
		Состояние = СостоянияПоДокументам[Документ];
		СтатусДокумента = СтатусРаботыПоСостояниюЭДО(Состояние);
		СтатусыДокументов[Документ] = СтатусДокумента;
	КонецЦикла;
	
	Возврат СтатусыДокументов;
	
КонецФункции
// Возвращает статус работы по состоянию документа ЭДО
// 
// Параметры:
//  СостояниеЭДО - ПеречислениеСсылка.СостоянияДокументовЭДО
// 
// Возвращаемое значение:
//  Строка - см. РаботаСЭПКлиентСервер.СтатусыРаботыСПодписаннымиОбъектами
Функция СтатусРаботыПоСостояниюЭДО(СостояниеЭДО)
	
	Состояния = Перечисления.СостоянияДокументовЭДО;
	Статусы = РаботаСЭПКлиентСервер.СтатусыРаботыСПодписаннымиОбъектами();
	
	СтатусыПоСостояниям = Новый Соответствие();
	СтатусыПоСостояниям[Состояния.Аннулирован] = Статусы.РаботаЗавершена;
	СтатусыПоСостояниям[Состояния.ЗакрытПринудительно] = Статусы.РаботаЗавершена;
	СтатусыПоСостояниям[Состояния.ЗакрытСОтклонением] = Статусы.РаботаЗавершена;
	СтатусыПоСостояниям[Состояния.ЗакрытСОтклонениемПриглашения] = Статусы.РаботаЗавершена;
	СтатусыПоСостояниям[Состояния.ЗакрытСОшибкойПередачи] = Статусы.НеОпределен;
	СтатусыПоСостояниям[Состояния.НеПолучен] = Статусы.НеОпределен;
	СтатусыПоСостояниям[Состояния.НеСформирован] = Статусы.НеОпределен;
	СтатусыПоСостояниям[Состояния.ОбменЗавершен] = Статусы.РаботаЗавершена;
	СтатусыПоСостояниям[Состояния.ОбменЗавершенСИсправлением] = Статусы.РаботаЗавершена;
	СтатусыПоСостояниям[Состояния.ОжидаетсяИзвещениеОПолучении] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ОжидаетсяИзвещениеПоОтклонению] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ОжидаетсяИсправление] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ОжидаетсяОтветНаПриглашение] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ОжидаетсяПередачаОператору] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ОжидаетсяПодтверждение] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ОжидаетсяПодтверждениеАннулирования] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ОжидаетсяПодтверждениеОператора] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяИзвещениеОПолучении] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяИзвещениеПоОтклонению] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяИсправлениеОшибкиПередачи] = Статусы.НеОпределен;
	СтатусыПоСостояниям[Состояния.ТребуетсяОтправка] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяОтправкаАннулирования] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяОтправкаИзвещения] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяОтправкаИзвещенияПоОтклонению] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяОтправкаОтклонения] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяОтправкаПриглашения] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяПовторнаяОтправка] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяПодготовкаКОтправке] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяПодготовкаКОтправкеАннулирования] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяПодготовкаКОтправкеИзвещения] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяПодготовкаКОтправкеИзвещенияПоОтклонению] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяПодготовкаКОтправкеОтклонения] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяПодписание] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяПодписаниеАннулирования] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяПодписаниеИзвещения] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяПодписаниеИзвещенияПоОтклонению] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяПодписаниеОтклонения] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяПодтверждениеАннулирования] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяУтверждение] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.ТребуетсяУточнение] = Статусы.ВРаботе;
	СтатусыПоСостояниям[Состояния.УдалитьОжидаетсяОбработкаОблачнымЭДО] = Статусы.ВРаботе;
	
	Статус = СтатусыПоСостояниям[СостояниеЭДО];
	Если Статус = Неопределено Тогда
		Возврат Статусы.НеОпределен;
	КонецЕсли;
	
	Возврат Статус;
	
КонецФункции

// Конец Локализация

#КонецОбласти
