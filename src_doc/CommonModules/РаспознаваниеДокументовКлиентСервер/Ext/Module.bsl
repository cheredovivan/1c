#Область СлужебныйПрограммныйИнтерфейс

// Распознанный текст содержит пустое значение поля.
// 
// Параметры:
//  ИмяПоля - Строка
//  РаспознанныйТекст - Строка
// 
// Возвращаемое значение:
//  Булево - Распознанный текст содержит пустое значение поля
Функция РаспознанныйТекстСодержитПустоеЗначениеПоля(ИмяПоля, Знач РаспознанныйТекст) Экспорт
	
	Если ПустаяСтрока(РаспознанныйТекст) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Реальных примеров, когда Грузополучатель = "он же" или прочерк, не найдено, но в целях
	// унификации и дополнительной проверки сделано по аналогии с полем Грузоотправитель
	Если ИмяПоля <> "Грузоотправитель"
		И ИмяПоля <> "Грузополучатель"
		И ИмяПоля <> "СтранаПроисхождения"
		И ИмяПоля <> "НомерГТД"
		Тогда
		
		Возврат Ложь;
	КонецЕсли;
	
	РаспознанныйТекст = НРег(СокрЛП(РаспознанныйТекст));
	
	Если РаспознанныйТекст = "он же" И (ИмяПоля = "Грузоотправитель" ИЛИ ИмяПоля = "Грузополучатель") Тогда
		Возврат Истина;
	Иначе
		// Исключения для вариантов написания "- -"
		РаспознанныйТекст = СтрЗаменить(РаспознанныйТекст, "—", ""); // код 8212
		РаспознанныйТекст = СтрЗаменить(РаспознанныйТекст, "–", ""); // код 8211
		РаспознанныйТекст = СтрЗаменить(РаспознанныйТекст, "-", ""); // код 45
		РаспознанныйТекст = СтрЗаменить(РаспознанныйТекст, "=", ""); // код 61
		РаспознанныйТекст = СтрЗаменить(РаспознанныйТекст, "_", ""); // код 95
		
		Возврат ПустаяСтрока(РаспознанныйТекст);
	КонецЕсли;
	
КонецФункции

// Это примитивный тип.
// 
// Параметры:
//  ПроверяемыйТип - Тип
// 
// Возвращаемое значение:
//  Булево 
Функция ЭтоПримитивныйТип(Знач ПроверяемыйТип) Экспорт
	
	Возврат ПроверяемыйТип = Тип("Строка")
	Или ПроверяемыйТип = Тип("Дата") 
	Или ПроверяемыйТип = Тип("Число") 
	Или ПроверяемыйТип = Тип("Булево");
	
КонецФункции

// Все координаты нулевые.
// 
// Параметры:
//  Координаты - Структура
//   * КоординатаX0 - Число
//   * КоординатаX1 - Число
//   * КоординатаY0 - Число
//   * КоординатаY1 - Число
// Возвращаемое значение:
//  Булево - Все координаты нулевые
Функция ВсеКоординатыНулевые(Координаты) Экспорт
	
	ТипКоординат = ТипЗнч(Координаты);
	Если ТипКоординат = Тип("ДанныеФормыЭлементКоллекции") Тогда
		Результат = (Координаты.КоординатаX0 = 0 И Координаты.КоординатаX1 = 0
			И Координаты.КоординатаY0 = 0 И Координаты.КоординатаY1 = 0);
	ИначеЕсли ТипКоординат = Тип("Массив") Тогда
		Результат = (Координаты[0] = 0 И Координаты[1] = 0 И Координаты[2] = 0 И Координаты[3] = 0);
	Иначе
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает строку по уникальному идентификатору.
// 
// Параметры:
//  Обьект - ФормаКлиентскогоПриложения
//  ИмяТаблицы - Строка
//  Идентификатор - УникальныйИдентификатор
// 
// Возвращаемое значение:
//  Неопределено - Строка по идентификатору
Функция СтрокаПоИдентификатору(Обьект, ИмяТаблицы, Идентификатор) Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Идентификатор", Идентификатор);
	
	НСтр = Обьект[ИмяТаблицы].НайтиСтроки(Отбор);
	
	Если НСтр.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат НСтр[0];
	
КонецФункции

// Возвращает порог уверенности для подстановки.
// 
// Возвращаемое значение:
//  Число - Порог уверенности для подстановки
Функция ПорогУверенностиДляПодстановки() Экспорт
	
	Возврат 30;
	
КонецФункции

// Возвращает порог уверенности для верификации.
// 
// Возвращаемое значение:
//  Число - Порог уверенности для верификации
Функция ПорогУверенностиДляВерификации() Экспорт
	
	Возврат 60;
	
КонецФункции

 // Установить отбор списка.
 // 
 // Параметры:
 //  Список - Поле
 //  ПараметрыОтбора - Структура
 Процедура УстановитьОтборСписка(Список, ПараметрыОтбора) Экспорт
	
	СтатусОбработкиРПД = ПараметрыОтбора.Получить("СтатусОбработкиРПД");
	
	Если ПараметрыОтбора.Получить("СтатусОбработкиРПД") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОтбораРПД = ПараметрыОтбораПоСтатусуОбработкиДокумента(СтатусОбработкиРПД);
	
	ТребуетПроверки = ПараметрыОтбораРПД.Получить("ТребуетПроверки");
	СтатусыРаспознавания = ПараметрыОтбораРПД.Получить("СтатусыРаспознавания");
	
	Если ТребуетПроверки = Неопределено Тогда 
		Параметр = Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ТребуетПроверки"));
		Параметр.Использование = Ложь;
	Иначе
		Список.Параметры.УстановитьЗначениеПараметра("ТребуетПроверки", ТребуетПроверки);
	КонецЕсли;
		
	Если СтатусыРаспознавания <> Неопределено Тогда
		Если СтатусыРаспознавания.Количество() = 0 Тогда 
			Параметр = Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("СтатусыРаспознавания"));
			Параметр.Использование = Ложь;
		Иначе
			Список.Параметры.УстановитьЗначениеПараметра("СтатусыРаспознавания", СтатусыРаспознавания);
		КонецЕсли;
	КонецЕсли;

 КонецПроцедуры
 
 // Параметры отбора по статусу обработки документа.
 // 
 // Параметры:
 //  СтатусОбработки - Строка
 // 
 // Возвращаемое значение:
 //  Соответствие - Соответствие из КлючИЗначение:
//   * Ключ - Строка
//   * Значение - СписокЗначений из ПеречислениеСсылка.ТипыФормализованныхДокументов
 Функция ПараметрыОтбораПоСтатусуОбработкиДокумента(СтатусОбработки) Экспорт
	
	Статусы = Новый СписокЗначений;
	ТребуетПроверки = Неопределено;
	
	Если СтатусОбработки = "Все" Тогда
		
		Статусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыРаспознаванияДокументов.НаРаспознавании"));
		Статусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыРаспознаванияДокументов.Распознан"));
		
	ИначеЕсли СтатусОбработки = "НаОбработке" Тогда
		
		Статусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыРаспознаванияДокументов.НаРаспознавании"));
		
	ИначеЕсли СтатусОбработки = "ТребуетсяПроверка" Тогда
		
		Статусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыРаспознаванияДокументов.Распознан"));
		ТребуетПроверки = Истина;
		
	ИначеЕсли СтатусОбработки = "Проверенные" Тогда
		
		Статусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыРаспознаванияДокументов.Распознан"));
		ТребуетПроверки = Ложь;

	КонецЕсли;
	
	СтруктураВозврата = Новый Соответствие;
	СтруктураВозврата.Вставить("ТребуетПроверки", ТребуетПроверки);
	СтруктураВозврата.Вставить("СтатусыРаспознавания", Статусы);
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти