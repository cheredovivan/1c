
////////////////////////////////////////////////////////////////////////////////
// Сотрудники (клиент, сервер)
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Проверяет что переданное значение является ссылкой на справочник Сотрудники
//
// Параметры:
//  Ссылка - Произвольный
//
// Возвращаемое значение:
//  Булево - Это сотрудник
//
Функция ЭтоСотрудник(Ссылка) Экспорт
	
	Возврат ТипЗнч(Ссылка) = Тип("СправочникСсылка.Сотрудники");
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Проверяет корректно ли задан перид действия сотрудника.
//
// Параметры:
//  ДатаНачала - Дата
//  ДатаОкончания - Дата
//
// Возвращаемое значение:
//  Булево
//
Функция ПериодДействияЗаполненКорректно(ДатаНачала, ДатаОкончания) Экспорт
	
	Если ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаОкончания)
		И ДатаОкончания < ДатаНачала Тогда
		
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Возвращает описание ошибки некорректного заполнения периода действия.
//
// Возвращаемое значение:
//  Строка
//
Функция ОписаниеОшибкиНекорректногоЗаполненияПериодаДействия() Экспорт
	
#Если Клиент Тогда
	КодОсновногоЯзыка = ОбщегоНазначенияКлиент.КодОсновногоЯзыка();
#Иначе
	КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
#КонецЕсли
	
	Возврат НСтр("ru = 'Окончание периода должно быть больше или равна началу.'", КодОсновногоЯзыка);
	
КонецФункции

// Возвращает описание ошибки "Сотрудник не уникален".
//
// Параметры:
//  ДатаНачалаДействия - Дата - начало периода действия.
//  ДатаОкончанияДействия - Дата - окончание периода действия.
//  СуществующийСотрудник - СправочникСсылка.Сотрудники - существующий сотрудник.
//  ДанныеСуществующегоСотрудника - Структура - данные сотрудника:
//    * ДатаНачалаДействия - Дата
//    * ДатаОкончанияДействия - Дата
//
// Возвращаемое значение:
//  Строка
//
Функция ОписаниеОшибкиСотрудникНеУникален(ДатаНачалаДействия, ДатаОкончанияДействия, СуществующийСотрудник,
		ДанныеСуществующегоСотрудника) Экспорт
	
#Если Клиент Тогда
	КодОсновногоЯзыка = ОбщегоНазначенияКлиент.КодОсновногоЯзыка();
#Иначе
	КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
#КонецЕсли
	
	ПредставлениеСуществующегоСотрудника = СтрШаблон(
		НСтр("ru = '%1, действует с %2 %3'", КодОсновногоЯзыка),
		Строка(СуществующийСотрудник),
		Формат(ДанныеСуществующегоСотрудника.ДатаНачалаДействия, "ДЛФ=D"),
		ПредставлениеДатыОкончанияДействия(ДанныеСуществующегоСотрудника.ДатаОкончанияДействия, КодОсновногоЯзыка));
	
	Возврат СтрШаблон(
		НСтр("ru = 'В периоде действия с %1 %2 уже имеется сотрудник с таким же именем, подразделением и должностью:
			|%3'", КодОсновногоЯзыка),
		Формат(ДатаНачалаДействия, "ДЛФ=D"),
		ПредставлениеДатыОкончанияДействия(ДатаОкончанияДействия, КодОсновногоЯзыка),
		ПредставлениеСуществующегоСотрудника);
	
КонецФункции

// Формирует описание ошибки запрета создания сотрудника по владельцу.
//
// Параметры:
//   Владелец - СправочникСсылка.ФизическиеЛица - владелец сотрудника.
//
// Возвращаемое значение:
//   Строка - описание ошибки запрета создания сотрудника по владельцу.
//
Функция ОписаниеОшибкиЗапретСозданияПоВладельцу(Владелец) Экспорт
	
#Если Клиент Тогда
	КодОсновногоЯзыка = ОбщегоНазначенияКлиент.КодОсновногоЯзыка();
#Иначе
	КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
#КонецЕсли
	
	Возврат СтрШаблон(
		НСтр("ru = 'Сотрудники физ. лица ""%1"" загружаются через механизм синхронизации данных.
			|Создавать сотрудников вручную запрещено.'", КодОсновногоЯзыка),
		Строка(Владелец));
	
КонецФункции

// Возвращает описание ошибки запрета установки пометки удаления для сотрудников,
// загруженных через механизм синхронизации данных.
//
// Возвращаемое значение:
//   Строка - описание ошибки.
//
Функция ОписаниеОшибкиЗапретУстановкиПометкиУдаления() Экспорт
	
#Если Клиент Тогда
	КодОсновногоЯзыка = ОбщегоНазначенияКлиент.КодОсновногоЯзыка();
#Иначе
	КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
#КонецЕсли
	
	Возврат НСтр("ru = 'Удаление сотрудников, загруженных через механизм синхронизации данных запрещено.
			|Для удаления сотрудника нужно предварительно очистить всю его кадровую историю.'",
		КодОсновногоЯзыка);
	
КонецФункции

// Формирует описание ошибки запрета изменения ключевых реквизитов сотрудника.
//
// Параметры:
//   Владелец - СправочникСсылка.ФизическиеЛица - сотрудник, для которого формируется описание ошибки.
//
// Возвращаемое значение:
//   Строка - описание ошибки запрета изменения реквизитов сотрудника.
//
Функция ОписаниеОшибкиЗапретИзмененияКлючевыхРеквизитов(Владелец) Экспорт
	
#Если Клиент Тогда
	КодОсновногоЯзыка = ОбщегоНазначенияКлиент.КодОсновногоЯзыка();
#Иначе
	КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
#КонецЕсли
	
	Возврат СтрШаблон(
		НСтр("ru = 'Сотрудники физ. лица ""%1"" загружаются через механизм синхронизации данных.
			|Изменять должность, подразделение и даты действия сотрудника вручную запрещено.'", КодОсновногоЯзыка),
		Строка(Владелец));
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеДатыОкончанияДействия(ДатаОкончанияДействия, КодОсновногоЯзыка)
	
	Если ЗначениеЗаполнено(ДатаОкончанияДействия) Тогда
		ПредставлениеДатыОкончанияДействия = СтрШаблон(
			НСтр("ru = 'по %1'", КодОсновногоЯзыка),
			Формат(ДатаОкончанияДействия, "ДЛФ=D"));
	Иначе
		ПредставлениеДатыОкончанияДействия = НСтр("ru = '(бессрочно)'", КодОсновногоЯзыка);
	КонецЕсли;
	
	Возврат ПредставлениеДатыОкончанияДействия;
	
КонецФункции

#КонецОбласти