
#Область ПрограммныйИнтерфейс

Функция ДобавитьУведомленияПоВнутреннемуДокументу(ПроизошедшееСобытие, ДокументПредприятия, ВидСобытия, Объект) Экспорт

// Локализация

	Если ВидСобытия = Справочники.ВидыБизнесСобытий.ИзменениеСостоянияДокументаПоЭДО Тогда

		ДанныеИзмененияСостоянияЭДО = ПроизошедшееСобытие.КонтекстСобытия.Получить();
		Если ДанныеИзмененияСостоянияЭДО.НаправлениеЭД = Перечисления.НаправленияЭДО.Исходящий
			И Перечисления.СостоянияЭДОДокументооборот.ИсходящийОтклоненКонтрагентом(
			ДанныеИзмененияСостоянияЭДО.НовоеСостояниеЭДО) И Не Перечисления.СостоянияЭДОДокументооборот.ИсходящийОтклоненКонтрагентом(
				ДанныеИзмененияСостоянияЭДО.СтароеСостояниеЭДО) Тогда

			РегистрыСведений.ОчередьУведомлений.ДобавитьУведомлениеПоСобытию(
				Сотрудники.ЛюбойПользовательСотрудника(ДокументПредприятия.Ответственный),
				Перечисления.СобытияУведомлений.ОтклонениеДокументаКонтрагентомПоЭДО, ДокументПредприятия,
				ДокументПредприятия);

			РегистрыСведений.ОчередьУведомлений.ДобавитьУведомлениеПоОбъекту(
				Перечисления.СобытияУведомлений.ОтклонениеДокументаКонтрагентомПоЭДО, ДокументПредприятия,
				ДокументПредприятия.ВидДокумента);
				
				Возврат Истина;

		ИначеЕсли Перечисления.СостоянияЭДОДокументооборот.ОбменВыполненУспешно(
			ДанныеИзмененияСостоянияЭДО.НовоеСостояниеЭДО) И Не Перечисления.СостоянияЭДОДокументооборот.ОбменВыполненУспешно(
			ДанныеИзмененияСостоянияЭДО.СтароеСостояниеЭДО) Тогда

			РегистрыСведений.ОчередьУведомлений.ДобавитьУведомлениеПоСобытию(
				Сотрудники.ЛюбойПользовательСотрудника(ДокументПредприятия.Ответственный),
				Перечисления.СобытияУведомлений.ОбменПоЭДОВыполненУспешно, ДокументПредприятия, ДокументПредприятия);

			РегистрыСведений.ОчередьУведомлений.ДобавитьУведомлениеПоОбъекту(
				Перечисления.СобытияУведомлений.ОбменПоЭДОВыполненУспешно, ДокументПредприятия,
				ДокументПредприятия.ВидДокумента);
				
				Возврат Истина;

		КонецЕсли;
		
	КонецЕсли;
// Локализация Конец

	Возврат Ложь;

КонецФункции

// Формирует текстовое представление документа предприятия для поступления и отклонения по ЭДО.
Функция СформироватьПредставлениеВнутреннегоДокумента(ДокументПредприятия, ВидСобытия, ДополнительноеОписание, КодЯзыка) Экспорт
	
	ПредставлениеВнутреннегоДокумента = "";
	
	// Локализация
	Если ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеДокумента
		И ДокументПредприятия.Источник = Справочники.ИсточникиДанных.ЭДО Тогда
		
		ДанныеСостоянияЭДО = ОбменСКонтрагентамиДОВызовСервера.ПолучитьСостояниеДокумента(ДокументПредприятия);
		
		ПредставлениеВнутреннегоДокумента = СтрШаблон(
			НСтр("ru = 'От контрагента %1 по ЭДО поступил новый документ: %2. Требуется проверка.'", КодЯзыка),
			ДанныеСостоянияЭДО.Контрагент,
			РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ДокументПредприятия));
		
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеВнутреннегоДокумента,
			НСтр("ru = 'Комментарий'", КодЯзыка),
			ДанныеСостоянияЭДО.Комментарий);
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ОтклонениеДокументаКонтрагентомПоЭДО Тогда
		
		ДанныеСостоянияЭДО = ОбменСКонтрагентамиДОВызовСервера.ПолучитьСостояниеДокумента(ДокументПредприятия);
		
		ПредставлениеВнутреннегоДокумента = СтрШаблон(
			НСтр("ru = 'Контрагент %1 по ЭДО отклонил документ: %2. Требуется корректировка.'", КодЯзыка),
			ДанныеСостоянияЭДО.Контрагент,
			РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ДокументПредприятия));
		
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеВнутреннегоДокумента,
			НСтр("ru = 'Комментарий'", КодЯзыка),
			ДанныеСостоянияЭДО.Комментарий);
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ОбменПоЭДОВыполненУспешно Тогда
		
		ДанныеСостоянияЭДО = ОбменСКонтрагентамиДОВызовСервера.ПолучитьСостояниеДокумента(ДокументПредприятия);
		
		ПредставлениеВнутреннегоДокумента = СтрШаблон(
			НСтр("ru = 'Обмен с контрагентом %1 по ЭДО выполнен успешно: %2.'", КодЯзыка),
			ДанныеСостоянияЭДО.Контрагент,
			РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ДокументПредприятия));
		
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеВнутреннегоДокумента,
			НСтр("ru = 'Комментарий'", КодЯзыка),
			ДанныеСостоянияЭДО.Комментарий);
			
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ЭДОДокументАннулирован Тогда

		ПредставлениеВнутреннегоДокумента = НСтр("ru = 'Документ ЭДО аннулирован'", КодЯзыка);
		
		МассивЭДО = ОбменЭДОДокументооборот.ЭлектронныеДокументыОбъектаУчета(ДокументПредприятия);
		ДокументЭДО = Неопределено;
		Если МассивЭДО.Количество() = 1 Тогда
			
			ДокументЭДО = МассивЭДО[0];
			Комментарий = ОбменЭДОДокументооборот.СостояниеДокументаПодробно(ДокументЭДО, ВидСобытия);
			Если ЗначениеЗаполнено(Комментарий) Тогда
				ПредставлениеВнутреннегоДокумента = ПредставлениеВнутреннегоДокумента 
				+ Символы.ВК + Символы.ВК + Комментарий + Символы.ВК + Символы.ВК;
			КонецЕсли;	
			
		КонецЕсли;	
		
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеВнутреннегоДокумента,,
			РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ДокументПредприятия));

	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ЭДОТребуетсяПодтверждениеАннулирования Тогда
		
		ПредставлениеВнутреннегоДокумента = НСтр("ru = 'Документ ЭДО: требуется подтверждение аннулирования'", КодЯзыка);
		
		МассивЭДО = ОбменЭДОДокументооборот.ЭлектронныеДокументыОбъектаУчета(ДокументПредприятия);
		ДокументЭДО = Неопределено;
		Если МассивЭДО.Количество() = 1 Тогда
			
			ДокументЭДО = МассивЭДО[0];
			Комментарий = ОбменЭДОДокументооборот.СостояниеДокументаПодробно(ДокументЭДО, ВидСобытия);
			Если ЗначениеЗаполнено(Комментарий) Тогда
				ПредставлениеВнутреннегоДокумента = ПредставлениеВнутреннегоДокумента 
				+ Символы.ВК + Символы.ВК + Комментарий + Символы.ВК + Символы.ВК;
			КонецЕсли;	
			
		КонецЕсли;	
		
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеВнутреннегоДокумента,,
			РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ДокументПредприятия));
			
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ЭДОДокументОтклонен Тогда
		
		ПредставлениеВнутреннегоДокумента = НСтр("ru = 'Документ ЭДО отклонен'", КодЯзыка);
		
		МассивЭДО = ОбменЭДОДокументооборот.ЭлектронныеДокументыОбъектаУчета(ДокументПредприятия);
		ДокументЭДО = Неопределено;
		Если МассивЭДО.Количество() = 1 Тогда
			
			ДокументЭДО = МассивЭДО[0];
			Комментарий = ОбменЭДОДокументооборот.СостояниеДокументаПодробно(ДокументЭДО, ВидСобытия);
			Если ЗначениеЗаполнено(Комментарий) Тогда
				ПредставлениеВнутреннегоДокумента = ПредставлениеВнутреннегоДокумента 
				+ Символы.ВК + Символы.ВК + Комментарий + Символы.ВК + Символы.ВК;
			КонецЕсли;
			
		КонецЕсли;	
		
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеВнутреннегоДокумента,,
			РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ДокументПредприятия));
	
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ЭДОДокументИсправлен Тогда
		
		МассивЭДО = ОбменЭДОДокументооборот.ЭлектронныеДокументыОбъектаУчета(ДокументПредприятия);
		ДокументЭДО = Неопределено;
		Если МассивЭДО.Количество() = 1 Тогда
			ДокументЭДО = МассивЭДО[0];
		КонецЕсли;	
		
		ПредставлениеВнутреннегоДокумента = СтрШаблон(
			НСтр("ru = 'Поступила новая версия отклоненного документа ЭДО %1'", КодЯзыка),
			Строка(ДокументЭДО));
		
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеВнутреннегоДокумента,,
			РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ДокументПредприятия));
		
		
	КонецЕсли;
	
	// Локализация Конец
	
	Возврат ПредставлениеВнутреннегоДокумента;
	
КонецФункции

// Формирует текст и пояснение оповещения.
// 
// Параметры:
//  ОбъектПодписки - СправочникСсылка, ДокументСсылка - Объект подписки формируемого уведомления.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий - ПеречислениеСсылка.СобытияУведомлений - Вид события формируемого уведомления.
//  ОбъектыУведомления - Массив - Объекты формируемого уведомления.
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ДополнительныеОписания - Соответствие - Дополнительные описания.
//  ТемаУведомления - Строка - Тема уведомления
//  ТекстУведомления - Строка - Текст уведомления
//  ТекстИПояснениеОповещения - Структура - Текст и пояснение оповещения
// 
// Возвращаемое значение:
//  Булево - Текст и пояснение заполнены, дальнейшей обработки не требуется
Функция СформироватьТекстИПояснениеОповещения(ОбъектПодписки, ВидСобытия, ОбъектыУведомления, ПолучательУведомления,
	СпособУведомления, ДополнительныеОписания, ТемаУведомления, ТекстУведомления, ТекстИПояснениеОповещения,
	КоличествоОбъектовУведомления) Экспорт

	// Локализация
	Если ВидСобытия = Перечисления.СобытияУведомлений.ЭДОДокументАннулирован
		Или ВидСобытия = Перечисления.СобытияУведомлений.ЭДОДокументОтклонен
		Или ВидСобытия = Перечисления.СобытияУведомлений.ЭДОТребуетсяПодтверждениеАннулирования
		Или ВидСобытия = Перечисления.СобытияУведомлений.ЭДОДокументИсправлен Тогда
		
		ТекстИПояснениеОповещения.ТекстОповещения = ТемаУведомления;
		ТекстИПояснениеОповещения.ПояснениеОповещения = "";
		
		КоличествоОтображаемыхСтрокПояснения = 3;
		Если КоличествоОбъектовУведомления > КоличествоОтображаемыхСтрокПояснения Тогда
			КоличествоОтображаемыхОбъектов = КоличествоОтображаемыхСтрокПояснения - 1;
		Иначе
			КоличествоОтображаемыхОбъектов = КоличествоОбъектовУведомления;
		КонецЕсли;
		
		МассивПояснениеОповещения = Новый Массив;
		Для Каждого ОбъектУведомления Из ОбъектыУведомления Цикл
			
			Если МассивПояснениеОповещения.Количество() >= КоличествоОтображаемыхОбъектов Тогда
				Прервать;
			КонецЕсли;
			
			МассивПояснениеОповещения.Добавить(Строка(ОбъектУведомления));
			
		КонецЦикла;
		
		Если КоличествоОбъектовУведомления > КоличествоОтображаемыхОбъектов Тогда
			ТекстИДругиеОбъекты = СтрШаблон(НСтр("ru = 'и другие (всего %1)'"), КоличествоОбъектовУведомления);
			МассивПояснениеОповещения.Добавить(ТекстИДругиеОбъекты);
		КонецЕсли;
		
		ТекстИПояснениеОповещения.ПояснениеОповещения = СтрСоединить(МассивПояснениеОповещения, Символы.ПС);
		
		Возврат Истина;

	КонецЕсли;
	// Локализация Конец
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти
