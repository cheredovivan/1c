
////////////////////////////////////////////////////////////////////////////////
// Бизнес процессы и задачи (сервер): Серверные процедуры подсистемы
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Принимает задачу к исполнению.
// 
// Параметры:
//  Задача - ЗадачаСсылка.ЗадачаИсполнителя.
//  ОтключитьОбновлениеЗадач - Булево.
//  КтоОтметил - СправочникСсылка.Сотрудники.
//  ВыбранныйФактическийИсполнитель - Структура.
// 
// Возвращаемое значение:
//  Структура - Результат принятия к исполнению:
//   * ПринятаКИсполнению - Булево.
//   * НужноВыбратьФактическогоИсполнителя - Булево.
//
Функция ПринятьКИсполнению(Задача, ОтключитьОбновлениеЗадач, КтоОтметил, ВыбранныйФактическийИсполнитель) Экспорт
	
	РезультатПринятияКИсполнению = Новый Структура;
	РезультатПринятияКИсполнению.Вставить("ПринятаКИсполнению", Ложь);
	РезультатПринятияКИсполнению.Вставить("НужноВыбратьФактическогоИсполнителя", Ложь);
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Задача.ЗадачаИсполнителя");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Задача);
		Блокировка.Заблокировать();
		
		РеквизитыЗадачиПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Задача,
			"Выполнена, СостояниеБизнесПроцесса, ПометкаУдаления");
		
		ДанныеПринятияКИсполнению = РаботаСБизнесПроцессами.ДанныеПринятияКИсполнению(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Задача));
		ДанныеПринятияКИсполнению = ДанныеПринятияКИсполнению[Задача];
		
		СведенияОбИсполнителях = РаботаСБизнесПроцессами.СведенияОбИсполнителяхЗадачПроцессов(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Задача));
		СведенияОбИсполнителях = СведенияОбИсполнителях[Задача];
		
		ПараметрыПринятияКИсполнению = Новый Структура(
			"ДатаПринятияКИсполнению,
			|Исполнитель,
			|ОтключитьОбновлениеЗадач");
		
		ПараметрыПринятияКИсполнению.ОтключитьОбновлениеЗадач = ОтключитьОбновлениеЗадач;
		
		ДоступноПринятиеКИсполнению =
			Не РеквизитыЗадачиПроцесса.Выполнена
			И РеквизитыЗадачиПроцесса.СостояниеБизнесПроцесса = Перечисления.СостоянияБизнесПроцессов.Активен
			И Не ДанныеПринятияКИсполнению.ПринятаКИсполнению
			И Не РеквизитыЗадачиПроцесса.ПометкаУдаления;
		Если ДоступноПринятиеКИсполнению Тогда
			
			ПараметрыПринятияКИсполнению.ДатаПринятияКИсполнению = ТекущаяДатаСеанса();
			
			ПараметрыПринятияКИсполнению.Исполнитель = СведенияОбИсполнителях.Исполнитель;
			
			Если Не ЗначениеЗаполнено(СведенияОбИсполнителях.Исполнитель) Тогда
				
				ДействиеЗадачи =
					Документы.ДействиеЗадачи.НайтиПоИсточнику(Задача);
				Если Не ЗначениеЗаполнено(ДействиеЗадачи) Тогда
					ВызватьИсключение СтрШаблон(
						НСтр("ru = 'Не удалось найти действие задачи для задачи процесса %1.'"),
						Задача);
				КонецЕсли;
				
				ДанныеФактическихИсполнителей = 
					РаботаСЗадачами.ОпределитьФактическихИсполнителей(
						ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДействиеЗадачи),
						КтоОтметил,
						ВыбранныйФактическийИсполнитель);
				
				РезультатПринятияКИсполнению.НужноВыбратьФактическогоИсполнителя = 
					ДанныеФактическихИсполнителей.ДействияБезФактИсполнителя.Количество() > 0
					Или ДанныеФактическихИсполнителей.ДействияБезКомуАдресовано.Количество() > 0;
				
				Если Не РезультатПринятияКИсполнению.НужноВыбратьФактическогоИсполнителя Тогда
					
					ПараметрыПринятияКИсполнению.Исполнитель =
						ДанныеФактическихИсполнителей.ФактическиеИсполнителиПоДействиям[ДействиеЗадачи];
					
					Если Не ЗначениеЗаполнено(ПараметрыПринятияКИсполнению.Исполнитель) Тогда
						ВызватьИсключение СтрШаблон(
							НСтр("ru = 'Не удалось определить фактического исполнителя дла задачи процесса %1.'"),
							Задача);
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если Не РезультатПринятияКИсполнению.НужноВыбратьФактическогоИсполнителя Тогда
				ПринятьКИсполнениюЗадачуПроцессаБезусловно(
					Задача,
					ПараметрыПринятияКИсполнению,
					Не ОбщегоНазначенияДокументооборот.ЭтоДокументооборотХолдинга());
				РезультатПринятияКИсполнению.ПринятаКИсполнению = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат РезультатПринятияКИсполнению;
	
КонецФункции

// Выполняет перенаправление (изменение исполнителя) задачи процесса.
//
// Параметры:
//  ЗадачаПроцесса - ЗадачаСсылка.ЗадачаИсполнителя
//  ИнфоОПеренаправлении - Структура
//  ПроверятьФункциональнуюОпцию - Булево
//  ОтключитьОбновлениеЗадач - Булево
//  ПроверятьПраваВПривилегированномРежиме - Булево
//
// Возвращаемое значение:
//   Булево - Истина, если перенаправление прошло успешно.
//
Функция ПеренаправитьЗадачу(Знач ЗадачаПроцесса, Знач ИнфоОПеренаправлении, ПроверятьФункциональнуюОпцию,
	ОтключитьОбновлениеЗадач, ПроверятьПраваВПривилегированномРежиме) Экспорт
	
	Если ПроверятьФункциональнуюОпцию
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьПеренаправлениеЗадач") Тогда
		
		Возврат Ложь;
	КонецЕсли;
	
	ЗадачаВыполнена = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗадачаПроцесса, "Выполнена");
	
	Если ЗадачаВыполнена Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ПривилегированныйРежим() Или ПроверятьПраваВПривилегированномРежиме Тогда
		ПраваНаЗадачу = ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(ЗадачаПроцесса);
		Если Не ПраваНаЗадачу.Изменение Тогда
			ВызватьИсключение НСтр("ru = 'Недостаточно прав для перенаправления задачи.
				|Обратитесь к администратору.'");
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоЗадачаОзнакомленияСРезультатом(ЗадачаПроцесса) Тогда
		ВызватьИсключение
			НСтр("ru = 'Задача ознакомления с результатом не может быть перенаправлена'");
	КонецЕсли;
	
	ПараметрыПеренаправления = Новый Структура(
		"ИнфоОПеренаправлении, ИдентификаторФормы, ОтключитьОбновлениеЗадач",
		ИнфоОПеренаправлении, Неопределено, ОтключитьОбновлениеЗадач);
	
	ПеренаправитьЗадачуПроцессаБезусловно(
		ЗадачаПроцесса,
		ПараметрыПеренаправления,
		Не ОбщегоНазначенияДокументооборот.ЭтоДокументооборотХолдинга());
	
	Возврат Истина;
	
КонецФункции

// Определяет ведущую корневой процесс.
// 
// Параметры:
//  Процесс - БизнесПроцессСсылка.
// 
// Возвращаемое значение:
//  БизнесПроцессСсылка - Главный корневой процесс.
//
Функция КорневойВедущийПроцесс(Процесс) Экспорт
	
	КорневойВедущийПроцесс = Неопределено;
	Если Не ЗначениеЗаполнено(Процесс) Тогда
		Возврат КорневойВедущийПроцесс;
	КонецЕсли;
	
	ВедущаяЗадача = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Процесс, "ВедущаяЗадача");
	
	Пока ЗначениеЗаполнено(ВедущаяЗадача) Цикл
		
		КорневойВедущийПроцесс =
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВедущаяЗадача, "БизнесПроцесс");
		ВедущаяЗадача =
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КорневойВедущийПроцесс, "ВедущаяЗадача");
		
	КонецЦикла;
	
	Возврат КорневойВедущийПроцесс;
	
КонецФункции

// Получить структуру с описанием формы выполнения задачи.
//
// Параметры
// - ЗадачаСсылка - ЗадачаСсылка.ЗадачаИсполнителя - задача
//
// Возвращаемое значение:
// - Структура - структура с описанием формы выполнения задачи
//
Функция ПолучитьФормуВыполненияЗадачи(ЗадачаСсылка) Экспорт
	
	Если ТипЗнч(ЗадачаСсылка) <> Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Неправильный тип параметра ЗадачаСсылка (передан: %1; ожидается: %2)'"),
			ТипЗнч(ЗадачаСсылка),
			"ЗадачаСсылка.ЗадачаИсполнителя");
		
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗадачаСсылка, "БизнесПроцесс, ТочкаМаршрута");
	Если Реквизиты.БизнесПроцесс = Неопределено Или Реквизиты.БизнесПроцесс.Пустая() Тогда
		Возврат Новый Структура();
	КонецЕсли;
	
	ТипБизнесПроцесса = Метаданные.НайтиПоТипу(ТипЗнч(Реквизиты.БизнесПроцесс));
	ПараметрыФормы = БизнесПроцессы[ТипБизнесПроцесса.Имя].ФормаВыполненияЗадачи(
		ЗадачаСсылка,
		Реквизиты.ТочкаМаршрута);
	Возврат ПараметрыФормы;
	
КонецФункции

// Проверяет, доступно ли прерывание процессов.
// 
// Параметры:
//  ПроверяемыеПроцессы - Массив из БизнесПроцессСсылка.
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//   * Ключ - БизнесПроцессСсылка.
//   * Значение - Булево. 
//
Функция ДоступноПрерываниеПроцессов(ПроверяемыеПроцессы) Экспорт
	
	ДоступноПрерываниеПроцессов = Новый Соответствие;
	
	Если ПроверяемыеПроцессы.Количество() = 0 Тогда
		
		Возврат ДоступноПрерываниеПроцессов;
		
	КонецЕсли;
	
	Для Каждого ПроверяемыйПроцесс Из ПроверяемыеПроцессы Цикл
		
		ДоступноПрерываниеПроцессов[ПроверяемыйПроцесс] = Ложь;
		
	КонецЦикла;
	
	РеквизитыПроцессов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		ПроверяемыеПроцессы,
		"Стартован, Завершен, Состояние, ВедущаяЗадача");
	
	Для Каждого ПроверяемыйПроцесс Из ПроверяемыеПроцессы Цикл
		
		РеквизитыПроцесса = РеквизитыПроцессов[ПроверяемыйПроцесс];
		ДоступноПрерываниеПроцесса =
			РеквизитыПроцесса.Стартован
			И Не РеквизитыПроцесса.Завершен
			И Не ЗначениеЗаполнено(РеквизитыПроцесса.ВедущаяЗадача)
			И РеквизитыПроцесса.Состояние <> Перечисления.СостоянияБизнесПроцессов.Прерван;
		ДоступноПрерываниеПроцессов[ПроверяемыйПроцесс] = ДоступноПрерываниеПроцесса;
		
	КонецЦикла;
	
	Возврат ДоступноПрерываниеПроцессов;
	
КонецФункции

// Есть ли хотя бы один исполнитель, назначенный на указанную роль.
//
// Параметры:
//   Роль - СправочникСсылка.ПолныеРоли - проверяемая роль.
//
// Результат:
//   Булево
//
Функция ЕстьИсполнителиРоли(Роль) Экспорт
	
	ИсполнителиРоли = РегистрыСведений.ИсполнителиРолей.ИсполнителиРоли(Роль);
	
	Возврат ИсполнителиРоли.Количество() > 0;
	
КонецФункции

// Возвращает участника процесса, которого необходимо ознакомить с результатом завершения процесса.
//
// Параметры:
//	Процесс - БизнесПроцессСсылка - Процесс, для которого необходимо получить ознакамливаемого c
//									результатом завершения процесса.
//
// Возвращаемое значение:
//	СправочникСсылка.Сотрудники, СправочникСсылка.ПолныеРоли
//
Функция ОзнакамливаемыйСРезультатом(Процесс) Экспорт

	МенеджерПроцесса = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Процесс);
	Попытка
		ОзнакамливаемыйСРезультатом = МенеджерПроцесса.ОзнакамливаемыйСРезультатом(Процесс);
	Исключение
		// Метода может не быть.
		ОзнакамливаемыйСРезультатом = Неопределено;
	КонецПопытки;
	
	Возврат ОзнакамливаемыйСРезультатом;
	
КонецФункции

// Возвращает описание результата завершения процесса.
//
// Параметры:
//	Процесс - БизнесПроцессСсылка - Процесс, для которого необходимо получить описание результата завершения.
//	КодЯзыка - Строка - Код языка пользователя
//
// Возвращаемое значение:
//	Строка - Описание результата завершения процесса.
//
Функция ОписаниеРезультатаЗавершенияПроцесса(Процесс, КодЯзыка) Экспорт

	МенеджерПроцесса = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Процесс);
	Попытка
		ОписаниеРезультата = МенеджерПроцесса.ОписаниеРезультатаЗавершенияПроцесса(Процесс, КодЯзыка);
	Исключение
		// Метода может не быть.
		ОписаниеРезультата = "";
	КонецПопытки;
	
	Возврат ОписаниеРезультата;
	
КонецФункции

// Возвращает информацию о прерывании процесса из протокола работы пользователя.
//  В случае если у текущего процесса отсутствует информация о прерывании,
//  то рекурсивно получает информацию у вышестоящего процесса.
//
// Возвращаемое значение:
//  Структура
//   Дата - дата прерывания процесса    	
//   Пользователь - пользователь прервавший процесс
//   ПричинаПрерывания - причина прерывания процесса
//
Функция ПолучитьИнформациюОПрерыванииПроцесса(БизнесПроцесс) Экспорт
	
	КтоИКогдаПрервалПроцесс = Новый Структура("Дата, Пользователь, ПричинаПрерывания");
	
	ИнформацияОПрерывании = ПротоколированиеРаботыСотрудников.ИнформацияОПрерыванииПроцессаИзПротокола(
		БизнесПроцесс);
		
	Если ИнформацияОПрерывании <> Неопределено Тогда			
		КтоИКогдаПрервалПроцесс.Дата = ИнформацияОПрерывании.Дата;
		КтоИКогдаПрервалПроцесс.Пользователь = ИнформацияОПрерывании.Сотрудник;
		ПричинаПрерывания = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			БизнесПроцесс, 
			"ПричинаПрерывания");
		КтоИКогдаПрервалПроцесс.Вставить("ПричинаПрерывания", ПричинаПрерывания);
		Возврат КтоИКогдаПрервалПроцесс;	
	КонецЕсли;	
	
	// Если в текущем процессе нет информации о прерывании, 
	//  значит прервали вышестоящий процесс, необходимо его найти		
	ГлавныеЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		БизнесПроцесс, 
		"ГлавнаяЗадача, ВедущаяЗадача");
	
	ГлавнаяЗадача = Неопределено;
	
	Если ЗначениеЗаполнено(ГлавныеЗадачи.ГлавнаяЗадача) Тогда
		ГлавнаяЗадача = ГлавныеЗадачи.ГлавнаяЗадача;
	ИначеЕсли ЗначениеЗаполнено(ГлавныеЗадачи.ВедущаяЗадача) Тогда
		ГлавнаяЗадача = ГлавныеЗадачи.ВедущаяЗадача;
	КонецЕсли;
	
	Если ГлавнаяЗадача = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПроцессГлавнойЗадачи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		ГлавнаяЗадача, 
		"БизнесПроцесс");
		
	Если Не ЗначениеЗаполнено(ПроцессГлавнойЗадачи) Тогда
		Возврат Неопределено;
	КонецЕсли;	
		
	Возврат ПолучитьИнформациюОПрерыванииПроцесса(ПроцессГлавнойЗадачи);		
		
КонецФункции


// Проверяет условия выполнения задач. В случае проблем - возвращает текст ошибки.
//
// Параметры:
//  Задача - ЗадачаСсылка - Ссылка на выполняемую задачу.
//  Параметры - Структура - Параметры.
// 
// Возвращаемое значение:
//  Структура - Результат проверки условия.
//
Функция ПроверитьУсловияЗапретаВыполнения(Задача, Знач Параметры = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Параметры = Неопределено Тогда
		Параметры = Новый Структура;
	КонецЕсли;
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("ЗапретВыполнения", Ложь);
	РезультатПроверки.Вставить("ТекстПредупреждения", "");
	РезультатПроверки.Вставить("ФорматированныйТекстПредупреждения", Новый ФорматированнаяСтрока(""));
	
	РеквизитыЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Задача,
		"БизнесПроцесс, ТочкаМаршрута");
	Если ТипЗнч(РеквизитыЗадачи.БизнесПроцесс) = Тип("БизнесПроцессСсылка.Приглашение") Тогда
		РезультатПриглашения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыЗадачи.БизнесПроцесс, "РезультатПриглашения");
		Параметры.Вставить("РезультатПриглашения", РезультатПриглашения);
	КонецЕсли;
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(РеквизитыЗадачи.БизнесПроцесс);
	ИспользуетУсловияЗапретаВыполненияЗадач = МенеджерОбъекта.ИспользуетУсловияЗапретаВыполненияЗадач(
		РеквизитыЗадачи.ТочкаМаршрута,
		Параметры);
	Если Не ИспользуетУсловияЗапретаВыполненияЗадач Тогда
		Возврат РезультатПроверки;
	КонецЕсли;
	
	ТекстПредупреждения = Новый Массив;
	ФорматированныйТекстПредупреждения = Новый Массив;
	КоличествоПредупреждений = 0;
	Для Каждого УсловиеЗапретаВыполнения Из УсловияЗапретаВыполнения(Задача) Цикл
		Если Справочники.УсловияЗадач.Проверить(Задача, УсловиеЗапретаВыполнения.Условие) Тогда
			ТекущийТекстПредупреждения = УсловиеЗапретаВыполнения.ТекстПредупреждения;
			Если Не ЗначениеЗаполнено(ТекущийТекстПредупреждения) Тогда
				ТекущийТекстПредупреждения = СтрШаблон(
					НСтр("ru = 'условие ""%1""'"),
					Строка(УсловиеЗапретаВыполнения.Условие));
			КонецЕсли;
			ТекстПредупреждения.Добавить(
				ТекстПредупрежденияЗапретаВыполненияСтрока(
					Задача,
					ТекущийТекстПредупреждения));
			ФорматированныйТекстПредупреждения.Добавить(
				ТекстПредупрежденияЗапретаВыполненияФорматированнаяСтрока(
					Задача,
					ТекущийТекстПредупреждения));
			КоличествоПредупреждений = КоличествоПредупреждений + 1;
		КонецЕсли;
	КонецЦикла;
	
	ЗапретВыполнения = КоличествоПредупреждений <> 0;
	Если ЗапретВыполнения Тогда
		
		Если КоличествоПредупреждений = 1 Тогда
			ЗаголовокПредупреждения = НСтр("ru = 'Для задачи настроено обязательное условие:'");
			ПодвалПредупреждения = НСтр("ru = 'Выполните условие и повторите исполнение задачи.'");
		Иначе
			ЗаголовокПредупреждения = НСтр("ru = 'Для задачи настроены обязательные условия:'");
			ПодвалПредупреждения = НСтр("ru = 'Выполните условия и повторите исполнение задачи.'");
		КонецЕсли;
		
		// Текст предупреждения
		ТекстПредупреждения =
			ЗаголовокПредупреждения
			+ Символы.ПС
			+ СтрСоединить(ТекстПредупреждения, ";" + Символы.ПС)
			+ "."
			+ Символы.ПС
			+ Символы.ПС
			+ ПодвалПредупреждения;
		
		// Форматированный текст предупреждения
		Для Индекс = 0 По КоличествоПредупреждений - 1 Цикл
			НоваяСтрока = Новый Массив;
			НоваяСтрока.Добавить(Символы.ПС);
			НоваяСтрока.Добавить(ФорматированныйТекстПредупреждения[Индекс]);
			Если Индекс <> КоличествоПредупреждений - 1 Тогда
				НоваяСтрока.Добавить(";");
			Иначе
				НоваяСтрока.Добавить(".");
			КонецЕсли;
			ФорматированныйТекстПредупреждения[Индекс] = Новый ФорматированнаяСтрока(НоваяСтрока);
		КонецЦикла;
		ФорматированныйТекстПредупреждения.Вставить(0, ЗаголовокПредупреждения);
		ФорматированныйТекстПредупреждения.Добавить(Символы.ПС + Символы.ПС + ПодвалПредупреждения);
		ФорматированныйТекстПредупреждения =
			Новый ФорматированнаяСтрока(ФорматированныйТекстПредупреждения);
		
	Иначе
		ТекстПредупреждения = "";
		ФорматированныйТекстПредупреждения = Новый ФорматированнаяСтрока("");
	КонецЕсли;
	
	РезультатПроверки.ЗапретВыполнения = ЗапретВыполнения;
	РезультатПроверки.ТекстПредупреждения = ТекстПредупреждения;
	РезультатПроверки.ФорматированныйТекстПредупреждения = ФорматированныйТекстПредупреждения;
	
	Возврат РезультатПроверки;
	
КонецФункции

// Проверяет, процесс на завершен ли процесс с положительным результатом.
// 
// Параметры:
// 	Процесс - БизнесПроцессСсылка
// 	
// Возвращаемое значение:
//  Булево
// 	
Функция ПроцессЗавершенСПоложительнымРезультатом(Процесс) Экспорт
	
	МенеджерПроцесса = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Процесс);
	Попытка
		ЕстьМетодРезультатВыполненияПроцесса = МенеджерПроцесса.ЕстьМетодРезультатВыполненияПроцесса();
	Исключение
		// Метода может не быть.
		ЕстьМетодРезультатВыполненияПроцесса = Ложь;
	КонецПопытки;
	
	Если ЕстьМетодРезультатВыполненияПроцесса И МенеджерПроцесса.РезультатВыполненияПроцесса(
		Процесс) = Перечисления.ВариантыВыполненияПроцессовИЗадач.Положительно Тогда

		Возврат Истина;
	КонецЕсли;	
	
	Возврат Ложь;
	
КонецФункции

// Определяет, является ли ссылка на объект шаблоном процесса или процессом.
Функция ЭтоПроцессИлиШаблонПроцесса(Ссылка) Экспорт
	
	Возврат ЭтоПроцесс(Ссылка) Или ЭтоШаблонПроцесса(Ссылка);
	
КонецФункции

// Возвращает запрос для расчета прав доступа по дескрипторам для объектов вида БизнесПроцессСсылка
// 
// Параметры:
//  
//  Дескрипторы - Массив Из СправочникСсылка.ДескрипторыДоступаОбъектов - Дескрипторы, чьи права нужно рассчитать.
//  ИдОбъекта - СправочникСсылка.ИдентификаторыОбъектовМетаданных - Устарел. Будет удален.
//  МенеджерОбъектаДоступа - СправочникМенеджер, ДокументМенеджер - Устарел. Будет удален.
// 
// Возвращаемое значение:
//	Запрос - запрос, который выберет права доступа для переданного массива дескрипторов
Функция ЗапросДляРасчетаПрав(Дескрипторы, ИдОбъекта = Неопределено, МенеджерОбъектаДоступа = Неопределено) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДескрипторыДоступаОбъектов.Ссылка КАК Дескриптор,
		|	СоставСубъектовПравДоступа.Сотрудник,
		|	ИСТИНА КАК Чтение,
		|	ИСТИНА КАК Добавление,
		|	ИСТИНА КАК Изменение,
		|	ИСТИНА КАК Удаление,
		|	ЛОЖЬ КАК УправлениеПравами
		|ПОМЕСТИТЬ НовыеПрава
		|ИЗ
		|	Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.УчастникиПроцессов КАК УчастникиПроцессов
		|		ПО ДескрипторыДоступаОбъектов.ОбъектДоступа = УчастникиПроцессов.Процесс
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектовПравДоступа
		|		ПО УчастникиПроцессов.Участник = СоставСубъектовПравДоступа.Субъект
		|ГДЕ
		|	ДескрипторыДоступаОбъектов.Ссылка В (&Дескрипторы)");
	ДокументооборотПраваДоступа.ДополнитьТекстЗапросаПоПравам(Запрос.Текст);
	
	Возврат Запрос;
	
КонецФункции

// Заполняет протокол расчета прав дескрипторов
// 
// Параметры:
//  
//  ПротоколРасчетаПрав - Массив Из Структура - протокол для заполнения
//  ЗапросПоПравам - Запрос - запрос, который использовался для расчета прав дескрипторов
//  Дескрипторы - Массив - массив дескрипторов, чьи права были рассчитаны
Процедура ЗаполнитьПротоколРасчетаПрав(ПротоколРасчетаПрав, ЗапросПоПравам) Экспорт
	
	ЗаписьПротокола = Новый Структура("Элемент, Описание",
		"УчастникиПроцесса", НСтр("ru = 'Участники процесса'"));
	ПротоколРасчетаПрав.Добавить(ЗаписьПротокола);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Принимает к исполнению задачу процесса без условно, с учетом переданных параметров.
//
// Параметры:
//  ЗадачаПроцесса - ЗадачаСсылка.ЗадачаИсполнителя
//  ПараметрыПринятия - Структура
//    * ДатаПринятияКИсполнению - Дата.
//    * Исполнитель - СправочникСсылка.Сотрудники.
//    * ОтключитьОбновлениеЗадач - Булево.
//  Немедленно - Булево - признак немедленного принятия задачи процесса к исполнению.
//
Процедура ПринятьКИсполнениюЗадачуПроцессаБезусловно(
	ЗадачаПроцесса, ПараметрыПринятия, Немедленно = Ложь) Экспорт
	
	Если ОбработкаОчередиЗаданийСервер.ОбработатьПринятиеКИсполнениюЗадачиПроцесса(
		ЗадачаПроцесса, ПараметрыПринятия, Немедленно) Тогда
		
		Возврат;
	КонецЕсли;
	
	ЗадачаПроцессаОбъект = ЗадачаПроцесса.ПолучитьОбъект();
	ЗадачаПроцессаОбъект.ПринятаКИсполнению = Истина;
	ЗадачаПроцессаОбъект.ДатаПринятияКИсполнению = ПараметрыПринятия.ДатаПринятияКИсполнению;
	ЗадачаПроцессаОбъект.Исполнитель = ПараметрыПринятия.Исполнитель;
	
	Если ПараметрыПринятия.ОтключитьОбновлениеЗадач Тогда
		ЗадачаПроцессаОбъект.ДополнительныеСвойства.Вставить("ОтключитьОбновлениеЗадач", Истина);
	КонецЕсли;
	
	ЗадачаПроцессаОбъект.Записать();
	
	ДействиеЗадачи = Документы.ДействиеЗадачи.НайтиПоИсточнику(ЗадачаПроцесса);
	Если ЗначениеЗаполнено(ДействиеЗадачи) Тогда
		ИнтеграцияЗадач.ОбновитьЗадачуПоСостояниюДействия(ДействиеЗадачи, Ложь);
	КонецЕсли;
	
	БизнесПроцессыИЗадачиСерверЛокализация.ПринятьКИсполнениюЗадачуПроцессаБезусловно(ЗадачаПроцесса, 
		ПараметрыПринятия, Немедленно);
КонецПроцедуры

// Отменяет принятие к исполнению задачи процесса без условно.
//
// Параметры:
//  ЗадачаПроцесса - ЗадачаСсылка.ЗадачаИсполнителя
//  ОтключитьОбновлениеЗадач - Булево.
//  Немедленно - Булево - признак немедленного принятия задачи процесса к исполнению.
//
Процедура ОтменитьПринятиеЗадачиПроцессаКИсполнениюБезусловно(
	ЗадачаПроцесса, ОтключитьОбновлениеЗадач, Немедленно = Ложь) Экспорт
	
	Если ОбработкаОчередиЗаданийСервер.ОбработатьОтменуПринятияКИсполнениюЗадачиПроцесса(
		ЗадачаПроцесса, ОтключитьОбновлениеЗадач, Немедленно) Тогда
		
		Возврат;
	КонецЕсли;
	
	ЗадачаПроцессаОбъект = ЗадачаПроцесса.ПолучитьОбъект();
	СтарыйИсполнитель = ЗадачаПроцессаОбъект.Исполнитель;
	
	ЗадачаПроцессаОбъект.ПринятаКИсполнению = Ложь;
	ЗадачаПроцессаОбъект.ДатаПринятияКИсполнению = "00010101000000";
	Если Не ЗадачаПроцессаОбъект.РольИсполнителя.Пустая() Тогда
		ЗадачаПроцессаОбъект.Исполнитель = Справочники.Сотрудники.ПустаяСсылка();
	КонецЕсли;
	
	Если ОтключитьОбновлениеЗадач Тогда
		ЗадачаПроцессаОбъект.ДополнительныеСвойства.Вставить("ОтключитьОбновлениеЗадач", Истина);
	КонецЕсли;
	
	ЗадачаПроцессаОбъект.Записать();

	БизнесПроцессыИЗадачиСерверЛокализация.ОтменитьПринятиеЗадачиПроцессаКИсполнениюБезусловно(ЗадачаПроцесса, 
		ОтключитьОбновлениеЗадач, Немедленно, СтарыйИсполнитель);
КонецПроцедуры

// Выполняет процедуры перенаправления задачи процесса.
//
// Параметры:
//	ЗадачаПроцесса - ЗадачаСсылка.ЗадачаИсполнителя- Перенаправляемая задача.
//	ПараметрыПеренаправления - Структура:
//		* ИнфоОПеренаправлении  - Структура - Информация о новом исполнителе задачи.
//		* ИдентификаторФормы - УникальныйИдентификатор - Идентификатор формы, из которой было вызвано перенаправление.
//		* ОтключитьОбновлениеЗадач - Булево - Признак отключения обновления задач.
//  Немедленно - Булево - признак немедленного перенаправления задачи процесса.
//
Процедура ПеренаправитьЗадачуПроцессаБезусловно(
	ЗадачаПроцесса, ПараметрыПеренаправления, Немедленно = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ОбработкаОчередиЗаданийСервер.ОбработатьПеренаправлениеЗадачиПроцесса(
		ЗадачаПроцесса, ПараметрыПеренаправления, Немедленно) Тогда
		
		Возврат;
	КонецЕсли;
	
	ИнфоОПеренаправлении = ИнтеграцияЗадач.ИнфоОПеренаправлении();
	ЗаполнитьЗначенияСвойств(ИнфоОПеренаправлении, ПараметрыПеренаправления.ИнфоОПеренаправлении);
	
	Если ЗначениеЗаполнено(ИнфоОПеренаправлении.ВладелецРоли) 
		И (Не ЗначениеЗаполнено(ИнфоОПеренаправлении.РольИсполнителя) 
		Или Не ОбщегоНазначения.СсылкаСуществует(ИнфоОПеренаправлении.РольИсполнителя)) Тогда
			
			ИнфоОПеренаправлении.РольИсполнителя = Справочники.ПолныеРоли.НайтиСоздатьПолнуюРоль(
				ИнфоОПеренаправлении.ВладелецРоли, ИнфоОПеренаправлении.ОсновнойОбъектАдресации,
					ИнфоОПеренаправлении.ДополнительныйОбъектАдресации);
					
	КонецЕсли;
	
	ИдентификаторФормы = ПараметрыПеренаправления.ИдентификаторФормы;
	ОтключитьОбновлениеЗадач = ПараметрыПеренаправления.ОтключитьОбновлениеЗадач;
	
	РеквизитыЗадачиПроцесса =
		ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗадачаПроцесса, "Исполнитель, РольИсполнителя");
	
	Попытка
		
		НачатьТранзакцию();
		
		// Сохранение старого исполнителя задачи
		Набор = РегистрыСведений.СведенияОбИсполнителяхЗадач.СоздатьНаборЗаписей();
		Набор.Отбор.Задача.Установить(ЗадачаПроцесса);
		Если ЗначениеЗаполнено(РеквизитыЗадачиПроцесса.Исполнитель) Тогда
			Набор.Отбор.Участник.Установить(РеквизитыЗадачиПроцесса.Исполнитель);
		Иначе
			Набор.Отбор.Участник.Установить(РеквизитыЗадачиПроцесса.РольИсполнителя);
		КонецЕсли;
		
		Запись = Набор.Добавить();
		Запись.Задача = ЗадачаПроцесса;
		Если ЗначениеЗаполнено(РеквизитыЗадачиПроцесса.Исполнитель) Тогда
			Запись.Участник = РеквизитыЗадачиПроцесса.Исполнитель;
		Иначе
			Запись.Участник = РеквизитыЗадачиПроцесса.РольИсполнителя;
		КонецЕсли;
		
		Набор.Записать(Истина);
		
		// Обработка перенаправления.
		
		ПеренаправлениеОбработано = 
			РаботаСПроцессамиПоДействиямСобытия.ОбработатьПеренаправлениеЗадачиПроцесса(
			ЗадачаПроцесса,
			ИнфоОПеренаправлении);
		
		Если Не ПеренаправлениеОбработано Тогда
			
			// Стандартная процедура перенаправления задачи процесса.
			
			ЗаблокироватьДанныеДляРедактирования(ЗадачаПроцесса,, ИдентификаторФормы);
			
			ЗадачаОбъект = ЗадачаПроцесса.ПолучитьОбъект();
			ЗаполнитьЗначенияСвойств(ЗадачаОбъект, ИнфоОПеренаправлении, 
				"Исполнитель, РольИсполнителя");
			
			Если Не ПустаяСтрока(ИнфоОПеренаправлении.Комментарий) Тогда
				ЗадачаОбъект.ДополнительныеСвойства.Вставить("КомментарийПеренаправления", ИнфоОПеренаправлении.Комментарий);
			КонецЕсли;	
			Если ИнфоОПеренаправлении.Свойство("Автоперенаправление") Тогда
				ЗадачаОбъект.ДополнительныеСвойства.Вставить("Автоперенаправление", ИнфоОПеренаправлении.Автоперенаправление);
			КонецЕсли;
			
			// Установка необходимости обновления прав доступа
			ЗадачаОбъект.ДополнительныеСвойства.Вставить("ДополнительныеПравообразующиеЗначенияИзменены");
			ЗадачаОбъект.ПринятаКИсполнению = Ложь;
			Если ОтключитьОбновлениеЗадач Тогда
				ЗадачаОбъект.ДополнительныеСвойства.Вставить("ОтключитьОбновлениеЗадач", Истина);
			КонецЕсли;
			ЗадачаОбъект.Записать();
			УстановитьПривилегированныйРежим(Ложь);
			
			ПриПеренаправленииЗадачи(ЗадачаОбъект, ОтключитьОбновлениеЗадач);
			
			ПротоколированиеРаботыСотрудников.ЗаписатьПеренаправлениеЗадачи(ЗадачаПроцесса);
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

Функция ТекущиеУчастникиПроцесса(Процесс) Экспорт
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Процесс)) Тогда
		МенеджерПроцесса = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Процесс);
	Иначе
		МенеджерПроцесса = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Процесс.Ссылка);
	КонецЕсли;
	
	Возврат МенеджерПроцесса.ТекущиеУчастникиПроцесса(Процесс);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Перенаправление

Процедура ПриПеренаправленииЗадачи(ЗадачаОбъект, ОтключитьОбновлениеЗадач = Ложь) 
	
	Если ЗадачаОбъект.БизнесПроцесс = Неопределено Или ЗадачаОбъект.БизнесПроцесс.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ТипБизнесПроцесса = Метаданные.НайтиПоТипу(ТипЗнч(ЗадачаОбъект.БизнесПроцесс));
	ЕстьМетодПриПеренаправленииЗадачи = Ложь;
	Попытка
		ЕстьМетодПриПеренаправленииЗадачи = БизнесПроцессы[ТипБизнесПроцесса.Имя].ЕстьМетодПриПеренаправленииЗадачи()
	Исключение
		// Функция "ЕстьМетодПриПеренаправленииЗадачи" может быть не определена для бизнес-процесса этого типа
	КонецПопытки;
	Если ЕстьМетодПриПеренаправленииЗадачи Тогда
		БизнесПроцессы[ТипБизнесПроцесса.Имя].ПриПеренаправленииЗадачи(ЗадачаОбъект.Ссылка, ОтключитьОбновлениеЗадач);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Определяет является ли задача задачей ознакомления с результатом.
//
// Парамеры:
//	Задача - ЗадачаСсылка.ЗадачаИсполнителя - Задача, которую необходимо проверить.
//
// Возвращаемое значение:
// 	Булево:
//		* Истина - задача является задачей ознакомления с результатом.
//		* Ложь - в противном случае.
//
Функция ЭтоЗадачаОзнакомленияСРезультатом(Задача) Экспорт

	РезультатФункции = Ложь;
	
	Если Не ТипЗнч(Задача) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	РеквизитыЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Задача, "БизнесПроцесс, ТочкаМаршрута");
	Если (РеквизитыЗадачи.ТочкаМаршрута = БизнесПроцессы.Подписание.ТочкиМаршрута.ОбработатьРезультат
			Или РеквизитыЗадачи.ТочкаМаршрута = БизнесПроцессы.Приглашение.ТочкиМаршрута.Ознакомиться
			Или РеквизитыЗадачи.ТочкаМаршрута = БизнесПроцессы.Регистрация.ТочкиМаршрута.Ознакомиться
			Или РеквизитыЗадачи.ТочкаМаршрута = БизнесПроцессы.Согласование.ТочкиМаршрута.Ознакомиться
			Или РеквизитыЗадачи.ТочкаМаршрута = БизнесПроцессы.Утверждение.ТочкиМаршрута.Ознакомиться)
			И ПроцессЗавершенСПоложительнымРезультатом(РеквизитыЗадачи.БизнесПроцесс) Тогда
		
		РезультатФункции = Истина;		
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

#КонецОбласти

// Вызывается при создании формы списка задач на сервере.
//
// Параметры
//  УсловноеОформление - УсловноеОформление - условное оформление списка задач
//
Процедура УстановитьОформлениеЗадач(Знач СписокЗадачИлиЕгоУсловноеОформление) Экспорт
	
	Если ТипЗнч(СписокЗадачИлиЕгоУсловноеОформление) = Тип("ДинамическийСписок") Тогда
		УсловноеОформление = СписокЗадачИлиЕгоУсловноеОформление.КомпоновщикНастроек.Настройки.УсловноеОформление;
		УсловноеОформление.ИдентификаторПользовательскойНастройки = "ОсновноеОформление";
	Иначе
		УсловноеОформление = СписокЗадачИлиЕгоУсловноеОформление;
	КонецЕсли;
	
	// недействительные объекты - Исполнитель
	ПредставлениеЭлемента = "Исполнитель недействителен (стандартная настройка)";
	ЭлементУсловногоОформления = ЭлементУсловногоОформленияПоПредставлению(УсловноеОформление, ПредставлениеЭлемента);
	
	ГруппаОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбораДанных.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ЭлементОтбораДанных = ГруппаОтбораДанных.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Исполнитель.Действует");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Ложь;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементОтбораДанных = ГруппаОтбораДанных.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Исполнитель.Недействителен");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Истина;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
	ЭлементЦветаОформления.Значение = Метаданные.ЭлементыСтиля.ТекстЗапрещеннойЯчейкиЦвет.Значение; 
	ЭлементЦветаОформления.Использование = Истина;
	
	Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("Исполнитель");
	
	// недействительные объекты - Автор
	ПредставлениеЭлемента = "Автор недействителен (стандартная настройка)";
	ЭлементУсловногоОформления = ЭлементУсловногоОформленияПоПредставлению(УсловноеОформление, ПредставлениеЭлемента);
	
	ГруппаОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбораДанных.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ЭлементОтбораДанных = ГруппаОтбораДанных.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Автор.Недействителен");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Истина;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементОтбораДанных = ГруппаОтбораДанных.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Автор.Действует");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Ложь;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
	ЭлементЦветаОформления.Значение = Метаданные.ЭлементыСтиля.ТекстЗапрещеннойЯчейкиЦвет.Значение; 
	ЭлементЦветаОформления.Использование = Истина;
	
	Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("Автор");
	
	// установка оформления для задач, не принятых к исполнению
	ПредставлениеЭлемента = "Задача не принята к исполнению (стандартная настройка)";
	ЭлементУсловногоОформления = ЭлементУсловногоОформленияПоПредставлению(УсловноеОформление, ПредставлениеЭлемента);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПринятаКИсполнению");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Ложь;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("Font");
	ЭлементЦветаОформления.Значение = ШрифтыСтиля.НеПринятыеКИсполнениюЗадачи;
	ЭлементЦветаОформления.Использование = Истина;
	
	Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеПредметЗаголовок = Новый ПолеКомпоновкиДанных("ПредметЗаголовок");
	Если УсловноеОформление.ДоступныеПоляПолей.НайтиПоле(ПолеПредметЗаголовок) = Неопределено Тогда
		Поле.Поле = Новый ПолеКомпоновкиДанных("Наименование");
	Иначе
		Поле.Поле = ПолеПредметЗаголовок;
	КонецЕсли;
	
	// установка оформления для задач, с подзадачами
	ПолеПодзадачи = Новый ПолеКомпоновкиДанных("ЕстьПодзадачи");
	Если УсловноеОформление.ДоступныеПоляОтбора.НайтиПоле(ПолеПодзадачи) <> Неопределено Тогда
		
		ПредставлениеЭлемента = "Задача с подзадачами (стандартная настройка)";
		ЭлементУсловногоОформления = ЭлементУсловногоОформленияПоПредставлению(УсловноеОформление, ПредставлениеЭлемента);
		
		ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = ПолеПодзадачи;
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораДанных.ПравоеЗначение = Истина;
		ЭлементОтбораДанных.Использование = Истина;
		
		ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
		ЭлементЦветаОформления.Значение = Новый Цвет(185, 170, 110);
		ЭлементЦветаОформления.Использование = Истина;
		
		Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеПредметЗаголовок = Новый ПолеКомпоновкиДанных("ПредметЗаголовок");
		Если УсловноеОформление.ДоступныеПоляПолей.НайтиПоле(ПолеПредметЗаголовок) = Неопределено Тогда
			Поле.Поле = Новый ПолеКомпоновкиДанных("Наименование");
		Иначе
			Поле.Поле = ПолеПредметЗаголовок;
		КонецЕсли;
	КонецЕсли;
	
	// установка оформления для выполненных задач
	ПредставлениеЭлемента = "Выполненные задачи (стандартная настройка)";
	ЭлементУсловногоОформления = ЭлементУсловногоОформленияПоПредставлению(УсловноеОформление, ПредставлениеЭлемента);

	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Выполнена");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Истина;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
	ЭлементЦветаОформления.Значение = Метаданные.ЭлементыСтиля.ВыполненнаяЗадача.Значение; 
	ЭлементЦветаОформления.Использование = Истина;
	
	// установка оформления для просроченных задач
	ПредставлениеЭлемента = БизнесПроцессыИЗадачиКлиентСервер.ПолучитьПредставлениеУсловногоОформленияПросроченныхЗадач();
	ЭлементУсловногоОформления = ЭлементУсловногоОформленияПоПредставлению(УсловноеОформление, ПредставлениеЭлемента);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СрокИсполненияДляОтображенияВСписке");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СрокИсполненияДляОтображенияВСписке");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ЭлементОтбораДанных.ПравоеЗначение = ТекущаяДатаСеанса();
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Выполнена");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Ложь;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
	ЭлементЦветаОформления.Значение =  Метаданные.ЭлементыСтиля.ПросроченныеДанныеЦвет.Значение;   
	ЭлементЦветаОформления.Использование = Истина;
	
	ЭлементОбластиОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ЭлементОбластиОформления.Поле = Новый ПолеКомпоновкиДанных("СрокИсполненияДляОтображенияВСписке");
	
КонецПроцедуры

// Вызывается при создании формы списка бизнес-процессов на сервере.
//
// Параметры
//  УсловноеОформление - УсловноеОформление - условное оформление списка бизнес-процессов
//
Процедура УстановитьОформлениеБизнесПроцессов(Знач УсловноеОформление) Экспорт

	// недействительные объекты - Исполнитель
	Если УсловноеОформление.ДоступныеПоляПолей.Элементы.Найти("Исполнитель") <> Неопределено Тогда
		ПредставлениеЭлемента = "Исполнитель недействителен (стандартная настройка)";
		ЭлементУсловногоОформления = ЭлементУсловногоОформленияПоПредставлению(УсловноеОформление, ПредставлениеЭлемента);
		
		ГруппаОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ГруппаОтбораДанных.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
		
		ЭлементОтбораДанных = ГруппаОтбораДанных.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Исполнитель.Действует");
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораДанных.ПравоеЗначение = Ложь;
		ЭлементОтбораДанных.Использование = Истина;
		
		ЭлементОтбораДанных = ГруппаОтбораДанных.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Исполнитель.Недействителен");
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораДанных.ПравоеЗначение = Истина;
		ЭлементОтбораДанных.Использование = Истина;
		
		ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
		ЭлементЦветаОформления.Значение = Метаданные.ЭлементыСтиля.ТекстЗапрещеннойЯчейкиЦвет.Значение;
		ЭлементЦветаОформления.Использование = Истина;
		
		Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		Поле.Поле = Новый ПолеКомпоновкиДанных("Исполнитель");
	КонецЕсли;
	
	// недействительные объекты - Автор
	ПредставлениеЭлемента = "Автор недействителен (стандартная настройка)";
	ЭлементУсловногоОформления = ЭлементУсловногоОформленияПоПредставлению(УсловноеОформление, ПредставлениеЭлемента);
	
	ГруппаОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбораДанных.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ЭлементОтбораДанных = ГруппаОтбораДанных.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Автор.Недействителен");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Истина;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементОтбораДанных = ГруппаОтбораДанных.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Автор.Действует");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Ложь;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
	ЭлементЦветаОформления.Значение = Метаданные.ЭлементыСтиля.ТекстЗапрещеннойЯчейкиЦвет.Значение;
	ЭлементЦветаОформления.Использование = Истина;
	
	Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("Автор");
	
	// завершенные процессы
	ПредставлениеЭлемента = "Процесс завершен (стандартная настройка)";
	ЭлементУсловногоОформления = ЭлементУсловногоОформленияПоПредставлению(УсловноеОформление, ПредставлениеЭлемента);

	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Завершен");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Истина;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
	ЭлементЦветаОформления.Значение = Метаданные.ЭлементыСтиля.ЗавершенныйБизнесПроцесс.Значение; 
	ЭлементЦветаОформления.Использование = Истина;
	
	Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("Наименование");
	Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("Дата");
	Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("СрокИсполнения");
	
	// нестартованные процессы
	ПредставлениеЭлемента = "Процесс не стартован (стандартная настройка)";
	ЭлементУсловногоОформления = ЭлементУсловногоОформленияПоПредставлению(УсловноеОформление, ПредставлениеЭлемента);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Стартован");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Ложь;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
	ЭлементЦветаОформления.Значение = Метаданные.ЭлементыСтиля.НеСтартованныйБизнесПроцесс.Значение; 
	ЭлементЦветаОформления.Использование = Истина;
	
	Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	Поле.Поле = Новый ПолеКомпоновкиДанных("Наименование");
	
	// просроченные процессы
	ПолеСрокНайден = Ложь;
	Для Каждого ДоступныйЭлемент Из ЭлементУсловногоОформления.Отбор.ДоступныеПоляОтбора.Элементы Цикл
		Если ДоступныйЭлемент.Поле = Новый ПолеКомпоновкиДанных("СрокИсполнения") Тогда
			ПолеСрокНайден = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПолеСрокНайден Тогда
		ПредставлениеЭлемента = "Процесс просрочен (стандартная настройка)";
		ЭлементУсловногоОформления = ЭлементУсловногоОформленияПоПредставлению(УсловноеОформление, ПредставлениеЭлемента);
		
		ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СрокИсполнения");
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
		ЭлементОтбораДанных.ПравоеЗначение = ТекущаяДатаСеанса();
		ЭлементОтбораДанных.Использование = Истина;
		
		ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Стартован");
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораДанных.ПравоеЗначение = Истина;
		ЭлементОтбораДанных.Использование = Истина;
		
		ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Завершен");
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораДанных.ПравоеЗначение = Ложь;
		ЭлементОтбораДанных.Использование = Истина;
		
		ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
		ЭлементЦветаОформления.Значение = Метаданные.ЭлементыСтиля.ПросроченныеДанныеЦвет.Значение; 
		ЭлементЦветаОформления.Использование = Истина;
		
		Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		Поле.Поле = Новый ПолеКомпоновкиДанных("СрокИсполнения");
	КонецЕсли;
	
	ПолеСрокНайден = Ложь;
	Для Каждого ДоступныйЭлемент Из ЭлементУсловногоОформления.Отбор.ДоступныеПоляОтбора.Элементы Цикл
		Если ДоступныйЭлемент.Поле = Новый ПолеКомпоновкиДанных("СрокИсполненияДатой") Тогда
			ПолеСрокНайден = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПолеСрокНайден Тогда
		ПредставлениеЭлемента = "Процесс просрочен (стандартная настройка)";
		ЭлементУсловногоОформления = ЭлементУсловногоОформленияПоПредставлению(УсловноеОформление, ПредставлениеЭлемента);
		
		ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СрокИсполненияДатой");
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
		ЭлементОтбораДанных.ПравоеЗначение = ТекущаяДатаСеанса();
		ЭлементОтбораДанных.Использование = Истина;
		
		ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Стартован");
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораДанных.ПравоеЗначение = Истина;
		ЭлементОтбораДанных.Использование = Истина;
		
		ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Завершен");
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораДанных.ПравоеЗначение = Ложь;
		ЭлементОтбораДанных.Использование = Истина;
		
		ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
		ЭлементЦветаОформления.Значение = Метаданные.ЭлементыСтиля.ПросроченныеДанныеЦвет.Значение; 
		ЭлементЦветаОформления.Использование = Истина;
		
		Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		Поле.Поле = Новый ПолеКомпоновкиДанных("СрокИсполненияДатой");
	КонецЕсли;

	// прерванные процессы
	Если УсловноеОформление.ДоступныеПоляПолей.Элементы.Найти("Состояние") <> Неопределено Тогда
	
		ПредставлениеЭлемента = "Процесс прерван (стандартная настройка)";
		ЭлементУсловногоОформления = ЭлементУсловногоОформленияПоПредставлению(УсловноеОформление, ПредставлениеЭлемента);

		ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Состояние");
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораДанных.ПравоеЗначение = Перечисления.СостоянияБизнесПроцессов.Прерван;
		ЭлементОтбораДанных.Использование = Истина;
		
		ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
		ЭлементЦветаОформления.Значение = Метаданные.ЭлементыСтиля.ЗавершенныйБизнесПроцесс.Значение; 
		ЭлементЦветаОформления.Использование = Истина;
		
		Поле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		Поле.Поле = Новый ПолеКомпоновкиДанных("Наименование");
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает строковое представление исполнителя задачи Исполнитель, 
// либо указанного в параметре РольИсполнителя.
//
// Параметры:
//  Исполнитель     - ПользовательСсылка  - исполнитель задачи.
//  РольИсполнителя  - Справочники.ПолныеРоли - роль.
//
// Возвращаемое значение:
//   Строка 
//
Функция ИсполнительСтрокой(Знач Исполнитель, Знач РольИсполнителя) Экспорт

	
	Если НЕ Исполнитель.Пустая() Тогда
		Возврат Строка(Исполнитель)
	ИначеЕсли НЕ РольИсполнителя.Пустая() Тогда
		Возврат РольСтрокой(РольИсполнителя);
	КонецЕсли;
	Возврат НСтр("ru = 'Не указан'");

КонецФункции

// Возвращает строковое представление роли РольИсполнителя.
//
// Параметры
//  РольИсполнителя  - Справочники.ПолныеРоли - роль.
//
// Возвращаемое значение:
//   Строка 
//
Функция РольСтрокой(Знач РольИсполнителя) Экспорт
	
	Если НЕ РольИсполнителя.Пустая() Тогда
		Возврат Строка(РольИсполнителя);
	КонецЕсли;
	Возврат НСтр("ru = 'Не указана'");

КонецФункции

// Помечает на удаление задачи указанного бизнес-процесса.
//
// Параметры
//  БизнесПроцессСсылка  - бизнес-процесс
//  ПометкаУдаления  - Булево - значение свойства ПометкаУдаления.
//
Процедура УстановитьПометкуУдаленияЗадач(БизнесПроцессСсылка, ПометкаУдаления) Экспорт
	
	НачатьТранзакцию();
	Попытка
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Задачи.Ссылка КАК Ссылка
			|ИЗ
			|	Задача.ЗадачаИсполнителя КАК Задачи
			|ГДЕ
			|	Задачи.БизнесПроцесс = &БизнесПроцесс
			|	И Задачи.ИсключенаИзПроцесса = ЛОЖЬ");
		Запрос.УстановитьПараметр("БизнесПроцесс", БизнесПроцессСсылка);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ЗадачаОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ЗадачаОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
		КонецЦикла;	
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,
			БизнесПроцессСсылка.Метаданные(),
			БизнесПроцессСсылка,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры	

// Установить формат отображения и редактирования поля формы типа Дата
// в зависимости от настроек подсистемы.
//
// Параметры
//  ПолеДаты  - элемент управления формы, поле со значением типа Дата.
//
Процедура УстановитьФорматДаты(ПолеДаты) Экспорт
	
	ИспользоватьДатуИВремяВСрокахЗадач = ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач");
	СтрокаФормата = ?(ИспользоватьДатуИВремяВСрокахЗадач, "ДЛФ=DT", "ДЛФ=D");
	Если ПолеДаты.Вид = ВидПоляФормы.ПолеВвода Тогда
		ПолеДаты.ФорматРедактирования 	= СтрокаФормата;
	Иначе	
		ПолеДаты.Формат					= СтрокаФормата;
	КонецЕсли;	
	ПолеДаты.Ширина = ?(ИспользоватьДатуИВремяВСрокахЗадач, 0, 8);
	
КонецПроцедуры		

// Проверяет, является ли указанная задача ведущей.
//
// Параметры
//  ЗадачаСсылка  - задача.
//
// Возвращаемое значение:
//   Булево
//
Функция БизнесПроцессыВедущейЗадачи(ЗадачаСсылка) Экспорт
	
	Результат = ВыбратьБизнесПроцессыВедущейЗадачи(ЗадачаСсылка);
	Возврат Результат.Выгрузить().ВыгрузитьКолонку("Ссылка");
		
КонецФункции	

// Возвращает массив подчиненных БП
//
Функция ПолучитьПодчиненныеБизнесПроцессы(ВедущаяЗадача) Экспорт
	
	Результат = Новый Массив;
	
	Шаблон =
		"ВЫБРАТЬ
		|	ТаблицаБизнесПроцесс.Ссылка КАК Ссылка
		|ИЗ
		|	БизнесПроцесс.[ИмяБизнесПроцесса] КАК ТаблицаБизнесПроцесс
		|ГДЕ
		|	ТаблицаБизнесПроцесс.ВедущаяЗадача = &ВедущаяЗадача
		|	И ТаблицаБизнесПроцесс.Стартован
		|	И (НЕ ТаблицаБизнесПроцесс.Завершен)";
	
	Для каждого БизнесПроцессМета Из Метаданные.БизнесПроцессы Цикл
		ТекстЗапроса = СтрЗаменить(Шаблон, "[ИмяБизнесПроцесса]", БизнесПроцессМета.Имя);
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("ВедущаяЗадача", ВедущаяЗадача);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат.Добавить(Выборка.Ссылка);
		КонецЦикла;
	КонецЦикла;
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РешениеВопросовВыполненияЗадач.Ссылка
		|ИЗ
		|	БизнесПроцесс.РешениеВопросовВыполненияЗадач КАК РешениеВопросовВыполненияЗадач
		|ГДЕ
		|	РешениеВопросовВыполненияЗадач.ПредметРассмотрения = &Задача";
	Запрос.УстановитьПараметр("Задача", ВедущаяЗадача);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.Ссылка);	
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает права доступа сотрудников или пользователей к переданным файлам.
// 
// Параметры:
//  Файлы - Массив Из СправочникСсылка.Файлы -
//  СотрудникиОтбор - Массив Из СправочникСсылка.Сотрудники - Сотрудники для отбора.
//  				- Неопределено -
//  ПользователиОтбор - Массив Из СправочникСсылка.Пользователи - Пользователи для отбора.
//					  - Неопределено -
// 
// Возвращаемое значение:
//  См. ДокументооборотПраваДоступа.ТаблицаПравСотрудниковПоОбъектам
Функция ПраваПравообладателейПоФайлам(Файлы, СотрудникиОтбор = Неопределено, ПользователиОтбор = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаПрав = ДокументооборотПраваДоступа.ТаблицаПравСотрудниковПоОбъектам();
	
	// Права на изменение файлов должны быть у всех участников процесса.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Файлы.Ссылка КАК ОбъектДоступа,
		|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
		|	ПраваПоДескрипторамДоступаОбъектов.Пользователь,
		|	ИСТИНА КАК Чтение,
		|	ИСТИНА КАК Добавление,
		|	ИСТИНА КАК Изменение,
		|	ИСТИНА КАК Удаление,
		|	ЛОЖЬ КАК УправлениеПравами
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|		ПО Файлы.ВладелецФайла = ДескрипторыДляОбъектов.Объект
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
		|		ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
		|ГДЕ
		|	Файлы.Ссылка В (&Файлы)
		|	И &ПраваПоДескрипторам_ОтборПравообладателей");
	
	Запрос.УстановитьПараметр("Файлы", Файлы);
	ДокументооборотПраваДоступа.ПодменитьОтборПравообладателейВЗапросе(
		Запрос, "&ПраваПоДескрипторам_ОтборПравообладателей", "ПраваПоДескрипторамДоступаОбъектов" ,
		СотрудникиОтбор, ПользователиОтбор);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаПрав.Добавить(), Выборка);
	КонецЦикла;
	
	ИдентификаторОМ = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Справочники.Файлы);
	ДокументооборотПраваДоступа.РасширитьТаблицуПравНеограниченнымиПравами(
		ТаблицаПрав, ИдентификаторОМ, Файлы, СотрудникиОтбор, ПользователиОтбор);
	
	Возврат ТаблицаПрав;
	
КонецФункции

// Формирует условия запрета выполнения для задачи.
//
// Параметры:
//  Задача - ЗадачаСсылка - Задача.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Условия запрета выполнения.
//   * Условие - СправочникСсылка.УсловияЗадач - Условие.
//   * ТекстПредупреждения - Строка - Текст предупреждения.
//
Функция УсловияЗапретаВыполнения(Задача) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	УсловияЗапретаВыполнения = Новый ТаблицаЗначений;
	УсловияЗапретаВыполнения.Колонки.Добавить("Условие");
	УсловияЗапретаВыполнения.Колонки.Добавить("ТекстПредупреждения");
	
	Если Не ЗначениеЗаполнено(Задача) Тогда
		Возврат УсловияЗапретаВыполнения;
	КонецЕсли;
	
	БизнесПроцесс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "БизнесПроцесс");
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(БизнесПроцесс);
	Если Не МенеджерОбъекта.ИспользуетУсловияЗапретаВыполненияЗадач() Тогда
		Возврат УсловияЗапретаВыполнения;
	КонецЕсли;
	
	Шаблон = ШаблонПроцесса(БизнесПроцесс);
	Если Не ЗначениеЗаполнено(Шаблон) Тогда
		Возврат УсловияЗапретаВыполнения;
	КонецЕсли;
	
	РезультатУсловияЗапретаВыполнения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		Шаблон,
		"УсловияЗапретаВыполнения");
	Если РезультатУсловияЗапретаВыполнения = Неопределено Тогда
		Возврат УсловияЗапретаВыполнения;
	КонецЕсли;
	УсловияЗапретаВыполнения = РезультатУсловияЗапретаВыполнения.Выгрузить();
	
	Возврат УсловияЗапретаВыполнения;
	
КонецФункции

// Получает шаблон процесса.
//
// Параметры:
//  БизнесПроцесс - БизнесПроцессСсылка - Бизнес процесс.
//
// Возвращаемое значение:
//  ПроизвольнаяСсылка - Шаблон процесса.
//
Функция ШаблонПроцесса(БизнесПроцесс) Экспорт
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(БизнесПроцесс, "Шаблон, ВедущаяЗадача");
	Шаблон = Реквизиты.Шаблон;
	Если Не ЗначениеЗаполнено(Реквизиты.Шаблон) И ЗначениеЗаполнено(Реквизиты.ВедущаяЗадача) Тогда
		Шаблон = ШаблонЭтапаПроцесса(БизнесПроцесс);
	КонецЕсли;
	
	Возврат Шаблон;
	
КонецФункции

// Получает шаблон этапа процесса.
//
// Параметры:
//  БизнесПроцесс - БизнесПроцессСсылка - Бизнес процесс, являющийся этапом комплексного процесса.
//
// Возвращаемое значение:
//  ПроизвольнаяСсылка - Шаблон процесса.
//
Функция ШаблонЭтапаПроцесса(БизнесПроцесс) Экспорт
	
	ШаблонЭтапа = Неопределено;
	
	ИдентификаторЭтапа = Неопределено;
	КомплексныйПроцесс = Неопределено;
	ИдентификаторыЭтапов = Новый Массив;
	ПроцессыКОбработке = Новый Массив;
	ПроцессыКОбработке.Добавить(БизнесПроцесс);
	Пока ПроцессыКОбработке.Количество() <> 0 Цикл
		ПроцессКОбработке = ПроцессыКОбработке[0];
		ПроцессыКОбработке.Удалить(0);
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	КомплексныйПроцессЭтапы.Ссылка,
			|	КомплексныйПроцессЭтапы.ИдентификаторЭтапа
			|ИЗ
			|	БизнесПроцесс.КомплексныйПроцесс.Этапы КАК КомплексныйПроцессЭтапы
			|ГДЕ
			|	КомплексныйПроцессЭтапы.ЗапущенныйБизнесПроцесс = &ЗапущенныйБизнесПроцесс";
		Запрос.УстановитьПараметр("ЗапущенныйБизнесПроцесс", ПроцессКОбработке);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			КомплексныйПроцесс = Выборка.Ссылка;
			ПроцессыКОбработке.Добавить(Выборка.Ссылка);
			ИдентификаторыЭтапов.Добавить(Выборка.ИдентификаторЭтапа);
			Если ИдентификаторЭтапа = Неопределено Тогда
				ИдентификаторЭтапа = Выборка.ИдентификаторЭтапа;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(КомплексныйПроцесс) Тогда
		Возврат ШаблонЭтапа;
	КонецЕсли;
	
	Шаблон = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КомплексныйПроцесс, "Шаблон");
	Если Не ЗначениеЗаполнено(Шаблон) Тогда
		Возврат ШаблонЭтапа;
	КонецЕсли;
	
	ШаблоныКОбработке = Новый Массив;
	ШаблоныКОбработке.Добавить(Шаблон);
	Пока ШаблоныКОбработке.Количество() <> 0 Цикл
		ШаблонКОбработке = ШаблоныКОбработке[0];
		ШаблоныКОбработке.Удалить(0);
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ШаблоныКомплексныхБизнесПроцессовЭтапы.ШаблонБизнесПроцесса КАК ШаблонБизнесПроцесса,
			|	ШаблоныКомплексныхБизнесПроцессовЭтапы.ИдентификаторЭтапа КАК ИдентификаторЭтапа
			|ИЗ
			|	Справочник.ШаблоныКомплексныхБизнесПроцессов.Этапы КАК ШаблоныКомплексныхБизнесПроцессовЭтапы
			|ГДЕ
			|	ШаблоныКомплексныхБизнесПроцессовЭтапы.Ссылка = &Ссылка
			|	И ШаблоныКомплексныхБизнесПроцессовЭтапы.ИдентификаторЭтапа В(&ИдентификаторыЭтапов)";
		Запрос.УстановитьПараметр("Ссылка", ШаблонКОбработке);
		Запрос.УстановитьПараметр("ИдентификаторыЭтапов", ИдентификаторыЭтапов);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ШаблоныКОбработке.Добавить(Выборка.ШаблонБизнесПроцесса);
			Если Выборка.ИдентификаторЭтапа = ИдентификаторЭтапа Тогда
				ШаблонЭтапа = Выборка.ШаблонБизнесПроцесса;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ШаблонЭтапа;
	
КонецФункции

// Текст предупреждения запрета выполнения в виде строки.
//
// Параметры:
//  Задача - ЗадачаСсылка - Задача.
//  ШаблонТекстаПредупреждения - Строка - Шаблон текста предупреждения.
// 
// Возвращаемое значение:
//  Строка - Текст предупреждения запрета выполнения.
//
Функция ТекстПредупрежденияЗапретаВыполненияСтрока(Задача, Шаблон) Экспорт
	
	ТекстПредупреждения = Шаблон;
	
	Предметы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "Предметы").Выгрузить();
	Для Каждого Предмет Из Предметы Цикл
		ШаблонПредмета = СтрШаблон("[Предметы.""%1""]", Предмет.ИмяПредмета);
		ТекстПредмета = СтрШаблон("%1 (%2)", Предмет.Предмет, Предмет.ИмяПредмета);
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, ШаблонПредмета, ТекстПредмета);
	КонецЦикла;
	
	ТекстПредупреждения = "- " + ТекстПредупреждения;
	
	Возврат ТекстПредупреждения;
	
КонецФункции

// Текст предупреждения запрета выполнения в виде строки.
//
// Параметры:
//  Предметы - Массив
//  Шаблон - Строка - Шаблон текста предупреждения.
// 
// Возвращаемое значение:
//  Строка - Текст предупреждения запрета выполнения.
//
Функция ТекстПредупрежденияЗапретаВыполненияСтрокаПредметы(Предметы, Шаблон) Экспорт
	
	ТекстПредупреждения = Шаблон;
	
	Для Каждого Предмет Из Предметы Цикл
		ШаблонПредмета = СтрШаблон("[Предметы.""%1""]", Предмет);
		ТекстПредмета = СтрШаблон("%1", Предмет);
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, ШаблонПредмета, ТекстПредмета);
	КонецЦикла;
	
	ТекстПредупреждения = "- " + ТекстПредупреждения;
	
	Возврат ТекстПредупреждения;
	
КонецФункции

// Текст предупреждения запрета выполнения в виде форматированной строки.
//
// Параметры:
//  Задача - ЗадачаСсылка - Задача.
//  ШаблонТекстаПредупреждения - Строка - Шаблон текста предупреждения.
//
// Возвращаемое значение:
//  ФорматированнаяСтрока - Текст предупреждения запрета выполнения.
//
Функция ТекстПредупрежденияЗапретаВыполненияФорматированнаяСтрока(Задача, Шаблон) Экспорт
	
	ОписанияСтрок = Новый Массив;
	ОписанияСтрок.Добавить("- ");
	ОписанияСтрок.Добавить(Шаблон);
	
	Предметы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "Предметы").Выгрузить();
	Для Каждого Предмет Из Предметы Цикл
		
		НавигационнаяСсылкаПредмета = Неопределено;
		Если ЗначениеЗаполнено(Предмет.Предмет) Тогда
			НавигационнаяСсылкаПредмета = ПолучитьНавигационнуюСсылку(Предмет.Предмет);
		КонецЕсли;
		
		ШаблонПредмета = СтрШаблон("[Предметы.""%1""]", Предмет.ИмяПредмета);
		ДлинаШаблонаПредмет = СтрДлина(ШаблонПредмета);
		ФорматированнаяСтрокаПредмет = Новый ФорматированнаяСтрока(
			СтрШаблон("%1 (%2)", Предмет.Предмет, Предмет.ИмяПредмета),,,,
			НавигационнаяСсылкаПредмета);
		
		Индекс = 0;
		Пока Индекс < ОписанияСтрок.Количество() Цикл
			
			ОписаниеСтроки = ОписанияСтрок[Индекс];
			Если ТипЗнч(ОписаниеСтроки) <> Тип("Строка") Тогда
				Индекс = Индекс + 1;
				Продолжить;
			КонецЕсли;
			
			ПозицияШаблона = СтрНайти(ОписаниеСтроки, ШаблонПредмета);
			Если ПозицияШаблона = 0 Тогда
				Индекс = Индекс + 1;
				Продолжить;
			КонецЕсли;
			
			ТекстДо = Лев(ОписаниеСтроки, ПозицияШаблона - 1);
			ТекстПосле = Сред(ОписаниеСтроки, ПозицияШаблона + ДлинаШаблонаПредмет);
			
			ОписанияСтрок.Удалить(Индекс);
			ОписанияСтрок.Вставить(Индекс, ТекстПосле);
			ОписанияСтрок.Вставить(Индекс, ФорматированнаяСтрокаПредмет);
			ОписанияСтрок.Вставить(Индекс, ТекстДо);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ТекстПредупреждения = Новый ФорматированнаяСтрока(ОписанияСтрок);
	
	Возврат ТекстПредупреждения;
	
КонецФункции

// Текст предупреждения запрета выполнения в виде форматированной строки.
//
// Параметры:
//  Предметы - Массив
//  Шаблон - Строка - Шаблон текста предупреждения.
//
// Возвращаемое значение:
//  ФорматированнаяСтрока - Текст предупреждения запрета выполнения.
//
Функция ТекстПредупрежденияЗапретаВыполненияФорматированнаяСтрокаПредметы(Предметы, Шаблон) Экспорт
	
	ОписанияСтрок = Новый Массив;
	ОписанияСтрок.Добавить("- ");
	ОписанияСтрок.Добавить(Шаблон);
	
	Для Каждого Предмет Из Предметы Цикл
		
		НавигационнаяСсылкаПредмета = Неопределено;
		Если ЗначениеЗаполнено(Предмет) Тогда
			НавигационнаяСсылкаПредмета = ПолучитьНавигационнуюСсылку(Предмет);
		КонецЕсли;
		
		ШаблонПредмета = СтрШаблон("[Предметы.""%1""]", Предмет);
		ДлинаШаблонаПредмет = СтрДлина(ШаблонПредмета);
		ФорматированнаяСтрокаПредмет = Новый ФорматированнаяСтрока(
			СтрШаблон("%1", Предмет),,,,
			НавигационнаяСсылкаПредмета);
		
		Индекс = 0;
		Пока Индекс < ОписанияСтрок.Количество() Цикл
			
			ОписаниеСтроки = ОписанияСтрок[Индекс];
			Если ТипЗнч(ОписаниеСтроки) <> Тип("Строка") Тогда
				Индекс = Индекс + 1;
				Продолжить;
			КонецЕсли;
			
			ПозицияШаблона = СтрНайти(ОписаниеСтроки, ШаблонПредмета);
			Если ПозицияШаблона = 0 Тогда
				Индекс = Индекс + 1;
				Продолжить;
			КонецЕсли;
			
			ТекстДо = Лев(ОписаниеСтроки, ПозицияШаблона - 1);
			ТекстПосле = Сред(ОписаниеСтроки, ПозицияШаблона + ДлинаШаблонаПредмет);
			
			ОписанияСтрок.Удалить(Индекс);
			ОписанияСтрок.Вставить(Индекс, ТекстПосле);
			ОписанияСтрок.Вставить(Индекс, ФорматированнаяСтрокаПредмет);
			ОписанияСтрок.Вставить(Индекс, ТекстДо);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ТекстПредупреждения = Новый ФорматированнаяСтрока(ОписанияСтрок);
	
	Возврат ТекстПредупреждения;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры и функции

// Возвращает массив состоящий из текущего пользователя и пользователей, 
// которых замещает текущий пользователь
// 
// Возвращаемое значение:
//  Массив Из СправочникСсылка.Пользователи, СправочникСсылка.Сотрудники - массив пользователей
//
Функция ИсполнителиЗадачПользователя(ТекущийПользователь = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Замещения = Справочники.ЗамещающиеИПомощники.АктуальныеЗамещенияСотрудников(
		Сотрудники.СотрудникиПользователя(ТекущийПользователь),
		ЗамещающиеИПомощники.ОбластиЗамещенияПоИдентификаторуОбъектаМетаданных(
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Задачи")));
	
	Результат = Новый Массив;
	Результат.Добавить(ТекущийПользователь);
	
	Если Замещения.Количество() Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат, Замещения.ВыгрузитьКолонку("Замещающий"), Истина);
	КонецЕсли;
	
КонецФункции

// Получить бизнес-процессы ведущей задачи ЗадачаСсылка.
//
Функция ВыбратьБизнесПроцессыВедущейЗадачи(ЗадачаСсылка) Экспорт
	
	Итерация = 0;
	ТекстЗапроса = "";
	Для Каждого ТипБизнесПроцесса Из Метаданные.БизнесПроцессы Цикл
		
		Итерация = Итерация + 1;
		
		Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + "
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|";
				
		КонецЕсли;
		
		ФрагментЗапроса = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Таблица.Ссылка КАК Ссылка
			|ИЗ
			|	&ТаблицаПолноеИмя КАК Таблица
			|ГДЕ
			|	Таблица.ВедущаяЗадача = &ВедущаяЗадача";
			
		ФрагментЗапроса = СтрЗаменить(ФрагментЗапроса, "&ТаблицаПолноеИмя" , ТипБизнесПроцесса.ПолноеИмя());
		ФрагментЗапроса = СтрЗаменить(ФрагментЗапроса, "Таблица" , ТипБизнесПроцесса.Имя);
		
		Если Итерация > 1 Тогда
			ФрагментЗапроса = СтрЗаменить(ФрагментЗапроса, "РАЗРЕШЕННЫЕ" , "");
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + ФрагментЗапроса;
			
	КонецЦикла;	
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ВедущаяЗадача", ЗадачаСсылка);
	Результат = Запрос.Выполнить();
	Возврат Результат;
		
КонецФункции	

// Вид события журнала регистрации для событий данной подсистемы.
//
Функция СобытиеЖурналаРегистрации() Экспорт
	
	Возврат НСтр("ru = 'Процессы и задачи'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	
КонецФункции

// Вызывается при изменении состояния бизнес-процесса для того, чтобы 
// распространить это изменение состояния на невыполненные задачи этого 
// бизнес-процесса.
//
Процедура ПриИзмененииСостоянияБизнесПроцесса(БизнесПроцесс, СтароеСостояние, НовоеСостояние) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗадачаИсполнителя.Ссылка
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
		|ГДЕ
		|	ЗадачаИсполнителя.БизнесПроцесс = &БизнесПроцесс
		|	И ЗадачаИсполнителя.Выполнена = Ложь";

	Запрос.УстановитьПараметр("БизнесПроцесс", БизнесПроцесс.Ссылка);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Задача = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		Задача.Заблокировать();
		Задача.СостояниеБизнесПроцесса =  НовоеСостояние;
		
		Если БизнесПроцесс.ДополнительныеСвойства.Свойство("ОтключитьОбновлениеЗадач") Тогда
			Задача.ДополнительныеСвойства.Вставить("ОтключитьОбновлениеЗадач", БизнесПроцесс.ДополнительныеСвойства.ОтключитьОбновлениеЗадач);
		КонецЕсли;
		
		Задача.Записать();
		
		ПриИзмененииСостоянияЗадачи(Задача.Ссылка, СтароеСостояние, НовоеСостояние);
	КонецЦикла;

КонецПроцедуры

Процедура ПриИзмененииСостоянияЗадачи(ЗадачаСсылка, СтароеСостояние, НовоеСостояние)
	
	Если Не РаботаСПроцессамиПоОбработкамОбъектовСобытия.ОбработатьИзменениеСостояниеВложенныхПроцессов(
		ЗадачаСсылка) Тогда
	
		// Меняем состояние вложенных бизнес-процессов
		Для каждого МетаданныеБизнесПроцесса Из Метаданные.БизнесПроцессы Цикл
			
			Если НЕ ПравоДоступа("Изменение", МетаданныеБизнесПроцесса) Тогда
			    Продолжить;
			КонецЕсли;
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	БизнесПроцессы.Ссылка
			|ИЗ
			|	&БизнесПроцесс КАК БизнесПроцессы
			|ГДЕ
			|   БизнесПроцессы.ВедущаяЗадача = &ВедущаяЗадача
			|   И БизнесПроцессы.ПометкаУдаления = Ложь
			|   И БизнесПроцессы.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Прерван)
			| 	И БизнесПроцессы.Завершен = Ложь";
			
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&БизнесПроцесс", МетаданныеБизнесПроцесса.ПолноеИмя());
			Запрос.УстановитьПараметр("ВедущаяЗадача", ЗадачаСсылка);
			
			Результат = Запрос.Выполнить();
			
			ВыборкаДетальныеЗаписи = Результат.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				
				ПроцессСсылка = ВыборкаДетальныеЗаписи.Ссылка;
				
				Если РаботаСПроцессамиПоОбработкамОбъектовСобытия.ОбработатьИзменениеСостоянияПроцессаОбработкиВИерархии(
					ПроцессСсылка, НовоеСостояние) Тогда
					
					Продолжить;
				КонецЕсли;	
				
				БизнесПроцесс = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
				БизнесПроцесс.Состояние = НовоеСостояние;
				БизнесПроцесс.ДополнительныеСвойства.Вставить("ИзменениеСостоянияВложенногоПроцесса", Истина);
				БизнесПроцесс.Записать();
				
			КонецЦикла;
			
		КонецЦикла;
	
	КонецЕсли;
	
	// Меняем состояние подчиненных бизнес-процессов
	Для каждого МетаданныеБизнесПроцесса Из Метаданные.БизнесПроцессы Цикл
		
		РеквизитГлавнаяЗадача = МетаданныеБизнесПроцесса.Реквизиты.Найти("ГлавнаяЗадача");
		Если РеквизитГлавнаяЗадача = Неопределено Тогда
			Продолжить;
		КонецЕсли;	
			
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	БизнесПроцессы.Ссылка
			|ИЗ
			|	&БизнесПроцесс КАК БизнесПроцессы
			|ГДЕ
			|   БизнесПроцессы.ГлавнаяЗадача = &ГлавнаяЗадача
			|   И БизнесПроцессы.ПометкаУдаления = Ложь
			|   И БизнесПроцессы.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Прерван)
			| 	И БизнесПроцессы.Завершен = Ложь";
			
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&БизнесПроцесс", МетаданныеБизнесПроцесса.ПолноеИмя());
		Запрос.УстановитьПараметр("ГлавнаяЗадача", ЗадачаСсылка);

		Результат = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = Результат.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			БизнесПроцесс = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			БизнесПроцесс.Состояние = НовоеСостояние;
			БизнесПроцесс.Записать();
			
		КонецЦикла;
		
	КонецЦикла;	
	
КонецПроцедуры
	
// Заполняет реквизит ГлавнаяЗадача при создании бизнес-процесса
// на основании другого бизнес-процесса
//
Процедура ЗаполнитьГлавнуюЗадачу(БизнесПроцессОбъект, ДанныеЗаполнения) Экспорт
	
	Если БизнесПроцессыИЗадачиПереопределяемый.ЗаполнитьГлавнуюЗадачу(БизнесПроцессОбъект, ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеЗаполнения = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		БизнесПроцессОбъект.ГлавнаяЗадача = ДанныеЗаполнения;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("ЗадачаИсполнителя") Тогда
		БизнесПроцессОбъект.ГлавнаяЗадача = ДанныеЗаполнения.ЗадачаИсполнителя;
	КонецЕсли;
	
КонецПроцедуры

Функция ЭлементУсловногоОформленияПоПредставлению(УсловноеОформление, Представление) Экспорт
	
	Для Каждого ЭлементОформления из УсловноеОформление.Элементы Цикл
		Если ЭлементОформления.Представление = Представление Тогда
			ЭлементОформления.Отбор.Элементы.Очистить();
			ЭлементОформления.Поля.Элементы.Очистить();
			Возврат ЭлементОформления;
		КонецЕсли;
	КонецЦикла;
	
	ЭлементОформления = УсловноеОформление.Элементы.Вставить(0);
	ЭлементОформления.Представление = Представление;
	
	Возврат ЭлементОформления;
	
КонецФункции

// Возвращает число бизнес-Процессов по данному шаблону
Функция ПолучитьЧислоБизнесПроцессовПоШаблону(Шаблон) Экспорт
	
	ТипСсылки = ТипЗнч(Шаблон);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(КОЛИЧЕСТВО(ОбъектДанных.Ссылка), 0) КАК ЧислоПроцессов
		|ИЗ
		|	БизнесПроцесс.Исполнение КАК ОбъектДанных
		|ГДЕ
		|	ОбъектДанных.Шаблон = &Шаблон";
		
	Если ТипСсылки = Тип("СправочникСсылка.ШаблоныОзнакомления") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Исполнение", "Ознакомление");
	ИначеЕсли ТипСсылки = Тип("СправочникСсылка.ШаблоныРассмотрения") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Исполнение", "Рассмотрение");
	ИначеЕсли ТипСсылки = Тип("СправочникСсылка.ШаблоныРегистрации") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Исполнение", "Регистрация");
	ИначеЕсли ТипСсылки = Тип("СправочникСсылка.ШаблоныСогласования") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Исполнение", "Согласование");
	ИначеЕсли ТипСсылки = Тип("СправочникСсылка.ШаблоныПодписания") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Исполнение", "Подписание");
	ИначеЕсли ТипСсылки = Тип("СправочникСсылка.ШаблоныУтверждения") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Исполнение", "Утверждение");
	ИначеЕсли ТипСсылки = Тип("СправочникСсылка.ШаблоныКомплексныхБизнесПроцессов") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Исполнение", "КомплексныйПроцесс");
	ИначеЕсли ТипСсылки = Тип("СправочникСсылка.ШаблоныПриглашения") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Исполнение", "Приглашение");
		
	КонецЕсли;	
	
	Запрос.УстановитьПараметр("Шаблон", Шаблон);	
		
	Выборка = Запрос.Выполнить().Выбрать();	
	Выборка.Следующий();
	Возврат Выборка.ЧислоПроцессов;
	
КонецФункции	

////////////////////////////////////////////////////////////////////////////////
// Служебный программный интерфейс

// См. описание этой же процедуры в модуле СтандартныеПодсистемыСервер.
Процедура ПриДобавленииОбработчиковСлужебныхСобытий(КлиентскиеОбработчики, СерверныеОбработчики) Экспорт
	
	// СЕРВЕРНЫЕ ОБРАБОТЧИКИ.
	
	СерверныеОбработчики["СтандартныеПодсистемы.ОбновлениеВерсииИБ\ПриДобавленииОбработчиковОбновления"].Добавить(
		"БизнесПроцессыИЗадачиСервер");
	
КонецПроцедуры

// Определить объекты метаданных, в модулях менеджеров которых ограничивается возможность 
// редактирования реквизитов при групповом изменении.
//
// Параметры:
//   Объекты - Соответствие - в качестве ключа указать полное имя объекта метаданных,
//                            подключенного к подсистеме "Групповое изменение объектов". 
//                            Дополнительно в значении могут быть перечислены имена экспортных функций:
//                            "РеквизитыНеРедактируемыеВГрупповойОбработке",
//                            "РеквизитыРедактируемыеВГрупповойОбработке".
//                            Каждое имя должно начинаться с новой строки.
//                            Если указана пустая строка, значит в модуле менеджера определены обе функции.
//
Процедура ПриОпределенииОбъектовСРедактируемымиРеквизитами(Объекты) Экспорт
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы

// Добавляет процедуры-обработчики обновления, необходимые данной подсистеме.
//
// Параметры:
//  Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                  общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.0.2.2";
	Обработчик.Процедура = "БизнесПроцессыИЗадачиСервер.ОбновлениеИнформационнойБазы";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.0.6.6";
	Обработчик.Процедура = "БизнесПроцессыИЗадачиСервер.ОбновлениеИнформационнойБазыПредметСтрокой";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.1.1.2";
	Обработчик.Процедура = "БизнесПроцессыИЗадачиСервер.ОбновлениеСостоянияИПринятияКИсполнению";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.2.1.1";
	Обработчик.Процедура = "БизнесПроцессыИЗадачиСервер.ОбновлениеКодаРолиИсполнителя";
	
КонецПроцедуры	

// Заполнить новое поле ПредметСтрокой у задачи ЗадачаИсполнителя.
// 
Процедура ОбновлениеИнформационнойБазыПредметСтрокой() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Задачи.Ссылка КАК Ссылка
		|ИЗ
		|	Задача.ЗадачаИсполнителя.Предметы КАК Задачи
		|ГДЕ
		|	Задачи.Предмет <> НЕОПРЕДЕЛЕНО";

	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗадачаОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		ЗадачаОбъект.ПредметСтрокой = МультипредметностьКлиентСервер.ПредметыСтрокой(ЗадачаОбъект.Предметы);
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ЗадачаОбъект);
	КонецЦикла;

КонецПроцедуры

// Обработчик подписки ИзменениеПредметаПередЗаписьюБизнесПроцесса
//
Процедура ИзменениеПредметаПередЗаписьюБизнесПроцессаПередЗаписью(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если МиграцияДанныхИзВнешнихСистемСервер.ЭтоОбъектИзДругойСистемы(Источник.ИсточникДанных) Тогда
		Возврат;
	КонецЕсли;	
	
	Если Источник.ДополнительныеСвойства.Свойство("ВидЗаписи")
		И Источник.ДополнительныеСвойства.ВидЗаписи <>
			"ЗаписьСОбновлением_Предметов_ПредметовЗадач_Проекта_ОбщегоСпискаПроцессов_РабочихГруппПредметов_РабочихГруппПроцессов_ДопРеквизитовПоПредметам" Тогда
		
		Возврат;
	КонецЕсли;
	
	Если Источник.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеПредметы = Неопределено;
	ПредыдущиеПредметы = Неопределено;
	Если Мультипредметность.ИзмененыПредметыДействийПроцесса(Источник, ТекущиеПредметы, ПредыдущиеПредметы) Тогда
		Мультипредметность.ПриИзмененииПредметовБизнесПроцесса(Источник, ТекущиеПредметы, ПредыдущиеПредметы);
	КонецЕсли;
	
КонецПроцедуры

// Обновляет коды в справочнике РолиИсполнителей
Процедура ОбновлениеКодаРолиИсполнителя() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РолиИсполнителей.Ссылка,
		|	РолиИсполнителей.Код
		|ИЗ
		|	Справочник.РолиИсполнителей КАК РолиИсполнителей";

	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ЗначениеКода = ВыборкаДетальныеЗаписи.Код;
		Если ПустаяСтрока(ЗначениеКода) Или СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ЗначениеКода) Тогда
			Продолжить;
		КонецЕсли;
		
		РольИсполнителейОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		РольИсполнителейОбъект.КраткоеПредставление = ЗначениеКода;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(РольИсполнителейОбъект);
		
	КонецЦикла;

КонецПроцедуры

// Обработчик подписи ЗадачаПриЗаписи
Процедура ПриЗаписиЗадачиСервер(Источник, Знач Предметы = Неопределено) Экспорт
	
	Если Источник.ДополнительныеСвойства.Свойство("ВидЗаписи")
		И Источник.ДополнительныеСвойства.ВидЗаписи <> 
			"ЗаписьСОбновлением_МоихДокументов_КешаИнформацииОбОбъектах_ВизСогласования_ПредметовПодчиненныхПроцессов_ДопРеквизитовПоПредметам_СобытийИзмененияПредметов" Тогда
		
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Предметы = Неопределено Тогда
		Предметы = Источник.Предметы;
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	Для Каждого Строка Из Предметы Цикл
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.КешИнформацииОбОбъектах");
		ЭлементБлокировки.УстановитьЗначение("Объект", Строка.Предмет);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	КонецЦикла;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка.Заблокировать();
		
		// Запись признака ЕстьЗадачи
		Если Источник.ПометкаУдаления
			Или Источник.Выполнена
			Или Источник.СостояниеБизнесПроцесса <> Перечисления.СостоянияБизнесПроцессов.Активен Тогда
			
			Для каждого Строка Из Предметы Цикл
				Если ЗначениеЗаполнено(Строка.Предмет) Тогда
					ЕстьЗадачи = ОбъектИмеетЗадачи(Строка.Предмет, Источник.Ссылка);
					РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(Строка.Предмет, "ЕстьЗадачи", ЕстьЗадачи);
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			
			Для каждого Строка Из Предметы Цикл
				
				Если ЗначениеЗаполнено(Строка.Предмет) Тогда
					
					ЕстьЗадачи = Истина;
					
					Если Источник.Предметы.Найти(Строка.Предмет, "Предмет") = Неопределено Тогда
						Если Не ОбъектИмеетЗадачи(Строка.Предмет, Источник.Ссылка) Тогда
							ЕстьЗадачи = Ложь;
						КонецЕсли;
					КонецЕсли;
					
					РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(Строка.Предмет, "ЕстьЗадачи", ЕстьЗадачи);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Пометить на удаление вложенные и подчиненные бизнес-процессы задачи ЗадачаСсылка.
//
// Параметры
//  ЗадачаСсылка                 - ЗадачаСсылка.ЗадачаИсполнителя
//  НовоеЗначениеПометкиУдаления - Булево
//
Процедура ПриПометкеУдаленияЗадачи(ЗадачаСсылка, НовоеЗначениеПометкиУдаления) Экспорт
	
	ОбъектЗадачи = ЗадачаСсылка.Метаданные();
	Если НовоеЗначениеПометкиУдаления Тогда
		ВыполнитьПроверкуПравДоступа("ИнтерактивнаяПометкаУдаления", ОбъектЗадачи);
	КонецЕсли;
	Если Не НовоеЗначениеПометкиУдаления Тогда
		ВыполнитьПроверкуПравДоступа("ИнтерактивноеСнятиеПометкиУдаления", ОбъектЗадачи);
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		// Помечаем вложенные бизнес-процессы
		УстановитьПривилегированныйРежим(Истина);
		ВложенныеБизнесПроцессы = БизнесПроцессыВедущейЗадачи(ЗадачаСсылка);
		Для Каждого ВложенныйБизнесПроцесс Из ВложенныеБизнесПроцессы Цикл
			БизнесПроцессОбъект = ВложенныйБизнесПроцесс.ПолучитьОбъект();
			БизнесПроцессОбъект.УстановитьПометкуУдаления(НовоеЗначениеПометкиУдаления);
		КонецЦикла;	
		УстановитьПривилегированныйРежим(Ложь);
		
		// Помечаем подчиненные бизнес-процессы
		ПодчиненныеБизнесПроцессы = БизнесПроцессыГлавнойЗадачи(ЗадачаСсылка);
		Для Каждого ПодчиненныйБизнесПроцесс Из ПодчиненныеБизнесПроцессы Цикл
			БизнесПроцессОбъект = ПодчиненныйБизнесПроцесс.ПолучитьОбъект();
			БизнесПроцессОбъект.Заблокировать();
			БизнесПроцессОбъект.УстановитьПометкуУдаления(НовоеЗначениеПометкиУдаления);
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Возвращает массив подчиненных указанной задаче бизнес-процессов
//
// Параметры
//  ЗадачаСсылка  - задача.
//
// Возвращаемое значение:
//   массив ссылок на бизнес-процессы
//
Функция БизнесПроцессыГлавнойЗадачи(ЗадачаСсылка) Экспорт
	
	Результат = Новый Массив;
	Для Каждого МетаданныеБизнесПроцесса Из Метаданные.БизнесПроцессы Цикл
		
		// У бизнес-процесса может и не быть главной задачи
		РеквизитГлавнаяЗадача = МетаданныеБизнесПроцесса.Реквизиты.Найти("ГлавнаяЗадача");
		Если РеквизитГлавнаяЗадача = Неопределено Тогда
			Продолжить;
		КонецЕсли;	
			
		ТекстЗапроса = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Таблица.Ссылка КАК Ссылка
			|ИЗ
			|	&ИмяБизнесПроцесса КАК Таблица
			|ГДЕ
			|	Таблица.ГлавнаяЗадача = &ГлавнаяЗадача";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Таблица", МетаданныеБизнесПроцесса.Имя);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяБизнесПроцесса", МетаданныеБизнесПроцесса.ПолноеИмя());
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("ГлавнаяЗадача", ЗадачаСсылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат.Добавить(Выборка.Ссылка);
		КонецЦикла;
			
	КонецЦикла;	
	
	Возврат Результат;
		
КонецФункции	

// Проверяет, что по объекту есть хоть одна незавершенная задача 
//
Функция ОбъектИмеетЗадачи(Предмет, Задача) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК Результат
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
		|ГДЕ
		|	ЗадачаИсполнителя.Предметы.Предмет = &Предмет
		|	И НЕ ЗадачаИсполнителя.Выполнена
		|	И ЗадачаИсполнителя.Ссылка <> &Ссылка
		|	И ЗадачаИсполнителя.СостояниеБизнесПроцесса = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
		|	И ЗадачаИсполнителя.ПометкаУдаления = ЛОЖЬ";
		
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.Параметры.Вставить("Предмет", Предмет);
	Запрос.Параметры.Вставить("Ссылка", Задача);
	
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

// Заполняет регистр КешИнформацииОбОбъектах при обновлении базы
Процедура ЗаполнитьРегистрКешИнформацииОбОбъектах() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Предметы.Предмет КАК Предмет
		|ИЗ
		|	Задача.ЗадачаИсполнителя.Предметы КАК Предметы
		|ГДЕ
		|	Предметы.Ссылка.ПометкаУдаления = ЛОЖЬ
		|	И Предметы.Ссылка.Выполнена = ЛОЖЬ
		|	И Предметы.Ссылка.СостояниеБизнесПроцесса = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)";
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Предмет = Выборка.Предмет;
		
		Если Не ЗначениеЗаполнено(Предмет) Тогда
			Продолжить; // этот тип владельца не рассматриваем
		КонецЕсли;
		
		РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(Предмет, "ЕстьЗадачи", Истина); 
		
	КонецЦикла;
	
КонецПроцедуры

// Получает все дочерние бизнес-процессы указанного бизнес-процесса в привилегированном режиме
Процедура ПолучитьДочерниеПроцессы(БизнесПроцесс, МассивПроцессов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДочерниеБизнесПроцессы.ДочернийПроцесс
		|ИЗ
		|	РегистрСведений.ДочерниеБизнесПроцессы КАК ДочерниеБизнесПроцессы
		|ГДЕ
		|	ДочерниеБизнесПроцессы.РодительскийПроцесс = &РодительскийПроцесс";

	Запрос.УстановитьПараметр("РодительскийПроцесс", БизнесПроцесс.Ссылка);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивПроцессов.Добавить(ВыборкаДетальныеЗаписи.ДочернийПроцесс);
	КонецЦикла;

КонецПроцедуры	

////////////////////////////////////////////////////////////////////////////////
// Обработчики условных вызовов в другие подсистемы

// Возвращает Истина, если используется подсистема ВнешниеЗадачиИБизнесПроцессы
Процедура ПриОпределенииИспользованияВнешнихЗадачИБизнесПроцессов(ПодсистемаИспользуется) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВнешниеБизнесПроцессыИЗадачи") Тогда
		ПодсистемаИспользуется = Истина;
	Иначе
		ПодсистемаИспользуется = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает Истина, если задача является внешней. 
//
// Параметры
//  ЗадачаСсылка - ЗадачаСсылка.ЗадачаИсполнителя
//
Процедура ПриОпределенииВнешнейЗадачи(ЗадачаСсылка, ЗадачаВнешняя) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВнешниеБизнесПроцессыИЗадачи") Тогда
	Иначе
		ЗадачаВнешняя = Ложь;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Вызывается при получении полей представления для бизнес-процессов. Параметры соответствуют
// одноименной процедуре в модуле менеджера бизнес-процесса.
//
Процедура ОбработкаПолученияПолейПредставления(Источник, Поля, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Наименование");
	Поля.Добавить("Дата");
	
КонецПроцедуры

// Вызывается при получении представления для бизнес-процессов. Параметры соответствуют
// одноименной процедуре в модуле менеджера бизнес-процесса.
//
Процедура ОбработкаПолученияПредставления(Источник, Данные, Представление, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстПредставления = НСтр("ru = '%1 от %2'");
	
	Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстПредставления,
		Данные.Наименование,
		Данные.Дата);
	
КонецПроцедуры

// Обновить признак принятия к исполнению задач при смене версии конфигурации.
// 
Процедура ОбновлениеСостоянияИПринятияКИсполнению() Экспорт
	
	// Обновления состояния бизнес-процессов и задач
	Для каждого МетаданныеБизнесПроцесса Из Метаданные.БизнесПроцессы Цикл
		
		РеквизитСостояние = МетаданныеБизнесПроцесса.Реквизиты.Найти("Состояние");
		Если РеквизитСостояние = Неопределено Тогда
			Продолжить;
		КонецЕсли;	
			
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ 
			|	БизнесПроцессы.Ссылка
			|ИЗ
			|	&БизнесПроцесс КАК БизнесПроцессы";
			
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&БизнесПроцесс", МетаданныеБизнесПроцесса.ПолноеИмя());

		Результат = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = Результат.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Состояние = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаДетальныеЗаписи.Ссылка, "Состояние");
			
			Если Состояние <> Перечисления.СостоянияБизнесПроцессов.Остановлен И  
				Состояние <> Перечисления.СостоянияБизнесПроцессов.Прерван Тогда
				
				БизнесПроцесс = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
				БизнесПроцесс.Заблокировать();
				БизнесПроцесс.Состояние = Перечисления.СостоянияБизнесПроцессов.Активен;
				БизнесПроцесс.Записать();
				
			КонецЕсли;	
			
		КонецЦикла;
		
	КонецЦикла;	
	
	// Обновления принятия к исполнению задач
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ 
		|	Задачи.Ссылка
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК Задачи";
		
	Результат = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ЗадачаОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		ЗадачаОбъект.Заблокировать();
		
		Если ЗадачаОбъект.Выполнена Тогда
			ЗадачаОбъект.ПринятаКИсполнению = Истина;
			ЗадачаОбъект.ДатаПринятияКИсполнению = ЗадачаОбъект.ДатаИсполнения;
		КонецЕсли;	
				
		Если ЗадачаОбъект.СостояниеБизнесПроцесса <> Перечисления.СостоянияБизнесПроцессов.Остановлен 
			И ЗадачаОбъект.СостояниеБизнесПроцесса <> Перечисления.СостоянияБизнесПроцессов.Прерван Тогда
				
			ЗадачаОбъект.СостояниеБизнесПроцесса = Перечисления.СостоянияБизнесПроцессов.Активен;
			
		КонецЕсли;	
		
		ЗадачаОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры	

// Формирует список подбора для указания исполнителя в полях
// ввода составного типа (Пользователь и Роль)
Функция СформироватьДанныеВыбораИсполнителя(Текст) Экспорт
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Пользователи.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.Наименование ПОДОБНО &Текст
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РолиИсполнителей.Ссылка
	|ИЗ
	|	Справочник.РолиИсполнителей КАК РолиИсполнителей
	|ГДЕ
	|	РолиИсполнителей.Наименование ПОДОБНО &Текст";
	Запрос.УстановитьПараметр("Текст", Текст + "%");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат ДанныеВыбора;
	
КонецФункции

// Проверяет, является ли бизнес процесс активным.
// 
// Параметры:
//  БизнесПроцесс - БизнесПроцессСсылка - ссылка на бизнес-процесс.
//
// Возвращаемое значение:
// Булево - Истина, если процесс активен,
//        - Ложь, в противном случае.
//
Функция БизнесПроцессАктивен(БизнесПроцесс) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИСТИНА
		|ИЗ
		|	РегистрСведений.ДанныеБизнесПроцессов КАК ДанныеБизнесПроцессов
		|ГДЕ
		|	ДанныеБизнесПроцессов.БизнесПроцесс = &БизнесПроцесс
		|	И ДанныеБизнесПроцессов.Завершен = ЛОЖЬ
		|	И ДанныеБизнесПроцессов.ПометкаУдаления = ЛОЖЬ
		|	И ДанныеБизнесПроцессов.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)";
	
	Запрос.УстановитьПараметр("БизнесПроцесс", БизнесПроцесс);
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

// Выполняем прочтение (только документов), заполняя реквизит Прочтен в ДеревоПредметов
//
// Параметры:
//  ФормаЗадачи  - ФормаКлиентскогоПриложения  - форма выполнения задачи.
Процедура УстановитьПрочтениеПриВыполненииИзКарточкиЗадачи(Форма) Экспорт
	
	// прочтение
	ПредметыЗадачи = Форма.ДеревоПриложений.ПолучитьЭлементы();
	Для Каждого Предмет Из ПредметыЗадачи Цикл
		Предмет.Прочтен = РаботаСПрочтениями.УстановитьСвойствоПрочтен(Предмет.Ссылка, Истина);
	КонецЦикла;	
	
КонецПроцедуры	

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий подсистем БСП

// Объявляет служебные события подсистемы БизнесПроцессыИЗадачи:
//
// Серверные события:
//   ПриОпределенииПредставленияПредметаВнешнейЗадачи,
//   ПриОпределенииСпискаФайлов,
//   ПриВыполненииЗадачиИсточника.
//
// См. описание этой же процедуры в модуле СтандартныеПодсистемыСервер.
Процедура ПриДобавленииСлужебныхСобытий(КлиентскиеСобытия, СерверныеСобытия) Экспорт
	
		
КонецПроцедуры

// Определяет список справочников, доступных для загрузки с помощью подсистемы "Загрузка данных из файла".
//
// Параметры:
//  ЗагружаемыеСправочники - ТаблицаЗначений - список справочников, в которые возможна загрузка данных.
//      * ПолноеИмя          - Строка - полное имя справочника (как в метаданных).
//      * Представление      - Строка - представление справочника в списке выбора.
//      * ПрикладнаяЗагрузка - Булево - если Истина, значит справочник использует собственный алгоритм загрузки и
//                                      в модуле менеджера справочника определены функции.
//
Процедура ПриОпределенииСправочниковДляЗагрузкиДанных(ЗагружаемыеСправочники) Экспорт
	
	
	
КонецПроцедуры

// Возвращает задачи сотрудника за определенный промежуток времени.
//
// Параметры:
//  ДатаНачала - Дата - С какой даты следует получать задачи.
//  ДатаОкончания - Дата - По какую дату следует получать задачи.
//  Сотрудник - СправочникСсылка.Сотрудники - Сотрудник, задачи которого необходимо получить.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица задач пользователя.
//
Функция ЗадачиСотрудника(ДатаНачала, ДатаОкончания, Сотрудник) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИсполнителиРолейИДелегаты.РольСотрудник КАК Исполнитель
		|ПОМЕСТИТЬ СотрудникиИсполнителиЗадач
		|ИЗ
		|	РегистрСведений.ИсполнителиРолейИДелегаты КАК ИсполнителиРолейИДелегаты
		|ГДЕ
		|	ИсполнителиРолейИДелегаты.ИсполнительДелегат = &ИсполнительДелегат
		|	И ИсполнителиРолейИДелегаты.ИмяОбластиДелегирования В ("""", ""Задачи"", ""ЗадачиПросмотр"")
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СотрудникиИсполнителиЗадач.Исполнитель КАК Исполнитель
		|ПОМЕСТИТЬ ИсполнителиЗадач
		|ИЗ
		|	СотрудникиИсполнителиЗадач КАК СотрудникиИсполнителиЗадач
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СотрудникиПользователей.Пользователь
		|ИЗ
		|	СотрудникиИсполнителиЗадач КАК СотрудникиИсполнителиЗадач
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
		|		ПО СотрудникиИсполнителиЗадач.Исполнитель = СотрудникиПользователей.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Задачи.Ссылка КАК Ссылка,
		|	Задачи.СрокИсполнения КАК СрокИсполнения,
		|	Задачи.Наименование КАК Наименование
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК Задачи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИсполнителиЗадач КАК ИсполнителиЗадач
		|		ПО Задачи.ТекущийИсполнитель = ИсполнителиЗадач.Исполнитель
		|ГДЕ
		|	Задачи.СрокИсполнения <= &ДатаОкончания
		|	И Задачи.СрокИсполнения >= &ДатаНачала
		|	И Задачи.Выполнена = ЛОЖЬ
		|	И Задачи.ПометкаУдаления = ЛОЖЬ
		|	И НЕ Задачи.БизнесПроцесс ССЫЛКА БизнесПроцесс.КомплексныйПроцесс
		|	И НЕ Задачи.БизнесПроцесс ССЫЛКА БизнесПроцесс.РешениеВопросовВыполненияЗадач
		|	И Задачи.СостояниеБизнесПроцесса = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("ИсполнительДелегат", Сотрудник);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Проверяет, является ли ссылка на объект процессом.
Функция ЭтоПроцесс(Ссылка)
	
	ТипОбъекта = ТипЗнч(Ссылка);
	
	ТипыПроцессов = Новый Массив;
	ТипыПроцессов.Добавить(Тип("БизнесПроцессСсылка.Исполнение"));
	ТипыПроцессов.Добавить(Тип("БизнесПроцессСсылка.КомплексныйПроцесс"));
	ТипыПроцессов.Добавить(Тип("БизнесПроцессСсылка.Ознакомление"));
	ТипыПроцессов.Добавить(Тип("БизнесПроцессСсылка.Приглашение"));
	ТипыПроцессов.Добавить(Тип("БизнесПроцессСсылка.Рассмотрение"));
	ТипыПроцессов.Добавить(Тип("БизнесПроцессСсылка.Регистрация"));
	ТипыПроцессов.Добавить(Тип("БизнесПроцессСсылка.РешениеВопросовВыполненияЗадач"));
	ТипыПроцессов.Добавить(Тип("БизнесПроцессСсылка.Согласование"));
	ТипыПроцессов.Добавить(Тип("БизнесПроцессСсылка.Подписание"));
	ТипыПроцессов.Добавить(Тип("БизнесПроцессСсылка.Утверждение"));
	
	Возврат ТипыПроцессов.Найти(ТипОбъекта) <> Неопределено;
	
КонецФункции

// Определяет, является ли ссылка на объект шаблоном процесса.
//
// Параметры:
//  СсылкаНаОбъект - ЛюбаяСсылка - ссылка на любой объект.
//
// Возвращаемое значение:
//  Булево - возвращает истину, если объект является шаблоном процесса.
//
Функция ЭтоШаблонПроцесса(Ссылка) Экспорт
	
	Возврат ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(Ссылка);
	
КонецФункции