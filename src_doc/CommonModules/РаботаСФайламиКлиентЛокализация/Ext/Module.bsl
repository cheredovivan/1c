
#Область ПрограммныйИнтерфейс

// См. РаботаСФайламиКлиент.ОткрытьФайлПриложением
Функция ОткрытьФайлПриложением(ДанныеФайла, ИмяОткрываемогоФайла, 
	УникальныйИдентификатор = Неопределено, ПараметрыВыполнения = Неопределено) Экспорт

// Локализация
	// Отображение формализованных файлов ЭД
	Если ДанныеФайла.Свойство("ОтключитьВстроенныйРедактор") Тогда
		ОтключитьВстроенныйРедактор = ДанныеФайла.ОтключитьВстроенныйРедактор;
	Иначе
		ОтключитьВстроенныйРедактор = Ложь;
	КонецЕсли;
	Если ОткрытьФормуЭДО(ДанныеФайла) И Не ОтключитьВстроенныйРедактор Тогда
		
		ПараметрыФормыПросмотраЭД = Новый Структура;
		ПараметрыФормыПросмотраЭД.Вставить("ФайлДО", ДанныеФайла.Ссылка);
		
		ОткрытьФорму("Обработка.ИнтерфейсДокументовЭДОДокументооборот.Форма.ПросмотрФайлаФормализованногоЭДО",
			ПараметрыФормыПросмотраЭД);
		
		Возврат Истина;
		
	КонецЕсли;
// Локализация Конец
	
	Возврат Ложь;
	
КонецФункции

// См. РаботаСФайлами.ДобавитьИзФайловойСистемыСРасширениемСинхронно
// Позволяет переопределить имя создаваемого файла
Процедура ДополнитьИмяСозданияФайла(ИмяСоздания, РасширениеФайла, ПараметрыВыполнения, АдресВременногоХранилищаФайла, ДополнительныеПараметры) Экспорт

// Локализация
	// Заполним имя по файлу ЭД, если это файл ЭД
	Если ОбщегоНазначенияБЭДКлиентПовтИсп.ЗначениеФункциональнойОпции("ИспользоватьОбменЭД")
		И Нрег(РасширениеФайла) = ".xml" 
		И ПараметрыВыполнения.ФормаВладелец.ИмяФормы = "Справочник.ДокументыПредприятия.Форма.ФормаЭлемента" Тогда 
		
		ДанныеПоФайлуЭД = ОбменСКонтрагентамиДОВызовСервера.СведенияОЭДИзФайла(АдресВременногоХранилищаФайла);
		Если ТипЗнч(ДанныеПоФайлуЭД) = Тип("Структура") И ДанныеПоФайлуЭД.Свойство("ИмяСоздания") Тогда
			ИмяСоздания = ДанныеПоФайлуЭД.ИмяСоздания;
			ДополнительныеПараметры.Вставить("ДанныеПоФайлуЭД", ДанныеПоФайлуЭД);
		КонецЕсли;
		
	КонецЕсли;
// Локализация Конец

КонецПроцедуры

// См. РаботаСФайлами.ДобавитьИзФайловойСистемыСРасширениемСинхронно
// Позволяет дополнить параметры оповещения для обработчиков оповещения
Процедура ДополнитьПараметрыОповещенияПриСозданииФайла(ПараметрыОповещения, АдресВременногоХранилищаФайла, ДополнительныеПараметры) Экспорт

// Локализация
	ДанныеПоФайлуЭД = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ДанныеПоФайлуЭД");
	Если ТипЗнч(ДанныеПоФайлуЭД) = Тип("Структура") Тогда
		Если ДанныеПоФайлуЭД.Свойство("СуммаДокумента")
			И ЗначениеЗаполнено(ДанныеПоФайлуЭД.СуммаДокумента) Тогда
			ПараметрыОповещения.Вставить("СуммаДокумента",ДанныеПоФайлуЭД.СуммаДокумента);
		КонецЕсли;
		Если ДанныеПоФайлуЭД.Свойство("Валюта")
			И ЗначениеЗаполнено(ДанныеПоФайлуЭД.Валюта) Тогда
			ПараметрыОповещения.Вставить("Валюта",ДанныеПоФайлуЭД.Валюта);
		КонецЕсли;
	КонецЕсли;
// Локализация Конец

КонецПроцедуры

// См. ОбщаяФорма.ВыгрузкаВсехФайловДокументаНаДиск.СохранитьПродолжение
Процедура ПриСохраненииФайловОбъектаНаДиск(Форма, Параметры, СписокФайлов) Экспорт
	
	// Локализация
	МассивЭДО = Параметры.МассивЭДО;
	Если МассивЭДО.Количество() > 0 Тогда
		
		ФорматыЭДОФорма = Форма.ФорматыЭДО;
		ПапкаДляЭкспорта = Форма.ПапкаДляЭкспорта;
		
		Если ФорматыЭДОФорма = 0 Или ФорматыЭДОФорма = 1 Или ФорматыЭДОФорма = 2 Тогда // PDF или "ФНС" или "Весь документооборот"
			
			АдресДанныхВХранилище = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор);
			СоответствиеФайловВыгрузки = Неопределено;
			
			Если ФорматыЭДОФорма = 0 Тогда
				СоответствиеФайловВыгрузки = РаботаСФайламиВызовСервераЛокализация.СформироватьФайлыВыгрузкиЭДВФорматеPDF(МассивЭДО, АдресДанныхВХранилище);
			ИначеЕсли ФорматыЭДОФорма = 1 Тогда
				СоответствиеФайловВыгрузки = РаботаСФайламиВызовСервераЛокализация.СформироватьФайлыВыгрузкиЭДДляФНС(МассивЭДО, АдресДанныхВХранилище);
			ИначеЕсли ФорматыЭДОФорма = 2 Тогда
				СоответствиеФайловВыгрузки = РаботаСФайламиВызовСервераЛокализация.СформироватьФайлыВыгрузкиЭДДокументооборотЦеликом(МассивЭДО, АдресДанныхВХранилище);
			КонецЕсли;
			
			Если ФорматыЭДОФорма = 0 Или ФорматыЭДОФорма = 2 Тогда
			
				КоличествоФайлов = СоответствиеФайловВыгрузки.Количество();
				
				МассивФайлов = Новый Массив;
				Для Каждого КлючЗначениеФайлВыгрузки Из СоответствиеФайловВыгрузки Цикл
					ОписаниеФайла = Новый ОписаниеПередаваемогоФайла(КлючЗначениеФайлВыгрузки.Ключ.ИмяФайла,
						КлючЗначениеФайлВыгрузки.Значение);
					МассивФайлов.Добавить(ОписаниеФайла);
				КонецЦикла;
				
				Если КоличествоФайлов = 1 Тогда
					
					ИмяФайла = МассивФайлов[0].Имя;
					АдресФайла = МассивФайлов[0].Хранение;
					ПутьКФайлу = ПапкаДляЭкспорта;
					Если Прав(ПутьКФайлу, 1) <> ПолучитьРазделительПутиКлиента() Тогда
						ПутьКФайлу = ПутьКФайлу + ПолучитьРазделительПутиКлиента();
					КонецЕсли;	
					ПутьКФайлу = ПутьКФайлу + ИмяФайла;

					ПолучитьФайл(АдресФайла, ПутьКФайлу, Ложь);  
					ИнтерфейсДокументовЭДОВызовСервера.УдалитьВыгруженныеФайлыИзХранилища(МассивФайлов);
					
				КонецЕсли; 
				
			ИначеЕсли ФорматыЭДОФорма = 1 Тогда
				
				РезультатВыгрузки = РаботаСФайламиВызовСервераЛокализация.ВыгрузкаЭДДляФНСПодготовитьДанныеДляВыгрузкиВКаталог(
					СоответствиеФайловВыгрузки, Новый УникальныйИдентификатор);
					
				ПапкаФНС = ПапкаДляЭкспорта;
				Если Прав(ПапкаФНС, 1) <> ПолучитьРазделительПутиКлиента() Тогда
					ПапкаФНС = ПапкаФНС + ПолучитьРазделительПутиКлиента();
				КонецЕсли;	
				ПапкаФНС = ПапкаФНС + НСтр("ru = 'Выгрузка для ФНС'") + ПолучитьРазделительПутиКлиента();
				СоздатьКаталог(ПапкаФНС);
					
				Для Каждого ОписаниеФайла Из РезультатВыгрузки.МассивСохраняемыхФайлов Цикл

					ИмяФайла = ОписаниеФайла.Имя;
					АдресФайла = ОписаниеФайла.Хранение;
					ПутьКФайлу = ПапкаФНС + ИмяФайла;

					ПолучитьФайл(АдресФайла, ПутьКФайлу, Ложь);
					
				КонецЦикла;
			
			КонецЕсли;
				
		КонецЕсли;
		
	КонецЕсли;
	
	Для Каждого ДанныеСтроки Из Параметры.МассивСтрокФайлов Цикл
		Если Не ДанныеСтроки.ЭтоЭДО Тогда
			СписокФайлов.Добавить(ДанныеСтроки.Файл);
		КонецЕсли;
	КонецЦикла;
	
	// Локализация Конец
	
КонецПроцедуры

// См. ОбщаяФорма.ВыгрузкаВсехФайловДокументаНаДиск.СохранитьПродолжениеИменаФайлов
Функция ПроверкиПередСохранитьПродолжениеИменаФайлов(КодВозврата, Параметры, Форма) Экспорт
	
	// Локализация
	МассивЭДО = Параметры.МассивЭДО;
	Если МассивЭДО.Количество() > 0  И Форма.ФорматыЭДО = 1 Тогда // ФНС
		
		Если РаботаСФайламиВызовСервераЛокализация.ЕстьНезавершенныеДокументы(МассивЭДО) Тогда
			
			Если МассивЭДО.Количество() = 1 Тогда
				ТекстВопроса = НСтр("ru = 'Для документа не завершен электронный документооборот.'");
			Иначе
				ТекстВопроса = НСтр("ru = 'Для некоторых документов не завершен электронный документооборот.'");
			КонецЕсли;
			ТекстВопроса = ТекстВопроса + " " + НСтр("ru = 'Продолжить выгрузку?'");
			
			Возврат ТекстВопроса;
			
		КонецЕсли;
		
	КонецЕсли;
	// Локализация Конец
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// См. ОбщаяФорма.ВыгрузкаВсехФайловДокументаНаДиск.СохранитьПослеУстановкиРасширения
Процедура СохранитьПослеУстановкиРасширения(Результат, ПараметрыВыполнения, СписокФайловДляВыбора, ПараметрыОповещения) Экспорт

// Локализация
	МассивЭДО = Новый Массив;
	Для Каждого СтрокаРодитель Из СписокФайловДляВыбора.ПолучитьЭлементы() Цикл
		Для Каждого ДанныеСтроки Из СтрокаРодитель.ПолучитьЭлементы() Цикл
			Если ДанныеСтроки.Выгружать И ДанныеСтроки.ЭтоЭДО Тогда
				МассивЭДО.Добавить(ДанныеСтроки.ВладелецФайла);
			КонецЕсли;

			Для Каждого ДанныеСтроки2 Из ДанныеСтроки.ПолучитьЭлементы() Цикл
				Если ДанныеСтроки2.Выгружать И ДанныеСтроки2.ЭтоЭДО Тогда
					МассивЭДО.Добавить(ДанныеСтроки2.ВладелецФайла);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ПараметрыОповещения.Вставить("МассивЭДО", МассивЭДО);
// Локализация Конец

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Локализация

// Проверяет, является ли данный файл документом ЭДО. Если да, надо открыть форму просмотра ЭДО.
// 
// Параметры:
//  ДанныеФайлаДляОткрытия - Структура
// 
// Возвращаемое значение:
//  Булево - Открыть форму ЭДО
Функция ОткрытьФормуЭДО(ДанныеФайлаДляОткрытия)
	
	Если ДанныеФайлаДляОткрытия.Свойство("НеОткрыватьФормуЭДО")
		И ДанныеФайлаДляОткрытия.НеОткрыватьФормуЭДО = Истина Тогда
		
		Возврат Ложь;
	КонецЕсли;
	
	Если ДанныеФайлаДляОткрытия.Свойство("УчаствуетВЭДО")
		И ДанныеФайлаДляОткрытия.УчаствуетВЭДО = Истина Тогда
		
		Возврат Истина;
	КонецЕсли;
	
	Если ДанныеФайлаДляОткрытия.Свойство("ЭтоXMLФайлаЭДО")
		И ДанныеФайлаДляОткрытия.ЭтоXMLФайлаЭДО = Истина Тогда
		
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Обрабатывает результат выгрузки электронных документов
// 
// Параметры:
//  Результат - Массив из ОписаниеПереданногоФайла
//  ДопПараметры - Неопределено
Процедура СохранитьФайлыВыгрузкиЭДЗавершение(Результат, ДопПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;

	ИнтерфейсДокументовЭДОВызовСервера.УдалитьВыгруженныеФайлыИзХранилища(Результат);
	
КонецПроцедуры

// Локализация Конец

#КонецОбласти