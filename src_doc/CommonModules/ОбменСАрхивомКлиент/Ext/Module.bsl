#Область ПрограммныйИнтерфейс

// Скачивает контейнер документа из 1С:Архива и сохраняет его на диск.
//
// Параметры:
//  Документ - СправочникСсылка.ДокументыПредприятия - документ.
//
Процедура СкачатьКонтейнерДокумента(Документ) Экспорт
	
	Если ТипЗнч(Документ) <> Тип("СправочникСсылка.ДокументыПредприятия") Тогда
		Возврат;
	КонецЕсли;
	
	ДиалогОткрытия = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогОткрытия.МножественныйВыбор = Ложь;
	ДиалогОткрытия.Заголовок = НСтр("ru = 'Выберите каталог для загрузки контейнера'");
	
	ДополнительныеПараметры = Новый Структура("Документ", Документ);
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"СкачатьКонтейнерДокументаПослеВыбораКаталога", ЭтотОбъект, ДополнительныеПараметры);
	ДиалогОткрытия.Показать(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Асинх Процедура СкачатьКонтейнерДокументаПослеВыбораКаталога(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Или Результат.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПапки = Результат[0];
	РазделительПути = ПолучитьРазделительПути();
	ИмяПапки = ИмяПапки + ?(Прав(ИмяПапки, 1) = РазделительПути, "", РазделительПути);
	
	Попытка
		ИмяКаталогаТестовое = ИмяПапки + "Test";
		СоздатьКаталогАсинх(ИмяКаталогаТестовое);
		УдалитьФайлыАсинх(ИмяКаталогаТестовое);
	Исключение
		ТекстОшибки = НСтр("ru = 'Недостаточно прав на запись файлов в выбранный каталог'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецПопытки;
	
	Состояние(НСтр("ru = 'Загрузка файла...'"));
	
	СведенияОФайле = ОбменСАрхивомВызовСервера.СкачатьКонтейнерДокумента(ДополнительныеПараметры.Документ);
	ПолноеИмяФайла = ИмяПапки + СведенияОФайле.Имя + "." + СведенияОФайле.Расширение;
	
	// Проверка на существование файла с таким именем.
	ДополнительныеПараметры.Вставить("АдресДвоичныхДанных", СведенияОФайле.АдресДвоичныхДанных);
	ДополнительныеПараметры.Вставить("ПолноеИмяФайла", ПолноеИмяФайла);
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"СкачатьКонтейнерДокументаПослеВопросаОПерезаписи", ЭтотОбъект, ДополнительныеПараметры);
	НайденныеФайлы = Ждать НайтиФайлыАсинх(ПолноеИмяФайла);
	Если НайденныеФайлы.Количество() > 0 Тогда
		НайденныйФайл = НайденныеФайлы[0];
		ТекстВопроса = СтрШаблон(НСтр("ru = 'В папке назначения уже есть файл ""%1"". Заменить его?'"),
			НайденныйФайл.Имя);
		ВариантыОтвета = Новый СписокЗначений;
		ВариантыОтвета.Добавить(КодВозвратаДиалога.Да);
		ВариантыОтвета.Добавить(КодВозвратаДиалога.Отмена);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, ВариантыОтвета,,
			КодВозвратаДиалога.Отмена, НСтр("ru = 'Замена файла'"));
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

Процедура СкачатьКонтейнерДокументаПослеВопросаОПерезаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПолноеИмяФайла = ДополнительныеПараметры.ПолноеИмяФайла;
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(ДополнительныеПараметры.АдресДвоичныхДанных);
	ДвоичныеДанные.Записать(ПолноеИмяФайла);
	УдалитьИзВременногоХранилища(ДополнительныеПараметры.АдресДвоичныхДанных);
	
	Состояние();
	ПоказатьОповещениеПользователя(НСтр("ru = 'Файл сохранен'"),
		"file://" + ПолноеИмяФайла, ПолноеИмяФайла);
	
КонецПроцедуры	

#КонецОбласти
