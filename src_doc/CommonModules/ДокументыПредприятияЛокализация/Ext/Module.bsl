
#Область ПрограммныйИнтерфейс

#Область ФормаЭлемента

// Вызывается из ПриСозданииНаСервере формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - См. Справочник.ДокументыПредприятия.Форма.ФормаЭлемента
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
//  Параметры - Структура
Процедура ПриСозданииНаСервереФормаЭлемента(Форма, Отказ, СтандартнаяОбработка, Параметры) Экспорт
	
	#Если Не ВнешнееСоединение Тогда
// Локализация
	Объект = Форма.Объект;

	Если ЗначениеЗаполнено(Форма.Параметры.Основание) Тогда // Ввод на основании
		
		Если ТипЗнч(Форма.Параметры.Основание) = Тип("Структура") Тогда
			ОснованиеДокумента = Форма.Параметры.Основание.Основание;
		Иначе
			ОснованиеДокумента = Форма.Параметры.Основание;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ОснованиеДокумента) = Тип("ДокументСсылка.ЭлектронныйДокументВходящийЭДО") Тогда
		
		ЗаполнитьРеквизитыПриОтраженииВходящегоЭДО(Форма);
		
	КонецЕсли;

	УстановитьВидимостьКомандыСозданияИсходящегоДокументаЭДО(Форма);
	
	УстановитьВидимостьГруппыЭДОВПодменюСервис(Форма);
	
	УстановитьВидимостьКомандСервиса1СShare(Форма);
	
	ЗаполнитьЗакладкуМЭДОВФормеСервер(Форма);

	// ИнтернетПоддержкаПользователей.СПАРКРиски, ПриСозданииНаСервере.
	Форма.ИспользоватьСервисСПАРКРиски = СПАРКРиски.ИспользованиеРазрешено();
	ПараметрыПроцедуры = Новый Структура("ВариантОтображения", "Однострочный");
	СПАРКРиски.ПриСозданииНаСервере(
		Форма,
		Неопределено,
		Объект.Контрагент,
		ОбщегоНазначенияДокументооборотКлиентСервер.ВидКонтрагентаСПАРК(Объект.Контрагент),
		ПараметрыПроцедуры);
	
	Если Не Форма.ИспользоватьСервисСПАРКРиски Тогда
		Форма.Элементы.КонтрагентыСводныйИндикатор.Видимость = Ложь;
		Форма.Элементы.СтороныСводныйИндикатор.Видимость = Ложь;
		Форма.Элементы.ГруппаИндексыСПАРКРиски.Видимость = Ложь;
		Форма.Элементы.ГруппаИндексыСПАРКРиски2.Видимость = Ложь;
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски, ПриСозданииНаСервере.

	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриСозданииНаСервереДокумент(Форма, Параметры);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
	// Локализация Конец
	#КонецЕсли
	
КонецПроцедуры

// Вызывается из ПриЗаписиНаСервере формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ТекущийОбъект - СправочникОбъект.ДокументыПредприятия
Процедура ПриЗаписиНаСервереФормаЭлемента(Форма, ТекущийОбъект) Экспорт
// Локализация
	РаботаСОбращениями.ЗафиксироватьСвязанныеДокументыПоВопросамОбращения(Форма, ТекущийОбъект.Ссылка);
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриЗаписиНаСервере(Форма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
// Локализация Конец	
КонецПроцедуры	

// Вызывается из ПриЧтенииНаСервере формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения 
//  ТекущийОбъект - СправочникОбъект.ДокументыПредприятия
Процедура ПриЧтенииНаСервереФормаЭлемента(Форма, ТекущийОбъект) Экспорт

	// Локализация
	ОбновитьРеквизитыЭДО(Форма, Истина);
	// Локализация Конец

КонецПроцедуры

// Вызывается из ПриИзмененииВидаДокумента формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения 
//  ПриСозданииНаСервере - Булево
Процедура ПриИзмененииВидаДокументаФормаЭлемента(Форма, ПриСозданииНаСервере) Экспорт

	// Локализация
	УстановитьВидимостьГруппыЭДОВПодменюСервис(Форма);
	
	УстановитьВидимостьКомандыСозданияИсходящегоДокументаЭДО(Форма);
	
	УстановитьВидимостьКомандСервиса1СShare(Форма);
	
	// Направление могло измениться с исходящего на входящий или наоборот, от этого зависит видимость элементов: 
	ВидимостьЭлементовМЭДОВФормеСервер(Форма);
	// Локализация Конец

КонецПроцедуры

// Вызывается из ФормаДокументаПриИзменении формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения 
Процедура ФормаДокументаПриИзмененииФормаЭлемента(Форма) Экспорт

	// Локализация
	УстановитьВидимостьКомандыСозданияИсходящегоДокументаЭДО(Форма);
	УстановитьВидимостьКомандИзмененияСвязиСДокументомЭДО(Форма);
	УстановитьВидимостьГруппыЭДОВПодменюСервис(Форма);
	// Локализация Конец

КонецПроцедуры

// Вызывается из ОбработкаОповещения формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - ТекущийОбъект - СправочникОбъект.ДокументыПредприятия
//  ИмяСобытия - Строка
//  Параметр - Произвольный
Процедура ОбработкаОповещенияФормаЭлемента(Форма, ИмяСобытия, Параметр) Экспорт

	// Локализация
	Объект = Форма.Объект;
	Если ИмяСобытия = "ОбновитьСостояниеЭД" Тогда
		
		Если Не ЗначениеЗаполнено(Параметр) 
			Или Параметр.ЭлектронныеДокументы.Найти(Форма.ТекущийДокументЭДО) <> Неопределено Тогда
			ОбновитьРеквизитыЭДО(Форма);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИмяСобытия = "ЭлектронныйДокументВходящий_ПодборДокументаУчета" Тогда
		ОбновитьРеквизитыЭДО(Форма);
	КонецЕсли;
	
	Если ИмяСобытия = ОбменЭДОДокументооборотКлиентСервер.ИмяСобытияСозданияИсходящегоЭДО() Тогда
		
		Если Параметр.ДокументДО <> Объект.Ссылка Тогда
			Возврат;
		КонецЕсли;
		
		Форма.ТекущийДокументЭДО = Параметр.СозданныйЭДО;
		Форма.СостояниеЭДО = ОбменЭДОДокументооборот.СостояниеДокументаИДоступностьКоманд(Параметр.СозданныйЭДО, Форма);
		ОбновитьРеквизитыЭДО(Форма);
		
	КонецЕсли;
	
	// Если обновился предмет сообщения МЭДО, то могло измениться состояние:
	Если ИмяСобытия = "Запись_УведомлениеМЭДО" Или ИмяСобытия = "Запись_КвитанцияМЭДО" Тогда
		Если Параметр.Документ = Объект.Ссылка Тогда
			МЭДОПереопределяемый.ОбновитьИсториюСостоянийМЭДОВФорме(Форма);
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_ДанныеДокументаМЭДО" Тогда
		Если Параметр.Документ = Объект.Ссылка Тогда
			ЗаполнитьЗакладкуМЭДОВФормеСервер(Форма);
		КонецЕсли;
	КонецЕсли;
	
	// Локализация Конец

КонецПроцедуры

// Вызывается из ЗаполнитьСписокПодписей формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - ТекущийОбъект - СправочникОбъект.ДокументыПредприятия
//  ИзПриСозданииНаСервере - Булево
Процедура ПриЗаполненииСпискаПодписейФормаЭлемента(Форма, ИзПриСозданииНаСервере) Экспорт

	// Локализация
	Если Форма.СвязанныеДокументыЭДО.Количество() <> 0 Тогда
		
		МассивЭД = Форма.СвязанныеДокументыЭДО.ВыгрузитьЗначения(); 
		СколькоДобавилиПодписей = 0;
		ОбменЭДОДокументооборот.ДополнитьСписокПодписей(МассивЭД, Форма.ЭлектронныеПодписи, 
			СколькоДобавилиПодписей, Форма.УникальныйИдентификатор);
		
		Форма.ВсегоПодписей = Форма.ВсегоПодписей + СколькоДобавилиПодписей;
		
		ТекстЗаголовка = НСтр("ru = 'ЭП'");
		Если Форма.ВсегоПодписей <> 0 Тогда
			ТекстЗаголовка = ТекстЗаголовка + " (" + Строка(Форма.ВсегоПодписей) + ")";
		КонецЕсли;
		Форма.Элементы.ГруппаЭП.Заголовок = ТекстЗаголовка;
		
		Если Форма.ВсегоПодписей = 0 Тогда 
			Форма.Элементы.ГруппаЭП.Видимость = Ложь;
		Иначе	
			Форма.Элементы.ГруппаЭП.Видимость = Истина;
		КонецЕсли;
		
	КонецЕсли;
	// Локализация Конец

КонецПроцедуры

// Вызывается при заполнении списка файлов формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - ТекущийОбъект - СправочникОбъект.ДокументыПредприятия
//  ЗаполнитьПризнакОригинал - Булево
Процедура ПриЗаполненииСпискаФайловФормаЭлемента(Форма, ЗаполнитьПризнакОригинал = Ложь) Экспорт

	// Локализация
	Форма.ФайлыЗаполненыСЭДО = (Форма.СвязанныеДокументыЭДО.Количество() <> 0);
	
	// добавим строку документа ЭДО
	Если Форма.СвязанныеДокументыЭДО.Количество() <> 0 Тогда
		
		РольФайла = Неопределено;
		Если Форма.Файлы.ПолучитьЭлементы().Количество() <> 0 Тогда
			Строка1 = Форма.Файлы.ПолучитьЭлементы().Получить(0);
			РольФайла = Строка1.РольФайла;
		КонецЕсли;
		
		ДокументыПредприятияКлиентСервер.ДобавитьСтрокуДокументаВФайлыПриНеобходимости(Форма);
			
		Для Каждого СтрокаСписка Из Форма.СвязанныеДокументыЭДО Цикл
			
			ДокЭДО = СтрокаСписка.Значение;
			
			Если Форма.Файлы.ПолучитьЭлементы().Количество() <> 0 Тогда
				СтрЭДО = Форма.Файлы.ПолучитьЭлементы().Вставить(1);
			Иначе
				СтрЭДО = Форма.Файлы.ПолучитьЭлементы().Вставить(0);
			КонецЕсли;
			
			СтрЭДО.ДокументЭДО = ДокЭДО;
			СтрЭДО.ЭтоДокументЭДО = Истина;
			
			СтрЭДО.Наименование = Строка(ДокЭДО) + НСтр("ru = ' (Документ ЭДО)'");
			
			СтрЭДО.ИндексКартинки = 30; // как xml  
			СтрЭДО.СтатусПроверкиЭП = -1;
			СтрЭДО.РольФайла = РольФайла; 
			
		КонецЦикла;
			
	КонецЕсли;
	// Локализация Конец

КонецПроцедуры

// Вызывается из ПередЗаписьюНаСервере формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения 
//  Отказ - Булево
//  ТекущийОбъект - СправочникОбъект.ДокументыПредприятия
//  ПараметрыЗаписи - Структура - Параметры записи
Процедура ПередЗаписьюНаСервереФормаЭлемента(Форма, Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт

// Локализация
	
	Объект = Форма.Объект;
	
	// Если это документ МЭДО и пользователь снимает регистрацию, то нужно предложить создать уведомление об отказе
	// в регистрации:
	РегистрацияДокументаСнята = Форма.ВидДокументаКэш.ЯвляетсяВходящейКорреспонденцией
		И Не ТекущийОбъект.Ссылка.Пустая() И Не ЗначениеЗаполнено(Объект.РегистрационныйНомер)
		И МЭДОПовтИсп.НужноСоздаватьУведомления()
		И МЭДОПереопределяемый.ЭтоДокументМЭДО(Форма, Перечисления.НаправленияСообщенийМЭДО.Входящее)
		И ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийОбъект.Ссылка, "РегистрационныйНомер"));
	
	Если РегистрацияДокументаСнята Тогда
		// У записанного объекта была регистрация, а сейчас она снята, 
		// значит надо предложить пользователю создать уведомление об отказе в регистрации:
		Форма.ОтмененаРегистрацияДокумента = Истина;
		Форма.ЕстьДоступКМЭДО = МЭДОПереопределяемый.ЕстьДоступКРаботеСМЭДО();
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.СПАРКРиски, ПередЗаписьюНаСервереДокумент.
	СПАРКРискиПереопределяемый.ПередЗаписьюНаСервереДокумент(Форма, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски, ПередЗаписьюНаСервереДокумент.

	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПередЗаписьюНаСервереДокумент(Форма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами

// Локализация Конец

КонецПроцедуры

// Вызывается из ПослеЗаписиНаСервере формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ТекущийОбъект - СправочникОбъект.ДокументыПредприятия
//  ПараметрыЗаписи - Структура - Параметры записи
Процедура ПослеЗаписиНаСервереФормаЭлемента(Форма, ТекущийОбъект, ПараметрыЗаписи) Экспорт

// Локализация
	Если ТипЗнч(Форма.Параметры.Основание) = Тип("ДокументСсылка.ЭлектронныйДокументВходящийЭДО") Тогда
		ОтражениеВУчетеДОВызовСервера.УстановитьСвязьЭлектронногоДокументаСОбъектомУчета(Форма.Параметры.Основание,
			Форма.Объект.Ссылка);
	КонецЕсли;
	
	УстановитьВидимостьКомандСервиса1СShare(Форма);
	
	МЭДОПереопределяемый.СохранитьДанныеМЭДОВФорме(
		Форма, МЭДОДокументооборот.НаправлениеОтКэшаВидаДокумента(Форма.ВидДокументаКэш));

	// ИнтернетПоддержкаПользователей.СПАРКРиски, ПослеЗаписиНаСервере.
	СПАРКРискиПереопределяемый.ПослеЗаписиНаСервере(Форма, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски, ПослеЗаписиНаСервере.
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПослеЗаписиНаСервере(Форма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами	
// Локализация Конец

КонецПроцедуры

// Вызывается из ОбработкаПроверкиЗаполненияНаСервере формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма документа
//  Отказ - Булево
//  ПроверяемыеРеквизиты - Массив из Строка
Процедура ОбработкаПроверкиЗаполненияНаСервереФормаЭлемента(Форма, Отказ, ПроверяемыеРеквизиты) Экспорт
	// Локализация
	РаботаСОбращениями.ПроверкаЗаполненияФормыНаСервере(Форма, Отказ, ПроверяемыеРеквизиты);
	// Локализация Конец
КонецПроцедуры

// Вызывается из МК_НастроитьЭлементыФормы формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения 
Процедура МК_НастроитьЭлементыФормыФормаЭлемента(Форма) Экспорт

// Локализация
	Элементы = Форма.Элементы;
	Элементы.Переместить(Элементы.ГруппаКомандыЭДО, Элементы.ГруппаВсеДействия);
// Локализация Конец

КонецПроцедуры

// Вызывается из метода КонтрагентыСпособОтправкиПриИзмененииНаСервере формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
// 
// Возвращаемое значение:
//  Булево - при обработке изменения способа отправки документ был записан, нужно показать оповещение пользователю
//
Функция КонтрагентыСпособОтправкиПриИзменении_ФормаЭлемента(Форма) Экспорт

	// Локализация
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	ТекДанные = Объект.Контрагенты.НайтиПоИдентификатору(Элементы.Контрагенты.ТекущаяСтрока);
	
	Если ТекДанные = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТекДанные.СпособОтправки = ПредопределенноеЗначение("Справочник.СпособыДоставки.МЭДО")
		И Объект.Ссылка.Пустая() И Форма.Записать() Тогда
		ВидимостьЭлементовМЭДОВФормеСервер(Форма);
		Возврат Истина; // Если записали документ, то нужно показать оповещение об этом
	КонецЕсли;
	
	ВидимостьЭлементовМЭДОВФормеСервер(Форма);
	// Локализация Конец
	
	Возврат Ложь;

КонецФункции

// Вызывается из метода СпособОтправкиПриИзменении формы элемента справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
// 
// Возвращаемое значение:
//  Булево - при обработке изменения способа отправки документ был записан, нужно показать оповещение пользователю
//
Функция СпособОтправкиПриИзменении_ФормаЭлемента(Форма) Экспорт

// Локализация
	Объект = Форма.Объект;
	
	Если Форма.СпособОтправки = ПредопределенноеЗначение("Справочник.СпособыДоставки.МЭДО")
		И Объект.Ссылка.Пустая() И Форма.Записать() Тогда 
		ВидимостьЭлементовМЭДОВФормеСервер(Форма);
		Возврат Истина; // Если записали документ, то нужно показать оповещение об этом
	КонецЕсли;
	
	ВидимостьЭлементовМЭДОВФормеСервер(Форма);

// Локализация Конец

	Возврат Ложь;

КонецФункции

// Локализация
// Вызывается из УстановитьДоступностьПолей в форме элемента справочника ДокументыПредприятия
// Устанавливает доступность полей в форме документа в зависимости от статуса распознавания
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура УстановитьДоступностьПоСтатусуРаспознаванияДокумента(Форма) Экспорт

	#Если Не ВнешнееСоединение Тогда
	
	Если Форма.Объект.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	ДокументСсылка = Форма.Объект.Ссылка;
	СтатусРаспознавания = Форма.СтатусРаспознаванияДокумента;
	ЭлементыФормы = Форма.Элементы;
	ЭлементыФормы.ГруппаРаспознаваниеДокумента.ЦветФона = ЦветаСтиля.ОтсутствиеЦветФона;

	ТребуетПроверки = РаспознаваниеДокументовСлужебныйКлиентСервер.НужнаВерификация(Форма);
	ЭтоОбъектЭтогоУзла = ОбщегоНазначенияДокументооборот.ОбъектЭтогоУзла(ДокументСсылка);
	
	НеРаспознан = 
			РаспознаваниеДокументовСлужебныйКлиентСервер.ДанныеДокументаНеРаспознаны(Форма);
	
	Если Не ЗначениеЗаполнено(СтатусРаспознавания) Тогда
		
		ПоказыватьДекорацию = Ложь;
		ДоступностьРеквизитов = Истина;
	
	Иначе
		
		ДоступностьРеквизитов = (СтатусРаспознавания <> Перечисления.СтатусыРаспознаванияДокументов.НаРаспознавании);
		
		Если ЗначениеЗаполнено(СтатусРаспознавания) и ЭтоОбъектЭтогоУзла = Ложь Тогда
			
			ДоступностьРеквизитов = 
			(СтатусРаспознавания = Перечисления.СтатусыРаспознаванияДокументов.Распознан И ТребуетПроверки = Ложь) 
								Или СтатусРаспознавания = Перечисления.СтатусыРаспознаванияДокументов.Прервано;
			
		КонецЕсли;
		
		ПредупреждающаяНадпись = "";
		Подсказка = "";
		
		Если СтатусРаспознавания = Перечисления.СтатусыРаспознаванияДокументов.НаРаспознавании Тогда
			
			ПредупреждающаяНадпись = НСтр("ru = 'Документ не сформирован. Выполняется распознавание.'");
			Форма.Заголовок = НСтр("ru = 'Документ не сформирован'", ОбщегоНазначения.КодОсновногоЯзыка());
			
		ИначеЕсли СтатусРаспознавания = Перечисления.СтатусыРаспознаванияДокументов.Распознан И ТребуетПроверки Тогда
			
				Если НеРаспознан = Истина Тогда
					
					РаспознанныйТипДокумента = 
						РаспознаваниеДокументовСлужебный.ТипРаспознанногоДокумента(ДокументСсылка);
					
					Если ЗначениеЗаполнено(РаспознанныйТипДокумента) Тогда
						ПредупреждающаяНадпись = НСтр("
						|ru = 'Для документа ""%"" нет соответствующего вида документа ДО в настройках сервиса. 
						|Невозможно заполнить карточку.'", ОбщегоНазначения.КодОсновногоЯзыка());
						
					Иначе
						ПредупреждающаяНадпись = НСтр("
						|ru = 'Не удалось распознать тип документа. Выполните заполнение вручную.'", 
							ОбщегоНазначения.КодОсновногоЯзыка());
					КонецЕсли;
					
					Подсказка = НСтр("
					|ru = 'Обратитесь к Администратору. Возможно тип документа не задан в настройках сервиса.'", 
						ОбщегоНазначения.КодОсновногоЯзыка());
				Иначе
					
					КоличествоРеквизитовДляВерификации = 
						РаспознаваниеДокументовСлужебныйКлиентСервер.КоличествоРеквизитовДляВерификации(Форма);
					
					ПредупреждающаяНадпись = НСтр("
					|ru = 'Документ распознан. Верификация реквизитов (%1) доступна по ссылке.'", 
					ОбщегоНазначения.КодОсновногоЯзыка());
					
					ПредупреждающаяНадпись = СтрШаблон(ПредупреждающаяНадпись, КоличествоРеквизитовДляВерификации);
					
				КонецЕсли;
			
		ИначеЕсли СтатусРаспознавания = Перечисления.СтатусыРаспознаванияДокументов.Прервано И 
			Форма.ИзмененСтатусРаспознавания = Истина Тогда
			
			ПредупреждающаяНадпись = НСтр("ru = 'Распознавание документа прервано пользователем. 
			|Информация доступна по ссылке.'", ОбщегоНазначения.КодОсновногоЯзыка());
			ЭлементыФормы.ГруппаРаспознаваниеДокумента.ЦветФона = ЦветаСтиля.ОбъектПомеченНаУдаление;
			
		Иначе
			
			ПредупреждающаяНадпись = "";
			
		КонецЕсли;
	
		ПоказыватьДекорацию = ПредупреждающаяНадпись <> "";
		
	КонецЕсли;
	
	ПредупреждающаяНадпись = СтрЗаменить(ПредупреждающаяНадпись, Символы.ПС, "");
	ЭлементыФормы.ГруппаРаспознаваниеДокумента.Видимость = ПоказыватьДекорацию;
	ЭлементыФормы.РаспознаваниеДокументаНадпись.Заголовок = ПредупреждающаяНадпись;
	
	Если Не ПустаяСтрока(Подсказка) Тогда
		ЭлементыФормы.РаспознаваниеДокументаНадпись.Подсказка = Подсказка;
	КонецЕсли;
	
	ЭлементыФормы.ГруппаКомПанели.Доступность = ДоступностьРеквизитов;
	ЭлементыФормы.ГруппаОсновнаяКомПанель.Доступность = ДоступностьРеквизитов;
	ЭлементыФормы.ГруппаКоманды.Доступность = ДоступностьРеквизитов;

	ЭлементыФормы.МК_ДеревоОбзора.Доступность = ДоступностьРеквизитов;
	ЭлементыФормы.ДекорацияКоманды.Доступность = ДоступностьРеквизитов;
	ЭлементыФормы.ГруппаНадписьОтсутствуетОригинал.Доступность = ДоступностьРеквизитов;
	
	Для Каждого Элемент Из ЭлементыФормы.ГруппаСтраницы.ПодчиненныеЭлементы Цикл
		Если Элемент.Имя = "Обзор" Тогда
			Продолжить;
		КонецЕсли;
		Элемент.Доступность = ДоступностьРеквизитов;
	КонецЦикла;
	
	Для Каждого Элемент Из ЭлементыФормы.ГруппаПредпросмотрСтраницы.ПодчиненныеЭлементы Цикл
		Если Элемент.Имя = "ГруппаHtml" Тогда
			Продолжить;
		КонецЕсли;
		Элемент.Доступность = ДоступностьРеквизитов;
	КонецЦикла;
	
	
	ЭлементыФормы.Файлы.ТолькоПросмотр = Не ДоступностьРеквизитов;
	ЭлементыФормы.Файлы.КонтекстноеМеню.Доступность = ДоступностьРеквизитов;
	
	Если ЗначениеЗаполнено(СтатусРаспознавания) Тогда
		ОформлениеВерифицируемыхПолейШапки(Форма);
	КонецЕсли;
	
	#Иначе 
		ТекстИсключения = НСтр("ru = 'Метод ДокументыПредприятияЛокализация.
							|УстановитьДоступностьПоСтатусуРаспознаванияДокумента не доступен во внешнем соединении.'",
								ОбщегоНазначения.КодОсновногоЯзыка());
		ВызватьИсключение ТекстИсключения; 
	
	#КонецЕсли
	
КонецПроцедуры
// Локализация Конец

// Вызывается из УстановитьДоступностьПолей в форме элемента справочника ДокументыПредприятия
// Позволяет переопределить доступность полей формы, устанавливаемую при обработке различных событий в ней
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура УстановитьДоступностьПолей(Форма) Экспорт
	
	// Локализация
	Если Не Форма.ДокументБлокированПоЭДО Тогда
		Возврат;
	КонецЕсли;
	
	МассивИмен = Справочники.ДокументыПредприятия.ПолучитьИменаКлючевыхРеквизитов();
	
	ПрефиксОбъекта = "Объект.";
	МассивИменСОбъектом = Новый Массив;
	Для Каждого ИмяПоля Из МассивИмен Цикл
		ИмяСОбъектом = ПрефиксОбъекта + ИмяПоля;
		Если ИмяПоля = "Файлы" Тогда
			МассивИменСОбъектом.Добавить(ИмяПоля);
		Иначе
			МассивИменСОбъектом.Добавить(ИмяСОбъектом);
		КонецЕсли;
	КонецЦикла;
	
	ЭлементыДляИмен = Новый Массив;
	ЭлементыДляИмен.Добавить(Форма.Элементы.Заголовок);
	ЭлементыДляИмен.Добавить(Форма.Элементы.Содержание);
	ЭлементыДляИмен.Добавить(Форма.Элементы.Организация);
	ЭлементыДляИмен.Добавить(Форма.Элементы.Контрагент);
	ЭлементыДляИмен.Добавить(Форма.Элементы.Сумма);
	ЭлементыДляИмен.Добавить(Форма.Элементы.Валюта);
	ЭлементыДляИмен.Добавить(Форма.Элементы.СтороныСторона);
	ЭлементыДляИмен.Добавить(Форма.Элементы.СтороныПодписан);
	ЭлементыДляИмен.Добавить(Форма.Элементы.СтороныПодписал);
	ЭлементыДляИмен.Добавить(Форма.Элементы.СтороныДатаПодписи);
	
	Для Каждого Элем Из ЭлементыДляИмен Цикл
		
		Если ТипЗнч(Элем) = Тип("ПолеФормы") ИЛИ ТипЗнч(Элем) = Тип("ТаблицаФормы") Тогда
			
			Если МассивИменСОбъектом.Найти(Элем.ПутьКДанным) <> Неопределено Тогда
				Если Элем.ПутьКДанным = "Файлы" Тогда 
					Элем.ИзменятьСоставСтрок = Не Форма.ДокументБлокированПоЭДО;
				Иначе
					Элем.ТолькоПросмотр = Форма.ДокументБлокированПоЭДО;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЦикла;
	// Локализация Конец
	
КонецПроцедуры

// Вызывается из ОбработкаПроверкиЗаполненияНаСервере формы элемента справочника ДокументыПредприятия
// Позволяет пропустить проверку заполненности контрагентов в форме документа
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма документа
// 
// Возвращаемое значение:
//  Булево - Пропустить проверку контрагентов при проверке заполнения
Функция ПропуститьПроверкуКонтрагентовПриПроверкеЗаполнения(Форма) Экспорт
	
	// Локализация
	Если Форма.ВидДокументаКэш.ЯвляетсяОбращениемОтГраждан 
		И Форма.ВидОбращения = Перечисления.ВидыОбращенийГраждан.Анонимное Тогда
		Возврат Истина;
	КонецЕсли;
	// Локализация Конец
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ФормаСпискаСПапками

// Вызывается из ПриСозданииНаСервере формы списка справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
Процедура ПриСозданииНаСервереФормаСпискаСПапками(Форма, Отказ, СтандартнаяОбработка) Экспорт

// Локализация
	Элементы = Форма.Элементы;
	
	// Сервис "1С:Share"
	Форма.ИспользоватьСервис1CShare = ИнтеграцияShareДокументооборот.ИспользоватьСервис1СShare();
	
	СостоянияЭДОПоЦветам = ИнтерфейсЭДОДокументооборот.НаборыСостоянийЭДОПоЦветамИконок();
	Форма.Список.Параметры.УстановитьЗначениеПараметра("СостоянияЭДОЗеленые", СостоянияЭДОПоЦветам.Зеленые);
	Форма.Список.Параметры.УстановитьЗначениеПараметра("СостоянияЭДОКрасные", СостоянияЭДОПоЦветам.Красные);
	Форма.Список.Параметры.УстановитьЗначениеПараметра("СостоянияЭДОСерые", СостоянияЭДОПоЦветам.Серые);

	ЕстьМЭДО = Ложь;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьМЭДО") Тогда
		ЕстьМЭДО = МЭДОПереопределяемый.ЕстьДоступКРаботеСМЭДО();
	КонецЕсли;
	Элементы.СостояниеМЭДО.Видимость = ЕстьМЭДО;
	Элементы.ОтборСостояниеМЭДО.Видимость = ЕстьМЭДО;      
	
// Локализация Конец     

	// Локализация
	РаспознаваниеАктивировано = РаспознаваниеДокументов.ИспользованиеДоступно();
	ДоступноРаспознаваниеПоРоли = Пользователи.РолиДоступны("РаспознаваниеДокументов");
	ДоступнаВерификацияПоРоли = Пользователи.РолиДоступны("ПроверкаРаспознанныхДокументов");
	
	Элементы.ГруппаРаспознавание.Видимость = РаспознаваниеАктивировано 
		И (ДоступноРаспознаваниеПоРоли ИЛИ ДоступнаВерификацияПоРоли);
	Элементы.ТребуетПроверки.Видимость = РаспознаваниеАктивировано И ДоступнаВерификацияПоРоли;
	Элементы.СтатусРаспознавания.Видимость = РаспознаваниеАктивировано 
		И (ДоступноРаспознаваниеПоРоли ИЛИ ДоступнаВерификацияПоРоли);  
	Элементы.ОтборСтатусОбработкиРПД.Видимость = РаспознаваниеАктивировано;
	// Локализация Конец

КонецПроцедуры

// Вызывается из ПриЗагрузкеДанныхИзНастроекНаСервере формы списка справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Настройки - Соответствие
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере_ФормаСпискаСПапками(Форма, Настройки) Экспорт

// Локализация
	Элементы = Форма.Элементы;
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(
		Элементы.ОтборСостояниеМЭДО, Форма.СостояниеМЭДО);

	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(
		Элементы.ОтборСтатусОбработкиРПД, Форма.СтатусОбработкиРПД);
// Локализация Конец

КонецПроцедуры

// Вызывается из УстановитьВидимостьПоУсловиямИФункционалнымОпциям формы списка справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Отказ - Булево
Процедура УстановитьВидимостьПоУсловиямИФункциональнымОпциямФормаСпискаСПапками(Форма) Экспорт

// Локализация
	Элементы = Форма.Элементы;
	Элементы.СостояниеЭДО.Видимость = ОбщегоНазначенияБЭД.ЗначениеФункциональнойОпции(
		"ИспользоватьОбменЭД");
// Локализация Конец

КонецПроцедуры

// Вызывается из УстановитьВидимостьПоУсловиямИФункционалнымОпциям формы списка справочника ДокументыПредприятия
// Позволяет переопределить группы колонок списка (колонки в левой части списка документов).
// Элементы группируются попарно, по два в каждую группу колонок.
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Отказ - Булево
//  МассивЭлементов - Массив из ПолеФормы - Массив колонок списка документов, которые будут сгруппированы 
//  ТекущиеГруппы - Соответствие из Число, ГруппаФормы - Соответствие групп колонок списка документов и их номера
Процедура ПриГруппировкеКолонокСписка(Форма, МассивЭлементов, ТекущиеГруппы) Экспорт

// Локализация
	Элементы = Форма.Элементы;
	МассивЭлементов.Добавить(Элементы.СостояниеЭДО);
	ТекущиеГруппы.Вставить(4, Элементы.ГруппаЭДО);
// Локализация Конец

КонецПроцедуры

// Вызывается из МК_АдаптироватьСписокДокументов формы списка справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Отказ - Булево
Процедура МК_АдаптироватьСписокДокументовФормаСпискаСПапками(Форма) Экспорт

// Локализация
	Форма.Элементы.ГруппаЭДО.Видимость = Ложь;
// Локализация Конец

КонецПроцедуры

// Вызывается из ОбновитьДеревоПерепискиСервер формы документа предприятия
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Дерево - ДеревоЗначений
//  Документ - СправочникСсылка.ДокументыПредприятия
//  ТекущийДокумент - СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение:
//  Булево - Дерево переписки заполнено, дальнейшая его обработка не требуется
Функция ЗаполнитьДеревоПерепискиСервер(Форма, Дерево, Документ, ТекущийДокумент) Экспорт
	
	// Локализация
	
	Если Форма.ЭтоРежимОбращенияГраждан Тогда
		
		// В этом режиме переписка в целом по контрагенту, вся история:
		Запрос = Новый Запрос(
			"// Все, кроме текущего документа:
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ДокументыПредприятия.Ссылка КАК Ссылка,
			|	ДокументыПредприятия.ДатаРегистрации,
			|	ДокументыПредприятия.РегистрационныйНомер КАК РегистрационныйНомер,
			|	ДокументыПредприятия.Заголовок КАК Заголовок,
			|	ДокументыПредприятия.ДатаСоздания Как ДатаСоздания,
			|	ЛОЖЬ КАК Отправлен,
			|	""Входящее"" КАК ВидСообщения
			|ИЗ
			|	Справочник.ДокументыПредприятия КАК ДокументыПредприятия
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.СвязиОбъектов КАК СвязьВОтветНа
			|		ПО ДокументыПредприятия.Ссылка = СвязьВОтветНа.Объект
			|		И (СвязьВОтветНа.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ПолученВОтветНа))
			|ГДЕ
			|	ДокументыПредприятия.Контрагент = &Контрагент
			|	И СвязьВОтветНа.СвязанныйОбъект ЕСТЬ NULL
			|	И НЕ ДокументыПредприятия.ПометкаУдаления
			|	И ДокументыПредприятия.ВидДокумента.ЯвляетсяОбращениемОтГраждан
			|	И ДокументыПредприятия.Ссылка <> &ТекущийДокумент
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|// Добавить текущий документ:
			|ВЫБРАТЬ
			|	ДокументыПредприятия.Ссылка КАК Ссылка,
			|	ДокументыПредприятия.ДатаРегистрации,
			|	ДокументыПредприятия.РегистрационныйНомер КАК РегистрационныйНомер,
			|	ДокументыПредприятия.Заголовок КАК Заголовок,
			|	ДокументыПредприятия.ДатаСоздания Как ДатаСоздания,
			|	ЛОЖЬ КАК Отправлен,
			|	""Входящее"" КАК ВидСообщения
			|ИЗ
			|	Справочник.ДокументыПредприятия КАК ДокументыПредприятия
			|ГДЕ
			|	ДокументыПредприятия.Ссылка = &ТекущийДокумент
			|	И ДокументыПредприятия.Контрагент = &Контрагент
			|	И НЕ ДокументыПредприятия.ПометкаУдаления
			|	И ДокументыПредприятия.ВидДокумента.ЯвляетсяОбращениемОтГраждан
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДокументыПредприятия.Ссылка,
			|	ДокументыПредприятия.ДатаРегистрации,
			|	ДокументыПредприятия.РегистрационныйНомер,
			|	ДокументыПредприятия.Заголовок,
			|	ДокументыПредприятия.ДатаСоздания,
			|	ТчКорреспонденты.Отправлен,
			|	""Исходящее"" КАК ВидСообщения
			|ИЗ
			|	Документ.Корреспонденция.Корреспонденты КАК ТчКорреспонденты
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	Справочник.ДокументыПредприятия КАК ДокументыПредприятия
			|		ПО ДокументыПредприятия.Ссылка = ТчКорреспонденты.Ссылка.Основание
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.СвязиОбъектов КАК СвязьВОтветНа
			|		ПО ДокументыПредприятия.Ссылка = СвязьВОтветНа.Объект
			|		И СвязьВОтветНа.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ОтправленВОтветНа)
			|ГДЕ
			|	ТчКорреспонденты.Корреспондент = &Контрагент
			|	И СвязьВОтветНа.СвязанныйОбъект ЕСТЬ NULL
			|	И НЕ ДокументыПредприятия.ПометкаУдаления
			|	И ДокументыПредприятия.ВидДокумента.ЯвляетсяИсходящейКорреспонденцией
			|
			|УПОРЯДОЧИТЬ ПО
			|	ДатаРегистрации");
		Запрос.УстановитьПараметр("Контрагент", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Контрагент"));
		Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ВыведенныеПисьма = Новый Массив;
			СтрокаДерева = Делопроизводство.ДобавитьПисьмоВДерево(
				Дерево.Строки, Выборка.Ссылка, ТекущийДокумент, ВыведенныеПисьма);
			Если СтрокаДерева <> Неопределено Тогда
				СтрокаДерева.ВидСообщения = Выборка.ВидСообщения;
			КонецЕсли

		КонецЦикла;
		
		Возврат Истина;
	
	КонецЕсли;
	// Локализация Конец
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область МодульОбъекта

// Вызывается из ПередЗаписью модуля объекта справочника ДокументыПредприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
Процедура ПередЗаписью_МодульОбъекта(Форма, Отказ) Экспорт

// Локализация
	РаботаСВнешнимПодписанием.ПередЗаписьюДокумента(Форма, Отказ);
// Локализация Конец
	
КонецПроцедуры		

// Вызывается из ПриЗаписи модуля объекта справочника ДокументыПредприятия
// 
// Параметры:
//  Объект - СправочникОбъект.ДокументыПредприятия
//  Отказ - Булево
Процедура ПриЗаписи_МодульОбъекта(Объект, Отказ) Экспорт

// Локализация
	РаботаСВнешнимПодписанием.ПриЗаписиДокумента(Объект, Отказ);
// Локализация Конец
	
КонецПроцедуры	

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Локализация
#Область ЭДО
// Заполняет реквизиты документа при создании его на основании входящего ЭД
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура ЗаполнитьРеквизитыПриОтраженииВходящегоЭДО(Форма)
	
	ДанныеБЭД = Форма.Параметры.ДанныеБЭД;
	
	ВестиУчетСторон = Форма.ВидДокументаКэш.ВестиУчетСторон;
	УчитыватьСумму = Форма.ВидДокументаКэш.УчитыватьСуммуДокумента;
	
	Если УчитыватьСумму Тогда
		
		Форма.Объект.Сумма = ДанныеБЭД.СуммаДокумента;
		
	КонецЕсли;
	
	Если ВестиУчетСторон Тогда
		
		Стороны = Форма.Объект.Стороны;
		Стороны.Очистить();
		СтрокаОрганизация = Стороны.Добавить(); 
		СтрокаОрганизация.Сторона = ДанныеБЭД.Организация;
		
		СтрокаКонтрагент = Стороны.Добавить(); 
		СтрокаКонтрагент.Сторона = ДанныеБЭД.Контрагент; 
		
		УстановленныеПодписи = ДанныеБЭД.УстановленныеПодписи;
		Если УстановленныеПодписи.Количество() > 0 Тогда
			
			СвойстваПодписи = УстановленныеПодписи[УстановленныеПодписи.ВГраница()];
			
			СтрокаКонтрагент.Подписан = Истина;
			СтрокаКонтрагент.Подписал =
			НайтиДобавитьКонтактноеЛицоИзСертификата(ДанныеБЭД.Контрагент, СвойстваПодписи.Сертификат);
			СтрокаКонтрагент.ДатаПодписи = СвойстваПодписи.ДатаПодписи;
			
		КонецЕсли; 
		
	Иначе
		
		Форма.Объект.Организация = ДанныеБЭД.Организация;
		РаботаСДокументами.ПриИзмененииОрганизацииВФормеДокумента(Форма);
		Форма.Объект.Контрагент = ДанныеБЭД.Контрагент;
		
	КонецЕсли;
	
	Форма.Объект.ФормаДокумента = Перечисления.ВариантыФормДокументов.Электронная;
	
КонецПроцедуры

// Устанавливает видимость команды создания исходящего документа ЭДО.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура УстановитьВидимостьКомандыСозданияИсходящегоДокументаЭДО(Форма)
	
	ЕстьВозможностьСоздатьИсходящийЭлектронныйДокумент = 
		ОбменЭДОДокументооборот.ЕстьВозможностьСоздатьИсходящийЭлектронныйДокумент(Форма);
	Форма.Элементы.СоздатьИсходящийДокументЭДО.Видимость = ЕстьВозможностьСоздатьИсходящийЭлектронныйДокумент;
	Форма.Элементы.СоздатьИсходящийДокументЭДОМенюЭДО.Видимость = ЕстьВозможностьСоздатьИсходящийЭлектронныйДокумент;
	
КонецПроцедуры

// Устанавливает видимость команд изменения связи с документом ЭДО.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура УстановитьВидимостьКомандИзмененияСвязиСДокументомЭДО(Форма)
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЭДОВДокументообороте") И ЗначениеЗаполнено(Форма.Объект.Ссылка)
		И Не Форма.Объект.ФормаДокумента = Перечисления.ВариантыФормДокументов.Бумажная Тогда
		 
		ЕстьЭлектронныйДокумент = ЗначениеЗаполнено(Форма.ТекущийДокументЭДО); 
		ЕстьПрава = Форма.ЭтоПолноправныйПользователь 
			Или ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ОбъектыУчетаДокументовЭДО);
		
		Форма.Элементы.СвязатьДокументСВходящимЭДО.Видимость = Не ЕстьЭлектронныйДокумент И ЕстьПрава;
		Форма.Элементы.РазорватьСвязьСЭлектроннымДокументом.Видимость = ЕстьЭлектронныйДокумент И ЕстьПрава;
		
	Иначе
		
		Форма.Элементы.СвязатьДокументСВходящимЭДО.Видимость = Ложь;
		Форма.Элементы.РазорватьСвязьСЭлектроннымДокументом.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает числовое значение статуса ЭДО для картинки
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура ОбновитьКартинкуЭДО(Форма)
	
	СостоянияЭДОПоЦветам = ИнтерфейсЭДОДокументооборот.НаборыСостоянийЭДОПоЦветамИконок();
	
	Если СостоянияЭДОПоЦветам.Красные.Найти(Форма.СостояниеЭДОССылка) <> Неопределено Тогда
		СтатусЭДО = 2;
	ИначеЕсли СостоянияЭДОПоЦветам.Зеленые.Найти(Форма.СостояниеЭДОССылка) <> Неопределено Тогда
		СтатусЭДО = 1;
	ИначеЕсли СостоянияЭДОПоЦветам.Серые.Найти(Форма.СостояниеЭДОССылка) <> Неопределено Тогда
		СтатусЭДО = 3;
	Иначе
		СтатусЭДО = 0;
	КонецЕсли;
	
	Форма.Элементы.КартинкаЭДО.Подсказка = Форма.СостояниеЭДО;
	
КонецПроцедуры

// Управляет видимостью подменю ЭДО в меню Сервис и видимостью команд в нем
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура УстановитьВидимостьГруппыЭДОВПодменюСервис(Форма)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЭДОВДокументообороте") Тогда
		Форма.Элементы.ГруппаСервисЭДО.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ЭД = ИнтеграцияЭДО.ОсновнойЭлектронныйДокументОбъектаУчета(Форма.Объект.Ссылка);
	
	Форма.Элементы.ОткрытьЭлектронныеДокументы.Видимость = ЗначениеЗаполнено(ЭД);
	Форма.Элементы.ПросмотретьЭлектронныйДокумент.Видимость = ЗначениеЗаполнено(ЭД);
	
	Форма.Элементы.СоздатьИсходящийДокументЭДОПодменюСервис.Видимость = 
		ОбменЭДОДокументооборот.ЕстьВозможностьСоздатьИсходящийЭлектронныйДокумент(Форма);
	
КонецПроцедуры

// Обновляет реквизиты ЭДО в форме документа при чтении на сервере и обработке оповещения
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
//  ПриЧтенииНаСервере - Булево - При чтении на сервере
Процедура ОбновитьРеквизитыЭДО(Форма, ПриЧтенииНаСервере = Ложь) Экспорт
	
	МассивЭД = ОбменЭДОДокументооборот.ЭлектронныеДокументыОбъектаУчета(Форма.Объект.Ссылка);
	Форма.СвязанныеДокументыЭДО.ЗагрузитьЗначения(МассивЭД);
	Если МассивЭД.Количество() <> 0 Тогда
		Форма.СостояниеЭДО = ОбменЭДОДокументооборот.СостояниеДокументаИДоступностьКоманд(МассивЭД[0], Форма);
		Форма.ТекущийДокументЭДО = МассивЭД[0];
	Иначе
		ОбменЭДОДокументооборот.ДоступностьКомандНет(Форма);
		Форма.ТекущийДокументЭДО = Неопределено;
		Форма.СостояниеЭДО = Неопределено;
		Форма.Элементы.СостояниеЭДО.Видимость = Ложь;
	КонецЕсли; 
	
	УстановитьВидимостьКомандИзмененияСвязиСДокументомЭДО(Форма);
	ОбновитьКартинкуЭДО(Форма);
	Форма.ПолучитьОбзорДокумента();
	УстановитьВидимостьГруппыЭДОВПодменюСервис(Форма); 
	
	Если Не ПриЧтенииНаСервере И Не Форма.ФайлыЗаполненыСЭДО И Форма.СвязанныеДокументыЭДО.Количество() <> 0 Тогда
		
		РаботаСДокументами.ЗаполнитьСписокФайловНаФормеДокумента(Форма);
		
	КонецЕсли;
	
	УстановитьДоступностьПолей(Форма);
	
КонецПроцедуры

// Выполняет поиск контактного лица контрагента по данным сертификата ЭП, при отсутствии добавляет новое.
// 
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты
//  Сертификат - см. ЭлектроннаяПодписьКлиентСервер.НовыеСвойстваПодписи
// 
// Возвращаемое значение:
//  СправочникСсылка.КонтактныеЛица - Найти добавить контактное лицо из сертификата
Функция НайтиДобавитьКонтактноеЛицоИзСертификата(Контрагент, Сертификат)
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонтактноеЛицо = Справочники.КонтактныеЛица.ПустаяСсылка();
	
	Если Не ЗначениеЗаполнено(Сертификат) Тогда
		Возврат КонтактноеЛицо;
	КонецЕсли;
	
	Если ТипЗнч(Сертификат) = Тип("ХранилищеЗначения") Тогда
		ДвоичныеДанныеСертификата = Сертификат.Получить();
	Иначе
		ДвоичныеДанныеСертификата = Сертификат;
	КонецЕсли;
	
	Субъект = ЭлектроннаяПодписьКлиентСервер.СвойстваСубъектаСертификата(
		Новый СертификатКриптографии(ДвоичныеДанныеСертификата));
		
	Если Не Субъект.Свойство("ОбщееИмя") Тогда
		Возврат КонтактноеЛицо;		
	КонецЕсли;
	
	ФИОКонтактногоЛица = Новый Массив;
	Если ЗначениеЗаполнено(Субъект.Фамилия) Тогда
		ФИОКонтактногоЛица.Добавить(Субъект.Фамилия);
	КонецЕсли;
	Если ЗначениеЗаполнено(Субъект.Имя) Тогда
		ФИОКонтактногоЛица.Добавить(Субъект.Имя);
		
		Если ЗначениеЗаполнено(Субъект.Отчество) Тогда
			ФИОКонтактногоЛица.Добавить(Субъект.Отчество);
		КонецЕсли;	
	КонецЕсли;
	Если Не ФИОКонтактногоЛица.Количество() Тогда
		ФИОКонтактногоЛица.Добавить(Субъект.ОбщееИмя);
	КонецЕсли;
	НаименованиеКонтактногоЛица = СтрСоединить(ФИОКонтактногоЛица, " ");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КонтактныеЛица.Ссылка КАК КонтактноеЛицо
		|ИЗ
		|	Справочник.КонтактныеЛица КАК КонтактныеЛица
		|ГДЕ
		|	КонтактныеЛица.Владелец = &Владелец
		|	И НЕ КонтактныеЛица.ПометкаУдаления
		|	И КонтактныеЛица.Наименование = &Наименование";
		
	Запрос.УстановитьПараметр("Владелец", Контрагент);
	Запрос.УстановитьПараметр("Наименование", НаименованиеКонтактногоЛица);
		
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		НачатьТранзакцию();
		Попытка
			// Добавляем новое контактное лицо
			КонтактноеЛицо = Справочники.КонтактныеЛица.СоздатьЭлемент();
			КонтактноеЛицо.Владелец = Контрагент;
			КонтактноеЛицо.Наименование = НаименованиеКонтактногоЛица;
			Если Субъект.Свойство("Должность") Тогда
				КонтактноеЛицо.Должность = Справочники.ДолжностиКонтактныхЛиц.НайтиСоздатьДолжность(Субъект.Должность);
			КонецЕсли;
			КонтактноеЛицо.Комментарий = НСтр("ru = 'Создан при загрузке ЭД.'");
			КонтактноеЛицо.ОбменДанными.Загрузка = Истина;
			КонтактноеЛицо.Записать();
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ВызватьИсключение;
		КонецПопытки;
		КонтактноеЛицо = КонтактноеЛицо.Ссылка;
		
	Иначе
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		КонтактноеЛицо = Выборка.КонтактноеЛицо; 
		
	КонецЕсли;
	
	Возврат КонтактноеЛицо;
	
КонецФункции

// Управляет видимостью команд сервиса 1С Share.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура УстановитьВидимостьКомандСервиса1СShare(Форма)
	
	Элементы = Форма.Элементы;
		
	// Сервис "1С:Share"
	Форма.ИспользоватьСервис1CShare = ИнтеграцияShareДокументооборот.ИспользоватьСервис1СShare();

	ВидимостьКомандСервиса1CShare = ИнтеграцияShareДокументооборотКлиентСервер.ВидимостьКомандСервиса1CShare(
			Форма, Форма.ВидДокументаКэш);
	Элементы.ПоделитьсяДокументомShare.Видимость = ВидимостьКомандСервиса1CShare.ИспользоватьСервис1CShare
		И ВидимостьКомандСервиса1CShare.Видимость;
	Элементы.ПоделитьсяДокументомShareКомПанель.Видимость = ВидимостьКомандСервиса1CShare.ИспользоватьСервис1CShare
		И ВидимостьКомандСервиса1CShare.Видимость;
	
КонецПроцедуры

// Имя элемента ТЧФормы по имени реквизита.
// 
// Параметры:
//  ЭлементФормы - ТаблицаФормы, ПолеФормы, ГруппаФормы, КнопкаФормы, ДекорацияФормы - Элемент формы
//  ИмяРеквизита - Строка
// 
// Возвращаемое значение:
//  Неопределено, ПолеФормы, ГруппаФормы, КнопкаФормы, ТаблицаФормы, ДекорацияФормы - Имя элемента ТЧФормы по имени реквизита
Функция ИмяЭлементаТЧФормыПоИмениРеквизита(Знач ЭлементФормы, ИмяРеквизита)
	
	Для Каждого ПодчиненныйЭлемент Из ЭлементФормы.ПодчиненныеЭлементы Цикл
		Если ПодчиненныйЭлемент.Вид = ВидГруппыФормы.ГруппаКолонок Тогда
			ПодчиненныйЭлемент = ИмяЭлементаТЧФормыПоИмениРеквизита(ПодчиненныйЭлемент, ИмяРеквизита);
		КонецЕсли;
		
		Если ПодчиненныйЭлемент = Неопределено Тогда
			Продолжить;
		КонецЕсли;
			
		Если СтрЗаканчиваетсяНа(ПодчиненныйЭлемент.ПутьКДанным, ИмяРеквизита) Тогда
			Возврат ПодчиненныйЭлемент;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область МЭДО

Процедура ЗаполнитьЗакладкуМЭДОВФормеСервер(Форма) Экспорт
	
	МЭДОПереопределяемый.ЗаполнитьЗакладкуМЭДОВФорме(
		Форма, МЭДОДокументооборот.НаправлениеОтКэшаВидаДокумента(Форма.ВидДокументаКэш));
	// От заполнения полей могла измениться видимость:
	ВидимостьЭлементовМЭДОВФормеСервер(Форма);
	
	Если ЗначениеЗаполнено(Форма.ТекущееСостояниеМЭДО) И Форма.ВидДокументаКэш.ЯвляетсяИсходящейКорреспонденцией Тогда
		// Должна была проставиться галочка Отправлен в "Корреспонденции":
		Документы.Корреспонденция.ЗаполнитьДанныеНаФормеДокумента(Форма);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВидимостьЭлементовМЭДОВФормеСервер(Форма) Экспорт
	
	МЭДОПереопределяемый.ВидимостьЭлементовМЭДОВФорме(
		Форма, МЭДОДокументооборот.НаправлениеОтКэшаВидаДокумента(Форма.ВидДокументаКэш));
	
КонецПроцедуры

#КонецОбласти

#Область РПД

// Выделить реквизиты для верификации.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура ОформлениеВерифицируемыхПолейШапки(Форма)
	
	#Если Не ВнешнееСоединение Тогда
	
	Для Каждого ЭлементФормы Из Форма.Элементы Цикл

		Если ТипЗнч(ЭлементФормы) <> Тип("ПолеФормы") Тогда
			Продолжить;
		КонецЕсли;
			
		Отбор = Новый Структура;
		Отбор.Вставить("ИмяРеквизита", СтрЗаменить(ЭлементФормы.ПутьКДанным, "Объект.", ""));
		
		НСтр = Форма.ВерифицируемыеРеквизитыШапки.НайтиСтроки(Отбор);
		
		Если НСтр.Количество() > 0 Тогда
			
			Если НСтр[0].Проверен = Ложь И НСтр[0].ТребуетПроверки = Истина Тогда
				
				ЭлементФормы.ЦветФона = ЦветаСтиля.ЦветФонаПредупреждения;
				
				Если Не ЗначениеЗаполнено(НСтр[0].УстановленноеЗначение) Тогда
					ЭлементФормы.ПодсказкаВвода = Строка(НСтр[0].РаспознанныйТекст); 
				КонецЕсли;
				
				Продолжить;
				
			КонецЕсли;
			
			ЭлементФормы.ПодсказкаВвода = "";
			ЭлементФормы.ЦветФона = Новый Цвет();
			
		КонецЕсли;
		
	КонецЦикла;
	
	#Иначе
		ТекстИсключения = НСтр("ru = 'Метод ДокументыПредприятияЛокализация.
							|УстановитьДоступностьПоСтатусуРаспознаванияДокумента не доступен во внешнем соединении.'",
								ОбщегоНазначения.КодОсновногоЯзыка());
	ВызватьИсключение ТекстИсключения; 
	
	#КонецЕсли
			
КонецПроцедуры

#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции_РежимОбращенийГраждан

// Вызывается из ПриСозданииНаСервере формы документа предприятия
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
Процедура ИзменитьВнешнийВидДляРежимаОбращений(Форма) Экспорт

	Элементы = Форма.Элементы;

	Форма.ЭтоРежимОбращенияГраждан = (
		Форма.КлючНазначенияИспользования = РаботаСОбращениямиКлиентСервер.КлючСписокОбращенийГраждан());
	Если Не Форма.ЭтоРежимОбращенияГраждан Тогда
		Возврат; // Если не установлен параметр ЭтоРежимОбращенияГраждан, то не будет и отбора.
	КонецЕсли;
	
	Форма.Список.Параметры.УстановитьЗначениеПараметра("ЭтоРежимОбращенияГраждан", Форма.ЭтоРежимОбращенияГраждан);

	Форма.Заголовок = НСтр("ru = 'Обращения граждан и организаций'");
	Форма.АвтоЗаголовок = Ложь;
	Элементы.ДеревоПереписки.Подсказка = НСтр("ru = 'Переписка по контрагенту или гражданину'");
	
	Элементы.СписокКонтекстноеМенюГруппаКнопкиОбращений.Видимость = Истина;
	Элементы.ОтборКонтрагент.ПодсказкаВвода = НСтр("ru = 'Гражданин/Организация'");

КонецПроцедуры

#КонецОбласти

// Локализация Конец

#КонецОбласти
