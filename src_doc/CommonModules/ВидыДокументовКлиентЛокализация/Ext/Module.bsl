
#Область ПрограммныйИнтерфейс

#Область ФормаЭлемента

// Вызывается из "ПередЗаписью" формы элемента справочника "Виды документов"
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  ПараметрыЗаписи - Структура
Процедура ПередЗаписью_ФормаЭлемента(Форма, Отказ, ПараметрыЗаписи) Экспорт
// Локализация
	Форма.ПроверитьЗаполнениеНастроекЭДОПередЗаписью(ПараметрыЗаписи, Отказ);
// Локализация Конец
КонецПроцедуры

// Локализация
// Вызывается из "ЯвляетсяОбращениемОтГражданПриИзменении" в форме элемента справочника "Виды документов"
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Отказ - Булево
//  Элемент - ПолеФормы
// 
// Возвращаемое значение:
//  Булево - Истина, если событие обработано и дальнейшей обработки в форме вида документа не требуется
Функция ЯвляетсяОбращениемОтГражданПриИзменении_ФормаЭлемента(Форма, Элемент) Экспорт

	Если Форма.ВидДокументаУчаствуетВОтраженииДокументовЭДО Тогда
		ТекстСообщения = 
			НСтр("ru='Нельзя сделать этот вид документа обращением, так как он используется при отражении документов ЭДО'");
		ПредупреждениеАсинх(ТекстСообщения);
		Форма.Объект.ЯвляетсяОбращениемОтГраждан = Ложь;
		Возврат Истина;
	КонецЕсли;

	Возврат Ложь;
КонецФункции
// Локализация Конец

// Вызывается из "ФормаДокументаПриИзменении" формы элемента справочника "Виды документов"
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Отказ - Булево
//  Элемент - ПолеФормы
// 
// Возвращаемое значение:
//  Булево - Истина, если событие обработано и дальнейшей обработки в форме вида документа не требуется
Функция ФормаДокументаПриИзменении_ФормаЭлемента(Форма, Элемент) Экспорт
// Локализация
	Объект = Форма.Объект;
	Если Объект.ИспользоватьЭДО И (Не ЗначениеЗаполнено(Объект.ФормаДокумента)
		Или Объект.ФормаДокумента = ПредопределенноеЗначение("Перечисление.ВариантыФормДокументов.Бумажная")) Тогда
		Объект.ИспользоватьЭДО = Ложь;
		Возврат Истина;
	КонецЕсли;
// Локализация Конец
	Возврат Ложь;
КонецФункции

// Вызывается из "ФормаДокументаОбработкаВыбора" формы элемента справочника "Виды документов"
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения 
//  Элемент - ПолеФормы
//  ВыбранноеЗначение - Произвольный
//  ДополнительныеДанные - Произвольный
//  СтандартнаяОбработка - Булево
Процедура ФормаДокументаОбработкаВыбора_ФормаЭлемента(Форма, Элемент, ВыбранноеЗначение, ДополнительныеДанные,
	СтандартнаяОбработка) Экспорт
// Локализация
	Если ВыбранноеЗначение = ПредопределенноеЗначение("Перечисление.ВариантыФормДокументов.Бумажная")
		И Форма.ВидДокументаУчаствуетВОтраженииДокументовЭДО  Тогда

		СтандартнаяОбработка = Ложь;
		ТекстСообщения = 
			НСтр("ru='Нельзя изменить форму на бумажную, так как этот вид
			|документа используется при отражении документов ЭДО'");
		ПредупреждениеАсинх(ТекстСообщения);
		
	КонецЕсли;
// Локализация Конец
КонецПроцедуры

// Вызывается из "ШаблонФормаДокументаОбработкаВыбора" формы элемента справочника "Виды документов"
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения 
//  Элемент - ПолеФормы
//  ВыбранноеЗначение - Произвольный
//  ДополнительныеДанные - Произвольный
//  СтандартнаяОбработка - Булево
Процедура ШаблонФормаДокументаОбработкаВыбора_ФормаЭлемента(Форма, Элемент, ВыбранноеЗначение, ДополнительныеДанные,
	СтандартнаяОбработка) Экспорт
// Локализация
	Если ВыбранноеЗначение = ПредопределенноеЗначение("Перечисление.ВариантыФормДокументов.Бумажная")
		И Форма.ВидДокументаУчаствуетВОтраженииДокументовЭДО  Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекстСообщения = 
			НСтр("ru='Нельзя изменить форму на бумажную, так как этот шаблон используется при отражении документов ЭДО'");
		
		ПредупреждениеАсинх(ТекстСообщения);
		
	КонецЕсли;
// Локализация Конец
КонецПроцедуры

#КонецОбласти

#КонецОбласти
