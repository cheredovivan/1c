
#Область ПрограммныйИнтерфейс

// См. ОбзорФайловВызовСервера.ПолучитьПредставлениеИзДвоичныхДанных
// Возвращаемое значение:
//  Булево - Обзор файла будет построен в методе локализации
Функция ЭтоЛокализуемоеПредставлениеИзДвоичныхДанных(РасширениеФайла) Экспорт

	Результат = Ложь;
// Локализация
	Если ОбзорФайловКлиентСервер.ЭтоXML(РасширениеФайла) Тогда
		Результат = Истина;
	КонецЕсли;
// Локализация Конец

	Возврат Результат;

КонецФункции

// См. ОбзорФайловВызовСервера.ПолучитьПредставлениеИзДвоичныхДанных
Функция ПолучитьПредставлениеИзДвоичныхДанных(ДвоичныеДанные, ДанныеФайла, ПараметрыПолученияПредставления,
	НаСервереЕстьImageMagick, ИспользоватьLibreOffice, Наименование, РасширениеФайла,
	ИмяФайлаСПутем, ИмяФайлаСПутемHTML) Экспорт

	// Локализация
	ПредставлениеHTMLФайла = "";
	
	ТаблДок = Неопределено;
	Если ДанныеФайла <> Неопределено Тогда
		ТаблДок = ТабличныйДокументФайлаXML(ДанныеФайла);
	Иначе
		ТаблДок = ТабличныйДокументДвоичныхДанныхXML(ДвоичныеДанные);
	КонецЕсли;
	
	Если ТаблДок <> Неопределено Тогда
	
		ВременнаяПапка = ПолучитьИмяВременногоФайла("");
		СоздатьКаталог(ВременнаяПапка);
		ВременнаяПапка = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ВременнаяПапка);
		ИмяФайлаСПутемHTML = ВременнаяПапка + Наименование + ".html";
		ТаблДок.Записать(ИмяФайлаСПутемHTML, ТипФайлаТабличногоДокумента.HTML5);
		
	Иначе
			
		КонвертацияXMLВHtml = ПолучитьОбщийМакет("КонвертацияXMLВHtml");
		ИмяXsl = ПолучитьИмяВременногоФайла("xsl");
		КонвертацияXMLВHtml.Записать(ИмяXsl);
	
		Преобразование = Новый ПреобразованиеXSL;
		Преобразование.ЗагрузитьИзФайла(ИмяXsl);
		ПредставлениеHTMLФайла = Преобразование.ПреобразоватьИзФайла(ИмяФайлаСПутем);
		
		УдалитьФайлы(ИмяXsl);
		
		Возврат ПредставлениеHTMLФайла;
	
	КонецЕсли;
	// Локализация Конец
		
КонецФункции

// Получает табличный документ по двоичным данным XML.
Функция ТабличныйДокументФайлаXML(ДанныеФайла) Экспорт
	
	// Локализация
	Если ДанныеФайла = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыВизуализации = ПараметрыВизуализацииШтампаЭДО();
	
	РезультатФормирования = ОбменСКонтрагентамиДОСлужебный.ПредставлениеДанныхПоФайлу(
		ДанныеФайла.Ссылка, ДанныеФайла.ТекущаяВерсия, ПараметрыВизуализации);
	
	Если Не РезультатФормирования.Успех
		Или ТипЗнч(РезультатФормирования.ТабличныйДокумент) <> Тип("ТабличныйДокумент") Тогда
			
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатФормирования.ТабличныйДокумент;
	// Локализация Конец
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Локализация
Функция ПараметрыВизуализацииШтампаЭДО()
	

	ПараметрыВизуализации = ИнтерфейсДокументовЭДО.НовыеПараметрыВизуализацииДокумента();
	
	Настройки = ОбменЭДОДокументооборот.НастройкиВизуализацииФайлаФормализованногоЭДО();
	
	ПараметрыВизуализации.ВыводитьДопДанные = Не Настройки.ОтключитьВыводДопДанных;
	ПараметрыВизуализации.ВыводитьКопияВерна = Не Настройки.ОтключитьВыводКопияВерна;
	ПараметрыВизуализации.ВыводитьБанковскиеРеквизиты = Настройки.ВыводитьБанковскиеРеквизиты;
	
	Возврат ПараметрыВизуализации;
	
КонецФункции
// Локализация Конец

// Локализация
// Получает табличный документ по двоичным данным XML.
// 
// Параметры:
//  ДвоичныеДанныеФайла - ДвоичныеДанные 
// 
// Возвращаемое значение:
//  Неопределено, ТабличныйДокумент, Строка - Табличный документ двоичных данных XML
//
Функция ТабличныйДокументДвоичныхДанныхXML(ДвоичныеДанныеФайла)
	
	СведенияОФайле = ОбменСКонтрагентамиДОСлужебный.СведенияОЭДИзФайла(ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла));
		
	// Если не смогли прочитать -- это не является неуспехом. Просто он не формализованный. Выводим как файл.
	Если ТипЗнч(СведенияОФайле) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	
	ВидДокумента = СведенияОФайле.ВидДокументаЭДО;
	ПараметрыВизуализации = ОбменСКонтрагентамиИнтеграция.НовыеПараметрыВизуализации();
	ПараметрыВизуализации.ВыводитьДопДанные           = Ложь;
	ПараметрыВизуализации.ВыводитьБанковскиеРеквизиты = Ложь;
	ПараметрыВизуализации.ВыводитьКопияВерна          = Ложь;
	
	РезультатПредставленияТабличногоДокумента =
		ЭлектронныеДокументыЭДО.ПредставлениеДанныхСообщения(
			ВидДокумента, ДвоичныеДанныеФайла,, ПараметрыВизуализации);
	
	Если ТипЗнч(РезультатПредставленияТабличногоДокумента) <> Тип("Структура")
		Или Не РезультатПредставленияТабличногоДокумента.Успех Тогда
		
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатПредставленияТабличногоДокумента.ПредставлениеДокумента;
	
КонецФункции
// Локализация Конец

#КонецОбласти
