
#Область ПрограммныйИнтерфейс

#Область СостоянияДокументовПоЭДО

// Возвращает состояние документа 1С:Документоборот в ЭДО.
//
// Параметры:
//  Документ - ОпределяемыйТип.ДокументДОДляЭДО - Документ, для которого необходимо получить сведения о состоянии.
//  Дата - Дата - Дата, на которую необходимо получить сведения.
//  Контрагент - СправочникСсылка.Контрагенты - Контрагент, для которого необходимо получить сведения.
// 
// Возвращаемое значение:
//  Структура - Содержит свежения о состоянии документа 1С:Документооборот в ЭДО.
//		см. РегистрыСведений.СостояниеДокументовПоЭДО.ПолучитьСостояниеДокумента
//
Функция ПолучитьСостояниеДокумента(Документ, Дата = Неопределено, Контрагент = Неопределено) Экспорт

	Возврат ОбменСКонтрагентамиДОСлужебный.СостояниеДокумента(Документ, Дата, Контрагент);

КонецФункции 

// Удаляет состояние документа документооборота по ЭДО
//
// Параметры:
//  СсылкаНаОбъект - ОпределяемыйТип.ДокументДОДляЭДО
//
Процедура УдалитьСостояниеДокументаЭДО(СсылкаНаОбъект) Экспорт
	
	РегистрыСведений.СостояниеДокументовПоЭДО.Удалить(СсылкаНаОбъект);
	
КонецПроцедуры

// Возвращает состояние версии документа ДО по ЭДО.
// 
// Параметры:
//  ДокументДО - ОпределяемыйТип.ДокументДОДляЭДО - Докуент 1С:Документооборот.
//  Контрагент - СправочникСсылка.Контрагенты - Контрагент по ЭДО.
//  Дата - Дата - Дата и время получения состояния
//  НаправлениеЭДО - ПеречислениеСсылка.НаправленияЭДО - Направление ЭДО для получения состояния
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияЭДОДокументооборот - состояние версии документа ДО по ЭДО.
Функция ДанныеСостоянияДокументаПоЭДО(ДокументДО, Знач Контрагент = Неопределено, Знач Дата = Неопределено,
	Знач НаправлениеЭДО = Неопределено) Экспорт

	Возврат РегистрыСведений.СостояниеДокументовПоЭДО.ДанныеСостоянияДокументаПоЭДО(
		ДокументДО, Контрагент, Дата, НаправлениеЭДО);

КонецФункции 

#КонецОбласти

#Область ДанныеОДокументеПоЭДО

// Возвращает соответствие документов ЭДО документам Документооборота
// 
// Параметры:
// 	ДокументыДО - Массив из ОпределяемыйТип.ДокументДОДляЭДО
// Возвращаемое значение:
// 	Соответствие из КлючИЗначение:
//     * Ключ - ОпределяемыйТип.ДокументДОДляЭДО - Документ ДО
//     * Значение - ДокументСсылка.ЭлектронныйДокументВходящийЭДО, ДокументСсылка.ЭлектронныйДокументИсходящийЭДО - Электронный документ документа ДО.
Функция ЭлектронныеДокументыДокументовДО(Знач ДокументыДО) Экспорт
	
	ЭлектронныеДокументыПоДокументам = Новый Соответствие;
	
	Для Каждого Документ Из ДокументыДО Цикл
		ЭлектронныеДокументыПоДокументам.Вставить(Документ);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент КАК ЭлектронныйДокумент,
		|	Файлы.ВладелецФайла КАК ДокументДО
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СлужебныеФайлыДокументов КАК СлужебныеФайлыДокументов
		|		ПО (СлужебныеФайлыДокументов.Файл = Файлы.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|		ПО (Файлы.ТекущаяВерсия = ОбъектыУчетаДокументовЭДО.ОбъектУчета
		|		И ОбъектыУчетаДокументовЭДО.Актуальный)
		|ГДЕ
		|	Файлы.ВладелецФайла В (&Документы)
		|	И СлужебныеФайлыДокументов.Файл ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Документы", ДокументыДО);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЭлектронныеДокументыПоДокументам[Выборка.ДокументДО] = Выборка.ЭлектронныйДокумент;
	КонецЦикла;
	
	Возврат ЭлектронныеДокументыПоДокументам;
	
КонецФункции

// Возвращает электронный документ, связанный с документом 1С:Документооборот
// 
// Параметры:
//  ДокументДО - ОпределяемыйТип.ДокументДОДляЭДО - Документ ДО для которого необходимо получить документ ЭДО
// 
// Возвращаемое значение:
//  ДокументСсылка.ЭлектронныйДокументВходящийЭДО, ДокументСсылка.ЭлектронныйДокументИсходящийЭДО, Неопределено -
//              Документ ЭДО с которым связан документ ДО, неопределено, если документа ЭДО не найдено.
Функция ЭлектронныйДокументДокументаДО(Знач ДокументДО) Экспорт
	
	Возврат ОбменСКонтрагентамиДОСлужебный.ЭлектронныйДокументДокументаДО(ДокументДО);
	
КонецФункции

// Возвращает данные о запущенных процессах аннулирвоания
// 
// Параметры:
//  Документ - ОпределяемыйТип.ДокументДОДляЭДО - Документ, для которого необходимо получить данные
//                                                    по запущеннып процессам аннулированияЭДО
// 
// Возвращаемое значение:
//  Структура:
//      * ЗапущеноАннулирование - Булево - Указывает, запущено ли аннулирование по документу
//      * АннулированиеЗавершено - Булево - Указывает, завершен ли процесс аннулирования по документу
//      * ДокументАннулирован - Булево - Указывает, аннулирован ли документ
//      * ТребуетсяСоздатьДокумент - Булево - Указывает, необходимо ли создать документ аннулирования
//      * АннулируетсяОтдельнымДокументом - Булево - Указывает, запущено ли аннулирование отдельным документом
//      * Направление - Перечисление.НаправленияЭДО - Направление аннулирования
//      * СообщениеЭДОАннулирования - ДокументСсылка.СообщениеЭДО - Сообщение ЭДО аннулирования
//      * ДокументАннулирования - ОпределяемыйТип.ДокументДОДляЭДО - Документ аннулирования
Функция ЗапущенныеПроцессыАннулированияДокумента(Знач Документ) Экспорт
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ЗапущеноАннулирование", Ложь);
	СтруктураВозврата.Вставить("АннулированиеЗавершено", Ложь);
	СтруктураВозврата.Вставить("ДокументАннулирован", Ложь);
	СтруктураВозврата.Вставить("ТребуетсяСоздатьДокумент", Ложь);
	СтруктураВозврата.Вставить("АннулируетсяОтдельнымДокументом", Ложь);
	СтруктураВозврата.Вставить("Направление", Перечисления.НаправленияЭДО.ПустаяСсылка());
	СтруктураВозврата.Вставить("СообщениеЭДОАннулирования", Документы.СообщениеЭДО.ПустаяСсылка());
	СтруктураВозврата.Вставить("ДокументАннулирования", ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ПустойДокументДО());
	
	// Аннулирование по ЭДО добавим позже
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

Функция СведенияОЭДИзФайлаДО(Файл, Версия = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Файл) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Версия) Тогда
		Версия = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Файл, "ТекущаяВерсия");
	КонецЕсли;
	
	ДанныеФайлаИДвоичныеДанные = РаботаСФайламиВызовСервера.ДанныеФайлаИДвоичныеДанные(Файл, Версия);
	
	Возврат СведенияОЭДИзФайла(ПоместитьВоВременноеХранилище(ДанныеФайлаИДвоичныеДанные.ДвоичныеДанные));
	
КонецФункции

// Сведения о формализованном электронном документе из файла
// 
// Параметры:
//  АдресВременногоХранилищаФайла - Строка - Адрес во временном хранилище на двоичные данные файла
// 
// Возвращаемое значение:
//  Структура, Неопределено - Неопределено, если файл не является формализованным. В ином случае:
// * ДатаДокумента - Дата - Дата документа
// * НомерДокумента - Строка - Номер документа
// * СуммаДокумента - Число - Сумма документа
// * ИмяСоздания - Строка - Имя для создания файла формата <Вид документа> №<Номер> от <Дата>
// * Валюта - СправочникСсылка.Валюты - Валюта документа
// * ТипДокументаЭДО - ПеречислениеСсылка.ТипыДокументовЭДО - Тип документа ЭДО
// * ВидДокументаЭДО - СправочникСсылка.ВидыДокументовЭДО - Вид документа ЭДО
Функция СведенияОЭДИзФайла(АдресВременногоХранилищаФайла) Экспорт
	
	Возврат ОбменСКонтрагентамиДОСлужебный.СведенияОЭДИзФайла(АдресВременногоХранилищаФайла);
	
КонецФункции

// Возвращает сертификаты криптографии, принадлежащие пользователю по массиву переданных отпечатков.
// 
// Параметры:
//  МассивОтпечатков - Массив из Строка - Массив отпечатков сертификатов.
//  Пользователь - СправочникСсылка.Пользователи - Пользователь для которого необходимо получить сертификаты.
//                                                 Если не задан - отбираются сертификаты по текущему пользователю.
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение - Сертификаты принадлежащие пользователю:
//    * Ключ - Строка - Отпечаток сертификата
//    * Значение - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - Ссылка на сертификат
Функция СертификатыПользователяПоОтпечаткам(Знач МассивОтпечатков, Знач Пользователь = Неопределено) Экспорт
	
	Если Пользователь = Неопределено Тогда
		ПользовательДляОтбора = Пользователи.ТекущийПользователь();
	Иначе
		ПользовательДляОтбора = Пользователь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СертификатыКлючейЭлектроннойПодписиИШифрования.Ссылка КАК Сертификат,
		|	СертификатыКлючейЭлектроннойПодписиИШифрования.Отпечаток КАК Отпечаток
		|ИЗ
		|	Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования КАК СертификатыКлючейЭлектроннойПодписиИШифрования
		|ГДЕ
		|	СертификатыКлючейЭлектроннойПодписиИШифрования.Пользователь = &Пользователь
		|	И СертификатыКлючейЭлектроннойПодписиИШифрования.Отпечаток В (&Отпечатки)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	СертификатыКлючейЭлектроннойПодписиИШифрованияПользователи.Ссылка,
		|	СертификатыКлючейЭлектроннойПодписиИШифрования.Отпечаток
		|ИЗ
		|	Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования.Пользователи КАК
		|		СертификатыКлючейЭлектроннойПодписиИШифрованияПользователи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования КАК
		|			СертификатыКлючейЭлектроннойПодписиИШифрования
		|		ПО СертификатыКлючейЭлектроннойПодписиИШифрованияПользователи.Ссылка = СертификатыКлючейЭлектроннойПодписиИШифрования.Ссылка
		|ГДЕ
		|	СертификатыКлючейЭлектроннойПодписиИШифрованияПользователи.Пользователь = &Пользователь
		|	И СертификатыКлючейЭлектроннойПодписиИШифрования.Отпечаток В (&Отпечатки)";
	Запрос.УстановитьПараметр("Пользователь", ПользовательДляОтбора);
	Запрос.УстановитьПараметр("Отпечатки", МассивОтпечатков);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СертификатыПоОтпечаткам = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		СертификатыПоОтпечаткам.Вставить(Выборка.Отпечаток, Выборка.Сертификат);
	КонецЦикла;
	
	Возврат СертификатыПоОтпечаткам;
	
КонецФункции

Процедура ДополнитьДанныеФайлаДляОткрытияСведениямиЭДО(ДанныеФайла) Экспорт
	
	ДанныеФайла.Вставить("УчаствуетВЭДО", Ложь);
	ДанныеФайла.Вставить("ЭтоXMLФайлаЭДО", Ложь);
	ДанныеФайла.Вставить("НеОткрыватьФормуЭДО", Ложь);
	
	Если НРег(ДанныеФайла.Расширение) <> "xml" Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектыУчета = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеФайла.Версия);
	
	ТаблицаЭДПоВерсииФайла = ИнтеграцияЭДО.ЭлектронныеДокументыОбъектовУчета(ОбъектыУчета);
	
	Если ТаблицаЭДПоВерсииФайла.Количество() > 0 Тогда
		ДанныеФайла.УчаствуетВЭДО = Истина;
	КонецЕсли;
	
	СведенияОЭД = СведенияОЭДИзФайлаДО(ДанныеФайла.Ссылка, ДанныеФайла.Версия);
	Если СведенияОЭД <> Неопределено Тогда
		ДанныеФайла.ЭтоXMLФайлаЭДО = Истина;
	КонецЕсли;
	
КонецПроцедуры

#Область ВыполнениеДействийПоЭДО

#Область ИсходящиеДокументы

// Ставит переданным документами и пакетам к отправке состояние по ЭДО в
//  Перечисления.СостоянияЭДОДокументооборот.ПоставленВОчередьНаОтправку.
//  Указывает на то, что по документу была запущена процедура отправки оператору ЭДО.
// 
// Параметры:
//  ДанныеДляОтправки - Структура:
//    * Документы - Соответствие Из КлючИЗначение:
//      ** Ключ - ОпределяемыйТип.ДокументДОДляЭДО - Документ ДО, по которому происходит отправка.
//      ** Значение - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО - Документ ЭДО,
//                                                                     связанный с отправляемым документом ДО
//    * Пакеты - Соответствие Из КлючИЗначение:
//      ** Ключ - УникальныйИдентификатор - УИД пакета ЭДО, по которому происходит отправка.
//      ** Значение - УникальныйИдентификатор - УИД пакета БЭД, связанный с отправляемым пакетом ДО
// 
// Возвращаемое значение:
//  Структура - Поставить в очередь на отправку:
//    * ПоставленныеВОчередьНаОтправку - Структура -:
//      ** Документы - Соответствие -
//        *** Ключ - ОпределяемыйТип.ДокументДОДляЭДО - Обработанный документ.
//        *** Значение - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО - Документ ЭДО,
//                                                                        связанный с отправляемым документом ДО
//      ** Пакеты - Соответствие -
//        *** Ключ - УникальныйИдентификатор - УИД обработанного пакета
//        *** Значение - УникальныйИдентификатор - УИД пакета БЭД, связанный с отправляемым пакетом ДО
//    * ОшибкиУстановкиСтатуса - Структура -:
//      ** Документы - Соответствие -
//        *** Ключ - ОпределяемыйТип.ДокументДОДляЭДО - Документ, который не удалось обработать.
//        *** Значение - Строка - Описание ошибки установки статуса
//      ** Пакеты - Соответствие -
//        *** Ключ - УникальныйИдентификатор - УИД пакета, который не удалось обработать
//        *** Значение - Строка - Описание ошибки установки статуса
Функция ПоставитьВОчередьНаОтправку(Знач ДанныеДляОтправки) Экспорт
	
	РезультатУстановкиСтатусов = Новый Структура;
	
	ПоставленныеВОчередьНаОтправку = Новый Структура;
	ПоставленныеВОчередьНаОтправку.Вставить("Документы", Новый Соответствие);
	ПоставленныеВОчередьНаОтправку.Вставить("Пакеты", Новый Соответствие);
	
	РезультатУстановкиСтатусов.Вставить("ПоставленныеВОчередьНаОтправку", ПоставленныеВОчередьНаОтправку);
	
	ОшибкиУстановкиСтатуса = Новый Структура;
	ОшибкиУстановкиСтатуса.Вставить("Документы", Новый Соответствие);
	ОшибкиУстановкиСтатуса.Вставить("Пакеты", Новый Соответствие);
	
	РезультатУстановкиСтатусов.Вставить("ОшибкиУстановкиСтатуса", ОшибкиУстановкиСтатуса);
	
	ДанныеПоСтатусам = ДанныеДляУстановкиСтатусаОтправки(ДанныеДляОтправки);
	
	Для Каждого ЭлементДокумента Из ДанныеПоСтатусам.Документы Цикл
		
		Документ = ЭлементДокумента.Ключ;
		ДанныеСостояния = ЭлементДокумента.Значение;
		
		Результат = ПоставитьВОчередьОтправкиДокумент(Документ, ДанныеСостояния);
		
		Если Результат.Успех Тогда
			РезультатУстановкиСтатусов.ПоставленныеВОчередьНаОтправку.Документы.Вставить(
				Документ, ДанныеДляОтправки.Документы[Документ]);
		Иначе
			РезультатУстановкиСтатусов.ОшибкиУстановкиСтатуса.Документы.Вставить(
				Документ, Результат.ОписаниеОшибки);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ЭлементПакета Из ДанныеПоСтатусам.Пакеты Цикл
		
		ИдентификаторПакета = ЭлементПакета.Ключ;
		ДанныеПоСтатусамДокументовПакета = ЭлементПакета.Значение;
		
		Результат = ПоставитьВОчередьОтправкиПакет(ИдентификаторПакета, ДанныеПоСтатусамДокументовПакета);
		
		Если Результат.Успех Тогда
			РезультатУстановкиСтатусов.ПоставленныеВОчередьНаОтправку.Пакеты.Вставить(
				ИдентификаторПакета, ДанныеДляОтправки.Пакеты[ИдентификаторПакета]);
		Иначе
			РезультатУстановкиСтатусов.ОшибкиУстановкиСтатуса.Пакеты.Вставить(
				ИдентификаторПакета, Результат.ОписаниеОшибки);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатУстановкиСтатусов;
	
КонецФункции

#КонецОбласти

#КонецОбласти

// Возвращает данные сертификатов доступных для подписания по ЭДО для документов.
// 
// Параметры:
//  Документы - Массив из ОпределяемыйТип.ДокументДОДляЭДО - Документы для которых необходимо получить
//                                                               даныне сертификатов для подписания по ЭДО.
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//    * Ключ - ОпределяемыйТип.ДокументДОДляЭДО - Документ ДО
//    * Значение - Соответствие из КлючИЗначение - Данные сертификатов для подписания по ЭДО документа:
//      ** Ключ - Строка - Отпечаток сертификата
//      ** Значение см. НовыеДанныеСертификатаЭДО
//  
Функция СертификатыЭДОДляДокументов(Знач Документы) Экспорт
	
	СертификатыЭДОДокументов = Новый Соответствие;
	Для Каждого Документ Из Документы Цикл
		СертификатыЭДОДокументов.Вставить(Документ, Новый Соответствие);
	КонецЦикла;
	
	ИдентификаторыЭДОДокументов = ИдентификаторыОрганизацииЭДОДокументов(Документы);
	
	УникальныеИдентификаторыЭДО = Новый Соответствие();
	Для Каждого Элемент Из ИдентификаторыЭДОДокументов Цикл
		Если ЗначениеЗаполнено(Элемент.Значение) Тогда
			УникальныеИдентификаторыЭДО.Вставить(Элемент.Значение, Истина);
		КонецЕсли;
	КонецЦикла;
	
	МассивУникальныхИдентификаторовЭДО = Новый Массив;
	Для Каждого Элемент Из УникальныеИдентификаторыЭДО Цикл
		МассивУникальныхИдентификаторовЭДО.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	СертификатыЭДОУчетныхЗаписей = СертификатыЭДОПоУчетнымЗаписям(МассивУникальныхИдентификаторовЭДО);
	
	СертификатыНаСервере = ОтпечаткиСертификатовДоступныхНаСервере();
	
	Для Каждого Элемент Из СертификатыЭДОДокументов Цикл
		
		Документ = Элемент.Ключ;
		ДанныеСертификатовПоОтпечаткам = Элемент.Значение;
		
		ИдентификаторЭДО = ИдентификаторыЭДОДокументов.Получить(Документ);
		
		Если Не ЗначениеЗаполнено(ИдентификаторЭДО) Тогда
			Продолжить;
		КонецЕсли;
		
		СертификатыИдентификатораЭДО = СертификатыЭДОУчетныхЗаписей.Получить(ИдентификаторЭДО);
		
		Если СертификатыИдентификатораЭДО = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ЭлементСертификата Из СертификатыИдентификатораЭДО Цикл
			
			Отпечаток = ЭлементСертификата.Ключ;
			ДанныеСертификатаЭДО = ЭлементСертификата.Значение;
			
			ДоступенНаСервере = (СертификатыНаСервере.Получить(Отпечаток) = Истина);
			
			ДанныеСертификата = НовыеДанныеСертификатаЭДО();
			ДанныеСертификата.Ссылка = ДанныеСертификатаЭДО.Ссылка;
			ДанныеСертификата.ДоступенПользователям = ДанныеСертификатаЭДО.ДоступенПользователям;
			ДанныеСертификата.ДействителенДо = ДанныеСертификатаЭДО.ДействителенДо;
			ДанныеСертификата.ДоступенНаСервере = ДоступенНаСервере;
			ДанныеСертификата.Доверенность = ДанныеСертификатаЭДО.Доверенность;
			
			ДанныеСертификатовПоОтпечаткам.Вставить(Отпечаток, ДанныеСертификата);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат СертификатыЭДОДокументов;
	
КонецФункции

Функция РольФайловДляОтправкиПоЭДО(ВидДокумента) Экспорт
	
	Возврат ОбменСКонтрагентамиДОСлужебный.РольФайловДляОтправкиПоЭДО(ВидДокумента);
	
КонецФункции

// Заполняет документ ЭДО данными при подписании
// 
// Параметры:
//  ПодписываемыеДанные - СправочникСсылка.Файлы
//  ДанныеДляЗаполненияЭДО - см. ФорматыЭДОДокументооборотКлиентСервер.НовыеДанныеДляЗаполненияФормализованногоДокумента
//  Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  Доверенность - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
// 
// Возвращаемое значение:
//  Структура:
// * ДвоичныеДанные - Неопределено, ДвоичныеДанные - Двоичные данные файла, если он изменен
// * ДанныеИзменены - Булево - Истина, если в ходе заполнения изменились данные файла
//
Функция ЗаполнитьДокументЭДОПриПодписании(Знач ПодписываемыеДанные, Знач ДанныеДляЗаполненияЭДО,
		Знач Сертификат, Знач Доверенность) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ДвоичныеДанные", Неопределено);
	Результат.Вставить("ДанныеИзменены", Ложь);
	
	ДанныеФайлаИДвоичныеДанные = РаботаСФайламиВызовСервера.ДанныеФайлаИДвоичныеДанные(ПодписываемыеДанные);
	
	ДвоичныеДанные = ДанныеФайлаИДвоичныеДанные.ДвоичныеДанные;
	Расширение = ДанныеФайлаИДвоичныеДанные.ДанныеФайла.Расширение;
	
	Результат.ДвоичныеДанные = ДвоичныеДанные;
	
	Если ВРег(Расширение) <> ВРег("xml") Тогда
		Возврат Результат;
	КонецЕсли;
	
	ОписаниеФайла = РаботаСФайламиБЭД.НовоеОписаниеФайла();
	ОписаниеФайла.ДвоичныеДанные = ДвоичныеДанные;
	ОписаниеФайла.ИмяФайла = ОбщегоНазначенияКлиентСервер.ПолучитьИмяСРасширением(
		ДанныеФайлаИДвоичныеДанные.ДанныеФайла.ПолноеНаименованиеВерсии,
		ДанныеФайлаИДвоичныеДанные.ДанныеФайла.Расширение);
	
	Если ДанныеДляЗаполненияЭДО.ПодписантДокумента = Неопределено Тогда
		ДанныеДляЗаполненияЭДО.ПодписантДокумента =
			ФорматыЭДОДокументооборотКлиентСервер.НовыеДанныеПодписантаДокумента();
	КонецЕсли;
	
	ДанныеДляЗаполненияЭДО.ПодписантДокумента.Сертификат = Сертификат;
	ДанныеДляЗаполненияЭДО.ПодписантДокумента.Доверенность = Доверенность;
	
	РезультатЗаполнения =
		ФорматыЭДОДокументооборот.ЗаполнитьДанныеФормализованногоДокумента(ОписаниеФайла, ДанныеДляЗаполненияЭДО);
	
	Если Не РезультатЗаполнения.ДанныеИзменены Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат.ДвоичныеДанные = РезультатЗаполнения.ДвоичныеДанные;
	Результат.ДанныеИзменены = РезультатЗаполнения.ДанныеИзменены;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьДанныеФайлаСУчетомПодписания(ПодписываемыеДаннные, ОтпечатокСертификата,
		Доверенности) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ДвоичныеДанные", Неопределено);
	Результат.Вставить("ДанныеИзменены", Ложь);
	
	ДанныеФайлаИДвоичныеДанные = РаботаСФайламиВызовСервера.ДанныеФайлаИДвоичныеДанные(ПодписываемыеДаннные);
	
	ДвоичныеДанные = ДанныеФайлаИДвоичныеДанные.ДвоичныеДанные;
	
	Результат.ДвоичныеДанные = ДвоичныеДанные;
	
	Расширение = ДанныеФайлаИДвоичныеДанные.ДанныеФайла.Расширение;
	
	Если ВРег(Расширение) <> ВРег("xml") Тогда
		Возврат Результат;
	КонецЕсли;
	
	ОписаниеФайла = РаботаСФайламиБЭД.НовоеОписаниеФайла();
	ОписаниеФайла.ДвоичныеДанные = ДвоичныеДанные;
	ОписаниеФайла.ИмяФайла = ОбщегоНазначенияКлиентСервер.ПолучитьИмяСРасширением(
		ДанныеФайлаИДвоичныеДанные.ДанныеФайла.ПолноеНаименованиеВерсии,
		ДанныеФайлаИДвоичныеДанные.ДанныеФайла.Расширение);
	
	СодержаниеДокумента = ФорматыЭДО.ПрочитатьСодержаниеДокумента(ОписаниеФайла);
	Если СодержаниеДокумента = Неопределено
		Или Не ФорматыЭДО.ЗаполнениеДанныхПодписантаДоступно(СодержаниеДокумента) Тогда
		
		Возврат Результат;
		
	Иначе
		
		Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПодписываемыеДаннные, "ВладелецФайла.Организация");
		
		Сертификат = Неопределено;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	СертификатыКлючейЭлектроннойПодписиИШифрования.Ссылка
			|ИЗ
			|	Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования КАК СертификатыКлючейЭлектроннойПодписиИШифрования
			|ГДЕ
			|	СертификатыКлючейЭлектроннойПодписиИШифрования.Отпечаток = &Отпечаток";
		Запрос.УстановитьПараметр("Отпечаток", ОтпечатокСертификата);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Сертификат = Выборка.Ссылка;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Сертификат) Тогда
			Возврат Результат;
		КонецЕсли;
		
		ОписаниеФайла = ФорматыЭДО.ЗаполнитьДанныеПодписантаДокумента(
			ОписаниеФайла, Организация, Сертификат, СодержаниеДокумента);
		
		Если ТипЗнч(Доверенности) = Тип("Массив") Тогда
			
			ДоверенностиДляЗанесенияВДокумент = Новый Массив;
			Для Каждого Доверенность Из Доверенности Цикл
				Если ЗначениеЗаполнено(Доверенность) Тогда
					ДоверенностиДляЗанесенияВДокумент.Добавить(Доверенность);
				КонецЕсли;
			КонецЦикла;
			
			Если ДоверенностиДляЗанесенияВДокумент.Количество() > 0
				И ФорматыЭДО.ЗаполнениеДанныхДоверенностиДоступно(СодержаниеДокумента) Тогда
				
				ОписаниеФайла = ФорматыЭДО.ЗаполнитьДанныеДоверенностейДокумента(
					ОписаниеФайла, Организация, ДоверенностиДляЗанесенияВДокумент, СодержаниеДокумента);
			КонецЕсли;
			
		КонецЕсли;
		
		Результат.ДвоичныеДанные = ОписаниеФайла.ДвоичныеДанные;
		Результат.ДанныеИзменены = Истина;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#Область ОтражениеДокументовВДО

// Виды документов доступные для отражения входящих ЭДО.
// 
// Возвращаемое значение:
//  Массив Из СправочникСсылка.ВидыДокументов - Виды документов доступные для отражения входящих ЭДО
Функция ВидыДокументовДоступныеДляОтраженияВходящихЭДО() Экспорт
	
	Возврат ОбменСКонтрагентамиДОСлужебный.ВидыДокументовДоступныеДляОтраженияВходящихЭДО();
	
КонецФункции

// Данные по отражению входящего документа ЭДО.
// 
// Параметры:
//  ДокументЭДО - ДокументСсылка.ЭлектронныйДокументВходящийЭДО - Документ ЭДО
// 
// Возвращаемое значение:
//  Структура:
// * ТребуетОтражения - Булево - Стоит ли документ в списке к созданию документов ДО
// * ОжидаетАвтоматическогоОтражения - Булево - Должен ли документ обрабатываться регламентным заданием
// * ЕстьДокументДО - Булево - Создан ли документ ДО по входящему ЭДО
Функция ДанныеПоОтражениюДокументаЭДО(Знач ДокументЭДО) Экспорт
	
	Возврат ОбменСКонтрагентамиДОСлужебный.ДанныеПоОтражениюДокументаЭДО(ДокументЭДО);
	
КонецФункции

// Снимает входящий ЭДО с автоматического отражения в документах ДО
// 
// Параметры:
//  ДокументЭДО - ДокументСсылка.ЭлектронныйДокументВходящийЭДО - Документ ЭДО
Процедура СнятьДокументЭДОСАвтоматическогоСоздания(Знач ДокументЭДО) Экспорт
	
	РегистрыСведений.ДокументыЭДОКСозданиюВДО.СнятьДокументСАвтоматическогоСоздания(ДокументЭДО);
	
КонецПроцедуры

// Удаляет входящий ЭДО из списка к созданию документов ДО
// 
// Параметры:
//  ДокументЭДО - ДокументСсылка.ЭлектронныйДокументВходящийЭДО - Документ ЭДО
Процедура УдалитьДокументЭДОИзСпискаКСозданиювДО(Знач ДокументЭДО) Экспорт
	
	РегистрыСведений.ДокументыЭДОКСозданиюВДО.УдалитьДокументИзСпискаКОтражению(ДокументЭДО);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВыполнениеДействийПоЭДО

#Область ИсходящиеДокументы

Функция ДокументыПакетов(ИдентификаторыПакетов)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СоставПакетовЭДОДокументооборот.Документ
		|ИЗ
		|	РегистрСведений.СоставПакетовЭДОДокументооборот КАК СоставПакетовЭДОДокументооборот
		|ГДЕ
		|	СоставПакетовЭДОДокументооборот.ИдентификаторПакета В (&ИдентификаторыПакетов)";
	Запрос.УстановитьПараметр("ИдентификаторыПакетов", ИдентификаторыПакетов);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Документ")
	
КонецФункции

Функция ДанныеДляУстановкиСтатусаОтправки(ДанныеДляОтправки)
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Документы", Новый Соответствие);
	СтруктураВозврата.Вставить("Пакеты", Новый Соответствие);
	
	ДокументыКОбработке = Новый Массив;
	Для Каждого Элемент Из ДанныеДляОтправки.Документы Цикл
		ДокументыКОбработке.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	ПакетыКОбработке = Новый Массив;
	Для Каждого Элемент Из ДанныеДляОтправки.Пакеты Цикл
		ПакетыКОбработке.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	ВсеДокументы = Новый Массив;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеДокументы, ДокументыКОбработке);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеДокументы, ДокументыПакетов(ПакетыКОбработке));
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДокументыДО.Ссылка КАК Документ,
		|	СоставПакетовЭДОДокументооборот.ИдентификаторПакета КАК ИдентификаторПакета,
		|	СостояниеДокументовПоЭДОСрезПоследних.СостояниеВерсииДокументаПоЭДО КАК Состояние,
		|	СостояниеДокументовПоЭДОСрезПоследних.Контрагент,
		|	СостояниеДокументовПоЭДОСрезПоследних.НаправлениеЭД КАК Направление
		|ИЗ
		|	Справочник.%ИмяСправочникаДокументаДО% КАК ДокументыДО
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеДокументовПоЭДО.СрезПоследних(, ДокументДО В (&Документы)) КАК
		|			СостояниеДокументовПоЭДОСрезПоследних
		|		ПО ДокументыДО.Ссылка = СостояниеДокументовПоЭДОСрезПоследних.ДокументДО
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставПакетовЭДОДокументооборот КАК СоставПакетовЭДОДокументооборот
		|		ПО ДокументыДО.Ссылка = СоставПакетовЭДОДокументооборот.Документ
		|ГДЕ
		|	ДокументыДО.Ссылка В (&Документы)";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"%ИмяСправочникаДокументаДО%",
		ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ИмяСправочникаДокументовДО());
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Документы", ВсеДокументы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.ИдентификаторПакета) Тогда
			
			ДокументыПакета = СтруктураВозврата.Пакеты.Получить(Выборка.ИдентификаторПакета);
			
			Если ДокументыПакета = Неопределено Тогда
				ДокументыПакета = Новый Соответствие;
				СтруктураВозврата.Пакеты.Вставить(Выборка.ИдентификаторПакета, ДокументыПакета);
			КонецЕсли;
			
			ДанныеСостояния = Новый Структура;
			ДанныеСостояния.Вставить("Состояние", Выборка.Состояние);
			ДанныеСостояния.Вставить("Контрагент", Выборка.Контрагент);
			ДанныеСостояния.Вставить("Направление", Выборка.Направление);
			
			ДокументыПакета.Вставить(Выборка.Документ, ДанныеСостояния);
			
		Иначе
			
			ДанныеСостояния = Новый Структура;
			ДанныеСостояния.Вставить("Состояние", Выборка.Состояние);
			ДанныеСостояния.Вставить("Контрагент", Выборка.Контрагент);
			ДанныеСостояния.Вставить("Направление", Выборка.Направление);
			
			СтруктураВозврата.Документы.Вставить(Выборка.Документ, ДанныеСостояния);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция ПоставитьВОчередьОтправкиПакет(Пакет, ДанныеОСостоянииДокументов)
	
	РезультатПостановкиВОчередь = Новый Структура;
	РезультатПостановкиВОчередь.Вставить("Успех", Ложь);
	РезультатПостановкиВОчередь.Вставить("ОписаниеОшибки", "");
	
	НачатьТранзакцию();
	Попытка
		
		Для Каждого ЭлементДокумента Из ДанныеОСостоянииДокументов Цикл
			
			Документ = ЭлементДокумента.Ключ;
			ДанныеСостояния = ЭлементДокумента.Значение;
			
			Результат = ПоставитьВОчередьОтправкиДокумент(Документ, ДанныеСостояния);
			
			Если Не Результат.Успех Тогда
				ВызватьИсключение
					НСтр("ru = 'Не удалось поставить пакет в очередь на отправку по причине:'")
					+ Символы.ПС + Результат.ОписаниеОшибки;
			КонецЕсли;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		РезультатПостановкиВОчередь.ОписаниеОшибки = ОписаниеОшибки();
		Возврат РезультатПостановкиВОчередь;
	КонецПопытки;
	
	РезультатПостановкиВОчередь.Успех = Истина;
	Возврат РезультатПостановкиВОчередь;
	
КонецФункции

Функция ПоставитьВОчередьОтправкиДокумент(Документ, ДанныеСостояния)
	
	РезультатПостановкиВОчередь = Новый Структура;
	РезультатПостановкиВОчередь.Вставить("Успех", Ложь);
	РезультатПостановкиВОчередь.Вставить("ОписаниеОшибки", "");
	
	Если ДанныеСостояния.Состояние =
		Перечисления.СостоянияЭДОДокументооборот.ПоставленВОчередьНаОтправку Тогда
		
		РезультатПостановкиВОчередь.Успех = Истина;
		Возврат РезультатПостановкиВОчередь;
	КонецЕсли;
	
	Если ДанныеСостояния.Состояние <> Перечисления.СостоянияЭДОДокументооборот.ОжидаетсяОтправка Тогда
		
		РезультатПостановкиВОчередь.ОписаниеОшибки = СтрШаблон(
			НСтр("ru = 'Документ %1 не может быть поставлен в очередь на отправку, поскольку имеет неверное состояние.
				|В очередь на отправку могут быть поставлены только документы в состоянии ""Ожидается отправка""'"),
			Документ);
		Возврат РезультатПостановкиВОчередь;
		
	КонецЕсли;
	
	Попытка
		
		ДанныеСостоянияКУстановке = ОбменСКонтрагентамиДОСлужебный.НовыеДанныеСостоянияДляУстановки();
		ДанныеСостоянияКУстановке.Состояние = Перечисления.СостоянияЭДОДокументооборот.ПоставленВОчередьНаОтправку;
		ДанныеСостоянияКУстановке.Комментарий = НСтр("ru = 'Начата отправка документа ЭДО оператору.'");
		ДанныеСостоянияКУстановке.Направление = ДанныеСостояния.Направление;
		
		ОбменСКонтрагентамиДОСлужебный.УстановитьСостояниеДокументаЭДО(
			Документ, ДанныеСостояния.Контрагент, ДанныеСостоянияКУстановке);
		
	Исключение
		РезультатПостановкиВОчередь.ОписаниеОшибки = СтрШаблон(
			НСтр("ru = 'Не удалось поставить в очередь отправки документ %1 по причине:'"), Документ)
			+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат РезультатПостановкиВОчередь;
	КонецПопытки;
	
	РезультатПостановкиВОчередь.Успех = Истина;
	Возврат РезультатПостановкиВОчередь;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СертификатыДляЭДО

Функция ИдентификаторыОрганизацииЭДОДокументов(Документы)
	
	ИдентификаторыОрганизацийЭДОДокументов = Новый Соответствие;
	Для Каждого Документ Из Документы Цикл
		ИдентификаторыОрганизацийЭДОДокументов.Вставить(Документ, Неопределено);
	КонецЦикла;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДокументыДО.Ссылка КАК Документ,
		|	ДокументыДО.Организация,
		|	ДокументыДО.Контрагент,
		|	НастройкиОтправкиДокументовПоЭДО.ВидДокументаЭДО
		|ПОМЕСТИТЬ ВидыДокументовЭДОДляОтправки
		|ИЗ
		|	Справочник.%ИмяСправочникаДокументаДО% КАК ДокументыДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтправкиДокументовПоЭДО КАК НастройкиОтправкиДокументовПоЭДО
		|		ПО ДокументыДО.ВидДокумента = НастройкиОтправкиДокументовПоЭДО.ВидДокумента
		|		И ДокументыДО.Организация = НастройкиОтправкиДокументовПоЭДО.Отправитель
		|		И ДокументыДО.Контрагент = НастройкиОтправкиДокументовПоЭДО.Получатель
		|ГДЕ
		|	ДокументыДО.Ссылка В (&Документы)
		|	И НастройкиОтправкиДокументовПоЭДО.Отправлять
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Файлы.ВладелецФайла КАК Документ,
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент,
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент.ИдентификаторОрганизации КАК ИдентификаторОрганизации
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СлужебныеФайлыДокументов КАК СлужебныеФайлыДокументов
		|		ПО Файлы.Ссылка = СлужебныеФайлыДокументов.Файл
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|		ПО (Файлы.ТекущаяВерсия = ОбъектыУчетаДокументовЭДО.ОбъектУчета
		|		И ОбъектыУчетаДокументовЭДО.Актуальный)
		|ГДЕ
		|	Файлы.ВладелецФайла В (&Документы)
		|	И НЕ Файлы.ПометкаУдаления
		|	И СлужебныеФайлыДокументов.Файл ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВидыДокументовЭДОДляОтправки.Документ,
		|	НастройкиОтправкиЭлектронныхДокументовПоВидам.ИдентификаторОтправителя КАК ИдентификаторОрганизации
		|ИЗ
		|	ВидыДокументовЭДОДляОтправки КАК ВидыДокументовЭДОДляОтправки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам КАК
		|			НастройкиОтправкиЭлектронныхДокументовПоВидам
		|		ПО ВидыДокументовЭДОДляОтправки.Организация = НастройкиОтправкиЭлектронныхДокументовПоВидам.Отправитель
		|		И ВидыДокументовЭДОДляОтправки.Контрагент = НастройкиОтправкиЭлектронныхДокументовПоВидам.Получатель
		|		И ВидыДокументовЭДОДляОтправки.ВидДокументаЭДО = НастройкиОтправкиЭлектронныхДокументовПоВидам.ВидДокумента
		|ГДЕ
		|	ВидыДокументовЭДОДляОтправки.ВидДокументаЭДО <> ЗНАЧЕНИЕ(Справочник.ВидыДокументовЭДО.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВидыДокументовЭДОДляОтправки.Документ,
		|	НастройкиОтправкиЭлектронныхДокументовПоВидам.ИдентификаторОтправителя КАК ИдентификаторОрганизации
		|ИЗ
		|	ВидыДокументовЭДОДляОтправки КАК ВидыДокументовЭДОДляОтправки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам КАК
		|			НастройкиОтправкиЭлектронныхДокументовПоВидам
		|		ПО ВидыДокументовЭДОДляОтправки.Организация = НастройкиОтправкиЭлектронныхДокументовПоВидам.Отправитель
		|		И ВидыДокументовЭДОДляОтправки.Контрагент = НастройкиОтправкиЭлектронныхДокументовПоВидам.Получатель
		|		И НастройкиОтправкиЭлектронныхДокументовПоВидам.ВидДокумента = &ВидДокументаПрочее
		|ГДЕ
		|	ВидыДокументовЭДОДляОтправки.ВидДокументаЭДО = ЗНАЧЕНИЕ(Справочник.ВидыДокументовЭДО.ПустаяСсылка)";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"%ИмяСправочникаДокументаДО%",
		ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ИмяСправочникаДокументовДО());
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Документы", Документы);
	Запрос.УстановитьПараметр("ВидДокументаПрочее",
		ЭлектронныеДокументыЭДО.ВидДокументаПоТипу(Перечисления.ТипыДокументовЭДО.Прочее));
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	КолвоРезультатов = РезультатыЗапроса.Количество();
	
	ВыборкаИдентификаторовПоЭД = РезультатыЗапроса[КолвоРезультатов - 2].Выбрать();
	ВыборкаИдентификаторовПоНастройкамОтправки = РезультатыЗапроса[КолвоРезультатов - 1].Выбрать();
	
	Пока ВыборкаИдентификаторовПоЭД.Следующий() Цикл
		
		ИдентификаторыОрганизацийЭДОДокументов.Вставить(
			ВыборкаИдентификаторовПоЭД.Документ, ВыборкаИдентификаторовПоЭД.ИдентификаторОрганизации);
		
	КонецЦикла;
	
	Пока ВыборкаИдентификаторовПоНастройкамОтправки.Следующий() Цикл
		
		Документ = ВыборкаИдентификаторовПоНастройкамОтправки.Документ;
		
		Если ИдентификаторыОрганизацийЭДОДокументов.Получить(Документ) = Неопределено Тогда
			ИдентификаторыОрганизацийЭДОДокументов.Вставить(
				Документ, ВыборкаИдентификаторовПоНастройкамОтправки.ИдентификаторОрганизации);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ИдентификаторыОрганизацийЭДОДокументов;
	
КонецФункции

Функция СертификатыЭДОПоУчетнымЗаписям(ИдентификаторыЭДО)
	
	СертификатыИдентификаторовЭДО = Новый Соответствие;
	Для Каждого ИдентификаторЭДО Из ИдентификаторыЭДО Цикл
		СертификатыИдентификаторовЭДО.Вставить(ИдентификаторЭДО, Новый Соответствие);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО,
		|	СертификатыУчетныхЗаписейЭДО.Сертификат,
		|	СертификатыУчетныхЗаписейЭДО.ДействителенДо,
		|	СертификатыКлючейЭлектроннойПодписиИШифрования.Отпечаток КАК Отпечаток,
		|	СертификатыКлючейЭлектроннойПодписиИШифрования.Пользователь КАК Владелец,
		|	СертификатыУчетныхЗаписейЭДО.Доверенность
		|ПОМЕСТИТЬ СертификатыЭДО
		|ИЗ
		|	РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыУчетныхЗаписейЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования КАК
		|			СертификатыКлючейЭлектроннойПодписиИШифрования
		|		ПО СертификатыУчетныхЗаписейЭДО.Сертификат = СертификатыКлючейЭлектроннойПодписиИШифрования.Ссылка
		|ГДЕ
		|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО В (&ИдентификаторыЭДО)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СертификатыЭДО.ИдентификаторЭДО,
		|	СертификатыЭДО.Сертификат,
		|	СертификатыЭДО.ДействителенДо,
		|	СертификатыЭДО.Отпечаток,
		|	СертификатыЭДО.Владелец,
		|	СертификатыЭДО.Доверенность
		|ИЗ
		|	СертификатыЭДО КАК СертификатыЭДО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СертификатыЭДО.ИдентификаторЭДО,
		|	СертификатыЭДО.Сертификат,
		|	СертификатыЭДО.ДействителенДо,
		|	СертификатыЭДО.Отпечаток,
		|	СертификатыКлючейЭлектроннойПодписиИШифрованияПользователи.Пользователь,
		|	СертификатыЭДО.Доверенность
		|ИЗ
		|	СертификатыЭДО КАК СертификатыЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования.Пользователи КАК
		|			СертификатыКлючейЭлектроннойПодписиИШифрованияПользователи
		|		ПО СертификатыКлючейЭлектроннойПодписиИШифрованияПользователи.Ссылка = СертификатыЭДО.Сертификат";
	Запрос.УстановитьПараметр("ИдентификаторыЭДО", ИдентификаторыЭДО);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	КолвоРезультатов = РезультатыЗапроса.Количество();
	
	ВыборкаПоВладельцам = РезультатыЗапроса[КолвоРезультатов - 2].Выбрать();
	ВыборкаПоПользователям = РезультатыЗапроса[КолвоРезультатов - 1].Выбрать();
	
	Пока ВыборкаПоВладельцам.Следующий() Цикл
		
		ИдентификаторЭДО = ВыборкаПоВладельцам.ИдентификаторЭДО;
		
		СертификатыИдентификатора = СертификатыИдентификаторовЭДО.Получить(ИдентификаторЭДО);
		
		ДанныеСертификата = СертификатыИдентификатора.Получить(ВыборкаПоВладельцам.Отпечаток);
		
		Если ДанныеСертификата = Неопределено Тогда
			
			ДанныеСертификата = НовыеДанныеСертификатаЭДО();
			ДанныеСертификата.Ссылка = ВыборкаПоВладельцам.Сертификат;
			ДанныеСертификата.ДействителенДо = ВыборкаПоВладельцам.ДействителенДо;
			ДанныеСертификата.Доверенность = ВыборкаПоВладельцам.Доверенность;
			
			СертификатыИдентификатора.Вставить(ВыборкаПоВладельцам.Отпечаток, ДанныеСертификата);
			
		КонецЕсли;
		
		ДанныеСертификата.ДоступенПользователям.Вставить(ВыборкаПоВладельцам.Владелец, Истина);
		
	КонецЦикла;
	
	Пока ВыборкаПоПользователям.Следующий() Цикл
		
		ИдентификаторЭДО = ВыборкаПоПользователям.ИдентификаторЭДО;
		
		СертификатыИдентификатора = СертификатыИдентификаторовЭДО.Получить(ИдентификаторЭДО);
		
		ДанныеСертификата = СертификатыИдентификатора.Получить(ВыборкаПоПользователям.Отпечаток);
		
		Если ДанныеСертификата = Неопределено Тогда
			
			ДанныеСертификата = НовыеДанныеСертификатаЭДО();
			ДанныеСертификата.Ссылка = ВыборкаПоПользователям.Сертификат;
			ДанныеСертификата.ДействителенДо = ВыборкаПоПользователям.ДействителенДо;
			ДанныеСертификата.Доверенность = ВыборкаПоПользователям.Доверенность;
			
			СертификатыИдентификатора.Вставить(ВыборкаПоПользователям.Отпечаток, ДанныеСертификата);
			
		КонецЕсли;
		
		ДанныеСертификата.ДоступенПользователям.Вставить(ВыборкаПоПользователям.Пользователь, Истина);
		
	КонецЦикла;
	
	Возврат СертификатыИдентификаторовЭДО;
	
КонецФункции

Функция ОтпечаткиСертификатовДоступныхНаСервере()
	
	ОтпечаткиСертификатов = Новый Соответствие;
	
	МенеджерКриптографии = ЭлектроннаяПодпись.МенеджерКриптографии("ПолучениеСертификатов", Ложь);
	
	Если МенеджерКриптографии = Неопределено Тогда
		Возврат ОтпечаткиСертификатов;
	КонецЕсли;
	
	ХранилищеСертификатовКриптографии = МенеджерКриптографии.ПолучитьХранилищеСертификатов(
		ТипХранилищаСертификатовКриптографии.ПерсональныеСертификаты);
	
	ВсеСертификаты = ХранилищеСертификатовКриптографии.ПолучитьВсе();
	
	Для Каждого Сертификат Из ВсеСертификаты Цикл
		
		Отпечаток = Base64Строка(Сертификат.Отпечаток);
		
		ОтпечаткиСертификатов.Вставить(Отпечаток, Истина);
		
	КонецЦикла;
	
	Возврат ОтпечаткиСертификатов;
	
КонецФункции

// Новые данные сертификата по учетной записи ЭДО
// 
// Возвращаемое значение:
//  Структура - Новые данные сертификата ЭДО:
// * Ссылка - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - Ссылка на сертификат шифрования
// * ДействителенДо - Дата - Дата действия сертификата
// * ДоступенПользователям - Соответствие из КлючИЗначение:
//       ** Ключ - СправочникСсылка.Пользователи - Пользователь
//       ** Значение - Булево - Доступен ли сертификат пользователю
// * ДоступенНаСервере - Булево - Доступен ли сертификат на сервере
// * Доверенность - ОпределяемыйТип.МашиночитаемаяДоверенность - МЧД, используемая для учетной записи ЭДО
//
Функция НовыеДанныеСертификатаЭДО()
	
	ДанныеСертификата = Новый Структура;
	ДанныеСертификата.Вставить("Ссылка", Справочники.СертификатыКлючейЭлектроннойПодписиИШифрования.ПустаяСсылка());
	ДанныеСертификата.Вставить("ДействителенДо", Дата(1, 1, 1));
	ДанныеСертификата.Вставить("ДоступенПользователям", Новый Соответствие);
	ДанныеСертификата.Вставить("ДоступенНаСервере", Ложь);
	ДанныеСертификата.Вставить("Доверенность", Справочники.МЧД003.ПустаяСсылка());
	
	Возврат ДанныеСертификата;
	
КонецФункции

#КонецОбласти

#КонецОбласти
