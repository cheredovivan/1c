#Область ПрограммныйИнтерфейс

// Скачивает контейнер документа из 1С:Архива и сохраняет его во временное хранилище.
//
// Параметры:
//  Документ - СправочникСсылка.ДокументыПредприятия - документ.
//
Функция СкачатьКонтейнерДокумента(Документ) Экспорт
	
	Если ТипЗнч(Документ) <> Тип("СправочникСсылка.ДокументыПредприятия") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВебАдрес = Константы.АдресПубликации1САрхива.Получить();
	ИмяПользователя = Константы.ИмяПользователя1САрхива.Получить();
	
	ВладелецХранилища = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Константы.ИмяПользователя1САрхива);
	Пароль = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(ВладелецХранилища);
	
	Если Не ЗначениеЗаполнено(ВебАдрес) Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не заполнена настройка ""%1""'"),
			Метаданные.Константы.АдресПубликации1САрхива.Синоним);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ИмяПользователя) Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не заполнена настройка ""%1""'"),
			Метаданные.Константы.ИмяПользователя1САрхива.Синоним);
	КонецЕсли;
	
	Определение = Новый WSОпределения(ВебАдрес + "/ws/ArchiveDocuments.1cws?wsdl", ИмяПользователя, Пароль);
	Прокси = Новый WSПрокси(Определение, "http://www.1c.ru/docmng", "ArchiveDocuments", "ArchiveDocumentsSoap");
	Прокси.Пользователь = ИмяПользователя;
	Прокси.Пароль = Пароль;
	
	ДанныеФайла = Прокси.GetContainer(Строка(Документ.УникальныйИдентификатор()));
	СведенияОФайлеДляКлиента = Новый Структура;
	СведенияОФайлеДляКлиента.Вставить("Имя", ДанныеФайла.Name);
	СведенияОФайлеДляКлиента.Вставить("Расширение", ДанныеФайла.Extension);
	СведенияОФайлеДляКлиента.Вставить("АдресДвоичныхДанных",
		ПоместитьВоВременноеХранилище(ДанныеФайла.BinaryData));
		
	ФормаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "ФормаДокумента");
	ЗаписьПротокола = Новый Структура("ОбъектДанных, ТипСобытия, Длительность, ОписаниеСобытия, ДополнительныеСведения");
	ЗаписьПротокола.ОбъектДанных = Документ;
	ЗаписьПротокола.ТипСобытия = Перечисления.ТипыСобытийПротоколаРаботыСотрудников.ОбменСАрхивом;
	ЗаписьПротокола.ОписаниеСобытия =
		?(ФормаДокумента = Перечисления.ВариантыФормДокументов.Электронная,
			НСтр("ru = 'Скачивание контейнера документа'"),
			НСтр("ru = 'Скачивание файлов документа'"));
	ПротоколированиеРаботыСотрудников.Записать(ЗаписьПротокола);
	
	Возврат СведенияОФайлеДляКлиента;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции



#КонецОбласти
