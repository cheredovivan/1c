
#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьСписокДляВыбораПользователем(РаспознанныйТекст, Варианты = Неопределено, КартинкаСоздание = 1) Экспорт 
	
	СписокДляВыбора = Новый СписокЗначений;
	
	Если КартинкаСоздание = 1 Тогда
		ТекстРаспознано = НСтр("ru = 'Создать: '");
		Картинка = БиблиотекаКартинок.ДобавитьЭлементСписка;
	Иначе
		ТекстРаспознано = НСтр("ru = 'Распознано: '");
		Если КартинкаСоздание = 2 Тогда 
			Картинка = БиблиотекаКартинок.Заменить;
		Иначе
			Картинка = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	ТекстРаспознано = Новый ФорматированнаяСтрока(ТекстРаспознано, Новый Шрифт(,,,Истина));
	СписокДляВыбора.Добавить(РаспознанныйТекст, Новый ФорматированнаяСтрока(
		ТекстРаспознано, """", РаспознанныйТекст, """"), Ложь, Картинка);
	
	Если Варианты = Неопределено Тогда
		Возврат СписокДляВыбора;
	КонецЕсли;
	
	Для Каждого Вариант Из Варианты Цикл
		ПараметрыВыбора = Новый Структура;
		ПараметрыВыбора.Вставить("Значение", Вариант.Значение);
		ПараметрыВыбора.Вставить("Уверенность", Вариант.Уверенность);
		
		ПредставлениеЗначения = Строка(Вариант.Значение);
		
		ЕстьДополнительноеЗначение = ОбщегоНазначенияКлиентСервер.
			ЕстьРеквизитИлиСвойствоОбъекта(Вариант, "ДополнительноеЗначение");
		
		Если ЕстьДополнительноеЗначение
			И ЗначениеЗаполнено(Вариант.ДополнительноеЗначение) Тогда
			
			ПредставлениеЗначения = ПредставлениеЗначения + " (" + Строка(Вариант.ДополнительноеЗначение) + ")";
		КонецЕсли;
		
		Уверенность = Новый ФорматированнаяСтрока("(" + НСтр("ru = 'увер. '") 
			+ Вариант.Уверенность + "%)", Новый Шрифт(,,,Истина));
		ПредставлениеЗначения = Новый ФорматированнаяСтрока(ПредставлениеЗначения, " ", Уверенность);
		СписокДляВыбора.Добавить(ПараметрыВыбора.Значение, ПредставлениеЗначения);
	КонецЦикла;
	
	Возврат СписокДляВыбора;
	
КонецФункции

Функция КлючСвойстваЯчеекТаблицы(ИмяРеквизита, ИдентификаторСтроки) Экспорт
	
	Возврат ИмяРеквизита + "_" + Строка(ИдентификаторСтроки);
	
КонецФункции

Функция ОпределитьЮридическоеФизическоеЛицо(ИНН) Экспорт
	
	ЭтоФизЛицо = (СтрДлина(ИНН) > 10);
	
	Возврат ?(ЭтоФизЛицо,
		ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо"),
		ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо"));
	
КонецФункции


Функция НаборДанныхСтрокиТаблицы(СвойстваЯчеекТаблицы, НомерСтрокиТЧ) Экспорт
	
	Результат = Новый Массив;
	
	Суффикс = "_" + Формат(НомерСтрокиТЧ, "ЧГ=");
	
	Для Каждого Свойства Из СвойстваЯчеекТаблицы Цикл
		
		Если СтрЗаканчиваетсяНа(Свойства.Ключ, Суффикс) Тогда
			Результат.Добавить(Свойства.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНаборКоординат(Данные = Неопределено) Экспорт
	
	КоординатыКартинки = Новый Массив(4);
	
	ТипДанных = ТипЗнч(Данные);
	Если ТипДанных = Тип("Структура") И Данные.Свойство("Координаты") Тогда
		КоординатыКартинки[0] = Данные.Координаты[0];
		КоординатыКартинки[1] = Данные.Координаты[1];
		КоординатыКартинки[2] = Данные.Координаты[2];
		КоординатыКартинки[3] = Данные.Координаты[3];
	ИначеЕсли ТипДанных = Тип("ДанныеФормыЭлементКоллекции") Или (Данные <> Неопределено
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Данные, "КоординатаX0")) Тогда
		
		КоординатыКартинки[0] = Данные.КоординатаX0;
		КоординатыКартинки[1] = Данные.КоординатаY0;
		КоординатыКартинки[2] = Данные.КоординатаX1;
		КоординатыКартинки[3] = Данные.КоординатаY1;
	Иначе
		КоординатыКартинки[0] = 0;
		КоординатыКартинки[1] = 0;
		КоординатыКартинки[2] = 0;
		КоординатыКартинки[3] = 0;
	КонецЕсли;
	
	Возврат КоординатыКартинки;
	
КонецФункции

Процедура ПриИзмененииКолонки(СтрокаТаблицы, ИмяРеквизита, ВыбранноеЗначение) Экспорт
	
	Если ИмяРеквизита = "СуммаНДС" Тогда
		ПриИзмененииСуммаНДС(СтрокаТаблицы);
	ИначеЕсли ИмяРеквизита = "Количество" ИЛИ ИмяРеквизита = "Цена" Тогда
		ПриИзмененииЦены(СтрокаТаблицы);
	ИначеЕсли ИмяРеквизита = "Сумма" Тогда
		ПриИзмененииСуммы(СтрокаТаблицы);
	ИначеЕсли ИмяРеквизита = "СтавкаНДС" Тогда
		ПриИзмененииСтавкиНДС(СтрокаТаблицы);
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеВерифицируемыхРеквизитов(Форма, ДанныеВерифицированы = Ложь, ДанныеПодобраны = Ложь) Экспорт
	
	Если Форма.ВерифицируемыеРеквизитыТЧ.Количество() = 0 И Форма.ВерифицируемыеРеквизитыШапки = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеРеквизитов = Новый Структура;
	ДанныеРеквизитов.Вставить("ВерифицируемыеРеквизитыТЧ", Форма.ВерифицируемыеРеквизитыТЧ);
	ДанныеРеквизитов.Вставить("ВерифицируемыеРеквизитыШапки", Форма.ВерифицируемыеРеквизитыШапки);
	ДанныеРеквизитов.Вставить("ДанныеВерифицированы", ДанныеВерифицированы);
	ДанныеРеквизитов.Вставить("ДанныеПодобраны", ДанныеПодобраны);
	
	Возврат ДанныеРеквизитов;
	
КонецФункции

Функция ВерифицируемыеТабличныеЧасти() Экспорт
	
	ВерифицируемыеТЧ = Новый Массив;
	
	ВерифицируемыеТЧ.Добавить("Товары");
	ВерифицируемыеТЧ.Добавить("Стороны");
	ВерифицируемыеТЧ.Добавить("Контрагенты");
	
	Возврат ВерифицируемыеТЧ;
	
КонецФункции

Процедура УстановитьУсловноеОформлениеДляТаблицы(Форма, Знач ИмяТЧ) Экспорт
	
	Если СтрНайти(ИмяТЧ, "Объект.") > 0 Тогда
		
		ИмяТаблицыДляОформления = СтрЗаменить(ИмяТЧ, "Объект.", "");
		ТаблицаФормы = Форма.Объект[ИмяТаблицыДляОформления];
		
	Иначе
		ТаблицаФормы = Форма[ИмяТЧ];
		ИмяТаблицыДляОформления = ИмяТЧ;
	КонецЕсли;
	
	ИменаРеквизитов = ИменаРаспознанныхРеквизитовПоИмениТабличнойЧасти(Форма, ИмяТаблицыДляОформления);
	
	Для Каждого ИмяРеквизита Из ИменаРеквизитов Цикл
		
		Элемент = Форма.УсловноеОформление.Элементы.Добавить();
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяТаблицыДляОформления + ИмяРеквизита);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = 
			Новый ПолеКомпоновкиДанных(ИмяТЧ + "." + ИмяРеквизита + "ТребуетПроверки");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Истина;
		
		#Если Клиент Тогда
			Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", 
											ОбщегоНазначенияКлиент.ЦветСтиля("ИнформационныйЦентрКрасныйЦвет"));
											
			Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", 
											ОбщегоНазначенияКлиент.ШрифтСтиля("ЖирныйШрифтБЭД"));
		#Иначе
			
			Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ИнформационныйЦентрКрасныйЦвет);
			Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ЖирныйШрифтБЭД);
			
		#КонецЕсли
		
		Для Каждого Стр Из ТаблицаФормы Цикл
			
			ДанныеРеквизита = ДанныеРеквизита(Форма, ИмяРеквизита, ИмяТаблицыДляОформления, Стр.Идентификатор);
			
			Если ДанныеРеквизита = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ДанныеРеквизита.СтрокаУдалена Тогда
				Продолжить;
			КонецЕсли;
			
			Если Не РаспознаваниеДокументовКлиентСервер.
								ЭтоПримитивныйТип(ТипЗнч(ДанныеРеквизита.РаспознанноеЗначение)) Тогда
				
				Элемент = Форма.УсловноеОформление.Элементы.Добавить();
				ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
				ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяТаблицыДляОформления + ИмяРеквизита);
				
				ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяТЧ + ".Идентификатор");
				ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
				ОтборЭлемента.ПравоеЗначение = Стр.Идентификатор;
				ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяТЧ + "." + ИмяРеквизита);
				ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
				
				#Если Клиент Тогда
					Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", 
											ОбщегоНазначенияКлиент.ЦветСтиля("ИнформационныйЦентрКрасныйЦвет"));
					Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", 
												ОбщегоНазначенияКлиент.ШрифтСтиля("ЖирныйШрифтБЭД"));
				#Иначе
					Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", 
																		ЦветаСтиля.ИнформационныйЦентрКрасныйЦвет);
					Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ЖирныйШрифтБЭД);
				#КонецЕсли
				
				Элемент.Оформление.УстановитьЗначениеПараметра("Текст", СтрШаблон(НСтр("ru = 'Не сопоставлен: %1'"), 
					ДанныеРеквизита.РаспознанныйТекст));
					
			КонецЕсли;
					
		КонецЦикла;
		
	КонецЦикла;
		
КонецПроцедуры

Функция ДанныеРеквизита(Форма, Знач ИмяРеквизита = "", Знач ИмяТЧ = Неопределено, 
																		Знач ИдентификаторСтроки = Неопределено) Экспорт
	Отбор = Новый Структура;
	
	Если Не ПустаяСтрока(ИмяРеквизита) Тогда
		Отбор.Вставить("ИмяРеквизита", ИмяРеквизита);
	КонецЕсли;
	
	Если ИмяТЧ <> Неопределено Тогда
		Отбор.Вставить("ТабличнаяЧасть", ИмяТЧ);
		Если ИдентификаторСтроки <> Неопределено Тогда
			Отбор.Вставить("Идентификатор", ИдентификаторСтроки);
		КонецЕсли;
		НСтр = Форма.ВерифицируемыеРеквизитыТЧ.НайтиСтроки(Отбор);
	Иначе
		НСтр = Форма.ВерифицируемыеРеквизитыШапки.НайтиСтроки(Отбор);
	КонецЕсли;
	
	Если НСтр.Количество() = 1 И Не ПустаяСтрока(ИмяРеквизита) Тогда
		Возврат НСтр[0];
	ИначеЕсли НСтр.Количество() > 0 Тогда
		Возврат НСтр;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ТекущиеЗначенияВерифицируемыхРеквизитовФормы(Форма) Экспорт
	
	Реквизиты = ВерифицируемыеРеквизитыДокумента(Ложь, Истина);
	
	ДанныеРеквизита = Новый Структура;
	
	ЭтоФормаДокумента = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект");
	
	Для Каждого ИмяРеквизита Из Реквизиты Цикл
		
		Если ЭтоФормаДокумента = Истина Тогда
			Если Форма.Объект.Свойство(ИмяРеквизита) Тогда
				ДанныеРеквизита.Вставить(ИмяРеквизита, Форма.Объект[ИмяРеквизита]);
			КонецЕсли;
		ИначеЕсли ИмяРеквизита = "Заголовок" Тогда
			ДанныеРеквизита.Вставить(ИмяРеквизита, Форма["ЗаголовокДокумента"]);
		ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, ИмяРеквизита) Тогда
			ДанныеРеквизита.Вставить(ИмяРеквизита, Форма[ИмяРеквизита]);
		Иначе
			Продолжить;
		КонецЕсли;
		
	КонецЦикла;

	Возврат ДанныеРеквизита
	
КонецФункции

Функция ВерифицируемыеРеквизитыДокумента(Строкой = Истина, ВключатьРеквизитыФормы = Ложь) Экспорт
	
	СоставРеквизитов = Новый Массив;
	СоставРеквизитов.Добавить("ВидДокумента");
	СоставРеквизитов.Добавить("Тематика");
	СоставРеквизитов.Добавить("Валюта");
	СоставРеквизитов.Добавить("Заголовок");
	СоставРеквизитов.Добавить("Содержание");
	СоставРеквизитов.Добавить("Организация");
	СоставРеквизитов.Добавить("Контрагент");
	СоставРеквизитов.Добавить("Комментарий");
	СоставРеквизитов.Добавить("Товары");
	СоставРеквизитов.Добавить("Контрагенты");
	СоставРеквизитов.Добавить("Стороны");
	СоставРеквизитов.Добавить("Сумма");
	СоставРеквизитов.Добавить("СуммаНДС");
	
	Если ВключатьРеквизитыФормы = Истина Тогда
		СоставРеквизитов.Добавить("ИсходящаяДата");
		СоставРеквизитов.Добавить("ИсходящийНомер");
	КонецЕсли;
	
	Если Строкой = Истина Тогда
		
		СтрокаРеквизиты = "";
		
		Для Каждого ИмяРеквизита Из СоставРеквизитов Цикл
			СтрокаРеквизиты = СтрокаРеквизиты + ИмяРеквизита + ","
		КонецЦикла;
		
		СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(СтрокаРеквизиты, 1);
		
		Возврат СтрокаРеквизиты;
		
	Иначе
		
		Возврат СоставРеквизитов;
		
	КонецЕсли;
	
КонецФункции

Функция НужнаВерификация(Форма) Экспорт
	
	УчитыватьНДС = УчитыватьНДС(Форма);
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, 
																			"ВерифицируемыеРеквизитыШапки") Тогда
		
		Для Каждого Стр Из Форма.ВерифицируемыеРеквизитыШапки Цикл
			
			Если СтрНайти(Стр.ИмяРеквизита, "НДС") > 0 Тогда
				Если УчитыватьНДС = Ложь Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если Стр.ТребуетПроверки = Истина И Стр.Проверен = Ложь Тогда
				Возврат Истина;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ВерифицируемыеРеквизитыТЧ") Тогда
	
		Для Каждого Стр Из Форма.ВерифицируемыеРеквизитыТЧ Цикл
			
			Если СтрНайти(Стр.ИмяРеквизита, "НДС") > 0 Тогда
				Если УчитыватьНДС = Ложь Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если Стр.СтрокаУдалена = Истина Тогда
				Продолжить;
			КонецЕсли;
			
			Если Стр.ТребуетПроверки = Истина И Стр.Проверен = Ложь Тогда
				Возврат Истина;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ЗаполненыДанныеВерификации(Форма) Экспорт
	
	КоличествоВРШапки = 0;
	КоличествоВРТЧ = 0;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ВерифицируемыеРеквизитыШапки") Тогда
		КоличествоВРШапки = Форма.ВерифицируемыеРеквизитыШапки.Количество();
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ВерифицируемыеРеквизитыТЧ") Тогда
		КоличествоВРТЧ = Форма.ВерифицируемыеРеквизитыТЧ.Количество();
	КонецЕсли;
	
	Возврат КоличествоВРШапки > 0 ИЛИ КоличествоВРТЧ > 0;
	
КонецФункции

Функция ДанныеДокументаНеРаспознаны(Форма) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ВерифицируемыеРеквизитыШапки") Тогда
		
		Отбор = Новый Структура("ИмяРеквизита", ИмяРеквизитаДокументНеРаспознан());
		НСтр = Форма.ВерифицируемыеРеквизитыШапки.НайтиСтроки(Отбор);
		Возврат НСтр.Количество() > 0;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ИмяРеквизитаДокументНеРаспознан() Экспорт
	
	Возврат "#Все";
	
КонецФункции

Функция КоличествоРеквизитовДляВерификации(Форма) Экспорт
	
	УчитыватьНДС = УчитыватьНДС(Форма);
	
	КоличествоТребующихПроверки = 0;
	
	Если Не ОбщегоНазначенияКлиентСервер.
			ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ВерифицируемыеРеквизитыШапки") Тогда
		Возврат 0;
	КонецЕсли;
	
	Для Каждого Стр Из Форма.ВерифицируемыеРеквизитыШапки Цикл
		
		Если Стр.ТребуетПроверки = Истина И Стр.Проверен = Ложь Тогда
			
			Если УчитыватьНДС = Ложь Тогда
				Если Стр.ИмяРеквизита = "СуммаНДС" Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			КоличествоТребующихПроверки = КоличествоТребующихПроверки + 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Стр Из Форма.ВерифицируемыеРеквизитыТЧ Цикл
		
		Если Стр.СтрокаУдалена = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		Если УчитыватьНДС = Ложь Тогда
			Если Стр.ИмяРеквизита = "СтавкаНДС" ИЛИ Стр.ИмяРеквизита = "СуммаНДС" Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если Стр.ТребуетПроверки = Истина И Стр.Проверен = Ложь Тогда
			КоличествоТребующихПроверки = КоличествоТребующихПроверки + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат КоличествоТребующихПроверки;
	
КонецФункции
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПриИзмененииСуммаНДС(СтрокаТаблицы) Экспорт
	
	ДелопроизводствоКлиентСервер.ПересчитатьСуммуНДСВСтрокеТЧ(СтрокаТаблицы, СтрокаТаблицы.СтавкаНДС);
	
КонецПроцедуры

Процедура ПриИзмененииЦены(СтрокаТаблицы)

	ДелопроизводствоКлиентСервер.ПересчитатьСуммуВСтрокеТЧ(СтрокаТаблицы, СтрокаТаблицы.СтавкаНДС);
	
КонецПроцедуры

Процедура ПриИзмененииСуммы(СтрокаТаблицы)
	
	ДелопроизводствоКлиентСервер.ПересчитатьЦенуПоСуммеВСтрокеТЧ(СтрокаТаблицы, СтрокаТаблицы.СтавкаНДС);
	
КонецПроцедуры

Процедура ПриИзмененииСтавкиНДС(СтрокаТаблицы)
	
	ДелопроизводствоКлиентСервер.ПересчитатьСуммуНДСВСтрокеТЧ(СтрокаТаблицы, СтрокаТаблицы.СтавкаНДС);
	
КонецПроцедуры

Функция НаборДанныхСозданияЭлемента(НаборДанных) Экспорт
	
	Строка = Новый Структура;
	Строка.Вставить("ИмяРеквизита");
	Строка.Вставить("НомерСтроки");
	Строка.Вставить("РаспознанныйТекст");
	Строка.Вставить("АдресКартинки");
	Строка.Вставить("СтрокВИзображении");
	Строка.Вставить("Координаты");
	Строка.Вставить("Значение");
	
	Результат = Новый Соответствие;
	Для Каждого СтрокаНабора Из НаборДанных Цикл
		
		ЗаполнитьЗначенияСвойств(Строка, СтрокаНабора);
		Строка.Координаты = ПолучитьНаборКоординат(СтрокаНабора);
		Результат.Вставить(Строка.ИмяРеквизита, Новый ФиксированнаяСтруктура(Строка));
		
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(Результат);
	
КонецФункции

Функция ИменаРаспознанныхРеквизитовПоИмениТабличнойЧасти(Форма, ИмяТаблицы)
	
	ИменаРеквизитов = Новый Массив;
	
	ДанныеРеквизитов = ДанныеРеквизита(Форма, , ИмяТаблицы);
	
	Если ДанныеРеквизитов = Неопределено Тогда
		Возврат ИменаРеквизитов;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеРеквизитов) = Тип("Массив") Тогда
		
		Для Каждого Реквизит Из ДанныеРеквизитов Цикл
			
			Если ИменаРеквизитов.Найти(Реквизит.ИмяРеквизита) = Неопределено Тогда
				ИменаРеквизитов.Добавить(Реквизит.ИмяРеквизита);
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат ИменаРеквизитов;
	КонецЕсли;
	
	ИменаРеквизитов.Добавить(ДанныеРеквизитов.ИмяРеквизита);
	
	Возврат ИменаРеквизитов;
	
КонецФункции

Функция УчитыватьНДС(Форма)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "УчитыватьНДС") Тогда
		Возврат Форма.УчитыватьНДС;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект") Тогда
		ПроверяемыйОбъект = Форма.Объект;
	Иначе
		ПроверяемыйОбъект = Форма;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ПроверяемыйОбъект, "Организация") 
		И ТипЗнч(ПроверяемыйОбъект.Организация) = Тип("СправочникСсылка.Организации") Тогда
		
		#Если Сервер Тогда
			Возврат ПроверяемыйОбъект.Организация.ПлательщикНДС;
		#КонецЕсли
		
		#Если Клиент Тогда
			Возврат ОбщегоНазначенияДокументооборотВызовСервера.
				ЗначениеРеквизитаОбъекта(ПроверяемыйОбъект.Организация, "ПлательщикНДС");
		#КонецЕсли
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти
