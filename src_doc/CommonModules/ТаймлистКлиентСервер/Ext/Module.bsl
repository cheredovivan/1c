#Область ПрограммныйИнтерфейс

// Проверяет, соответствует ли переданное расширение файла списку,
// указанному в настройке.
// 
// Параметры:
//  РасширениеФайла - Строка
//  УчитыватьИспользованиеСервиса - Булево
//
// Возвращаемое значение:
//  Булево - Истина, если расширение входит в список допустимых.
//
Функция ЭтоРасширениеФайлаТаймлист(РасширениеФайла, УчитыватьИспользованиеСервиса = Ложь) Экспорт
	
	МассивРасширений = ТаймлистВызовСервераПовтИсп.РасширенияФайловТаймлист();
	РасширениеФайлаБезТочки = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(РасширениеФайла);
	
	ФайлРасширенияТаймлист = МассивРасширений.Найти(НРег(РасширениеФайлаБезТочки)) <> Неопределено;
	
	Если Не УчитыватьИспользованиеСервиса Тогда
		Возврат ФайлРасширенияТаймлист;
	КонецЕсли;
	
	#Если Клиент Тогда
		ТаймлистИспользуется = ТаймлистКлиентПовтИсп.ТаймлистИспользуется();
	#Иначе
		ТаймлистИспользуется = ТаймлистПовтИсп.ТаймлистИспользуется();
	#КонецЕсли
	
	Если Не ТаймлистИспользуется Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ФайлРасширенияТаймлист;
	
КонецФункции

// Возвращает строку расширений аудио- и видеофайлов, загружаемых в сервис Таймлист, по умолчанию.
// 
// Возвращаемое значение:
//  Строка
//
Функция РасширенияФайловТаймлистПоУмолчанию() Экспорт
	
	Возврат "mp4, mov, avi, webm, ogg, mpeg, mp3, m4a";
	
КонецФункции

// Возвращает стиль гиперссылки, выводимой в обзор файла Таймлист.
//
// Возвращаемое значение:
//  Строка
//
Функция СтильГиперссылки() Экспорт
	
	Стиль =
		"<head>
		|<style type=""text/css"">
		|.block
		|{
		|	width: 45%;
		|	font-family: Arial, Helvetica, sans-serif;
		|	font-size: 90%;
		|	line-height: 1.25;
		|	text-align: left;
		|	position: static;
		|}
		|a:visited {
		|	color: #006699; text-decoration: none;}
		|a:hover {
		|	color: #006699; text-decoration: underline;}
		|</style>
		|</head>";
	
	Возврат Стиль;
	
КонецФункции

// Возвращает строковый ключ отображения расшифровки в HTML обзор.
// 
// Возвращаемое значение:
//  Строка
//
Функция КлючОтображенияРасшифровкиHTML() Экспорт
	
	Возврат "TimelistEditTranscript";
	
КонецФункции

// Возвращает строковый ключ отображения автопротокоола в HTML обзор.
// 
// Возвращаемое значение:
//  Строка
//
Функция КлючОтображенияАвтопротоколаHTML() Экспорт
	
	Возврат "TimelistEditAutoprotocol";
	
КонецФункции

// Проверяет, поддерживает ли форма с переданным именем выполнение команд работы с сервисом.
// 
// Параметры:
//  ИмяФормы - Строка
// 
// Возвращаемое значение:
//  Булево
// 
Функция ФормаПоддерживаетПолучениеРасшифровкиАвтопротокола(ИмяФормы) Экспорт
	
	Если ИмяФормы = "Документ.Задача.Форма.Задачи"
		Или ИмяФормы = "Документ.ДействиеЗадачи.Форма.ФормаЭлемента"
		Или ИмяФормы = "ОбщаяФорма.СвязиОбъекта"
		Или ИмяФормы = "ОбщаяФорма.СогласованиеВРежимеЗамечанийФормаСогласующего"
		Или ИмяФормы = "ОбщаяФорма.СогласованиеВРежимеЗамечанийФормаИнициатора"
		Или ИмяФормы = "ОбщаяФорма.СогласованиеВРежимеЗамечанийФормаТолькоПросмотр" Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти
