#Область ПрограммныйИнтерфейс

#Область ОбщегоНазначения

// Возвращает номер версии протокола обмена с сервисом распознавания.
//
// Возвращаемое значение:
//  Строка - номер версии протокола обмена .
//
Функция ВерсияПротоколаОбмена() Экспорт
	
	Возврат "1.2";
	
КонецФункции

// Возвращает идентификатор подсистемы распознавания документов.
// 
// Возвращаемое значение:
//  Строка - Идентификатор подсистемы
//
Функция ИдентификаторПодсистемы() Экспорт
	
	Возврат "БиблиотекаРаспознаванияДокументов";
	
КонецФункции

// Максимальный размер очереди документов на распознавании.
// 
// Возвращаемое значение:
//  Число - Максимальный размер очереди документов на распознавании
Функция МаксимальныйРазмерОчередиДокументовНаРаспознавании() Экспорт
	
	Возврат 20;
	
КонецФункции

// Обработчик регламентного задания ЗаполнениеКарточекДокументов
//
Процедура ЗаполнениеКарточекДокументов() Экспорт
	
	Отказ = Ложь;
	РегламентноеЗадание = Метаданные.РегламентныеЗадания.ЗаполнениеКарточекДокументов;
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(РегламентноеЗадание, Отказ);
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	МиграцияДанныхИзВнешнихСистемСервер.ПриНачалеРаботыРегламентногоЗадания(
		РегламентноеЗадание);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыРаспознаванияДокументов = ПараметрыРаспознаванияДокументов();
	
	Если ВозможенЗапускРаспознаванияДокументов(ПараметрыРаспознаванияДокументов) Тогда
		
		ЗаписьЖурналаРегистрации(
			ПараметрыРаспознаванияДокументов.СобытиеДляЖурналаРегистрации,
			УровеньЖурналаРегистрации.Информация, , ,
			НСтр("ru = 'Начато регламентное распознавание документов'"));
			
		ВыполнитьОбменССервисомРаспознаванияДокументов();
		
		ЗаписьЖурналаРегистрации(
			ПараметрыРаспознаванияДокументов.СобытиеДляЖурналаРегистрации,
			УровеньЖурналаРегистрации.Информация,,,
			НСтр("ru = 'Закончено регламентное распознавание документов'"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СервисРаспознавания

// Проверяет авторизацию аккаунта.
// 
// Возвращаемое значение:
//  Булево - Аккаунт авторизован
//
Функция АккаунтАвторизован() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыАвторизации = ТекущиеПараметрыАвторизации();
	Возврат АвторизацияВыполнена(ПараметрыАвторизации);
	
КонецФункции

// Проверяет активацию аккаунта.
// 
// Параметры:
//  ВыбрасыватьИсключение - Булево - Выбрасывать исключение
// 
// Возвращаемое значение:
//  Булево - Аккаунт активирован
//
Функция АккаунтАктивирован(ВыбрасыватьИсключение = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыАвторизации = ТекущиеПараметрыАвторизации();
	
	Если Не АвторизацияВыполнена(ПараметрыАвторизации) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если АктивацияВыполнена(ПараметрыАвторизации) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Попытка
		Результат = РаспознаваниеДокументовКоннекторСлужебный.СостояниеАктивацииУчетнойЗаписи();
		Если Результат.Состояние = "Активирован" Тогда
			ПараметрыАвторизации.Состояние = Результат.Состояние;
			ЗаписатьПараметрыАвторизации(ПараметрыАвторизации);
		КонецЕсли;
	Исключение
		Если ВыбрасыватьИсключение Тогда
			ВызватьИсключение;
		Иначе
			
			ТекстОшибки = НСтр("ru = 'Попытка проверки активации аккаунта Портала 1С:ИТС не удалось по причине: %1'",
				 ОбщегоНазначения.КодОсновногоЯзыка());
				
			ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ПолныйТекстОшибки = СтрШаблон(ТекстОшибки, ПодробноеПредставлениеОшибки);
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , ,
				 ПолныйТекстОшибки);
			
			Возврат Ложь;
			
		КонецЕсли;
	КонецПопытки;
	
	Возврат Результат.Состояние = "Активирован";
	
КонецФункции

// Выполняет авторизацию по тикету ИТС.
Процедура ВыполнитьАвторизациюПоТикетуИТС() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	ПараметрыАвторизации = ТекущиеПараметрыАвторизации();
	Если АвторизацияВыполнена(ПараметрыАвторизации) Тогда
		Возврат;
	КонецЕсли;

	ВладелецТикета = "DocumentRecognition";
	РезультатПолученияТикета = РаспознаваниеЛокализация.РезультатПолученияТикета(ВладелецТикета);
	
	Если ПустаяСтрока(РезультатПолученияТикета.Тикет) Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось получить тикет авторизации с Портала 1С:ИТС
			           |по причине:
			           |%1'", ОбщегоНазначения.КодОсновногоЯзыка());
		ПолныйТекстСообщения = СтрШаблон(ТекстСообщения, РезультатПолученияТикета.СообщениеОбОшибке);
			           
		Если РаботаВМоделиСервиса.РазделениеВключено() Тогда
			ВызватьИсключение ТекстСообщения;
		Иначе
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение, , , ПолныйТекстСообщения);
				
		КонецЕсли;
	КонецЕсли;
	
	Если РезультатПолученияТикета.КодОшибки = "НеверныйЛогинИлиПароль"
		Или РезультатПолученияТикета.КодОшибки = "ПревышеноКоличествоПопыток" Тогда
		
		ТекстСообщения = НСтр("ru = 'Не удалось получить тикет авторизации с Портала 1С:ИТС по причине: %1
				|Перейдите в настройки интернет-поддержки и измените данные авторизации.'", 
					ОбщегоНазначения.КодОсновногоЯзыка());
					
		ПолныйТекстСообщения = СтрШаблон(ТекстСообщения, РезультатПолученияТикета.СообщениеОбОшибке);
		
		ВызватьИсключение ПолныйТекстСообщения;
		
	КонецЕсли;
	
	Область = ОбластьАвторизации();
	Область.ТипАутентификации = "ПоТикетуИТС";

	Если ПустаяСтрока(РезультатПолученияТикета.КодОшибки) Тогда
		
		РезультатАвторизации = РаспознаваниеДокументовКоннекторСлужебный.ВыполнитьАвторизациюПоТикетуИТС(
			РезультатПолученияТикета.Тикет,
			Область,
			ПараметрыАвторизации.ИдентификаторИБ);

	Иначе
		
		УстановитьПривилегированныйРежим(Истина);
		ДанныеАутентификации = РаспознаваниеЛокализация.ДанныеАутентификацииПользователяИнтернетПоддержки();
		Если ДанныеАутентификации <> Неопределено Тогда
			Логин  = ДанныеАутентификации.Логин;
			Пароль = ДанныеАутентификации.Пароль;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
		
		РезультатАвторизации = РаспознаваниеДокументовКоннекторСлужебный.ВыполнитьАвторизациюПоЛогинуПаролюИТС(
			Логин,
			Пароль,
			Область,
			ПараметрыАвторизации.ИдентификаторИБ);
		
	КонецЕсли;
	
	ПараметрыАвторизации.ИдентификаторИБ = РезультатАвторизации.ИдентификаторИБ;
	ПараметрыАвторизации.ТокенДоступа = РезультатАвторизации.ТокенДоступа;
	ПараметрыАвторизации.Состояние = РезультатАвторизации.Состояние;
	ПараметрыАвторизации.ТипАутентификации = "ПоТикетуИТС";
	ПараметрыАвторизации.Логин = "";

	ЗаписатьПараметрыАвторизации(ПараметрыАвторизации);

	УстановитьПривилегированныйРежим(Ложь);

	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

// Функциональная опция ИспользоватьСервисРаспознаванияДокументовДляЗаполненияКарточек включена,
// информационная база зарегистрирована в сервисе распознавания документов,
// активация учетной записи выполнена
// 
// Параметры:
//  СРасшифровкой - Булево - включать описание причины не работоспособности сервиса
// 
// Возвращаемое значение:
//  Булево, Структура - Использование доступно:
// * ИспользованиеДоступно - Булево - 
// * Расшифровка - Неопределено, Строка - 
Функция ИспользованиеДоступно(СРасшифровкой = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеСРасшифровкой = Новый Структура("ИспользованиеДоступно, Расшифровка", Истина, Неопределено);
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСервисРаспознаванияДокументовДляЗаполненияКарточек") Тогда
		Если СРасшифровкой Тогда
			ДанныеСРасшифровкой.ИспользованиеДоступно = Ложь;
			ДанныеСРасшифровкой.Расшифровка = НСтр("ru = 'Сервис 1С:РПД не подключен. Обратитесь к Администратору.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
			Возврат ДанныеСРасшифровкой;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыАвторизации = ТекущиеПараметрыАвторизации();
	
	Если Не АвторизацияВыполнена(ПараметрыАвторизации) Тогда
		Если СРасшифровкой Тогда
			ДанныеСРасшифровкой.ИспользованиеДоступно = Ложь;
			ДанныеСРасшифровкой.Расшифровка = НСтр("ru = '
			|Не выполнена авторизация в сервисе 1С:РПД. Обратитесь к Администратору.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
			Возврат ДанныеСРасшифровкой;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Не АктивацияВыполнена(ПараметрыАвторизации) Тогда
		Если СРасшифровкой Тогда
			ДанныеСРасшифровкой.ИспользованиеДоступно = Ложь;
			ДанныеСРасшифровкой.Расшифровка = НСтр("ru = '
			|Не выполнена активация сервиса 1С:РПД. Обратитесь к Администратору.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
			Возврат ДанныеСРасшифровкой;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если СРасшифровкой = Истина Тогда
		//чтобы не выключать в интерфейсе, только при интерактивном обращении к сервису с выводом причины
		СостоянияПоБалансу = СостоянияПоБалансу();
		
		Если Не (СостоянияПоБалансу.СостояниеБаланса = "БалансПоложительный" 
				Или СостоянияПоБалансу.СостояниеБаланса = "БалансПоложительныйДатаОкончания") Тогда
					
			ДанныеСРасшифровкой.ИспользованиеДоступно = Ложь;
			ДанныеСРасшифровкой.Расшифровка = НСтр("ru = '
			|Баланс страниц распознавания нулевой. Обратитесь к Администратору.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		КонецЕсли;
		
		Возврат ДанныеСРасшифровкой;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Проверяет подключение к сервису распознавания.
// 
// Параметры:
//  ПытатьсяПодключитьсяПриПроверке - Булево - Пытаться подключиться при проверке
//  ВыбрасыватьИсключение - Булево - Выбрасывать исключение
// 
// Возвращаемое значение:
//  Булево - Подключено к сервису распознавания
//
Функция ПодключеноКСервисуРаспознавания(ПытатьсяПодключитьсяПриПроверке = Истина, ВыбрасыватьИсключение = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыАвторизации = ТекущиеПараметрыАвторизации();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если АвторизацияВыполнена(ПараметрыАвторизации) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Не ПытатьсяПодключитьсяПриПроверке Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если РаспознаваниеЛокализация.ВыполненаАвторизацияЧерезИТС() Тогда
		Попытка
			ВыполнитьАвторизациюПоТикетуИТС();
		Исключение
			Если ВыбрасыватьИсключение Тогда
				ВызватьИсключение;
			Иначе
				ТекстОшибки = СтрШаблон(
					НСтр("ru = 'Попытка автоматической авторизации по тикету с Портала 1С:ИТС не удалось
					|по причине:
					|%1'", ОбщегоНазначения.КодОсновногоЯзыка()),
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ЗаписьЖурналаРегистрации(
					СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
				
				Возврат Ложь;
			КонецЕсли;
		КонецПопытки;
		
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Возвращает параметры баланса
// 
// Возвращаемое значение:
//  Структура - Состояния по балансу:
// * СостояниеБаланса - Строка - 
// * АдресЭлПочты - Строка - 
// * ДатаОкончания - Дата - 
// * Баланс - Число - 
// * Кредит - Число - 
// * Использовано - Число - 
// * ПоказатьТребованиеВключитьИТС - Булево - 
// * ПоказатьПредупреждениеНулевойБаланс - Булево - 
// * ПоказатьПредупреждениеСервисОтключен - Булево - 
// * ВыполнятьЗагрузкуРезультатов - Булево - 
// * ПодключениеТестовогоПериодаДоступно - Булево - 
Функция СостоянияПоБалансу() Экспорт
	
	СостояниеБаланса = "ПроверкаСтатусаПодключения";
	// Возможные состояния:
	// * НеПоказыватьБаланс
	// * ПроверкаСтатусаПодключения
	// * Ошибка
	// * НеАвторизован
	// * НеАктивирован
	// * ПилотныйРежим
	// * ПилотныйРежимДатаОкончания
	// * ПилотныйРежимДатаОкончанияНаступила
	// * БалансНулевой
	// * БалансПоложительный
	// * БалансПоложительныйДатаОкончания
	// * БалансОтрицательный
	// * СервисОтключен
	
	Результат = Новый Структура;
	Результат.Вставить("СостояниеБаланса", СостояниеБаланса);
	Результат.Вставить("АдресЭлПочты", "");
	Результат.Вставить("ДатаОкончания", Дата("00010101"));
	Результат.Вставить("Баланс", 0);
	Результат.Вставить("Кредит", 0);
	Результат.Вставить("Использовано", 0);
	Результат.Вставить("ПоказатьТребованиеВключитьИТС", Ложь);
	Результат.Вставить("ПоказатьПредупреждениеНулевойБаланс", Ложь);
	Результат.Вставить("ПоказатьПредупреждениеСервисОтключен", Ложь);
	Результат.Вставить("ВыполнятьЗагрузкуРезультатов", Истина);
	Результат.Вставить("ПодключениеТестовогоПериодаДоступно", Ложь);
	
	Если Не АккаунтАвторизован() Тогда
		Результат.СостояниеБаланса = "НеАвторизован";
		Результат.ВыполнятьЗагрузкуРезультатов = Ложь;
		Возврат Результат;
	КонецЕсли;
	
	Если Не АккаунтАктивирован() Тогда
		Результат.СостояниеБаланса = "НеАктивирован";
		Результат.ВыполнятьЗагрузкуРезультатов = Ложь;
		Возврат Результат;
	КонецЕсли;
	
	Попытка
		ТекущийБаланс = РаспознаваниеДокументовSDK.ТекущийБаланс();
	Исключение
		Результат.СостояниеБаланса = "Ошибка";
		Возврат Результат;
	КонецПопытки;
	
	Если ТекущийБаланс.АвторизацияТикетИТС Тогда
		Результат.ПодключениеТестовогоПериодаДоступно = ПодключениеТестовогоПериодаДоступно();
	КонецЕсли;
	
	Если ТекущийБаланс.АвторизацияТикетИТС Или ТекущийБаланс.ТарификацияВключена Тогда
		
		Если ТекущийБаланс.Баланс = 0 Тогда
			
			Результат.СостояниеБаланса = "БалансНулевой";
			Результат.ДатаОкончания = ТекущийБаланс.ДатаОкончания;
			
			Если ЗначениеЗаполнено(Результат.ДатаОкончания) Тогда
				Результат.ПоказатьПредупреждениеНулевойБаланс = Истина;
			КонецЕсли;
			
		ИначеЕсли ТекущийБаланс.Баланс > 0 Тогда
			
			Если ЗначениеЗаполнено(ТекущийБаланс.ДатаОкончания) Тогда
				Результат.СостояниеБаланса = "БалансПоложительныйДатаОкончания";
				Результат.ДатаОкончания = ТекущийБаланс.ДатаОкончания;
				Результат.Баланс = ТекущийБаланс.Баланс;
			Иначе
				Результат.СостояниеБаланса = "БалансПоложительный";
				Результат.Баланс = ТекущийБаланс.Баланс;
			КонецЕсли;
			
		Иначе // ТекущийБаланс.Баланс < 0
			
			Если ТекущийБаланс.Лимит > 0 Тогда
				
				Результат.СостояниеБаланса = "БалансОтрицательный";
				Результат.ДатаОкончания = ТекущийБаланс.ДатаОкончания;
				Результат.Кредит = ТекущийБаланс.Лимит - ТекущийБаланс.Баланс;
				Результат.Использовано = -ТекущийБаланс.Баланс;
				
				Если ЗначениеЗаполнено(Результат.ДатаОкончания) Тогда
					Результат.ПоказатьПредупреждениеНулевойБаланс = Истина;
				КонецЕсли;
				
			Иначе // ТекущийБаланс.Лимит <= 0
				
				Результат.СостояниеБаланса = "СервисОтключен";
				Результат.ДатаОкончания = ТекущийБаланс.ДатаОкончания;
				Результат.Использовано = -ТекущийБаланс.Баланс;
				
				Результат.ПоказатьПредупреждениеСервисОтключен = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе // Не ТекущийБаланс.АвторизацияТикетИТС И Не ТекущийБаланс.ТарификацияВключена
		
		Если Не ЗначениеЗаполнено(ТекущийБаланс.ДатаОкончания) Тогда
			
			Результат.СостояниеБаланса = "ПилотныйРежим";
			
		ИначеЕсли ТекущийБаланс.ДатаОкончания > ТекущаяУниверсальнаяДата() Тогда
			
			Результат.СостояниеБаланса = "ПилотныйРежимДатаОкончания";
			Результат.ДатаОкончания = ТекущийБаланс.ДатаОкончания;
			
		Иначе
			Результат.СостояниеБаланса = "ПилотныйРежимДатаОкончанияНаступила";
			Результат.ДатаОкончания = ТекущийБаланс.ДатаОкончания;
			Результат.ПоказатьТребованиеВключитьИТС = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Для использования требуется привилегированный режим.
// 
// Возвращаемое значение:
//  Структура - Текущие параметры авторизации:
//  * ИдентификаторИБ - Строка 
//  * ТокенДоступа - Строка 
//  * Состояние - Строка 
//  * ТипАутентификации - Строка 
//  * Логин - Строка 
Функция ТекущиеПараметрыАвторизации() Экспорт
	
	ДанныеВБезопасномХранилище = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		ИдентификаторПодсистемы(),
		"ИдентификаторИБ, ТокенДоступа, ТипАутентификации, Логин, Состояние");
	
	Если ДанныеВБезопасномХранилище.ИдентификаторИБ <> Неопределено
		И ДанныеВБезопасномХранилище.ТокенДоступа <> Неопределено
		И ДанныеВБезопасномХранилище.ТипАутентификации <> Неопределено Тогда
		
		Если ДанныеВБезопасномХранилище.Состояние = Неопределено Тогда
			ДанныеВБезопасномХранилище.Состояние = "Активирован";
		КонецЕсли;
		
		Возврат ДанныеВБезопасномХранилище;
	КонецЕсли;
	
	Возврат НовыеПараметрыАвторизации();
	
КонецФункции

// Удаляет данные авторизации.
// 
// Параметры:
//  УдалитьИдентификаторИБ - Булево - Удалить идентификатор ИБ
//
Процедура УдалитьДанныеАвторизации(УдалитьИдентификаторИБ = Истина) Экспорт
	
	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		ВызватьИсключение НСтр("ru = 'Удалять данные авторизации доступно только Администратору.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыАвторизации = ТекущиеПараметрыАвторизации();
	
	ПараметрыАвторизации.ТокенДоступа = "";
	ПараметрыАвторизации.ТипАутентификации = "НеВыполнена";
	ПараметрыАвторизации.Состояние = "Ожидает";
	Если УдалитьИдентификаторИБ Тогда
		ПараметрыАвторизации.ИдентификаторИБ = "";
	КонецЕсли;
	ПараметрыАвторизации.Логин = "";
	
	ЗаписатьПараметрыАвторизации(ПараметрыАвторизации);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти 

#Область СлужебныйПрограммныйИнтерфейс

// См. РегламентныеЗаданияПереопределяемый.ПриОпределенииНастроекРегламентныхЗаданий.
Процедура ПриОпределенииНастроекРегламентныхЗаданий(Настройки) Экспорт
	
	Настройка = Настройки.Добавить();
	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ЗаполнениеКарточекДокументов;
	Настройка.ФункциональнаяОпция = 
		Метаданные.ФункциональныеОпции.ИспользоватьСервисРаспознаванияДокументовДляЗаполненияКарточек;
	Настройка.РаботаетСВнешнимиРесурсами = Истина;
	Настройка.ФункциональныйРазделительВМоделиСервиса = Настройка.ФункциональнаяОпция;
	Настройка.ДоступноВПодчиненномУзлеКОД = Истина;
	Настройка.ДоступноВМоделиСервиса = Ложь;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	
КонецПроцедуры

// Выполняет обмен данными с сервисом распознавания документов.
Процедура ВыполнитьОбменССервисомРаспознаванияДокументов() Экспорт
	
	ФайлыДляОбработки = ФайлыДокументовДляРаспознавания();
	
	Для Каждого ПараметрыФайла Из ФайлыДляОбработки.Выгрузка Цикл
		
		Попытка
			
			РегистрыСведений.ФайлыДокументовНаРаспознавании.ЗаблокироватьФайл(ПараметрыФайла.Файл);
			ВыполнитьВыгрузкуФайлаВСервисРаспознаванияДокументов(ПараметрыФайла);
			
		Исключение
			
			ТекстОшибки = НСтр("ru = 'Не удалось выгрузить файл в сервис распознавания: %1'", 
				ОбщегоНазначения.КодОсновногоЯзыка());
				
			ОписаниеОшибки = ОписаниеОшибки();
			ПолныйТекстОшибки = СтрШаблон(ТекстОшибки, ОписаниеОшибки);
				
			РегистрыСведений.ФайлыДокументовНаРаспознавании.
				ЗафиксироватьОшибкуПриРаспознавании(ПараметрыФайла.Файл, ПолныйТекстОшибки);
			Продолжить;
			
		КонецПопытки;
		
		РегистрыСведений.ФайлыДокументовНаРаспознавании.РазблокироватьФайл(ПараметрыФайла.Файл);
		
	КонецЦикла;
	
	Для Каждого ПараметрыЗагрузкиФайла Из ФайлыДляОбработки.Загрузка Цикл
		
		Попытка
			ВыполнитьЗагрузкуРезультатаРаспознавания(ПараметрыЗагрузкиФайла);
		Исключение
			
			ТекстОшибки = НСтр("ru = 'Не удалось загрузить результат распознавания: %1'", 
				ОбщегоНазначения.КодОсновногоЯзыка());
				
			ОписаниеОшибки = ОписаниеОшибки();
			ПолныйТекстОшибки = СтрШаблон(ТекстОшибки, ОписаниеОшибки);
				
			РегистрыСведений.ФайлыДокументовНаРаспознавании.
				ЗафиксироватьОшибкуПриРаспознавании(ПараметрыФайла.Файл, ПолныйТекстОшибки);
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

// Событие журнала регистрации.
// 
// Возвращаемое значение:
//  Строка - Событие журнала регистрации
Функция СобытиеЖурналаРегистрации() Экспорт
	
	Возврат НСтр("ru = 'РаспознаваниеДокументов'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

// Возвращает параметры распознавания для сервиса распознавания первичных документов.
//
// Возвращаемое значение:
//  Структура:
//   * ЯзыкРаспознавания - Строка - язык распознавания.
//   * СоответствиеВидовДокументов - ТаблицаЗначений
Функция ПараметрыЗаполненияКарточекДокументов() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ЯзыкРаспознавания", "");
	Параметры.Вставить("СоответствиеВидовДокументов", РегистрыСведений.
		СоответствиеВидовРаспознаваемыхДокументов.СоответствиеВидовРаспознаваемыхДокументов());
	
	Возврат Параметры;
	
КонецФункции

// Установить параметры заполнения карточек документов.
// 
// Параметры:
//  Параметры - Структура
Процедура УстановитьПараметрыЗаполненияКарточекДокументов(Знач Параметры) Экспорт
	
	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		ВызватьИсключение НСтр(
			"ru = 'Запись параметров распознавания разрешена только пользователям с полными правами.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
	КонецЕсли;
	РегистрыСведений.СоответствиеВидовРаспознаваемыхДокументов.ЗаписатьДанные(Параметры.СоответствиеВидовДокументов);
	
КонецПроцедуры

// Возвращает текущий статус распознавания документа.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
//  ВозвращатьСтатусПрервано - Булево
//  ТолькоСтатус - Булево
//
// Возвращаемое значение:
//  Неопределено, Структура, ПеречислениеСсылка.СтатусыРаспознаванияДокументов - 
Функция ТекущаяИнформацияОСтатусеРаспознаванияДокумента(ДокументСсылка, Знач ВозвращатьСтатусПрервано = Ложь, 
		Знач ТолькоСтатус = Ложь) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСервисРаспознаванияДокументовДляЗаполненияКарточек") Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	Если Не ЗначениеЗаполнено(ДокументСсылка) Тогда 
		Возврат Неопределено;	
	КонецЕсли;
	 
	Возврат РегистрыСведений.СостоянияРаспознаваемыхДокументов.ТекущийСтатусПоДокументу(ДокументСсылка, 
		ВозвращатьСтатусПрервано, ТолькоСтатус);
	
КонецФункции

// Возвращает информацию о статусе распознавания документа.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение:
//  Неопределено, Структура - Информация о распознавании документа:
// * СтатусРаспознавания - ПеречислениеСсылка.СтатусыРаспознаванияДокументов
// * ТребуетПроверки - Булево - 
Функция ИнформацияОРаспознаванииДокумента(ДокументСсылка) Экспорт
	
	ИнформацияОСтатусе = ТекущаяИнформацияОСтатусеРаспознаванияДокумента(ДокументСсылка);

	Если ИнформацияОСтатусе = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТребуетПроверки = ТребуетПроверки(ДокументСсылка);
	
	СтруктураВозврата = Новый Структура("СтатусРаспознавания, ТребуетПроверки", ИнформацияОСтатусе.СтатусРаспознавания, 
		ТребуетПроверки);
	
	Возврат СтруктураВозврата;
	
КонецФункции

// // Возвращает историю распознавания документа.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение:
//  ТаблицаЗначений
Функция ИсторияРаспознаванияДокумента(ДокументСсылка) Экспорт
	
	Возврат РегистрыСведений.СостоянияРаспознаваемыхДокументов.ИсторияРаспознавания(ДокументСсылка);
	
КонецФункции

// Возвращает признак необходимости проверки.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
// Возвращаемое значение:
//  Булево
Функция ТребуетПроверки(ДокументСсылка) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСервисРаспознаванияДокументовДляЗаполненияКарточек") Тогда
		Возврат Ложь;
	КонецЕсли;
		
	ТребуетПроверки = Ложь;
	
	ТребуетПроверки = РегистрыСведений.ВерификацияРеквизитовРаспознанныхДокументов.
			НеобходимаПроверкаРеквизитов(ДокументСсылка) Или 
			РегистрыСведений.ВерификацияРеквизитовТЧРаспознанныхДокументов.
			НеобходимаПроверкиРеквизитовТабличныхЧастей(ДокументСсылка);
				
	Возврат ТребуетПроверки;
	
КонецФункции

// Прерывает процесс распознавания документа.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
//  ОписаниеОшибки - Строка
Процедура ПрерватьРаспознаваниеДокумента(ДокументСсылка, ОписаниеОшибки = "") Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		РегистрыСведений.ФайлыДокументовНаРаспознавании.УдалитьЗаписьПоДокументу(ДокументСсылка);
		РегистрыСведений.ВерификацияРеквизитовРаспознанныхДокументов.УдалитьДанныеДокумента(ДокументСсылка);
		РегистрыСведений.ВерификацияРеквизитовТЧРаспознанныхДокументов.УдалитьДанныеДокумента(ДокументСсылка);
		РегистрыСведений.СостоянияРаспознаваемыхДокументов.ДобавитьЗапись(ДокументСсылка, 
			Перечисления.СтатусыРаспознаванияДокументов.Прервано);
			
		Заголовок = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Заголовок");
		
		Если Не ПустаяСтрока(Заголовок) Тогда
			ЗаголовокДокумента = Заголовок; 
		Иначе
			ЗаголовокДокумента = НСтр("ru = '<Распознавание документа прервано>'", 
				ОбщегоНазначения.КодОсновногоЯзыка());
		КонецЕсли;
		
		ЗначенияРеквизитов = Новый Соответствие; 
		ЗначенияРеквизитов.Вставить("СтатусРаспознавания", Неопределено);
		ЗначенияРеквизитов.Вставить("Заголовок", ЗаголовокДокумента);
		
		Делопроизводство.ЗаписатьДанныеДокумента(ДокументСсылка, ЗначенияРеквизитов);
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ИнформацияОбОшибке = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Не удалось прервать процесс распознавания: ""%1""'", 
			ОбщегоНазначения.КодОсновногоЯзыка()), ИнформацияОбОшибке);
		
	КонецПопытки;
	
КонецПроцедуры

// Устанавливает статус распознавания документа.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
//  Статус - ПеречислениеСсылка.СтатусыРаспознаванияДокументов
//  Заголовок - строка
//  ТребуетПроверки -Булево
Процедура УстановитьСтатусРаспознаванияДокумента(ДокументСсылка, Знач Статус, Знач Заголовок="", 
		Знач ТребуетПроверки = Неопределено) Экспорт
	
	РегистрыСведений.СостоянияРаспознаваемыхДокументов.ДобавитьЗапись(ДокументСсылка, Статус); 
	
	ЗначенияРеквизитов = Новый Соответствие; 
	ЗначенияРеквизитов.Вставить("СтатусРаспознавания", Статус);
	
	Если ТребуетПроверки <> Неопределено Тогда
		ЗначенияРеквизитов.Вставить("ТребуетПроверки", ТребуетПроверки);
	КонецЕсли;
	
	Если Статус = Перечисления.СтатусыРаспознаванияДокументов.НаРаспознавании Тогда
		
		Если Не ПустаяСтрока(Заголовок) Тогда
			
			ЗаголовокДокумента = СтрШаблон(
	 			НСтр("ru = '<%1 - Выполняется распознавание...>'", 
	 			ОбщегоНазначения.КодОсновногоЯзыка()),Заголовок);

		Иначе
			
			ЗаголовокДокумента = НСтр("ru = '<Документ не сформирован. Выполняется распознавание...>'", 
				ОбщегоНазначения.КодОсновногоЯзыка());
				
		КонецЕсли;
		
		ЗначенияРеквизитов.Вставить("Заголовок", ЗаголовокДокумента);
	
	ИначеЕсли Не ПустаяСтрока(Заголовок) Тогда
			
		ЗаголовокДокумента = СтрШаблон(
			НСтр("ru = '%1'", ОбщегоНазначения.КодОсновногоЯзыка()),Заголовок); 

		ЗначенияРеквизитов.Вставить("Заголовок", ЗаголовокДокумента);
			
	КонецЕсли; 
	
	Делопроизводство.ЗаписатьДанныеДокумента(ДокументСсылка, ЗначенияРеквизитов);
	
КонецПроцедуры

// Проверяет возможность запуска распознавания документов
// 
// Параметры:
//  ПараметрыРаспознаванияДокументов - Структура
// 
// Возвращаемое значение:
//  Булево - Возможен запуск распознавания документов
Функция ВозможенЗапускРаспознаванияДокументов(Знач ПараметрыРаспознаванияДокументов) Экспорт
	
	Если Не ПараметрыРаспознаванияДокументов.ИспользованиеДоступно Тогда
		Возврат Ложь;
	КонецЕсли;
	

	Возврат Истина;
	
КонецФункции

// Инициализирует и возвращает структуру параметров распознавания
// 
// Возвращаемое значение:
//  Структура - Параметры распознавания документов:
// * ИспользованиеДоступно - Булево 
// * СобытиеДляЖурналаРегистрации - Строка 
// * КоличествоДокументовНаРаспознавании - Число
Функция ПараметрыРаспознаванияДокументов() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("ИспользованиеДоступно", Ложь);
	Параметры.Вставить("СобытиеДляЖурналаРегистрации","");
	Параметры.Вставить("КоличествоДокументовНаРаспознавании",0);
	
	Параметры.ИспользованиеДоступно = ИспользованиеДоступно();
	Параметры.СобытиеДляЖурналаРегистрации = НСтр("ru = 'Распознавание документов'", 
		ОбщегоНазначения.КодОсновногоЯзыка());
	Параметры.КоличествоДокументовНаРаспознавании =
		РегистрыСведений.ФайлыДокументовНаРаспознавании.КоличествоДокументовНаРаспознавании();
	
	Возврат Параметры;
	
КонецФункции

// Возобновляет распознавание файла документа.
// 
// Параметры:
//  Файл - СправочникСсылка.ВерсииФайлов
Процедура ВозобновитьРаспознаваниеФайлаДокумента(Файл) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		
		РегистрыСведений.ФайлыДокументовНаРаспознавании.ЗаблокироватьФайл(Файл);
		Владелец = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Файл, "Владелец");
		ДокументСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "ВладелецФайла");		
		РегистрыСведений.ФайлыДокументовНаРаспознавании.СброситьОшибку(Файл);
		РегистрыСведений.СостоянияРаспознаваемыхДокументов.ДобавитьЗапись(ДокументСсылка, 
			Перечисления.СтатусыРаспознаванияДокументов.НаРаспознавании);
		РегистрыСведений.ВерификацияРеквизитовРаспознанныхДокументов.УдалитьДанныеДокумента(ДокументСсылка);
		РегистрыСведений.ВерификацияРеквизитовТЧРаспознанныхДокументов.УдалитьДанныеДокумента(ДокументСсылка);
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Заполняет необходимые  параметры для корректной записи распознаваемого документа
// 
// Параметры:
//  ДокументОбъект - СправочникОбъект.ДокументыПредприятия
//  ОбходПроверокПоВидуДокумента - Булево
Процедура ПередЗаписьюРаспознаваемогоДокумента(ДокументОбъект, ОбходПроверокПоВидуДокумента = Ложь) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСервисРаспознаванияДокументовДляЗаполненияКарточек") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументОбъект.ВидДокумента) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ДополнительныеСвойства = ДокументОбъект.ДополнительныеСвойства;
	
	//устанавливается при загрузке данных если вид документа не распознан
	Если ДополнительныеСвойства.Свойство("ДокументНеРаспознан") Тогда
			ОбходПроверокПоВидуДокумента = Истина;
		Возврат;
	КонецЕсли;
	
	//запись пустой карточки при отправке на распознавание
	Если ДополнительныеСвойства.Свойство("ДокументОтправленНаРаспознавание") Тогда
			ОбходПроверокПоВидуДокумента = ДополнительныеСвойства.ДокументОтправленНаРаспознавание;
		Возврат; // признак установлен в форме
	КонецЕсли;
	
	ОбходПроверокПоВидуДокумента = ДокументНаРаспознавании(ДокументОбъект);
	
	ЭтоОбъектЭтогоУзла = ОбщегоНазначенияДокументооборот.ОбъектЭтогоУзла(ДокументОбъект.Ссылка);
	
	ПометкаУдаленияСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.Ссылка,"ПометкаУдаления");
	
	Если ДокументОбъект.ПометкаУдаления Тогда
		// изменена пометка на удаление у документа без вида документа - отключим проверки
		СтатусРаспознавания = РегистрыСВедений.СостоянияРаспознаваемыхДокументов.ТекущийСтатусПоДокументу(
			ДокументОбъект.Ссылка, Истина, Истина);
				
		ДокументОбъект.ДополнительныеСвойства.Вставить("ПометкаУдаления", Истина);
		
		Если СтатусРаспознавания = Перечисления.СтатусыРаспознаванияДокументов.Распознан Тогда
				
				Если ЭтоОбъектЭтогоУзла = Ложь Тогда
					ВызватьИсключение(НСтр("ru = 'Пометка на удаление не распознанных документов, 
					|верификация которых не выполнена, 
					|доступна только в узле создания!'"));
				КонецЕсли;
				
				// Пометка на удаление - распознавание завершено, вид документа не распознан
				ДокументОбъект.ОбменДанными.Загрузка = Истина;
				ДокументОбъект.ДополнительныеСвойства.Вставить("ДокументНеРаспознан", Истина);

		ИначеЕсли СтатусРаспознавания = Перечисления.СтатусыРаспознаванияДокументов.НаРаспознавании Тогда 
		
			Если ЭтоОбъектЭтогоУзла = Ложь Тогда
				ВызватьИсключение(НСтр("ru = 'Пометка на удаление документов, находящихся на распознавании, 
				|доступна только в узле создания!'"));
			КонецЕсли;
		
			ДокументОбъект.ОбменДанными.Загрузка = Истина;
						
			РегистрыСведений.ФайлыДокументовНаРаспознавании.УдалитьЗаписьПоДокументу(ДокументОбъект.Ссылка);
			РегистрыСведений.СостоянияРаспознаваемыхДокументов.УдалитьЗаписьПоДокументу(ДокументОбъект.Ссылка);
			ДополнительныеСвойства.Вставить("СтатусРаспознавания", 
				Перечисления.СтатусыРаспознаванияДокументов.ПустаяСсылка());
				
		Иначе
			
			ОбходПроверокПоВидуДокумента = Истина;
			//Для обхода всех остальных проверок по виду документа
			ДокументОбъект.ДополнительныеСвойства.Вставить("ДокументОтправленНаРаспознавание", Истина);
			
		КонецЕсли;
		
	ИначеЕсли ДокументОбъект.ПометкаУдаления <> ПометкаУдаленияСсылка Тогда //снятие пометки
		
		// для обнуления статуса распознавания в списке
		ДокументОбъект.ДополнительныеСвойства.Вставить("ПометкаУдаления", Истина);
		// для обхода проверок в модуле документа
		ОбходПроверокПоВидуДокумента = Истина;
		//Для обхода всех остальных проверок по виду документа
		ДокументОбъект.ДополнительныеСвойства.Вставить("ДокументОтправленНаРаспознавание", Истина);
			
	КонецЕсли;
	
КонецПроцедуры

// Выполняет запись реквизитов распознаваемого документа
// 
// Параметры:
//  ДокументОбъект - СправочникОбъект.ДокументыПредприятия
//  Отказ - Булево
Процедура ПриЗаписиРаспознаваемогоДокумента(ДокументОбъект, Отказ = Ложь) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСервисРаспознаванияДокументовДляЗаполненияКарточек") Тогда
		Возврат;
	КонецЕсли;
	
	ДопСвойства = ДокументОбъект.ДополнительныеСвойства;
	
	ДанныеВерификации = Неопределено;
	
	Если ДопСвойства.Свойство("ДанныеВерификации", ДанныеВерификации) Тогда
		
		Если ДанныеВерификации <> Неопределено Тогда
			
			ДоступнаВерификацияПоРоли = Пользователи.РолиДоступны("ПроверкаРаспознанныхДокументов");
			
			Если ДоступнаВерификацияПоРоли = Ложь Тогда
				
				ТекстОшибки = 
				НСтр("ru = 'Верификация доступна только пользователю с ролью ""Проверка распознанных документов"".'",
					ОбщегоНазначения.КодОсновногоЯзыка());
					
				ВызватьИсключение(ТекстОшибки);
				
			КонецЕсли;
			
			ПодготовитьДанныеВерификацииДляЗаписи(ДанныеВерификации);
			ЗаписатьВерифицированныеРеквизитыДокумента(ДокументОбъект.Ссылка, ДанныеВерификации, Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументОбъект.ВидДокумента) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗаписьРеквизитовДокументаСОбходомПроверок(ДокументОбъект) Тогда
		
		ОписаниеОшибки = "";
		
		РегистрыСведений.ДанныеДокументовПредприятия.ЗаписатьРеквизитыРаспознаваемогоДокумента(ДокументОбъект, 
			ОписаниеОшибки);
			
		Если Не ПустаяСтрока(ОписаниеОшибки) Тогда
			Отказ = Истина;
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка, , ,
				ОписаниеОшибки);
				
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Отправляет файл документа на распознавание.
// 
// Параметры:
//  Файл - СправочникСсылка.Файлы
Процедура ОтправитьФайлДокументаНаРаспознавание(Файл) Экспорт
	
	ПараметрыРаспознаванияДокументов = ПараметрыРаспознаванияДокументов();
	
	Если Не ПараметрыРаспознаванияДокументов.ИспользованиеДоступно Тогда
		Возврат;
	КонецЕсли;
	
	МаксимальныйРазмерОчереди = МаксимальныйРазмерОчередиДокументовНаРаспознавании();
	
	Если ПараметрыРаспознаванияДокументов.КоличествоДокументовНаРаспознавании > МаксимальныйРазмерОчереди Тогда
		
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Размер очереди документов на распознавании превышает максимальный размер: %1'", 
				ОбщегоНазначения.КодОсновногоЯзыка()), МаксимальныйРазмерОчереди);
				
		ЗаписьЖурналаРегистрации(ПараметрыРаспознаванияДокументов.СобытиеДляЖурналаРегистрации, 
			УровеньЖурналаРегистрации.Ошибка, , ,
			ТекстСообщения);
			
		ВызватьИсключение(ТекстСообщения);
		
	КонецЕсли;
	
	СостоянияПоБалансу = СостоянияПоБалансу();
	
	Если Не (СостоянияПоБалансу.СостояниеБаланса = "БалансПоложительный" 
			Или СостоянияПоБалансу.СостояниеБаланса = "БалансПоложительныйДатаОкончания") Тогда
			
		ТекстСообщения = НСтр("ru = 'Баланс страниц распознавания нулевой'", 
				ОбщегоНазначения.КодОсновногоЯзыка());
			
		ЗаписьЖурналаРегистрации(ПараметрыРаспознаванияДокументов.СобытиеДляЖурналаРегистрации, 
			УровеньЖурналаРегистрации.Ошибка, , ,
			ТекстСообщения);	
			
		ВызватьИсключение(ТекстСообщения);
				
	КонецЕсли;
	
	Если СостоянияПоБалансу.СостояниеБаланса = "БалансПоложительныйДатаОкончания" 
		И СостоянияПоБалансу.ДатаОкончания < ТекущаяДатаСеанса() Тогда 
		
		ТекстСообщения = НСтр("ru = 'Истек срок действия тарифа'", 
				ОбщегоНазначения.КодОсновногоЯзыка());
			
		ЗаписьЖурналаРегистрации(ПараметрыРаспознаванияДокументов.СобытиеДляЖурналаРегистрации, 
			УровеньЖурналаРегистрации.Ошибка, , ,
			ТекстСообщения);
			
		ВызватьИсключение(ТекстСообщения);
			
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗначРеквизитовФайла = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Файл, 
		"ТекущаяВерсия, ВладелецФайла, ПометкаУдаления, ВладелецФайла.ПометкаУдаления");
	
	Если ЗначРеквизитовФайла.ПометкаУдаления ИЛИ ЗначРеквизитовФайла.ВладелецФайлаПометкаУдаления = Истина Тогда
		
		ТекстСообщения = СтрШаблон(
								 НСтр("ru = 'Файл, или владелец файла помечен на удаление ""%1"".'", 
								 	ОбщегоНазначения.КодОсновногоЯзыка()), 
										ПолучитьНавигационнуюСсылку(ЗначРеквизитовФайла.ТекущаяВерсия));
		ЗаписьЖурналаРегистрации(ПараметрыРаспознаванияДокументов.СобытиеДляЖурналаРегистрации, 
			УровеньЖурналаРегистрации.Ошибка, , ,
			ТекстСообщения);
			
		ВызватьИсключение(ТекстСообщения);
		
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		РегистрыСведений.ФайлыДокументовНаРаспознавании.ДобавитьФайл(ЗначРеквизитовФайла.ТекущаяВерсия);
		УстановитьСтатусРаспознаванияДокумента(ЗначРеквизитовФайла.ВладелецФайла, 
			Перечисления.СтатусыРаспознаванияДокументов.НаРаспознавании);
			
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();

		ОписаниеОшибкиИнфо = ОписаниеОшибки();
		ТекстСообщения = СтрШаблон(
								 НСтр("ru = 'Не удалось отправить файл документа на распознавание ""%1"".'", 
								 	ОбщегоНазначения.КодОсновногоЯзыка()), 
										ПолучитьНавигационнуюСсылку(ЗначРеквизитовФайла.ТекущаяВерсия));
		ТекстСообщения = ТекстСообщения + Строка(ОписаниеОшибкиИнфо);
		
		ЗаписьЖурналаРегистрации(ПараметрыРаспознаванияДокументов.СобытиеДляЖурналаРегистрации, 
			УровеньЖурналаРегистрации.Ошибка, , ,
			ТекстСообщения);
			
		ВызватьИсключение ТекстСообщения;
			
	КонецПопытки;
		
КонецПроцедуры

// Обновляет полномочия ролями для выполнения распознавания документов
Процедура ОбновитьПолномочия() Экспорт
	
	УправлениеДоступомДокументооборот.ДобавитьРольВПолномочия(
		"ПроверкаРаспознанныхДокументов", 
		"eeb325ab-db51-11de-a544-00179ab398dc");
	УправлениеДоступомДокументооборот.ДобавитьРольВПолномочия(
		"РаспознаваниеДокументов", 
		"eeb325ab-db51-11de-a544-00179ab398dc");
		
КонецПроцедуры

// Соответствие видов распознаваемых документов.
// 
// Возвращаемое значение:
//  Соответствие  из КлючИЗначение:
//   * Ключ - Строка.
//   * Значение - ПеречислениеСсылка.ТипыФормализованныхДокументов
Функция СоответствиеВидовРаспознаваемыхДокументов() Экспорт
	
	СоответствиеТипов = Новый Соответствие;
	СоответствиеТипов.Вставить("ACT", Перечисления.ТипыФормализованныхДокументов.АктОбОказанииУслуг);
	СоответствиеТипов.Вставить("SCH", Перечисления.ТипыФормализованныхДокументов.СчетНаОплату);
	СоответствиеТипов.Вставить("SF", Перечисления.ТипыФормализованныхДокументов.СчетФактура);
	СоответствиеТипов.Вставить("TG12", Перечисления.ТипыФормализованныхДокументов.ТОРГ12);
	СоответствиеТипов.Вставить("UPD", Перечисления.ТипыФормализованныхДокументов.УПД);
	
	Возврат СоответствиеТипов;
	
КонецФункции

// Документ на распознавании.
// 
// Параметры:
//  Документ - СправочникОбъект.ДокументыПредприятия
//  СправочникОбъект.ДокументыПредприятия
// 
// Возвращаемое значение:
//  Булево - Документ на распознавании
Функция ДокументНаРаспознавании(Документ) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСервисРаспознаванияДокументовДляЗаполненияКарточек") Тогда
		Возврат Ложь;
	КонецЕсли;

	ЭтоОбъект = ((ТипЗнч(Документ) = Тип("СправочникОбъект.ДокументыПредприятия")));

	Если ЭтоОбъект И Документ.ДополнительныеСвойства.Свойство("ДокументНеРаспознан") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ЭтоОбъект И Документ.ДополнительныеСвойства.Свойство("ДокументОтправленНаРаспознавание")Тогда
		Возврат Истина;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Документ.Ссылка) Тогда
		
		НаРаспознавании = РегистрыСВедений.СостоянияРаспознаваемыхДокументов.
			ДокументНаходитсяНаРаспознавании(Документ.Ссылка);
			
		Если НаРаспознавании И ЭтоОбъект Тогда
			
			// добавим признак в доп. свойства, чтобы при повторном вызове не было обращения к регистру 
			Документ.ДополнительныеСвойства.Вставить("ДокументОтправленНаРаспознавание", Истина);
			
		КонецЕсли; 
		
		Возврат НаРаспознавании;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция НоваяНоменклатураКонтрагентаПоРаспознаннымДанным(ДанныеСтрокиТаблицы, Владелец) Экспорт
	
	НоваяНоменклатураКонтрагента = 
		СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента(Владелец);
		
	СоставКлючевыхРеквизитов = Новый Массив;
	
	Для Каждого Реквизит Из ДанныеСтрокиТаблицы Цикл
		
		Если ПустаяСтрока(Реквизит.РаспознанныйТекст) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Реквизит.ИмяРеквизита = "Номенклатура" Тогда
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Реквизит, "РаспознанноеЗначение")
				И ЗначениеЗаполнено(Реквизит.РаспознанноеЗначение) 
					И Реквизит.УверенностьРаспознанногоЗначения = 100 Тогда
				Возврат Неопределено; // уже найдена
			ИначеЕсли Не ПустаяСтрока(Реквизит.РаспознанныйТекст) Тогда
				НоваяНоменклатураКонтрагента.Наименование = Реквизит.РаспознанныйТекст;
				СоставКлючевыхРеквизитов.Добавить("Наименование");
			Иначе
				Продолжить;
			КонецЕсли;
			
			Продолжить;
			
		КонецЕсли;
		
		Если Реквизит.ИмяРеквизита = "Артикул" Тогда
			
			НоваяНоменклатураКонтрагента.Артикул = Реквизит.РаспознанныйТекст;
			СоставКлючевыхРеквизитов.Добавить("Артикул");
			
			Продолжить;
			
		КонецЕсли;
		
		Если Реквизит.ИмяРеквизита = "ЕдиницаИзмерения" Тогда
		
			НоваяНоменклатураКонтрагента.ЕдиницаИзмерения = Реквизит.РаспознанныйТекст;
			СоставКлючевыхРеквизитов.Добавить("ЕдиницаИзмерения");
			
			Продолжить;
		
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПустаяСтрока(НоваяНоменклатураКонтрагента.Наименование) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НоваяНоменклатураКонтрагента.Идентификатор = СопоставлениеНоменклатурыКонтрагентов.
		ИдентификаторНоменклатурыКонтрагентаПоНатуральнымКлючам(НоваяНоменклатураКонтрагента, СоставКлючевыхРеквизитов);
		
	Возврат НоваяНоменклатураКонтрагента;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Авторизация

// Инициализирует структуру параметров авторизации.
// 
// Возвращаемое значение:
//  Структура - Новые параметры авторизации:
// * ИдентификаторИБ - Строка - 
// * ТокенДоступа - Строка - 
// * Состояние - Строка - 
// * ТипАутентификации - Строка - 
// * Логин - Строка - 
Функция НовыеПараметрыАвторизации()
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторИБ", "");
	Результат.Вставить("ТокенДоступа", "");
	Результат.Вставить("Состояние", "Ожидает");
	Результат.Вставить("ТипАутентификации", "НеВыполнена"); // ПоТикетуИТС, ПоЛогинуПаролю
	Результат.Вставить("Логин", "");
	
	Возврат Результат;
	
КонецФункции

// Записывает параметры авторизации.
// 
// Параметры:
//  Параметры - Структура, Произвольный, Неопределено - Параметры:
// * ИдентификаторИБ - Строка - 
// * ТокенДоступа - Строка - 
// * Состояние - Строка - 
// * ТипАутентификации - Строка - 
// * Логин - Строка - 
Процедура ЗаписатьПараметрыАвторизации(Параметры)
	
	ИдентификаторПодсистемы = ИдентификаторПодсистемы();
	НачатьТранзакцию();
	Попытка
		ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(ИдентификаторПодсистемы);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
			ИдентификаторПодсистемы,
			Параметры.ИдентификаторИБ,
			"ИдентификаторИБ");
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
			ИдентификаторПодсистемы,
			Параметры.ТокенДоступа,
			"ТокенДоступа");
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
			ИдентификаторПодсистемы,
			Параметры.Состояние,
			"Состояние");
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
			ИдентификаторПодсистемы,
			Параметры.ТипАутентификации,
			"ТипАутентификации");
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
			ИдентификаторПодсистемы,
			Параметры.Логин,
			"Логин");
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	ПараметрыАвторизации = Новый Структура;
	ПараметрыАвторизации.Вставить("Состояние", Параметры.Состояние);
	ПараметрыАвторизации.Вставить("ТипАутентификации", Параметры.ТипАутентификации);
	
КонецПроцедуры

// Проверяет выполнена ли авторизация.
// 
// Параметры:
//  ПараметрыАвторизации - Структура, Произвольный, Неопределено - Параметры авторизации:
// * ИдентификаторИБ - Строка - 
// * ТокенДоступа - Строка - 
// * Состояние - Строка - 
// * ТипАутентификации - Строка - 
// * Логин - Строка - 
// 
// Возвращаемое значение:
//  Булево - Авторизация выполнена
Функция АвторизацияВыполнена(ПараметрыАвторизации)
	
	Возврат ПараметрыАвторизации.ТипАутентификации <> "НеВыполнена";
	
КонецФункции

// Проверяет выполнена ли активация.
// 
// Параметры:
//  ПараметрыАвторизации - Структура, Произвольный, Неопределено - Параметры авторизации:
// * ИдентификаторИБ - Строка - 
// * ТокенДоступа - Строка - 
// * Состояние - Строка - 
// * ТипАутентификации - Строка - 
// * Логин - Строка - 
// 
// Возвращаемое значение:
//  Булево - Активация выполнена
Функция АктивацияВыполнена(ПараметрыАвторизации)
	
	Возврат ПараметрыАвторизации.ТипАутентификации = "ПоЛогинуПаролю"
		Или ПараметрыАвторизации.Состояние = "Активирован";
	
КонецФункции

// Возвращает параметры области авторизации
// 
// Возвращаемое значение:
//  Структура - Область авторизации:
// * Конфигурация - Строка - 
// * ВерсияКонфигурации - Строка - 
// * ИнформацияМоделиСервиса - Структура - :
// ** РазделениеВключено - Булево - 
// ** ДоступноИспользованиеРазделенныхДанных - Булево - 
// ** ЗначениеРазделителяСеанса - Число - 
// ** АбонентЭтогоПриложения - Строка - 
// ** АдресПриложения - Строка - 
// * ИнформацияИТС - Структура - :
// ** ЛогинПользователяИТС - Строка - 
// * ТекущаяУниверсальнаяДата - Строка - 
// * ТипАутентификации - Строка - 
// * ИдентификаторИнформационнойБазы - Строка, Произвольный - 
Функция ОбластьАвторизации()
	
	Попытка
		
		АбонентЭтогоПриложения = ПрограммныйИнтерфейсСервиса.АбонентЭтогоПриложения().Наименование;
		
	Исключение
		
		АбонентЭтогоПриложения = НСтр("ru = '<Неопределено>'", ОбщегоНазначения.КодОсновногоЯзыка());
				ТекстОшибки = НСтр("ru = 'Не удалось получить абонента приложения:
															|%1'", ОбщегоНазначения.КодОсновногоЯзыка());
				ИнформацияОбОшибке = ИнформацияОбОшибке();
				ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
				
				ПолныйТекстОшибки = СтрШаблон(ТекстОшибки, ИнформацияОбОшибке);
				
				ЗаписьЖурналаРегистрации(
					СобытиеЖурналаРегистрации(),
					УровеньЖурналаРегистрации.Ошибка, , ,
					ПолныйТекстОшибки);	
						
	КонецПопытки;
	
	Попытка
		
		ЗначениеРазделителяСеанса = РаботаВМоделиСервиса.ЗначениеРазделителяСеанса();
		
		АдресПриложения = ПрограммныйИнтерфейсСервиса.СвойстваПриложения(ЗначениеРазделителяСеанса).АдресПриложения;
			
	Исключение
		
		АдресПриложения = "";
		
		ТекстОшибки = НСтр("ru = 'Не удалось получить адрес приложения:%1'", ОбщегоНазначения.КодОсновногоЯзыка());
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		
		ПолныйТекстОшибки = СтрШаблон(ТекстОшибки, ИнформацияОбОшибке);
		
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка, , ,
			ПолныйТекстОшибки);
			
	КонецПопытки;
	
	ИнформацияМоделиСервиса = Новый Структура;
	ИнформацияМоделиСервиса.Вставить("РазделениеВключено", РаботаВМоделиСервиса.РазделениеВключено());
	ИнформацияМоделиСервиса.Вставить("ДоступноИспользованиеРазделенныхДанных", РаботаВМоделиСервиса.
		ДоступноИспользованиеРазделенныхДанных());
	ИнформацияМоделиСервиса.Вставить("ЗначениеРазделителяСеанса", РаботаВМоделиСервиса.ЗначениеРазделителяСеанса());
	ИнформацияМоделиСервиса.Вставить("АбонентЭтогоПриложения", АбонентЭтогоПриложения);
	ИнформацияМоделиСервиса.Вставить("АдресПриложения", АдресПриложения);
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеАутентификации = РаспознаваниеЛокализация.ДанныеАутентификацииПользователяИнтернетПоддержки();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ДанныеАутентификации <> Неопределено Тогда
		ЛогинПользователяИТС  = ДанныеАутентификации.Логин;
	Иначе
		ЛогинПользователяИТС = НСтр("ru = '<Неопределено>'", ОбщегоНазначения.КодОсновногоЯзыка());
	КонецЕсли;
	
	ИнформацияИТС = Новый Структура;
	ИнформацияИТС.Вставить("ЛогинПользователяИТС", ЛогинПользователяИТС);
	
	Результат = Новый Структура;
	Результат.Вставить("Конфигурация", Метаданные.Имя);
	Результат.Вставить("ВерсияКонфигурации", Метаданные.Версия);
	Результат.Вставить("ИнформацияМоделиСервиса", ИнформацияМоделиСервиса);
	Результат.Вставить("ИнформацияИТС", ИнформацияИТС);
	Результат.Вставить("ТекущаяУниверсальнаяДата", ЗаписатьДатуJSON(ТекущаяУниверсальнаяДата(), ФорматДатыJSON.ISO));
	Результат.Вставить("ТипАутентификации", НСтр("ru = '<Неопределено>'", ОбщегоНазначения.КодОсновногоЯзыка()));
	Результат.Вставить("ИдентификаторИнформационнойБазы", СтандартныеПодсистемыСервер.ИдентификаторИнформационнойБазы());
	
	Возврат Результат;
	
КонецФункции

// Проверяет доступность тестового периода
// 
// Возвращаемое значение:
//  Булево - Подключение тестового периода доступно
Функция ПодключениеТестовогоПериодаДоступно()
	
	ИдентификаторСервиса = РаспознаваниеКлиентСервер.ИдентификаторСервиса();
	Идентификаторы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторСервиса);
	ОписательДоступности = РаспознаваниеЛокализация.ДоступноПодключениеТестовогоПериода(Идентификаторы);
	Если Не ОписательДоступности.Ошибка Тогда
		Возврат ОписательДоступности.СервисыСопровождения[ИдентификаторСервиса] = "Доступно";
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ЗагрузкаВыгрузка

// Выполнить выгрузку файла в сервис распознавания документов.
// 
// Параметры:
//  ПараметрыФайла - СтрокаТаблицыЗначений - Параметры файла
Процедура ВыполнитьВыгрузкуФайлаВСервисРаспознаванияДокументов(Знач ПараметрыФайла)
	
	Если ПустаяСтрока(ПараметрыФайла.ИдентификаторЗадания) Тогда
		
		ИменаФайловДляРаспознавания = Новый Массив;
		ДатаСоздания = ТекущаяУниверсальнаяДата();
		ИменаФайловДляРаспознавания.Добавить(ПараметрыФайла.Файл.Наименование + "." + ПараметрыФайла.Файл.Расширение);
		ИдентификаторЗадания = РаспознаваниеДокументовSDK.СоздатьЗаданиеРаспознавания(ИменаФайловДляРаспознавания, 
			ДатаСоздания);
			
	Иначе
		
		ИдентификаторЗадания = ПараметрыФайла.ИдентификаторЗадания;
		
	КонецЕсли;
	
	Результат = РаспознаваниеДокументовSDK.ПолучитьСостояниеОбработкиЗадания(ИдентификаторЗадания);
	
	Если Результат.ОбработкаЗавершена Или Результат.АдресаЗагрузкиФайлов.Количество() = 0 Тогда
		
		ОписаниеОшибки = НСтр("ru = 'Произошла ошибка при создании задания.'", ОбщегоНазначения.КодОсновногоЯзыка());
		РегистрыСведений.ФайлыДокументовНаРаспознавании.ИзменитьСостояние(ПараметрыФайла.Файл, "", "", ОписаниеОшибки);
		Возврат;
		
	КонецЕсли;
	
	АдресЗагрузкиФайла = Результат.АдресаЗагрузкиФайлов[0];
		
		ИдентификаторФайла = РаспознаваниеДокументовSDK.ЗагрузкаФайлаПоАдресу(АдресЗагрузкиФайла,
			РаботаСФайлами.ДвоичныеДанныеФайла(ПараметрыФайла.Файл));
			
	РегистрыСведений.ФайлыДокументовНаРаспознавании.ИзменитьСостояние(ПараметрыФайла.Файл, ИдентификаторФайла, 
		ИдентификаторЗадания);
		
КонецПроцедуры

// Проверяет - загружены ли данные и запускает процедуру загрузки.
// 
// Параметры:
//  ПараметрыЗагрузки - СтрокаТаблицыЗначений
Процедура ВыполнитьЗагрузкуРезультатаРаспознавания(Знач ПараметрыЗагрузки)
			
	СостояниеЗадания = РаспознаваниеДокументовSDK.ПолучитьСостояниеОбработкиЗадания(
															ПараметрыЗагрузки.ИдентификаторЗадания);
	
	Если СостояниеЗадания.РаспознанныеДокументы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗадания = ПараметрыЗагрузки.ИдентификаторЗадания;
	Файл = ПараметрыЗагрузки.Файл;
	
	Владелец = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Файл, "Владелец");
	ДокументСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "ВладелецФайла");
	
	Для Каждого РаспознанныйДокумент Из СостояниеЗадания.РаспознанныеДокументы Цикл
		
		ИдентификаторДокумента = РаспознанныйДокумент.ИдентификаторДокумента;
		
		ДанныеУжеЗагружены = РегистрыСведений.РезультатыРаспознаванияДокументов.ДанныеУжеЗагружены(
			ИдентификаторДокумента);
		
		Если ДанныеУжеЗагружены Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыЗагрузки = Новый Структура;
		ПараметрыЗагрузки.Вставить("ИдентификаторДокумента", РаспознанныйДокумент.ИдентификаторДокумента);
		ПараметрыЗагрузки.Вставить("ИдентификаторЗадания", ИдентификаторЗадания);
		ПараметрыЗагрузки.Вставить("Документ", ДокументСсылка);
		ПараметрыЗагрузки.Вставить("Файл", Файл);
		
		ЗагрузкаДанныхИЗаполнениеДокумента(ПараметрыЗагрузки);
		
		Прервать; // в текущей реализации на каждый файл один документ
		
	КонецЦикла;
		
КонецПроцедуры

// Загружает распознанные данные в регистры, подбирает значения из БД и заполняет документ
// 
// Параметры:
//  ПараметрыЗагрузки - Структура - Параметры заполнения:
// * ИдентификаторДокумента - Строка - 
// * ИдентификаторЗадания - Строка
// * Документ - СправочникСсылка.ДокументыПредприятия
// * Файл - СправочникСсылка.ВерсииФайлов
Процедура ЗагрузкаДанныхИЗаполнениеДокумента(Знач ПараметрыЗагрузки)
	
	РаспознанныеДанные = РезультатПоИдентификатору(ПараметрыЗагрузки.ИдентификаторДокумента);
	
	Если РаспознанныеДанные.ОписаниеОшибки <> "" Тогда
		
		РегистрыСведений.ФайлыДокументовНаРаспознавании.ЗафиксироватьОшибкуПриРаспознавании(ПараметрыЗагрузки.Файл, 
			РаспознанныеДанные.ОписаниеОшибки);
		Возврат;
		
	КонецЕсли;
	
	ДокументСсылка = ПараметрыЗагрузки.Документ;
	
	НачатьТранзакцию();
	
	ДокументРаспознан = Истина;
	
	Попытка
		
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить(Метаданные.Справочники.ДокументыПредприятия.ПолноеИмя());
		ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", ДокументСсылка);
		БлокировкаДанных.Заблокировать();
		
		РегистрыСведений.ФайлыДокументовНаРаспознавании.УдалитьФайл(ПараметрыЗагрузки.Файл);
		РегистрыСведений.СостоянияРаспознаваемыхДокументов.ДобавитьЗапись(ПараметрыЗагрузки.Документ, 
			Перечисления.СтатусыРаспознаванияДокументов.Распознан);
			
		Если РаспознанныеДанные.Загружен = Ложь Тогда
			РегистрыСведений.РезультатыРаспознаванияДокументов.ДобавитьЗапись(ДокументСсылка,
				ПараметрыЗагрузки.ИдентификаторДокумента, ПараметрыЗагрузки.ИдентификаторЗадания, 
				ПараметрыЗагрузки.Файл, РаспознанныеДанные);
		КонецЕсли;
	
		ОписаниеОшибки = "";
		
		ДокументРаспознан = Ложь;
		
		ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
		
		Если РаспознанныеДанные.ТипФормализованногоДокумента <> Неопределено Тогда 
			
			ДанныеДляЗаполнения = ПодготовитьДанныеДляЗаполнения(РаспознанныеДанные, 
				ОписаниеОшибки);
			
			Если Не ПустаяСтрока(ОписаниеОшибки) Тогда
				ВызватьИсключение(ОписаниеОшибки);
			КонецЕсли;
			
			ПодобратьЗначенияИЗаписатьРаспознанныеДанные(ПараметрыЗагрузки.Файл, ДокументСсылка,
				ДанныеДляЗаполнения);
				
			ДанныеДляЗаполнения.НаправлениеДокумента 
				= НаправлениеДокументаПоРаспознаннымДанным(ДанныеДляЗаполнения.ТаблицаРеквизитов);
			
			ЗаполнитьРеквизитыПоВидуДокумента(ДокументОбъект, ДанныеДляЗаполнения);
			
			ДокументРаспознан = Истина;
			
		КонецЕсли;
		
		Если ДокументРаспознан = Ложь Или Не ЗначениеЗаполнено(ДокументОбъект.ВидДокумента) Тогда
			// обход всех проверок
			ДокументОбъект.ДополнительныеСвойства.Вставить("ДокументНеРаспознан", Истина);
			ЗафиксироватьДокументНеРаспознан(ДокументСсылка);
			
		Иначе
			
			ДокументОбъект.ДополнительныеСвойства.Вставить("ЗаполнениеРаспознаваемогоДокумента", Истина);
			
		КонецЕсли;
		
		ДокументОбъект.Записать();
			
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = НСтр("ru = 'Не удалось заполнить документ: %1'", ОбщегоНазначения.КодОсновногоЯзыка());
			ОписаниеОшибки = СтрШаблон(ТекстОшибки, ОписаниеОшибки());
			
		РегистрыСведений.ФайлыДокументовНаРаспознавании.ЗафиксироватьОшибкуПриРаспознавании(ПараметрыЗагрузки.Файл, 
			ОписаниеОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеКарточкиДокумента

// Заполняет реквизиты документа распознанными данными.
// Контрагентов и организации подставляем только при уверенности = 100, нечеткий поиск (НП) используем только для 
// формирования списков выбора. Номенклатуру подставляем используя механизм сопоставления номенклатуры БЭД и НП
// 
// Параметры:
//  ДокументОбъект - Неопределено, СправочникОбъект.ДокументыПредприятия - Документ объект
//  ДанныеДляЗаполнения - Неопределено, Структура - Данные для заполнения:
// * ТаблицаРеквизитов - ТаблицаЗначений - :
// ** ИмяРеквизита - Строка - имя распознанного реквизита
// ** КоординатаX0 - Число - координаты распознанного реквизита на изображении
// ** КоординатаX1 - Число - см. КоординатаX0
// ** КоординатаY0 - Число - см. КоординатаX0
// ** КоординатаY1 - Число - см. КоординатаX0
// ** ОбластьИзображения - ХранилищеЗначения - область изображение по координатам
// ** РаспознанноеЗначение - ОпределяемыйТип.РаспознаваемыеРеквизитыДокумента
// ** РаспознанныйТекст - Строка - распознанный текст реквизита
// ** СтрокВИзображении - Число - количество строк в данных распознанного реквизита
// ** УверенностьРаспознанногоЗначения - Число - от 0 до 100 по методике Д-Винклера
// * ТаблицаРеквизитовТЧ - ТаблицаЗначений - :
// ** ИмяРеквизита - Строка - имя распознанного реквизита
// ** ИмяТаблицы - Строка - имя распознанной таблицы
// ** НомерСтрокиТаблицы - Число - номер строки распознанной таблицы от 1
// ** КоординатаX0 - Число - координаты распознанного реквизита на изображении
// ** КоординатаX1 - Число - см. КоординатаX0
// ** КоординатаY0 - Число - см. КоординатаX0
// ** КоординатаY1 - Число - см. КоординатаX0
// ** ОбластьИзображения - ХранилищеЗначения - область изображение по координатам
// ** РаспознанноеЗначение - ОпределяемыйТип.РаспознаваемыеРеквизитыДокумента
// ** РаспознанныйТекст - Строка - распознанный текст реквизита
// ** СтрокВИзображении - Число - количество строк в данных распознанного реквизита
// ** УверенностьРаспознанногоЗначения - Число
// * ТипФормализованногоДокумента - Неопределено, ПеречислениеСсылка.ТипыФормализованныхДокументов - тип расп. док-та 
// * НаправлениеДокумента - Неопределено, ПеречислениеСсылка.НаправленияЭДО - направление распознанного док-та
// * ВидДокументаКеш - Неопределено, Структура - Кеш вида документа ДО
Процедура ЗаполнитьРеквизитыПоВидуДокумента(ДокументОбъект, ДанныеДляЗаполнения)
	
	ПорогУверенностиДляПодстановки = РаспознаваниеДокументовКлиентСервер.ПорогУверенностиДляПодстановки();
	
	ДанныеВидДокумента = ВидФормализованногоДокумента(ДанныеДляЗаполнения.ТипФормализованногоДокумента);
		
	Если Не ЗначениеЗаполнено(ДанныеВидДокумента.ВидДокумента) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеВидДокумента.ШаблонДокумента) Тогда
		ДанныеЗаполнения = Новый Структура;
		ДанныеЗаполнения.Вставить("ШаблонДокумента", ДанныеВидДокумента.ШаблонДокумента);
		ДокументОбъект.ОбработкаЗаполнения(ДанныеЗаполнения, Истина);
	КонецЕсли;
	
	ДокументОбъект.ВидДокумента = ДанныеВидДокумента.ВидДокумента;
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОбъект.ВидДокумента, 
		"ВестиУчетПоОрганизациям,
		| ВестиУчетПоКонтрагентам, ВестиУчетСторон, ВестиУчетТоваровИУслуг, ВестиУчетПоТематикам, 
		| ЯвляетсяВходящейКорреспонденцией, ЯвляетсяИсходящейКорреспонденцией, ИспользоватьСрокИсполнения,
		| УчитыватьСуммуДокумента");
	
	ДанныеДляЗаполнения.ВидДокументаКеш = ЗначенияРеквизитов;
	
	Если ЗначенияРеквизитов.ВестиУчетПоТематикам = Истина Тогда
		ДокументОбъект.Тематика = ДанныеВидДокумента.Тематика;
	КонецЕсли;
	
	ОчиститьСтороныДокумента(ДокументОбъект, ДанныеДляЗаполнения);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") 
		И ЗначенияРеквизитов.ВестиУчетПоОрганизациям = Истина Тогда
		
		ЗаполнитьОрганизациюДокумента(ДокументОбъект, ДанныеДляЗаполнения, ПорогУверенностиДляПодстановки);
		
	КонецЕсли;
	
	Если ЗначенияРеквизитов.ВестиУчетСторон = Истина Тогда
		
		ЗаполнитьСтороныДокумента(ДокументОбъект, ДанныеДляЗаполнения, ПорогУверенностиДляПодстановки);
		
	КонецЕсли;
	
	Если ЗначенияРеквизитов.ВестиУчетПоКонтрагентам Тогда
		
		ЗаполнитьКонтрагентовДокумента(ДокументОбъект, ДанныеДляЗаполнения, ПорогУверенностиДляПодстановки);
		
	КонецЕсли;
	
	Если ЗначенияРеквизитов.ВестиУчетТоваровИУслуг Тогда
		
		СопоставитьНоменклатуруКонтрагентов(ДанныеДляЗаполнения);
		ЗаполнитьТабличнуюЧастьДокумента(ДокументОбъект, "Товары", ДанныеДляЗаполнения, ПорогУверенностиДляПодстановки);
		
	КонецЕсли;
	
	Если ЗначенияРеквизитов.УчитыватьСуммуДокумента Тогда
		
		ЗаполнитьСуммыДокумента(ДокументОбъект, ДанныеДляЗаполнения);
		
	КонецЕсли;
	
	ЗаполнитьПрочиеРеквизиты(ДокументОбъект, ДанныеДляЗаполнения);

КонецПроцедуры

// Выполняет очистку заполняемых сторон документа перед загрузкой распознанных данных.
// 
// Параметры:
//  ДокументОбъект - Неопределено, СправочникОбъект.ДокументыПредприятия - Документ объект
//  ДанныеДляЗаполнения - Структура - см ЗагрузкаДанныхИЗаполнениеДокумента()
Процедура ОчиститьСтороныДокумента(ДокументОбъект, Знач ДанныеДляЗаполнения)
	
	Если ДанныеДляЗаполнения.ВидДокументаКеш.ВестиУчетПоОрганизациям Тогда
		ДокументОбъект.Организация = Справочники.Организации.ПустаяСсылка();
		ДокументОбъект.Контрагенты.Очистить();
	КонецЕсли;
	
	Если ДанныеДляЗаполнения.ВидДокументаКеш.ВестиУчетПоКонтрагентам Тогда
		ДокументОбъект.Контрагент = Справочники.Контрагенты.ПустаяСсылка();
		ДокументОбъект.Контрагенты.Очистить();
	КонецЕсли;
	
	Если ДанныеДляЗаполнения.ВидДокументаКеш.ВестиУчетСторон Тогда
		ДокументОбъект.Стороны.Очистить();
	КонецЕсли;
	
	Если ДанныеДляЗаполнения.ВидДокументаКеш.ВестиУчетТоваровИУслуг Тогда
		ДокументОбъект.Товары.Очистить();
	КонецЕсли;
	
КонецПроцедуры

// Заполняет организацию документа.
// 
// Параметры:
//  ДокументОбъект - Неопределено, СправочникОбъект.ДокументыПредприятия - Документ объект
//  ДанныеДляЗаполнения - Структура - см ЗагрузкаДанныхИЗаполнениеДокумента() -ДанныеДляЗаполнения
//  ПорогУверенностиДляПодстановки - Число - мин. порог от 0 до 100 по Д-Винклеру для заполнения реквизита
Процедура ЗаполнитьОрганизациюДокумента(ДокументОбъект, ДанныеДляЗаполнения, ПорогУверенностиДляПодстановки)
	
	ТаблицаРеквизитов = ДанныеДляЗаполнения.ТаблицаРеквизитов.Скопировать();
	ТаблицаРеквизитов.Очистить();
	ТаблицаРеквизитов.Колонки.Добавить("Приоритет", ОбщегоНазначения.ОписаниеТипаЧисло(3));
	
	ВозможныеРеквизитыСторон = ВозможныеЗначенияРеквизитовСторон(ДанныеДляЗаполнения.НаправлениеДокумента, , Истина);
	
	ДокументОбъект.Организация = Справочники.Организации.ПустаяСсылка();
	
	РеквизитСМаксимальнойУверенностью = Неопределено;
	
	Для Каждого Реквизит Из ВозможныеРеквизитыСторон Цикл
		
		РаспознанныйРеквизит = 
			ДанныеДляЗаполнения.ТаблицаРеквизитов.Найти(Реквизит.ИмяРаспознанногоРеквизита + "Организация", 
				"ИмяРеквизита");
		
		Если РаспознанныйРеквизит = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если РеквизитСМаксимальнойУверенностью = Неопределено 
			ИЛИ РеквизитСМаксимальнойУверенностью.УверенностьРаспознанногоЗначения 
				< РаспознанныйРеквизит.УверенностьРаспознанногоЗначения Тогда
			
			РеквизитСМаксимальнойУверенностью = РаспознанныйРеквизит;
			
		КонецЕсли;
		
		НРекв = ТаблицаРеквизитов.Добавить();
		ЗаполнитьЗначенияСвойств(НРекв, РаспознанныйРеквизит);
		ЗаполнитьЗначенияСвойств(НРекв, Реквизит, "Приоритет");
		
	КонецЦикла;
	
	// сначала попробуем реквизит с максимальной уверенностью, далее по приоритету
	
	Если ЗначениеЗаполнено(РеквизитСМаксимальнойУверенностью.РаспознанноеЗначение) 
		И Не СторонаЕстьВДокументе(ДокументОбъект, РеквизитСМаксимальнойУверенностью, ДанныеДляЗаполнения) Тогда
				
		Если РеквизитСМаксимальнойУверенностью.УверенностьРаспознанногоЗначения >= ПорогУверенностиДляПодстановки Тогда
			
			ДокументОбъект.Организация = РеквизитСМаксимальнойУверенностью.РаспознанноеЗначение;
			
		КонецЕсли;
			
		Если ДанныеДляЗаполнения.ВидДокументаКеш.ВестиУчетСторон Тогда
			
			НовСтр = ДокументОбъект.Стороны.Добавить();
			НовСтр.Идентификатор = Новый УникальныйИдентификатор;
			
			НовСтр.Сторона = Справочники.Организации.ПустаяСсылка();
			
			Если РеквизитСМаксимальнойУверенностью.УверенностьРаспознанногоЗначения 
				>= ПорогУверенностиДляПодстановки Тогда
				НовСтр.Сторона = РеквизитСМаксимальнойУверенностью.РаспознанноеЗначение;
			КонецЕсли;
			
			НовСтр.Наименование = НаименованиеСтороны(РеквизитСМаксимальнойУверенностью);
			
			ПараметрыВерификации = ПараметрыВерификации();
			ПараметрыВерификации.ИмяРеквизита = "Сторона";
			ПараметрыВерификации.ТабличнаяЧасть = "Стороны";
			ПараметрыВерификации.ДанныеРаспознанногоРеквизита = РеквизитСМаксимальнойУверенностью;
			ПараметрыВерификации.Идентификатор = НовСтр.Идентификатор;
			ПараметрыВерификации.УстановленноеЗначение = НовСтр.Сторона;
			
			ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификации);
																		
		Иначе

			ПараметрыВерификации = ПараметрыВерификации();
			ПараметрыВерификации.ИмяРеквизита = "Организация";
			ПараметрыВерификации.ДанныеРаспознанногоРеквизита = РеквизитСМаксимальнойУверенностью;
			ПараметрыВерификации.УстановленноеЗначение = ДокументОбъект.Организация;
			ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификации);

		КонецЕсли;
		
		РеквизитСМаксимальнойУверенностью.ДобавленВДокумент = Истина;
		
		Возврат;
		
	КонецЕсли;
	
	ТаблицаРеквизитов.Сортировать("Приоритет Возр"); // 1 - самый высокий
	
	Для Каждого Реквизит Из ТаблицаРеквизитов Цикл
		
		// первый по приоритету, независимо от того распознан или нет
		Если СторонаЕстьВДокументе(ДокументОбъект, Реквизит, ДанныеДляЗаполнения) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Реквизит.УверенностьРаспознанногоЗначения >= ПорогУверенностиДляПодстановки Тогда
			ДокументОбъект.Организация = Реквизит.РаспознанноеЗначение;
		КонецЕсли;
			
		Если ДанныеДляЗаполнения.ВидДокументаКеш.ВестиУчетСторон Тогда
			
			НовСтр = ДокументОбъект.Стороны.Добавить();
			НовСтр.Сторона = Справочники.Организации.ПустаяСсылка();
			НовСтр.Идентификатор = Новый УникальныйИдентификатор;
			
			Если Реквизит.УверенностьРаспознанногоЗначения >= ПорогУверенностиДляПодстановки Тогда
				НовСтр.Сторона = Реквизит.РаспознанноеЗначение;
			КонецЕсли;
			
			НовСтр.Наименование = НаименованиеСтороны(Реквизит);
			
			ПараметрыВерификации = ПараметрыВерификации();
			ПараметрыВерификации.ИмяРеквизита = "Сторона";
			ПараметрыВерификации.ТабличнаяЧасть = "Стороны";
			ПараметрыВерификации.ДанныеРаспознанногоРеквизита = Реквизит;
			ПараметрыВерификации.Идентификатор = НовСтр.Идентификатор;
			ПараметрыВерификации.УстановленноеЗначение = НовСтр.Сторона;
			
			ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификации);
			
		Иначе
			
			
			ПараметрыВерификации = ПараметрыВерификации();
			ПараметрыВерификации.ИмяРеквизита = "Организация";
			ПараметрыВерификации.ДанныеРаспознанногоРеквизита = Реквизит;
			ПараметрыВерификации.УстановленноеЗначение = ДокументОбъект.Организация;
			
			ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификации);
				
		КонецЕсли;
		
		ОтметитьДобавлениеРеквизитаВДокумент(Реквизит, ДанныеДляЗаполнения);
		
		Прервать;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет контрагентов документа.
// 
// Параметры:
//  ДокументОбъект - Неопределено, СправочникОбъект.ДокументыПредприятия - Документ объект
//  ДанныеДляЗаполнения - Структура - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
//  ПорогУверенностиДляПодстановки - Число - мин. порог от 0 до 100 по Д-Винклеру для заполнения реквизита
Процедура ЗаполнитьКонтрагентовДокумента(ДокументОбъект, ДанныеДляЗаполнения, ПорогУверенностиДляПодстановки)
	
	ТаблицаРеквизитов = ДанныеДляЗаполнения.ТаблицаРеквизитов.Скопировать();
	ТаблицаРеквизитов.Очистить();
	ТаблицаРеквизитов.Колонки.Добавить("Приоритет", ОбщегоНазначения.ОписаниеТипаЧисло(3));
	
	ВозможныеРеквизиты = ВозможныеЗначенияРеквизитовСторон(ДанныеДляЗаполнения.НаправлениеДокумента);
	
	ДокументОбъект.Контрагент = Справочники.Контрагенты.ПустаяСсылка();
	ДокументОбъект.Контрагенты.Очистить();
	
	Для Каждого Реквизит Из ВозможныеРеквизиты Цикл
		
		РаспознанныйРеквизит = ДанныеДляЗаполнения.ТаблицаРеквизитов.Найти(Реквизит.ИмяРаспознанногоРеквизита, 
			"ИмяРеквизита");
			
		Если РаспознанныйРеквизит = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НРекв = ТаблицаРеквизитов.Добавить();
		ЗаполнитьЗначенияСвойств(НРекв, РаспознанныйРеквизит);
		ЗаполнитьЗначенияСвойств(НРекв, Реквизит, "Приоритет");
		
	КонецЦикла;
	
	ТаблицаРеквизитов.Сортировать("Приоритет Возр"); // 1 - самый высокий
	
	ПараметрыВерификацииКонтрагента = Неопределено;
	
	ПараметрыВерификацииКонтрагентов = Новый Массив;
	
	Для Каждого Реквизит Из ТаблицаРеквизитов Цикл
		
		Если СторонаЕстьВДокументе(ДокументОбъект, Реквизит, ДанныеДляЗаполнения) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДокументОбъект.Контрагент) Тогда 
			// в шапку - первый по приоритету, независимо от того распознан или нет
			Нстр = ДокументОбъект.Контрагенты.Добавить();
			Нстр.Идентификатор = Новый УникальныйИдентификатор;
			
			Если Реквизит.УверенностьРаспознанногоЗначения >= ПорогУверенностиДляПодстановки Тогда
				ДокументОбъект.Контрагент = Реквизит.РаспознанноеЗначение;
				Нстр.Контрагент = Реквизит.РаспознанноеЗначение;
				ДанныеДляЗаполнения.ВладелецНоменклатуры = Реквизит.РаспознанноеЗначение;
			КонецЕсли;
			
			ПараметрыВерификацииКонтрагента = ПараметрыВерификации();
			ПараметрыВерификацииКонтрагента.ИмяРеквизита = "Контрагент";
			ПараметрыВерификацииКонтрагента.ДанныеРаспознанногоРеквизита = Реквизит;
			ПараметрыВерификацииКонтрагента.УстановленноеЗначение = Нстр.Контрагент;
			
			ПараметрыВерификации = ПараметрыВерификации();
			ПараметрыВерификации.ИмяРеквизита = "Контрагент";
			ПараметрыВерификации.ТабличнаяЧасть = "Контрагенты";
			ПараметрыВерификации.Идентификатор = Нстр.Идентификатор;
			ПараметрыВерификации.ДанныеРаспознанногоРеквизита = Реквизит;
			ПараметрыВерификации.УстановленноеЗначение = Нстр.Контрагент;
			
			ПараметрыВерификацииКонтрагентов.Добавить(ПараметрыВерификации);
			
		Иначе
			
			Если ЗначениеЗаполнено(Реквизит.РаспознанноеЗначение) 
				И ДокументОбъект.Контрагенты.Найти(Реквизит.РаспознанноеЗначение) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
				
			Нстр = ДокументОбъект.Контрагенты.Добавить();
			Нстр.Идентификатор = Новый УникальныйИдентификатор;
			
			Если Реквизит.УверенностьРаспознанногоЗначения >= ПорогУверенностиДляПодстановки Тогда
				Нстр.Контрагент = Реквизит.РаспознанноеЗначение;
			КонецЕсли;
			
			ПараметрыВерификации = ПараметрыВерификации();
			ПараметрыВерификации.ИмяРеквизита = "Контрагент";
			ПараметрыВерификации.ТабличнаяЧасть = "Контрагенты";
			ПараметрыВерификации.Идентификатор = Нстр.Идентификатор;
			ПараметрыВерификации.ДанныеРаспознанногоРеквизита = Реквизит;
			ПараметрыВерификации.УстановленноеЗначение = Нстр.Контрагент;
			
			ПараметрыВерификацииКонтрагентов.Добавить(ПараметрыВерификации);
				
		КонецЕсли;
		
		ОтметитьДобавлениеРеквизитаВДокумент(Реквизит, ДанныеДляЗаполнения);
		
	КонецЦикла;
	
	Если ДокументОбъект.Контрагенты.Количество() > 1 Тогда // верифицировать нужно только в шапке
	
		Для Каждого ПараметрыВерификации Из ПараметрыВерификацииКонтрагентов Цикл
			ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификации);
		КонецЦикла;
	
	ИначеЕсли ДокументОбъект.Контрагенты.Количество() = 1 И ПараметрыВерификацииКонтрагента <> Неопределено Тогда
		
		ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификацииКонтрагента);
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет стороны документа.
// 
// Параметры:
//  ДокументОбъект - Неопределено, СправочникОбъект.ДокументыПредприятия - Документ объект
//  ДанныеДляЗаполнения - Структура - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
//  ПорогУверенностиДляПодстановки - Число - от 0 до 100 
Процедура ЗаполнитьСтороныДокумента(ДокументОбъект, ДанныеДляЗаполнения, ПорогУверенностиДляПодстановки)
	
	ВозможныеРеквизитыОрганизации = 
		ВозможныеЗначенияРеквизитовСторон(ДанныеДляЗаполнения.НаправлениеДокумента, 
			ДанныеДляЗаполнения.ТипФормализованногоДокумента, Истина);
	
	ВозможныеРеквизиты = ВозможныеЗначенияРеквизитовСторон(ДанныеДляЗаполнения.НаправлениеДокумента, 
		ДанныеДляЗаполнения.ТипФормализованногоДокумента);
	
	Для Каждого Реквизит Из ВозможныеРеквизитыОрганизации Цикл
		
		РаспознанныйРеквизит = 
			ДанныеДляЗаполнения.ТаблицаРеквизитов.
				Найти(Реквизит.ИмяРаспознанногоРеквизита + "Организация", "ИмяРеквизита");
				
		Если РаспознанныйРеквизит = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// Если одна организация уже есть в документе - остальные не берем если они пустые (значит контрагенты)
		Если Не ЗначениеЗаполнено(РаспознанныйРеквизит.РаспознанноеЗначение) 
													И ДокументОбъект.Стороны.Количество() > 0 Тогда
				Продолжить;
		КонецЕсли;
		
		Если СторонаЕстьВДокументе(ДокументОбъект, РаспознанныйРеквизит, ДанныеДляЗаполнения) Тогда
			Продолжить;
		КонецЕсли;
		
		Аналоги = АналогиСДругойРольюВДокументе(РаспознанныйРеквизит, ДанныеДляЗаполнения, Истина, Истина);
		
		ЗначениеДляПодстановки = РаспознанныйРеквизит;
		
		// если есть заполненный аналог с более высокой уверенностью возьмем его значение
		Для Каждого Аналог Из Аналоги Цикл
			
			Если СторонаЕстьВДокументе(ДокументОбъект, Аналог, ДанныеДляЗаполнения) Тогда
				Продолжить;
			КонецЕсли;
			
			Если Аналог.УверенностьРаспознанногоЗначения > ЗначениеДляПодстановки.УверенностьРаспознанногоЗначения Тогда
				ЗначениеДляПодстановки = Аналог;
			КонецЕсли;
			
		КонецЦикла;
		
		НСторона = ДокументОбъект.Стороны.Добавить();
		
		Если ЗначениеДляПодстановки.УверенностьРаспознанногоЗначения >= ПорогУверенностиДляПодстановки Тогда
			НСторона.Сторона = ЗначениеДляПодстановки.РаспознанноеЗначение;
		КонецЕсли;
		
		НСторона.Наименование = НаименованиеСтороны(ЗначениеДляПодстановки);
		
		НСторона.Идентификатор = Новый УникальныйИдентификатор;
		
		ПараметрыВерификации = ПараметрыВерификации();
		ПараметрыВерификации.ИмяРеквизита = "Сторона";
		ПараметрыВерификации.ТабличнаяЧасть = "Стороны";
		ПараметрыВерификации.ДанныеРаспознанногоРеквизита = ЗначениеДляПодстановки;
		ПараметрыВерификации.Идентификатор = НСторона.Идентификатор;
		ПараметрыВерификации.УстановленноеЗначение = НСторона.Сторона;
		
		ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификации);
		
		ОтметитьДобавлениеРеквизитаВДокумент(ЗначениеДляПодстановки, ДанныеДляЗаполнения);
		
	КонецЦикла;
	
	ПопыткаЗаполненияВладельцаНоменклатурыПройдена = ЗначениеЗаполнено(ДанныеДляЗаполнения.ВладелецНоменклатуры);
	
	Для Каждого Реквизит Из ВозможныеРеквизиты Цикл
		
		РаспознанныйРеквизит = 
			ДанныеДляЗаполнения.ТаблицаРеквизитов.Найти(Реквизит.ИмяРаспознанногоРеквизита, "ИмяРеквизита");
		
		Если РаспознанныйРеквизит = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если РаспознанныйРеквизит.РаспознанныйТекст = "" Тогда
			Продолжить;
		КонецЕсли;
		
		Если СторонаЕстьВДокументе(ДокументОбъект, РаспознанныйРеквизит, ДанныеДляЗаполнения) Тогда
			Продолжить;
		КонецЕсли;
		
		Аналоги = АналогиСДругойРольюВДокументе(РаспознанныйРеквизит, ДанныеДляЗаполнения, Истина, Истина);
		
		ЗначениеДляПодстановки = РаспознанныйРеквизит;
		
		Для Каждого Аналог Из Аналоги Цикл
			
			Если СторонаЕстьВДокументе(ДокументОбъект, Аналог, ДанныеДляЗаполнения) Тогда
				Продолжить;
			КонецЕсли;
			
			Если Аналог.УверенностьРаспознанногоЗначения > ЗначениеДляПодстановки.УверенностьРаспознанногоЗначения Тогда
				ЗначениеДляПодстановки = Аналог;
			КонецЕсли;
			
		КонецЦикла;
		
		НСторона = ДокументОбъект.Стороны.Добавить();
		НСторона.Идентификатор = Новый УникальныйИдентификатор;
		
		Если ЗначениеДляПодстановки.УверенностьРаспознанногоЗначения >= ПорогУверенностиДляПодстановки Тогда
			НСторона.Сторона = ЗначениеДляПодстановки.РаспознанноеЗначение;
		Иначе
			НСторона.Сторона = Новый(ТипЗнч(ЗначениеДляПодстановки.РаспознанноеЗначение));
		КонецЕсли;
		
		НСторона.Наименование = НаименованиеСтороны(ЗначениеДляПодстановки);
		
		Если Не ПопыткаЗаполненияВладельцаНоменклатурыПройдена Тогда
			ПопыткаЗаполненияВладельцаНоменклатурыПройдена = Истина; // первый контрагент
			Если ЗначениеЗаполнено(НСторона.Сторона) Тогда
				ДанныеДляЗаполнения.ВладелецНоменклатуры = ЗначениеДляПодстановки.РаспознанноеЗначение;
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыВерификации = ПараметрыВерификации();
		ПараметрыВерификации.ИмяРеквизита = "Сторона";
		ПараметрыВерификации.ТабличнаяЧасть = "Стороны";
		ПараметрыВерификации.ДанныеРаспознанногоРеквизита = ЗначениеДляПодстановки;
		ПараметрыВерификации.Идентификатор = НСторона.Идентификатор;
		ПараметрыВерификации.УстановленноеЗначение = НСторона.Сторона;
		
		ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификации);
		ОтметитьДобавлениеРеквизитаВДокумент(ЗначениеДляПодстановки, ДанныеДляЗаполнения);
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет табличную часть документа.
// 
// Параметры:
//  ДокументОбъект - Неопределено, СправочникОбъект.ДокументыПредприятия - Документ объект
//  ИмяТаблицы - Строка - в текущей версии имя ТЧ и имя распознанной таблицы документа
//  РаспознанныеДанные - Структура - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
//  ПорогУверенностиДляПодстановки - Число - мин. порог от 0 до 100 для заполнения реквизита
Процедура ЗаполнитьТабличнуюЧастьДокумента(
			ДокументОбъект, Знач ИмяТаблицы, Знач РаспознанныеДанные, Знач ПорогУверенностиДляПодстановки)
	
	ТЧДокумента = ДокументОбъект[ИмяТаблицы];
	
	ТЧДокумента.Очистить();
	
	РеквизитыТаблицы = РаспознанныеДанные.ТаблицаРеквизитовТЧ.Скопировать();
	РеквизитыТаблицы.Очистить();
	
	Отбор = Новый Структура("ИмяТаблицы", ИмяТаблицы);
	СтрокиТаблицы = РаспознанныеДанные.ТаблицаРеквизитовТЧ.НайтиСтроки(Отбор);
	
	Для Каждого СтрокаТаблицы Из СтрокиТаблицы Цикл
		НСтр = РеквизитыТаблицы.Добавить();
		ЗаполнитьЗначенияСвойств(НСтр, СтрокаТаблицы);
	КонецЦикла;
	
	КоличествоСтрокРеквизитов = РеквизитыТаблицы.Количество();
	
	Если КоличествоСтрокРеквизитов = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыТаблицы.Сортировать("НомерСтрокиТаблицы Возр");
	
	КоличествоСтрок = РеквизитыТаблицы[КоличествоСтрокРеквизитов - 1].НомерСтрокиТаблицы;
	
	Для НомерСтроки = 1 По КоличествоСтрок Цикл // Количество строк д.б. аналогично распознанным данным
		НСтр = ТЧДокумента.Добавить();
		НСтр.Идентификатор = Новый УникальныйИдентификатор;
	КонецЦикла;
	
	ПараметрыДобавленияРеквизита = Новый Структура;
	ПараметрыДобавленияРеквизита.Вставить("ИмяТаблицы", ИмяТаблицы);
	ПараметрыДобавленияРеквизита.Вставить("ДокументОбъект", ДокументОбъект);
	ПараметрыДобавленияРеквизита.Вставить("ПорогУверенностиДляПодстановки", ПорогУверенностиДляПодстановки);
	
	НеобходимаВерификацияСумм = НеобходимаВерификацияСумм(РаспознанныеДанные);
	
	Для Каждого ДанныеРеквизитаТаблицы Из РеквизитыТаблицы Цикл
		
		Если Метаданные.Справочники.ДокументыПредприятия.ТабличныеЧасти.Товары.Реквизиты.Найти(
			ДанныеРеквизитаТаблицы.ИмяРеквизита) = Неопределено Тогда
				Продолжить;
		КонецЕсли;
		
		ПараметрыДобавленияРеквизита.Вставить("ДанныеРеквизита", ДанныеРеквизитаТаблицы);
		
		ДобавитьЗначениеРеквизитаТабличнойЧасти(ПараметрыДобавленияРеквизита, НеобходимаВерификацияСумм);
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет прочие реквизиты документа (кроме сторон).
// 
// Параметры:
//  ДокументОбъект - СправочникОбъект.ДокументыПредприятия
//  РаспознанныеДанные - Структура - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
Процедура ЗаполнитьПрочиеРеквизиты(ДокументОбъект, Знач РаспознанныеДанные)
	
	Валюта = Делопроизводство.ПолучитьВалютуПоУмолчанию();

	Для Каждого ДанныеРеквизита Из РаспознанныеДанные.ТаблицаРеквизитов Цикл
		
		Если ДанныеРеквизита.ИмяРеквизита = "НомерДокумента" Тогда
			
			Если РаспознанныеДанные.ВидДокументаКеш.ЯвляетсяВходящейКорреспонденцией = Истина Тогда
				
				ПараметрыВерификации = ПараметрыВерификации();
				ПараметрыВерификации.ИмяРеквизита = "ИсходящийНомер";
				ПараметрыВерификации.ДанныеРаспознанногоРеквизита = ДанныеРеквизита;
				
				ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификации);
				
			КонецЕсли;
			
		ИначеЕсли ДанныеРеквизита.ИмяРеквизита = "ДатаДокумента" Тогда
			
			ПараметрыВерификации = ПараметрыВерификации();
			ПараметрыВерификации.ИмяРеквизита = "ИсходящаяДата";
			ПараметрыВерификации.ДанныеРаспознанногоРеквизита = ДанныеРеквизита;
			
			Если РаспознанныеДанные.ВидДокументаКеш.ЯвляетсяВходящейКорреспонденцией = Истина Тогда
				ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификации);
			КонецЕсли;
		
		ИначеЕсли ДанныеРеквизита.ИмяРеквизита = "СрокОплаты" 
			И РаспознанныеДанные.ВидДокументаКеш.ИспользоватьСрокИсполнения Тогда
		
			ПараметрыВерификации = ПараметрыВерификации();
			ПараметрыВерификации.ИмяРеквизита = "СрокОплаты";
			ПараметрыВерификации.ДанныеРаспознанногоРеквизита = ДанныеРеквизита;

			ДокументОбъект.СрокИсполнения = ДанныеРеквизита.РаспознанноеЗначение;
			ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификации);
			
		Иначе
			
			Продолжить;
			
		КонецЕсли;
		
	КонецЦикла;
		
	ДокументОбъект.Валюта = Валюта;
		
	ДокументОбъект.ФормаДокумента = Перечисления.ВариантыФормДокументов.Бумажная;
		
КонецПроцедуры

// Заполняет суммы документа.
// 
// Параметры:
//  ДокументОбъект - Неопределено, СправочникОбъект.ДокументыПредприятия - Документ объект
//  РаспознанныеДанные - Структура - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения 
Процедура ЗаполнитьСуммыДокумента(ДокументОбъект, Знач РаспознанныеДанные)
	
	ДанныеИтогоСумма = РаспознанныеДанные.ТаблицаРеквизитов.Найти("ИтогоСумма", "ИмяРеквизита");
	ДанныеИтогоСуммаНДС = РаспознанныеДанные.ТаблицаРеквизитов.Найти("ИтогоСуммаНДС", "ИмяРеквизита");
	ДанныеИтогоВсего = РаспознанныеДанные.ТаблицаРеквизитов.Найти("ИтогоВсего", "ИмяРеквизита");
	
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяРеквизита","СуммаВключаетНДС");
	ДанныеСуммаВключаетНДС = РаспознанныеДанные.ТаблицаРеквизитов.НайтиСтроки(Отбор);
	
	СуммаВключаетНДС = Ложь;
	
	Если ДанныеСуммаВключаетНДС.Количество() > 0 Тогда
		Если СтрНайти(ДанныеСуммаВключаетНДС[0].РаспознанныйТекст, "числе НДС") > 0 Тогда
			СуммаВключаетНДС = Истина;
		КонецЕсли;
	КонецЕсли;
	
	НеобходимаВерификацияСумм = НеобходимаВерификацияСумм(РаспознанныеДанные, СуммаВключаетНДС);
		
	Если ДанныеИтогоВсего <> Неопределено И ДанныеИтогоВсего.РаспознанноеЗначение > 0 Тогда
		
		ДокументОбъект.Сумма = ДанныеИтогоВсего.РаспознанноеЗначение;
		
		Если НеобходимаВерификацияСумм = Ложь Тогда
			ДанныеИтогоВсего.УверенностьРаспознанногоЗначения = 100;
		КонецЕсли;
		
		ПараметрыВерификации = ПараметрыВерификации();
		ПараметрыВерификации.ИмяРеквизита = "Сумма";
		ПараметрыВерификации.ДанныеРаспознанногоРеквизита = ДанныеИтогоВсего;
		ПараметрыВерификации.УстановленноеЗначение = ДокументОбъект.Сумма;
		ПараметрыВерификации.ВерифицироватьСуммы = НеобходимаВерификацияСумм;
		
		ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификации);
	
	ИначеЕсли ДанныеИтогоСумма <> Неопределено И ДанныеИтогоСумма.РаспознанноеЗначение > 0 Тогда
		
		Если СуммаВключаетНДС = Истина Тогда
			
			ДокументОбъект.Сумма = ДанныеИтогоСумма.РаспознанноеЗначение;
			
			ПараметрыВерификации = ПараметрыВерификации();
			ПараметрыВерификации.ИмяРеквизита = "Сумма";
			ПараметрыВерификации.ДанныеРаспознанногоРеквизита = ДанныеИтогоСумма;
			ПараметрыВерификации.УстановленноеЗначение = ДокументОбъект.Сумма;
			ПараметрыВерификации.ВерифицироватьСуммы = НеобходимаВерификацияСумм;
		
			ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификации);
			
		Иначе
			
			ДокументОбъект.Сумма = ДанныеИтогоСумма.РаспознанноеЗначение + ДанныеИтогоСуммаНДС.РаспознанноеЗначение;
			
			ПараметрыВерификации = ПараметрыВерификации();
			ПараметрыВерификации.ИмяРеквизита = "Сумма";
			ПараметрыВерификации.ДанныеРаспознанногоРеквизита = ДанныеИтогоСумма;
			ПараметрыВерификации.УстановленноеЗначение = ДокументОбъект.Сумма;
			ПараметрыВерификации.ВерифицироватьСуммы = НеобходимаВерификацияСумм;
			
			ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификации);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДокументОбъект.СуммаНДС = ДанныеИтогоСуммаНДС.РаспознанноеЗначение;
	
	Если НеобходимаВерификацияСумм = Ложь Тогда
		ДанныеИтогоСуммаНДС.УверенностьРаспознанногоЗначения = 100;
	КонецЕсли;
	
	ПараметрыВерификации = ПараметрыВерификации();
	ПараметрыВерификации.ИмяРеквизита = "СуммаНДС";
	ПараметрыВерификации.ДанныеРаспознанногоРеквизита = ДанныеИтогоСуммаНДС;
	ПараметрыВерификации.УстановленноеЗначение = ДокументОбъект.СуммаНДС;
	ПараметрыВерификации.ВерифицироватьСуммы = НеобходимаВерификацияСумм;
	
	ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификации);
	
КонецПроцедуры

#КонецОбласти

#Область Верификация

// Формирует набор данных для заполнения параметров верификации реквизита.
// 
// Возвращаемое значение:
//  Структура 
// * Документ - СправочникСсылка.ДокументыПредприятия - верифицируемый документ
// * ИмяРеквизита - Строка - имя реквизита документа
// * ДанныеРаспознанногоРеквизита - СтрокаТаблицыЗначений - см. ЗаполнитьРеквизитыПоВидуДокумента
// * ТабличнаяЧасть - Строка - имя табличной части документа
// * Идентификатор - УникальныйИдентификатор - идентификатор строки табличной части документа
// * ИмяРаспознанногоРеквизита - Строка - имя распознанного реквизита
// * ИмяТаблицы - Строка - имя распознанной таблицы
// * НомерСтрокиТаблицы - Число - номер строки распознанной таблицы
// * Проверен - Булево - признак верификации реквизита
// * ТребуетПроверки - Булево - признак необходимости верификации реквизита
// * УстановленноеЗначение - ОпределяемыйТип.РаспознаваемыеРеквизитыДокумента - установленное значение реквизита
// * ВерифицироватьСуммы - Булево - признак обязательной верификации сумм
Функция ПараметрыВерификации()
	
	ПараметрыВерификации = Новый Структура;
	ПараметрыВерификации.Вставить("Документ", Неопределено);
	ПараметрыВерификации.Вставить("ИмяРеквизита", Неопределено);
	ПараметрыВерификации.Вставить("ДанныеРаспознанногоРеквизита", Неопределено);
	ПараметрыВерификации.Вставить("ТабличнаяЧасть", Неопределено);
	ПараметрыВерификации.Вставить("Идентификатор", Неопределено);
	ПараметрыВерификации.Вставить("ИмяРаспознанногоРеквизита", Неопределено);
	ПараметрыВерификации.Вставить("ИмяТаблицы", Неопределено);
	ПараметрыВерификации.Вставить("НомерСтрокиТаблицы", 0);
	ПараметрыВерификации.Вставить("Проверен", Ложь);
	ПараметрыВерификации.Вставить("ТребуетПроверки", Ложь);
	ПараметрыВерификации.Вставить("УстановленноеЗначение", Неопределено);
	ПараметрыВерификации.Вставить("ВерифицироватьСуммы", Ложь);
	
	Возврат ПараметрыВерификации;
	
КонецФункции

// Записывает параметры верификации реквизита.
// 
// Параметры:
//  ДокументОбъект - СправочникОбъект.ДокументыПредприятия
//  ПараметрыВерификации - Структура - см. ПараметрыВерификации()
//  ДанныеРаспознанногоРеквизита - СтрокаТаблицыЗначений - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
//  ИмяТЧ - Строка - имя табличной части документа
//  Идентификатор - Неопределено, УникальныйИдентификатор - идентификатор строки ТЧ документа
//  ПорогУверенности - Число - мин. порог от 0 до 100 по Д-Винклеру для заполнения реквизита
//  ВерифицироватьСуммы - Булево - признак необходимости верификации сумм
Процедура ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификации)
	
	ДанныеРаспознанногоРеквизита = ПараметрыВерификации.ДанныеРаспознанногоРеквизита;
	ПорогУверенностиДляВерификации = РаспознаваниеДокументовКлиентСервер.ПорогУверенностиДляВерификации();
	
	ПараметрыВерификации.Документ = ДокументОбъект.Ссылка;
	ПараметрыВерификации.ИмяРаспознанногоРеквизита = ДанныеРаспознанногоРеквизита.ИмяРеквизита;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеРаспознанногоРеквизита, "ИмяТаблицы") Тогда
		ПараметрыВерификации.ИмяТаблицы = ДанныеРаспознанногоРеквизита.ИмяТаблицы;
		ПараметрыВерификации.НомерСтрокиТаблицы = ДанныеРаспознанногоРеквизита.НомерСтрокиТаблицы;
	КонецЕсли;
	
	Координаты = РаспознаваниеДокументовСлужебныйКлиентСервер.ПолучитьНаборКоординат(ДанныеРаспознанногоРеквизита);
	
	Если ТипЗнч(ДанныеРаспознанногоРеквизита.РаспознанноеЗначение) = Тип("Число") Тогда 
		
		Если ПараметрыВерификации.ВерифицироватьСуммы = Истина Тогда
			ТребуетПроверки = Истина;
		Иначе
			ТребуетПроверки = Ложь;
		КонецЕсли;
	
	ИначеЕсли ДанныеРаспознанногоРеквизита.УверенностьРаспознанногоЗначения 
				< ПорогУверенностиДляВерификации 
					И Не РаспознаваниеДокументовКлиентСервер.ВсеКоординатыНулевые(Координаты) Тогда
		
		ТребуетПроверки = Истина;
		
	Иначе
		
		ТребуетПроверки = Ложь;
		
	КонецЕсли;
	
	ПараметрыВерификации.ТребуетПроверки = ТребуетПроверки;
	
	Если ПустаяСтрока(ПараметрыВерификации.ТабличнаяЧасть) Тогда
		
		РегистрыСведений.ВерификацияРеквизитовРаспознанныхДокументов.
			ДобавитьПроверяемыйРеквизит(ПараметрыВерификации);
			
	Иначе
		
		РегистрыСведений.ВерификацияРеквизитовТЧРаспознанныхДокументов.
			ДобавитьПроверяемыйРеквизит(ПараметрыВерификации);
			
	КонецЕсли;

КонецПроцедуры

// Возвращает признак необходимости верификации сумм документа
// 
// Параметры:
//  РаспознанныеДанные - Структура - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
// 
// Возвращаемое значение:
//  Булево - Необходима верификация сумм
Функция НеобходимаВерификацияСумм(Знач РаспознанныеДанные, СуммаВключаетНДС = Неопределено)

	ДанныеИтогоСумма = РаспознанныеДанные.ТаблицаРеквизитов.Найти("ИтогоСумма", "ИмяРеквизита");
	ДанныеИтогоСуммаНДС = РаспознанныеДанные.ТаблицаРеквизитов.Найти("ИтогоСуммаНДС", "ИмяРеквизита");
	ДанныеИтогоВсего = РаспознанныеДанные.ТаблицаРеквизитов.Найти("ИтогоВсего", "ИмяРеквизита");
	
	НеобходимаВерификацияСумм = Ложь;
	
	Если ДанныеИтогоСумма = Неопределено И ДанныеИтогоСуммаНДС = Неопределено И ДанныеИтогоВсего = Неопределено Тогда
		НеобходимаВерификацияСумм = Истина; // Сумм либо нет, либо не распознаны
	Иначе
		
		ДанныеИтогоСумма = ?(ДанныеИтогоСумма = Неопределено, 0, ДанныеИтогоСумма.РаспознанноеЗначение);
		ДанныеИтогоСуммаНДС = ?(ДанныеИтогоСуммаНДС = Неопределено, 0, ДанныеИтогоСуммаНДС.РаспознанноеЗначение);
		ДанныеИтогоВсего = ?(ДанныеИтогоВсего = Неопределено, 0, ДанныеИтогоВсего.РаспознанноеЗначение);
	
		Если СуммаВключаетНДС <> Неопределено Тогда
			
			Если СуммаВключаетНДС = Истина Тогда
				
				Если ДанныеИтогоСумма = ДанныеИтогоВсего Тогда
					СуммаНДС10 = ОКР(ДанныеИтогоСумма / 110 *10, 2);
					СуммаНДС18 = ОКР(ДанныеИтогоСумма / 118 *18, 2);
					СуммаНДС20 = ОКР(ДанныеИтогоСумма / 120 *20, 2);
					
					НеобходимаВерификацияСумм = Не (СуммаНДС10 = ДанныеИтогоСуммаНДС 
						ИЛИ СуммаНДС18 = ДанныеИтогоСуммаНДС ИЛИ СуммаНДС20 = ДанныеИтогоСуммаНДС 
						ИЛИ ДанныеИтогоСуммаНДС = 0);
				Иначе
					
					НеобходимаВерификацияСумм = ДанныеИтогоВсего <> ДанныеИтогоСумма + ДанныеИтогоСуммаНДС;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НеобходимаВерификацияСумм;
	
КонецФункции

Процедура ПодготовитьДанныеВерификацииДляЗаписи(ДанныеВерификации)
	
	Для Каждого ЭлементДанных Из ДанныеВерификации Цикл
		
		Если Не (ЭлементДанных.Ключ = "ВерифицируемыеРеквизитыТЧ" 
			ИЛИ ЭлементДанных.Ключ = "ВерифицируемыеРеквизитыШапки") Тогда
				Продолжить;
		КонецЕсли;
		
		Отбор = Новый Структура("ИмяРеквизита", "");
		СтрокиКУдалению = ДанныеВерификации[ЭлементДанных.Ключ].НайтиСтроки(Отбор);
		// строки не привязанные к элементам формы
		Для Каждого Стр Из СтрокиКУдалению Цикл
			
			ДанныеВерификации[ЭлементДанных.Ключ].Удалить(Стр);
			
		КонецЦикла;
		
		// удаленные строки
		Если ЭлементДанных.Ключ = "ВерифицируемыеРеквизитыТЧ" Тогда
			Отбор = Новый Структура("СтрокаУдалена", Истина);
			СтрокиКУдалению = ДанныеВерификации[ЭлементДанных.Ключ].НайтиСтроки(Отбор);
			
			Для Каждого Стр Из СтрокиКУдалению Цикл
				
				ДанныеВерификации[ЭлементДанных.Ключ].Удалить(Стр);
				
			КонецЦикла;
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьВерифицированныеРеквизитыДокумента(ДокументСсылка, ДанныеВерификацииРеквизитов, Отказ = Ложь) Экспорт
	
	НачатьТранзакцию();
	
	ДанныеВерифицированы = ДанныеВерификацииРеквизитов.ДанныеВерифицированы;
	
	Попытка
		
		// если это документ с не распознанными данными установим признак проверки
		Отбор = Новый Структура;
		Отбор.Вставить("ИмяРеквизита", РаспознаваниеДокументовСлужебныйКлиентСервер.ИмяРеквизитаДокументНеРаспознан());
		НСтр = ДанныеВерификацииРеквизитов.ВерифицируемыеРеквизитыШапки.НайтиСтроки(Отбор);
		Если НСтр.Количество() > 0 Тогда
			НСтр[0].ТребуетПроверки = Ложь;
		КонецЕсли;
		
		
		РегистрыСведений.ВерификацияРеквизитовРаспознанныхДокументов.
			ЗаписатьДанныеДокумента(ДокументСсылка, ДанныеВерификацииРеквизитов.ВерифицируемыеРеквизитыШапки, 
				ДанныеВерифицированы);
			
		РегистрыСведений.ВерификацияРеквизитовТЧРаспознанныхДокументов.
			ЗаписатьДанныеДокумента(ДокументСсылка, ДанныеВерификацииРеквизитов.ВерифицируемыеРеквизитыТЧ, 
				ДанныеВерифицированы);
			
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		Отказ = Истина;
		ОтменитьТранзакцию();
		ОписаниеОшибкиИнфо = ОписаниеОшибки();
		ТекстСообщения = НСтр("ru = 'Не удалось записать данные верификации распознанных реквизитов 
								 |для документа ""%1, %2"".'",  ОбщегоНазначения.КодОсновногоЯзыка());
								 
		ТекстСообщения = СтрШаблон(ТекстСообщения, ДокументСсылка, ОписаниеОшибкиИнфо);
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
			
	КонецПопытки;
		
КонецПроцедуры

#КонецОбласти

// Устанавливает отметку о добавлении реквизита в документ.
// 
// Параметры:
//  ДобавленныйРеквизит - СтрокаТаблицыЗначений - см ЗагрузкаДанныхИЗаполнениеДокумента() -ДанныеДляЗаполнения
//  ДанныеДляЗаполнения - Структура - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
Процедура ОтметитьДобавлениеРеквизитаВДокумент(ДобавленныйРеквизит, ДанныеДляЗаполнения)
	
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяРеквизита");
	Отбор.Вставить("КоординатаX0");
	Отбор.Вставить("КоординатаX1");
	Отбор.Вставить("КоординатаY0");
	Отбор.Вставить("КоординатаY1");
	
	ЗаполнитьЗначенияСвойств(Отбор, ДобавленныйРеквизит);

	НСтр = ДанныеДляЗаполнения.ТаблицаРеквизитов.НайтиСтроки(Отбор);
	
	Если НСтр.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НСтр[0].ДобавленВДокумент = Истина;
	
КонецПроцедуры

Функция НаправлениеДокументаПоРаспознаннымДанным(РаспознанныеРеквизиты, ИмяПоля = "ИмяРеквизита") Экспорт
	
	ВходящиеИмена = Новый Массив;
	ВходящиеИмена.Добавить("Покупатель");
	ВходящиеИмена.Добавить("Грузополучатель");
	ВходящиеИмена.Добавить("Плательщик");
	
	ИсходящиеИмена = Новый Массив;
	ИсходящиеИмена.Добавить("Продавец");
	ИсходящиеИмена.Добавить("Грузоотправитель");
	
	РеквизитВходящейОрганизации = Неопределено;
	РеквизитИсходящейОрганизации = Неопределено;
	
	ЕстьУстановленноеЗначение = РаспознанныеРеквизиты.Колонки.Найти("УстановленноеЗначение") <> Неопределено;
	
	НайденныеРеквизитыОрганизации = РаспознанныеРеквизиты.Скопировать();
	
	НайденныеРеквизитыОрганизации.Очистить();
	
	Для Каждого Реквизит Из РаспознанныеРеквизиты Цикл
		
		//если передана таблица верификации то смотрим установленное значение, если оно заполнено
		//при верификации направление может измениться
		Если ЕстьУстановленноеЗначение = Истина И ЗначениеЗаполнено(Реквизит.УстановленноеЗначение)
			И ТипЗнч(Реквизит.УстановленноеЗначение) = Тип("СправочникСсылка.Организации") Тогда
				ИмяПроверяемогоПоля = "УстановленноеЗначение";
		Иначе
			ИмяПроверяемогоПоля = "РаспознанноеЗначение";
		КонецЕсли;
		
		Если ТипЗнч(Реквизит.РаспознанноеЗначение) <> Тип("СправочникСсылка.Организации")
			Или Не ЗначениеЗаполнено(Реквизит[ИмяПроверяемогоПоля]) Тогда
			Продолжить;
		КонецЕсли;
		
		НРек = НайденныеРеквизитыОрганизации.Добавить();
		
		ЗаполнитьЗначенияСвойств(НРек, Реквизит);
		
	КонецЦикла;
	
	НайденныеРеквизитыОрганизации.Сортировать("УверенностьРаспознанногоЗначения Убыв");
	
	Для Каждого Реквизит Из НайденныеРеквизитыОрганизации Цикл
		
		Для Каждого ВходящееИмя Из ВходящиеИмена Цикл
			Если СтрНайти(Реквизит[ИмяПоля], ВходящееИмя) > 0 
				И ЗначениеЗаполнено(Реквизит[ИмяПроверяемогоПоля]) Тогда
					
					РеквизитВходящейОрганизации = Реквизит;
				
					Прервать;
				
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого ИсходящееИмя Из ИсходящиеИмена Цикл
			Если СтрНайти(Реквизит[ИмяПоля], ИсходящееИмя) > 0 
				И ЗначениеЗаполнено(Реквизит[ИмяПроверяемогоПоля]) Тогда
					
					РеквизитИсходящейОрганизации = Реквизит;
				
					Прервать;
				
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(РеквизитВходящейОрганизации) ИЛИ ЗначениеЗаполнено(РеквизитИсходящейОрганизации) Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если РеквизитВходящейОрганизации <> Неопределено И РеквизитИсходящейОрганизации <> Неопределено Тогда
		
		НаправлениеДокумента = Перечисления.НаправленияЭДО.Интеркампани;
		
	ИначеЕсли РеквизитВходящейОрганизации <> Неопределено Тогда
		
		НаправлениеДокумента = Перечисления.НаправленияЭДО.Входящий;
		
	ИначеЕсли РеквизитИсходящейОрганизации <> Неопределено Тогда
		
		НаправлениеДокумента = Перечисления.НаправленияЭДО.Исходящий;
		
	Иначе
		
		НаправлениеДокумента = Неопределено;
		
	КонецЕсли;
	
	Возврат НаправлениеДокумента;
	
КонецФункции

// Проверяет, есть ли сторона в документе.
// 
// Параметры:
//  ДокументОбъект - Неопределено, СправочникОбъект.ДокументыПредприятия - Документ объект
//  Реквизит - СтрокаТаблицыЗначений - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
//  РаспознанныеДанные - Структура - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
// Возвращаемое значение:
//  Булево - Сторона есть в документе
Функция СторонаЕстьВДокументе(ДокументОбъект, Знач Реквизит, Знач РаспознанныеДанные)
	
	Если Реквизит.ДобавленВДокумент = Истина Тогда
		Возврат Истина;
	КонецЕсли;
	
	ЗначениеРеквизита = Реквизит.РаспознанноеЗначение;
	
	Если ТипЗнч(ЗначениеРеквизита) = Тип("СправочникСсылка.Организации") Тогда
		
		Если ЗначениеЗаполнено(ДокументОбъект.Организация) Тогда
			
			Если ДокументОбъект.Организация = ЗначениеРеквизита Тогда
				Возврат Истина;
			КонецЕсли;
		
			Если ДокументОбъект.Стороны.Найти(ЗначениеРеквизита, "Сторона") <> Неопределено Тогда
				Возврат Истина; 
			КонецЕсли;
			
		КонецЕсли;
		
		Если ДобавленАналогПоКлючевымРеквизитам(Реквизит, РаспознанныеДанные) 
			Тогда 
				Возврат Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипЗнч(ЗначениеРеквизита) = Тип("СправочникСсылка.Контрагенты") Тогда
		
		Если ЗначениеЗаполнено(ДокументОбъект.Контрагент) Тогда
			
			Если ДокументОбъект.Контрагент = ЗначениеРеквизита Тогда
				Возврат Истина;
			КонецЕсли;
	
			Если ДокументОбъект.Контрагенты.Найти(ЗначениеРеквизита, "Контрагент") <> Неопределено Тогда
				Возврат Истина;
			КонецЕсли;
			
		КонецЕсли;

		Если ДобавленАналогПоКлючевымРеквизитам(Реквизит, РаспознанныеДанные) 
			Тогда 
				Возврат Истина;
		КонецЕсли;
		
	КонецЕсли;

	Возврат Ложь;
	
КонецФункции

// Проверяет, есть ли среди распознанных сторон в документе, стороны с аналогичными ключевыми реквизитами (ИНН, КПП)
// 
// Параметры:
//  ПроверяемыйРеквизит - СтрокаТаблицыЗначений - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
//  РаспознанныеДанные - Неопределено, Структура - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
// Возвращаемое значение:
//  Булево - Добавлен аналог по ключевым реквизитам
Функция ДобавленАналогПоКлючевымРеквизитам(Знач ПроверяемыйРеквизит, Знач РаспознанныеДанные)
	
	// Проверим по имени
	Для Каждого Реквизит Из РаспознанныеДанные.ТаблицаРеквизитов Цикл
		
		Если СтрЗаменить(Реквизит.ИмяРеквизита, "Организация", "") 
			= СтрЗаменить(ПроверяемыйРеквизит.ИмяРеквизита, "Организация", "") 
				И Реквизит.ДобавленВДокумент = Истина Тогда
					Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ПроверяемыйРеквизит.РаспознанноеЗначение) Тогда
		
		Отбор = Новый Структура("РаспознанноеЗначение", ПроверяемыйРеквизит.РаспознанноеЗначение);
		НСтр = РаспознанныеДанные.ТаблицаРеквизитов.НайтиСтроки(Отбор);
		
		Для Каждого НайденнаяСтрока Из НСтр Цикл
			
			Если НайденнаяСтрока.ИмяРеквизита = ПроверяемыйРеквизит.ИмяРеквизита Тогда
				Продолжить;
			КонецЕсли;
			
			Если НайденнаяСтрока.ДобавленВДокумент = Истина Тогда
				Возврат Истина;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	АналогиСДругойРольюВДокументе = АналогиСДругойРольюВДокументе(ПроверяемыйРеквизит, РаспознанныеДанные);
	
	Для Каждого Аналог Из АналогиСДругойРольюВДокументе Цикл
		
		Если Аналог.ДобавленВДокумент = Истина Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Возвращает реквизиты с аналогичными данными но другой ролью в документе.
// 
// Параметры:
//  ПроверяемыйРеквизит - СтрокаТаблицыЗначений - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
//  РаспознанныеДанные - Структура - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
//  ТолькоЗаполненные - Булево - учитывать только с заполненным распознанным значением
//  УчестьТип - Булево - учитывать совпадающие по типу реквизиты
// 
// Возвращаемое значение:
//  Массив из СтрокаТаблицыЗначений - данные реквизитов с другой ролью в документе
Функция АналогиСДругойРольюВДокументе(Знач ПроверяемыйРеквизит, Знач РаспознанныеДанные, Знач ТолькоЗаполненные = Ложь, 
																					УчестьТип = Ложь)
	
	Аналоги = Новый Массив;
	
	Если ЗначениеЗаполнено(ПроверяемыйРеквизит.РаспознанноеЗначение) Тогда
		
		Отбор = Новый Структура("РаспознанноеЗначение", ПроверяемыйРеквизит.РаспознанноеЗначение);
		НСтр = РаспознанныеДанные.ТаблицаРеквизитов.НайтиСтроки(Отбор);
		
		Для Каждого НайденнаяСтрока Из НСтр Цикл
			
			Если НайденнаяСтрока.ИмяРеквизита = ПроверяемыйРеквизит.ИмяРеквизита Тогда
				Продолжить;
			КонецЕсли;
			
			Если Аналоги.Найти(НайденнаяСтрока) = Неопределено Тогда
				Аналоги.Добавить(НайденнаяСтрока);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	КлючевыеРеквизиты = РаспознаваниеДокументовСлужебный.КлючевыеРеквизиты(ПроверяемыйРеквизит.ИмяРеквизита, 
		РаспознанныеДанные.ТипФормализованногоДокумента);
	
	КлючевыеРеквизитыДругихСторон = Новый Массив;
		
	// проверим среди распознанных реквизитов - добавлены ли в документ
	Для Каждого КлючевойРеквизит Из КлючевыеРеквизиты Цикл
		
		Отбор = Новый Структура;
		Отбор.Вставить("ИмяРеквизита", КлючевойРеквизит);
		
		ДанныеРеквизита = РаспознанныеДанные.ТаблицаРеквизитов.НайтиСтроки(Отбор);
		ЗначениеРеквизита = Неопределено;
		
		Если ДанныеРеквизита.Количество() > 0 Тогда
			ЗначениеРеквизита = ДанныеРеквизита[0].РаспознанноеЗначение;
		КонецЕсли;
	
		Если Не ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		
		// найдем такие же по значению 
		Отбор = Новый Структура;
		Отбор.Вставить("РаспознанноеЗначение", ЗначениеРеквизита);
		
		НайденныеРеквизитыПоЗначению = РаспознанныеДанные.ТаблицаРеквизитов.НайтиСтроки(Отбор);
		
		Если НайденныеРеквизитыПоЗначению.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого Реквизит Из НайденныеРеквизитыПоЗначению Цикл
			
			Если КлючевыеРеквизиты.Найти(Реквизит.ИмяРеквизита) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			// нашли реквизит с таким же значением
			КлючевыеРеквизитыДругихСторон.Добавить(Реквизит);
			 
		КонецЦикла;
		
	КонецЦикла;
	
	Если КлючевыеРеквизитыДругихСторон.Количество() = 0 Тогда // нет сторон с такими же значениями ключевых реквизитов
		// проверим по распознанному тексту
		Если Не ПустаяСтрока(ПроверяемыйРеквизит.РаспознанныйТекст) Тогда
			Отбор = Новый Структура;
			Отбор.Вставить("РаспознанныйТекст", ПроверяемыйРеквизит.РаспознанныйТекст);
			НайденныеРеквизитыПоЗначению = РаспознанныеДанные.ТаблицаРеквизитов.НайтиСтроки(Отбор);
			
			Если НайденныеРеквизитыПоЗначению.Количество() > 0 Тогда
				
				Для Каждого НайденныйРеквизит Из НайденныеРеквизитыПоЗначению Цикл
					
					Если НайденныйРеквизит.ИмяРеквизита = ПроверяемыйРеквизит.ИмяРеквизита Тогда
						Продолжить;
					КонецЕсли;
					
					Если Аналоги.Найти(НайденныйРеквизит) <> Неопределено Тогда
						Продолжить;
					КонецЕсли;
					
					Если УчестьТип = Истина И ТипЗнч(НайденныйРеквизит.РаспознанноеЗначение) 
						<> ТипЗнч(ПроверяемыйРеквизит.РаспознанноеЗначение) Тогда
						Продолжить;
					КонецЕсли;
					
					Если ТолькоЗаполненные И Не ЗначениеЗаполнено(НайденныйРеквизит.РаспознанноеЗначение) Тогда
						Продолжить;
					КонецЕсли;
						
					Аналоги.Добавить(НайденныйРеквизит);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат Аналоги;
		
	КонецЕсли;
	
	ВсеСтороны = ВозможныеЗначенияРеквизитовСторон(, РаспознанныеДанные.ТипФормализованногоДокумента);
	
	Для Каждого Сторона Из ВсеСтороны Цикл
		
		КлючевыеРеквизиты = РаспознаваниеДокументовСлужебный.КлючевыеРеквизиты(Сторона.ИмяРаспознанногоРеквизита, 
			РаспознанныеДанные.ТипФормализованногоДокумента);
	
		Для Каждого Реквизит Из КлючевыеРеквизитыДругихСторон Цикл
			
			Если КлючевыеРеквизиты.Найти(Реквизит.ИмяРеквизита) <> Неопределено Тогда
				
				НСтр = РаспознанныеДанные.ТаблицаРеквизитов.Найти(Сторона.ИмяРаспознанногоРеквизита, "ИмяРеквизита");
				
				Если Аналоги.Найти(НСтр) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Если УчестьТип = Истина И ТипЗнч(НСтр.РаспознанноеЗначение) 
					<> ТипЗнч(ПроверяемыйРеквизит.РаспознанноеЗначение) Тогда
					Продолжить;
				КонецЕсли;
				
				Если ТолькоЗаполненные И Не ЗначениеЗаполнено(НСтр.РаспознанноеЗначение) Тогда
					Продолжить;
				КонецЕсли;
				
				Аналоги.Добавить(НСтр);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Аналоги;
	
КонецФункции

// Добавить значение реквизита табличной части.
// 
// Параметры:
//  ПараметрыДобавления - Структура - Параметры добавления:
// * ИмяТаблицы - Строка - 
// * ПараметрыСопоставления - Структура, Неопределено - 
// * ДокументОбъект - СправочникОбъект.ДокументыПредприятия - 
// * ПорогУверенностиДляПодстановки - Число - мин. порог от 0 до 100 по Д-Винклеру для заполнения реквизита
// * ДанныеРеквизита - СтрокаТаблицыЗначений - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
//  НеобходимаВерификацияСумм - Булево - признак необходимости верификации сумм документа
Процедура ДобавитьЗначениеРеквизитаТабличнойЧасти(Знач ПараметрыДобавления, Знач НеобходимаВерификацияСумм)
	
	ДанныеРеквизита = ПараметрыДобавления.ДанныеРеквизита;
	
	Если ДанныеРеквизита.НомерСтрокиТаблицы = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРеквизита = ДанныеРеквизита.ИмяРеквизита;
	ДокументОбъект = ПараметрыДобавления.ДокументОбъект;
	ИмяТаблицы = ПараметрыДобавления.ИмяТаблицы;
	ПорогУверенностиДляПодстановки = ПараметрыДобавления.ПорогУверенностиДляПодстановки;
	
	НомерСтроки = ДанныеРеквизита.НомерСтрокиТаблицы - 1;
	
	СтрокаТЧ = ДокументОбъект[ИмяТаблицы][НомерСтроки];
	
	ЭтоПримитивныйТип = 
				РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(ТипЗнч(ДанныеРеквизита.РаспознанноеЗначение));
	
	Если ЭтоПримитивныйТип
			ИЛИ ДанныеРеквизита.УверенностьРаспознанногоЗначения >= ПорогУверенностиДляПодстановки Тогда
			
		СтрокаТЧ[ИмяРеквизита] = ДанныеРеквизита.РаспознанноеЗначение;
			
	КонецЕсли;
	
	ПараметрыВерификации = ПараметрыВерификации();
	ПараметрыВерификации.ИмяРеквизита = ИмяРеквизита;
	ПараметрыВерификации.ТабличнаяЧасть = ИмяТаблицы;
	ПараметрыВерификации.ДанныеРаспознанногоРеквизита = ДанныеРеквизита;
	ПараметрыВерификации.Идентификатор = СтрокаТЧ.Идентификатор;
	ПараметрыВерификации.ВерифицироватьСуммы = НеобходимаВерификацияСумм;
	ПараметрыВерификации.УстановленноеЗначение = СтрокаТЧ[ИмяРеквизита];
	
	ЗаписатьПараметрыВерификацииРеквизита(ДокументОбъект, ПараметрыВерификации);
	
КонецПроцедуры

// Возвращает возможные имена реквизитов сторон документа
// 
// Параметры:
//  НаправлениеДокумента - Неопределено, ПеречислениеСсылка.НаправленияЭДО - Направление документа
//  ТипДокумента - Неопределено, ПеречислениеСсылка.ТипыФормализованныхДокументов - Тип документа
//  ДляОрганизации - Булево - логика только для организации
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Имена реквизитов сторон:
// * ИмяРаспознанногоРеквизита - Строка
// * Приоритет - Число - 1 - приоритет при подстановке, 1- самый высокий
Функция ВозможныеЗначенияРеквизитовСторон(НаправлениеДокумента = Неопределено, ТипДокумента = Неопределено, ДляОрганизации = Ложь)
	
	ИменаСторон = Новый ТаблицаЗначений;
	ИменаСторон.Колонки.Добавить("ИмяРаспознанногоРеквизита", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ИменаСторон.Колонки.Добавить("Приоритет", ОбщегоНазначения.ОписаниеТипаЧисло(3));
	
	Если НаправлениеДокумента = Перечисления.НаправленияЭДО.Входящий Тогда
		
		Если ДляОрганизации Тогда
			
			ДобавитьВТаблицуРеквизитов(ИменаСторон, "Покупатель", 1);
			ДобавитьВТаблицуРеквизитов(ИменаСторон, "Грузополучатель", 2);
			ДобавитьВТаблицуРеквизитов(ИменаСторон, "Исполнитель", 3);

		Иначе
			
			ДобавитьВТаблицуРеквизитов(ИменаСторон, "Продавец", 1);
			ДобавитьВТаблицуРеквизитов(ИменаСторон, "Грузоотправитель", 2);
		
		КонецЕсли;
		
	ИначеЕсли НаправлениеДокумента = Перечисления.НаправленияЭДО.Исходящий Тогда
		
		Если ДляОрганизации Тогда
			
			ДобавитьВТаблицуРеквизитов(ИменаСторон, "Продавец", 1);
			ДобавитьВТаблицуРеквизитов(ИменаСторон, "Грузоотправитель", 2);
			
		Иначе
		
			ДобавитьВТаблицуРеквизитов(ИменаСторон, "Покупатель", 1);
			ДобавитьВТаблицуРеквизитов(ИменаСторон, "Грузополучатель", 2);
			ДобавитьВТаблицуРеквизитов(ИменаСторон, "Исполнитель", 3);
			
		КонецЕсли;
		
	Иначе
		
		ДобавитьВТаблицуРеквизитов(ИменаСторон, "Покупатель", 1);
		ДобавитьВТаблицуРеквизитов(ИменаСторон, "Продавец", 1);
		ДобавитьВТаблицуРеквизитов(ИменаСторон, "Грузоотправитель", 1);
		ДобавитьВТаблицуРеквизитов(ИменаСторон, "Грузополучатель", 1);
		ДобавитьВТаблицуРеквизитов(ИменаСторон, "Исполнитель", 1);

	КонецЕсли;
	
	Возврат ИменаСторон;
	
КонецФункции

// Добавляет данные в таблицу.
// 
// Параметры:
//  ТаблицаРеквизитов - ТаблицаЗначений - Таблица реквизитов:
// * ИмяРаспознанногоРеквизита - Строка
// * Приоритет - Число
//  ИмяРеквизита - Строка - Имя реквизита
//  Приоритет - Число - Приоритет
Процедура ДобавитьВТаблицуРеквизитов(ТаблицаРеквизитов, ИмяРеквизита, Приоритет)
	
	Нстр = ТаблицаРеквизитов.Добавить();
	Нстр.ИмяРаспознанногоРеквизита = ИмяРеквизита;
	Нстр.Приоритет = Приоритет;
	
КонецПроцедуры


// Инициализирет структуру данных для распознанных реквизитов документа.
// 
// Параметры:
//  СоответствиеРеквизитов - ТаблицаЗначений 
// Возвращаемое значение:
//  Структура - Данные необходимые для заполнения реквизитов документа
Функция ДанныеРеквизитовДокумента(Знач СоответствиеРеквизитов)
	
	ТаблицаРеквизитов = Новый ТаблицаЗначений;
	ТаблицаРеквизитов.Колонки.Добавить("ИмяРеквизита", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаРеквизитов.Колонки.Добавить("КоординатаX0", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ТаблицаРеквизитов.Колонки.Добавить("КоординатаX1", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ТаблицаРеквизитов.Колонки.Добавить("КоординатаY0", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ТаблицаРеквизитов.Колонки.Добавить("КоординатаY1", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ТаблицаРеквизитов.Колонки.Добавить("ОбластьИзображения", Новый ОписаниеТипов("ХранилищеЗначения"));
	ТаблицаРеквизитов.Колонки.Добавить("РаспознанноеЗначение", 
		Метаданные.ОпределяемыеТипы.РаспознаваемыеРеквизитыДокумента.Тип);
	ТаблицаРеквизитов.Колонки.Добавить("РаспознанныйТекст", ОбщегоНазначения.ОписаниеТипаСтрока(1000));
	ТаблицаРеквизитов.Колонки.Добавить("СтрокВИзображении", ОбщегоНазначения.ОписаниеТипаЧисло(3, 0));
	ТаблицаРеквизитов.Колонки.Добавить("УверенностьРаспознанногоЗначения", ОбщегоНазначения.ОписаниеТипаЧисло(3, 0));
	ТаблицаРеквизитов.Колонки.Добавить("ЭлементСоздан", Новый ОписаниеТипов("Булево"));
	ТаблицаРеквизитов.Колонки.Добавить("ДобавленВДокумент", Новый ОписаниеТипов("Булево"));
	
	ТаблицаРеквизитовТЧ = Новый ТаблицаЗначений;
	ТаблицаРеквизитовТЧ.Колонки.Добавить("ИмяРеквизита", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаРеквизитовТЧ.Колонки.Добавить("ИмяТаблицы", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаРеквизитовТЧ.Колонки.Добавить("НомерСтрокиТаблицы", ОбщегоНазначения.ОписаниеТипаЧисло(5, 0));
	ТаблицаРеквизитовТЧ.Колонки.Добавить("КоординатаX0", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ТаблицаРеквизитовТЧ.Колонки.Добавить("КоординатаX1", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ТаблицаРеквизитовТЧ.Колонки.Добавить("КоординатаY0", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ТаблицаРеквизитовТЧ.Колонки.Добавить("КоординатаY1", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ТаблицаРеквизитовТЧ.Колонки.Добавить("ОбластьИзображения", Новый ОписаниеТипов("ХранилищеЗначения"));
	ТаблицаРеквизитовТЧ.Колонки.Добавить("РаспознанноеЗначение", 
		Метаданные.ОпределяемыеТипы.РаспознаваемыеРеквизитыДокумента.Тип);
	ТаблицаРеквизитовТЧ.Колонки.Добавить("РаспознанныйТекст", ОбщегоНазначения.ОписаниеТипаСтрока(1000));
	ТаблицаРеквизитовТЧ.Колонки.Добавить("СтрокВИзображении", ОбщегоНазначения.ОписаниеТипаЧисло(3, 0));
	ТаблицаРеквизитовТЧ.Колонки.Добавить("УверенностьРаспознанногоЗначения", ОбщегоНазначения.ОписаниеТипаЧисло(3, 0));
	ТаблицаРеквизитовТЧ.Колонки.Добавить("ЭлементСоздан", Новый ОписаниеТипов("Булево"));
	ТаблицаРеквизитовТЧ.Колонки.Добавить("ДобавленВДокумент", Новый ОписаниеТипов("Булево"));
	
	Для Каждого ДанныеСопоставления Из СоответствиеРеквизитов Цикл
		
		Если ДанныеСопоставления.ЭтоРеквизитТабличнойЧасти Тогда
			
			НомерСтроки = 1;
			
			Реквизит = РаспознаваниеДокументовСлужебный
				.РеквизитТаблицыДокумента(ТаблицаРеквизитовТЧ, НомерСтроки, ДанныеСопоставления.ИмяРеквизита);
			Реквизит.РаспознанноеЗначение = ДанныеСопоставления.ОписаниеТипа.ПривестиЗначение();
			
			Реквизит.ИмяТаблицы = "Товары";
			
		Иначе
			
			Реквизит = РаспознаваниеДокументовСлужебный
				.РеквизитДокумента(ТаблицаРеквизитов, ДанныеСопоставления.ИмяРеквизита);
			Реквизит.РаспознанноеЗначение = ДанныеСопоставления.ОписаниеТипа.ПривестиЗначение();
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ТаблицаРеквизитов", ТаблицаРеквизитов);
	СтруктураВозврата.Вставить("ТаблицаРеквизитовТЧ", ТаблицаРеквизитовТЧ);
	СтруктураВозврата.Вставить("ТипФормализованногоДокумента", Неопределено);
	СтруктураВозврата.Вставить("НаправлениеДокумента", Неопределено);
	СтруктураВозврата.Вставить("ВладелецНоменклатуры", Неопределено);
	СтруктураВозврата.Вставить("ВидДокументаКеш", Неопределено);
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Выполняет рекурсивное заполнение реквизитов документа.
// 
// Параметры:
//  СоответствиеJSON - Соответствие из Строка
//  АдресJSON - Строка - Адрес JSON
//  СоответствиеРеквизитов - ТаблицаЗначений - Соответствие реквизитов:
// * ИмяРеквизита - Строка
// * ОписаниеТипа - ОпределяемыйТип.РаспознаваемыеРеквизитыДокумента - в зависимости от типа реквизита
// * Адрес - Строка
// * НеОтображать - Булево - не используется
// * НетНаПечатнойФорме - Булево - не используется
// * ЭтоРеквизитТабличнойЧасти - Булево
// * КлассСущности - Строка
// * ПодклассСущности - Строка
// * Путь - Строка
//  РеквизитыДокумента - Структура - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
//  ТабличнаяЧасть - Неопределено - Табличная часть
//  НомерСтроки - Число - Номер строки табличной части
//  ПредСоответствиеJSON - Структура, Неопределено - Пред соответствие JSON
Процедура ЗаполнитьРеквизитыРекурсивно(СоответствиеJSON, АдресJSON, СоответствиеРеквизитов, 
		РеквизитыДокумента, ТабличнаяЧасть = Неопределено, НомерСтроки = 0, Знач ПредСоответствиеJSON = Неопределено)
	
	Перем СтрТабРек;
	
	Для Каждого ТекСоотв Из СоответствиеJSON Цикл
		Если ТипЗнч(ТекСоотв.Значение) = Тип("Соответствие") Тогда
			
			Если ТекСоотв.Значение["type"] = "container" ИЛИ НЕ ТекСоотв.Значение.Получить("child") = Неопределено Тогда
				
				Если ТекСоотв.Значение.Получить("postprocessed") = Истина Тогда
					ЗаполнитьЗначенияРеквизита(СтрТабРек, АдресJSON + СокрЛП(ТекСоотв.Ключ) + ".", 
						РеквизитыДокумента, НомерСтроки, СоответствиеРеквизитов, ТабличнаяЧасть, ТекСоотв.Значение, 
							СоответствиеJSON);
				КонецЕсли;
				НовСоответствие = ТекСоотв.Значение["child"];
				ПредСоответствиеJSON = ТекСоотв;
				
			ИначеЕсли ТекСоотв.Значение["type"] = "table" Тогда
				
				ДанныеСтрок = ТекСоотв.Значение["rows"];
				АдресJSON = ТекСоотв.Ключ + ".Строка.";
				ИмяТаблицы = ТекСоотв.Ключ;
				
				РаспознаваниеДокументовСлужебный.ПриФормированииДанныхТабличныхЧастей(
					РеквизитыДокумента.ТипФормализованногоДокумента, ИмяТаблицы, АдресJSON, ДанныеСтрок);
				
				Для Каждого СтрокаТаблицы Из ДанныеСтрок Цикл
					ЗаполнитьРеквизитыРекурсивно(СтрокаТаблицы, АдресJSON, СоответствиеРеквизитов, 
					РеквизитыДокумента, ИмяТаблицы, ДанныеСтрок.Найти(СтрокаТаблицы) + 1);
				КонецЦикла;
				
				ДанныеПодвала = ТекСоотв.Значение["footer"];
				ИмяТаблицы = ТекСоотв.Ключ;
				
				РаспознаваниеДокументовСлужебный.ПриФормированииДанныхТабличныхЧастей(
					РеквизитыДокумента.ТипФормализованногоДокумента, ИмяТаблицы, АдресJSON, ДанныеСтрок);
				
				Для Каждого СтрокаТаблицы Из ДанныеПодвала Цикл
					АдресJSON = ИмяТаблицы + "." + СтрокаТаблицы.Ключ + ".";
					ЗаполнитьРеквизитыРекурсивно(СтрокаТаблицы.Значение, АдресJSON, 
						СоответствиеРеквизитов, РеквизитыДокумента);
				КонецЦикла;
				
				АдресJSON = "";
				Продолжить;
				
			Иначе
				НовСоответствие = ТекСоотв.Значение;
			КонецЕсли;
			
			ЗаполнитьРеквизитыРекурсивно(НовСоответствие, АдресJSON + СокрЛП(ТекСоотв.Ключ) + ".", 
				СоответствиеРеквизитов, РеквизитыДокумента, ТабличнаяЧасть, НомерСтроки, ПредСоответствиеJSON);
				
		Иначе
			
			ЗаполнитьЗначенияРеквизита(СтрТабРек, АдресJSON, РеквизитыДокумента, НомерСтроки, 
				СоответствиеРеквизитов, ТабличнаяЧасть, СоответствиеJSON, ПредСоответствиеJSON);
				
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполнияет значения реквизита.
// 
// Параметры:
//  СтрТабРек - Неопределено
//  АдресJSON - Строка, Число - Адрес JSON
//  РеквизитыДокумента - Структура - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения:
//  НомерСтроки - Число - Номер строки
//  СоответствиеРеквизитов - ТаблицаЗначений - Соответствие реквизитов:
// * ИмяРеквизита - Строка - 
// * ОписаниеТипа - ОпределяемыйТип.РаспознаваемыеРеквизитыДокумента - 
// * Адрес - Строка - 
// * НеОтображать - Булево - 
// * НетНаПечатнойФорме - Булево - 
// * ЭтоРеквизитТабличнойЧасти - Булево - 
// * КлассСущности - Строка - 
// * ПодклассСущности - Строка - 
// * Путь - Строка - 
//  ТабличнаяЧасть - Неопределено - Табличная часть
//  СоответствиеJSON - Соответствие из Строка - Соответствие JSON
//  ПредСоответствиеJSON - Соответствие из Строка
Процедура ЗаполнитьЗначенияРеквизита(СтрТабРек, Знач АдресJSON, РеквизитыДокумента, Знач НомерСтроки, 
	Знач СоответствиеРеквизитов, Знач ТабличнаяЧасть, Знач СоответствиеJSON, Знач ПредСоответствиеJSON = Неопределено)
	
	Если СтрТабРек <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекАдрес = Лев(АдресJSON, СтрДлина(АдресJSON) - 1); // Убираем точку в конце

	СтрокиОтбора = СоответствиеРеквизитов.НайтиСтроки(Новый Структура("Адрес", ТекАдрес));
	
	Для Каждого ТекРеквизит Из СтрокиОтбора Цикл
		
		Если ТабличнаяЧасть = Неопределено Тогда
			Отбор = Новый Структура("ИмяРеквизита", ТекРеквизит.ИмяРеквизита);
			НайденныеСтроки = РеквизитыДокумента.ТаблицаРеквизитов.НайтиСтроки(Отбор);
			СтрТабРек = НайденныеСтроки[0];
		Иначе
			Если НомерСтроки = 1 Тогда
				Отбор = Новый Структура("НомерСтрокиТаблицы, ИмяРеквизита", 1, ТекРеквизит.ИмяРеквизита);
				СтрТабРек = РеквизитыДокумента.ТаблицаРеквизитовТЧ.НайтиСтроки(Отбор)[0];
			Иначе
				СтрТабРек = РеквизитыДокумента.ТаблицаРеквизитовТЧ.Добавить();
				СтрТабРек.ИмяТаблицы = "Товары";
			КонецЕсли;
			СтрТабРек.НомерСтрокиТаблицы = НомерСтроки;
		КонецЕсли;
		
		СтрТабРек.ИмяРеквизита = ТекРеквизит.ИмяРеквизита;
		СтрТабРек.РаспознанноеЗначение = ТекРеквизит.ОписаниеТипа.ПривестиЗначение();
		
		Для Каждого ТекСоотв Из СоответствиеJSON Цикл
			Если ТипЗнч(ТекСоотв.Значение) = Тип("Соответствие") Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТекСоотв.Ключ = "text" Тогда
				СтрТабРек.РаспознанныйТекст = ТекСоотв.Значение;
				ТекТипРеквизита = ТипЗнч(СтрТабРек.РаспознанноеЗначение);
				Если РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(ТекТипРеквизита) Тогда
					Если ТекТипРеквизита = Тип("Строка") Тогда
						СтрТабРек.РаспознанноеЗначение = СокрЛП(ТекСоотв.Значение);
					Иначе
						СтрТабРек.РаспознанноеЗначение = РаспознаваниеДокументовСериализацияСлужебныйКлиентСервер.ПривестиТип(
							ТекСоотв.Значение, ТекТипРеквизита);
					КонецЕсли;
				КонецЕсли; 
			ИначеЕсли ТекСоотв.Ключ = "chunk_img" Тогда
				Если ТекСоотв.Значение = Неопределено Тогда
					Если ПредСоответствиеJSON = Неопределено Или ПредСоответствиеJSON.Значение["chunk_img"] = Неопределено Тогда
						СтрТабРек.СтрокВИзображении = 0;
					Иначе 
						СтрТабРек.ОбластьИзображения = РаспознаваниеДокументовКоннекторСлужебный.ИзвлечьДанныеКартинки(
							ПредСоответствиеJSON.Значение["chunk_img"]);
					КонецЕсли;
				Иначе
					СтрТабРек.ОбластьИзображения = РаспознаваниеДокументовКоннекторСлужебный.ИзвлечьДанныеКартинки(
						ТекСоотв.Значение);
				КонецЕсли;
			ИначеЕсли ТекСоотв.Ключ = "num_lines" Тогда
				СтрТабРек.СтрокВИзображении = ТекСоотв.Значение;
			ИначеЕсли ТекСоотв.Ключ = "bbox" Тогда
				Если ТипЗнч(ТекСоотв.Значение) = Тип("Массив") И ТекСоотв.Значение.Количество() = 4 Тогда
					СтрТабРек.КоординатаX0 = ТекСоотв.Значение[0];
					СтрТабРек.КоординатаY0 = ТекСоотв.Значение[1];
					СтрТабРек.КоординатаX1 = ТекСоотв.Значение[2];
					СтрТабРек.КоординатаY1 = ТекСоотв.Значение[3];
				КонецЕсли;
			КонецЕсли;
				
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает результат распознавания по идентификатору.
// 
// Параметры:
//  ИдентификаторДокумента - Строка - Идентификатор документа
// 
// Возвращаемое значение: 
//  Структура: 
// * Данные - Структура
// * Загружен - Булево - 
// * ТипФормализованногоДокумента - ПеречислениеСсылка.ТипыФормализованныхДокументов, Неопределено - 
// * ОписаниеОшибки - Строка
Функция РезультатПоИдентификатору(Знач ИдентификаторДокумента)
	
	// сначала из регистра
	Результат = РегистрыСведений.РезультатыРаспознаванияДокументов.
		РезультатПоИдентификатору(ИдентификаторДокумента);
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Данные");
	СтруктураВозврата.Вставить("Загружен", Ложь);
	СтруктураВозврата.Вставить("ТипФормализованногоДокумента", Неопределено);
	СтруктураВозврата.Вставить("ОписаниеОшибки", "");
	
	Если Результат <> Неопределено Тогда
		
		СтруктураВозврата.Данные = Результат;
		СтруктураВозврата.Загружен = Истина;
		СтруктураВозврата.ТипФормализованногоДокумента = ТипФормализованногоДокумента(Результат);

	Иначе

		Результат = РаспознаваниеДокументовКоннекторСлужебный.
			РезультатПоИдентификатору(ИдентификаторДокумента);
		
		Если Результат.КодСостояния <> 200 Тогда
			
			ТекстОшибки = НСтр("ru = 'Не удалось получить результат по идентификатору документа. Код состояния: %1'", 
					ОбщегоНазначения.КодОсновногоЯзыка());
					
			СтруктураВозврата.ОписаниеОшибки = СтрШаблон(ТекстОшибки, Результат.КодСостояния);
				
			Возврат СтруктураВозврата;
			
		КонецЕсли;
		
		ТипФормализованногоДокумента = ТипФормализованногоДокумента(Результат);
		
		Если ТипФормализованногоДокумента <> Неопределено Тогда // универсальный формат
			
			СтруктураВозврата.ТипФормализованногоДокумента = ТипФормализованногоДокумента;
			
		КонецЕсли;
			
		СтруктураВозврата.Данные = Результат;
		
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Возвращает тип формализованного документа из набора типов распознаваемых сервисом
// 
// Параметры:
//  ДанныеДокумента - Структура
// 
// Возвращаемое значение:
//  Неопределено, Произвольный, ПеречислениеСсылка.ТипыФормализованныхДокументов - Тип формализованного документа
Функция ТипФормализованногоДокумента(Знач ДанныеДокумента)
	
	ДесериализованноеЗначение = Неопределено;
	Если ТипЗнч(ДанныеДокумента) = Тип("Структура") И ДанныеДокумента.Свойство("ДесериализованноеЗначение",
		ДесериализованноеЗначение) Тогда 
		ДесериализованноеЗначение = ДесериализованноеЗначение.Получить("result");
		Если ТипЗнч(ДесериализованноеЗначение) <> Тип("Соответствие") Тогда
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

	Если ТипЗнч(ДесериализованноеЗначение.Получить("document")) = Тип("Соответствие") Тогда
		ТипДокумента = ДесериализованноеЗначение["document"].Получить("type");
	Иначе
		ТипДокумента = Неопределено;
	КонецЕсли;
	
	СоответствиеТипов = СоответствиеВидовРаспознаваемыхДокументов();
	
	ТипСсылкой = СоответствиеТипов.Получить(ТипДокумента);
	
	Если ТипСсылкой <> Перечисления.ТипыФормализованныхДокументов.НеопределенныйДокумент Тогда
		Возврат ТипСсылкой;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет вид документа по типу данных и направлению
// 
// Параметры:
//  ТипФормализованногоДокумента - ПеречислениеСсылка.ТипыФормализованныхДокументов
//  Направление - ПеречислениеСсылка.НаправленияЭДО
// 
// Возвращаемое значение:
//  Структура - Вид формализованного документа:
// * ВидДокумента - СправочникСсылка.ВидыДокументов - 
// * Тематика - СправочникСсылка.ТематикиДокументов - 
// * ШаблонДокумента - СправочникСсылка.ШаблоныДокументов - 
Функция ВидФормализованногоДокумента(ТипФормализованногоДокумента)
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос("ВЫБРАТЬ
	|	СоответствиеВидовРаспознаваемыхДокументов.ВидДокумента КАК ВидДокумента,
	|	СоответствиеВидовРаспознаваемыхДокументов.Тематика КАК Тематика,
	|	СоответствиеВидовРаспознаваемыхДокументов.Шаблон КАК ШаблонДокумента
	|ИЗ
	|	РегистрСведений.СоответствиеВидовРаспознаваемыхДокументов КАК СоответствиеВидовРаспознаваемыхДокументов
	|ГДЕ
	|	СоответствиеВидовРаспознаваемыхДокументов.ТипФормализованногоДокумента = &ТипДокумента");

	Запрос.УстановитьПараметр("ТипДокумента", ТипФормализованногоДокумента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВидТематика = Новый Структура("ВидДокумента, Тематика, ШаблонДокумента");
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ВидТематика, Выборка);
	КонецЕсли;
	
	Возврат ВидТематика;
	
КонецФункции

// Файлы документов для распознавания.
// 
// Возвращаемое значение:
//  Структура - Файлы документов для распознавания:
// * Выгрузка - ТаблицаЗначений
// * Загрузка - ТаблицаЗначений
Функция ФайлыДокументовДляРаспознавания()
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ФайлыДокументовНаРаспознавании.Файл КАК Файл,
		|	ФайлыДокументовНаРаспознавании.ИдентификаторЗадания КАК ИдентификаторЗадания,
		|	ФайлыДокументовНаРаспознавании.ИдентификаторФайла КАК ИдентификаторФайла
		|ПОМЕСТИТЬ ВТ_ВсеФайлы
		|ИЗ
		|	РегистрСведений.ФайлыДокументовНаРаспознавании КАК ФайлыДокументовНаРаспознавании
		|ГДЕ
		|	ФайлыДокументовНаРаспознавании.КоличествоПопыток < &МаксимальноеКоличествоПопыток
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ВсеФайлы.Файл КАК Файл,
		|	ВТ_ВсеФайлы.ИдентификаторЗадания КАК ИдентификаторЗадания,
		|	ВТ_ВсеФайлы.ИдентификаторФайла КАК ИдентификаторФайла
		|ИЗ
		|	ВТ_ВсеФайлы КАК ВТ_ВсеФайлы
		|ГДЕ
		|	ВТ_ВсеФайлы.ИдентификаторФайла = """"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ВсеФайлы.Файл КАК Файл,
		|	ВТ_ВсеФайлы.ИдентификаторЗадания КАК ИдентификаторЗадания,
		|	ВТ_ВсеФайлы.ИдентификаторФайла КАК ИдентификаторФайла
		|ИЗ
		|	ВТ_ВсеФайлы КАК ВТ_ВсеФайлы
		|ГДЕ
		|	ВТ_ВсеФайлы.ИдентификаторФайла <> """"");
	
	Запрос.УстановитьПараметр("МаксимальноеКоличествоПопыток",
		РегистрыСведений.ФайлыДокументовНаРаспознавании.МаксимальноеКоличествоНеудачныхПопытокОбработки() );
	Результат = Запрос.ВыполнитьПакет();
	СтруктураВозврата = Новый Структура("Выгрузка, Загрузка", Результат[1].Выгрузить(), Результат[2].Выгрузить());
	Возврат СтруктураВозврата;
	
КонецФункции

// Подбирает значения реквизитов из базы данных и записывает распознанные данные в регистры
// 
// Параметры:
//  Файл - СправочникСсылка.ВерсииФайлов - Файл
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия, Произвольный - Документ ссылка
//  РаспознанныеДанные - Структура - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
Процедура ПодобратьЗначенияИЗаписатьРаспознанныеДанные(Знач Файл, ДокументСсылка, РаспознанныеДанные)
	
	РаспознаваниеДокументовСлужебный.ДобавитьВариантыНайденныеПоискомПоНесколькимРеквизитам(
		РаспознанныеДанные, ДокументСсылка, Истина);
		
	РегистрыСведений.РаспознанныеДанные.ЗаписатьДанные(Файл, 
		РаспознанныеДанные.ТаблицаРеквизитов);
	РегистрыСведений.РаспознанныеДанныеТаблиц.ЗаписатьДанные(Файл, 
		РаспознанныеДанные.ТаблицаРеквизитовТЧ, "Товары");
		
КонецПроцедуры

// Проверяет необходимость записи реквизитов с обходом проверок.
// 
// Параметры:
//  ДокументОбъект - СправочникОбъект.ДокументыПредприятия
// 
// Возвращаемое значение:
//  Булево - Запись реквизитов распознаваемого документа
Функция ЗаписьРеквизитовДокументаСОбходомПроверок(ДокументОбъект)
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("ДокументНеРаспознан") Тогда
		
		Возврат Истина;
		
	Иначе
		
		СтатусРаспознавания = Неопределено;
		
		ПометкаНаУдалениеПроцессНеЗавершен = 
			(ДокументОбъект.ДополнительныеСвойства.Свойство("СтатусРаспознавания", СтатусРаспознавания) 
				И (СтатусРаспознавания = Перечисления.СтатусыРаспознаванияДокументов.НаРаспознавании 
				ИЛИ СтатусРаспознавания = Перечисления.СтатусыРаспознаванияДокументов.Прервано) 
				И ДокументОбъект.ПометкаУдаления);
		
		ДокументОтправленНаРаспознавание = Ложь;
		// если это карточка распознаваемого документа в статусе отправки на распознавание или пометка на удаление
		Если ДокументОбъект.ДополнительныеСвойства.Свойство("ДокументОтправленНаРаспознавание", 
			ДокументОтправленНаРаспознавание) И ДокументОтправленНаРаспознавание = Истина 
			ИЛИ ПометкаНаУдалениеПроцессНеЗавершен Тогда
				Возврат Истина;
				
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат Ложь;
		
КонецФункции

// Формирует данные заполнения документа.
// 
// Параметры:
//  РаспознанныеДанные - Структура:
// * Данные - Структура - не обработанный результат распознавания
// * Загружен - Булево - признак что данные распознавания документа уже есть в базе
// * ТипФормализованногоДокумента - ПеречислениеСсылка.ТипыФормализованныхДокументов
// * ОписаниеОшибки - Строка
//  ОписаниеОшибки - Строка
// 
// Возвращаемое значение:
//  Структура - см ЗагрузкаДанныхИЗаполнениеДокумента() - ДанныеДляЗаполнения
Функция ПодготовитьДанныеДляЗаполнения(РаспознанныеДанные, ОписаниеОшибки)
	
	ДесериализованноеЗначение = РаспознанныеДанные.Данные.ДесериализованноеЗначение.Получить("result");
	
	Если ТипЗнч(ДесериализованноеЗначение) <> Тип("Соответствие") Тогда
		ВызватьИсключение НСтр("ru = 'Ожидалось, что ДесериализованноеЗначение будет типа Соответствие.'");
	КонецЕсли;
	
	Если ТипЗнч(ДесериализованноеЗначение.Получить("document")) = Тип("Соответствие") Тогда
		ТипДокумента = ДесериализованноеЗначение["document"].Получить("type");
	Иначе
		ТипДокумента = Неопределено;
	КонецЕсли;
	
	СоответствиеТипов = СоответствиеВидовРаспознаваемыхДокументов();
	
	ТипСсылкой = СоответствиеТипов.Получить(ТипДокумента);
	
	Если ТипСсылкой = Неопределено Тогда
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Идентификатор документа: %1
			           |Неизвестный тип документа.'"),
			РаспознанныеДанные.ИдентификаторЗадания);
		Возврат Неопределено;
	КонецЕсли;
	
	СвойстваШапки = ДесериализованноеЗначение["Страница"];
	СоответствияШапки = СвойстваШапки["child"];
	
	СоответствиеРеквизитов = РаспознаваниеДокументовСлужебный.ПолучитьСопоставлениеРеквизитовИСвойствJSON(ТипСсылкой);
	
	ДанныеДокумента = ДанныеРеквизитовДокумента(СоответствиеРеквизитов);
	
	ДанныеДокумента.ТипФормализованногоДокумента = ТипСсылкой;
	
	ЗаполнитьРеквизитыРекурсивно(СоответствияШапки, "", СоответствиеРеквизитов, ДанныеДокумента);
	
	Возврат ДанныеДокумента;
	
КонецФункции

Функция НаименованиеСтороны(Знач РаспознаныйРеквизит)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИмяРеквизита = СтрЗаменить(РаспознаныйРеквизит.ИмяРеквизита, "Организация", "");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	НаименованияСторон.Ссылка
	|ИЗ
	|	Справочник.НаименованияСторон КАК НаименованияСторон
	|ГДЕ
	|	НаименованияСторон.Наименование = &Наименование
	|	И НаименованияСторон.ПометкаУдаления = Ложь";
	
	Запрос.УстановитьПараметр("Наименование", ИмяРеквизита);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Справочники.НаименованияСторон.ПустаяСсылка();
	
КонецФункции

Процедура СопоставитьНоменклатуруКонтрагентов(РаспознанныеДанные)
	
	Если Не ЗначениеЗаполнено(РаспознанныеДанные.ВладелецНоменклатуры) Тогда
		Возврат;
	КонецЕсли;
	
	НомерСтроки = 0;
	СчетчикПустыхСтрок = 0;

	Отбор = Новый Структура("ИмяТаблицы", "Товары");
	
	Пока СчетчикПустыхСтрок < 3 Цикл
		
		НомерСтроки = НомерСтроки + 1;
		
		Отбор.Вставить("НомерСтрокиТаблицы", НомерСтроки);
		ДанныеСтроки = РаспознанныеДанные.ТаблицаРеквизитовТЧ.НайтиСтроки(Отбор);
		
		Если ДанныеСтроки.Количество() = 0 Тогда
			СчетчикПустыхСтрок = СчетчикПустыхСтрок + 1;
			Продолжить;
		КонецЕсли;
		
		СопоставитьНоменклатуру(ДанныеСтроки, РаспознанныеДанные.ВладелецНоменклатуры);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СопоставитьНоменклатуру(ДанныеСтрокиТаблицы, Владелец)
	
	НоваяНоменклатураКонтрагента = НоваяНоменклатураКонтрагентаПоРаспознаннымДанным(ДанныеСтрокиТаблицы, Владелец);
	
	Если НоваяНоменклатураКонтрагента = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Отбор = Новый Структура("НоменклатураКонтрагента", НоваяНоменклатураКонтрагента);
		
	СоответствияНоменклатуры = СопоставлениеНоменклатурыКонтрагентов.НайтиСоответствиеНоменклатуры(Отбор, Истина);
	
	Для Каждого СоотвествиеНоменклатуры Из СоответствияНоменклатуры Цикл
		
		Если ЗначениеЗаполнено(СоотвествиеНоменклатуры.НоменклатураИБ) Тогда
			ЗаполнитьДанныеСтрокиСопоставленнойНоменклатуройИБ(ДанныеСтрокиТаблицы, 
				СоотвествиеНоменклатуры.НоменклатураИБ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеСтрокиСопоставленнойНоменклатуройИБ(ДанныеСтрокиТаблицы, НоменклатураИБ)
	
	Для Каждого Реквизит Из ДанныеСтрокиТаблицы Цикл
		
		Если Реквизит.ИмяРеквизита = "Номенклатура" Тогда
			Реквизит.РаспознанноеЗначение = НоменклатураИБ.Номенклатура;
			Реквизит.УверенностьРаспознанногоЗначения = 
				РаспознаваниеДокументовКлиентСервер.ПорогУверенностиДляВерификации();
			Продолжить;
		КонецЕсли;
		
		Если Реквизит.ИмяРеквизита = "ЕдиницаИзмерения" И Не ЗначениеЗаполнено(Реквизит.РаспознанноеЗначение) Тогда
			Реквизит.РаспознанноеЗначение = НоменклатураИБ.Упаковка;
			Реквизит.УверенностьРаспознанногоЗначения = 
				РаспознаваниеДокументовКлиентСервер.ПорогУверенностиДляВерификации();
			Продолжить;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает признак необходимости проверки всех реквизитов документа
// 
// Параметры:
//  Документ - СправочникОбъект.ДокументыПредприятия
Процедура ЗафиксироватьДокументНеРаспознан(Документ)
	
	ПараметрыВерификации = ПараметрыВерификации();
	ПараметрыВерификации.Документ = Документ.Ссылка;
	ПараметрыВерификации.ИмяРеквизита =  РаспознаваниеДокументовСлужебныйКлиентСервер.ИмяРеквизитаДокументНеРаспознан();
	ПараметрыВерификации.ТребуетПроверки = Истина;
	
	РегистрыСведений.ВерификацияРеквизитовРаспознанныхДокументов.ДобавитьПроверяемыйРеквизит(ПараметрыВерификации);
	
КонецПроцедуры

#КонецОбласти