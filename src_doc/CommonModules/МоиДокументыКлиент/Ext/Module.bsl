////////////////////////////////////////////////////////////////////////////////
// Мои документы (клиент)
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Добавляет переданный документ в регистр "Мои документы".
//
// Параметры:
//   МассивДокументов - Массив из СправочникСсылка.ДокументыПредприятия - Содержит ссылки на добавляемые документы.
//
Процедура ДобавитьВМоиДокументы(МассивДокументов) Экспорт 
	
	КоличествоДокументов = МассивДокументов.Количество();
	Если КоличествоДокументов = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = "";
	
	МоиДокументыВызовСервера.ДобавитьВМоиДокументы(МассивДокументов);
	
	Если КоличествоДокументов = 1 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Документ ""%1"" добавлен в список ""Мои документы"".'"),
			Строка(МассивДокументов[0]));
		НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(МассивДокументов[0]);
	Иначе 
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Документы (%1) добавлены в список ""Мои документы"".'"),
			Строка(КоличествоДокументов));
		НавигационнаяСсылка = "";
	КонецЕсли;
	
	Оповестить("Запись_МоиДокументы", МассивДокументов, МассивДокументов[0]);
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Список ""Мои документы"" обновлен'"),
		НавигационнаяСсылка,
		ТекстСообщения,
		БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

// Удаляет все записи по документам из регистра "Мои документы" для текущего пользователя.
// 
// Параметры:
//  МассивДокументов - Массив из СправочникСсылка.ДокументыПредприятия
// 
Процедура УдалитьИзМоихДокументов(МассивДокументов) Экспорт
	
	КоличествоДокументов = МассивДокументов.Количество();
	Если КоличествоДокументов = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	МоиДокументыВызовСервера.УдалитьИзМоихДокументов(МассивДокументов);
	
	ТекстСообщения = "";
	Если КоличествоДокументов = 1 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Документ ""%1"" добавлен в список ""Мои документы"".'"),
			Строка(МассивДокументов[0]));
		НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(МассивДокументов[0]);
	Иначе 
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Документы (%1) добавлены в список ""Мои документы"".'"),
			Строка(КоличествоДокументов));
		НавигационнаяСсылка = "";
	КонецЕсли;
	
	Оповестить("Запись_МоиДокументы", МассивДокументов);
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Список ""Мои документы"" обновлен'"),
		НавигационнаяСсылка,
		ТекстСообщения,
		БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

#КонецОбласти