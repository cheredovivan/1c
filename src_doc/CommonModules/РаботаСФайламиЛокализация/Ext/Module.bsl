
#Область ПрограммныйИнтерфейс

// Можно ли удалить данный файл.
//
// Параметры:
//  ФайлОбъект - СправочникОбъект.Файлы
//  СтрокаОшибки - Строка
//
// Возвращаемое значение:
//  Булево - Возвращает всегда Истина для роли ПолныеПрава.
//
Функция ВозможноУдалитьФайл(ФайлОбъект, СтрокаОшибки = "") Экспорт
	
	// Локализация
	ВладелецФайла = ФайлОбъект.ВладелецФайла;
	Если ДелопроизводствоКлиентСервер.ЭтоДокумент(ВладелецФайла) Тогда
		Если ОбменЭДОДокументооборот.ДокументБлокированПоЭДО(ВладелецФайла, ФайлОбъект.Ссылка) Тогда
			СтрокаОшибки = НСтр("ru = 'Нельзя пометить на удаление файл документа, отправленного по ЭДО.'");
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	// Локализация Конец
	
	Возврат Истина;
	
КонецФункции

// См. РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия
Процедура ДополнитьДанныеФайлаДляОткрытия(ДанныеФайла) Экспорт

// Локализация
	ОбменСКонтрагентамиДОВызовСервера.ДополнитьДанныеФайлаДляОткрытияСведениямиЭДО(ДанныеФайла);
// Локализация Конец

КонецПроцедуры

// Получить визуализацию эп для PDF.
// 
// Параметры:
//  Параметры - Структура - См. РаботаСФайламиВызовСервера.ПолучитьВизуализациюЭпДляPDF
// 
// Возвращаемое значение:
//  СправочникСсылка.Файлы, Неопределено, ДвоичныеДанные - Получить визуализацию эп для PDF
Функция ПриПолученииВизуализацииЭПДляPDF(Параметры) Экспорт
	
	ДвоичныеДанныеНовые = Неопределено;
	
	// Локализация
	Если Параметры.НастройкиШтампаЭП.ИспользоватьВнешнийСервисВставкиЭП Тогда
		
		ПараметрыДобавленияШтамповВДокумент = Интеграция1СШтампКлиентСервер.НовыеПараметрыШтампированияФайлаДокумента();

		СтраницаВставкиШтампаЭП = Параметры.НастройкиШтампаЭП.СтраницаВставкиШтампаЭП;  
		ПоложениеНаСтранице = Параметры.НастройкиШтампаЭП.РасположениеШтампаЭП;
		
		ВариантРасположения = ВизуализацияЭПВызовСервера.ВариантРасположения1СШтамп(ПоложениеНаСтранице);
		
		ПравилаРасположения = ВизуализацияЭПВызовСервера.ПравилаРасположения1СШтамп(СтраницаВставкиШтампаЭП);
		
		ВариантРасположенияПоКоординатам = "ПоКоординатам"; // тут руками считаем.  
		
		ПараметрыШтампов = Новый Массив;
		
		НомерШтампа = 0; // сквозная нумерация
		НомерШтампаБезПоложения = 0; // только "обычные" штампы, для расчета смещения
		НакопленнаяВысотаКартинок = 0;       
		
		НакопленнаяШиринаКартинок = 0;
		НомерШтампаПоY = 0;
		ЧислоШтампов = Параметры.ДанныеОтметок.Количество();
		
		Если ЧислоШтампов = 1 Тогда                         
			
			ДанныеОтметки = Параметры.ДанныеОтметок[0];
			
			КартинкаШтампа = Новый Картинка(ДанныеОтметки.ДвоичныеДанныеОтметки, Истина);
			
			// По общим настройкам для информационной базы:
			
			МассивШтампов = Новый Массив;
			МассивШтампов.Добавить(ДанныеОтметки.ДвоичныеДанныеОтметки);

			ПараметрыШтамповЛокальный = ВизуализацияЭПВызовСервера.ПараметрыШтампов(МассивШтампов, "png");
			ПараметрыФайлаШтампа = ПараметрыШтамповЛокальный[0];
			
			ПараметрыФайлаШтампа.ПараметрыРазмещения.Расположение           = ВариантРасположения;
			ПараметрыФайлаШтампа.ПравилаРасположения.ПравилоРасположения = ПравилаРасположения;
			
			ПараметрыШтампов.Добавить(ПараметрыФайлаШтампа);
			
			НомерШтампаБезПоложения = НомерШтампаБезПоложения + 1;
			
		Иначе	
		
			Для Каждого ДанныеОтметки Из Параметры.ДанныеОтметок Цикл
				
				КартинкаШтампа = Новый Картинка(ДанныеОтметки.ДвоичныеДанныеОтметки, Истина);
				
				// По общим настройкам для информационной базы:
				
				МассивШтампов = Новый Массив;
				МассивШтампов.Добавить(ДанныеОтметки.ДвоичныеДанныеОтметки);

				ПараметрыШтамповЛокальный = ВизуализацияЭПВызовСервера.ПараметрыШтампов(МассивШтампов, "png");
				ПараметрыФайлаШтампа = ПараметрыШтамповЛокальный[0];
				
				ПараметрыФайлаШтампа.ПараметрыРазмещения.Расположение           = ВариантРасположенияПоКоординатам;
				ПараметрыФайлаШтампа.ПравилаРасположения.ПравилоРасположения = ПравилаРасположения;
				
				Координаты = ВизуализацияЭПВызовСервера.КоординатыДля1СШтампПоНастройкам(
					ДанныеОтметки.ДвоичныеДанныеОтметки, ПоложениеНаСтранице, 
					НакопленнаяВысотаКартинок, НакопленнаяШиринаКартинок,
					ЧислоШтампов, НомерШтампаПоY);
				
				ПараметрыФайлаШтампа.ПараметрыРазмещения.Координаты.Верх        = Координаты.Сверху;
				ПараметрыФайлаШтампа.ПараметрыРазмещения.Координаты.Лево        = Координаты.Слева;
				
				ПараметрыШтампов.Добавить(ПараметрыФайлаШтампа);
				
				НомерШтампаБезПоложения = НомерШтампаБезПоложения + 1;
				
			КонецЦикла;           
			
		КонецЕсли;
		
		// тут рег штампы наш
		Если ЗначениеЗаполнено(Параметры.ФайлРегистрационныйШтампОрганизации) Тогда
			
			НастройкиРегНомера = ШтрихкодированиеСервер.ПерсональныеНастройкиПоложенияРегНомера();
			
			ПравилаРасположенияРегШтамп = ВизуализацияЭПВызовСервера.ПравилаРасположения1СШтамп(НастройкиРегНомера.СтраницаВставки);
		    ВариантРасположенияРегШтамп = ВизуализацияЭПВызовСервера.ВариантРасположения1СШтамп(НастройкиРегНомера.ПоложениеНаСтранице);    
			
			МассивШтампов = Новый Массив;
			МассивШтампов.Добавить(Параметры.ДвДанныеФайлаРегШтамп);

			ПараметрыШтамповЛокальный = ВизуализацияЭПВызовСервера.ПараметрыШтампов(МассивШтампов, "png");
			ПараметрыФайлаШтампа = ПараметрыШтамповЛокальный[0];
			
			ПараметрыФайлаШтампа.Настройки.КоэффициентИзмененияРазмера = 0.5;// вместо 0.25
			
			ПараметрыФайлаШтампа.ПараметрыРазмещения.Расположение = ВариантРасположенияРегШтамп;
			ПараметрыФайлаШтампа.ПравилаРасположения.ПравилоРасположения = ПравилаРасположенияРегШтамп;
			
			ПараметрыШтампов.Добавить(ПараметрыФайлаШтампа);
			
			Если Параметры.ЕстьПоложениеРегШтампаОрганизации Тогда // тут точно X Y

				ПараметрыФайлаШтампа.ПараметрыРазмещения.Координаты.Лево = 
					Цел(Параметры.ПоложениеРегШтампаОрганизации.Слева * 3.0);
				ПараметрыФайлаШтампа.ПараметрыРазмещения.Координаты.Верх = 
					Цел(Параметры.ПоложениеРегШтампаОрганизации.Сверху * 3.0);
				
				ПравилаРасположенияТекущее = ВизуализацияЭПВызовСервера.ПравилаРасположения1СШтамп(
					Параметры.ПоложениеРегШтампаОрганизации.Страница);
				ПараметрыФайлаШтампа.ПравилаРасположения.ПравилоРасположения = ПравилаРасположенияТекущее;
				
			Иначе  // общие настройки - без координат, только угол указан, скажем "Лево верх"
					
				ПараметрыФайлаШтампа.ПараметрыРазмещения.Расположение = ВариантРасположенияРегШтамп;
				ПараметрыФайлаШтампа.ПравилаРасположения.ПравилоРасположения = ПравилаРасположенияРегШтамп;
				
			КонецЕсли;
			
		КонецЕсли;
		
		// штрихкод
		Если ЗначениеЗаполнено(Параметры.ФайлШтрихкод) Тогда    
			
			НастройкиШтрихкода = ШтрихкодированиеСервер.ПолучитьПерсональныеНастройкиПоложенияШтрихкодаНаСтранице();
			
			МассивШтампов = Новый Массив;
			МассивШтампов.Добавить(Параметры.ДвДанныеФайлаШтрихкод);

			ПараметрыШтамповЛокальный = ВизуализацияЭПВызовСервера.ПараметрыШтампов(МассивШтампов, "png");
			ПараметрыФайлаШтампа = ПараметрыШтамповЛокальный[0];
			
			ПараметрыФайлаШтампа.ПараметрыРазмещения.Расположение = 
				ВизуализацияЭПВызовСервера.ВариантРасположения1СШтамп(НастройкиШтрихкода.ПоложениеНаСтранице);
			// штрихкод всегда первая страница
			ПараметрыФайлаШтампа.ПравилаРасположения.ПравилоРасположения = Интеграция1СШтампКлиентСервер.ПравилоРасположенияНаПервойСтранице();
			
			Если НастройкиШтрихкода.ПоложениеНаСтранице = Перечисления.МестаВставкиКартинки.ПроизвольноеПоложение Тогда
				
				ПараметрыФайлаШтампа.ПараметрыРазмещения.Координаты.Лево = 
					Цел(ОбщегоНазначенияДокументооборотКлиентСервер.СтрокаВЧисло(НастройкиШтрихкода.СмещениеПоГоризонтали) * 3.0 );
				ПараметрыФайлаШтампа.ПараметрыРазмещения.Координаты.Верх = 
					Цел(ОбщегоНазначенияДокументооборотКлиентСервер.СтрокаВЧисло(НастройкиШтрихкода.СмещениеПоВертикали)  * 3.0 );
				
			КонецЕсли;	
			
			ПараметрыШтампов.Добавить(ПараметрыФайлаШтампа);
			
		КонецЕсли;
		
		// тут вызов 1С Штамп
		ПараметрыДобавленияШтамповВДокумент.ПараметрыДокумента.ДвоичныеДанные    = Параметры.ДвДанныеФайлаPdf;
		ПараметрыДобавленияШтамповВДокумент.ПараметрыДокумента.Расширение        = Параметры.ДанныеФайла.Расширение;
		ПараметрыДобавленияШтамповВДокумент.ПараметрыШтампов.ПоПозиционированию  = ПараметрыШтампов;
		
		ПользовательскийСценарий = НСтр("ru = 'Вставка штампа в файл 1С Документооборот'");
		РезультатОбработкиДокумента = Интеграция1СШтамп.РезультатУстановкиШтамповВДокумент(
			ПараметрыДобавленияШтамповВДокумент, ПользовательскийСценарий, 35);

		Если РезультатОбработкиДокумента.ЕстьОшибки Тогда
			
			ТекстОшибки = РезультатОбработкиДокумента.ИнформацияОбОшибках.ТекстОшибки;
			
			Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда
				ТекстОшибки = Нстр("ru ='Не удалось получить результат штампирования файла.'", 
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			КонецЕсли;
					
			ЗаписьЖурналаРегистрации("ПолучитьВизуализациюЭпДляPDF", 
				УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.Файлы, Параметры.ДанныеФайла.Ссылка, ТекстОшибки);
				
			Параметры.НастройкиШтампаЭП.ИспользоватьВнешнийСервисВставкиЭП = Ложь;
			Параметры.ДанныеФайла.Вставить("НастройкиШтампаЭП", Параметры.НастройкиШтампаЭП);
			
			Возврат РаботаСФайламиВызовСервера.ДвоичныеДанныеВизуализацииЭпДляPDF(Параметры.ДанныеФайла,
				Параметры.ИдентификаторФормы, Параметры.ТребуетсяЗаполнитьВизуализацию);
			
		КонецЕсли;
		
		ДвоичныеДанныеНовые = РезультатОбработкиДокумента.ДанныеФайла.ДвоичныеДанные;
		
	КонецЕсли;
	// Локализация Конец
	
	Возврат ДвоичныеДанныеНовые;
	
КонецФункции

// Проверяет необходимость пропуска создания отметки подписи для документа.
//
// Параметры:
//  Документ - СправочникСсылка.ДокументыПредприятия - Документ, для которого необходимо проверить необходимость пропуска создания отметки подписи.
//
// Возвращаемое значение:
//  Булево - Истина, если необходимо пропустить создание отметки подписи для документа, иначе Ложь.
//
Функция ПропуститьСозданиеОтметкиПодписиДляДокумента(Документ) Экспорт

	Результат = Ложь;

// Локализация
	// Исключение - для входящих МЭДО отметку генерировать не надо:
	Если МЭДОДокументооборот.ЭтоДокументМЭДОСВнешнейПодписью(Документ) Тогда
		Результат = Истина;
	КонецЕсли;
// Локализация Конец

	Возврат Результат;

КонецФункции

// См. ОбщаяФорма.ВыгрузкаВсехФайловДокументаНаДиск.ЗаполнитьСписокФайлов
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  СтрокаРодитель - СтрокаДереваЗначений
Процедура ПриЗаполненииСпискаФайловДляВыгрузкиНаДиск(Форма, СтрокаРодитель) Экспорт
	
	// Локализация
	
	// документы ЭДО
	ЕстьЭДО = Ложь;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД") И Не Форма.ТолькоОригиналы Тогда
		
		МассивЭД = ОбменЭДОДокументооборот.ЭлектронныеДокументыОбъектаУчета(СтрокаРодитель.ВладелецФайла, Ложь);
		ЕстьЭДО = МассивЭД.Количество() > 0 ;
		
		Для Каждого ДокументЭДО Из МассивЭД Цикл
			
			НоваяСтрокаЭДО = СтрокаРодитель.Строки.Добавить();
			НоваяСтрокаЭДО.Выгружать = Истина;
			НоваяСтрокаЭДО.ИндексКартинки = 30;
			НоваяСтрокаЭДО.ЭтоЭДО = Истина;
			НоваяСтрокаЭДО.ПолноеНаименование = Строка(ДокументЭДО);
			НоваяСтрокаЭДО.ВладелецФайла = ДокументЭДО;
			НоваяСтрокаЭДО.ЭтоРодитель = Истина;
			
		КонецЦикла;
		
	КонецЕсли;
	Форма.Элементы.ГруппаЭДО.Видимость = ЕстьЭДО;
	
	// Локализация Конец
	
КонецПроцедуры

#КонецОбласти