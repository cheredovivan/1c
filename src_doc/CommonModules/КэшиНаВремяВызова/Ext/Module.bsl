////////////////////////////////////////////////////////////////////////////////
// Кэши на время взова, для оптимизации работы (сервер).
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Заполняет кеш областми просмотра.
//
// Параметры:
//  ЗадачаИлиДействиеЗадачи - ДокументСсылка.Задача, ДокументСсылка.ДействиеЗадачи - Задача или действие.
//
// Возвращаемое значение:
//  См. КэшиНаВремяВызоваПовтИсп.КэшЗадачаОбластьПросмотра 
//
Функция ИнициализироватьКэшЗадачаОбластьПросмотра(ЗадачаИлиДействиеЗадачи) Экспорт
	
	КэшОбластиПросмотра = КэшиНаВремяВызоваПовтИсп.КэшЗадачаОбластьПросмотра();
	
	Если КэшОбластиПросмотра.Заполнен Или Не ЗначениеЗаполнено(ЗадачаИлиДействиеЗадачи) Тогда
		Возврат КэшОбластиПросмотра;
	КонецЕсли;
	
	Если ТипЗнч(ЗадачаИлиДействиеЗадачи) = Тип("ДокументСсылка.ДействиеЗадачи") Тогда
		
		РеквизитыДействияЗадачи =
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				ЗадачаИлиДействиеЗадачи,
				"Ссылка, Задача, Исполнитель, Источник,
				|ВидДействия, ВидУчастника, ЕстьОсобыйСрок,
				|ОсобыйЗаголовок, ОсобоеОписание, ОсобоеОписаниеФорматированное,
				|ОсобыйСрок, РезультатДействия, РезультатДействияТекстом, ФактическийИсполнитель,
				|СостояниеУчастникаЗадачи, Флаг, ПометкаУдаления, Приложения, ВерсияДанных");
		
		КэшОбластиПросмотра.ДействиеЗадачи = ЗадачаИлиДействиеЗадачи;
		КэшОбластиПросмотра.РеквизитыДействияЗадачи = РеквизитыДействияЗадачи;
		КэшОбластиПросмотра.Задача = РеквизитыДействияЗадачи.Задача;
		КэшОбластиПросмотра.ДанныеДействияЗадачи = РаботаСЗадачами.ДанныеДействияЗадачи(ЗадачаИлиДействиеЗадачи);
		
	ИначеЕсли ТипЗнч(ЗадачаИлиДействиеЗадачи) = Тип("ДокументСсылка.Задача") Тогда
		
		КэшОбластиПросмотра.ДействиеЗадачи = Документы.ДействиеЗадачи.ПустаяСсылка();
		КэшОбластиПросмотра.РеквизитыДействияЗадачи = Неопределено;
		КэшОбластиПросмотра.Задача = ЗадачаИлиДействиеЗадачи;
		КэшОбластиПросмотра.ДанныеДействияЗадачи = Неопределено;
		
	Иначе
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Неожиданный тип задачи или действия задачи %1 (%2).'"),
			ЗадачаИлиДействиеЗадачи,
			ТипЗнч(ЗадачаИлиДействиеЗадачи));
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КэшОбластиПросмотра.Задача) Тогда
		КэшОбластиПросмотра.ЗадачаОбъект = КэшОбластиПросмотра.Задача.ПолучитьОбъект();
	Иначе
		КэшОбластиПросмотра.ЗадачаОбъект = Неопределено;
	КонецЕсли;
	
	Если КэшОбластиПросмотра.ЗадачаОбъект <> Неопределено
		И КэшОбластиПросмотра.ЗадачаОбъект.Приложения.Количество() = 1
		И ДелопроизводствоКлиентСервер.ЭтоДокумент(КэшОбластиПросмотра.ЗадачаОбъект.Приложения[0].Приложение) Тогда
		ПредметДокумент = КэшОбластиПросмотра.ЗадачаОбъект.Приложения[0].Приложение;
		КэшОбластиПросмотра.ДанныеОбзораДокумента = ОбзорЗадач.ДанныеОбзораДокумента(ПредметДокумент);
		Если КэшОбластиПросмотра.ДанныеОбзораДокумента.Количество() = 0 Тогда
			КэшОбластиПросмотра.ДанныеОбзораДокумента = Неопределено;
		КонецЕсли;
	Иначе
		КэшОбластиПросмотра.ДанныеОбзораДокумента = Неопределено;
	КонецЕсли;
	
	Если КэшОбластиПросмотра.РеквизитыДействияЗадачи <> Неопределено
		И ЗначениеЗаполнено(КэшОбластиПросмотра.РеквизитыДействияЗадачи.Источник)
		И ТипЗнч(КэшОбластиПросмотра.РеквизитыДействияЗадачи.Источник) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		КэшОбластиПросмотра.ЗадачаПроцесса = КэшОбластиПросмотра.РеквизитыДействияЗадачи.Источник;
		КэшОбластиПросмотра.РеквизитыЗадачиПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			КэшОбластиПросмотра.ЗадачаПроцесса,
			"БизнесПроцесс, ТочкаМаршрута, Выполнена, ИсключенаИзПроцесса, ПометкаУдаления, ИсточникДанных, Предметы");
		КэшОбластиПросмотра.Процесс = КэшОбластиПросмотра.РеквизитыЗадачиПроцесса.БизнесПроцесс;
		КэшОбластиПросмотра.ДействиеОбработки = РегистрыСведений.ПроцессыДействий.ДействиеПоПроцессу(
			КэшОбластиПросмотра.Процесс);
	ИначеЕсли КэшОбластиПросмотра.РеквизитыДействияЗадачи <> Неопределено
		И ЗначениеЗаполнено(КэшОбластиПросмотра.РеквизитыДействияЗадачи.Источник)
		И ТипЗнч(КэшОбластиПросмотра.РеквизитыДействияЗадачи.Источник) = Тип("СправочникСсылка.ДействияОзнакомления") Тогда
		КэшОбластиПросмотра.ЗадачаПроцесса = Задачи.ЗадачаИсполнителя.ПустаяСсылка();
		КэшОбластиПросмотра.РеквизитыЗадачиПроцесса = Неопределено;
		КэшОбластиПросмотра.Процесс = Неопределено;
		КэшОбластиПросмотра.ДействиеОбработки = КэшОбластиПросмотра.РеквизитыДействияЗадачи.Источник;
	Иначе
		КэшОбластиПросмотра.ЗадачаПроцесса = Задачи.ЗадачаИсполнителя.ПустаяСсылка();
		КэшОбластиПросмотра.РеквизитыЗадачиПроцесса = Неопределено;
		КэшОбластиПросмотра.Процесс = Неопределено;
		КэшОбластиПросмотра.ДействиеОбработки = Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КэшОбластиПросмотра.Процесс) Тогда
		ТипПроцесса = ТипЗнч(КэшОбластиПросмотра.Процесс);
		МассивРеквизитыПроцесса = Новый Массив;
		МассивРеквизитыПроцесса.Добавить("Завершен"); // Реквизиты всех процессов
		Если ТипПроцесса = Тип("БизнесПроцессСсылка.Исполнение") Тогда
			МассивРеквизитыПроцесса.Добавить(
				"НомерИтерации, ИдентификаторПроверяющего, РезультатыИсполнения, РезультатыПроверки");
		ИначеЕсли ТипПроцесса = Тип("БизнесПроцессСсылка.Подписание") Тогда
			МассивРеквизитыПроцесса.Добавить(
				"СпособПодписания, НомерИтерации, Участники, РезультатыПодписания, РезультатыОбработок");
		ИначеЕсли ТипПроцесса = Тип("БизнесПроцессСсылка.Приглашение") Тогда
			МассивРеквизитыПроцесса.Добавить(
				"НомерИтерации, ИдентификаторОбрабатывающегоРезультат, Исполнители, РезультатыПриглашения,
				|РезультатыОзнакомлений");
		ИначеЕсли ТипПроцесса = Тип("БизнесПроцессСсылка.Регистрация") Тогда
			МассивРеквизитыПроцесса.Добавить(
				"НомерИтерации, ИдентификаторИсполнителя, ИдентификаторОбрабатывающегоРезультат, РезультатыРегистрации,
				|РезультатыОзнакомлений");
		ИначеЕсли ТипПроцесса = Тип("БизнесПроцессСсылка.Согласование") Тогда
			МассивРеквизитыПроцесса.Добавить(
				"НомерИтерации, ИдентификаторОбрабатывающегоРезультат, Исполнители, РезультатыСогласования,
				|РезультатыОзнакомлений, ОграничиватьЦиклы, КоличествоИтераций");
		ИначеЕсли ТипПроцесса = Тип("БизнесПроцессСсылка.Утверждение") Тогда
			МассивРеквизитыПроцесса.Добавить(
				"ПодписыватьЭП, НомерИтерации, ИдентификаторИсполнителя,
				|ИдентификаторОбрабатывающегоРезультат, РезультатыУтверждения, РезультатыОзнакомлений");
		КонецЕсли;
		ИменаРеквизитовПроцесса = СтрСоединить(МассивРеквизитыПроцесса, ", ");
		КэшОбластиПросмотра.РеквизитыПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			КэшОбластиПросмотра.Процесс,
			ИменаРеквизитовПроцесса);
	Иначе
		КэшОбластиПросмотра.РеквизитыПроцесса = Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КэшОбластиПросмотра.ДействиеОбработки) Тогда
		ТипДействияОбработки = ТипЗнч(КэшОбластиПросмотра.ДействиеОбработки);
		МассивРеквизитыДействия = Новый Массив;
		МассивРеквизитыДействия.Добавить("ВидДействия, НастройкаДействия, Предмет"); // Реквизиты всех действий
		Если ТипДействияОбработки = Тип("СправочникСсылка.ДействияИсполнения") Тогда
			МассивРеквизитыДействия.Добавить("Участники, ПоПунктам");
		ИначеЕсли ТипДействияОбработки = Тип("СправочникСсылка.ДействияОзнакомления") Тогда
			МассивРеквизитыДействия.Добавить("ПодписыватьУЭП, ОжидатьЗавершения");
		ИначеЕсли ТипДействияОбработки = Тип("СправочникСсылка.ДействияСогласования") Тогда
			МассивРеквизитыДействия.Добавить("СогласованиеВРежимеЗамечаний, Участники, Этапы");
		ИначеЕсли ТипДействияОбработки = Тип("СправочникСсылка.ДействияПодписания") Тогда
			МассивРеквизитыДействия.Добавить("Участники, Этапы");
		КонецЕсли;
		ИменаРеквизитовДействия = СтрСоединить(МассивРеквизитыДействия, ", ");
		КэшОбластиПросмотра.РеквизитыДействияОбработки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			КэшОбластиПросмотра.ДействиеОбработки,
			ИменаРеквизитовДействия);
		КэшОбластиПросмотра.ОбработкаОбъекта = РегистрыСведений.ДействияОбработкиОбъектов.ОбработкаДействия(
			КэшОбластиПросмотра.ДействиеОбработки);
		КэшОбластиПросмотра.ВидДействия = КэшОбластиПросмотра.РеквизитыДействияОбработки.ВидДействия;
	Иначе
		КэшОбластиПросмотра.РеквизитыДействияОбработки = Неопределено;
		КэшОбластиПросмотра.ОбработкаОбъекта = Справочники.ОбработкиОбъектов.ПустаяСсылка();
		КэшОбластиПросмотра.ВидДействия = Справочники.ВидыДействий.ПустаяСсылка();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КэшОбластиПросмотра.ОбработкаОбъекта) Тогда
		КэшОбластиПросмотра.РеквизитыОбработкиОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			КэшОбластиПросмотра.ОбработкаОбъекта,
			"Владелец, ДатаСоздания");
	Иначе
		КэшОбластиПросмотра.РеквизитыОбработкиОбъекта = Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КэшОбластиПросмотра.ВидДействия) Тогда
		КэшОбластиПросмотра.РеквизитыВидаДействия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			КэшОбластиПросмотра.ВидДействия,
			"ЗапретитьПостановкуПодзадач");
	Иначе
		КэшОбластиПросмотра.РеквизитыВидаДействия = Неопределено;
	КонецЕсли;
	
	КэшОбластиПросмотра.Заполнен = Истина;
	
	Возврат КэшОбластиПросмотра;
	
КонецФункции

// Сбрасывает кэш областми просмотра.
//
Процедура СброситьКэшЗадачаОбластьПросмотра() Экспорт
	
	КэшОбластиПросмотра = КэшиНаВремяВызоваПовтИсп.КэшЗадачаОбластьПросмотра();
	Если Не КэшОбластиПросмотра.Заполнен Тогда
		Возврат;
	КонецЕсли;
	
	КэшОбластиПросмотра.Заполнен = Ложь;
	КэшОбластиПросмотра.Задача = Документы.Задача.ПустаяСсылка();
	КэшОбластиПросмотра.ЗадачаОбъект = Неопределено;
	КэшОбластиПросмотра.ДействиеЗадачи = Документы.ДействиеЗадачи.ПустаяСсылка();
	КэшОбластиПросмотра.РеквизитыДействияЗадачи = Неопределено;
	КэшОбластиПросмотра.ДанныеДействияЗадачи = Неопределено;
	КэшОбластиПросмотра.ДанныеОбзораДокумента = Неопределено;
	КэшОбластиПросмотра.ЗадачаПроцесса = Задачи.ЗадачаИсполнителя.ПустаяСсылка();
	КэшОбластиПросмотра.РеквизитыЗадачиПроцесса = Неопределено;
	КэшОбластиПросмотра.Процесс = Неопределено;
	КэшОбластиПросмотра.РеквизитыПроцесса = Неопределено;
	КэшОбластиПросмотра.ДействиеОбработки = Неопределено;
	КэшОбластиПросмотра.РеквизитыДействияОбработки = Неопределено;
	КэшОбластиПросмотра.ОбработкаОбъекта = Справочники.ОбработкиОбъектов.ПустаяСсылка();
	КэшОбластиПросмотра.РеквизитыОбработкиОбъекта = Неопределено;
	КэшОбластиПросмотра.ВидДействия = Справочники.ВидыДействий.ПустаяСсылка();
	КэшОбластиПросмотра.РеквизитыВидаДействия = Неопределено;
	
КонецПроцедуры

// Инициализирует кэш для формы элементы справочника документы предприятия.
//
// Параметры:
//  ДокументОбъект - СправочникОбъект.ДокументыПредприятия.
// 
// Возвращаемое значение:
//  Структура - Кэш для формы элементы справочника документы предприятия:
//   * Инициализирован - Булево - Кэш инициализирован.
//   * КэшДокумента - Структура - Кэш документа:
//     ** Ссылка - СправочникСсылка.ДокументыПредприятия.
//     ** ВидДокумента - СправочникСсылка.ВидыДокументов.
//     ** Шаблон - СправочникСсылка.ШаблоныДокументов.
//     ** ЕстьФайлыШаблона - Булево.
//     ** Контрагент - СправочникСсылка.Контрагенты.
//     ** ПодписалОтКонтрагента - СправочникСсылка.КонтактныеЛица.
//     ** ДатаСоздания - Дата.
//     ** ДатаРегистрации - Дата.
//     ** Стороны - ТабличнаяЧасть.
//   * КэшВидаДокумента - Структура - Кэш вида документа. См. Делопроизводство.КэшВидаДокумента.
//   * УчастникиРабочейГруппы - См. РегистрыСведений.РабочиеГруппы.ПолучитьУчастниковПоОбъекту.
//   * ВсеСостоянияДокумента - См. Делопроизводство.ПолучитьВсеСостоянияДокумента.
//   * КэшОбработки - Структура - Кэш обработки:
//     ** Ссылка - СправочникСсылка.ОбработкиОбъектов.
//     ** ДатаСоздания - Дата.
//     ** Состояние - ПеречислениеСсылка.СостоянияОбработкиОбъектов.
//     ** ДатаИзмененияСостояния - Дата.
//     ** ДатаНачала - Дата.
//
Функция ИнициализироватьДокументыПредприятияФормаЭлемента(ДокументОбъект) Экспорт
	
	КэшДокументыПредприятияФормаЭлемента =
		КэшиНаВремяВызоваПовтИсп.КэшДокументыПредприятияФормаЭлемента();
	
	КэшДокументыПредприятияФормаЭлемента.КэшДокумента.Ссылка = ДокументОбъект.Ссылка;
	КэшДокументыПредприятияФормаЭлемента.КэшДокумента.ВидДокумента = ДокументОбъект.ВидДокумента;
	КэшДокументыПредприятияФормаЭлемента.КэшДокумента.Шаблон = ДокументОбъект.Шаблон;
	КэшДокументыПредприятияФормаЭлемента.КэшДокумента.ЕстьФайлыШаблона =
		РаботаСФайламиВызовСервера.ПолучитьВсеПодчиненныеФайлы(ДокументОбъект.Шаблон, Ложь).Количество() > 0;
	КэшДокументыПредприятияФормаЭлемента.КэшДокумента.Контрагент = ДокументОбъект.Контрагент;
	КэшДокументыПредприятияФормаЭлемента.КэшДокумента.ПодписалОтКонтрагента =
		ДокументОбъект.ПодписалОтКонтрагента;
	КэшДокументыПредприятияФормаЭлемента.КэшДокумента.ДатаСоздания = ДокументОбъект.ДатаСоздания;
	КэшДокументыПредприятияФормаЭлемента.КэшДокумента.ДатаРегистрации =
		ДокументОбъект.ДатаРегистрации;
	КэшДокументыПредприятияФормаЭлемента.КэшДокумента.Стороны = ДокументОбъект.Стороны;
	
	КэшДокументыПредприятияФормаЭлемента.КэшВидаДокумента =
		Делопроизводство.КэшВидаДокумента(ДокументОбъект.ВидДокумента);
	
	КэшДокументыПредприятияФормаЭлемента.УчастникиРабочейГруппы =
		РегистрыСведений.РабочиеГруппы.ПолучитьУчастниковПоОбъекту(ДокументОбъект.Ссылка);
	
	КэшДокументыПредприятияФормаЭлемента.ВсеСостоянияДокумента =
		Делопроизводство.ПолучитьВсеСостоянияДокумента(ДокументОбъект.Ссылка);
		
	ДатаОбработки = Дата(1, 1, 1);
	ОбработкаОбъекта = Справочники.ОбработкиОбъектов.ОбработкаОбъекта(ДокументОбъект.Ссылка);
	КэшДокументыПредприятияФормаЭлемента.КэшОбработки.Ссылка = ОбработкаОбъекта;
	Если ЗначениеЗаполнено(ОбработкаОбъекта) Тогда
		РеквизитыОбработки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ОбработкаОбъекта,
			"ДатаСоздания, Состояние, ДатаИзмененияСостояния, ДатаНачала");
		КэшДокументыПредприятияФормаЭлемента.КэшОбработки.ДатаСоздания =
			РеквизитыОбработки.ДатаСоздания;
		КэшДокументыПредприятияФормаЭлемента.КэшОбработки.Состояние =
			РеквизитыОбработки.Состояние;
		КэшДокументыПредприятияФормаЭлемента.КэшОбработки.ДатаИзмененияСостояния =
			РеквизитыОбработки.ДатаИзмененияСостояния;
		КэшДокументыПредприятияФормаЭлемента.КэшОбработки.ДатаНачала =
			РеквизитыОбработки.ДатаНачала; 
			
		ДатаОбработки = РеквизитыОбработки.ДатаСоздания;	
	КонецЕсли;  
		
	КэшДокументыПредприятияФормаЭлемента.ТекущиеДействияПодписания = ДействияСервер.ТекущиеДействияПодписания(
		ДокументОбъект.Ссылка, ДокументОбъект.ВидДокумента, ДатаОбработки);		

	КэшДокументыПредприятияФормаЭлемента.ТекущиеДействияУтверждения = ДействияСервер.ТекущиеДействияУтверждения(
		ДокументОбъект.Ссылка, ДокументОбъект.ВидДокумента, ДатаОбработки);		
		
	// Кэш отмечаем инициализированным в самом конце, чтобы не ограничивать используемые в расчётах методы.
	КэшДокументыПредприятияФормаЭлемента.Инициализирован = Истина;
	
	Возврат КэшДокументыПредприятияФормаЭлемента;
	
КонецФункции

// Переносит кэш для формы элементы справочника документы предприятия.
//
// Параметры:
//  ПереносКэшаИзЧтенияВСоздание - Структура - Переносимый кэш:
//   * Инициализирован - Булево - Кэш инициализирован.
//   * КэшДокумента - Структура - Кэш документа:
//     ** Ссылка - СправочникСсылка.ДокументыПредприятия.
//     ** ВидДокумента - СправочникСсылка.ВидыДокументов.
//     ** Шаблон - СправочникСсылка.ШаблоныДокументов.
//     ** ЕстьФайлыШаблона - Булево.
//     ** Контрагент - СправочникСсылка.Контрагенты.
//     ** ПодписалОтКонтрагента - СправочникСсылка.КонтактныеЛица.
//     ** ДатаСоздания - Дата.
//     ** ДатаРегистрации - Дата.
//     ** Стороны - ТабличнаяЧасть.
//   * КэшВидаДокумента - Структура - Кэш вида документа. См. Делопроизводство.КэшВидаДокумента.
//   * УчастникиРабочейГруппы - См. РегистрыСведений.РабочиеГруппы.ПолучитьУчастниковПоОбъекту.
//   * ВсеСостоянияДокумента - См. Делопроизводство.ПолучитьВсеСостоянияДокумента.
//   * КэшОбработки - Структура - Кэш обработки:
//     ** Ссылка - СправочникСсылка.ОбработкиОбъектов.
//     ** ДатаСоздания - Дата.
//     ** Состояние - ПеречислениеСсылка.СостоянияОбработкиОбъектов.
//     ** ДатаИзмененияСостояния - Дата.
//     ** ДатаНачала - Дата.
//
Процедура ПеренестиДокументыПредприятияФормаЭлемента(ПереносКэшаИзЧтенияВСоздание) Экспорт
	
	Если ПереносКэшаИзЧтенияВСоздание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КэшДокументыПредприятияФормаЭлемента =
		КэшиНаВремяВызоваПовтИсп.КэшДокументыПредприятияФормаЭлемента();
	
	КэшДокументыПредприятияФормаЭлемента.Инициализирован = Истина;
	
	КэшДокументыПредприятияФормаЭлемента.Вставить("КэшДокумента",
		ПереносКэшаИзЧтенияВСоздание.КэшДокумента);
	
	КэшДокументыПредприятияФормаЭлемента.Вставить("КэшВидаДокумента",
		ПереносКэшаИзЧтенияВСоздание.КэшВидаДокумента);
	
	КэшДокументыПредприятияФормаЭлемента.Вставить("УчастникиРабочейГруппы",
		ПереносКэшаИзЧтенияВСоздание.УчастникиРабочейГруппы);
	
	КэшДокументыПредприятияФормаЭлемента.Вставить("ВсеСостоянияДокумента",
		ПереносКэшаИзЧтенияВСоздание.ВсеСостоянияДокумента);
	
	КэшДокументыПредприятияФормаЭлемента.Вставить("КэшОбработки",
		ПереносКэшаИзЧтенияВСоздание.КэшОбработки);

	КэшДокументыПредприятияФормаЭлемента.Вставить("ТекущиеДействияПодписания",
		ПереносКэшаИзЧтенияВСоздание.ТекущиеДействияПодписания);

	КэшДокументыПредприятияФормаЭлемента.Вставить("ТекущиеДействияУтверждения",
		ПереносКэшаИзЧтенияВСоздание.ТекущиеДействияУтверждения);
		
КонецПроцедуры

// Формирует кэш обзора документа.
// 
// Возвращаемое значение:
//  См. КэшиНаВремяВызоваПовтИсп.КэшОбзораДокумента
// 
Функция КэшОбзораДокумента() Экспорт
	
	КэшОбзораДокумента = КэшиНаВремяВызоваПовтИсп.КэшОбзораДокумента();
	
	Возврат КэшОбзораДокумента;
	
КонецФункции

// Заполняет кэш обзора документа.
//
// Параметры:
//  Документ - СправочникСсылка.ДокументыПредприятия
//
// Возвращаемое значение:
//  См. КэшиНаВремяВызоваПовтИсп.КэшОбзораДокумента
//
Функция ИнициализироватьКэшОбзораДокумента(Документ) Экспорт
	
	КэшОбзораДокумента = КэшиНаВремяВызоваПовтИсп.КэшОбзораДокумента();
	КэшОбзораДокумента.Заполнен = Ложь;
	
	Если ЗначениеЗаполнено(Документ) Тогда
		КэшОбзораДокумента.Документ = Документ;
		КэшОбзораДокумента.РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Документ,
			"ВидДокумента, Контрагент, ПодписалОтКонтрагента, ДатаСоздания, ДатаРегистрации");
		ВидДокумента = КэшОбзораДокумента.РеквизитыДокумента.ВидДокумента;
	Иначе
		КэшОбзораДокумента.Документ = Справочники.ДокументыПредприятия.ПустаяСсылка();
		КэшОбзораДокумента.РеквизитыДокумента = Неопределено;
		ВидДокумента = Справочники.ВидыДокументов.ПустаяСсылка();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидДокумента) Тогда
		КэшОбзораДокумента.ВидДокумента = ВидДокумента;
	Иначе
		КэшОбзораДокумента.ВидДокумента = Справочники.ВидыДокументов.ПустаяСсылка();
	КонецЕсли;
	КэшОбзораДокумента.КэшВидаДокумента = Делопроизводство.КэшВидаДокумента(КэшОбзораДокумента.ВидДокумента);
	
	КэшОбзораДокумента.Заполнен = ЗначениеЗаполнено(КэшОбзораДокумента.Документ);
	
	Возврат КэшОбзораДокумента;
	
КонецФункции

#КонецОбласти