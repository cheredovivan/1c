
#Область ПрограммныйИнтерфейс

// Вызывается из ОбменСАрхивом.ВыгрузитьДанные, дополняет выгрузку локализуемыми данными
// 
// Параметры:
//  ПараметрыТранспорта - см. ОбменСАрхивомТранспорт.ПараметрыТранспортаДляСеансаОбмена
//  
Процедура ВыгрузитьДанные(ПараметрыТранспорта) Экспорт

	// Локализация
	ОтправитьМЧД(ПараметрыТранспорта);
	// Локализация Конец

КонецПроцедуры

// Вызывается из ПроверитьЭПДокументовДляВыгрузкиВАрхив модуля объекта документа ПередачаДелВАрхив
// 
// Параметры:
//  МассивОшибок - Массив из Строка
//  ДелаДокументов - Соответствие
//  ДокументыДО - Массив из СправочникСсылка.ДокументыПредприятия
Процедура ПриПроверкеЭП_ПередачаДелВАрхив(МассивОшибок, ДелаДокументов, ДокументыДО) Экспорт

	// Локализация

	ВсеДокументыЭДО = Новый Массив;
	ДокументыДОПоДокументамЭДО = Новый Соответствие;
	ДокументыЭДО(ДокументыДО, ВсеДокументыЭДО, ДокументыДОПоДокументамЭДО);
	
	ТаблицаФайловЭДО = ЭлектронныеДокументыЭДО.ПрисоединенныеФайлыЭлектронныхДокументов(ВсеДокументыЭДО);
	ФайлыЭДО = ТаблицаФайловЭДО.ВыгрузитьКолонку("ПрисоединенныйФайл");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЭП.ПодписанныйОбъект КАК Файл,
		|	ЭП.КомуВыданСертификат КАК КомуВыданСертификат
		|ИЗ
		|	РегистрСведений.ЭлектронныеПодписи КАК ЭП
		|ГДЕ
		|	ЭП.ПодписанныйОбъект В (&ФайлыЭДО)
		|	И НЕ ЭП.ПодписьВерна
		|	И ЭП.ДатаПроверкиПодписи <> ДАТАВРЕМЯ(1, 1, 1)";
	Запрос.Параметры.Вставить("ФайлыЭДО", ФайлыЭДО);	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НайденнаяСтрока = ТаблицаФайловЭДО.Найти(Выборка.Файл, "ПрисоединенныйФайл");
		Если НайденнаяСтрока = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ДокументДО = ДокументыДОПоДокументамЭДО[НайденнаяСтрока.ЭлектронныйДокумент];
		МассивОшибок.Добавить(СтрШаблон(НСтр("ru = 'Недействительная подпись файла ""%1"" документа ""%2"" в деле ""%3"", подписал ""%4""'"),
			Выборка.Файл, ДокументДО, ДелаДокументов[ДокументДО], Выборка.КомуВыданСертификат));
	КонецЦикла;

	// Локализация Конец

КонецПроцедуры

// Вызывается из ПроверитьЗаполнениеДокументовДляВыгрузкиВАрхив модуля объекта документа ПередачаДелВАрхив
// 
// Параметры:
//  КоличествоФайлов - Число - Количество файлов ДО и ЭДО
//  ДокументДО - СправочникСсылка.ДокументыПредприятия - документ для проверки количества файлов.
//
Процедура ПриПроверкеЭлектронногоДокумента_ПередачаДелВАрхив(КоличествоФайлов, ДокументДО) Экспорт

	// Локализация

	Если КоличествоФайлов > 0 Тогда
		Возврат;
	КонецЕсли;

	ДокументыДО = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументДО);
	ВсеДокументыЭДО = Новый Массив;
	ДокументыЭДО(ДокументыДО, ВсеДокументыЭДО);
	
	ТаблицаФайловЭДО = ЭлектронныеДокументыЭДО.ПрисоединенныеФайлыЭлектронныхДокументов(ВсеДокументыЭДО);
	КоличествоФайлов = ТаблицаФайловЭДО.ВыгрузитьКолонку("ПрисоединенныйФайл").Количество();

	// Локализация Конец

КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Дополняет метаданные, исключаемые из выгрузки НСИ
// 
// Параметры:
//  МассивМетаданных - Массив из ОбъектМетаданных
// 
Процедура ДополнитьИсключаемыеМетаданныеВыгрузкиНСИ(МассивМетаданных) Экспорт
	
	// Локализация
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивМетаданных, МетаданныеМЧД());
	// Локализация Конец
	
КонецПроцедуры

// Дополнить документ в пакете.
// 
// Параметры:
//  СвойстваКонтейнераXDTO Свойства контейнера XDTO
//  ВыборкаДокумент Выборка документ
//  КонтейнерДокументаXDTO Контейнер документа XDTO
//  ВременнаяПапка Временная папка
//  ТаблицаФайловДокумента Таблица файлов документа
//  ЕстьОшибки Есть ошибки
Процедура ДополнитьДокументВПакете(
				СвойстваКонтейнераXDTO,
				ВыборкаДокумент,
				КонтейнерДокументаXDTO,
				ВременнаяПапка,
				ТаблицаФайловДокумента,
				ЕстьОшибки) Экспорт

	// Локализация

	ТаблицаФайловДокумента.Колонки.Добавить("ФайлЭДО");
	ТаблицаФайловДокумента.Колонки.Добавить("СообщениеЭДО");
	ТаблицаФайловДокумента.Колонки.Добавить("ТипЭлементаРегламентаЭДО");

	// Свойства документа ЭДО.
	ЭлектронныйДокумент = ОбменСКонтрагентамиДОСлужебный.ЭлектронныйДокументДокументаДО(ВыборкаДокумент.Ссылка);
	ЭтоЭДО = ЗначениеЗаполнено(ЭлектронныйДокумент);
	
	Если ЭтоЭДО Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ЭлектронныйДокументВходящийЭДО.НомерДокумента КАК НомерДокумента,
			|	ЭлектронныйДокументВходящийЭДО.ДатаДокумента КАК ДатаДокумента,
			|	ЭлектронныйДокументВходящийЭДО.ТипРегламента КАК ТипРегламента,
			|	СообщениеЭДО.ТипЭлементаРегламента КАК ТипЭлементаРегламента,
			|	СообщениеЭДО.ВидСообщения.ТипДокумента КАК ТипДокументаЭДО,
			|	СообщениеЭДО.Направление КАК Направление
			|ИЗ
			|	Документ.ЭлектронныйДокументВходящийЭДО КАК ЭлектронныйДокументВходящийЭДО
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СообщениеЭДО КАК СообщениеЭДО
			|		ПО ЭлектронныйДокументВходящийЭДО.Ссылка = СообщениеЭДО.ЭлектронныйДокумент
			|			И (СообщениеЭДО.ТипЭлементаРегламента = ЗНАЧЕНИЕ(Перечисление.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя))
			|ГДЕ
			|	ЭлектронныйДокументВходящийЭДО.Ссылка = &ЭлектронныйДокумент
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЭлектронныйДокументИсходящийЭДО.НомерДокумента,
			|	ЭлектронныйДокументИсходящийЭДО.ДатаДокумента,
			|	ЭлектронныйДокументИсходящийЭДО.ТипРегламента,
			|	СообщениеЭДО.ТипЭлементаРегламента,
			|	СообщениеЭДО.ВидСообщения.ТипДокумента,
			|	СообщениеЭДО.Направление
			|ИЗ
			|	Документ.ЭлектронныйДокументИсходящийЭДО КАК ЭлектронныйДокументИсходящийЭДО
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СообщениеЭДО КАК СообщениеЭДО
			|		ПО ЭлектронныйДокументИсходящийЭДО.Ссылка = СообщениеЭДО.ЭлектронныйДокумент
			|			И (СообщениеЭДО.ТипЭлементаРегламента = ЗНАЧЕНИЕ(Перечисление.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя))
			|ГДЕ
			|	ЭлектронныйДокументИсходящийЭДО.Ссылка = &ЭлектронныйДокумент";
		
		Запрос.УстановитьПараметр("ЭлектронныйДокумент", ЭлектронныйДокумент);
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			ТипXDTOСвойстваЭДО = СвойстваКонтейнераXDTO.Получить("EDIProperties").Тип;
			СтрокаСвойстваЭДО = ФабрикаXDTO.Создать(ТипXDTOСвойстваЭДО);
			СтрокаСвойстваЭДО.Direction = 
				Метаданные.Перечисления.НаправленияЭДО.ЗначенияПеречисления[
					Перечисления.НаправленияЭДО.Индекс(Выборка.Направление)].Имя;
			СтрокаСвойстваЭДО.Number = Выборка.НомерДокумента;
			СтрокаСвойстваЭДО.Date = Выборка.ДатаДокумента;
			КонтейнерДокументаXDTO.EDIProperties = СтрокаСвойстваЭДО;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ОбъектыУчетаДокументовЭДО.ОбъектУчета КАК ОбъектУчета
			|ИЗ
			|	РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
			|ГДЕ
			|	ОбъектыУчетаДокументовЭДО.Актуальный = ИСТИНА
			|	И ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент = &ЭлектронныйДокумент";
			
		Запрос.УстановитьПараметр("ЭлектронныйДокумент", ЭлектронныйДокумент);
			
		ТаблицаДокументовУчета = Запрос.Выполнить().Выгрузить();
		ДокументУчета =
			?(ТаблицаДокументовУчета.Количество(), ТаблицаДокументовУчета[0].ОбъектУчета, Неопределено);
	
		ЭлектронныеДокументы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭлектронныйДокумент);
		ТаблицаФайловЭДО = ЭлектронныеДокументыЭДО.ПрисоединенныеФайлыЭлектронныхДокументов(ЭлектронныеДокументы);
		
		Для Каждого СтрокаФайлаЭДО Из ТаблицаФайловЭДО Цикл
			ФайлЭДО = СтрокаФайлаЭДО.ПрисоединенныйФайл;
			СообщениеЭДО = СтрокаФайлаЭДО.СообщениеЭДО;
			ТипЭлементаРегламентаЭДО = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СообщениеЭДО, "ТипЭлементаРегламента");
			
			ДанныеФайла = РаботаСФайлами.ДанныеФайла(ФайлЭДО);
			ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
			
			ИмяФайлаДокументаБезПути = ДанныеФайла.ИмяФайла;
			Попытка
				ДвоичныеДанныеФайла.Записать(ВременнаяПапка + ИмяФайлаДокументаБезПути);
			Исключение
				Событие = ОбменСАрхивом.СобытиеДляЛога();
				Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка;
				Событие.ОсновнойОбъект = ВыборкаДокумент.СдаточнаяОпись;
				Событие.Объект = ВыборкаДокумент.Ссылка;
				Событие.КраткоеОписание = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
				Событие.ДополнительныеСведения = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ОбменСАрхивом.ЗаписатьВЛог(Событие);
				ЕстьОшибки = Истина;
				Продолжить;
			КонецПопытки;
			ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.SHA256);
			ХешированиеДанных.ДобавитьФайл(ВременнаяПапка + ИмяФайлаДокументаБезПути);
			СтрокаФайла = ТаблицаФайловДокумента.Добавить();
			СтрокаФайла.ФайлЭДО = ФайлЭДО;
			СтрокаФайла.СообщениеЭДО = СообщениеЭДО;
			СтрокаФайла.ИнформацияОтправителя =
				ТипЭлементаРегламентаЭДО = Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя;
			СтрокаФайла.ИмяВоВременнойПапке = ИмяФайлаДокументаБезПути;
			СтрокаФайла.ХешСумма = Base64Строка(ХешированиеДанных.ХешСумма);
			СтрокаФайла.ТипЭлементаРегламентаЭДО = ТипЭлементаРегламентаЭДО;
			СтрокаФайла.РазмерФайла = ФайлЭДО.Размер;
			СтрокаФайла.ДатаИзмененияФайла = ФайлЭДО.ДатаМодификацииУниверсальная;
			
			Если ЗначениеЗаполнено(ДокументУчета) И СтрокаФайла.ИнформацияОтправителя Тогда
				Если ТипЗнч(ДокументУчета) = Тип("СправочникСсылка.ВерсииФайлов") Тогда
					ФайлДокументаУчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументУчета, "Владелец");
					СтрокаФайла.ФайлДО = ФайлДокументаУчета;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

	// Локализация Конец

КонецПроцедуры

// Дополнить строку файла в документе.
// 
// Параметры:
//  СтрокаФайла Строка файла
//  ЭлементXDTOФайлы Элемент XDTOФайлы
//  ВыборкаДокумент Выборка документ
//  ПодписанныйФайлДокумента Подписанный файл документа
Процедура ДополнитьСтрокуФайлаВДокументе(СтрокаФайла, ЭлементXDTOФайлы, ВыборкаДокумент, ПодписанныйФайлДокумента) Экспорт
	
	// Локализация
	
	ЭлектронныйДокумент = ОбменСКонтрагентамиДОСлужебный.ЭлектронныйДокументДокументаДО(ВыборкаДокумент.Ссылка);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЖурналДействийПоЭДО.СтатусСообщения КАК СтатусСообщения,
		|	ЖурналДействийПоЭДО.ДатаИзменения КАК ДатаИзменения,
		|	ЖурналДействийПоЭДО.Сообщение КАК СообщениеЭДО,
		|	ЖурналДействийПоЭДО.Сообщение.ОсновнойФайл КАК СообщениеОсновнойФайл
		|ИЗ
		|	РегистрСведений.ЖурналДействийПоЭДО КАК ЖурналДействийПоЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СообщениеЭДОПрисоединенныеФайлы КАК ФайлыЭДО
		|		ПО ЖурналДействийПоЭДО.Сообщение.ОсновнойФайл = ФайлыЭДО.Ссылка
		|ГДЕ
		|	ЖурналДействийПоЭДО.ЭлектронныйДокумент = &ЭлектронныйДокумент";
	Запрос.УстановитьПараметр("ЭлектронныйДокумент", ЭлектронныйДокумент);
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаЖурналаДействийПоЭДО = Запрос.Выполнить().Выгрузить();
	
	ЭтоФайлЭДО = ЗначениеЗаполнено(СтрокаФайла.ФайлЭДО);
		Если ЭтоФайлЭДО Тогда
		
			ЭлементXDTOФайлы.Size = СтрокаФайла.ФайлЭДО.Размер;
			ЭлементXDTOФайлы.Modified = СтрокаФайла.ФайлЭДО.ДатаМодификацииУниверсальная;
			
			ТипЭлементаРегламентаЭДО = СтрокаФайла.ТипЭлементаРегламентаЭДО;
			ЭлементXDTOЖурналЭДО = ФабрикаXDTO.Создать(ЭлементXDTOФайлы.Свойства().Получить("EDIStatuses").Тип);
			ЭлементXDTOФайлы.EDIFileType = Строка(ТипЭлементаРегламентаЭДО);
			ЗаписиЖурналаДействийПоЭДО =
				ТаблицаЖурналаДействийПоЭДО.НайтиСтроки(Новый Структура("СообщениеОсновнойФайл, СообщениеЭДО", СтрокаФайла.ФайлЭДО, СтрокаФайла.СообщениеЭДО));
			Для Каждого ЗаписьЖурналаДействийПоЭДО Из ЗаписиЖурналаДействийПоЭДО Цикл
				ЭлементXDTOЖурналЭДО.Date = ЗаписьЖурналаДействийПоЭДО.ДатаИзменения;
				ЭлементXDTOЖурналЭДО.Value =
					Метаданные.Перечисления.СтатусыСообщенийЭДО.ЗначенияПеречисления[
						Перечисления.СтатусыСообщенийЭДО.Индекс(ЗаписьЖурналаДействийПоЭДО.СтатусСообщения)].Имя;
				ЭлементXDTOФайлы.EDIStatuses.Добавить(ЭлементXDTOЖурналЭДО);
			КонецЦикла;
			
		Иначе
			ЭлементXDTOФайлы.Size = СтрокаФайла.ФайлДО.ТекущаяВерсияРазмер;
			ЭлементXDTOФайлы.Modified = СтрокаФайла.ФайлДО.ТекущаяВерсияДатаМодификацииФайла;
		КонецЕсли;
		
		ПодписанныйФайлДокумента = ?(ЭтоФайлЭДО, СтрокаФайла.ФайлЭДО, СтрокаФайла.ФайлДО);
		
	// Локализация Конец
	
КонецПроцедуры

#КонецОбласти

// Локализация
#Область СлужебныеПроцедурыИФункции

Процедура ДокументыЭДО(ДокументыДО, ВсеДокументыЭДО, ДокументыДОПоДокументамЭДО = Неопределено)
	
	ДокументыЭДОПоДокументамДО = ОбменЭДОДокументооборот.ДокументыЭДОДокументовДО(ДокументыДО);
	Для Каждого КлючИЗначение Из ДокументыЭДОПоДокументамДО Цикл
		ДокументДО = КлючИЗначение.Ключ;
		МассивЭДО = КлючИЗначение.Значение;
		Для Каждого ДокументЭДО Из МассивЭДО Цикл
			ВсеДокументыЭДО.Добавить(ДокументЭДО);
			Если ДокументыДОПоДокументамЭДО <> Неопределено Тогда
				ДокументыДОПоДокументамЭДО[ДокументЭДО] = ДокументДО;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОтправитьМЧД(ПараметрыТранспорта)
	
	УзелАрхив = ПланыОбмена.ОбменНСИСАрхивом.УзелАрхив();
	МетаданныеМЧД = МетаданныеМЧД();
	ОбъектыСОшибками = Новый Массив;
	
	ОбъектыДляВыгрузки = ОбменСАрхивом.ПорцияИзмененныхДанныхДляВыгрузки(МетаданныеМЧД);
	Пока ОбъектыДляВыгрузки.Количество() > 0 Цикл
		
		Для Каждого МЧДСсылка Из ОбъектыДляВыгрузки Цикл
			
			НачатьТранзакцию();
			Попытка
				ВыгрузитьДоверенность(МЧДСсылка, ПараметрыТранспорта);
				ПланыОбмена.УдалитьРегистрациюИзменений(УзелАрхив, МЧДСсылка);
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ОбъектыСОшибками.Добавить(МЧДСсылка);
				
				Событие = ОбменСАрхивом.СобытиеДляЛога();
				Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка;
				Событие.ОсновнойОбъект = МЧДСсылка;
				Событие.КраткоеОписание = СтрШаблон(НСтр("ru = 'Ошибка выгрузки МЧД: %1'"),
					ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				Событие.ДополнительныеСведения = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ОбменСАрхивом.ЗаписатьВЛог(Событие);
			КонецПопытки;
			
		КонецЦикла;
		
		ОбъектыДляВыгрузки = ОбменСАрхивом.ПорцияИзмененныхДанныхДляВыгрузки(МетаданныеМЧД, ОбъектыСОшибками);
		
	КонецЦикла;
	
КонецПроцедуры

// Метаданные МЧД
// 
// Возвращаемое значение:
//  Массив из ОбъектМетаданных - Метаданные МЧД
Функция МетаданныеМЧД()
	
	МетаданныеМЧД = Новый Массив;
	Для Каждого ТипМЧД Из Метаданные.ОпределяемыеТипы.МашиночитаемаяДоверенность.Тип.Типы() Цикл
		МетаданныеМЧД.Добавить(Метаданные.НайтиПоТипу(ТипМЧД));
	КонецЦикла;
	
	Возврат МетаданныеМЧД;
	
КонецФункции

// Выгружает МЧД в каталог для отправки в 1С:Архив.
// 
// Параметры:
//  МЧДСсылка - СправочникСсылка - ссылка на МЧД.
//  ПараметрыТранспорта - см. ОбменСАрхивомТранспорт.ПараметрыТранспортаДляСеансаОбмена.
//
Процедура ВыгрузитьДоверенность(МЧДСсылка, ПараметрыТранспорта)
	
	ВременнаяПапка = ПолучитьИмяВременногоФайла("") + ПолучитьРазделительПути();
	СоздатьКаталог(ВременнаяПапка);
	
	ДанныеМЧД = ДанныеМЧДДляВыгрузки(МЧДСсылка, ВременнаяПапка);
	Если ДанныеМЧД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбязательныеДанные = Новый Массив;
	ОбязательныеДанные.Добавить("Тип");
	ОбязательныеДанные.Добавить("НомерДоверенности");
	НезаполненныеОбязательныеДанные = Новый Массив;
	Если ДанныеМЧД.ЕстьВРеестреФНС Тогда
		ОбязательныеДанные.Добавить("ИННДоверителя");
	Иначе
		ОбязательныеДанные.Добавить("ИмяФайлаМЧД");
		ОбязательныеДанные.Добавить("ИмяФайлаПодписиМЧД");
	КонецЕсли;
	Для Каждого ИмяОбязательногоСвойства Из ОбязательныеДанные Цикл
		Если Не ЗначениеЗаполнено(ДанныеМЧД[ИмяОбязательногоСвойства]) Тогда
			НезаполненныеОбязательныеДанные.Добавить(ИмяОбязательногоСвойства);
		КонецЕсли;
	КонецЦикла;
	Если НезаполненныеОбязательныеДанные.Количество() > 0 Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не заполнены обязательные данные доверенности: %1'"),
			СтрСоединить(НезаполненныеОбязательныеДанные, ","));
	КонецЕсли;
	
	ИмяФайлаОписанияМЧД = ВременнаяПапка + "Warrant.xml";
	ИмяZipФайлаВыгрузки = ВременнаяПапка + ОбменСАрхивом.ТекущаяОтметкаВремени() + ".zip";
	ЗаписьZipФайла = Новый ЗаписьZipФайла(ИмяZipФайлаВыгрузки);

	ПространствоИменСхемы = ОбменСАрхивом.ВерсияФорматаДляОтправкиСообщений();
	ЭлементXDTOМЧД = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИменСхемы, "Warrant"));
	СвойстваМЧДXDTO = ЭлементXDTOМЧД.Свойства();
	ЭлементXDTOМЧД.Type = ДанныеМЧД.Тип;
	ЭлементXDTOМЧД.Number = ДанныеМЧД.НомерДоверенности;
	ЭлементXDTOМЧД.IsInRegister = ДанныеМЧД.ЕстьВРеестреФНС;
	ЭлементXDTOМЧД.EntrusterTIN = ДанныеМЧД.ИННДоверителя;
	Если СвойстваМЧДXDTO.Получить("Comment") <> Неопределено Тогда
		ЭлементXDTOМЧД.Comment = ДанныеМЧД.Комментарий;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеМЧД.ИмяФайлаМЧД) Тогда
		ЗаписьZipФайла.Добавить(ВременнаяПапка + ДанныеМЧД.ИмяФайлаМЧД);
		ЭлементXDTOМЧД.WarrantFileName = ДанныеМЧД.ИмяФайлаМЧД;
	ИначеЕсли ДанныеМЧД.ЕстьВРеестреФНС <> Истина Тогда
		ВызватьИсключение НСтр("ru = 'Не заполнен файл доверенности'");
	КонецЕсли;
	Если ЗначениеЗаполнено(ДанныеМЧД.ИмяФайлаПодписиМЧД) Тогда
		ЗаписьZipФайла.Добавить(ВременнаяПапка + ДанныеМЧД.ИмяФайлаПодписиМЧД);
		ЭлементXDTOМЧД.SignatureFileName = ДанныеМЧД.ИмяФайлаПодписиМЧД;
	ИначеЕсли ДанныеМЧД.ЕстьВРеестреФНС <> Истина Тогда
		ВызватьИсключение НСтр("ru = 'Не заполнен файл подписи доверенности'");
	КонецЕсли;
	
	ПотокЗаписи = Новый ЗаписьXML();
	ПотокЗаписи.ОткрытьФайл(ИмяФайлаОписанияМЧД);
	ПотокЗаписи.ЗаписатьОбъявлениеXML();
	Попытка
		ФабрикаXDTO.ЗаписатьXML(ПотокЗаписи, ЭлементXDTOМЧД,,,, НазначениеТипаXML.Явное);
		ПотокЗаписи.Закрыть();
		ЗаписьZipФайла.Добавить(ИмяФайлаОписанияМЧД);
	Исключение
		ОбменСАрхивом.УдалитьВременныеФайлы(ВременнаяПапка);
		ВызватьИсключение;
	КонецПопытки;
	
	Попытка
		ЗаписьZipФайла.Записать();
		ФайлВыгрузки = Новый Файл(ИмяZipФайлаВыгрузки);
		ИмяZipФайлаВыгрузкиФинальное = ОбменСАрхивом.ТекущаяОтметкаВремени() + ".zip";
		ПереместитьФайл(ФайлВыгрузки.ПолноеИмя, ФайлВыгрузки.Путь + ИмяZipФайлаВыгрузкиФинальное);
		ОбменСАрхивомТранспорт.ОтправитьФайл(ФайлВыгрузки.Путь + ИмяZipФайлаВыгрузкиФинальное, ПараметрыТранспорта);
		ОбменСАрхивом.УдалитьВременныеФайлы(ВременнаяПапка);
		
		Событие = ОбменСАрхивом.СобытиеДляЛога(ИмяZipФайлаВыгрузкиФинальное);
		Событие.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Информация;
		Событие.ОсновнойОбъект = МЧДСсылка;
		Событие.КраткоеОписание = НСтр("ru = 'Выгрузка МЧД завершена'");
		ОбменСАрхивом.ЗаписатьВЛог(Событие);
	Исключение
		ОбменСАрхивом.УдалитьВременныеФайлы(ВременнаяПапка);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Возвращает данные указанной МЧД, необходимые для выгрузки в 1С:Архив.
//
// Параметры:
//  МЧДСсылка - СправочникСсылка - ссылка на МЧД.
//  ВременнаяПапка - Строка - временная папка, которую можно использовать для формирования файлов.
//    После выгрузки МЧД папка будет удалена автоматически.
//
// Возвращаемое значение:
//  Структура, Неопределено - свойства МЧД, см. НовоеОписаниеМЧД.
//    Неопределено, если выгрузка этого элемента не требуется.
// 
Функция ДанныеМЧДДляВыгрузки(МЧДСсылка, ВременнаяПапка)
	
	ОписаниеМЧД = Неопределено;
	Если Не ЗначениеЗаполнено(МЧДСсылка) Тогда
		Возврат ОписаниеМЧД;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.МашиночитаемыеДоверенности") Тогда
		
		// В ДО нет этой подсистемы.
		
	Иначе
		
		ОписаниеМЧД = Неопределено;
	
		ТипМЧД = ТипЗнч(МЧДСсылка);
		ОбрабатываемыеТипы = Новый Массив;
		ОбрабатываемыеТипы.Добавить(Тип("СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов"));
		ОбрабатываемыеТипы.Добавить(Тип("СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций"));
		ОбрабатываемыеТипы.Добавить(Тип("СправочникСсылка.МЧД003"));
		Если ОбрабатываемыеТипы.Найти(ТипМЧД) = Неопределено Тогда
			Возврат ОписаниеМЧД;
		КонецЕсли;
		
		Если ТипМЧД = Тип("СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов") Тогда
			РеквизитыМЧД = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				МЧДСсылка, "Подписана, НомерДоверенности, ДоверительИНН, ЕстьВРеестреФНС, Комментарий");
		ИначеЕсли ТипМЧД = Тип("СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций") Тогда
			РеквизитыМЧД = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				МЧДСсылка, "Подписана, НомерДоверенности, ДоверительЮЛ_ИНН, СтатусВРеестреФНС, Комментарий");
		ИначеЕсли ТипМЧД = Тип("СправочникСсылка.МЧД003") Тогда
			РеквизитыМЧД = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				МЧДСсылка, "Подписана, НомерДоверенности, Доверители, СтатусВРеестреФНС, Комментарий");
		КонецЕсли;
		Если РеквизитыМЧД.Подписана <> Истина Тогда
			Возврат ОписаниеМЧД;
		КонецЕсли;
		
		ОписаниеМЧД = НовоеОписаниеМЧД();
		Если ТипМЧД = Тип("СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов") Тогда
			РеквизитыМЧД = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				МЧДСсылка, "НомерДоверенности, ДоверительИНН, ЕстьВРеестреФНС, Комментарий");
			ОписаниеМЧД.Тип = "ON_DOVBB";
			ОписаниеМЧД.НомерДоверенности = РеквизитыМЧД.НомерДоверенности;
			ОписаниеМЧД.ЕстьВРеестреФНС = РеквизитыМЧД.ЕстьВРеестреФНС;
			ОписаниеМЧД.ИННДоверителя = РеквизитыМЧД.ДоверительИНН;
			ОписаниеМЧД.Комментарий = РеквизитыМЧД.Комментарий;
		ИначеЕсли ТипМЧД = Тип("СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций") Тогда
			РеквизитыМЧД = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				МЧДСсылка, "НомерДоверенности, ДоверительЮЛ_ИНН, СтатусВРеестреФНС, Комментарий");
			ДоверительныеСтатусы = МашиночитаемыеДоверенности.ДоверительныеСтатусыВРеестреФНС();
			ОписаниеМЧД.Тип = "ON_DOVBB";
			ОписаниеМЧД.НомерДоверенности = РеквизитыМЧД.НомерДоверенности;
			ОписаниеМЧД.ЕстьВРеестреФНС = ДоверительныеСтатусы.Найти(РеквизитыМЧД.СтатусВРеестреФНС) <> Неопределено;
			ОписаниеМЧД.ИННДоверителя = РеквизитыМЧД.ДоверительЮЛ_ИНН;
			ОписаниеМЧД.Комментарий = РеквизитыМЧД.Комментарий;
		ИначеЕсли ТипМЧД = Тип("СправочникСсылка.МЧД003") Тогда
			РеквизитыМЧД = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				МЧДСсылка, "НомерДоверенности, Доверители, СтатусВРеестреФНС, Комментарий");
			СтрокаДоверителя = РеквизитыМЧД.Доверители.Выгрузить()[0];
			ДоверительныеСтатусы = МашиночитаемыеДоверенности.ДоверительныеСтатусыВРеестреФНС();
			ОписаниеМЧД.Тип = "EMCHD";
			ОписаниеМЧД.НомерДоверенности = РеквизитыМЧД.НомерДоверенности;
			ОписаниеМЧД.ЕстьВРеестреФНС = ДоверительныеСтатусы.Найти(РеквизитыМЧД.СтатусВРеестреФНС) <> Неопределено;
			ОписаниеМЧД.ИННДоверителя = СтрокаДоверителя.ИНН;
			ОписаниеМЧД.Комментарий = РеквизитыМЧД.Комментарий;
		КонецЕсли;
		
		// Файл zip выгружается стандартными средствами БЭД,
		// затем распаковывается и берутся файлы xml и p7s.
		РезультатСохранения = МашиночитаемыеДоверенности.ВыгрузитьДанныеДоверенности(МЧДСсылка);
		Если РезультатСохранения.Ошибка Тогда
			ВызватьИсключение РезультатСохранения.ТекстОшибки;
		КонецЕсли;
		ВременнаяПапкаФайловМЧД = ПолучитьИмяВременногоФайла("") + ПолучитьРазделительПути();
		СоздатьКаталог(ВременнаяПапкаФайловМЧД);
		ИмяZipФайлаДанныхМЧД = ВременнаяПапкаФайловМЧД + РезультатСохранения.ОписаниеФайла.ИмяФайла;
		РезультатСохранения.ОписаниеФайла.ДвоичныеДанные.Записать(ИмяZipФайлаДанныхМЧД);
		ЧтениеZipФайла = Новый ЧтениеZipФайла(ИмяZipФайлаДанныхМЧД);
		ЧтениеZipФайла.ИзвлечьВсе(ВременнаяПапкаФайловМЧД);
		ЧтениеZipФайла.Закрыть();
		НайденныеФайлыXML = НайтиФайлы(ВременнаяПапкаФайловМЧД, "*.xml");
		НайденныеФайлыP7S = НайтиФайлы(ВременнаяПапкаФайловМЧД, "*.p7s");
		Если НайденныеФайлыXML.Количество() = 0 Тогда
			ВызватьИсключение НСтр("ru = 'Не найден файл МЧД'");
		КонецЕсли;
		Если НайденныеФайлыP7S.Количество() = 0 Тогда
			ВызватьИсключение НСтр("ru = 'Не найден файл подписи МЧД'");
		КонецЕсли;
		ФайлМЧД = НайденныеФайлыXML[0];
		ФайлПодписиМЧД = НайденныеФайлыP7S[0];
		ПереместитьФайл(ФайлМЧД.ПолноеИмя, ВременнаяПапка + ФайлМЧД.Имя);
		ПереместитьФайл(ФайлПодписиМЧД.ПолноеИмя, ВременнаяПапка + ФайлПодписиМЧД.Имя);
		УдалитьФайлы(ВременнаяПапкаФайловМЧД);
		
		ОписаниеМЧД.ИмяФайлаМЧД = ФайлМЧД.Имя;
		ОписаниеМЧД.ИмяФайлаПодписиМЧД = ФайлПодписиМЧД.Имя;
		
	КонецЕсли;
	
	Возврат ОписаниеМЧД;
	
КонецФункции

// Возвращает структуру описания МЧД для последующей выгрузки в 1С:Архив.
//
// Возвращаемое значение:
//  Структура:
//   * Тип - Строка - значение из перечисления WarrantType пакета XDTO.
//      "EMCHD" для формата 003, "ON_DOVBB" для предыдущих форматов.
//   * НомерДоверенности - Строка - номер доверенности.
//   * ЕстьВРеестреФНС - Булево - Истина, если доверенность помещена в реестр ФНС.
//   * ИННДоверителя - Строка - ИНН доверителя.
//   * ИмяФайлаМЧД - Строка - локальное имя файла доверенности в папке ВременнаяПапка.
//       Обязательно для нереестровых доверенностей.
//   * ИмяФайлаПодписиМЧД - Строка - локальное имя файла подписи доверенности в папке ВременнаяПапка.
//       Обязательно для нереестровых доверенностей.
//   * Комментарий - Строка - произвольный комментарий.
// 
// Если выгрузка элемента не требуется, возвращается Неопределено.
//
Функция НовоеОписаниеМЧД()
	
	Описание = Новый Структура;
	Описание.Вставить("Тип", "");
	Описание.Вставить("НомерДоверенности", "");
	Описание.Вставить("ЕстьВРеестреФНС", Ложь);
	Описание.Вставить("ИННДоверителя", "");
	Описание.Вставить("ИмяФайлаМЧД", "");
	Описание.Вставить("ИмяФайлаПодписиМЧД", "");
	Описание.Вставить("Комментарий", "");
	
	Возврат Описание;
	
КонецФункции

#КонецОбласти
// Локализация Конец