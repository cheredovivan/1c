
#Область ПрограммныйИнтерфейс

// см. УчетНДС.ЗначениеСтавкиНДС
//
Функция ЗначениеСтавкиНДС(СтавкаНДС) Экспорт
	
	Возврат УчетНДСВызовСервераПовтИсп.ЗначениеСтавкиНДС(СтавкаНДС);
	
КонецФункции

// По части наименования формирует список для выбора ставки НДС.
//
// Параметры:
//  Объект - ДанныеФормыСтруктура - данные документа или номенклатуры на форме.
//  Текст - Строка - введенная с клавиатуры поисковая строка.
//
// Возвращаемое значение:
//  СписокЗначений из СправочникСсылка.СтавкиНДС
//
Функция ДанныеВыбораСтавкиНДС(Объект, Текст = "") Экспорт
	
	ДанныеВыбора = Новый СписокЗначений;
	ТекущаяДатаСеанса = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	Если ТипЗнч(Объект.Ссылка) = Тип("СправочникСсылка.Номенклатура") Тогда
		
		Если ЗначениеЗаполнено(Текст) Тогда
			ДанныеВыбора = УчетНДСВызовСервера.ДанныеВыбораСтавкиНДС(Текст);
		Иначе
			ДанныеВыбора = УчетНДСВызовСервера.ДанныеВыбораСтавкиНДС(, ТекущаяДатаСеанса);
		КонецЕсли;
		
		Возврат ДанныеВыбора;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ДатаРегистрации) Тогда
		ДанныеВыбора = УчетНДСВызовСервера.ДанныеВыбораСтавкиНДС(Текст, Объект.ДатаРегистрации);
	ИначеЕсли Не ЗначениеЗаполнено(Объект.ДатаРегистрации) И Объект.ДатаНачалаДействия > ТекущаяДатаСеанса Тогда
		ДанныеВыбора = УчетНДСВызовСервера.ДанныеВыбораСтавкиНДС(Текст, Объект.ДатаНачалаДействия);
	ИначеЕсли ЗначениеЗаполнено(Текст) Тогда
		ДанныеВыбора = УчетНДСВызовСервера.ДанныеВыбораСтавкиНДС(Текст);
	ИначеЕсли Не ЗначениеЗаполнено(Текст) Тогда
		ДанныеВыбора = УчетНДСВызовСервера.ДанныеВыбораСтавкиНДС(, Объект.ДатаСоздания);
	КонецЕсли;
	
	Возврат ДанныеВыбора;
	
КонецФункции

#КонецОбласти
