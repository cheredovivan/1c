
////////////////////////////////////////////////////////////////////////////////
// Работа с бизнес-процессами клиент
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс_ФормаПроцесса

// Выполняет обработку оповещения в форме бизнес процесса.
// процедура предназначена для вызова из форм объектов бизнес-процессов
//
Процедура ОбработкаОповещенияФормаБизнесПроцесса(ИмяСобытия, Параметр, Источник, Форма) Экспорт
	
	Если ИмяСобытия = "БизнесПроцессИзменен" И Источник <> Форма Тогда
		Если Параметр = Форма.Объект.Ссылка
			ИЛИ (ТипЗнч(Параметр) = Тип("Массив")
				И Параметр.Найти(Форма.Объект.Ссылка) <> Неопределено) Тогда
			
			Попытка
				Форма.ОбновитьДоступностьЭлементовФормыПоСостояниюПроцесса(Истина);
			Исключение
				// У формы может и не быть процедуры ОбновитьДоступностьЭлементовФормыПоСостояниюПроцесса.
				Форма.Прочитать();
			КонецПопытки;
			
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "СтартПроцессаПослеВопроса"
		И Параметр.ИдентификаторФормы = Форма.УникальныйИдентификатор Тогда
		
		Если Параметр.ПараметрыЗаписиПроцесса.Свойство(
			"ИзменениеОтложенногоПроцесса") Тогда
			
			Если Форма.Записать(Параметр.ПараметрыЗаписиПроцесса)
				И Параметр.ПараметрыЗаписиПроцесса.Свойство(
					"ЗакрытьФормуПослеЗаписи") Тогда
				
				Форма.Закрыть();
			КонецЕсли;
			
		ИначеЕсли Параметр.ПараметрыЗаписиПроцесса.Свойство(
			"ИзменениеСостоянияПроцесса") Тогда
			
			Если Форма.Записать(Параметр.ПараметрыЗаписиПроцесса) Тогда
			
				ПоказатьОповещениеПользователя(
					Параметр.ПараметрыЗаписиПроцесса.
						СообщениеПриИзмененииСостоянияПроцесса,
					ПолучитьНавигационнуюСсылку(Форма.Объект.Ссылка),
					Строка(Форма.Объект.Ссылка),
					БиблиотекаКартинок.Информация32);
					ОповеститьОбИзменении(Форма.Объект.Ссылка);
				ОповеститьОбИзменении(Форма.Объект.Ссылка);
				
			КонецЕсли;
			
		Иначе
			Если Форма.Записать(Параметр.ПараметрыЗаписиПроцесса)
				И Параметр.ПараметрыЗаписиПроцесса.Свойство(
					"ЗакрытьФормуПослеЗаписи") Тогда
				
				Форма.Закрыть();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДекорацияОписаниеОбработкаНавигационнойСсылки(
	Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка, Форма) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	СтартПроцессовКлиент.ДекорацияОписаниеОбработкаНавигационнойСсылки(
		НавигационнаяСсылкаФорматированнойСтроки, Форма);
	
КонецПроцедуры

// Выполняет обработку нажатия на декорацию описания в карточках процессов.
//
// Параметры:
//  Элемент - ДекорацияФормы - декорация с описанием состояния в карточке процесса.
//  Форма - ФормаКлиентскогоПриложения - карточка процесса.
//
Процедура ДекорацияОписаниеНажатие(Элемент, Форма) Экспорт
	
	Если Форма.Объект.Состояние = ПредопределенноеЗначение(
		"Перечисление.СостоянияБизнесПроцессов.Прерван") Тогда
		
		КомандыРаботыСБизнесПроцессамиКлиент.ПоказатьПричинуПрерывания(Форма);
		
	ИначеЕсли ТипЗнч(Форма.НастройкаСтарта) = Тип("Структура")
		И Не ЗначениеЗаполнено(Форма.НастройкаСтарта.ДатаОтложенногоСтарта)
		И Форма.НастройкаСтарта.Состояние = ПредопределенноеЗначение(
			"Перечисление.СостоянияПроцессовДляЗапуска.СтартОтменен") Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Ошибка при старте процесса'"));
		ПараметрыФормы.Вставить("ТекстСообщения", Форма.НастройкаСтарта.ПричинаОтменыСтарта);
		ОткрытьФорму("ОбщаяФорма.Сообщение", ПараметрыФормы, Форма);
		
	КонецЕсли;
	
КонецПроцедуры

// Показывает оповещение после записи процесса из карточки.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса.
//
Процедура ПоказатьОповещениеПослеЗаписиПроцесса(Форма, ПараметрыЗаписи) Экспорт
	
	Если ПараметрыЗаписи.Свойство("Старт") И ПараметрыЗаписи.Старт
		Или ОбработкаОчередиЗаданийКлиентСервер.ЭтоСтартПроцессаЧерезОчередьЗаданий(ПараметрыЗаписи)
		Или ПараметрыЗаписи.Свойство("ОтложенныйСтартПроцесса") И ПараметрыЗаписи.ОтложенныйСтартПроцесса Тогда
		
		ТекстОпопвещения = НСтр("ru = 'Старт'");
	Иначе
		ТекстОпопвещения = НСтр("ru = 'Изменение:'");
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(
		ТекстОпопвещения,
		ПолучитьНавигационнуюСсылку(Форма.Объект.Ссылка),
		Строка(Форма.Объект.Ссылка),
		БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_КоличествоИтераций

// Обработчик ПриИзменении поля КоличествоИтераций в карточке процесса/шаблона.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//
Процедура КоличествоИтерацийПриИзменении(Форма) Экспорт
	
	Форма.ОбновитьСрокиИсполненияОтложенно("КоличествоИтераций");
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_УчастникПроцесса

// Обработчик ПриИзменении поля участника процесса/шаблона.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  ОписаниеОповещения - описание оповещения, которое следует выполнить в этой процедуры.
//
Процедура УчастникПриИзменении(Форма, ОписаниеОповещения = Неопределено) Экспорт
	
	Попытка
		Форма.ИзменитьРеквизитыНевыполненныхЗадач = Истина;
	Исключение
	КонецПопытки;
	
	Форма.Модифицированность = Истина;
	
	Если ОписаниеОповещения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик ПриИзменении поля участника со сроком исполнения процесса/шаблона.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  Участник - строка - наименование реквизита участника в процессе (Например, Исполнитель, Проверяющий, Автор).
//
Процедура УчастникСоСрокомИсполненияПриИзменении(Форма, Участник) Экспорт
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Форма", Форма);
	ДопПараметры.Вставить("Участник", Участник);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОбновитьСтрокиИсполненияПриИзмененииУчастника", ЭтотОбъект, ДопПараметры);
	
	УчастникПриИзменении(Форма, ОписаниеОповещения);
	
КонецПроцедуры

// Продолжение процедуры УчастникСоСрокомИсполненияПриИзменении
Процедура ОбновитьСтрокиИсполненияПриИзмененииУчастника(Результат , ДопПараметры) Экспорт
	
	Форма = ДопПараметры.Форма;
	Участник = ДопПараметры.Участник;
	
	Форма.ОбновитьСрокиИсполненияОтложенно(Участник);
	
КонецПроцедуры

// Обработчик НачалоВыбора поля участника процесса/шаблона.
//
// Параметры:
//  Элемент - ПолеФормы - поле участника в карточке процесса/шаблона.
//  ВыбранноеЗначение - СправочникСсылка.Пользователи,
//                      СправочникСсылка.ПолныеРоли,
//                      Строка - выбранный участник.
//  СтандартнаяОбработка - Булево - признак стандартной обработки начала выбора.
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  Участник - строка - наименование реквизита участника в процессе (Например, Исполнитель, Проверяющий, Автор).
//  ОписаниеОповещения - описание оповещения, которое следует выполнить после этого обработчика.
//
Процедура УчастникНачалоВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, Форма,
	Участник = "", ОписаниеОповещения = Неопределено) Экспорт
	
	Попытка
		ЭтоШаблон = ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(Форма.Объект.Ссылка);
	Исключение
		ЭтоШаблон = Ложь;
	КонецПопытки;
	
	Если ЭтоШаблон Тогда
		РаботаСАдреснойКнигойКлиент.ВыбратьУчастникаДляШаблонаПроцесса(
			Элемент, ВыбранноеЗначение, СтандартнаяОбработка, Форма, Участник, ОписаниеОповещения);
	Иначе
		РаботаСАдреснойКнигойКлиент.ВыбратьУчастникаПроцесса(
			Элемент, ВыбранноеЗначение, СтандартнаяОбработка, Форма, Участник, ОписаниеОповещения);
	КонецЕсли;
		
КонецПроцедуры

// Обработчик НачалоВыбора поля участника со сроком исполнения процесса/шаблона.
//
// Параметры:
//  Элемент - ПолеФормы - поле участника в карточке процесса/шаблона.
//  ВыбранноеЗначение - СправочникСсылка.Пользователи,
//                      СправочникСсылка.ПолныеРоли,
//                      Строка - выбранный участник.
//  СтандартнаяОбработка - Булево - признак стандартной обработки начала выбора.
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  Участник - строка - наименование реквизита участника в процессе (Например, Исполнитель, Проверяющий, Автор).
//
Процедура УчастникСоСрокомИсполненияНачалоВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, Форма,
	Участник = "") Экспорт
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Форма", Форма);
	ДопПараметры.Вставить("Участник", Участник);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗавершениеВыбораУчастникСоСрокомИсполнения", ЭтотОбъект, ДопПараметры);
		
	УчастникНачалоВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, Форма, Участник, ОписаниеОповещения);
		
КонецПроцедуры

// Продолжение процедуры УчастникСоСрокомИсполненияНачалоВыбора.
Процедура ЗавершениеВыбораУчастникСоСрокомИсполнения(ВыбранноеЗначение, ДопПараметры) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДопПараметры.Форма;
	Участник = ДопПараметры.Участник;
	
	Форма.ОбновитьСрокиИсполненияОтложенно(Участник);
	
КонецПроцедуры

// Обработчик Очистка поля участника процесса/шаблона.
//
// Параметры:
//  СтандартнаяОбработка - Булево - признак стандартной обработки очистки.
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  Участник - строка - наименование реквизита участника в процессе (Например, Исполнитель, Проверяющий, Автор).
//
Процедура УчастникОчистка(СтандартнаяОбработка, Форма, Участник) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(Форма.Объект[Участник]) = Тип("СправочникСсылка.Сотрудники") Тогда
		Форма.Объект[Участник] = ПредопределенноеЗначение("Справочник.Сотрудники.ПустаяСсылка");
	Иначе
		Форма.Объект[Участник] = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
	КонецЕсли;
	
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

// Обработчик Очистка поля участника со сроком исполнения процесса/шаблона.
//
// Параметры:
//  СтандартнаяОбработка - Булево - признак стандартной обработки очистки.
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  Участник - строка - наименование реквизита участника в процессе (Например, Исполнитель, Проверяющий, Автор).
//
Процедура УчастникСоСрокомИсполненияОчистка(СтандартнаяОбработка, Форма, Участник) Экспорт
	
	УчастникОчистка(СтандартнаяОбработка, Форма, Участник);
	
	Форма.ОбновитьСрокиИсполненияОтложенно(Участник);
	
КонецПроцедуры

// Обработчик Очистка поля участника процесса/шаблона.
//
// Параметры:
//  СтандартнаяОбработка - Булево - признак стандартной обработки открытия.
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  Участник - строка - наименование реквизита участника в процессе (Например, Исполнитель, Проверяющий, Автор).
//
Процедура УчастникОткрытие(СтандартнаяОбработка, Форма, Участник) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Форма.Объект[Участник]) Тогда
		ПоказатьЗначение(, Форма.Объект[Участник]);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик ОбработкаВыбора поля участника процесса/шаблона.
//
// Параметры:
//  СтандартнаяОбработка - Булево - признак стандартной обработки выбора.
//
Процедура УчастникОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	СотрудникиКлиент.ОбработкаВыбораКонтейнера(
		Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

// Обработчик АвтоПодбор поля участника процесса/шаблона.
//
// Параметры:
//  Текст - Строка - Строка текста, введенная в поле
//  ДанныеВыбора - СписокЗначений - В этот список следует поместить данные для выбора.
//  СтандартнаяОбработка - Булево - признак стандартной обработки автоподбора.
//
Процедура УчастникАвтоПодбор(Текст, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДополнениеТипа = Новый ОписаниеТипов("СправочникСсылка.ПолныеРоли");
		ДанныеВыбора = СотрудникиВызовСервера.СформироватьДанныеВыбора(Текст, ДополнениеТипа);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик ОкончаниеВводаТекста поля участника процесса/шаблона.
//
// Параметры:
//  Текст - Строка - Строка текста, введенная в поле
//  ДанныеВыбора - СписокЗначений - В этот список следует поместить данные для выбора.
//  СтандартнаяОбработка - Булево - признак стандартной обработки автоподбора.
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  Участник - строка - наименование реквизита участника в процессе (Например, Исполнитель, Проверяющий, Автор).
//
Процедура УчастникОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка, Форма, Участник) Экспорт
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДополнениеТипа = Новый ОписаниеТипов("СправочникСсылка.ПолныеРоли");
		ДанныеВыбора = СотрудникиВызовСервера.СформироватьДанныеВыбора(Текст, ДополнениеТипа);
	Иначе
		
		Если ТипЗнч(Форма.Объект[Участник]) = Тип("СправочникСсылка.Сотрудники") Тогда
			Форма.Объект[Участник] = ПредопределенноеЗначение("Справочник.Сотрудники.ПустаяСсылка");
		Иначе
			Форма.Объект[Участник] = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
		КонецЕсли;
		
		Форма.Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_ТаблицаИсполнители

// Вызывает диалог подбора исполнителей для процесса Исполнения
// с последующим помщением выбранных исполнителей в процесс.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  ЭлементТаблица - ТаблицаФормы - элемент управления таблица.
//  РеквизитТаблица - ДанныеФормыКоллекция - реквизит формы.
//
Процедура ПодобратьИсполнителей(Форма, ЭлементТаблица, РеквизитТаблица) Экспорт
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Форма", Форма);
	ДопПараметры.Вставить("ЭлементТаблица", ЭлементТаблица);
	ДопПараметры.Вставить("РеквизитТаблица", РеквизитТаблица);
	
	ОписаниеОповещенияОбработкиВыбора = Новый ОписаниеОповещения(
		"ЗавершитьПодборИсполнителейДляПроцессов", ЭтотОбъект, ДопПараметры);
	
	ЭтоШаблонПроцесса = ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(Форма.Объект.Ссылка);
	
	Если ЭтоШаблонПроцесса Тогда
		РаботаСАдреснойКнигойКлиент.ПодобратьИсполнителейДляШаблоновПроцессов(
			Форма, ОписаниеОповещенияОбработкиВыбора);
	Иначе
		РаботаСАдреснойКнигойКлиент.ПодобратьИсполнителейДляПроцессов(
			Форма, РеквизитТаблица, ОписаниеОповещенияОбработкиВыбора);
	КонецЕсли;
	
КонецПроцедуры

// см. ПодобратьИсполнителей
Процедура ЗавершитьПодборИсполнителейДляПроцессов(
	ВыбранныеИсполнители, ДопПараметры) Экспорт
	
	Если ВыбранныеИсполнители = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДопПараметры.Форма;
	ЭлементТаблица = ДопПараметры.ЭлементТаблица;
	РеквизитТаблица = ДопПараметры.РеквизитТаблица;
	
	КоличествоСтрок = РеквизитТаблица.Количество();
	
	ИспользоватьДатуИВремяВСрокахЗадач = Форма.ИспользоватьДатуИВремяВСрокахЗадач;
	
	// Удаление пропавших строк.
	РеквизитыСрока =
		"ВариантУстановкиСрокаИсполнения, СрокИсполнения,
		|СрокИсполненияДни, СрокИсполненияЧасы, СрокИсполненияМинуты";
	ДанныеСрокаПоследнейСтроки = Новый Структура(РеквизитыСрока);
	Для Инд = 1 По КоличествоСтрок Цикл
		
		Строка = РеквизитТаблица[КоличествоСтрок - Инд];
		
		УдалитьИсполнителя = (ВыбранныеИсполнители.Найти(Строка.Исполнитель) = Неопределено);
		Если УдалитьИсполнителя Тогда
			ЗаполнитьЗначенияСвойств(ДанныеСрокаПоследнейСтроки, Строка, РеквизитыСрока);
			РеквизитТаблица.Удалить(Строка);
		КонецЕсли;
		
	КонецЦикла;
	
	Если РеквизитТаблица.Количество() <> 0 Тогда
		ДанныеСрокаПоследнейСтроки = Неопределено;
	КонецЕсли;
	
	ИндексИзмененнойСтроки = РеквизитТаблица.Количество();
	
	// Добавление новых строк.
	Для Каждого ВыбранныйАдресат Из ВыбранныеИсполнители Цикл
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Исполнитель", ВыбранныйАдресат);
		
		НайденныеСтроки = РеквизитТаблица.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() = 0 Тогда
			
			НоваяСтрокаИсполнитель = РеквизитТаблица.Добавить();
			НоваяСтрокаИсполнитель.Исполнитель = ВыбранныйАдресат;
			
			// Если удаляется последняя строка - то её срок переносится ко вновь добавленным строкам.
			Если ДанныеСрокаПоследнейСтроки <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(НоваяСтрокаИсполнитель, ДанныеСрокаПоследнейСтроки, РеквизитыСрока);
			КонецЕсли;
			
			СрокиИсполненияПроцессовКлиент.ЗаполнитьСрокВНовойСтрокеТаблицыИсполнители(
				НоваяСтрокаИсполнитель,
				РеквизитТаблица,
				Неопределено,
				ИспользоватьДатуИВремяВСрокахЗадач);
		КонецЕсли;
		
	КонецЦикла;
	
	Форма.Модифицированность = Истина;
	
	Форма.ОбновитьСрокиИсполненияОтложенно("Исполнители", ИндексИзмененнойСтроки);
	
	Оповестить("ЗавершенПодборИсполнителей", Форма);
	
КонецПроцедуры

// Проставляет порядок исполнения у исполнителей в зависимости от выбранного варианта исполнения
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - карточка процесса, шаблона процесса
//   ВариантИсполнения - ПеречислениеСсылка.ВариантыМаршрутизацииЗадач - вариант исполнения задач
//   ПредыдущийВариантИсполнения -  ПеречислениеСсылка.ВариантыМаршрутизацииЗадач - предыдущий вариант исполнения задач
//   ТаблицаИсполнители - ДанныеФормыКоллекция - таблици исполнители в форме
//   ИмяРеквизитаПорядокИсполнения - Строка - Имя реквизита ПорядокИсполнения в таблице исполнителей.
//
Процедура ПриИзмененииВариантаИсполнения(Форма,
	ВариантИсполнения,
	ПредыдущийВариантИсполнения,
	ТаблицаИсполнители,
	ИмяРеквизитаПорядокИсполнения = "ПорядокИсполнения") Экспорт
	
	Если ВариантИсполнения = ПредыдущийВариантИсполнения Тогда
		Возврат;
	КонецЕсли;
	
	Если ТаблицаИсполнители.Количество() = 0 Тогда
		ПредыдущийВариантИсполнения = ВариантИсполнения;
		Возврат;
	КонецЕсли;
	
	ВариантыМаршуртизацииЗадач = РаботаСБизнесПроцессамиКлиентСервер.ВариантыМаршуртизацииЗадач();
	ВариантыПорядкаВыполненияЗадач = РаботаСБизнесПроцессамиКлиентСервер.ВариантыПорядкаВыполненияЗадач();
	
	Если ВариантИсполнения = ВариантыМаршуртизацииЗадач.Смешанно Тогда
		Для Каждого Строка Из ТаблицаИсполнители Цикл
			Если ПредыдущийВариантИсполнения = ВариантыМаршуртизацииЗадач.Последовательно Тогда
				Строка[ИмяРеквизитаПорядокИсполнения] = ВариантыПорядкаВыполненияЗадач.ПослеПредыдущего;
			ИначеЕсли ПредыдущийВариантИсполнения = ВариантыМаршуртизацииЗадач.Параллельно Тогда
				Строка[ИмяРеквизитаПорядокИсполнения] = ВариантыПорядкаВыполненияЗадач.ВместеСПредыдущим;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ВариантИсполнения = ВариантыМаршуртизацииЗадач.Параллельно Тогда
		Для Каждого Строка Из ТаблицаИсполнители Цикл
			Строка[ИмяРеквизитаПорядокИсполнения] = ВариантыПорядкаВыполненияЗадач.ВместеСПредыдущим;
		КонецЦикла;
	ИначеЕсли ВариантИсполнения = ВариантыМаршуртизацииЗадач.Последовательно Тогда
		Для Каждого Строка Из ТаблицаИсполнители Цикл
			Строка[ИмяРеквизитаПорядокИсполнения] = ВариантыПорядкаВыполненияЗадач.ПослеПредыдущего;
		КонецЦикла;
	КонецЕсли;
	
	ПредыдущийВариантИсполнения = ВариантИсполнения;
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьШаг(ТаблицаИсполнители);
	
	ИндексПервойСтроки = 0;
	Если ТаблицаИсполнители[0].Свойство("Ответственный")
		И ТаблицаИсполнители[0].Ответственный Тогда
		
		ИндексПервойСтроки = 1;
	КонецЕсли;
	
	СрокиИсполненияПроцессовКлиент.ЗаполнитьСрокиИсполненияВТаблицеИсполнителейПоТекущейСтроке(
		ТаблицаИсполнители, ТаблицаИсполнители[ИндексПервойСтроки], ВариантИсполнения);
	
	Форма.ЗаполнитьПредставлениеСроковИсполнения();
	
	Форма.ОбновитьСрокиИсполненияОтложенно("ВариантИсполнения");
	
КонецПроцедуры

// Обработчик события выбора таблицы исполнителей
Процедура ИсполнителиВыбор(Форма, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка) Экспорт
	
	Если Форма.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("ИсполнителиОписаниеУсловия") <> Неопределено
		И Поле = Форма.Элементы.ИсполнителиОписаниеУсловия Тогда
		
		ИменаПредметов = МультипредметностьКлиентСервер.ПолучитьМассивИменПредметовОбъекта(Форма.Объект);
		
		МультипредметностьКлиент.УстановкаАлгоритмаПроверки(
			ИменаПредметов, Элемент, Поле, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события ПриАктивизацииСтроки таблицы исполнителей
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса, шаблона процесса
//  ЭлементТаблица - ТаблицаФормы - элемент управления таблицей Исполнители.
//  ЭлементПредставлениеСрока - Полеформы - элемент представление срока исполнения в таблице Исполнители.
//  ДоступностьПоШаблону - Булево - принимает Истину, если процесс не ограничен соответствующей настройкой
//                                  доступности в шаблоне.
//  ВестиУчетПереносаСроков - Булево - значение настройки ведения учета переноса срока.
//  ЗаявкаНаПереносСрока - БизнесПроцессСсылка.РешениеВопросовВыполненияЗадач - заявка на перенос срока.
//
Процедура ИсполнителиПриАктивизацииСтроки(
	Форма,
	ЭлементИсполнители,
	ЭлементПредставлениеСрока,
	ДоступностьПоШаблону = Неопределено,
	ВестиУчетПереносаСроков = Неопределено,
	ЗаявкаНаПереносСрока = Неопределено) Экспорт
	
	ТекущиеДанные = ЭлементИсполнители.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыДоступности = 
		СрокиИсполненияПроцессовКлиентСервер.ПараметрыДоступностиЭлементаУправления();
	ПараметрыДоступности.ДоступностьПоШаблону = ДоступностьПоШаблону;
	ПараметрыДоступности.ВестиУчетПереносаСроков = ВестиУчетПереносаСроков;
	ПараметрыДоступности.ЗаявкаНаПереносСрока = ЗаявкаНаПереносСрока;
	
	СрокиИсполненияПроцессовКлиентСервер.НастроитьЭлементУправленияСроком(
		Форма,
		ЭлементПредставлениеСрока,
		ТекущиеДанные.СрокИсполненияПредставление,
		ПараметрыДоступности);
	
КонецПроцедуры

// Обработчик события начала редактирования таблицы исполнителей
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса, шаблона процесса
//  НоваяСтрока - Булево - Признак новой строки в таблице ЭлементИсполнители
//  ЭлементИсполнители - ТаблицаФормы - элемент управления таблицей Исполнители.
//  РеквизитИсполнители - ДанныеФормыКоллекция - реквизит формы с таблицей Исполнителей.
//  ВариантМаршрутизацииЗадач - ПеречислениеСсылка.ВариантыМаршрутизацииЗадач
//  ИмяРеквизитаПорядокИсполнения - Строка - имя реквизита ПорядокИсполнения в таблице Исполнители.
//
Процедура ИсполнителиПриНачалеРедактирования(
	Форма,
	НоваяСтрока,
	ЭлементИсполнители,
	РеквизитИсполнители,
	ВариантМаршрутизацииЗадач = Неопределено,
	ИмяРеквизитаПорядокИсполнения = Неопределено) Экспорт
	
	Если НоваяСтрока Тогда
		
		ТекущиеДанные = ЭлементИсполнители.ТекущиеДанные;
		ТекущиеДанные.Исполнитель = ПредопределенноеЗначение("Справочник.Сотрудники.ПустаяСсылка");
		
		Если ЗначениеЗаполнено(ИмяРеквизитаПорядокИсполнения) Тогда
			
			ВариантыМаршуртизацииЗадач = РаботаСБизнесПроцессамиКлиентСервер.ВариантыМаршуртизацииЗадач();
			ВариантыПорядкаВыполненияЗадач = РаботаСБизнесПроцессамиКлиентСервер.ВариантыПорядкаВыполненияЗадач();
			
			Если ВариантМаршрутизацииЗадач = ВариантыМаршуртизацииЗадач.Смешанно Тогда
				ИндексСтроки = РеквизитИсполнители.Индекс(ТекущиеДанные);
				Если ИндексСтроки > 0 Тогда
					ДанныеПредыдущейСтроки = РеквизитИсполнители[ИндексСтроки - 1];
					ТекущиеДанные[ИмяРеквизитаПорядокИсполнения] = ДанныеПредыдущейСтроки[ИмяРеквизитаПорядокИсполнения];
				Иначе
					ТекущиеДанные[ИмяРеквизитаПорядокИсполнения] = ВариантыПорядкаВыполненияЗадач.ПослеПредыдущего;
				КонецЕсли;
			ИначеЕсли ВариантМаршрутизацииЗадач = ВариантыМаршуртизацииЗадач.Последовательно Тогда
				ТекущиеДанные[ИмяРеквизитаПорядокИсполнения] = ВариантыПорядкаВыполненияЗадач.ПослеПредыдущего;
			Иначе
				ТекущиеДанные[ИмяРеквизитаПорядокИсполнения] = ВариантыПорядкаВыполненияЗадач.ВместеСПредыдущим;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Форма.Элементы.Найти("ИсполнителиОписаниеУсловия") <> Неопределено Тогда
			ТекущиеДанные.ОписаниеУсловия = МультипредметностьКлиентСервер.ПолучитьТекстОписанияУсловия(
				ТекущиеДанные.ИмяПредметаУсловия, ТекущиеДанные.Условие);
		КонецЕсли;
		
		СрокиИсполненияПроцессовКлиент.ЗаполнитьСрокВНовойСтрокеТаблицыИсполнители(
			ТекущиеДанные,
			РеквизитИсполнители,
			ВариантМаршрутизацииЗадач,
			Форма.ИспользоватьДатуИВремяВСрокахЗадач);
			
		Если ЗначениеЗаполнено(ИмяРеквизитаПорядокИсполнения) Тогда
			РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьШаг(РеквизитИсполнители);
			ОбновитьДоступностьКомандПеремещения(Форма, ЭлементИсполнители, РеквизитИсполнители);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события при окончании редактирования в таблице исполнителей
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса, шаблона процесса
//  НоваяСтрока - Булево - Признак новой строки в таблице ЭлементИсполнители
//  ОтменаРедактирования - Булево - Признак отмены рекдактирования строки в таблице ЭлементИсполнители.
//  ЭлементИсполнители - ТаблицаФормы - элемент управления таблицей Исполнители.
//  РеквизитИсполнители - ДанныеФормыКоллекция - реквизит формы с таблицей Исполнителей.
//
Процедура ИсполнителиПриОкончанииРедактирования(
	Форма, НоваяСтрока, ОтменаРедактирования, ЭлементИсполнители, РеквизитИсполнители) Экспорт
	
	ВыполнитьОбновлениеСроков = Ложь;
	ИндексИзмененнойСтроки = РеквизитИсполнители.Количество();
	
	Если ОтменаРедактирования Тогда
		ВыполнитьОбновлениеСроков = Истина;
	ИначеЕсли НоваяСтрока Тогда
		
		ВариантыУстановкиСрокаИсполнения = 
			СрокиИсполненияПроцессовКлиентСервер.ВариантыУстановкиСрокаИсполнения();
			
		ТекущиеДанные = ЭлементИсполнители.ТекущиеДанные;
		
		Если ТекущиеДанные.ВариантУстановкиСрокаИсполнения = 
			ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок
				И Не ЗначениеЗаполнено(ТекущиеДанные.СрокИсполнения) Тогда
			
			ВыполнитьОбновлениеСроков = Истина;
		ИначеЕсли ТекущиеДанные.ВариантУстановкиСрокаИсполнения = 
			ВариантыУстановкиСрокаИсполнения.ТочныйСрок
				И (ТекущиеДанные.СрокИсполненияДни = 0
					Или ТекущиеДанные.СрокИсполненияЧасы = 0
					Или ТекущиеДанные.СрокИсполненияМинуты = 0) Тогда
			
			ВыполнитьОбновлениеСроков = Истина;
		КонецЕсли;
		
		ИндексИзмененнойСтроки = РеквизитИсполнители.Индекс(ТекущиеДанные);
	КонецЕсли;
	
	Если ВыполнитьОбновлениеСроков Тогда
		Форма.ОбновитьСрокиИсполненияОтложенно("Исполнители", ИндексИзмененнойСтроки);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события после удаления в таблице исполнителей
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса, шаблона процесса
//  РеквизитИсполнители - ДанныеФормыКоллекция - реквизит формы с таблицей Исполнителей.
//
Процедура ИсполнителиПослеУдаления(Форма, РеквизитИсполнители) Экспорт
	
	ИндексИзмененнойСтроки = РеквизитИсполнители.Количество();
	
	Форма.ОбновитьСрокиИсполненияОтложенно("Исполнители", ИндексИзмененнойСтроки);
	
КонецПроцедуры

// Обработчик события при изменении исполнителя
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса, шаблона процесса
//  ЭлементИсполнители - ТаблицаФормы - элемент управления таблицей Исполнители.
//  РеквизитИсполнители - ДанныеФормыКоллекция - реквизит формы с таблицей Исполнителей.
//
Процедура ИсполнительПриИзменении(Форма, ЭлементИсполнители, РеквизитИсполнители) Экспорт
	
	ТекущиеДанные = ЭлементИсполнители.ТекущиеДанные;
	
	ИндексИзмененнойСтроки = РеквизитИсполнители.Индекс(ТекущиеДанные);
	
	Форма.Модифицированность = Истина;
	
	Форма.ОбновитьСрокиИсполненияОтложенно("Исполнители", ИндексИзмененнойСтроки);
	
КонецПроцедуры

// Обработчик события НачалоВыбора исполнителя
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса, шаблона процесса
//  СтандартнаяОбработка - Булево - признак стандартной обработки начала выбора.
//  ЭлементИсполнители - ТаблицаФормы - элемент управления таблицей Исполнители.
//  РеквизитИсполнители - ДанныеФормыКоллекция - реквизит формы с таблицей Исполнителей.
//
Процедура ИсполнительНачалоВыбора(Форма, СтандартнаяОбработка,
	ЭлементИсполнители, РеквизитИсполнители) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Стартован = Ложь;
	Если СтрЗаканчиваетсяНа(Форма.ИмяФормы, ".ФормаБизнесПроцесса")
		И Форма.Объект.Свойство("Стартован") Тогда
		
		Стартован = Форма.Объект.Стартован;
	КонецЕсли;
	
	ТекущиеДанные = ЭлементИсполнители.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Исполнитель) Или Стартован Тогда
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Форма", Форма);
		ПараметрыОповещения.Вставить("ЭлементИсполнители", ЭлементИсполнители);
		ПараметрыОповещения.Вставить("РеквизитИсполнители", РеквизитИсполнители);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ЗавершениеВыбораИсполнителя", ЭтотОбъект, ПараметрыОповещения);
		
		Если ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(Форма.Объект.Ссылка) Тогда
			РаботаСАдреснойКнигойКлиент.ВыбратьИсполнителяДляШаблонаПроцесса(
				Форма, ЭлементИсполнители, ОписаниеОповещения);
		Иначе
			РаботаСАдреснойКнигойКлиент.ВыбратьИсполнителяДляПроцесса(
				Форма, ЭлементИсполнители, ОписаниеОповещения);
		КонецЕсли;
	Иначе
		ПодобратьИсполнителей(Форма, ЭлементИсполнители, РеквизитИсполнители);
	КонецЕсли;
	
КонецПроцедуры

// см. процедуры ИсполнительНачалоВыбора,
//               ИсполнительИсполненияНачалоВыбора,
//               ИсполнительСогласованияНачалоВыбора
Процедура ЗавершениеВыбораИсполнителя(Результат, ПараметрыОповещения) Экспорт
	
	Форма = ПараметрыОповещения.Форма;
	ЭлементИсполнители = ПараметрыОповещения.ЭлементИсполнители;
	РеквизитИсполнители = ПараметрыОповещения.РеквизитИсполнители;
	
	ТекущиеДанные = ЭлементИсполнители.ТекущиеДанные;
	
	ИндексИзмененнойСтроки = РеквизитИсполнители.Индекс(ТекущиеДанные);
	
	Форма.ОбновитьСрокиИсполненияОтложенно("Исполнители", ИндексИзмененнойСтроки);
	
КонецПроцедуры

// Обработчик события очистки поля исполнитель
//
// Параметры:
//  СтандартнаяОбработка - Булево - признак стандартной обработки начала выбора.
//  ЭлементИсполнители - ТаблицаФормы - элемент управления таблицей Исполнители.
//
Процедура ИсполнительОчистка(СтандартнаяОбработка, ЭлементИсполнители) Экспорт
	
	ТекущиеДанные = ЭлементИсполнители.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные.Исполнитель = ПредопределенноеЗначение("Справочник.Сотрудники.ПустаяСсылка");
	
КонецПроцедуры

// Обработчик события ОбработкаВыбора поля исполнитель
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса, шаблона процесса
//  ВыбранноеЗначение - СправочниСсылка.Пользователи,
//                      СправочниСсылка.ПолныеРоли,
//                      Строка - исполнитель процесса/шаблона или автоподстановка.
//  ЭлементИсполнители - ТаблицаФормы - элемент управления таблицей Исполнители.
//
Процедура ИсполнительОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	СотрудникиКлиент.ОбработкаВыбораКонтейнера(
		Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

// Обработчик события АвтоПодбор поля исполнитель
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса, шаблона процесса
//  Текст - Введенный текст
//  ДанныеВыбора - СправочниСсылка.Пользователи,
//                 СправочниСсылка.ПолныеРоли,
//                 Строка - исполнитель процесса/шаблона или автоподстановка.
//  СтандартнаяОбработка - Булево - признак стандартной обработки авто подбора.
//
Процедура ИсполнительАвтоПодбор(
	Форма, Текст, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДополнениеТипа = Новый ОписаниеТипов(
			"СправочникСсылка.АвтоподстановкиДляПроцессов, СправочникСсылка.ПолныеРоли");
		ДанныеВыбора = СотрудникиВызовСервера.СформироватьДанныеВыбора(Текст, ДополнениеТипа);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события ОкончаниеВводаТекста поля исполнитель
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса, шаблона процесса
//  Текст - Введенный текст
//  ДанныеВыбора - СправочниСсылка.Пользователи,
//                 СправочниСсылка.ПолныеРоли,
//                 Строка - исполнитель процесса/шаблона или автоподстановка.
//  СтандартнаяОбработка - Булево - признак стандартной обработки авто подбора.
//
Процедура ИсполнительОкончаниеВводаТекста(Форма, Текст, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДополнениеТипа = Новый ОписаниеТипов(
			"СправочникСсылка.АвтоподстановкиДляПроцессов, СправочникСсылка.ПолныеРоли");
		ДанныеВыбора = СотрудникиВызовСервера.СформироватьДанныеВыбора(Текст, ДополнениеТипа);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик ПриИзменении у поля ПорядокИсполнения
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса, шаблона процесса
//  ЭлементИсполнители - ТаблицаФормы - элемент управления таблицей Исполнители.
//  РеквизитИсполнители - ДанныеФормыКоллекция - реквизит формы с таблицей Исполнителей.
//
Процедура ПорядокИсполненияПриИзмененииТаблицыИсполнители(Форма, ЭлементИсполнители, РеквизитИсполнители) Экспорт
	
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьШаг(РеквизитИсполнители);
	
	ТекущиеДанные = ЭлементИсполнители.ТекущиеДанные;
	
	ИндексИзмененнойСтроки = РеквизитИсполнители.Индекс(ТекущиеДанные);
	
	// Сроки исполнения процессов
	Форма.ОбновитьСрокиИсполненияОтложенно("Исполнители", ИндексИзмененнойСтроки);
	
КонецПроцедуры

// Устанавливает доступность команд ПереместитьВверх и ПереместитьВниз в зависимости от
// положения строки.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса, шаблона процесса
//  ЭлементИсполнители - ТаблицаФормы - элемент управления таблицей Исполнители.
//  РеквизитИсполнители - ДанныеФормыКоллекция - реквизит формы с таблицей Исполнителей.
//
Процедура ОбновитьДоступностьКомандПеремещения(Форма, ЭлементИсполнители, РеквизитИсполнители) Экспорт
	
	Форма.Элементы.ПереместитьВверх.Доступность = Ложь;
	Форма.Элементы.ПереместитьВниз.Доступность = Ложь;
	
	Если Форма.ТолькоПросмотр
		Или Не ЭлементИсполнители.ИзменятьПорядокСтрок Тогда
		
		Возврат;
	КонецЕсли;
	
	КоличествоИсполнителей = РеквизитИсполнители.Количество();
	
	Если КоличествоИсполнителей = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = ЭлементИсполнители.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	НомерСтроки = РеквизитИсполнители.Индекс(ТекущиеДанные) + 1;
	
	Если НомерСтроки >= 2 Тогда
		Форма.Элементы.ПереместитьВверх.Доступность = Истина;
	КонецЕсли;
	
	Если НомерСтроки = 1 И КоличествоИсполнителей > 1
		Или НомерСтроки > 1 И НомерСтроки < КоличествоИсполнителей Тогда
		
		Форма.Элементы.ПереместитьВниз.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_ТаблицаИсполнители_ПроцессИсполенние

// Вызывает диалог подбора исполнителей для процесса Исполнения
// с последующим помещением выбранных исполнителей в процесс.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  ЭлементТаблица - ТаблицаФормы - элемент управления таблица.
//  РеквизитТаблица - ДанныеФормыКоллекция - реквизит формы.
//  ВариантИсполнения - ПеречислениеСсылка.ВариантыМаршрутизацииЗадач - вариант исполнения задач процесса.
//
Процедура ПодобратьИсполнителейИсполнения(Форма, ЭлементИсполнители, РеквизитИсполнители, ВариантИсполнения) Экспорт
	
	ЭтоШаблонПроцесса = Ложь;
	СрокПункта = Неопределено;
	Если Не СтрЗаканчиваетсяНа(Форма.ИмяФормы, ".НастройкаДочернегоИсполнения") Тогда
		
		ЭтоШаблонПроцесса = ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(Форма.Объект.Ссылка);
		
	Иначе
		СрокПункта = Форма.Срок;
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Форма", Форма);
	ДопПараметры.Вставить("ЭлементИсполнители", ЭлементИсполнители);
	ДопПараметры.Вставить("РеквизитИсполнители", РеквизитИсполнители);
	ДопПараметры.Вставить("ЭтоШаблонПроцесса", ЭтоШаблонПроцесса);
	ДопПараметры.Вставить("ВариантИсполнения", ВариантИсполнения);
	ДопПараметры.Вставить("СрокПункта", СрокПункта);
	
	ОписаниеОповещенияОбработкиВыбора = Новый ОписаниеОповещения(
		"ЗавершитьПодборИсполнителейИсполнения", ЭтотОбъект, ДопПараметры);
		
	Если ЭтоШаблонПроцесса Тогда
		РаботаСАдреснойКнигойКлиент.ПодобратьИсполнителейДляШаблоновПроцессов(
			Форма, ОписаниеОповещенияОбработкиВыбора, Ложь);
	Иначе
		РаботаСАдреснойКнигойКлиент.ПодобратьИсполнителейДляПроцессов(
			Форма, РеквизитИсполнители, ОписаниеОповещенияОбработкиВыбора, Ложь);
	КонецЕсли;
	
КонецПроцедуры

// см. ПодобратьИсполнителейИсполнения
Процедура ЗавершитьПодборИсполнителейИсполнения(
	ВыбранныеИсполнители, ДопПараметры) Экспорт
	
	Если ВыбранныеИсполнители = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДопПараметры.Форма;
	ЭлементИсполнители = ДопПараметры.ЭлементИсполнители;
	РеквизитИсполнители = ДопПараметры.РеквизитИсполнители;
	ЭтоШаблонПроцесса = ДопПараметры.ЭтоШаблонПроцесса;
	ВариантИсполнения = ДопПараметры.ВариантИсполнения;
	
	КоличествоСтрок = РеквизитИсполнители.Количество();
	
	ВариантыМаршуртизацииЗадач = РаботаСБизнесПроцессамиКлиентСервер.ВариантыМаршуртизацииЗадач();
	
	ВариантыПорядкаВыполненияЗадач = РаботаСБизнесПроцессамиКлиентСервер.ВариантыПорядкаВыполненияЗадач();
	
	// Удаление пропавших строк из таблицы Исполнители.
	РеквизитыСрока =
		"ВариантУстановкиСрокаИсполнения, СрокИсполнения,
		|СрокИсполненияДни, СрокИсполненияЧасы, СрокИсполненияМинуты";
	ДанныеСрокаПоследнейСтроки = Новый Структура(РеквизитыСрока);
	Для Инд = 1 По КоличествоСтрок Цикл
		
		Строка = РеквизитИсполнители[КоличествоСтрок - Инд];
		
		УдалитьИсполнителя = Истина;
		Для Каждого ВыбранныйИсполнитель Из ВыбранныеИсполнители Цикл
			Если Строка.Исполнитель = ВыбранныйИсполнитель Тогда
				УдалитьИсполнителя = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если УдалитьИсполнителя Тогда
			ЗаполнитьЗначенияСвойств(ДанныеСрокаПоследнейСтроки, Строка, РеквизитыСрока);
			РеквизитИсполнители.Удалить(Строка);
		КонецЕсли;
		
	КонецЦикла;
	
	Если РеквизитИсполнители.Количество() <> 0 Тогда
		ДанныеСрокаПоследнейСтроки = Неопределено;
	КонецЕсли;
	
	ИндексИзмененнойСтроки = РеквизитИсполнители.Количество();
	
	// Обработка выбранных контактов
	Для Каждого ВыбранныйАдресат1 Из ВыбранныеИсполнители Цикл
		
		//Находим количество дублей для выбранного адресата.
		КоличествоВхождений = 0;
		Для Каждого ВыбранныйАдресат2 Из ВыбранныеИсполнители Цикл
			Если ВыбранныйАдресат1 = ВыбранныйАдресат2 Тогда
				
				КоличествоВхождений = КоличествоВхождений + 1;
			КонецЕсли;
		КонецЦикла;
		
		// Ищем выбранный контакт в таблице Исполнители
		СтруктураДляПоиска = Новый Структура;
		СтруктураДляПоиска.Вставить("Исполнитель", ВыбранныйАдресат1);
		НайденныеСтроки = РеквизитИсполнители.НайтиСтроки(СтруктураДляПоиска);
		КоличествоНайденныхСтрок = НайденныеСтроки.Количество();
		
		// Если выбранный контакт встречается в списке выбранных больше, чем
		// в таблице Исполнители, тогда добавляем недостающих конец списка.
		Если КоличествоВхождений > КоличествоНайденныхСтрок Тогда
			
			Пока КоличествоВхождений <> КоличествоНайденныхСтрок Цикл
				
				НоваяСтрокаИсполнитель = РеквизитИсполнители.Добавить();
				НоваяСтрокаИсполнитель.Исполнитель = ВыбранныйАдресат1;
				
				// Если удаляется последняя строка - то её срок переносится ко вновь добавленным строкам.
				Если ДанныеСрокаПоследнейСтроки <> Неопределено Тогда
					ЗаполнитьЗначенияСвойств(НоваяСтрокаИсполнитель, ДанныеСрокаПоследнейСтроки, РеквизитыСрока);
				КонецЕсли;
				
				Если ВариантИсполнения = ВариантыМаршуртизацииЗадач.Последовательно Тогда
					НоваяСтрокаИсполнитель.ПорядокИсполнения = ВариантыПорядкаВыполненияЗадач.ПослеПредыдущего;
				Иначе
					НоваяСтрокаИсполнитель.ПорядокИсполнения = ВариантыПорядкаВыполненияЗадач.ВместеСПредыдущим;
				КонецЕсли;
				
				Если ЭтоШаблонПроцесса Тогда
					НоваяСтрокаИсполнитель.ОписаниеУсловия = МультипредметностьКлиентСервер.ПолучитьТекстОписанияУсловия(
						НоваяСтрокаИсполнитель.ИмяПредметаУсловия, НоваяСтрокаИсполнитель.Условие);
				КонецЕсли;
				
				СрокиИсполненияПроцессовКлиент.ЗаполнитьСрокВНовойСтрокеТаблицыИсполнители(
					НоваяСтрокаИсполнитель,
					РеквизитИсполнители,
					ВариантИсполнения,
					Форма.ИспользоватьДатуИВремяВСрокахЗадач);
				
				// Заполним сроком пункта, если он задан
				Если ЗначениеЗаполнено(ДопПараметры.СрокПункта)
					И Не ЗначениеЗаполнено(НоваяСтрокаИсполнитель.СрокИсполнения)
					И Не ЗначениеЗаполнено(НоваяСтрокаИсполнитель.СрокИсполненияДни)
					И Не ЗначениеЗаполнено(НоваяСтрокаИсполнитель.СрокИсполненияЧасы)
					И Не ЗначениеЗаполнено(НоваяСтрокаИсполнитель.СрокИсполненияМинуты) Тогда
						
					НоваяСтрокаИсполнитель.СрокИсполнения = ДопПараметры.СрокПункта;
					НоваяСтрокаИсполнитель.ВариантУстановкиСрокаИсполнения = ПредопределенноеЗначение(
						"Перечисление.ВариантыУстановкиСрокаИсполнения.ТочныйСрок");
					НоваяСтрокаИсполнитель.СрокИсполненияПредставление = 
						СрокиИсполненияПроцессовКлиентСервер.ПредставлениеСрокаИсполнения(
							НоваяСтрокаИсполнитель.СрокИсполнения,
							НоваяСтрокаИсполнитель.СрокИсполненияДни,
							НоваяСтрокаИсполнитель.СрокИсполненияЧасы,
							НоваяСтрокаИсполнитель.СрокИсполненияМинуты,
							Форма.ИспользоватьДатуИВремяВСрокахЗадач,
							НоваяСтрокаИсполнитель.ВариантУстановкиСрокаИсполнения);
				КонецЕсли;
				
				КоличествоНайденныхСтрок = КоличествоНайденныхСтрок + 1;
			КонецЦикла;
			
		// Если выбранный контакт встречается в списке выбранных меньше, чем
		// в таблице Исполнители, тогда удаляем лишние строки с конца списка.
		ИначеЕсли КоличествоВхождений < КоличествоНайденныхСтрок Тогда
			Пока КоличествоВхождений <> КоличествоНайденныхСтрок Цикл
				РеквизитИсполнители.Удалить(НайденныеСтроки[КоличествоНайденныхСтрок - 1]);
				КоличествоНайденныхСтрок = КоличествоНайденныхСтрок - 1;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьШаг(РеквизитИсполнители);
	
	ОбновитьДоступностьКомандПеремещенияИНазначитьОтветственным(
		Форма, ЭлементИсполнители, РеквизитИсполнители);
	
	Форма.Модифицированность = Истина;
	
	Форма.ОбновитьСрокиИсполненияОтложенно("Исполнители", ИндексИзмененнойСтроки);
	
КонецПроцедуры

// Выполняет перемещение исполнителя вверх/вниз в таблице
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  ЭлементТаблица - ТаблицаФормы - элемент управления таблица.
//  РеквизитТаблица - ДанныеФормыКоллекция - реквизит формы.
//  ВариантИсполнения - ПеречислениеСсылка.ВариантыМаршрутизацииЗадач - вариант исполнения задач процесса.
//  Смещение - Число - значение на которое необходимо исполнителя (1 или -1).
//
Процедура ПереместитьИсполнителяПроцессаИсполнения(
	Форма, ЭлементИсполнители, РеквизитИсполнители, ВариантИсполнения, Смещение) Экспорт
	
	ТекущиеДанные = ЭлементИсполнители.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Текст = НСтр("ru = 'Перемещение невозможно. Ответственным может быть назначен только первый исполнитель в списке.'");
	
	НомерСтроки = РеквизитИсполнители.Индекс(ТекущиеДанные) + 1;
	
	Если Смещение > 0 Тогда
		Если НомерСтроки = РеквизитИсполнители.Количество() Тогда
			Возврат;
		КонецЕсли;
		Если НомерСтроки = 1
			И ТекущиеДанные.Ответственный Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
			Возврат;
		КонецЕсли;
	Иначе
		Если НомерСтроки = 1 Тогда
			Возврат;
		КонецЕсли;
		Если НомерСтроки = 2
			И РеквизитИсполнители[0].Ответственный Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	РеквизитИсполнители.Сдвинуть(НомерСтроки - 1, Смещение);
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьШаг(РеквизитИсполнители);
	
	Форма.Модифицированность = Истина;
	
	СрокиИсполненияПроцессовКлиент.ЗаполнитьСрокиИсполненияВТаблицеИсполнителейПоТекущейСтроке(
		РеквизитИсполнители, ТекущиеДанные, ВариантИсполнения);
		
	Форма.ЗаполнитьПредставлениеСроковИсполнения();
	
	ОбновитьДоступностьКомандПеремещенияИНазначитьОтветственным(
		Форма, ЭлементИсполнители, РеквизитИсполнители);
		
	ИндексИзмененнойСтроки = РеквизитИсполнители.Индекс(ТекущиеДанные);
	Если Смещение > 0 Тогда
		ИндексИзмененнойСтроки = ИндексИзмененнойСтроки - 1;
	КонецЕсли;
		
	Форма.ОбновитьСрокиИсполненияОтложенно("Исполнители", ИндексИзмененнойСтроки);
	
КонецПроцедуры

// Устанавливает или снимает отметку с ответственного исполнителя
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  ЭлементИсполнители - ТаблицаФормы - элемент управления таблица.
//  РеквизитИсполнители - ДанныеФормыКоллекция - реквизит формы.
//  ВариантИсполнения - ПеречислениеСсылка.ВариантыМаршрутизацииЗадач - вариант исполнения задач процесса.
//
Процедура НазначитьОтветственным(
	Форма,
	ЭлементИсполнители,
	РеквизитИсполнители,
	ВариантИсполнения) Экспорт
	
	ТекущиеДанные = ЭлементИсполнители.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Ответственный = Не ТекущиеДанные.Ответственный;
	
	ИдентификаторТекущейСтроки = ТекущиеДанные.ПолучитьИдентификатор();
	Для Каждого УчастникЭтапа Из РеквизитИсполнители Цикл
		Если УчастникЭтапа.ПолучитьИдентификатор() = ИдентификаторТекущейСтроки Тогда
			Продолжить;
		КонецЕсли;
		УчастникЭтапа.Ответственный = Ложь;
	КонецЦикла;
	
	Если ТекущиеДанные.Ответственный Тогда
		ИндексСтроки = РеквизитИсполнители.Индекс(ТекущиеДанные);
		Если ИндексСтроки Тогда
			РеквизитИсполнители.Сдвинуть(ИндексСтроки, - ИндексСтроки);
		КонецЕсли;
	КонецЕсли;

	Форма.Элементы.НазначитьОтветственным.Пометка = ТекущиеДанные.Ответственный;
	Форма.Модифицированность = Истина;
	
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьШаг(РеквизитИсполнители);
	
	ВариантыМаршуртизацииЗадач = РаботаСБизнесПроцессамиКлиентСервер.ВариантыМаршуртизацииЗадач();
	
	Если Не ТекущиеДанные.Ответственный
		И РеквизитИсполнители.Количество() > 1
		И ВариантИсполнения = ВариантыМаршуртизацииЗадач.Параллельно Тогда
		
		СрокиИсполненияПроцессовКлиент.ЗаполнитьСрокиИсполненияВТаблицеИсполнителейПоТекущейСтроке(
			РеквизитИсполнители, ТекущиеДанные, ВариантИсполнения);
		
	КонецЕсли;
	
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьШаг(РеквизитИсполнители);
	
	ОбновитьДоступностьКомандПеремещенияИНазначитьОтветственным(
		Форма, ЭлементИсполнители, РеквизитИсполнители);
	
	Форма.ОбновитьСрокиИсполненияОтложенно("Исполнители");
	
	ОчиститьСообщения();
	
КонецПроцедуры

// Обработчик события ПриИзменении поля ВариантИсполнения
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  ЭлементТаблица - ТаблицаФормы - элемент управления таблица.
//  ВариантИсполнения - ПеречислениеСсылка.ВариантыМаршрутизацииЗадач - вариант исполнения задач процесса.
//  ТаблицаИсполнители - ДанныеФормыКоллекция - реквизит формы.
//
Процедура ВариантИсполненияПриИзмененииПроцессаИсполнения(
	Форма, ЭлементИсполнители,
	ВариантИсполнения, ПредыдущийВариантИсполнения, ТаблицаИсполнители) Экспорт
	
	ПриИзмененииВариантаИсполнения(
		Форма,
		ВариантИсполнения,
		ПредыдущийВариантИсполнения,
		ТаблицаИсполнители);
	
	Форма.УстановитьДоступность();
	
	ОбновитьДоступностьКомандПеремещенияИНазначитьОтветственным(
		Форма, ЭлементИсполнители, ТаблицаИсполнители);
	
КонецПроцедуры

// Обработчик события ПриАктивизацииСтроки таблицы исполнителей процесса Исполнения
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  ЭлементИсполнители - ТаблицаФормы - элемент управления таблица.
//  ЭлементУправленияСроком - ПолеФормы - элемент управления сроком исполнения участника.
//  РеквизитТаблица - ДанныеФормыКоллекция - реквизит формы с исполнителями.
//  ДоступностьПоШаблону - Булево - принимает Истину, если процесс не ограничен соответствующей настройкой
//                                  доступности в шаблоне.
//  ВестиУчетПереносаСроков - Булево - значение настройки ведения учета переноса срока.
//  ЗаявкаНаПереносСрока - БизнесПроцессСсылка.РешениеВопросовВыполненияЗадач - заявка на перенос срока.
//
Процедура ИсполнителиИсполненияПриАктивизацииСтроки(
	Форма, ЭлементИсполнители, ЭлементУправленияСроком, РеквизитТаблица,
	ДоступностьПоШаблону = Неопределено,
	ВестиУчетПереносаСроков = Неопределено,
	ЗаявкаНаПереносСрока = Неопределено) Экспорт
	
	ТекущиеДанные = ЭлементИсполнители.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ИсполнителиПриАктивизацииСтроки(
		Форма,
		ЭлементИсполнители,
		ЭлементУправленияСроком,
		ДоступностьПоШаблону,
		ВестиУчетПереносаСроков,
		ЗаявкаНаПереносСрока);
	
	ОбновитьДоступностьКомандПеремещенияИНазначитьОтветственным(
		Форма, ЭлементИсполнители, РеквизитТаблица);
	
КонецПроцедуры

// Обработчик события после удаления в таблице исполнителей процесса Исполнения
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  ЭлементИсполнители - ТаблицаФормы - элемент управления таблица.
//  РеквизитИсполнители - ДанныеФормыКоллекция - реквизит формы с исполнителями.
//  ВариантИсполнения - ПеречислениеСсылка.ВариантыМаршрутизацииЗадач - вариант исполнения задач процесса.
//
Процедура ИсполнителиИсполненияПослеУдаления(
	Форма, ЭлементИсполнители, РеквизитИсполнители, ВариантИсполнения) Экспорт
	
	Если РеквизитИсполнители.Количество() = 1
		И РеквизитИсполнители[0].Ответственный Тогда
		
		НазначитьОтветственным(
			Форма,
			ЭлементИсполнители,
			РеквизитИсполнители,
			ВариантИсполнения);
	Иначе
		РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьШаг(РеквизитИсполнители);
		
		ОбновитьДоступностьКомандПеремещенияИНазначитьОтветственным(
			Форма, ЭлементИсполнители, РеквизитИсполнители);
			
		ИсполнителиПослеУдаления(Форма, РеквизитИсполнители);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик НачалоВыбора у поля Исполнитель процесса Исполнения
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  СтандартнаяОбработка - Булево - признак стандартной обработки начала выбора.
//  ЭлементИсполнители - ТаблицаФормы - элемент управления таблица.
//  РеквизитИсполнители - ДанныеФормыКоллекция - реквизит формы с исполнителями.
//  ВариантИсполнения - ПеречислениеСсылка.ВариантыМаршрутизацииЗадач - вариант исполнения задач процесса.
//
Процедура ИсполнительИсполненияНачалоВыбора(Форма, СтандартнаяОбработка,
	ЭлементИсполнители, РеквизитИсполнители, ВариантИсполнения) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Стартован = Ложь;
	Если СтрЗаканчиваетсяНа(Форма.ИмяФормы, ".ФормаБизнесПроцесса")
		И Форма.Объект.Свойство("Стартован") Тогда
		
		Стартован = Форма.Объект.Стартован;
	КонецЕсли;
	
	ЕстьШаблон = Ложь;
	Если Не СтрЗаканчиваетсяНа(Форма.ИмяФормы, ".НастройкаДочернегоИсполнения") Тогда
		
		ЕстьШаблон = ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(Форма.Объект.Ссылка);
		
	КонецЕсли;
	
	ТекущиеДанные = ЭлементИсполнители.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Исполнитель) Или Стартован Тогда
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Форма", Форма);
		ПараметрыОповещения.Вставить("ЭлементИсполнители", ЭлементИсполнители);
		ПараметрыОповещения.Вставить("РеквизитИсполнители", РеквизитИсполнители);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ЗавершениеВыбораИсполнителя", ЭтотОбъект, ПараметрыОповещения);
		
		Если ЕстьШаблон Тогда
			РаботаСАдреснойКнигойКлиент.ВыбратьИсполнителяДляШаблонаПроцесса(
				Форма, ЭлементИсполнители, ОписаниеОповещения);
		Иначе
			РаботаСАдреснойКнигойКлиент.ВыбратьИсполнителяДляПроцесса(
				Форма, ЭлементИсполнители, ОписаниеОповещения);
		КонецЕсли;
	Иначе
		ПодобратьИсполнителейИсполнения(
			Форма, ЭлементИсполнители, РеквизитИсполнители, ВариантИсполнения);
	КонецЕсли;
	
КонецПроцедуры

// Обновляет доступность команд перемещения и назначения ответственного в карточке
// процесса/шаблона
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  ЭлементТаблица - ТаблицаФормы - элемент управления таблица.
//  РеквизитТаблица - ДанныеФормыКоллекция - реквизит формы с исполнителями.
//
Процедура ОбновитьДоступностьКомандПеремещенияИНазначитьОтветственным(
	Форма, ЭлементТаблица, РеквизитТаблица) Экспорт
	
	Форма.Элементы.ПереместитьВверх.Доступность = Ложь;
	Форма.Элементы.ПереместитьВниз.Доступность = Ложь;
	Форма.Элементы.НазначитьОтветственным.Доступность = Ложь;
	
	Если Не ЭлементТаблица.ИзменятьПорядокСтрок Тогда
		Возврат;
	КонецЕсли;
	
	Форма.Элементы.НазначитьОтветственным.Пометка = Ложь;
	
	ТекущиеДанные = ЭлементТаблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	КоличествоИсполнителей = РеквизитТаблица.Количество();
	
	Если КоличествоИсполнителей = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если РеквизитТаблица[0].Ответственный Тогда
		ВерхняяГраница = 2;
		МинимальноеЧислоИсполнителейДляИзмененияПорядка = 3;
	Иначе
		ВерхняяГраница = 1;
		МинимальноеЧислоИсполнителейДляИзмененияПорядка = 2;
	КонецЕсли;
	
	НижняяГраница = КоличествоИсполнителей;
	
	// Номер строки получаем по индекс, т.к. РеквизитТаблица может
	// не иметь поля НомерСтроки.
	НомерСтроки = РеквизитТаблица.Индекс(ТекущиеДанные) + 1;
	
	Если КоличествоИсполнителей >= МинимальноеЧислоИсполнителейДляИзмененияПорядка Тогда
		
		Если НомерСтроки < ВерхняяГраница Тогда
			// ничего не делаем
		ИначеЕсли НомерСтроки = ВерхняяГраница Тогда
			Форма.Элементы.ПереместитьВниз.Доступность = Истина;
		ИначеЕсли НомерСтроки = НижняяГраница Тогда
			Форма.Элементы.ПереместитьВверх.Доступность = Истина;
		Иначе
			Форма.Элементы.ПереместитьВверх.Доступность = Истина;
			Форма.Элементы.ПереместитьВниз.Доступность = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Стартован = Ложь;
	ВариантИсполнения = Неопределено;	
	Если СтрЗаканчиваетсяНа(Форма.ИмяФормы, ".ФормаБизнесПроцесса")
		И Форма.Объект.Свойство("Стартован") Тогда
		
		Стартован = Форма.Объект.Стартован;
		ВариантИсполнения = Форма.Объект.ВариантИсполнения;
	КонецЕсли;
	
	Форма.Элементы.НазначитьОтветственным.Пометка = ТекущиеДанные.Ответственный;
	
	МожноНазначитьОтветственного = Истина;
	Если Стартован
		И (ВариантИсполнения <> ПредопределенноеЗначение("Перечисление.ВариантыМаршрутизацииЗадач.Параллельно")
			Или ТекущиеДанные.Пройден) Тогда
		МожноНазначитьОтветственного = Ложь;
	КонецЕсли;
	
	Форма.Элементы.НазначитьОтветственным.Доступность = 
		КоличествоИсполнителей > 1
		И МожноНазначитьОтветственного;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_ТаблицаИсполнители_ПроцессСогласования

// Вызывает диалог подбора исполнителей для процесса Согласования
// с последующим помещением выбранных исполнителей в процесс.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  ЭлементТаблица - ТаблицаФормы - элемент управления таблицы с исполнителями.
//  РеквизитТаблица - ДанныеФормыКоллекция - реквизит формы с исполнителями.
//
Процедура ПодобратьИсполнителейСогласования(Форма, ЭлементТаблица, РеквизитТаблица) Экспорт
	
	ЭтоШаблонПроцесса = ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(Форма.Объект.Ссылка);
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Форма", Форма);
	ДопПараметры.Вставить("ЭлементТаблица", ЭлементТаблица);
	ДопПараметры.Вставить("РеквизитТаблица", РеквизитТаблица);
	ДопПараметры.Вставить("ЭтоШаблонПроцесса", ЭтоШаблонПроцесса);
	
	ОписаниеОповещенияОбработкиВыбора = Новый ОписаниеОповещения(
		"ЗавершитьПодборИсполнителейСогласования", ЭтотОбъект, ДопПараметры);
	
	Если ЭтоШаблонПроцесса Тогда
		РаботаСАдреснойКнигойКлиент.ПодобратьИсполнителейДляШаблоновПроцессов(
			Форма, ОписаниеОповещенияОбработкиВыбора);
	Иначе
		РаботаСАдреснойКнигойКлиент.ПодобратьИсполнителейДляПроцессов(
			Форма, РеквизитТаблица, ОписаниеОповещенияОбработкиВыбора);
	КонецЕсли;
	
КонецПроцедуры

// см. ПодобратьИсполнителейСогласования
Процедура ЗавершитьПодборИсполнителейСогласования(
	ВыбранныеИсполнители, ДопПараметры) Экспорт
	
	Если ВыбранныеИсполнители = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДопПараметры.Форма;
	ЭлементТаблица = ДопПараметры.ЭлементТаблица;
	РеквизитТаблица = ДопПараметры.РеквизитТаблица;
	ЭтоШаблонПроцесса = ДопПараметры.ЭтоШаблонПроцесса;
	
	КоличествоСтрок = РеквизитТаблица.Количество();
	
	ВариантыМаршуртизацииЗадач = РаботаСБизнесПроцессамиКлиентСервер.ВариантыМаршуртизацииЗадач();
	
	ВариантыПорядкаВыполненияЗадач = РаботаСБизнесПроцессамиКлиентСервер.ВариантыПорядкаВыполненияЗадач();
	
	ВариантМаршрутизацииЗадач = 
		РаботаСБизнесПроцессамиКлиентСервер.ВариантМаршрутизацииЗадач(Форма.Объект);
		
	ИспользоватьДатуИВремяВСрокахЗадач = Форма.ИспользоватьДатуИВремяВСрокахЗадач;
	
	// Удаление пропавших строк.
	РеквизитыСрока =
		"ВариантУстановкиСрокаИсполнения, СрокИсполнения,
		|СрокИсполненияДни, СрокИсполненияЧасы, СрокИсполненияМинуты";
	ДанныеСрокаПоследнейСтроки = Новый Структура(РеквизитыСрока);
	Для Инд = 1 По КоличествоСтрок Цикл
		
		Строка = РеквизитТаблица[КоличествоСтрок - Инд];
		
		УдалитьИсполнителя = Истина;
		Для Каждого ВыбранныйИсполнитель Из ВыбранныеИсполнители Цикл
			Если Строка.Исполнитель = ВыбранныйИсполнитель Тогда
				УдалитьИсполнителя = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если УдалитьИсполнителя Тогда
			ЗаполнитьЗначенияСвойств(ДанныеСрокаПоследнейСтроки, Строка, РеквизитыСрока);
			РеквизитТаблица.Удалить(Строка);
		КонецЕсли;
		
	КонецЦикла;
	
	Если РеквизитТаблица.Количество() <> 0 Тогда
		ДанныеСрокаПоследнейСтроки = Неопределено;
	КонецЕсли;
	
	ИндексИзмененнойСтроки = РеквизитТаблица.Количество();
	
	// Добавление новых строк.
	Для Каждого ВыбранныйАдресат Из ВыбранныеИсполнители Цикл
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Исполнитель", ВыбранныйАдресат);
		
		НайденныеСтроки = РеквизитТаблица.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() = 0 Тогда
			
			НоваяСтрокаИсполнитель = РеквизитТаблица.Добавить();
			НоваяСтрокаИсполнитель.Исполнитель = ВыбранныйАдресат;
			
			// Если удаляется последняя строка - то её срок переносится ко вновь добавленным строкам.
			Если ДанныеСрокаПоследнейСтроки <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(НоваяСтрокаИсполнитель, ДанныеСрокаПоследнейСтроки, РеквизитыСрока);
			КонецЕсли;
			
			Если ВариантМаршрутизацииЗадач = ВариантыМаршуртизацииЗадач.Смешанно Тогда
				ИндексСтроки = РеквизитТаблица.Индекс(НоваяСтрокаИсполнитель);
				Если ИндексСтроки > 0 Тогда
					ПредыдущаяСтрока = РеквизитТаблица[ИндексСтроки - 1];
					НоваяСтрокаИсполнитель.ПорядокСогласования = ПредыдущаяСтрока.ПорядокСогласования;
				Иначе
					НоваяСтрокаИсполнитель.ПорядокСогласования = ВариантыПорядкаВыполненияЗадач.ПослеПредыдущего;
				КонецЕсли;
			ИначеЕсли ВариантМаршрутизацииЗадач = ВариантыМаршуртизацииЗадач.Последовательно Тогда
				НоваяСтрокаИсполнитель.ПорядокСогласования = ВариантыПорядкаВыполненияЗадач.ПослеПредыдущего;
			Иначе
				НоваяСтрокаИсполнитель.ПорядокСогласования = ВариантыПорядкаВыполненияЗадач.ВместеСПредыдущим;
			КонецЕсли;
			
			СрокиИсполненияПроцессовКлиент.ЗаполнитьСрокВНовойСтрокеТаблицыИсполнители(
					НоваяСтрокаИсполнитель, РеквизитТаблица, ВариантМаршрутизацииЗадач, ИспользоватьДатуИВремяВСрокахЗадач);
			
			Если ЭтоШаблонПроцесса Тогда
				НоваяСтрокаИсполнитель.ОписаниеУсловия = МультипредметностьКлиентСервер.ПолучитьТекстОписанияУсловия(
					НоваяСтрокаИсполнитель.ИмяПредметаУсловия, НоваяСтрокаИсполнитель.Условие);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Форма.Модифицированность = Истина;
	
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьШаг(РеквизитТаблица);
	
	ОбновитьДоступностьКомандПеремещения(Форма, ЭлементТаблица, РеквизитТаблица);
	
	Форма.ОбновитьСрокиИсполненияОтложенно("Исполнители", ИндексИзмененнойСтроки);
	
КонецПроцедуры

// Выполняет перемещение исполнителя в таблице
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  ЭлементТаблица - ТаблицаФормы - элемент управления таблица.
//  РеквизитТаблица - ДанныеФормыКоллекция - реквизит формы.
//  Смещение - Число - значение, на которое необходимо исполнителя (1 или -1).
//
Процедура ПереместитьИсполнителяПроцессаСогласования(
	Форма, ЭлементТаблица, РеквизитТаблица, Смещение) Экспорт
	
	ТекущиеДанные = ЭлементТаблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Смещение > 0 Тогда
		Если ТекущиеДанные.НомерСтроки = РеквизитТаблица.Количество() Тогда
			Возврат;
		КонецЕсли;
	Иначе
		Если ТекущиеДанные.НомерСтроки = 1 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	РеквизитТаблица.Сдвинуть(ТекущиеДанные.НомерСтроки - 1, Смещение); 
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьШаг(РеквизитТаблица);
	
	Форма.Модифицированность = Истина;
	
	ВариантМаршрутизацииЗадач = 
		РаботаСБизнесПроцессамиКлиентСервер.ВариантМаршрутизацииЗадач(Форма.Объект);
	
	СрокиИсполненияПроцессовКлиент.ЗаполнитьСрокиИсполненияВТаблицеИсполнителейПоТекущейСтроке(
		РеквизитТаблица, ТекущиеДанные, ВариантМаршрутизацииЗадач);
		
	Форма.ЗаполнитьПредставлениеСроковИсполнения();
	
	ОбновитьДоступностьКомандПеремещения(Форма, ЭлементТаблица, РеквизитТаблица);
	
	ИндексИзмененнойСтроки = РеквизитТаблица.Индекс(ТекущиеДанные);
	
	Форма.ОбновитьСрокиИсполненияОтложенно("Исполнители", ИндексИзмененнойСтроки);
	
КонецПроцедуры

// Обработчик события ПриИзменении поля ВариантСогласования
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//
Процедура ВариантСогласованияПриИзменении(Форма) Экспорт
	
	ПриИзмененииВариантаИсполнения(
		Форма,
		Форма.Объект.ВариантСогласования,
		Форма.ПредыдущийВариантСогласования,
		Форма.Объект.Исполнители,
		"ПорядокСогласования");
	
	Форма.УстановитьДоступность();
	
	ОбновитьДоступностьКомандПеремещения(Форма, Форма.Элементы.Исполнители, Форма.Объект.Исполнители);
	
КонецПроцедуры

// Обработчик события ПриАктивизацииСтроки таблицы исполнителей процесса Согласования
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  ДоступностьПоШаблону - Булево - принимает Истину, если процесс не ограничен соответствующей настройкой
//                                  доступности в шаблоне.
//  ВестиУчетПереносаСроков - Булево - значение настройки ведения учета переноса срока.
//  ЗаявкаНаПереносСрока - БизнесПроцессСсылка.РешениеВопросовВыполненияЗадач - заявка на перенос срока.
//
Процедура ИсполнителиСогласованияПриАктивизацииСтроки(
	Форма,
	ДоступностьПоШаблону = Неопределено,
	ВестиУчетПереносаСроков = Неопределено,
	ЗаявкаНаПереносСрока = Неопределено) Экспорт
	
	ИсполнителиПриАктивизацииСтроки(
		Форма,
		Форма.Элементы.Исполнители,
		Форма.Элементы.ИсполнителиСрокИсполненияПредставление,
		ДоступностьПоШаблону,
		ВестиУчетПереносаСроков,
		ЗаявкаНаПереносСрока);
	
	ОбновитьДоступностьКомандПеремещения(Форма, Форма.Элементы.Исполнители, Форма.Объект.Исполнители);
	
КонецПроцедуры

// Обработчик события после удаления в таблице исполнителей процесса Исполнения
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//
Процедура ИсполнителиСогласованияПослеУдаления(Форма) Экспорт
	
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьШаг(Форма.Объект.Исполнители);
	ОбновитьДоступностьКомандПеремещения(Форма, Форма.Элементы.Исполнители, Форма.Объект.Исполнители);
	ИсполнителиПослеУдаления(Форма, Форма.Объект.Исполнители);
	
КонецПроцедуры

// Обработчик НачалоВыбора у поля Исполнитель процесса Исполнения
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - карточка процесса/шаблона
//  СтандартнаяОбработка - Булево - признак стандартной обработки начала выбора.
//  ЭлементИсполнители - ТаблицаФормы - элемент управления таблица.
//  РеквизитИсполнители - ДанныеФормыКоллекция - реквизит формы с исполнителями.
//
Процедура ИсполнительСогласованияНачалоВыбора(
	Форма, СтандартнаяОбработка, ЭлементИсполнители, РеквизитИсполнители) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Стартован = Ложь;
	Если СтрЗаканчиваетсяНа(Форма.ИмяФормы, ".ФормаБизнесПроцесса")
		И Форма.Объект.Свойство("Стартован") Тогда
		
		Стартован = Форма.Объект.Стартован;
	КонецЕсли;
	
	ТекущиеДанные = Форма.Элементы.Исполнители.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Исполнитель) Или Стартован Тогда 
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Форма", Форма);
		ПараметрыОповещения.Вставить("ЭлементИсполнители", ЭлементИсполнители);
		ПараметрыОповещения.Вставить("РеквизитИсполнители", РеквизитИсполнители);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ЗавершениеВыбораИсполнителя", ЭтотОбъект, ПараметрыОповещения);
		
		Если ШаблоныБизнесПроцессовКлиентСервер.ЭтоШаблонПроцесса(Форма.Объект.Ссылка) Тогда
			РаботаСАдреснойКнигойКлиент.ВыбратьИсполнителяДляШаблонаПроцесса(
				Форма, ЭлементИсполнители, ОписаниеОповещения);
		Иначе
			РаботаСАдреснойКнигойКлиент.ВыбратьИсполнителяДляПроцесса(
				Форма, ЭлементИсполнители, ОписаниеОповещения);
		КонецЕсли;
	Иначе
		ПодобратьИсполнителейСогласования(Форма, ЭлементИсполнители, РеквизитИсполнители);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_Автор

Процедура АвторСоСрокомИсполненияПриИзменении(Форма, Элемент) Экспорт
	
	Форма.ОбновитьСрокиИсполненияОтложенно("Автор");
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_ТрудозатратыУчастниковПроцесса

Функция СтруктураСтрокиТрудозатратУчастникаПроцесса(РольВПроцессе, ПолеТрудозатрат, Трудозатраты,
	Участник = Неопределено, Шаг = 0, НомерСтроки = 0) Экспорт
	
	СтруктураСтроки = Новый Структура;
	СтруктураСтроки.Вставить("РольВПроцессе", РольВПроцессе);
	СтруктураСтроки.Вставить("ПолеТрудозатрат", ПолеТрудозатрат);
	СтруктураСтроки.Вставить("Шаг", Шаг);
	СтруктураСтроки.Вставить("Участник", Участник);
	СтруктураСтроки.Вставить("Трудозатраты", Трудозатраты);
	СтруктураСтроки.Вставить("НомерСтроки", НомерСтроки);
	
	Возврат СтруктураСтроки;
	
КонецФункции

// Открывает форму настройки трудозатрат для участников процесса.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма процесса
//   ТолькоПросмотр - Булево - если Истина, то форма открывается только для просмотра.
//                             Необязательный параметр.
//   ЕдиницаИзмеренияТрудозатрат - ПеречислениеСсылка.ЕдиницыТрудозатрат.
//   Настройка - Структура
//     ТрудозатратыПланИсполнителя - Число - плановые трудозатраты исполнителя. Необязательное свойство.
//     Заголовок_ТрудозатратыПланИсполнителя - Строка - заголовок для поля ТрудозатратыПланИсполнителя в форме.
//                                                      Необязательное свойство.
//     ИсполнительСтрокой - Строка - исполнитель строкой. Необязательное свойство.
//     ТрудозатратыПланПроверяющего - Число - плановые трудозатраты проверяющего. Необязательное свойство.
//     ПроверяющийСтрокой - Строка - проверяющий строкой. Необязательное свойство.
//     ТрудозатратыПланКонтролера - Число - плановые трудозатраты контролера. Необязательное свойство.
//     КонтролерСтрокой - Строка - контролер строкой. Необязательное свойство.
//     ТрудозатратыПланАвтора - Число - плановые трудозатраты автора. Необязательное свойство.
//     АвторСтрокой - Строка - автор строкой. Необязательное свойство.
//     Исполнители - Массив - плановые трудозатраты нескольких исполнителей. Необязательное свойство.
//        Структура
//           НомерСтроки
//           Исполнитель
//           ТрудозатратыПланИсполнителя
//           Шаг
//     Заголовок_Исполнители_Исполнитель - Строка - заголовок для поля Исполнитель в таблице Исполнители.
//                                                  Необязательное свойство.
//
Процедура НастроитьТрудозатратУчастниковПроцесса(Форма, Настройки) Экспорт
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Настройки", Настройки);
	ДопПараметры.Вставить("Форма", Форма);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОписаниеТрудозатратНажатие_Продолжение", РаботаСБизнесПроцессамиКлиент, ДопПараметры);
	
	ОткрытьФорму(
		"ОбщаяФорма.НастройкаТрудозатратУчастниковПроцесса",
		Настройки,,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеТрудозатратНажатие_Продолжение(Участники, Параметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Участники) Тогда
		Возврат;
	КонецЕсли;
	
	Форма = Параметры.Форма;
	
	ИмяРеквизитаОбъекта = "Объект";
	Если Параметры.Настройки.Свойство("ИмяРеквизитаОбъекта") Тогда
		ИмяРеквизитаОбъекта = Параметры.Настройки.ИмяРеквизитаОбъекта;
	КонецЕсли;
	
	ИмяТаблицыИсполнители = "Исполнители";
	Если Параметры.Настройки.Свойство("ИмяТаблицыИсполнители") Тогда
		ИмяТаблицыИсполнители = Параметры.Настройки.ИмяТаблицыИсполнители;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяРеквизитаОбъекта) Тогда
		Объект = Форма[ИмяРеквизитаОбъекта];
	Иначе
		Объект = Форма;
	КонецЕсли;
	
	Для Каждого СтрУчастник ИЗ Участники Цикл
		
		Если ЗначениеЗаполнено(СтрУчастник.НомерСтроки) Тогда
			ТаблицаИсполнителей = Объект[ИмяТаблицыИсполнители];
			СтрИсполнитель = ТаблицаИсполнителей[СтрУчастник.НомерСтроки - 1];
			СтрИсполнитель[СтрУчастник.ПолеТрудозатрат] = СтрУчастник.Трудозатраты;
			Продолжить;
		КонецЕсли;
		
		Объект[СтрУчастник.ПолеТрудозатрат] = СтрУчастник.Трудозатраты;
		
	КонецЦикла;
	
	Форма.Модифицированность = Истина;
	
	Оповестить("ОбновитьТрудозатратыУчастниковПроцесса",, Форма);
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_РаботаСДеревомПроцессовИЗадач

// Разворачивает дерево процессов и задач.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма с деревом процессов и задач.
//
Процедура РазвернутьДеревоПроцессовИЗадач(Форма) Экспорт
	
	ЭлементыДерева = Форма.ДеревоЗадач.ПолучитьЭлементы();
	
	Для каждого Строка ИЗ ЭлементыДерева Цикл
		Форма.Элементы.ДеревоЗадач.Развернуть(Строка.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
	
КонецПроцедуры

// Процедура обработки оповещений для форм содержащих
// дерево процессов и задач.
//
Процедура ОбработкаОповещенияДляДереваЗадач(
		ИмяСобытия, Параметр, Источник, Форма) Экспорт
	
	Если (ИмяСобытия = "ЗадачаИзменена" И Источник <> Форма)
		Или ИмяСобытия = "БизнесПроцессИзменен"
		Или (ИмяСобытия = "ИзменилсяФлаг" И ТипЗнч(Параметр[0]) = Тип("ЗадачаСсылка.ЗадачаИсполнителя"))
		Или (ИмяСобытия = "ЗаписьКонтроля" И ЗначениеЗаполнено(Параметр.Предмет))
		Или ИмяСобытия = "Запись_Задача"
		Или ИмяСобытия = "Запись_ДействиеЗадачи" Тогда
		
		Форма.ОбновитьДеревоПроцессовЗадач();
	КонецЕсли;
	
КонецПроцедуры

// Процедура обработки события ПриАктивизацииСтроки для дерева процессов и задач.
//
Процедура ДеревоЗадачПриАктивизацииСтроки(Элемент, Форма) Экспорт
	
	Если Форма.Элементы.ДеревоЗадач.ТекущиеДанные <> Неопределено Тогда
		Форма.ТекущаяСтрокаВДереве = Форма.Элементы.ДеревоЗадач.ТекущиеДанные.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

// Процедура обработки события Выбор для дерева процессов и задач.
//
Процедура ДеревоЗадачВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, Форма) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если Элемент.ТекущийЭлемент = Форма.Элементы.ДеревоЗадачНомерФлага Тогда
		ВыбраннаяСтрока = Форма.Элементы.ДеревоЗадач.ТекущаяСтрока;
		СтрокаТаблицы = Форма.ДеревоЗадач.НайтиПоИдентификатору(ВыбраннаяСтрока);
		Если ТипЗнч(СтрокаТаблицы.Ссылка) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
			РаботаСФлагамиОбъектовКлиент.ПереключитьФлагЗадачи(СтрокаТаблицы.Ссылка);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ОткрытьТекущуюСтрокуДереваЗадач(Форма);
	
КонецПроцедуры

// Процедура обработки события ПередНачаломИзменения для дерева процессов и задач.
//
Процедура ДеревоЗадачПередНачаломИзменения(Элемент, Отказ, Форма) Экспорт
	
	Отказ = Истина;
	
	ОткрытьТекущуюСтрокуДереваЗадач(Форма);
	
КонецПроцедуры

// Процедура обработки события Выбор для списка активных задач.
//
Процедура СписокАктивныхЗадачВыбор(
	Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, Форма) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если Элемент.ТекущийЭлемент = Форма.Элементы.СписокАктивныхЗадачНомерФлага Тогда
		ВыбраннаяСтрока = Форма.Элементы.СписокАктивныхЗадач.ТекущаяСтрока;
		СтрокаТаблицы = Форма.СписокАктивныхЗадач.НайтиПоИдентификатору(ВыбраннаяСтрока);
		Если ТипЗнч(СтрокаТаблицы.Ссылка) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
			РаботаСФлагамиОбъектовКлиент.ПереключитьФлагЗадачи(СтрокаТаблицы.Ссылка);
			Возврат;
		ИначеЕсли ТипЗнч(СтрокаТаблицы.Ссылка) = Тип("ДокументСсылка.ДействиеЗадачи") Тогда
			РаботаСЗадачамиКлиент.ПереключитьФлаг(СтрокаТаблицы.Ссылка);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ОткрытьЗадачуСпискаАктивныхЗадач(Форма);
	
КонецПроцедуры

// Процедура обработки события ПриАктивизацииСтроки для списка активных задач.
//
Процедура СписокАктивныхЗадачПриАктивизацииСтроки(Элемент, Форма) Экспорт
	
	Если Форма.Элементы.СписокАктивныхЗадач.ТекущиеДанные <> Неопределено Тогда
		
		Форма.ТекущаяСтрокаВСпискеАктивныхЗадач = 
			Форма.Элементы.СписокАктивныхЗадач.ТекущиеДанные.Ссылка;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура обработки события ПередНачаломИзменения для списка активных задач.
//
Процедура СписокАктивныхЗадачПередНачаломИзменения(Элемент, Отказ, Форма) Экспорт
	
	Отказ = Истина;
	
	ОткрытьЗадачуСпискаАктивныхЗадач(Форма);
	
КонецПроцедуры

// Устанавливает флаг для текущей задаче в таблице.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения
//   Флаг - ПеречислениеСсылка.ФлагиОбъектов
//   ИмяТаблицы - Строка - имя таблицы с задачами в форме.
//
Процедура УстановитьФлаги(Форма, Флаг, ИмяТаблицы = "") Экспорт
	
	ВыделенныеСтроки = Новый Массив;
	
	ТаблицаФормы = Форма.Элементы[ИмяТаблицы];
	
	МассивВыделенныхСтрок = ТаблицаФормы.ВыделенныеСтроки;
	Для Каждого Стр Из МассивВыделенныхСтрок Цикл
		СтрокаДерева = Форма[ИмяТаблицы].НайтиПоИдентификатору(Стр);
		ВыделенныеСтроки.Добавить(СтрокаДерева.Ссылка);
	КонецЦикла;
	
	РаботаСФлагамиОбъектовКлиент.УстановитьФлагиЗадачам(ВыделенныеСтроки, Флаг);
	
КонецПроцедуры

// Находит и устанавливает текущей строкой в дереве "процессов и задач", в списке активных задач,
// процесс или задачу, по которых была открыта форма иерархии процессов и задач.
//
// Параметры:
//   Предмет - процесс или задача по которых была открыта форма иерархии процессов и задач.
//    - БизнесПроцессСсылка
//    - ЗадачаСсылка.ЗадачаИсполнителя
//   Форма - ФормаКлиентскогоПриложения - форма с списком активных задач.
//
Процедура ПерейтиКТекущемуОбъекту(Предмет, Форма) Экспорт
	
	Форма.ТекущаяСтрокаВДереве = Предмет;
	Форма.ТекущаяСтрокаВСпискеАктивныхЗадач = Предмет;
	
	РаботаСБизнесПроцессамиКлиентСервер.УстановитьТекущуюСтроку(Форма.ДеревоЗадач.ПолучитьЭлементы(), Форма);
	РаботаСБизнесПроцессамиКлиентСервер.УстановитьТекущуюСтрокуВСпискеАктивныхЗадач(Форма);
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_ФормаЗадачиПроцесса

// Открывает диалог выбор исполнителя задачи, если текущий пользователь
// отличается от исполнителя.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма с командой выполнения задачи.
//   ИсполнительЗадачи - СправочникСсылка.Пользователи, СправочникСсылка.Сотрудники - исполнитель, указанный в задаче.
//   ТекущийПользователь - СправочникСсылка.Пользователи - текущий пользователь
//   ФактическийИсполнительЗадачи - СправочникСсылка.Пользователи - фактический, если он определен.
//   ОписаниеОповещения - описание оповещения для продолжения выполнения задачи.
//
Процедура ВыбратьИсполнителяЗадачи(Форма, ИсполнительЗадачи, ТекущийПользователь,
	ФактическийИсполнительЗадачи, ОписаниеОповещения) Экспорт
	
	Если ЗначениеЗаполнено(ИсполнительЗадачи)
		И Не СотрудникиВызовСервера.ЭтоСотрудникПользователя(ИсполнительЗадачи)
		И ИсполнительЗадачи <> ТекущийПользователь
		И Не ЗначениеЗаполнено(ФактическийИсполнительЗадачи) Тогда

		ВыполнитьОбработкуОповещения(ОписаниеОповещения, ТекущийПользователь);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОписаниеОповещения, ИсполнительЗадачи);
	
КонецПроцедуры

// Открывает диалог выбора шаблона текста - из формы бизнес-процесса
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма с командой выполнения задачи.
//   ТипПоля - Строка - "Наименование"  или "Описание" 
//   Область -  Перечисление.ОбластиПримененияШаблоновТекстов
//
Процедура ВыбратьШаблонТекстаРеализация(Форма, ТипПоля, Область) Экспорт
	
	ЗаголовокФормыВыбора = НСтр("ru = 'Выбор шаблона наименования'");
	ЗаголовокФормыСоздания = НСтр("ru = 'Шаблон наименования'");
	ИмяОбработчика = "НаименованиеНачалоВыбораПродолжение";
	
	Если ТипПоля = "Описание" Тогда
		ЗаголовокФормыВыбора = НСтр("ru = 'Выбор шаблона описания'");
		ЗаголовокФормыСоздания = НСтр("ru = 'Шаблон описания'");
		ИмяОбработчика = "ОписаниеНачалоВыбораПродолжение";
	ИначеЕсли ТипПоля = "РезультатВыполнения" Тогда
		ЗаголовокФормыВыбора = НСтр("ru = 'Выбор шаблона комментария'");
		ЗаголовокФормыСоздания = НСтр("ru = 'Шаблон комментария'");
		ИмяОбработчика = "РезультатВыполненияНачалоВыбораПродолжение";
	ИначеЕсли ТипПоля = "РезультатВыполненияЗадачиМне" Тогда
		ЗаголовокФормыВыбора = НСтр("ru = 'Выбор шаблона комментария'");
		ЗаголовокФормыСоздания = НСтр("ru = 'Шаблон комментария'");
		ИмяОбработчика = "РезультатВыполненияЗадачиМнеНачалоВыбораПродолжение";
	ИначеЕсли ТипПоля = "НаименованиеОзнакомления" Тогда
		ЗаголовокФормыВыбора = НСтр("ru = 'Выбор шаблона наименования'");
		ЗаголовокФормыСоздания = НСтр("ru = 'Шаблон наименования'");
		ИмяОбработчика = "НаименованиеОзнакомленияНачалоВыбораПродолжение";
	ИначеЕсли ТипПоля = "ОписаниеОзнакомления" Тогда
		ЗаголовокФормыВыбора = НСтр("ru = 'Выбор шаблона описания'");
		ЗаголовокФормыСоздания = НСтр("ru = 'Шаблон описания'");
		ИмяОбработчика = "ОписаниеОзнакомленияНачалоВыбораПродолжение";
	ИначеЕсли ТипПоля = "НаименованиеИсполнение" Тогда
		ЗаголовокФормыВыбора = НСтр("ru = 'Выбор шаблона наименования'");
		ЗаголовокФормыСоздания = НСтр("ru = 'Шаблон наименования'");
		ИмяОбработчика = "НаименованиеИсполнениеНачалоВыбораПродолжение";
	ИначеЕсли ТипПоля = "ОписаниеИсполнение" Тогда
		ЗаголовокФормыВыбора = НСтр("ru = 'Выбор шаблона описания'");
		ЗаголовокФормыСоздания = НСтр("ru = 'Шаблон описания'");
		ИмяОбработчика = "ОписаниеИсполнениеНачалоВыбораПродолжение";
	ИначеЕсли ТипПоля = "НаименованиеБизнесПроцесса" Тогда
        ЗаголовокФормыВыбора = НСтр("ru = 'Выбор шаблона наименования'");
        ЗаголовокФормыСоздания = НСтр("ru = 'Шаблон наименования'");
		ИмяОбработчика = "НаименованиеБизнесПроцессаНачалоВыбораПродолжение";
	КонецЕсли;	
	
	ПараметрыФормы = Новый Структура("ОбластьПрименения, Заголовок, ЗаголовокФормыСоздания", 
		Область,
		ЗаголовокФормыВыбора,
		ЗаголовокФормыСоздания);
		
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Форма", Форма);
		
	ОписаниеОповещения = Новый ОписаниеОповещения(
		ИмяОбработчика,
		ЭтотОбъект,
		ДопПараметры);
		
	ОткрытьФорму("Справочник.ШаблоныТекстов.ФормаВыбора", 
		ПараметрыФормы, Форма,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеНачалоВыбораПродолжение(Результат, Параметры) Экспорт 
	
	Форма = Параметры.Форма;
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.ШаблоныТекстов")  Тогда 
		Форма.Объект.Наименование = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			Результат, "Шаблон");
        Форма.Модифицированность = Истина;    
	КонецЕсли;	
		
КонецПроцедуры	

&НаКлиенте
Процедура НаименованиеБизнесПроцессаНачалоВыбораПродолжение(Результат, Параметры) Экспорт 
	
	Форма = Параметры.Форма;
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.ШаблоныТекстов")  Тогда 
		Форма.Объект.НаименованиеБизнесПроцесса = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			Результат, "Шаблон");
        Форма.Модифицированность = Истина;    
	КонецЕсли;	
		
КонецПроцедуры	

&НаКлиенте
Процедура ОписаниеНачалоВыбораПродолжение(Результат, Параметры) Экспорт 
	
	Форма = Параметры.Форма;
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.ШаблоныТекстов")  Тогда 
		Форма.Объект.Описание = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			Результат, "Шаблон");
        Форма.Модифицированность = Истина;    
	КонецЕсли;	
		
КонецПроцедуры	

&НаКлиенте
Процедура РезультатВыполненияНачалоВыбораПродолжение(Результат, Параметры) Экспорт 
	
	Форма = Параметры.Форма;
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.ШаблоныТекстов")  Тогда 
		Форма.Объект.РезультатВыполнения = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			Результат, "Шаблон");
        Форма.Модифицированность = Истина;    
	КонецЕсли;	
		
КонецПроцедуры	

&НаКлиенте
Процедура РезультатВыполненияЗадачиМнеНачалоВыбораПродолжение(Результат, Параметры) Экспорт 
	
	Форма = Параметры.Форма;
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.ШаблоныТекстов")  Тогда 
		Форма.РезультатВыполнения = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			Результат, "Шаблон");
        Форма.Модифицированность = Истина;    
	КонецЕсли;	
		
КонецПроцедуры	

&НаКлиенте
Процедура НаименованиеОзнакомленияНачалоВыбораПродолжение(Результат, Параметры) Экспорт 
	
	Форма = Параметры.Форма;
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.ШаблоныТекстов")  Тогда 
		Форма.НаименованиеОзнакомления = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			Результат, "Шаблон");
        Форма.Модифицированность = Истина;    
	КонецЕсли;	
		
КонецПроцедуры	

&НаКлиенте
Процедура ОписаниеОзнакомленияНачалоВыбораПродолжение(Результат, Параметры) Экспорт 
	
	Форма = Параметры.Форма;
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.ШаблоныТекстов")  Тогда 
		Форма.ОписаниеОзнакомления = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			Результат, "Шаблон");
        Форма.Модифицированность = Истина;    
	КонецЕсли;	
		
КонецПроцедуры	

&НаКлиенте
Процедура НаименованиеИсполнениеНачалоВыбораПродолжение(Результат, Параметры) Экспорт 
	
	Форма = Параметры.Форма;
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.ШаблоныТекстов")  Тогда 
		Форма.НаименованиеИсполнения = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			Результат, "Шаблон");
        Форма.Модифицированность = Истина;    
	КонецЕсли;	
		
КонецПроцедуры	

&НаКлиенте
Процедура ОписаниеИсполнениеНачалоВыбораПродолжение(Результат, Параметры) Экспорт 
	
	Форма = Параметры.Форма;
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.ШаблоныТекстов")  Тогда 
		Форма.ОписаниеИсполнения = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			Результат, "Шаблон");
        Форма.Модифицированность = Истина;    
	КонецЕсли;	
		
КонецПроцедуры	

// Выполняет проверку условий запрета выполнения задач.
//
// Параметры:
//  Задача - ЗадачаСсылка - Задача.
//  ЕстьУсловияЗапретаВыполнения - Булево - Есть условия запрета выполнения.
// 
// Возвращаемое значение:
//  Булево - Запрет выполнения.
//
Функция ЗапретВыполнения(Задача, ЕстьУсловияЗапретаВыполнения = Истина) Экспорт
	
	ЗапретВыполнения = Ложь;
	
	Если ЕстьУсловияЗапретаВыполнения Тогда
		РезультатПроверки = РаботаСБизнесПроцессамиВызовСервера.ПроверитьУсловияЗапретаВыполнения(Задача);
		Если РезультатПроверки.ЗапретВыполнения Тогда
			ЗапретВыполнения = Истина;
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Задача", Задача);
			ПараметрыФормы.Вставить("ТекстПредупреждения", РезультатПроверки.ФорматированныйТекстПредупреждения);
			ОткрытьФорму("ОбщаяФорма.ЗапретВыполнения", ПараметрыФормы);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗапретВыполнения;
	
КонецФункции

// Выполняет проверку условий запрета выполнения задачи ознакомления с результатом приглашения.
//
// Параметры:
//  Задача - ЗадачаСсылка - Задача.
//  РезультатПриглашения - ПеречислениеСсылка.ОбщиеРезультатыПриглашения - Результат приглашения.
//  ЕстьУсловияЗапретаВыполнения - Булево - Есть условия запрета выполнения.
// 
// Возвращаемое значение:
//  Булево - Запрет выполнения.
//
Функция ЗапретВыполненияПриглашениеПовторить(Задача, РезультатПриглашения, ЕстьУсловияЗапретаВыполнения = Истина) Экспорт
	
	ЗапретВыполнения = Ложь;
	
	Если РезультатПриглашения = ПредопределенноеЗначение("Перечисление.ОбщиеРезультатыПриглашения.ПринятоОбязательнымиУчастниками") Тогда
		Возврат ЗапретВыполнения;
	КонецЕсли;
	
	ЗапретВыполнения = ЗапретВыполнения(Задача, ЕстьУсловияЗапретаВыполнения);
	
	Возврат ЗапретВыполнения;
	
КонецФункции

// Выполняет проверку условий запрета выполнения задач.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма.
// 
// Возвращаемое значение:
//  Булево - Запрет выполнения.
//
Функция ЗапретВыполненияИзФормы(Форма) Экспорт
	
	Возврат ЗапретВыполнения(Форма.Объект.Ссылка, Форма.ЕстьУсловияЗапретаВыполнения);
	
КонецФункции

Процедура ЗакончитьРедактированиеФайловПоЗадаче(Форма, Отказ) Экспорт
	
	#Если ВебКлиент Тогда
		Возврат;
	#КонецЕсли	
	
	МассивЗахваченныхФайлов = ПолучитьЗахваченныеФайлы(Форма);
	
	Если МассивЗахваченныхФайлов.Количество() > 0 Тогда
				
		ОбъектСсылка = Истина; // только для проверки на заполненность
		
		Расширение = "";
		МассивЗахваченныхТолькоWordФайлов = Новый Массив;
		СписокЗанятыеWordФайлы = ДелопроизводствоКлиент.ПолучитьЗанятыеВнешнимиПрограммамиФайлы(
			ОбъектСсылка, Форма.УникальныйИдентификатор, МассивЗахваченныхФайлов, Расширение,
			МассивЗахваченныхТолькоWordФайлов);

		Если СписокЗанятыеWordФайлы.Количество() <> 0 Тогда
			
			НазваниеПрограммы = ДелопроизводствоКлиент.ПолучитьНазваниеПрограммыЗахвата(
				Расширение, СписокЗанятыеWordФайлы.Количество());
			
			СтрокаФайлов = "";
			Для Каждого ИмяРасширение Из СписокЗанятыеWordФайлы Цикл
				Если Не ПустаяСтрока(СтрокаФайлов) Тогда
					СтрокаФайлов = СтрокаФайлов + Символы.ВК;
				КонецЕсли;	
				СтрокаФайлов = СтрокаФайлов + ИмяРасширение;
			КонецЦикла;
			
			Если СписокЗанятыеWordФайлы.Количество() > 1 Тогда
				ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Для закрытия карточки задачи закройте файлы, занятые %1:
					| 
					|%2.'"),
					НазваниеПрограммы, СтрокаФайлов);
			Иначе	
				
				ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Для закрытия карточки задачи закройте файл, занятый %1:
					| 
					|%2.'"),
					НазваниеПрограммы, СтрокаФайлов);
				
			КонецЕсли;	
			
			Кнопки = Новый СписокЗначений;
			Кнопки.Добавить(КодВозвратаДиалога.ОК);
			Кнопки.Добавить(КодВозвратаДиалога.Отмена);
			
			ДопПараметры = Новый Структура("Форма", 
				Форма);
			
			Обработчик = Новый ОписаниеОповещения("ЗакрытиеКарточкиПослеВопросаФайлыЗанятыWordПередЗакрытием", 
				ЭтотОбъект, ДопПараметры);
			ПоказатьВопрос(Обработчик, ТекстСообщения, Кнопки);
			Отказ = Истина;
			
			Возврат;
			
		КонецЕсли;
		
		НеОсвобождатьЗанятыеWordФайлы = Истина;
		ДелопроизводствоКлиент.ПоместитьИзмененияФайлов(
			ОбъектСсылка, Форма.УникальныйИдентификатор, 
			МассивЗахваченныхТолькоWordФайлов, НеОсвобождатьЗанятыеWordФайлы);
		
	КонецЕсли;
	
КонецПроцедуры
	
&НаКлиенте
Процедура ЗакрытиеКарточкиПослеВопросаФайлыЗанятыWordПередЗакрытием(РезультатВопроса, ДопПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		
		// повторить операцию
		Форма = ДопПараметры.Форма;	
		Форма.Закрыть();

	КонецЕсли;	
	
КонецПроцедуры

Функция ПолучитьЗахваченныеФайлы(Форма)
	
	МассивЗахваченныхФайлов = Новый Массив;
	
	// мы в форме, и на руках ДеревоПриложений
		
	ДеревоПриложений = Форма.ДеревоПриложений;
	
	Для Каждого СтрокаДокумента Из ДеревоПриложений.ПолучитьЭлементы() Цикл
		
		// если Предмет - Файл
		Если ТипЗнч(СтрокаДокумента.Ссылка) = Тип("СправочникСсылка.Файлы") Тогда
			
			Если СтрокаДокумента.РедактируетТекущийПользователь Тогда
				
				ДанныеФайла = Новый Структура("Ссылка, ПолноеИмяФайлаВРабочемКаталоге, РедактируетТекущийПользователь, ДатаМодификацииУниверсальнаяВБазе, Размер, Расширение");
				ЗаполнитьЗначенияСвойств(ДанныеФайла, СтрокаДокумента);
				ДанныеФайла.РедактируетТекущийПользователь = Истина;
				
				МассивЗахваченныхФайлов.Добавить(ДанныеФайла);
					
			КонецЕсли;
			
		КонецЕсли;	
		
		Для Каждого ФайлыСтрока Из СтрокаДокумента.ПолучитьЭлементы() Цикл  // считаем что строго 2 уровневое дерево
			
			Если ФайлыСтрока.РедактируетТекущийПользователь Тогда
				
				ДанныеФайла = Новый Структура("Ссылка, ПолноеИмяФайлаВРабочемКаталоге, РедактируетТекущийПользователь, ДатаМодификацииУниверсальнаяВБазе, Размер, Расширение");
				ЗаполнитьЗначенияСвойств(ДанныеФайла, ФайлыСтрока);
				ДанныеФайла.РедактируетТекущийПользователь = Истина;
				
				МассивЗахваченныхФайлов.Добавить(ДанныеФайла);
					
			КонецЕсли;
			
			Для Каждого ФайлыСтрока3 Из ФайлыСтрока.ПолучитьЭлементы() Цикл  // считаем что строго 3 уровневое дерево - т.к. появились роли файлов
				
				Если ФайлыСтрока3.РедактируетТекущийПользователь Тогда
					
					ДанныеФайла = Новый Структура("Ссылка, ПолноеИмяФайлаВРабочемКаталоге, РедактируетТекущийПользователь, ДатаМодификацииУниверсальнаяВБазе, Размер, Расширение");
					ЗаполнитьЗначенияСвойств(ДанныеФайла, ФайлыСтрока3);
					ДанныеФайла.РедактируетТекущийПользователь = Истина;
					
					МассивЗахваченныхФайлов.Добавить(ДанныеФайла);
						
				КонецЕсли;
				
			КонецЦикла;	
			
		КонецЦикла;	
		
	КонецЦикла;	
	
	
	Возврат МассивЗахваченныхФайлов;
	
КонецФункции

Функция ПолучитьМассивДокументов(Форма)
	
	МассивДокументов = Новый Массив;
	
	// мы в форме, и на руках ДеревоПриложений
		
	ДеревоПриложений = Форма.ДеревоПриложений;
	
	Для Каждого СтрокаДокумента Из ДеревоПриложений.ПолучитьЭлементы() Цикл
		
		Если ДелопроизводствоКлиентСервер.ЭтоСсылкаНаДокумент(СтрокаДокумента.Ссылка) Тогда
			МассивДокументов.Добавить(СтрокаДокумента.Ссылка);
		КонецЕсли; 	
		
	КонецЦикла;	
	
	Возврат МассивДокументов;
	
КонецФункции

// Вызывается после установки недействительности предмета и проверяет необходимость прерывания процессов.
//
Процедура ПослеУстановкиНедействительностиПредмета(Форма, Предмет) Экспорт
	
	Если Не ЗначениеЗаполнено(Предмет) Тогда
		Возврат;
	КонецЕсли;
	
	Процессы = РаботаСБизнесПроцессамиВызовСервера.ПроцессыПоПредметуКоторыеМожетПрерватьТекущийПользователь(Предмет);
	Если Процессы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = СтрШаблон(НСтр("ru = 'Есть активные процессы:
		|%1
		|Прервать их?'"),
		СтрСоединить(Процессы, Символы.ПС));
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Предмет", Предмет);
	ДополнительныеПараметры.Вставить("Процессы", Процессы);
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Прервать'"));
	Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Не прерывать'"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеУстановкиНедействительностиПредметаПослеВопроса",
		ЭтотОбъект,
		ДополнительныеПараметры);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки);
	
КонецПроцедуры

// Вызывается после установки недействительности предмета, после ответа на вопрос о прерывании.
//
Процедура ПослеУстановкиНедействительностиПредметаПослеВопроса(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСБизнесПроцессамиВызовСервера.ПрерватьПроцессы(ДополнительныеПараметры.Процессы,
		НСтр("ru = 'Предмет более не действителен'"));
	
	Оповестить("БизнесПроцессПрерван",
		Новый Структура("СсылкаНаПредметБизнесПроцесса", ДополнительныеПараметры.Предмет));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс_ПроцессПодписание

#Область ДеревоУчастников

// Возвращает отметки прохождения процесса из таблиц Этапы и Участники.
// 
// Параметры:
// 	ПроцессОбъект - ДанныеФормыСтруктура - данные процесса или его шаблона в карточке. 
// 	
// Возвращаемое значение:
//  Соответствие
//    * Ключ - УникальныйИдентификатор - идентфикатор этапа или участника
//    * Значение - Булево - отметка прохождения
// 	
Функция ОтметкиПрохожденияПроцессаПодписания(ПроцессОбъект) Экспорт
	
	ОтметкиПрохождения = Новый Соответствие;
	
	Для Каждого СтрокаЭтапа Из ПроцессОбъект.Этапы Цикл
		ОтметкиПрохождения[СтрокаЭтапа.Идентификатор] = СтрокаЭтапа.Пройден; 
	КонецЦикла;
	
	Для Каждого СтрокаУчастника Из ПроцессОбъект.Участники Цикл
		ОтметкиПрохождения[СтрокаУчастника.Идентификатор] = СтрокаУчастника.Пройден; 
	КонецЦикла;
	
	Возврат ОтметкиПрохождения;
	
КонецФункции

// Заполняет отметки прохождения этапов и участников в процессе подписания. 
// 
// Параметры:
// 	ПроцессОбъект - ДанныеФормыСтруктура - данные процесса или его шаблона в карточке.
// 	ОтмекиПроходения - Соответствие - см. ОтметкиПрохожденияПроцессаПодписания
//
Процедура ЗаполнитьОтметкиПрохожденияВПроцессеПодписания(ПроцессОбъект, ОтмекиПроходения) Экспорт
	
	Для Каждого СтрокаЭтапа Из ПроцессОбъект.Этапы Цикл
		ОтметкаПрохождения = ОтмекиПроходения[СтрокаЭтапа.Идентификатор];
		Если ОтметкаПрохождения <> Неопределено Тогда
			СтрокаЭтапа.Пройден = ОтметкаПрохождения;
		КонецЕсли;
	КонецЦикла;
		
	Для Каждого СтрокаУчастника Из ПроцессОбъект.Участники Цикл
		ОтметкаПрохождения = ОтмекиПроходения[СтрокаУчастника.Идентификатор];
		Если ОтметкаПрохождения <> Неопределено Тогда
			СтрокаУчастника.Пройден = ОтметкаПрохождения;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Обрабатывает добавление в дерево участников подписания.
// 
// Параметры:
// 	УчастникиПроцесса - ДанныеФормыКоллекцияЭлементовДерева - дерево с участниками в форме.
// 	УчастникиПроцессаЭлемент - ТаблицаФормы - элемент управления деревом участников.
// 	СпособПодписания - ПеречислениеСсылка.СпособыПодписанияПредметаПроцесса
// 	ВремяВСроках - Признак использования времени в сроках исполнения.
// 	Форма - ФормаКлиентскогоПриложения - карточка процесса или шаблона
// 
Процедура ОбработатьДобавлениеВДеревоУчастниковПодписания(
	УчастникиПроцесса, УчастникиПроцессаЭлемент, СпособПодписания, ВремяВСроках, Форма) Экспорт

	ТекущиеДанные = УчастникиПроцессаЭлемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрокаУчастника = Неопределено;
	НачатьРедактирование = Ложь;

	Если ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ОбработатьРезультатПодписания() Тогда
		
		// Если текущая строка обработки результата, то начиаем редактировать участника.
		НачатьРедактирование = Истина;
	
	ИначеЕсли ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ПодписатьСУчастником() Тогда
		
		// Если текущая строка Подписать, с единственным участником, то
		// переносим настройки участника в подчиненную строку, и добавляем еще одну
		// для нового участника.

		Если ВСтрокеЗаполненыОтображаемыеДанныеУчастникаПодписания(ТекущиеДанные) Тогда

			СтрокаУчастникаПодписания = ДобавитьСтрокуУчастникаЭтапаПодписания(ТекущиеДанные);
			СкопироватьДанныеУчастникаПодписания(СтрокаУчастникаПодписания, ТекущиеДанные);
			ОчиститьДанныеУчастникаПодписания(ТекущиеДанные);
			ТекущиеДанные.ТипСтроки = РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_Подписать();

			НоваяСтрокаУчастника = ДобавитьСтрокуУчастникаЭтапаПодписания(ТекущиеДанные);
			ЗаполнитьСрокиИсполненияВСтрокеУчастникаПодписания(НоваяСтрокаУчастника, ВремяВСроках);

		КонецЕсли;

		НачатьРедактирование = Истина;

	ИначеЕсли ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_Подписать() Тогда
		
		// Если текущая строка Подписать:
		// Если есть этапы, то добавляем этап в конец.
		// Иначе добавляем участника в конец. 
		Если ЕстьЭтапыПодписания(УчастникиПроцесса) Тогда
			ДобавитьСтрокуЭтапаПодписания(
				УчастникиПроцесса, УчастникиПроцессаЭлемент, СпособПодписания);
		Иначе
			НоваяСтрокаУчастника = ДобавитьСтрокуУчастникаЭтапаПодписания(ТекущиеДанные);
			ЗаполнитьСрокиИсполненияВСтрокеУчастникаПодписания(НоваяСтрокаУчастника, ВремяВСроках);
			НачатьРедактирование = Истина;
		КонецЕсли;
	ИначеЕсли ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_УчастникЭтапаПодписания() Тогда
		
		// Если текущая ветка участника подписания, то добавляем нового участника
		// в вышестоящего родителя.
		СтрокаРодитель = ТекущиеДанные.ПолучитьРодителя();
		НоваяСтрокаУчастника = ДобавитьСтрокуУчастникаЭтапаПодписания(СтрокаРодитель);
		ЗаполнитьСрокиИсполненияВСтрокеУчастникаПодписания(НоваяСтрокаУчастника, ВремяВСроках);
		НачатьРедактирование = Истина;
	ИначеЕсли ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ЭтапПодписания() Тогда
		
		//Если текущая строка является этапом, то добавляем в него строку участника.  
		НоваяСтрокаУчастника = ДобавитьСтрокуУчастникаЭтапаПодписания(ТекущиеДанные);
		ЗаполнитьСрокиИсполненияВСтрокеУчастникаПодписания(НоваяСтрокаУчастника, ВремяВСроках);
		НачатьРедактирование = Истина;
	КонецЕсли;

	Если НоваяСтрокаУчастника <> Неопределено Тогда
		УчастникиПроцессаЭлемент.ТекущаяСтрока = НоваяСтрокаУчастника.ПолучитьИдентификатор();
	КонецЕсли;

	Если НачатьРедактирование Тогда
		УчастникиПроцессаЭлемент.ТекущийЭлемент = 
			УчастникиПроцессаЭлемент.ПодчиненныеЭлементы.ЭтапУчастник_Представление;
		УчастникиПроцессаЭлемент.ИзменитьСтроку();
	КонецЕсли;
	
	Форма.ОбновитьСрокиИсполненияОтложенно("Участники");
	
КонецПроцедуры

// Обрабатывает начало редактирования в дереве участников подписания.
// 
// Параметры:
//  УчастникиПроцессаЭлемент - ТаблицаФормы - элемент управления деревом участников. 
// 	СпособПодписания - ПеречислениеСсылка.СпособыПодписанияПредметаПроцесса
// 	ДополнениеТипа - ОписаниеТипов, Неопределено - позволяет определить доп. типы данных,
// 	                 среди которых выполняется поиск. По умолчанию поиск выполняется только
// 	                 по справочнику Сотрудники.
// 	Отказ - Булево
// 	ИменаПредметов - Массив, Неопределено - массив имен предметов для автоподстановок.
// 	  * СправочникСсылка.ИменаПредметов
//
Процедура ОбработатьНачалоРедактированияВДеревеУчастниковПодписания(
	УчастникиПроцессаЭлемент, СпособПодписания, ДополнениеТипа, Отказ,
	Форма, ИменаПредметов = Неопределено) Экспорт
	
	// По умолчанию запрещаем любое редактирование.
	Отказ = Истина;
	
	ТекущиеДанные = УчастникиПроцессаЭлемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Стартован = Ложь;
	Если СтрЗаканчиваетсяНа(Форма.ИмяФормы, ".ФормаБизнесПроцесса")
		И Форма.Объект.Свойство("Стартован") Тогда
		
		Стартован = Форма.Объект.Стартован;
	КонецЕсли;

	Если Стартован И ТекущиеДанные.Пройден Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Задача текущего исполнителя уже завершена, изменение исполнителя невозможно!'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
		
	ЭтоПодписаниеНаБумаге =
		(СпособПодписания = 
		ПредопределенноеЗначение("Перечисление.СпособыПодписанияПредметаПроцесса.НаБумаге")); 
	
	ЭтоСтрокаУчастинкаПодписания =
		(ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ПодписатьСУчастником())
		Или  (ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_УчастникЭтапаПодписания());
		
	ЭтоСтрокаЭтапа = ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ЭтапПодписания();
	
	ЭтоСтрокаОбработкиРезультата = ТекущиеДанные.ТипСтроки	= 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ОбработатьРезультатПодписания(); 
	
	ТекущийЭлементДерева = УчастникиПроцессаЭлемент.ТекущийЭлемент;
	ПодчиненныеЭлементыДерева = УчастникиПроцессаЭлемент.ПодчиненныеЭлементы;
	
	Если ЭтоСтрокаЭтапа Тогда
		Если Стартован Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'В стартованном процессе изменение настроек этапа невозможно!'"));
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		ИзменитьСтрокаЭтапаПодписания(ТекущиеДанные, УчастникиПроцессаЭлемент, СпособПодписания, Форма);
	ИначеЕсли ЭтоСтрокаОбработкиРезультата Тогда
			
		Если ТекущийЭлементДерева = ПодчиненныеЭлементыДерева.ЭтапУчастник_Представление
			Или ТекущийЭлементДерева = ПодчиненныеЭлементыДерева.СрокИсполненияПредставление Тогда
			
			// Для строки участника разрешаем редактирование полей: участники, сроки.
			Отказ = Ложь;
		КонецЕсли;
	ИначеЕсли ЭтоСтрокаУчастинкаПодписания Тогда
		
		Если ТекущийЭлементДерева = ПодчиненныеЭлементыДерева.ЭтапУчастник_Представление Тогда
			Если ЭтоПодписаниеНаБумаге И ТекущиеДанные.Участник <> ТекущиеДанные.Подписывающий
				И ЗначениеЗаполнено(ТекущиеДанные.Участник)
				И ЗначениеЗаполнено(ТекущиеДанные.Участник)  Тогда
				
				НастроитьУчастникаПроцессаПодписанияНаБумаге(
					УчастникиПроцессаЭлемент, ИменаПредметов, ДополнениеТипа);
			Иначе
				Отказ = Ложь;
			КонецЕсли;
		ИначеЕсли ТекущийЭлементДерева = ПодчиненныеЭлементыДерева.СрокИсполненияПредставление Тогда
			Отказ = Ложь;
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

// Обрабатывает удаление в дереве участников подписания.
// 
// Параметры:
// 	УчастникиПроцесса - ДанныеФормыКоллекцияЭлементовДерева - дерево с участниками в форме.
// 	УчастникиПроцессаЭлемент - ТаблицаФормы - элемент управления деревом участников.
// 	Форма - ФормаКлиентскогоПриложения 
//
Процедура ОбработатьУдалениеВДеревеУчастниковПодписания(
	УчастникиПроцесса, УчастникиПроцессаЭлемент, Форма) Экспорт

	ТекущиеДанные = УчастникиПроцессаЭлемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда

		Возврат;
	КонецЕсли;
	
	// Строку подписания без участника не удаляем - это строка 0го уровня, она остается всегда.
	Если ТекущиеДанные.ТипСтроки = РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_Подписать() Тогда
		Возврат;
	КонецЕсли;
	
	Стартован = Ложь;
	Если СтрЗаканчиваетсяНа(Форма.ИмяФормы, ".ФормаБизнесПроцесса")
		И Форма.Объект.Свойство("Стартован") Тогда
		
		Стартован = Форма.Объект.Стартован;
	КонецЕсли;
	
	Если Стартован И ТекущиеДанные.Пройден Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Задача текущего исполнителя уже завершена, удаление исполнителя невозможно!'"));
		Возврат;
	КонецЕсли;
	
	Форма.Модифицированность = Истина;
	
	// Если это строка подписания с единственным участником или строка обработки результата,
	// то очищаем данные об участнике подписания.
	Если ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ПодписатьСУчастником()
		
		Или ТекущиеДанные.ТипСтроки	= 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ОбработатьРезультатПодписания() Тогда

		ОчиститьДанныеУчастникаПодписания(ТекущиеДанные);
		Форма.ОбновитьСрокиИсполненияОтложенно("Участники");
		Возврат;
	КонецЕсли;

	СтрокаРодитель = ТекущиеДанные.ПолучитьРодителя();
	ПодчиненныеСтроки = СтрокаРодитель.ПолучитьЭлементы();
	
	// Если в подписании нет этапов и осталось всего 2 частника, одни из которых
	// сейчас удаляется, то оставшийся объединятеся со строкой задания Подписать.
	Если ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_УчастникЭтапаПодписания() Тогда

		Если Не ЕстьЭтапыПодписания(УчастникиПроцесса) Тогда

			Если ПодчиненныеСтроки.Количество() = 2 Тогда

				Для Каждого СтрокаУчастника Из ПодчиненныеСтроки Цикл
					Если СтрокаУчастника = ТекущиеДанные Тогда
						Продолжить;
					КонецЕсли;
					СкопироватьДанныеУчастникаПодписания(СтрокаРодитель, СтрокаУчастника);
					СтрокаРодитель.ТипСтроки = 
						РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ПодписатьСУчастником();
					ПодчиненныеСтроки.Удалить(СтрокаУчастника);
					Прервать;
				КонецЦикла;

			КонецЕсли;

		КонецЕсли;

		ПодчиненныеСтроки.Удалить(ТекущиеДанные);
		Форма.ОбновитьСрокиИсполненияОтложенно("Участники");
		Возврат;

	КонецЕсли;
	
	// Этап подписания просто удаляется, вместе со всеми участниками.
	Если ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ЭтапПодписания() Тогда
		ПодчиненныеСтроки.Удалить(ТекущиеДанные);
		Если ПодчиненныеСтроки.Количество() = 0 Тогда
			СтрокаРодитель.ТипСтроки = 
				РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ПодписатьСУчастником();
		КонецЕсли;
		Форма.ОбновитьСрокиИсполненияОтложенно("Участники");
		Возврат;
	КонецЕсли;

КонецПроцедуры

// Обрабатывает начало подбора в дерево участников подписания.
// 
// Параметры:
// 	УчастникиПроцесса
// 	УчастникиПроцессаЭлемент - ТаблицаФормы - элемент управления деревом участников.
//  ПоказыватьАвтоподстановки - Булево - признак отобра автоподстановок при выборе.
// 	ИменаПредметов - Массив, Неопределено
// 	  * СправочникСсылка.ИменаПредметов
// 
Процедура ПодборатьУчастниковПодписанияВДерево(
	УчастникиПроцесса, УчастникиПроцессаЭлемент,
	ДополнениеТипа, ИменаПредметов = Неопределено) Экспорт
	
	СтрокаРодитель = СтрокаРодительДляДобавленияУчастниковПодписанияВДерево(
		УчастникиПроцесса, УчастникиПроцессаЭлемент);
	
	ВыбранныеАдресаты = Новый Массив;
	Для Каждого СтрокаУчастника Из СтрокаРодитель.ПолучитьЭлементы() Цикл
		ВыбранныеАдресаты.Добавить(СтрокаУчастника.Участник);
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимРаботыФормы", 2);
	ПараметрыФормы.Вставить("УпрощенныйИнтерфейс", Истина);
	ПараметрыФормы.Вставить("ОтображатьСотрудников", Истина);
	ПараметрыФормы.Вставить("ПодменятьПользователейСотрудниками", Истина);
	ПараметрыФормы.Вставить("ЗаголовокФормы", НСтр("ru = 'Подбор участников'"));
	ПараметрыФормы.Вставить("ЗаголовокСпискаВыбранных", НСтр("ru = 'Выбранные сотрудники и роли:'"));
	ПараметрыФормы.Вставить("ЗаголовокСпискаАдреснойКниги", НСтр("ru = 'Все сотрудники и роли:'"));
	ПараметрыФормы.Вставить("ВыбранныеАдресаты", ВыбранныеАдресаты);
	ПараметрыФормы.Вставить("КонтролироватьДублиАдресатов", Истина);
	
	Если ДополнениеТипа.СодержитТип(Тип("СправочникСсылка.ПолныеРоли")) Тогда
		ПараметрыФормы.Вставить("ОтображатьРоли", Истина);
	КонецЕсли;
	
	Если ДополнениеТипа.СодержитТип(Тип("СправочникСсылка.АвтоподстановкиДляПроцессов")) Тогда	
		ПараметрыФормы.Вставить("ОтображатьАвтоподстановкиПоПроцессам", Истина);
	КонецЕсли;
	
	Если ИменаПредметов <> Неопределено Тогда
		СписокПредметов = Новый СписокЗначений();
		СписокПредметов.ЗагрузитьЗначения(ИменаПредметов);
		ПараметрыФормы.Вставить("ИменаПредметов", СписокПредметов);
	КонецЕсли;
	
	РаботаСАдреснойКнигойКлиент.ВыбратьАдресатов(
		ПараметрыФормы, УчастникиПроцессаЭлемент);
	
КонецПроцедуры

// Обрабатывает подбор в дерево участников подписания. 
// 
// Параметры:
// 	УчастникиПроцесса - ДанныеФормыКоллекцияЭлементовДерева - дерево с участниками в форме.
// 	УчастникиПроцессаЭлемент - ТаблицаФормы - элемент управления деревом участников.
// 	ВыбранноеЗначение - Массив - массив выбранных участников.
// 	ВремяВСроках - Булево - признак использования времени в сроках.
//
Процедура ОбработатьПодборУчастниковПодписанияВДерево(УчастникиПроцесса, УчастникиПроцессаЭлемент,
	ВыбранноеЗначение, ВремяВСроках, Форма) Экспорт
	
	КэшНовыхУчастников = Новый Соответствие;
	Для Каждого НовыйУчастник Из ВыбранноеЗначение Цикл
		КэшНовыхУчастников[НовыйУчастник] = Истина;
	КонецЦикла;
	
	СтрокаРодитель = СтрокаРодительДляДобавленияУчастниковПодписанияВДерево(
		УчастникиПроцесса, УчастникиПроцессаЭлемент);
	
	КэшСтрокУчастников = Новый Соответствие();
	
	СтрокиУчастников = СтрокаРодитель.ПолучитьЭлементы();
	
	// Удаляем строки исключенных участников и формируем кэш тех, кто остался.
	Индекс = СтрокиУчастников.Количество() - 1;
	Пока Индекс >= 0 Цикл
		СтрокаУчатсника = СтрокиУчастников[Индекс];
		Индекс = Индекс - 1;
		Если КэшНовыхУчастников[СтрокаУчатсника.Участник] = Истина Тогда
			КэшСтрокУчастников[СтрокаУчатсника.Участник] = СтрокаУчатсника;
			Продолжить
		КонецЕсли;
		СтрокиУчастников.Удалить(СтрокаУчатсника);
	КонецЦикла;
	
	// Добавляем строки для новых участников.
	Для Каждого НовыйУчастник Из ВыбранноеЗначение Цикл
		Если КэшСтрокУчастников[НовыйУчастник] <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаУчастника = ДобавитьСтрокуУчастникаЭтапаПодписания(СтрокаРодитель);
		СтрокаУчастника.Участник = НовыйУчастник;
		СтрокаУчастника.Подписывающий = НовыйУчастник;
		ОбновитьТочкуМаршрутаУчастникаПодписания(СтрокаУчастника);
		РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьПредставлениеУчастникаПодписания(
			СтрокаУчастника);
		
		ЗаполнитьСрокиИсполненияВСтрокеУчастникаПодписания(СтрокаУчастника, ВремяВСроках);
		
		КэшСтрокУчастников[СтрокаУчастника.Участник] = СтрокаУчастника;
		
	КонецЦикла;
	
	// Корректируем тип родительской строки, если это строка с заданием.
	Если СтрокаРодитель.ТипСтроки =
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ПодписатьСУчастником()
		
		Или СтрокаРодитель.ТипСтроки =
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_Подписать() Тогда
		
		Если ВыбранноеЗначение.Количество() > 0 Тогда
			СтрокаРодитель.ТипСтроки =
				РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_Подписать();
		Иначе
			СтрокаРодитель.ТипСтроки =
				РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ПодписатьСУчастником();
		КонецЕсли;
		
	КонецЕсли;
	
	УчастникиПроцессаЭлемент.Обновить();
	УчастникиПроцессаЭлемент.ТекущаяСтрока = СтрокаРодитель.ПолучитьИдентификатор();
	УчастникиПроцессаЭлемент.Развернуть(УчастникиПроцессаЭлемент.ТекущаяСтрока, Истина);
	
	Форма.ОбновитьСрокиИсполненияОтложенно("Участники");
	
КонецПроцедуры

// Обрабатывает очистку поля с участником подписания в дереве.
// 
// Параметры:
// 	УчастникиПроцессаЭлемент - ТаблицаФормы - элемент управления деревом участников.
//
Процедура ОбработатьОчисткуУчастникаПодписанияВДереве(УчастникиПроцессаЭлемент) Экспорт
	
	ТекущиеДанные = УчастникиПроцессаЭлемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Участник = Неопределено;
	
	ЭтоСтрокаУчастинкаПодписания =
		(ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ПодписатьСУчастником())
		Или  (ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_УчастникЭтапаПодписания());
	
	Если ЭтоСтрокаУчастинкаПодписания Тогда	 
		ТекущиеДанные.Подписывающий = Неопределено;
		ОбновитьТочкуМаршрутаУчастникаПодписания(ТекущиеДанные);
	КонецЕсли;
	
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьПредставлениеУчастникаПодписания(
		ТекущиеДанные);
	
КонецПроцедуры

// Обрабатывает открытие участника подписания в поле строки дерева участников. 
// 
// Параметры:
// 	УчастникиПроцессаЭлемент - ТаблицаФормы - элемент управления деревом участников.
//
процедура ОбработатьОткрытиеУчастникаПодписанияИзДереве(УчастникиПроцессаЭлемент) Экспорт
	
	ТекущиеДанные = УчастникиПроцессаЭлемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, ТекущиеДанные.Участник);
	
КонецПроцедуры

// Обрабатывает выбор участника подписания из поля дерева участников.
// 
// Параметры:
// 	Элемент - ПолеФормы - поле с участником.
// 	ВыбранноеЗначение -
// 	  СправочникСсылка.Сотрудники,
// 	  СправочникСсылка.ПолныеРоли,
// 	  СправочникСсылка.АвтоподстановкиДляПроцессов
// 	СтандартнаяОбработка - Булево - параметр управляющий использованием стандартной логики
//                                  обработки выбора подписания.
//
Процедура ОбработатьВыборУчастникаПодписанияВДереве(Элемент, ВыбранноеЗначение,
	СтандартнаяОбработка) Экспорт
	
	СотрудникиКлиент.ОбработкаВыбораКонтейнера(
		Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	Если СтандартнаяОбработка = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаУчастника = Элемент.Родитель.ТекущиеДанные;
	СтрокаУчастника.Участник = ВыбранноеЗначение;
	
	ЭтоСтрокаУчастинкаПодписания =
		(СтрокаУчастника.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ПодписатьСУчастником())
		Или  (СтрокаУчастника.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_УчастникЭтапаПодписания());
	
	Если ЭтоСтрокаУчастинкаПодписания Тогда	
		СтрокаУчастника.Подписывающий = ВыбранноеЗначение;
		ОбновитьТочкуМаршрутаУчастникаПодписания(СтрокаУчастника);
	КонецЕсли;
	
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьПредставлениеУчастникаПодписания(
		СтрокаУчастника);
	
КонецПроцедуры

// Обрабатывает автобподбор участника подписания в поле дерева участников. 
// 
// Параметры:
// 	Текст - Строка - введенный текст. 
// 	СтандартнаяОбработка - параметр управляющий использованием стандартной логики
//                         обработки выбора подписания.
// 	ДанныеВыбора - Массив - данные для списка выбора, отображаемого пользователю.
// 	ДополнениеТипа - ОписаниеТипов, Неопределено - позволяет определить доп. типы данных,
// 	                 среди которых выполняется поиск. По умолчанию поиск выполняется только
// 	                 по справочнику Сотрудники.
//
Процедура ОбработатьАвтоподборУчастникаПодписанияВДереве(Текст, СтандартнаяОбработка, ДанныеВыбора,
	ДополнениеТипа = Неопределено) Экспорт
	
	ТекстДляОбработки = СокрЛ(Текст);
	Если Не ЗначениеЗаполнено(ТекстДляОбработки) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеВыбора = СотрудникиВызовСервера.СформироватьДанныеВыбора(
		ТекстДляОбработки, ДополнениеТипа);
	
КонецПроцедуры

// Обрабатывает начало выбора участника подписания в поле дерева участников.
// 
// Параметры:
// 	УчастникиПроцессаЭлемент - ТаблицаФормы - элемент управления деревом участников.
//  СпособПодписания - ПеречислениеСсылка.СпособыПодписанияПредметаПроцесса
// 	ИменаПредметов - Массив, Неопределено - массив имен предметов для автоподстановок.
// 	  * СправочникСсылка.ИменаПредметов
// 	ДополнениеТипа - ОписаниеТипов, Неопределено - позволяет определить доп. типы данных,
// 	                 среди которых выполняется поиск. По умолчанию поиск выполняется только
// 	                 по справочнику Сотрудники.
//
Процедура ОбработатьНачалоВыбораУчастникаПодписанияВДереве(УчастникиПроцессаЭлемент,
	СпособПодписания, ИменаПредметов = Неопределено, ДополнениеТипа = Неопределено) Экспорт
	
	ТекущиеДанные = УчастникиПроцессаЭлемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоПодписаниеНаБумаге =
		(СпособПодписания = 
		ПредопределенноеЗначение("Перечисление.СпособыПодписанияПредметаПроцесса.НаБумаге")); 
	
	ЭтоСтрокаУчастинкаПодписания =
		(ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ПодписатьСУчастником())
		Или  (ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_УчастникЭтапаПодписания());
	
	Если ЭтоПодписаниеНаБумаге И ЭтоСтрокаУчастинкаПодписания Тогда
		НастроитьУчастникаПроцессаПодписанияНаБумаге(
			УчастникиПроцессаЭлемент, ИменаПредметов, ДополнениеТипа);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимРаботыФормы", 1);
	ПараметрыФормы.Вставить("УпрощенныйИнтерфейс", Истина);
	ПараметрыФормы.Вставить("ОтображатьСотрудников", Истина);
	ПараметрыФормы.Вставить("ПодменятьПользователейСотрудниками", Истина);
	
	Если ДополнениеТипа <> Неопределено 
		И ДополнениеТипа.СодержитТип(Тип("СправочникСсылка.ПолныеРоли")) Тогда
			
		ПараметрыФормы.Вставить("ОтображатьРоли", Истина);
	КонецЕсли;
	
	Если ДополнениеТипа <> Неопределено 
		И ДополнениеТипа.СодержитТип(Тип("СправочникСсылка.АвтоподстановкиДляПроцессов")) Тогда
		
		ПараметрыФормы.Вставить("ОтображатьАвтоподстановкиПоПроцессам", Истина);
	КонецЕсли;
	
	Если ИменаПредметов <> Неопределено Тогда
		СписокПредметов = Новый СписокЗначений();
		СписокПредметов.ЗагрузитьЗначения(ИменаПредметов);
		ПараметрыФормы.Вставить("ИменаПредметов", СписокПредметов);
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Участник) Тогда
		ПараметрыФормы.Вставить("ВыбранныеАдресаты", ТекущиеДанные.Участник);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ЗаголовокФормы", НСтр("ru = 'Выбор участника'"));
	
	РаботаСАдреснойКнигойКлиент.ВыбратьАдресатов(
		ПараметрыФормы,
		УчастникиПроцессаЭлемент.ПодчиненныеЭлементы.ЭтапУчастник_Представление);
	
КонецПроцедуры

// Добавляет строку этапа подписания в дерево участников.
// 
// Параметры:
// 	УчастникиПроцесса - ДанныеФормыКоллекцияЭлементовДерева - дерево с участниками в форме.
// 	УчастникиПроцессаЭлемент - ТаблицаФормы - элемент управления деревом участников.
// 	СпособПодписания - ПеречислениеСсылка.СпособыПодписанияПредметаПроцесса
//
Процедура ДобавитьСтрокуЭтапаПодписания(УчастникиПроцесса, УчастникиПроцессаЭлемент,
	СпособПодписания) Экспорт

	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("УчастникиПроцесса", УчастникиПроцесса);
	ДопПараметры.Вставить("УчастникиПроцессаЭлемент", УчастникиПроцессаЭлемент);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СпособПодписания", СпособПодписания);	
	
	ОткрытьФорму("ОбщаяФорма.КарточкаЭтапаПодписания", ПараметрыФормы, УчастникиПроцессаЭлемент,,,,
		Новый ОписаниеОповещения("ЗавершитьДобавлениеЭтапаПодписания", ЭтотОбъект, ДопПараметры),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

// Добавляет строку участника подписания для текущей позиции дереве участников.
// При необходимости текущая позиция корректируется в зависимости от типа строки.
// 
// Параметры:
// 	УчастникиПроцесса - ДанныеФормыКоллекцияЭлементовДерева - дерево с участниками в форме.
// 	УчастникиПроцессаЭлемент - ТаблицаФормы - элемент управления деревом участников.
// 	СпособПодписания - ПеречислениеСсылка.СпособыПодписанияПредметаПроцесса
// 	ВремяВСроках - Булево - признак использования времени в сроках исполнения.
// 	Форма - ФормаКлиентскогоПриложения - карточка процесса или шаблона
//
Процедура ДобавитьСтрокуУчастникаПодписанияДляТекущегоРодителя(УчастникиПроцесса,
	УчастникиПроцессаЭлемент, СпособПодписания, ВремяВСроках, Форма) Экспорт
	
	// Определяем строку родителя, в которую добавляем участника общ. обработчиком добавления
	// элементов в дерево участников.

	СтрокаРодитель = СтрокаРодительДляДобавленияУчастниковПодписанияВДерево(
		УчастникиПроцесса, УчастникиПроцессаЭлемент);

	УчастникиПроцессаЭлемент.ТекущаяСтрока = СтрокаРодитель.ПолучитьИдентификатор();
	ОбработатьДобавлениеВДеревоУчастниковПодписания(
		УчастникиПроцесса, УчастникиПроцессаЭлемент, СпособПодписания,
		ВремяВСроках, Форма);

КонецПроцедуры

// Обрабатывает перемещение строки в дереве участников процесса подписания.
// 
// Параметры:
// 	УчастникиПроцессаЭлемент
// 	Смещение - Число - для смещения вверх следует передать -1, вниз 1
// 	Форма - ФормаКлиентскогоПриложения - карточка процесса или шаблона  
//
Процедура ОбработатьПеремещениеСтрокиВДеревеУчастниковПодписания(
	УчастникиПроцессаЭлемент, Смещение, Форма) Экспорт
	
	ТекущиеДанные = УчастникиПроцессаЭлемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Смещение допустимо только для строк участников и этапов.
	Если ТекущиеДанные.ТипСтроки <> РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ЭтапПодписания()
		
		И ТекущиеДанные.ТипСтроки <> 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_УчастникЭтапаПодписания() Тогда
		
		Возврат;
	КонецЕсли;
	
	Стартован = Ложь;
	Если СтрЗаканчиваетсяНа(Форма.ИмяФормы, ".ФормаБизнесПроцесса")
		И Форма.Объект.Свойство("Стартован") Тогда
		
		Стартован = Форма.Объект.Стартован;
	КонецЕсли;
	
	Если Стартован И ТекущиеДанные.Пройден Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Задача текущего исполнителя уже завершена, изменение порядка исполнителей невозможно!'"));
		Возврат;
	КонецЕсли;
	
	Форма.Модифицированность = Истина;	
	
	СтрокаРодителя = ТекущиеДанные.ПолучитьРодителя();
	ПодчиненныеЭлементыРодителя = СтрокаРодителя.ПолучитьЭлементы();
	
	ИндексТекущегоЭлемента = ПодчиненныеЭлементыРодителя.Индекс(ТекущиеДанные);
	
	// Ничего не делаем, если новый индекс (новое положение строки) выходит за рамки
	// диапазона индексов строк. 
	НовыйИндекс = ИндексТекущегоЭлемента + Смещение; 
	Если НовыйИндекс < 0 Или НовыйИндекс > ПодчиненныеЭлементыРодителя.Количество() - 1 Тогда
		Возврат;
	КонецЕсли;
	
	ПодчиненныеЭлементыРодителя.Сдвинуть(ИндексТекущегоЭлемента, Смещение);
	
	УчастникиПроцессаЭлемент.Обновить();
	
КонецПроцедуры

//  Разворачивает дерево участников подписания.
// 
// Параметры:
// 	УчастникиПроцесса - ДанныеФормыКоллекцияЭлементовДерева - дерево с участниками в форме.
// 	УчастникиПроцессаЭлемент - ТаблицаФормы - элемент управления деревом участников.
//
Процедура РазвернутьДеревоУчастниковПодписания(УчастникиПроцесса, УчастникиПроцессаЭлемент) Экспорт
	
	СтрокаДерева = СтрокаДевераУчастниковСЗаданиемПодписать(УчастникиПроцесса);
	УчастникиПроцессаЭлемент.Развернуть(СтрокаДерева.ПолучитьИдентификатор(), Истина);
	
КонецПроцедуры

// Обрабатывает смену способа подписания процесса или шаблона процесса.
// 
// Параметры:
// 	НовыйСпособПодписания - ПеречислениеСсылка.СпособыПодписанияПредметаПроцесса
// 	УчастникиПроцесса - ДанныеФормыКоллекцияЭлементовДерева - дерево с участниками в форме.
// 	УчастникиПроцессаЭлемент - ТаблицаФормы - элемент управления деревом участников.
// 	Форма - ФормаКлиентскогоПриложения - карточка процесса или шаблона.
//
Процедура ОбработатьСменуСпособаПодписания(
	НовыйСпособПодписания, УчастникиПроцесса, УчастникиПроцессаЭлемент, Форма) Экспорт
	
	СтрокаДевераПодписать = СтрокаДевераУчастниковСЗаданиемПодписать(УчастникиПроцесса);
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьПорядокВыполненияВСтрокеСЗаданиемПодписать(
		СтрокаДевераПодписать,
		НовыйСпособПодписания);
	НормализоватьСрокиИсполненияУчастниковПодписания(СтрокаДевераПодписать);
	
	НормализоватьПорядокВыполненияЭтаповПоСпособуПодписания(
		УчастникиПроцесса, НовыйСпособПодписания);
		
	НормализоватьСоставУчастниковПоСпособуПодписания(УчастникиПроцесса, НовыйСпособПодписания);
	
	УчастникиПроцессаЭлемент.Обновить();
	
	Форма.ОбновитьСрокиИсполненияОтложенно("СпособПодписания");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет проверки перед стартом бизнес-процесса
Процедура ПередСтартомБизнесПроцесса(
	Объект, 
	Отказ,
	ИдентификаторФормыПроцесса,
	ПараметрыЗаписиПроцесса) Экспорт
	
	Предметы = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(Объект);
	
	Если Предметы.Количество() > 0 Тогда
		
		Если ТипЗнч(Объект.Ссылка) = Тип("БизнесПроцессСсылка.Рассмотрение") Тогда
			// Если при старте процесса пользователь еще не отвечал на вопрос о старте с незарегистрированными документами или 
			// он ответил отрицательно на один из следующих за этим вопросов, будет задач вопрос о старте при занятых файлах у документов.
			Если Не ПараметрыЗаписиПроцесса.Свойство("СтартоватьПриНезарегистрированныхДокументах") Тогда
				
				ПараметрыОбработчикаОповещения = Новый Структура;
				ПараметрыОбработчикаОповещения.Вставить("ИдентификаторФормыПроцесса", ИдентификаторФормыПроцесса);
				ПараметрыОбработчикаОповещения.Вставить("ПараметрыЗаписиПроцесса", ПараметрыЗаписиПроцесса);
				
				ОписаниеОповещения = Новый ОписаниеОповещения(
					"ЗавершениеВопросаОСтартеПриНезарегистрированныхДокументах", 
					ЭтотОбъект,
					ПараметрыОбработчикаОповещения);
				
				НезарегистрированныеПредметы = Новый Массив;
				Для каждого Предмет из Предметы Цикл
					Если ДелопроизводствоКлиентСервер.ЭтоСсылкаНаДокумент(Предмет) Тогда
						РегистрационныйНомер = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(Предмет, "РегистрационныйНомер");
						Если ПустаяСтрока(РегистрационныйНомер) Тогда
							НезарегистрированныеПредметы.Добавить(Предмет);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				Кнопки = Новый СписокЗначений;
				Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Отправить'"));
				Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Отмена'"));
				
				Если НезарегистрированныеПредметы.Количество() = 1 Тогда
					ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Документ ""%1"" не зарегистрирован. 
						|Отправить на рассмотрение?'"), Предмет);
					ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , КодВозвратаДиалога.Нет, НСТР("ru = 'Запуск процесса'"));
					Отказ = Истина;
					Возврат;
				ИначеЕсли НезарегистрированныеПредметы.Количество() > 1 Тогда
					СтрокаПредметов = "";
					Для каждого Предмет из НезарегистрированныеПредметы Цикл
						СтрокаПредметов = СтрокаПредметов + Символы.ПС + Строка(Предмет)
					КонецЦикла;
					ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Документы %1 не зарегистрированы. 
						|Отправить на рассмотрение?'"), СтрокаПредметов);
					ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , КодВозвратаДиалога.Нет, НСТР("ru = 'Запуск процесса'"));
					Отказ = Истина;
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		МассивДокументов = Новый Массив;
		МассивЗанятыхФайлов = Новый Массив;
		
		Для каждого Предмет из Предметы Цикл
			Если ДелопроизводствоКлиентСервер.ЭтоСсылкаНаДокумент(Предмет) Тогда
				МассивДокументов.Добавить(Предмет);
			ИначеЕсли ТипЗнч(Предмет) = Тип("СправочникСсылка.Файлы") Тогда
				Редактирует = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(Предмет, "Редактирует");
				Если ЗначениеЗаполнено(Редактирует) Тогда
					МассивЗанятыхФайлов.Добавить(Предмет);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		// Если при старте процесса пользователь еще не отвечал на вопрос о старте с занятыми файлами документов или 
		// он ответил отрицательно на один из следующих за этим вопросов, будет задан вопрос о старте при занятых файлах у документов.
		Если Не ПараметрыЗаписиПроцесса.Свойство("СтартоватьПриЗанятыхФайлахДокументов") Тогда
			Если МассивДокументов.Количество() > 0 Тогда
				
				СообщениеВопрос = НСтр("ru = 'Выполнить запуск процесса?'");
				СообщениеЗаголовок = НСтр("ru = 'Некоторые файлы приложенных документов заняты для редактирования:'");
				Заголовок = НСтр("ru = 'Запуск процесса'");
				
				ПараметрыФормы = Новый Структура;
				ПараметрыФормы.Вставить("СообщениеВопрос", СообщениеЗаголовок);
				ПараметрыФормы.Вставить("СообщениеЗаголовок", СообщениеЗаголовок);
				ПараметрыФормы.Вставить("Заголовок", Заголовок);
				ПараметрыФормы.Вставить("ВладелецФайла", МассивДокументов);
				ПараметрыФормы.Вставить("ТекстКнопкиЗакончитьИЗакрыть", НСтр("ru = 'Закончить редактирование и выполнить'"));
				ПараметрыФормы.Вставить("ТекстКнопкиЗакрыть", НСтр("ru = 'Выполнить'"));
				
				ФиксированныйМассивВсеСотрудникиТекущегоПользователя =
					СотрудникиКлиент.ВсеСотрудникиТекущегоПользователя();
				МассивВсеСотрудникиТекущегоПользователя = Новый Массив(
					ФиксированныйМассивВсеСотрудникиТекущегоПользователя);
				СотрудникиТекущегоПользователя = Новый СписокЗначений();
				СотрудникиТекущегоПользователя.ЗагрузитьЗначения(МассивВсеСотрудникиТекущегоПользователя);
				ПараметрыФормы.Вставить("СотрудникиТекущегоПользователя", СотрудникиТекущегоПользователя);
				
				ТекущийПользователь = ФайловыеФункцииКлиентПовтИсп.ПолучитьПерсональныеНастройкиРаботыСФайлами().ТекущийПользователь;
				ПараметрыФормы.Вставить("ТекущийПользователь", ТекущийПользователь);
				
				ПараметрыОбработчикаОповещения = Новый Структура;
				ПараметрыОбработчикаОповещения.Вставить("ИдентификаторФормыПроцесса", ИдентификаторФормыПроцесса);
				ПараметрыОбработчикаОповещения.Вставить("ПараметрыЗаписиПроцесса", ПараметрыЗаписиПроцесса);
				
				ОписаниеОповещения = Новый ОписаниеОповещения(
					"ЗавершениеВопросаОСтартеПриЗанятыхФайлахДокументов", 
					ЭтотОбъект,
					ПараметрыОбработчикаОповещения);
				// Открываем диалог с занятыми файлами документов и прерываем процесс записи процесса.
				// Процесс записи будет начат заново, когда пользователь ответ утвердительно.
				// В этом случае вопрос уже не будет задан повторно.
				РаботаСФайламиКлиент.ОткрытьДиалогСписокЗанятыхФайлов(ПараметрыФормы, ОписаниеОповещения);
				Если ПараметрыФормы.КоличествоЗанятыхФайлов > 0 Тогда
					Отказ = Истина;
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Если при старте процесса пользователь еще не отвечал на вопрос о старте при занятых файлах или 
		// он ответил отрицательно на один из следующих за этим вопросов, будет задач вопрос о старте при занятых файлах.
		Если Не ПараметрыЗаписиПроцесса.Свойство("СтартоватьПриЗанятыхФайлах") Тогда
			Если МассивЗанятыхФайлов.Количество() > 0 Тогда
				
				ПараметрыОбработчикаОповещения = Новый Структура;
				ПараметрыОбработчикаОповещения.Вставить("ИдентификаторФормыПроцесса", ИдентификаторФормыПроцесса);
				ПараметрыОбработчикаОповещения.Вставить("ПараметрыЗаписиПроцесса", ПараметрыЗаписиПроцесса);
				
				ОписаниеОповещения = Новый ОписаниеОповещения(
					"ЗавершениеВопросаОСтартеПриЗанятыхФайлах", 
					ЭтотОбъект,
					ПараметрыОбработчикаОповещения);
					
				ТекстВопроса = НСтр("ru = 'Некоторые приложенные файлы заняты для редактирования. Выполнить запуск процесса?'");
				Если МассивЗанятыхФайлов = 1 Тогда
					ТекстВопроса = НСтр("ru = 'Приложенный файл занят для редактирования. Выполнить запуск процесса?'");
				КонецЕсли;
				
				// Открываем диалог с вопросом и прерываем процесс записи процесса.
				// Процесс записи будет начат заново, когда пользователь ответ утвердительно.
				// В этом случае вопрос уже не будет задан повторно.
				ПоказатьВопрос(
					ОписаниеОповещения,
					ТекстВопроса,
					РежимДиалогаВопрос.ДаНет,,
					КодВозвратаДиалога.Нет, 
					НСтр("ru = 'Запуск процесса'"));
				
				Отказ = Истина;
				Возврат;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПараметрыЗаписиПроцесса.Свойство("СтартоватьПриОшибкахВПроектах") Тогда
		РаботаСПроектамиКлиент.ПередСтартомБизнесПроцесса(Объект, Отказ, ПараметрыЗаписиПроцесса, ИдентификаторФормыПроцесса);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВопросаОСтартеПриНезарегистрированныхДокументах(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		// Если пользователь ответил утвердительно, то информируем форму процесса о том,
		//	что запуск может быть возобновлен.
		Параметры.ПараметрыЗаписиПроцесса.Вставить("СтартоватьПриНезарегистрированныхДокументах", Истина);
		ПараметрыОповещения = Новый Структура();
		ПараметрыОповещения.Вставить("ИдентификаторФормы", Параметры.ИдентификаторФормыПроцесса);
		ПараметрыОповещения.Вставить("ПараметрыЗаписиПроцесса", Параметры.ПараметрыЗаписиПроцесса);
		Оповестить("СтартПроцессаПослеВопроса", ПараметрыОповещения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗавершениеВопросаОСтартеПриЗанятыхФайлахДокументов(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		// Если пользователь ответил утвердительно, то информируем форму процесса о том,
		//	что запуск может быть возобновлен.
		Параметры.ПараметрыЗаписиПроцесса.Вставить("СтартоватьПриЗанятыхФайлахДокументов", Истина);
		ПараметрыОповещения = Новый Структура();
		ПараметрыОповещения.Вставить("ИдентификаторФормы", Параметры.ИдентификаторФормыПроцесса);
		ПараметрыОповещения.Вставить("ПараметрыЗаписиПроцесса", Параметры.ПараметрыЗаписиПроцесса);
		Оповестить("СтартПроцессаПослеВопроса", ПараметрыОповещения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗавершениеВопросаОСтартеПриЗанятыхФайлах(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		// Если пользователь ответил утвердительно, то информируем форму процесса о том,
		//	что запуск может быть возобновлен.
		Параметры.ПараметрыЗаписиПроцесса.Вставить("СтартоватьПриЗанятыхФайлах", Истина);
		ПараметрыОповещения = Новый Структура();
		ПараметрыОповещения.Вставить("ИдентификаторФормы", Параметры.ИдентификаторФормыПроцесса);
		ПараметрыОповещения.Вставить("ПараметрыЗаписиПроцесса", Параметры.ПараметрыЗаписиПроцесса);
		Оповестить("СтартПроцессаПослеВопроса", ПараметрыОповещения);
	Иначе
		Параметры.ПараметрыЗаписиПроцесса.Удалить("СтартоватьПриНезарегистрированныхДокументах");
		Параметры.ПараметрыЗаписиПроцесса.Удалить("СтартоватьПриЗанятыхФайлахДокументов");
		Параметры.ПараметрыЗаписиПроцесса.Удалить("СтартоватьПриЗанятыхФайлах");
	КонецЕсли;
	
КонецПроцедуры

// Выполняет открытие карточки "Исполнителя", если он установлен
Процедура ОткрытьИсполнителя(Исполнитель) Экспорт
	
	Если ЗначениеЗаполнено(Исполнитель) Тогда
		ПоказатьЗначение(, Исполнитель);
	КонецЕсли;
	
КонецПроцедуры

// Проверяет есть ли занятые файлы и сразу помещает.
Процедура ПроверитьНаличиеЗанятыхФайлов(Форма, ОписаниеОповещения, 
	УникальныйИдентификатор = Неопределено) Экспорт
	
	#Если ВебКлиент Тогда
		ПроверитьНаличиеЗанятыхФайлов_НеПроверяемыеРасширения(Форма, ОписаниеОповещения);	
		Возврат;
	#КонецЕсли	
	
	МассивЗахваченныхФайлов = ПолучитьЗахваченныеФайлы(Форма);
	
	Если МассивЗахваченныхФайлов.Количество() > 0 Тогда
				
		ОбъектСсылка = Истина; // только для проверки на заполненность
		МассивЗахваченныхТолькоWordФайлов = Новый Массив;
		
		Расширение = "";
		СписокЗанятыеWordФайлы = ДелопроизводствоКлиент.ПолучитьЗанятыеВнешнимиПрограммамиФайлы(
			ОбъектСсылка, УникальныйИдентификатор, МассивЗахваченныхФайлов, Расширение,
			МассивЗахваченныхТолькоWordФайлов);

		Если СписокЗанятыеWordФайлы.Количество() <> 0 Тогда
			
			НазваниеПрограммы = ДелопроизводствоКлиент.ПолучитьНазваниеПрограммыЗахвата(
				Расширение, СписокЗанятыеWordФайлы.Количество());
			
			СтрокаФайлов = "";
			Для Каждого ИмяРасширение Из СписокЗанятыеWordФайлы Цикл
				Если Не ПустаяСтрока(СтрокаФайлов) Тогда
					СтрокаФайлов = СтрокаФайлов + Символы.ВК;
				КонецЕсли;	
				СтрокаФайлов = СтрокаФайлов + ИмяРасширение;
			КонецЦикла;
			
			Если СписокЗанятыеWordФайлы.Количество() > 1 Тогда
				ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Для выполнения задачи закройте файлы, занятые %1:
					| 
					|%2.'"),
					НазваниеПрограммы, СтрокаФайлов);
			Иначе	
				
				ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Для выполнения задачи закройте файл, занятый %1:
					| 
					|%2.'"),
					НазваниеПрограммы, СтрокаФайлов);
				
			КонецЕсли;	
			
			Кнопки = Новый СписокЗначений;
			Кнопки.Добавить(КодВозвратаДиалога.ОК);
			Кнопки.Добавить(КодВозвратаДиалога.Отмена);
			
			ДопПараметры = Новый Структура("Форма, ОписаниеОповещения, УникальныйИдентификатор", 
				Форма, ОписаниеОповещения, УникальныйИдентификатор);
			
			Обработчик = Новый ОписаниеОповещения("ПослеВопросаФайлыЗанятыWordПередЗакрытием", 
				ЭтотОбъект, ДопПараметры);
			ПоказатьВопрос(Обработчик, ТекстСообщения, Кнопки);
			Отказ = Истина;
			
			Возврат;
			
		КонецЕсли;
		
		НеОсвобождатьЗанятыеWordФайлы = Истина;
		ДелопроизводствоКлиент.ПоместитьИзмененияФайлов(
			ОбъектСсылка, УникальныйИдентификатор, 
			МассивЗахваченныхТолькоWordФайлов, НеОсвобождатьЗанятыеWordФайлы);
			
		ПроверитьНаличиеЗанятыхФайлов_НеПроверяемыеРасширения(Форма, ОписаниеОповещения);	
		
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаФайлыЗанятыWordПередЗакрытием(РезультатВопроса, ДопПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		
		// повторить операцию
		Форма = ДопПараметры.Форма;	
		ОписаниеОповещения = ДопПараметры.ОписаниеОповещения;
		УникальныйИдентификатор = ДопПараметры.УникальныйИдентификатор;
		ПроверитьНаличиеЗанятыхФайлов(Форма, ОписаниеОповещения, УникальныйИдентификатор);

	КонецЕсли;	
	
КонецПроцедуры

Функция ПроверитьНаличиеЗанятыхФайлов_НеПроверяемыеРасширения(Форма, ОписаниеОповещения) Экспорт
	
	МассивЗанятыхФайлов = ПолучитьЗахваченныеФайлы(Форма);
	МассивДокументов = ПолучитьМассивДокументов(Форма);
	
	ПараметрыОбработчикаОповещения = Новый Структура;
	ПараметрыОбработчикаОповещения.Вставить("МассивЗанятыхФайлов", МассивЗанятыхФайлов);
	ПараметрыОбработчикаОповещения.Вставить("ОписаниеОповещения", ОписаниеОповещения);
	
	ОписаниеОповещенияВопрос = Новый ОписаниеОповещения(
		"ВопросОВыполненииПриЗанятыхФайлах", 
		ЭтотОбъект,
		ПараметрыОбработчикаОповещения);
			
	Если МассивЗанятыхФайлов.Количество() > 0 Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СообщениеВопрос", НСтр("ru = 'Выполнить задачу?'"));
		ПараметрыФормы.Вставить("СообщениеЗаголовок", НСтр("ru = 'Некоторые файлы приложенных документов заняты для редактирования:'"));
		ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Выполнение задачи'"));
		ПараметрыФормы.Вставить("ТекстКнопкиЗакончитьИЗакрыть", НСтр("ru = 'Закончить редактирование и выполнить'"));
		ПараметрыФормы.Вставить("ТекстКнопкиЗакрыть", НСтр("ru = 'Выполнить'"));
		ПараметрыФормы.Вставить("ВладелецФайла", МассивДокументов);
		
		ФиксированныйМассивВсеСотрудникиТекущегоПользователя =
			СотрудникиКлиент.ВсеСотрудникиТекущегоПользователя();
		МассивВсеСотрудникиТекущегоПользователя = Новый Массив(
			ФиксированныйМассивВсеСотрудникиТекущегоПользователя);
		СотрудникиТекущегоПользователя = Новый СписокЗначений();
		СотрудникиТекущегоПользователя.ЗагрузитьЗначения(МассивВсеСотрудникиТекущегоПользователя);
		ПараметрыФормы.Вставить("СотрудникиТекущегоПользователя", СотрудникиТекущегоПользователя);
		
		ТекущийПользователь = ФайловыеФункцииКлиентПовтИсп.ПолучитьПерсональныеНастройкиРаботыСФайлами().ТекущийПользователь;
		ПараметрыФормы.Вставить("ТекущийПользователь", ТекущийПользователь);
		
		РаботаСФайламиКлиент.ОткрытьДиалогСписокЗанятыхФайлов(ПараметрыФормы, ОписаниеОповещенияВопрос);
		
		Если ПараметрыФормы.КоличествоЗанятыхФайлов = 0 Тогда
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);	
		КонецЕсли;		
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);	
	КонецЕсли;
	
КонецФункции

Процедура ВопросОВыполненииПриЗанятыхФайлах(Результат, Параметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда 
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОписаниеОповещения, КодВозвратаДиалога.Да);	
	
КонецПроцедуры

Процедура УстановитьГлавнуюЗадачуБизнесПроцессИзФормыОбъекта(Форма) Экспорт
	
	Если ЗначениеЗаполнено(Форма.Объект.ВедущаяЗадача) Тогда
		ПоказатьПредупреждение( ,
			НСтр("ru = 'Невозможно установить главную задачу процессу, входящему в составной или комплексный процесс.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КромеЗадачБизнесПроцесса", Форма.Объект.Ссылка);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"УстановитьГлавнуюЗадачуБизнесПроцессИзФормыОбъектаПродолжение", ЭтотОбъект, Форма);
		
	ОткрытьФорму("Задача.ЗадачаИсполнителя.ФормаВыбора", ПараметрыФормы,
		Форма.Элементы.ГлавнаяЗадача,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

Процедура УстановитьГлавнуюЗадачуБизнесПроцессИзФормыОбъектаПродолжение(ВыбранноеЗначение, Форма) Экспорт 
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда 
		Форма.Объект.ГлавнаяЗадача = ВыбранноеЗначение;
		Форма.Элементы.ГлавнаяЗадача.Видимость = Истина;
	КонецЕсли;	
	
КонецПроцедуры

// Устанавливает указанную строку текущей в дереве приложений в карточке задачи
Процедура УстановитьТекущуюСтрокуВДеревеПриложений(Форма, ЭлементыДерева, ТекущаяСсылкаВДереве, ТекущееИмяПредметаВДереве = Неопределено) Экспорт
	
	Для Каждого Элемент Из ЭлементыДерева Цикл
		Форма.Элементы.ДеревоПриложений.Развернуть(Элемент.ПолучитьИдентификатор(), Ложь);
	КонецЦикла;

	ЧислоСтрокДерева = ЭлементыДерева.Количество();
	ТекущаяСтрокаДерева = -1;
	
	Для каждого Эл Из ЭлементыДерева Цикл
		
		ТекущаяСтрокаДерева = ТекущаяСтрокаДерева + 1;
		
		Если Эл.Ссылка = ТекущаяСсылкаВДереве Или (ЗначениеЗаполнено(Эл.ИмяПредмета) И Эл.ИмяПредмета = ТекущееИмяПредметаВДереве) Тогда
			Форма.Элементы.ДеревоПриложений.ТекущаяСтрока = Эл.ПолучитьИдентификатор();
			Возврат;
		Иначе
			УстановитьТекущуюСтрокуВДеревеПриложений(
				Форма,
				Эл.ПолучитьЭлементы(),
				ТекущаяСсылкаВДереве, ТекущееИмяПредметаВДереве);
		КонецЕсли;
			 
	КонецЦикла;
	
	Если ТекущаяСтрокаДерева >= 0 И Не ЗначениеЗаполнено(Форма.Элементы.ДеревоПриложений.ТекущаяСтрока) Тогда
		Если ТипЗнч(Эл.ПолучитьРодителя()) <> Тип("ДанныеФормыЭлементДерева") Тогда
			Форма.Элементы.ДеревоПриложений.ТекущаяСтрока = Эл.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает доступность команд работы с файлами в карточке задачи
Процедура УстановитьДоступностьКомандРаботыСФайлами(Форма, Приложения, ЧислоПредметов = Неопределено) Экспорт

	ОткрытьКарточкуДоступность = Истина;
	ОткрытьДляПросмотраДоступность = Истина;
	РедактироватьДоступность = Истина;
	ЗакончитьРедактированиеДоступность = Истина;
	УдалитьПредметДоступность = Истина;
	СохранитьКакДоступность = Истина;
	
	Если ЧислоПредметов = Неопределено Тогда
		Попытка
			ЧислоПредметов = Форма.Объект.Предметы.Количество();
		Исключение
			// Если в форме нет реквизита Объект, то ничего не делаем.
			Возврат;
		КонецПопытки;
	КонецЕсли;
	
	ОбщаяДоступность = (ЧислоПредметов > 0);
	
	Если Приложения.ТекущиеДанные <> Неопределено Тогда
		
		ТекущаяСтрока = Приложения.ТекущиеДанные.Ссылка;
		
		КоличествоВыделенныхСтрок = Приложения.ВыделенныеСтроки.Количество() = 1;
		
		Если КоличествоВыделенныхСтрок = 1
			И Не Приложения.ТекущиеДанные.ДоступноУдаление Тогда
			УдалитьПредметДоступность = Ложь;
		КонецЕсли;
		
		Если ТипЗнч(ТекущаяСтрока) = Тип("СправочникСсылка.Файлы") Тогда
			
			Если Приложения.ТекущиеДанные.РедактируетДругойПользователь
				ИЛИ Приложения.ТекущиеДанные.ПодписанЭП Тогда
				РедактироватьДоступность = Ложь;
			КонецЕсли;
			
			Если Не Приложения.ТекущиеДанные.РедактируетТекущийПользователь
				ИЛИ Приложения.ТекущиеДанные.ПодписанЭП Тогда
				ЗакончитьРедактированиеДоступность = Ложь;
			КонецЕсли;
			
		Иначе
			
			ОткрытьДляПросмотраДоступность = Ложь;
			РедактироватьДоступность = Ложь;
			ЗакончитьРедактированиеДоступность = Ложь;
			
		КонецЕсли;
		
		Если КоличествоВыделенныхСтрок = 1 И Не ЗначениеЗаполнено(ТекущаяСтрока) Тогда
			ОткрытьДляПросмотраДоступность = Ложь;
			РедактироватьДоступность = Ложь;
			ЗакончитьРедактированиеДоступность = Ложь;
			УдалитьПредметДоступность = Ложь;
			СохранитьКакДоступность = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Форма.Элементы.Найти("ДеревоПриложенийКонтекстноеМенюОткрытьКарточку") <> Неопределено Тогда
		Форма.Элементы.ДеревоПриложенийКонтекстноеМенюОткрытьКарточку.Доступность =
			ОбщаяДоступность И ОткрытьКарточкуДоступность;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("ДеревоПриложенийКонтекстноеМенюСохранитьКак") <> Неопределено Тогда
		Форма.Элементы.ДеревоПриложенийКонтекстноеМенюСохранитьКак.Доступность =
			ОбщаяДоступность И СохранитьКакДоступность;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("ДеревоПриложенийКонтекстноеМенюОткрытьДляПросмотра") <> Неопределено Тогда
		Форма.Элементы.ДеревоПриложенийКонтекстноеМенюОткрытьДляПросмотра.Доступность =
			ОбщаяДоступность И ОткрытьДляПросмотраДоступность;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("ДеревоПриложенийКонтекстноеМенюРедактировать") <> Неопределено Тогда
		Форма.Элементы.ДеревоПриложенийКонтекстноеМенюРедактировать.Доступность =
			ОбщаяДоступность И РедактироватьДоступность;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("ДеревоПриложенийКонтекстноеМенюЗакончитьРедактирование") <> Неопределено Тогда
		Форма.Элементы.ДеревоПриложенийКонтекстноеМенюЗакончитьРедактирование.Доступность =
			ОбщаяДоступность И ЗакончитьРедактированиеДоступность;
	КонецЕсли;
	
	Если Форма.Элементы.Найти("ДеревоПриложенийКонтекстноеМенюУдалитьПредмет") <> Неопределено Тогда
		Форма.Элементы.ДеревоПриложенийКонтекстноеМенюУдалитьПредмет.Доступность =
			ОбщаяДоступность И УдалитьПредметДоступность;
	КонецЕсли;
	
КонецПроцедуры

// Обработка команды "Закончить редактирование" в карточке задачи
Процедура ЗакончитьРедактированиеТекущегоФайла(Форма, Приложения, ЧислоПредметов = Неопределено) Экспорт
	
	Если Приложения.ТекущиеДанные.Ссылка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Приложения.ТекущиеДанные.Ссылка) = Тип("СправочникСсылка.Файлы") Тогда
	
		ПараметрыОбработчика = Новый Структура;
		ПараметрыОбработчика.Вставить("Форма", Форма);
		ПараметрыОбработчика.Вставить("Приложения", Приложения);
		ПараметрыОбработчика.Вставить("ЧислоПредметов", ЧислоПредметов);
		Обработчик = Новый ОписаниеОповещения("ОбновитьДоступностьКомандРаботыСФайламиПослеРедактирования", ЭтотОбъект, ПараметрыОбработчика);
		
		ПараметрыОбновленияФайла = РаботаСФайламиКлиент.ПараметрыОбновленияФайла(Обработчик, 
			Приложения.ТекущиеДанные.Ссылка, 
			Форма.УникальныйИдентификатор);
		ПараметрыОбновленияФайла.ХранитьВерсии = Приложения.ТекущиеДанные.ХранитьВерсии;
		ПараметрыОбновленияФайла.РедактируетТекущийПользователь = Приложения.ТекущиеДанные.РедактируетТекущийПользователь;
		ПараметрыОбновленияФайла.Редактирует = Приложения.ТекущиеДанные.Редактирует;
		РаботаСФайламиКлиент.ЗакончитьРедактированиеСОповещением(ПараметрыОбновленияФайла);
		
	Иначе
		
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите файл.'"));
		
	КонецЕсли;
	
КонецПроцедуры

// Обработка команды "Редактировать" в карточке задачи
Процедура РедактироватьТекущийФайл(Форма, Приложения, ЧислоПредметов = Неопределено) Экспорт
	
	Если Приложения.ТекущиеДанные.Ссылка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Приложения.ТекущиеДанные.Ссылка) = Тип("СправочникСсылка.Файлы") Тогда
		
		ПараметрыОбработчика = Новый Структура;
		ПараметрыОбработчика.Вставить("Форма", Форма);
		ПараметрыОбработчика.Вставить("Приложения", Приложения);
		ПараметрыОбработчика.Вставить("ЧислоПредметов", ЧислоПредметов);
		Обработчик = Новый ОписаниеОповещения("ОбновитьДоступностьКомандРаботыСФайламиПослеРедактирования", ЭтотОбъект, ПараметрыОбработчика);
		
		РаботаСФайламиКлиент.РедактироватьСОповещением(Обработчик, Приложения.ТекущиеДанные.Ссылка);
		
	Иначе
		
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите файл.'"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступностьКомандРаботыСФайламиПослеРедактирования(Результат, ПараметрыВыполнения) Экспорт
	
	УстановитьДоступностьКомандРаботыСФайлами(ПараметрыВыполнения.Форма, ПараметрыВыполнения.Приложения, 
		ПараметрыВыполнения.ЧислоПредметов);	
	
КонецПроцедуры

// Развернуть дерево приложений
Процедура ДеревоПриложенийРазвернуть(Форма) Экспорт
	
	Для Каждого Элемент Из Форма.ДеревоПриложений.ПолучитьЭлементы() Цикл
		
		// сперва Предметы
		Форма.Элементы.ДеревоПриложений.Развернуть(Элемент.ПолучитьИдентификатор(), Ложь);
		
		// тут роли
		Для Каждого ЭлементДереваВторогоУровня ИЗ Элемент.ПолучитьЭлементы() Цикл
			Если ЗначениеЗаполнено(ЭлементДереваВторогоУровня.РольФайла) И
				ЭлементДереваВторогоУровня.РольОбязательная Тогда
				Форма.Элементы.ДеревоПриложений.Развернуть(ЭлементДереваВторогоУровня.ПолучитьИдентификатор(), Ложь);
			КонецЕсли;	
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры	

// Обработка события Выбор у дерева приложений в карточке задачи
Процедура ДеревоПриложенийВыбор(Форма, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		
		ТекущиеДанные = Форма.Элементы.ДеревоПриложений.ТекущиеДанные;
		ТекущаяСтрока = ТекущиеДанные.Ссылка;
		
		Если ТекущиеДанные.ЭтоРольФайла Тогда
			Возврат;
		КонецЕсли;	
		
		Если ТипЗнч(ТекущаяСтрока) = Тип("СправочникСсылка.Файлы") Тогда
			
			КакОткрывать = ФайловыеФункцииКлиентПовтИсп.ПолучитьПерсональныеНастройкиРаботыСФайлами().ДействиеПоДвойномуЩелчкуМыши;
			Если КакОткрывать = "ОткрыватьКарточку" Тогда
				ПоказатьЗначение(, ТекущаяСтрока);
				Возврат;
			КонецЕсли;
			
			ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(
				ТекущаяСтрока,
				Неопределено,
				Форма.УникальныйИдентификатор);
			
			ПараметрыОбработчика = Новый Структура;
			ПараметрыОбработчика.Вставить("ДанныеФайла", ДанныеФайла);
			ПараметрыОбработчика.Вставить("Форма", Форма);
			ПараметрыОбработчика.Вставить("Элемент", Элемент);
			Обработчик = Новый ОписаниеОповещения("СписокВыборПослеВыбораРежимаРедактирования", ЭтотОбъект, ПараметрыОбработчика);
			
			РаботаСФайламиКлиент.ВыбратьРежимИРедактироватьФайл(Обработчик, ДанныеФайла, Форма.Элементы.ДеревоПриложенийКонтекстноеМенюРедактировать.Доступность);
			
		Иначе
			
			ПоказатьЗначение(, ТекущаяСтрока);
			
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборПослеВыбораРежимаРедактирования(Результат, ПараметрыВыполнения) Экспорт
	
	РезультатОткрыть = "Открыть";
	РезультатРедактировать = "Редактировать";
	РезультатОткрытьКарточку = "ОткрытьКарточку";
	
	Если Результат = РезультатРедактировать Тогда
		Обработчик = Новый ОписаниеОповещения("ОбновитьКоманды", ЭтотОбъект, ПараметрыВыполнения);
		РаботаСФайламиКлиент.РедактироватьФайл(Обработчик, ПараметрыВыполнения.ДанныеФайла);
	ИначеЕсли Результат = РезультатОткрыть Тогда
		РаботаСФайламиКлиент.ОткрытьФайлСОповещением(Неопределено, ПараметрыВыполнения.ДанныеФайла); 
	ИначеЕсли Результат = РезультатОткрытьКарточку Тогда
		ПоказатьЗначение(, ПараметрыВыполнения.ДанныеФайла.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКоманды(Результат, ПараметрыВыполнения) Экспорт
	
	УстановитьДоступностьКомандРаботыСФайлами(ПараметрыВыполнения.Форма, ПараметрыВыполнения.Элемент);
	
КонецПроцедуры

// Обработка команды Просмотреть в  дереве приложений в карточке задачи
Процедура ОткрытьТекущийФайлДляПросмотра(Форма, Приложения) Экспорт
	
	Если Приложения.ТекущиеДанные.Ссылка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Приложения.ТекущиеДанные.Ссылка) = Тип("СправочникСсылка.Файлы") Тогда
		
		ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(
			Приложения.ТекущиеДанные.Ссылка,
			Неопределено,
			Форма.УникальныйИдентификатор);
			
		КомандыРаботыСФайламиКлиент.Открыть(ДанныеФайла);
		
	Иначе
		
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите файл.'"));
		
	КонецЕсли;
	
КонецПроцедуры

// Обработка команды Сохранить как в дереве приложений в карточке задачи
Процедура СохранитьТекущийФайл(Форма, Приложения) Экспорт
	
	Если Приложения.ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущаяСсылка = Приложения.ТекущиеДанные.Ссылка;
	Если Приложения.ВыделенныеСтроки.Количество() > 1 Тогда
		СписокФайловДляВыгрузки = Новый СписокЗначений;
		Для Каждого ВыбраннаяСтрока Из Приложения.ВыделенныеСтроки Цикл
			ДанныеСтроки = Приложения.ДанныеСтроки(ВыбраннаяСтрока);
			Если ТипЗнч(ДанныеСтроки.Ссылка) = Тип("СправочникСсылка.Файлы") Тогда
				СписокФайловДляВыгрузки.Добавить(ДанныеСтроки.Ссылка);
			КонецЕсли;
		КонецЦикла;
		
		Если СписокФайловДляВыгрузки.Количество() > 1 Тогда
			РаботаСФайламиКлиент.СохранитьФайлыКак(СписокФайловДляВыгрузки, Форма.УникальныйИдентификатор);
		ИначеЕсли СписокФайловДляВыгрузки.Количество() = 1 Тогда
			ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляСохранения(СписокФайловДляВыгрузки[0].Значение, Неопределено, Форма.УникальныйИдентификатор);
			КомандыРаботыСФайламиКлиент.СохранитьКак(ДанныеФайла, Форма.УникальныйИдентификатор);
		Иначе
			ПоказатьПредупреждение(,НСтр("ru = 'Выберите файл.'"));
		КонецЕсли;
	Иначе
		Если ТипЗнч(ТекущаяСсылка) = Тип("СправочникСсылка.Файлы") Тогда
			ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляСохранения(ТекущаяСсылка, Неопределено, Форма.УникальныйИдентификатор);
			КомандыРаботыСФайламиКлиент.СохранитьКак(ДанныеФайла, Форма.УникальныйИдентификатор);
		Иначе
			ПоказатьПредупреждение(,НСтр("ru = 'Выберите файл.'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Помечает/снимает пометку удаления с бизнес-процессов. В случае если у пользователя нет прав, то
// генерируется исключение.
//
// Параметры:
//	 - СсылкаНаОбъект - ФормаКлиентскогоПриложения - форма объекта из которой следует выполнить действие.
//
Процедура ПометитьНаУдалениеБизнесПроцесс(ФормаОбъекта) Экспорт
	
	Если ТипЗнч(ФормаОбъекта) <> Тип("ФормаКлиентскогоПриложения") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПоискаРеквизита = Новый Структура;
	СтруктураПоискаРеквизита.Вставить("Объект", Неопределено);
	
	ЗаполнитьЗначенияСвойств(СтруктураПоискаРеквизита, ФормаОбъекта);
	
	Если ТипЗнч(СтруктураПоискаРеквизита.Объект) <> Тип("ДанныеФормыСтруктура") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПоискаРеквизита.Очистить();
	СтруктураПоискаРеквизита.Вставить("Ссылка", Неопределено);
	
	ЗаполнитьЗначенияСвойств(СтруктураПоискаРеквизита, ФормаОбъекта.Объект);
	
	Если НЕ ЗначениеЗаполнено(СтруктураПоискаРеквизита.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПоискаРеквизита.Очистить();
	СтруктураПоискаРеквизита.Вставить("ПометкаУдаления", Неопределено);
	
	ЗаполнитьЗначенияСвойств(СтруктураПоискаРеквизита, ФормаОбъекта.Объект);
	
	Если НЕ ЗначениеЗаполнено(СтруктураПоискаРеквизита.ПометкаУдаления) Тогда
		Возврат;
	КонецЕсли;
	
	Если ФормаОбъекта.Модифицированность Тогда
		
		Если ФормаОбъекта.Объект.ПометкаУдаления Тогда
			ТекстВопроса =
				НСтр("ru = 'Для снятия отметки удаления необходимо записать внесенные вами изменения. Записать данные?'");
		Иначе
			ТекстВопроса =
				НСтр("ru = 'Для установки отметки удаления необходимо записать внесенные вами изменения. Записать данные?'");
		КонецЕсли;
		
	Иначе
		
		ФормаОбъекта.ЗаблокироватьДанныеФормыДляРедактирования();
		
		Если ФормаОбъекта.Объект.ПометкаУдаления Тогда
			ТекстВопросФормат =
				НСтр("ru = 'Снять с ""%1"" пометку на удаление?'");
		Иначе
			ТекстВопросФормат =
				НСтр("ru = 'Пометить ""%1"" на удаление?'");
		КонецЕсли;
		
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстВопросФормат,
			Строка(ФормаОбъекта.Объект.Ссылка));
		
	КонецЕсли;
	
	ОписаниеОповещения = 
		Новый ОписаниеОповещения("ПометитьНаУдалениеБизнесПроцессЗавершение", ЭтотОбъект, ФормаОбъекта);
		
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

Процедура ПометитьНаУдалениеБизнесПроцессЗавершение(Ответ, ФормаОбъекта) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Если ФормаОбъекта.Модифицированность Тогда
		ФормаОбъекта.Записать();
	КонецЕсли;
	
	РаботаСБизнесПроцессамиВызовСервера.ПометитьНаУдалениеБизнесПроцесс(
		ФормаОбъекта.Объект.Ссылка);
		
	Оповестить("БизнесПроцессИзменен", ФормаОбъекта.Объект.Ссылка);
	
	ОповеститьОбИзменении(ФормаОбъекта.Объект.Ссылка);
	
КонецПроцедуры

// Обработка команды ЗаписатьИЗакрыть в форме процесса
Процедура ЗаписатьИЗакрыть(Команда, Форма) Экспорт
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("ЗакрытьФормуПослеЗаписи", Истина);
	
	Если Форма.Записать(ПараметрыЗаписи) Тогда
		Форма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////
// Общие функции и процедуры для механизма бизнес-процесса "Исполнение"

// Возвращает структуру с доступными действиями для строки таблицы
//
// Параметры:
//   СтрокаТаблицы - текущие данные динамического списка
//
// Возвращаемое значение:
//   Структура - доступные действия над процессом текущей строки
//      Остановить - Булево - признак того что текущий процесс можно остановить
//      Продолжить - Булево - признак того что текущий процесс можно продолжить (сделать активным)
//
Функция ДоступныеДействияПоИзменениюСостоянияПроцесса(СтрокаТаблицы) Экспорт
	
	ДоступныеДействия = Новый Структура;
	ДоступныеДействия.Вставить("Остановить", Ложь);
	ДоступныеДействия.Вставить("Продолжить", Ложь);
	
	Если СтрокаТаблицы.Завершен
		ИЛИ НЕ СтрокаТаблицы.Стартован
		ИЛИ СтрокаТаблицы.Состояние = 
			ПредопределенноеЗначение("Перечисление.СостоянияБизнесПроцессов.ПустаяСсылка")
		ИЛИ ЗначениеЗаполнено(СтрокаТаблицы.ВедущаяЗадача) Тогда
		
		ДоступныеДействия.Остановить = Ложь;
		ДоступныеДействия.Продолжить = Ложь;
		
	ИначеЕсли СтрокаТаблицы.Состояние = 
			ПредопределенноеЗначение("Перечисление.СостоянияБизнесПроцессов.Активен") Тогда
			
		ДоступныеДействия.Остановить = Истина;
		ДоступныеДействия.Продолжить = Ложь;
		
	ИначеЕсли СтрокаТаблицы.Состояние = 
		ПредопределенноеЗначение("Перечисление.СостоянияБизнесПроцессов.Остановлен") Тогда
		
		ДоступныеДействия.Остановить = Ложь;
		ДоступныеДействия.Продолжить = Истина;
		
	КонецЕсли;
	
	Возврат ДоступныеДействия;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_РаботаСДеревомПроцессовИЗадач

// Открывает процесс или задачу соответствующую текущей строке дерева процессов и задач.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма с деревом процессов и задач.
//
Процедура ОткрытьТекущуюСтрокуДереваЗадач(Форма)
	
	Если Форма.Элементы.ДеревоЗадач.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСсылка = Форма.Элементы.ДеревоЗадач.ТекущиеДанные.Ссылка;
		
	Если ТипЗнч(ТекущаяСсылка) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		
		БизнесПроцессыИЗадачиКлиент.ОткрытьФормуВыполненияЗадачи(ТекущаяСсылка);
	
	ИначеЕсли ТипЗнч(ТекущаяСсылка) = Тип("ДокументСсылка.Задача") Тогда
		
		РаботаСЗадачамиКлиент.ОткрытьКарточкуАвтораЗадачи(ТекущаяСсылка);
			
	ИначеЕсли ТипЗнч(ТекущаяСсылка) = Тип("ДокументСсылка.ДействиеЗадачи") Тогда
		
		РаботаСЗадачамиКлиент.ОткрытьКарточкуИсполнителяЗадачи(ТекущаяСсылка);
		
	Иначе
		
		РаботаСКомплекснымиБизнесПроцессамиКлиент.ВыполнитьЗамерОткрытияФормыКП(ТекущаяСсылка);
		ПоказатьЗначение(, ТекущаяСсылка);
		
	КонецЕсли;

КонецПроцедуры

// Открывает текущую задачу списка СписокАктивныхЗадач
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма с списком активных задач.
//
Процедура ОткрытьЗадачуСпискаАктивныхЗадач(Форма)
	
	Если Форма.Элементы.СписокАктивныхЗадач.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСЗадачамиКлиент.ОткрытьКарточкуИсполнителяЗадачи(
		Форма.Элементы.СписокАктивныхЗадач.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ПроцессПодписание

#Область ДеревоУчастников

Функция ВСтрокеЗаполненыОтображаемыеДанныеУчастникаПодписания(СтрокаУчастника)

	ИменаРеквизитовСДаннымиУчастника = СтрРазделить(
		РеквизитыСтрокиСОтображаемымиДаннымиУчастника(), "," + Символы.ПС, Ложь);

	Для Каждого ИмяРеквизита Из ИменаРеквизитовСДаннымиУчастника Цикл
		Если ЗначениеЗаполнено(СтрокаУчастника[ИмяРеквизита]) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;

	Возврат Ложь;

КонецФункции

Функция РеквизитыСтрокиСОтображаемымиДаннымиУчастника()
	
	Возврат "Участник,
			|Подписывающий,
			|ВариантУстановкиСрокаИсполнения,
			|СрокИсполнения,
			|СрокИсполненияДни,
			|СрокИсполненияЧасы,
			|СрокИсполненияМинуты";
	
КонецФункции

Функция РеквизитыСтрокиСДаннымиУчастникаПодписания()

	Возврат "ИдентификаторУчастника,
			|Участник,
			|Подписывающий,
			|ВариантУстановкиСрокаИсполнения,
			|СрокИсполнения,
			|СрокИсполненияДни,
			|СрокИсполненияЧасы,
			|СрокИсполненияМинуты,
			|ТочкаМаршрута,
			|ЭтапУчастник_Представление,
			|СрокИсполненияПредставление";

КонецФункции

Процедура СкопироватьДанныеУчастникаПодписания(СтрокаПриемник, СтрокаИсточник)

	ЗаполнитьЗначенияСвойств(
		СтрокаПриемник, СтрокаИсточник,
		РеквизитыСтрокиСДаннымиУчастникаПодписания());

КонецПроцедуры

Процедура ОчиститьДанныеУчастникаПодписания(СтрокаУчастника)

	ЗаполнитьЗначенияСвойств(СтрокаУчастника,
		Новый Структура(РеквизитыСтрокиСДаннымиУчастникаПодписания()));

КонецПроцедуры

Функция ЕстьЭтапыПодписания(УчастникиПроцесса)

	Для Каждого СтрокаНулевогоУровня Из УчастникиПроцесса.ПолучитьЭлементы() Цикл

		Если СтрокаНулевогоУровня.ТипСтроки <> 
			РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_Подписать() Тогда
			
			Продолжить;
		КонецЕсли;

		СтрокиЭтапов = СтрокаНулевогоУровня.ПолучитьЭлементы();
		Если СтрокиЭтапов.Количество() > 0 И СтрокиЭтапов[0].ТипСтроки = 
			РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ЭтапПодписания() Тогда

			Возврат Истина;
		КонецЕсли;

		Прервать;

	КонецЦикла;

	Возврат Ложь;

КонецФункции

Функция СтрокаДевераУчастниковСЗаданиемПодписать(УчастникиПроцесса)

	Для Каждого СтрокДерева Из УчастникиПроцесса.ПолучитьЭлементы() Цикл
		Если СтрокДерева.ТипСтроки = РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_Подписать()
			
			Или СтрокДерева.ТипСтроки = 
			РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ПодписатьСУчастником() Тогда

			Возврат СтрокДерева;
		КонецЕсли;
	КонецЦикла;

	Возврат Неопределено;

КонецФункции

Функция СтрокаРодительДляДобавленияУчастниковПодписанияВДерево(
	УчастникиПроцесса, УчастникиПроцессаЭлемент)
	
	ТекущиеДанные = УчастникиПроцессаЭлемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
		
		Или ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ОбработатьРезультатПодписания() Тогда
		
		// Если в таблице ничего не выделено, то за текущую строку берем задание подписать.
		// Если выделено задание Обработать результат, то также берем строку задания Подписать.
		ТекущиеДанные = СтрокаДевераУчастниковСЗаданиемПодписать(УчастникиПроцесса);
	КонецЕсли;

	СтрокаРодитель = Неопределено;
	Если ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_УчастникЭтапаПодписания() Тогда
		
		// Если текущая строка участника подписания, то берем ее родителя.
		СтрокаРодитель = ТекущиеДанные.ПолучитьРодителя();

	ИначеЕсли ТекущиеДанные.ТипСтроки = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_Подписать()
		
		И ЕстьЭтапыПодписания(УчастникиПроцесса) Тогда
		
		// Если есть этапы, и текущая стока - задание подписать, то берем последний этап.
		СтрокиЭтапов = ТекущиеДанные.ПолучитьЭлементы();
		СтрокаРодитель = СтрокиЭтапов[СтрокиЭтапов.Количество() - 1];

	Иначе // ТипСтроки_Подписать или ТипСтроки_ПодписатьСУчастником или ТипСтроки_ЭтапПодписания
		
		// В других случаях оставляем текущую строку родителем.
		СтрокаРодитель = ТекущиеДанные;

	КонецЕсли;
	
	Возврат СтрокаРодитель;
	
КонецФункции

Процедура ЗавершитьДобавлениеЭтапаПодписания(ПараметрыЭтапа, ДопПараметры) Экспорт

	Если ПараметрыЭтапа = Неопределено Тогда
		Возврат;
	КонецЕсли;

	СтрокаРодитель = СтрокаДевераУчастниковСЗаданиемПодписать(ДопПараметры.УчастникиПроцесса);

	БылиЭтапы = ЕстьЭтапыПодписания(ДопПараметры.УчастникиПроцесса);
	
	// Добавляем строку этапа в самый конец
	СтрокаЭтапа = СтрокаРодитель.ПолучитьЭлементы().Добавить();
	СтрокаЭтапа.ТипСтроки = РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ЭтапПодписания();
	СтрокаЭтапа.ИдентификаторЭтапа = Новый УникальныйИдентификатор;
	СтрокаЭтапа.НаименованиеЭтапа = ПараметрыЭтапа.НаименованиеЭтапа;
	СтрокаЭтапа.ПорядокВыполненияУчастниками = ПараметрыЭтапа.ПорядокВыполненияУчастниками;
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьПредставлениеСтрокиЭтапаПодписания(СтрокаЭтапа);

	Если Не БылиЭтапы Тогда
		// Если раньше этапов не было, то переносим в новый этап существующих участников.
		Если СтрокаРодитель.ТипСтроки = 
			РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ПодписатьСУчастником() Тогда
			
			// Если ранее участник хранился в строке с заданием, то
			// разделяем их и переносим сведения об участнике в этап.
			Если ВСтрокеЗаполненыОтображаемыеДанныеУчастникаПодписания(СтрокаРодитель) Тогда
				СтрокаУчастникаЭтапа = ДобавитьСтрокуУчастникаЭтапаПодписания(СтрокаЭтапа);
				СкопироватьДанныеУчастникаПодписания(СтрокаУчастникаЭтапа, СтрокаРодитель);
				РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьПредставлениеУчастникаПодписания(
					СтрокаУчастникаЭтапа);
				ОчиститьДанныеУчастникаПодписания(СтрокаРодитель);
			КонецЕсли;
			СтрокаРодитель.ТипСтроки = РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_Подписать();
		Иначе
			// Если было несколько участников, то их все переносим в этап.
			
			СтрокиУчастников = СтрокаРодитель.ПолучитьЭлементы();
			Для Каждого СтрокаУчастника Из СтрокиУчастников Цикл
				
				Если СтрокаУчастника.ТипСтроки <> 
					РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_УчастникЭтапаПодписания() Тогда
					
					Продолжить;
				КонецЕсли;
				
				СтрокаУчастникаЭтапа = ДобавитьСтрокуУчастникаЭтапаПодписания(СтрокаЭтапа);
				СкопироватьДанныеУчастникаПодписания(СтрокаУчастникаЭтапа, СтрокаУчастника);
				РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьПредставлениеУчастникаПодписания(
					СтрокаУчастникаЭтапа);
			КонецЦикла;
			
			Индекс = СтрокиУчастников.Количество() - 1;
			Пока Индекс >= 0 Цикл
				СтрокаУчастника = СтрокиУчастников[Индекс];
				Если СтрокаУчастника.ТипСтроки = 
					РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_УчастникЭтапаПодписания() Тогда
					
					СтрокиУчастников.Удалить(СтрокаУчастника);
				КонецЕсли;
				Индекс = Индекс - 1;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	ДопПараметры.УчастникиПроцессаЭлемент.Обновить();
	ДопПараметры.УчастникиПроцессаЭлемент.Развернуть(СтрокаРодитель.ПолучитьИдентификатор(), Истина);
	ДопПараметры.УчастникиПроцессаЭлемент.ТекущаяСтрока = СтрокаЭтапа.ПолучитьИдентификатор();

КонецПроцедуры

Процедура ИзменитьСтрокаЭтапаПодписания(СтрокаЭтапа, УчастникиПроцессаЭлемент, СпособПодписания, Форма)

	ПараметрФормы = Новый Структура;
	ПараметрФормы.Вставить("НаименованиеЭтапа", СтрокаЭтапа.НаименованиеЭтапа);
	ПараметрФормы.Вставить("ПорядокВыполненияУчастниками", СтрокаЭтапа.ПорядокВыполненияУчастниками);
	ПараметрФормы.Вставить("СпособПодписания", СпособПодписания);
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Форма", Форма);
	ДопПараметры.Вставить("СтрокаЭтапа", СтрокаЭтапа);
	
	ОткрытьФорму("ОбщаяФорма.КарточкаЭтапаПодписания", ПараметрФормы, УчастникиПроцессаЭлемент, , , ,
		Новый ОписаниеОповещения("ЗавершитьИзменениеСтрокиЭтапаПодписания", ЭтотОбъект, ДопПараметры),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

Процедура ЗавершитьИзменениеСтрокиЭтапаПодписания(ПараметрыЭтапа, ДопПараметры) Экспорт

	Если ПараметрыЭтапа = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаЭтапа = ДопПараметры.СтрокаЭтапа;
	Форма = ДопПараметры.Форма;
	
	СтрокаЭтапа.НаименованиеЭтапа = ПараметрыЭтапа.НаименованиеЭтапа;
	СтрокаЭтапа.ПорядокВыполненияУчастниками = ПараметрыЭтапа.ПорядокВыполненияУчастниками;
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьПредставлениеСтрокиЭтапаПодписания(СтрокаЭтапа);
	
	НормализоватьСрокиИсполненияУчастниковПодписания(СтрокаЭтапа);
	
	Форма.ОбновитьСрокиИсполненияОтложенно("Участники");
	
КонецПроцедуры

Процедура НормализоватьСрокиИсполненияУчастниковПодписания(СтрокаРодитель)
	
	Если СтрокаРодитель.ПорядокВыполненияУчастниками = ПредопределенноеЗначение(
		"Перечисление.ПорядокВыполненияЭтапаДействия.Последовательно") Тогда
		
		Возврат;
	КонецЕсли;
	
	СтрокиУчастников = СтрокаРодитель.ПолучитьЭлементы();
	Если СтрокиУчастников.Количество() < 2 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаЭталон = СтрокиУчастников[0];
	
	Если СтрокаЭталон.ТипСтроки <>
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_УчастникЭтапаПодписания() Тогда
		
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаУчастника Из СтрокиУчастников Цикл
		
		Если СтрокаУчастника = СтрокаЭталон Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтрокаУчастника, СтрокаЭталон,
			"ВариантУстановкиСрокаИсполнения, СрокИсполнения, СрокИсполненияДни,
			|СрокИсполненияЧасы, СрокИсполненияМинуты, СрокИсполненияПредставление");
		
	КонецЦикла; 
	
КонецПроцедуры

Функция ДобавитьСтрокуУчастникаЭтапаПодписания(СтрокаРодитель)

	СтрокаДерева = СтрокаРодитель.ПолучитьЭлементы().Добавить();
	СтрокаДерева.ТипСтроки = РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_УчастникЭтапаПодписания();
	СтрокаДерева.ИдентификаторУчастника = Новый УникальныйИдентификатор;
	СтрокаДерева.ИдентификаторЭтапа = СтрокаРодитель.ИдентификаторЭтапа;
	ОбновитьТочкуМаршрутаУчастникаПодписания(СтрокаДерева);
	
	Возврат СтрокаДерева;

КонецФункции

Процедура ЗаполнитьСрокиИсполненияВСтрокеУчастникаПодписания(СтрокаУчастника, ВремяВСроках)
	
	СтрокаРодитель = СтрокаУчастника.ПолучитьРодителя();
	Если СтрокаРодитель = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрокаРодитель.ПорядокВыполненияУчастниками =
		ПредопределенноеЗначение("Перечисление.ПорядокВыполненияЭтапаПроцесса.Последовательно") Тогда
	
		СтрокаУчастника.СрокИсполненияДни = 1;
		СтрокаУчастника.ВариантУстановкиСрокаИсполнения = ПредопределенноеЗначение(
			"Перечисление.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок");
		
		СтрокаУчастника.СрокИсполненияПредставление = 
			СрокиИсполненияПроцессовКлиентСервер.ПредставлениеСрокаИсполнения(
			СтрокаУчастника.СрокИсполнения,
			СтрокаУчастника.СрокИсполненияДни,
			СтрокаУчастника.СрокИсполненияЧасы,
			СтрокаУчастника.СрокИсполненияМинуты,
			ВремяВСроках,
			СтрокаУчастника.ВариантУстановкиСрокаИсполнения);	
		
	Иначе
		ПодчиненныеСтроки = СтрокаРодитель.ПолучитьЭлементы();
		Если ПодчиненныеСтроки.Количество() > 1 Тогда
			ЗаполнитьЗначенияСвойств(СтрокаУчастника, ПодчиненныеСтроки[0],
				"ВариантУстановкиСрокаИсполнения, СрокИсполнения, СрокИсполненияДни,
				|СрокИсполненияЧасы, СрокИсполненияМинуты, СрокИсполненияПредставление"); 
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НастроитьУчастникаПроцессаПодписанияНаБумаге(УчастникиПроцессаЭлемент,
ИменаПредметов = Неопределено, ДополнениеТипа = Неопределено)
	
	СтрокаУчастника = УчастникиПроцессаЭлемент.ТекущиеДанные;
	Если СтрокаУчастника = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработчикЗавершения = Новый ОписаниеОповещения(
		"ЗавершитьНастройкуУчастникаПроцессаПодписанияНаБумаге",
		ЭтотОбъект, УчастникиПроцессаЭлемент);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Участник", СтрокаУчастника.Участник); 
	ПараметрыФормы.Вставить("Подписывающий", СтрокаУчастника.Подписывающий);
	
	Если ИменаПредметов <> Неопределено Тогда
		СписокИменПредметов = Новый СписокЗначений();
		СписокИменПредметов.ЗагрузитьЗначения(ИменаПредметов);
		ПараметрыФормы.Вставить("ИменаПредметов", СписокИменПредметов);
	КонецЕсли;
	
	Если ДополнениеТипа <> Неопределено Тогда
		ПараметрыФормы.Вставить("ДополнениеТипа", ДополнениеТипа);
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.НастройкаУчастникаПроцессаПодписанияНаБумаге",
		ПараметрыФормы, УчастникиПроцессаЭлемент,,,, ОбработчикЗавершения);
	
КонецПроцедуры

Процедура ЗавершитьНастройкуУчастникаПроцессаПодписанияНаБумаге(
	НовыеПараметрыУчастника, УчастникиПроцессаЭлемент) Экспорт
	
	Если НовыеПараметрыУчастника = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаУчастника = УчастникиПроцессаЭлемент.ТекущиеДанные;
	Если СтрокаУчастника = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрокаУчастника.Участник <> НовыеПараметрыУчастника.Участник Тогда
		СтрокаУчастника.Участник = НовыеПараметрыУчастника.Участник;
	КонецЕсли; 
	
	Если СтрокаУчастника.Подписывающий <> НовыеПараметрыУчастника.Подписывающий Тогда
		СтрокаУчастника.Подписывающий = НовыеПараметрыУчастника.Подписывающий;
	КонецЕсли;
	
	ОбновитьТочкуМаршрутаУчастникаПодписания(СтрокаУчастника);
	
	РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьПредставлениеУчастникаПодписания(
		СтрокаУчастника);
	
	УчастникиПроцессаЭлемент.ЗакончитьРедактированиеСтроки(Ложь);
	
КонецПроцедуры

Процедура НормализоватьПорядокВыполненияЭтаповПоСпособуПодписания(
	УчастникиПроцесса, СпособПодписания)
	
	// Если подписание на бумаге, то у всех этапов должно быть последование выполнение участниками.
	
	Если СпособПодписания <> ПредопределенноеЗначение(
		"Перечисление.СпособыПодписанияПредметаПроцесса.НаБумаге") Тогда
		
		Возврат;
	КонецЕсли;
	
	ПоследовательноеВыполнение = ПредопределенноеЗначение(
		"Перечисление.ПорядокВыполненияЭтапаПроцесса.Последовательно");
	
	СтрокаДевераПодписать = СтрокаДевераУчастниковСЗаданиемПодписать(УчастникиПроцесса);
		
	Для Каждого СтрокаДерева Из СтрокаДевераПодписать.ПолучитьЭлементы() Цикл
		
		Если СтрокаДерева.ТипСтроки <> 
			РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ЭтапПодписания() Тогда
			
			Продолжить;
		КонецЕсли;
		
		Если СтрокаДерева.ПорядокВыполненияУчастниками = ПоследовательноеВыполнение Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаДерева.ПорядокВыполненияУчастниками = ПоследовательноеВыполнение;
		РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьПредставлениеСтрокиЭтапаПодписания(
			СтрокаДерева);
		
	КонецЦикла;
			
КонецПроцедуры

Процедура НормализоватьСоставУчастниковПоСпособуПодписания(УчастникиПроцесса, СпособПодписания)
	
	// Если подписание не на бумаге, то в процессе не должно быть обеспечивающих подписание.
	
	Если СпособПодписания = ПредопределенноеЗначение(
		"Перечисление.СпособыПодписанияПредметаПроцесса.НаБумаге") Тогда
		
		Возврат;
	КонецЕсли;
	
	СтрокиДляОбработки = Новый Массив;
	
	СтрокаДевераПодписать = СтрокаДевераУчастниковСЗаданиемПодписать(УчастникиПроцесса);
	СтрокиДляОбработки.Добавить(СтрокаДевераПодписать);
	
	ТипСтроки_ПодписатьСУчастником = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_ПодписатьСУчастником();
		
	ТипСтроки_УчастникЭтапаПодписания = 
		РаботаСБизнесПроцессамиКлиентСервер.ТипСтроки_УчастникЭтапаПодписания();
	
	Пока СтрокиДляОбработки.Количество() > 0 Цикл
		
		СтрокаДерева = СтрокиДляОбработки[0];
		
		Если СтрокаДерева.ТипСтроки = ТипСтроки_ПодписатьСУчастником
			Или СтрокаДерева.ТипСтроки = ТипСтроки_УчастникЭтапаПодписания Тогда
			
			Если СтрокаДерева.Участник <> СтрокаДерева.Подписывающий Тогда
				СтрокаДерева.Участник = СтрокаДерева.Подписывающий;
				ОбновитьТочкуМаршрутаУчастникаПодписания(СтрокаДерева);
			КонецЕсли;
			
			РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьПредставлениеУчастникаПодписания(
				СтрокаДерева);
			
		Иначе
			Для Каждого ПодчиненнаяСтрока Из СтрокаДерева.ПолучитьЭлементы() Цикл
				СтрокиДляОбработки.Добавить(ПодчиненнаяСтрока);
			КонецЦикла;
		КонецЕсли;
				
		СтрокиДляОбработки.Удалить(0);
				
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьТочкуМаршрутаУчастникаПодписания(СтрокаУчастника)
	
	Если СтрокаУчастника.Участник <> СтрокаУчастника.Подписывающий Тогда
		СтрокаУчастника.ТочкаМаршрута = ПредопределенноеЗначение(
			"БизнесПроцесс.Подписание.ТочкаМаршрута.ОбеспечитьПодписание");
	Иначе
		СтрокаУчастника.ТочкаМаршрута = ПредопределенноеЗначение(
			"БизнесПроцесс.Подписание.ТочкаМаршрута.Подписать");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти