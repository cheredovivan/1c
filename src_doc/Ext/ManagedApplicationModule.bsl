///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

// СтандартныеПодсистемы

// Хранилище глобальных переменных.
//
// ПараметрыПриложения - Соответствие из КлючИЗначение:
//   * Ключ - Строка - имя переменной в формате "ИмяБиблиотеки.ИмяПеременной";
//   * Значение - Произвольный - значение переменной.
//
// Пример инициализации:
//   ИмяПараметра = "СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации";
//   Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
//     ПараметрыПриложения.Вставить(ИмяПараметра, Новый СписокЗначений);
//   КонецЕсли;
//  
// Пример использования:
//   ПараметрыПриложения["СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации"].Добавить(...);
//   ПараметрыПриложения["СтандартныеПодсистемы.СообщенияДляЖурналаРегистрации"] = ...;
Перем ПараметрыПриложения Экспорт;
// Конец СтандартныеПодсистемы

Перем СотрудникиТекущегоПользователя Экспорт;
Перем РолиТекущегоПользователя Экспорт;
Перем ОсновнойСотрудник Экспорт;

// РаботаСФайлами
Перем КомпонентаПолученияКартинкиИзБуфера Экспорт; // компонента получения картинки из буфера обмена
// Конец РаботаСФайлами

// Почта
Перем ВыполненаПроверкаНаличияНесохраненныхПисем Экспорт; // Выполнен поиск на диске несохраненных писем
Перем НеУдалятьАвтосохраненныеФайлы Экспорт; // Если Истина, то при выходе не будут удаляться автосохраненные файлы
Перем ПараметрыПроверкиОрфографииТекст Экспорт;
// Почта

Перем КомпонентаРаспознаванияШтрихкодов Экспорт; // Компонента распознавания штрихкодов

// Переменные общего обработчика ожидания
Перем ДатаПоследнейПроверкиПочты Экспорт;
Перем ДатаПоследнегоСохраненияЗамеров Экспорт;
Перем ИнтервалПроверкиПочты Экспорт;
Перем ИнтервалСохраненияЗамеров Экспорт;
Перем ДатаПоследнейЗаписиЖурналаРегистрации Экспорт;
Перем ДатаПоследнейПроверкиОчисткиКешаФайлов Экспорт;
Перем ДатаПоследнегоАвтопомещенияФайлов Экспорт;

Перем ПараметрыФормыНастройкиОтложенногоСтарта Экспорт;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередНачаломРаботыСистемы()
	
#Если МобильныйКлиент Тогда
	Если ОсновнойСерверДоступен() = Ложь Тогда
		Возврат;
	КонецЕсли;
#КонецЕсли
	
	// СтандартныеПодсистемы
#Если МобильныйКлиент Тогда
	Выполнить("СтандартныеПодсистемыКлиент.ПередНачаломРаботыСистемы()");
#Иначе
	СтандартныеПодсистемыКлиент.ПередНачаломРаботыСистемы();
#КонецЕсли
	// Конец СтандартныеПодсистемы
	
КонецПроцедуры

Процедура ПриНачалеРаботыСистемы()
	
	// СтандартныеПодсистемы
#Если МобильныйКлиент Тогда
	Выполнить("СтандартныеПодсистемыКлиент.ПриНачалеРаботыСистемы()");
#Иначе
	СтандартныеПодсистемыКлиент.ПриНачалеРаботыСистемы();
#КонецЕсли
	// Конец СтандартныеПодсистемы
	
КонецПроцедуры

Процедура ПередЗавершениемРаботыСистемы(Отказ, ТекстПредупреждения)
	
	// СтандартныеПодсистемы
#Если МобильныйКлиент Тогда
	Выполнить("СтандартныеПодсистемыКлиент.ПередЗавершениемРаботыСистемы(Отказ, ТекстПредупреждения)");
#Иначе
	СтандартныеПодсистемыКлиент.ПередЗавершениемРаботыСистемы(Отказ, ТекстПредупреждения);
#КонецЕсли
	// Конец СтандартныеПодсистемы
	
КонецПроцедуры

Процедура ОбработкаПолученияФормыВыбораПользователейСистемыВзаимодействия(НазначениеВыбора,
			Форма, ИдентификаторОбсуждения, Параметры, ВыбраннаяФорма, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы
#Если МобильныйКлиент Тогда
	Выполнить("СтандартныеПодсистемыКлиент.ОбработкаПолученияФормыВыбораПользователейСистемыВзаимодействия(НазначениеВыбора,
		|Форма, ИдентификаторОбсуждения, Параметры, ВыбраннаяФорма, СтандартнаяОбработка)");
#Иначе
	СтандартныеПодсистемыКлиент.ОбработкаПолученияФормыВыбораПользователейСистемыВзаимодействия(НазначениеВыбора,
		Форма, ИдентификаторОбсуждения, Параметры, ВыбраннаяФорма, СтандартнаяОбработка);
#КонецЕсли
	// Конец СтандартныеПодсистемы
	
	ОбсужденияДокументооборотКлиент.ОбработкаПолученияФормыВыбораПользователейСистемыВзаимодействия(
			НазначениеВыбора, Форма, ИдентификаторОбсуждения, Параметры, ВыбраннаяФорма, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПриГлобальномПоиске(СтрокаПоиска, ПланПоиска)
	
		// СтандартныеПодсистемы
#Если МобильныйКлиент Тогда
	Выполнить("СтандартныеПодсистемыКлиент.ПриГлобальномПоиске(СтрокаПоиска, ПланПоиска)");
#Иначе
	СтандартныеПодсистемыКлиент.ПриГлобальномПоиске(СтрокаПоиска, ПланПоиска);
#КонецЕсли
	// Конец СтандартныеПодсистемы
	
	МодульПриложенияКлиентЛокализация.ПриГлобальномПоиске(СтрокаПоиска, ПланПоиска);
	
КонецПроцедуры

Процедура ПриВыбореРезультатаГлобальногоПоиска(ЭлементРезультата, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы
#Если МобильныйКлиент Тогда
	Выполнить("СтандартныеПодсистемыКлиент.ПриВыбореРезультатаГлобальногоПоиска(ЭлементРезультата,
		|СтандартнаяОбработка)");
#Иначе
	СтандартныеПодсистемыКлиент.ПриВыбореРезультатаГлобальногоПоиска(ЭлементРезультата,
		СтандартнаяОбработка);
#КонецЕсли
	// Конец СтандартныеПодсистемы
	
	МодульПриложенияКлиентЛокализация.ПриВыбореРезультатаГлобальногоПоиска(ЭлементРезультата, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПриВыбореДействияРезультатаГлобальногоПоиска(ЭлементРезультата, Действие)
	
	// СтандартныеПодсистемы
#Если МобильныйКлиент Тогда
	Выполнить("СтандартныеПодсистемыКлиент.ПриВыбореДействияРезультатаГлобальногоПоиска(ЭлементРезультата,
		|Действие)");
#Иначе
	СтандартныеПодсистемыКлиент.ПриВыбореДействияРезультатаГлобальногоПоиска(ЭлементРезультата, Действие);
#КонецЕсли
	// Конец СтандартныеПодсистемы
	
КонецПроцедуры

Процедура ОбработкаПереходаПоНавигационнойСсылке(ДанныеПереходаПоНавигационнойСсылке, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы
#Если МобильныйКлиент Тогда
	Выполнить("СтандартныеПодсистемыКлиент.ОбработкаПереходаПоНавигационнойСсылке(ДанныеПереходаПоНавигационнойСсылке,
		|СтандартнаяОбработка)");
#Иначе
	СтандартныеПодсистемыКлиент.ОбработкаПереходаПоНавигационнойСсылке(ДанныеПереходаПоНавигационнойСсылке,
		СтандартнаяОбработка);
#КонецЕсли
	// Конец СтандартныеПодсистемы
	
#Если Не ТолстыйКлиентУправляемоеПриложение Тогда
	СсылкаДляПерехода = ДанныеПереходаПоНавигационнойСсылке.ОтносительнаяНавигационнаяСсылка;
	БазоваяСсылкаДляПерехода = ДанныеПереходаПоНавигационнойСсылке.БазоваяНавигационнаяСсылка;
	ПараметрыСсылки = ДанныеПереходаПоНавигационнойСсылке.ПараметрыНавигационнойСсылки;
#Иначе
	СсылкаДляПерехода = "";
	БазоваяСсылкаДляПерехода = "";
	ПараметрыСсылки = Новый Соответствие;
#КонецЕсли
	
	ТипСсылки = РаботаС_HTMLКлиент.ОпределитьТипСсылки1СДокументооборота(СсылкаДляПерехода);
	ЭтоСсылкаНаОбъект = СтрНайти(СсылкаДляПерехода, "e1cib/data/") <> 0 И ПараметрыСсылки["ref"] <> Неопределено;
	
	Если ЭтоСсылкаНаОбъект И СтрНайти(СсылкаДляПерехода, "Документ.ДействиеЗадачи") <> 0 Тогда
		
		КарточкаОткрыта = РаботаСЗадачамиКлиент.ОткрытьКарточкуИсполнителяЗадачи(СсылкаДляПерехода);
		
		Если КарточкаОткрыта Тогда
			
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
	
	ИначеЕсли РаботаС_HTMLКлиент.СсылкуМожноОбработать(ТипСсылки, ПараметрыСсылки) Тогда
		
		СтандартнаяОбработка = Ложь;
		РаботаС_HTMLКлиент.ОбработатьСсылку1СДокументооборотаБезСхемы(СсылкаДляПерехода,,
			БазоваяСсылкаДляПерехода);
		
	ИначеЕсли МодульПриложенияКлиентЛокализация.ОбработкаПереходаПоНавигационнойСсылке(
		ДанныеПереходаПоНавигационнойСсылке, СтандартнаяОбработка, СсылкаДляПерехода, БазоваяСсылкаДляПерехода, ПараметрыСсылки) Тогда
		
		СтандартнаяОбработка = Ложь;
		
	ИначеЕсли ЭтоСсылкаНаОбъект И СтрНайти(СсылкаДляПерехода, "Справочник.ЗаписиРабочегоКалендаря") <> 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		ЗаписьРабочегоКалендаря = РаботаС_HTMLВызовСервера.СсылкаПоНавигационной(СсылкаДляПерехода);
		РаботаСРабочимКалендаремКлиент.ПоказатьЗаписьКалендаря(ЗаписьРабочегоКалендаря);
		
	ИначеЕсли ЭтоСсылкаНаОбъект И СтрНайти(СсылкаДляПерехода, "Справочник.ТемыОбсуждений") <> 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		ТемаФорума = РаботаС_HTMLВызовСервера.СсылкаПоНавигационной(СсылкаДляПерехода);
		РаботаСОбсуждениямиКлиент.ОткрытьТему(ТемаФорума);
		
	ИначеЕсли ЭтоСсылкаНаОбъект И СтрНайти(СсылкаДляПерехода, "Справочник.СообщенияОбсуждений") <> 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		СообщениеФорума = РаботаС_HTMLВызовСервера.СсылкаПоНавигационной(СсылкаДляПерехода);
		РаботаСОбсуждениямиКлиент.ОткрытьСообщение(СообщениеФорума);
		
	ИначеЕсли ЭтоСсылкаНаОбъект И СтрНайти(СсылкаДляПерехода, "Документ.Бронь") <> 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		Бронь = РаботаС_HTMLВызовСервера.СсылкаПоНавигационной(СсылкаДляПерехода);
		БронированиеПомещенийКлиент.ОткрытьБронь(Бронь);
		
	ИначеЕсли ЭтоСсылкаНаОбъект И СтрНайти(СсылкаДляПерехода, "Документ.Отсутствие") <> 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		Отсутствие = РаботаС_HTMLВызовСервера.СсылкаПоНавигационной(СсылкаДляПерехода);
		ОтсутствияКлиент.ОткрытьОтсутствие(Отсутствие);
		
	ИначеЕсли ЭтоСсылкаНаОбъект И СтрНайти(СсылкаДляПерехода, "Справочник.Мероприятия") <> 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		Мероприятие = РаботаС_HTMLВызовСервера.СсылкаПоНавигационной(СсылкаДляПерехода);
		УправлениеМероприятиямиКлиент.ОткрытьМероприятие(Мероприятие);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриВыбореДействияСообщенияСистемыВзаимодействия(Сообщение, Действие)
	
	РаботаСЧатБотомКлиент.ОбработкаДействияСообщения(Сообщение, Действие, Неопределено);
	
КонецПроцедуры

Процедура ОбработкаВнешнегоСобытия(Источник, Событие, Данные)
	
	ИнтеграцияПодсистемБПОКлиент.ОбработкаВнешнегоСобытия(Источник, Событие, Данные);
	
КонецПроцедуры

Процедура ПриЗавершенииРаботыСистемы()
	
	ВстроеннаяПочтаКлиент.УдалитьАвтосохраненныеФайлы();
	
КонецПроцедуры

Процедура АвтоПодборПользователейСистемыВзаимодействия(Текст, ДанныеВыбора, НазначениеВыбора, Форма, ИдентификаторОбсуждения)
	
	ОбсужденияДокументооборотКлиент.АвтоПодборПользователейСистемыВзаимодействия(
		Текст, ДанныеВыбора, НазначениеВыбора, Форма, ИдентификаторОбсуждения)
	
КонецПроцедуры

Процедура ОбработкаФормированияКомандВходящегоЗапросаПоделиться(ДанныеЗапросаПоделиться, Команды)

#Если МобильныйКлиент Тогда
	МК_Клиент.ДобавитьКомандыПоделиться(ДанныеЗапросаПоделиться, Команды);
#КонецЕсли

КонецПроцедуры

#КонецОбласти
