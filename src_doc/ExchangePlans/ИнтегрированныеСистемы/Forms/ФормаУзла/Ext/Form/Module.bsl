#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЗаписьИнтегрированнойСистемы");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АдресВебСервисаПриИзменении(Элемент)
	
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоддерживаетПравилаЗагрузкиДанныхВДОПриИзменении(Элемент)
	
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура АдресХостаВнешнихНавигационныхСсылок(Команда)
	
	ДанныеУзла = ДанныеУзла();
	ОповещениеОЗакрытии = Новый ОписаниеОповещения(
		"АдресХостаВнешнихНавигационныхСсылокПриИзменении",
		ЭтотОбъект,
		ДанныеУзла);
	ОткрытьФорму(
		"ПланОбмена.ИнтегрированныеСистемы.Форма.АдресХостаВнешнихНавигационныхСсылок",
		ДанныеУзла,,,,,
		ОповещениеОЗакрытии);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура АдресХостаВнешнихНавигационныхСсылокПриИзменении(Результат, ДанныеУзла) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Объект.АдресХостаВнешнихНавигационныхСсылок = Результат.АдресХостаВнешнихНавигационныхСсылок;
	Объект.ТипРасположенияХостаВнешнихНавигационныхСсылок = Результат.ТипРасположенияХостаВнешнихНавигационныхСсылок;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Функция ДанныеУзла()
	
	ДанныеУзла = Новый Структура;
	ДанныеУзла.Вставить("Наименование", Объект.Наименование);
	ДанныеУзла.Вставить("АдресВебСервиса", Объект.АдресВебСервиса);
	ДанныеУзла.Вставить("АдресХостаВнешнихНавигационныхСсылок", Объект.АдресХостаВнешнихНавигационныхСсылок);
	ДанныеУзла.Вставить("ТипРасположенияХостаВнешнихНавигационныхСсылок", Объект.ТипРасположенияХостаВнешнихНавигационныхСсылок);
	
	Возврат ДанныеУзла;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьЭлементов()
	
	Элементы.АдресХостаВнешнихНавигационныхСсылок.Видимость = Объект.ПоддерживаетПравилаЗагрузкиДанныхВДО
		И ЗначениеЗаполнено(Объект.АдресВебСервиса);
	
КонецПроцедуры

#КонецОбласти