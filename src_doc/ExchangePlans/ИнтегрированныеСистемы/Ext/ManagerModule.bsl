#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует ЭтотУзел при первоначальном заполнении ИБ.
//
Процедура ИнициализироватьПредопределенныйУзел() Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ИнтегрированныеСистемы.Ссылка КАК Ссылка
			|ИЗ
			|	ПланОбмена.ИнтегрированныеСистемы КАК ИнтегрированныеСистемы
			|ГДЕ
			|	ИнтегрированныеСистемы.Код = &КодЭтогоУзла
			|	И НЕ ИнтегрированныеСистемы.ЭтотУзел");
		Запрос.УстановитьПараметр("КодЭтогоУзла", КодЭтогоУзла());
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			УзелСНекорректнымКодом = Выборка.Ссылка.ПолучитьОбъект();
			УзелСНекорректнымКодом.Код = СледующийНомер();
			ПодготовитьУзелКЗаписи(УзелСНекорректнымКодом);
			УзелСНекорректнымКодом.Записать();
		КонецЕсли;
		
		ФиксированныйГУИД = "539c38a6-9e4a-4d51-ad18-5040b97d7348"; // Требуется для корректной синхронизации
		// узлов в редакции Холдинг.
		
		СтарыйЭтотУзелСсылка = ЭтотУзел();
		ТекущийКодЭтогоУзла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтарыйЭтотУзелСсылка, "Код");
		
		Если Строка(СтарыйЭтотУзелСсылка.УникальныйИдентификатор()) = ФиксированныйГУИД Тогда
			
			Если ТекущийКодЭтогоУзла <> КодЭтогоУзла() Тогда
				// Код предопределенного узла был установлен некорректно.
				ЭтотУзелОбъект = СтарыйЭтотУзелСсылка.ПолучитьОбъект();
				ЗаполнитьДанныеЭтогоУзла(ЭтотУзелОбъект);
				ПодготовитьУзелКЗаписи(ЭтотУзелОбъект);
				ЭтотУзелОбъект.Записать();
			КонецЕсли;
			
		Иначе
			
			Если ТекущийКодЭтогоУзла = КодЭтогоУзла() Тогда
				// Нужно изменить код для предотвращения коллизии.
				ЭтотУзелОбъект = СтарыйЭтотУзелСсылка.ПолучитьОбъект();
				ЭтотУзелОбъект.Код = СледующийНомер();
				ПодготовитьУзелКЗаписи(ЭтотУзелОбъект);
				ЭтотУзелОбъект.Записать();
			КонецЕсли;
			
			СсылкаНовыйЭтотУзел = ПолучитьСсылку(Новый УникальныйИдентификатор(ФиксированныйГУИД));
			
			Если ОбщегоНазначения.СсылкаСуществует(СсылкаНовыйЭтотУзел) Тогда
				НовыйЭтотУзел = СсылкаНовыйЭтотУзел.ПолучитьОбъект();
			Иначе
				НовыйЭтотУзел = СоздатьУзел();
				НовыйЭтотУзел.УстановитьСсылкуНового(СсылкаНовыйЭтотУзел);
			КонецЕсли;
			
			ЗаполнитьДанныеЭтогоУзла(НовыйЭтотУзел);
			ПодготовитьУзелКЗаписи(НовыйЭтотУзел);
			НовыйЭтотУзел.Записать();
			
			СтарыйЭтотУзел = СтарыйЭтотУзелСсылка.ПолучитьОбъект();
			СтарыйЭтотУзел.ЭтотУзел = Ложь;
			ПодготовитьУзелКЗаписи(СтарыйЭтотУзел);
			СтарыйЭтотУзел.Записать();
			СтарыйЭтотУзел.Удалить();
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Возвращает наименование предопределенного узла.
//
// Возвращаемое значение:
//   Строка
//
Функция НаименованиеЭтогоУзла() Экспорт
	
	Возврат НСтр("ru='1С:Документооборот'");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьДанныеЭтогоУзла(ЭтотУзелОбъект)
	
	ЭтотУзелОбъект.ЭтотУзел = Истина;
	ЭтотУзелОбъект.Код = КодЭтогоУзла();
	ЭтотУзелОбъект.Наименование = НаименованиеЭтогоУзла();
	
КонецПроцедуры

Функция КодЭтогоУзла()
	
	Возврат "001";
	
КонецФункции

Процедура ПодготовитьУзелКЗаписи(Узел)
	
	Узел.ДополнительныеСвойства.Вставить("ОтключитьСинхронизациюПользователей");
	Узел.ДополнительныеСвойства.Вставить("ОтключитьОтметкиВремени");
	Узел.ОбменДанными.Загрузка = Истина;
	
КонецПроцедуры

Функция СледующийНомер()
	
	МаксимальныйНомер = 1;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ИнтегрированныеСистемы.Код КАК Код
		|ИЗ
		|	ПланОбмена.ИнтегрированныеСистемы КАК ИнтегрированныеСистемы");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Не ПустаяСтрока(Выборка.Код) Тогда
			МаксимальныйНомер = Макс(МаксимальныйНомер, Число(Выборка.Код));
		КонецЕсли;
	КонецЦикла;
	
	Возврат Формат(МаксимальныйНомер + 1, "ЧЦ=3; ЧН=0; ЧВН=; ЧГ=0");
	
КонецФункции

#КонецОбласти

#КонецЕсли