#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбменДаннымиСервер.ФормаУзлаПриСозданииНаСервере(ЭтотОбъект, Отказ);
	
	ЭтоВариантНастройкиОбменаЗУП =
		(Объект.ВариантНастройки = ПланыОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.ВариантНастройкиОбменаЗУП());
	
	ИнициализироватьВариантыСинхронизации();
	ИнициализироватьСоответствиеРеквизитовМетаданным();
	
	ПравилоОтбораСправочников = ?(Объект.ИспользоватьОтборПоОрганизациям,
		"СинхронизироватьДанныеТолькоПоВыбраннымОрганизациям",
		"СинхронизироватьДанныеПоВсемОрганизациям");
	
	Если ОбщегоНазначения.РазделениеВключено() И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		Элементы.Наименование.Видимость = Ложь;
		Элементы.Служебные.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ВерсияФорматаОбмена.СписокВыбора.Очистить();
	ВерсииФормата = ОбменДаннымиСервер.ЗначениеНастройкиПланаОбмена(
		"СинхронизацияДанныхЧерезУниверсальныйФормат", "ВерсииФорматаОбмена");
	Для Каждого ВерсияФормата Из ВерсииФормата Цикл
		Элементы.ВерсияФорматаОбмена.СписокВыбора.Добавить(ВерсияФормата.Ключ);
	КонецЦикла;
	
	УстановитьДоступностьНаСервере();
	ОбновитьНаименованиеКомандФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Для Каждого ЭлементСоответствия Из СоответствиеРеквизитовМетаданным Цикл
		Если ЭлементСоответствия.Направление = НаправлениеЗагрузка() Тогда
			НоваяСтрока = НастройкиЗагрузки.Добавить();
			НоваяСтрока.Загружать = Истина;
			НоваяСтрока.ИмяМетаданных = ЭлементСоответствия.ИмяМетаданных;
			НоваяСтрока.Представление = ЭлементСоответствия.ПредставлениеРодительныйПадеж;
		КонецЕсли;
	КонецЦикла;
	Для Каждого Строка Из Объект.ЗапретЗагрузки Цикл
		Соответствие = НайтиСтроку(
			СоответствиеРеквизитовМетаданным,
			Строка.ПолноеИмяОбъектаМетаданных,
			"ИмяМетаданных",
			НаправлениеЗагрузка());
		Если Соответствие <> Неопределено Тогда
			УстановитьНастройкуЗагрузки(Соответствие.ИмяМетаданных, Ложь);
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьЗаголовокСтраницыПравилаОтправкиДанных();
	ОбновитьЗаголовокСтраницыПравилаПолученияДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ОбновитьДанныеОбъекта(ВыбранноеЗначение);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ФормаНастройкиПередЗакрытием(Отказ, ЭтотОбъект, ЗавершениеРаботы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	Оповестить("Запись_УзелПланаОбмена");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ОбменДаннымиКлиент.ПередЗаписью(ЭтотОбъект, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ЗапретЗагрузки.Очистить();
	Для Каждого ЭлементСоответствия Из СоответствиеРеквизитовМетаданным Цикл
		Если ЭлементСоответствия.Направление = НаправлениеЗагрузка()
				И НастройкаЗагрузки(НастройкиЗагрузки, ЭлементСоответствия.ИмяМетаданных) = Ложь Тогда
			НоваяСтрока = ТекущийОбъект.ЗапретЗагрузки.Добавить();
			НоваяСтрока.ПолноеИмяОбъектаМетаданных = ЭлементСоответствия.ИмяМетаданных;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ОбменДаннымиСервер.ФормаУзлаПриЗаписиНаСервере(ТекущийОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьИнтерфейс();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВыгружатьБанковскиеСчетаПриИзменении(Элемент)
	
	ОбновитьЗаголовокСтраницыПравилаОтправкиДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьДокументыФизЛицПриИзменении(Элемент)
	
	ОбновитьЗаголовокСтраницыПравилаОтправкиДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьДолжностиПриИзменении(Элемент)
	
	ОбновитьЗаголовокСтраницыПравилаОтправкиДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьКонтактныеЛицаПриИзменении(Элемент)
	
	ОбновитьЗаголовокСтраницыПравилаОтправкиДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьКонтрагентовПриИзменении(Элемент)
	
	ОбновитьЗаголовокСтраницыПравилаОтправкиДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьОрганизацииПриИзменении(Элемент)
	
	ОбновитьЗаголовокСтраницыПравилаОтправкиДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьСтатьиДДСПриИзменении(Элемент)
	
	ОбновитьЗаголовокСтраницыПравилаОтправкиДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьСтруктуруПредприятияПриИзменении(Элемент)
	
	ОбновитьЗаголовокСтраницыПравилаОтправкиДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьТоварыИУслугиПриИзменении(Элемент)
	
	ОбновитьЗаголовокСтраницыПравилаОтправкиДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательДокументыНеОтправлятьПриИзменении(Элемент)
	
	УстановитьДоступностьНаСервере();
	УстановитьРежимВыгрузкиДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательДокументыОтправлятьАвтоматическиПриИзменении(Элемент)
	
	УстановитьДоступностьНаСервере();
	УстановитьРежимВыгрузкиДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательДокументыОтправлятьВручнуюПриИзменении(Элемент)
	
	УстановитьДоступностьНаСервере();
	УстановитьРежимВыгрузкиДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательОтбораСправочниковБезОтбораПриИзменении(Элемент)
	
	УсловияОграниченияСинхронизации();
	УстановитьДоступностьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательОтбораСправочниковСОтборомПриИзменении(Элемент)
	
	УсловияОграниченияСинхронизации();
	УстановитьДоступностьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательОтправлятьНСИАвтоматическиПриИзменении(Элемент)
	
	УстановитьДоступностьНаСервере();
	УстановитьРежимВыгрузкиСправочников();
	
	Для Каждого ЭлементСоответствия Из СоответствиеРеквизитовМетаданным Цикл
		Если ЭлементСоответствия.Направление = НаправлениеВыгрузка() Тогда
			Объект[ЭлементСоответствия.ИмяРеквизита] = Истина;
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьЗаголовокСтраницыПравилаОтправкиДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательОтправлятьНСИНикогдаПриИзменении(Элемент)
	
	Объект.ПравилаОтправкиДокументов = ВариантСинхронизацииДокументовНеСинхронизировать;
	УстановитьДоступностьНаСервере();
	УстановитьРежимВыгрузкиСправочников();
	УстановитьРежимВыгрузкиДокументов();
	
	Для Каждого ЭлементСоответствия Из СоответствиеРеквизитовМетаданным Цикл
		Если ЭлементСоответствия.Направление = НаправлениеВыгрузка() Тогда
			Объект[ЭлементСоответствия.ИмяРеквизита] = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьЗаголовокСтраницыПравилаОтправкиДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательОтправлятьНСИПоНеобходимостиПриИзменении(Элемент)
	
	Если Объект.ПравилаОтправкиДокументов = ВариантСинхронизацииДокументовНеСинхронизировать Тогда
		Объект.ПравилаОтправкиДокументов = ВариантСинхронизацииДокументовАвтоматическаяСинхронизация;
	КонецЕсли;
	
	УстановитьДоступностьНаСервере();
	УстановитьРежимВыгрузкиСправочников();
	УстановитьРежимВыгрузкиДокументов();
	
	Для Каждого ЭлементСоответствия Из СоответствиеРеквизитовМетаданным Цикл
		Если ЭлементСоответствия.Направление = НаправлениеВыгрузка() Тогда
			Объект[ЭлементСоответствия.ИмяРеквизита] = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьЗаголовокСтраницыПравилаОтправкиДанных();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкиЗагрузки

&НаКлиенте
Процедура НастройкиЗагрузкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.НастройкиЗагрузки.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.Загружать = Не ТекущиеДанные.Загружать;
	КонецЕсли;
	
	НастройкиЗагрузкиПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗагрузкиЗагружатьПриИзменении(Элемент)
	
	НастройкиЗагрузкиПриИзменении();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагружатьВсе(Команда)
	
	Для Каждого ЭлементСоответствия Из СоответствиеРеквизитовМетаданным Цикл
		Если ЭлементСоответствия.Направление = НаправлениеЗагрузка()
				И ЭлементСоответствия.ИмяМетаданных <> "Справочник.Сотрудники" Тогда
			УстановитьНастройкуЗагрузки(ЭлементСоответствия.ИмяМетаданных, Истина);
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьЗаголовокСтраницыПравилаПолученияДанных();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура НеЗагружатьНичего(Команда)
	
	Для Каждого ЭлементСоответствия Из СоответствиеРеквизитовМетаданным Цикл
		Если ЭлементСоответствия.Направление = НаправлениеЗагрузка() Тогда
			УстановитьНастройкуЗагрузки(ЭлементСоответствия.ИмяМетаданных, Ложь);
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьЗаголовокСтраницыПравилаПолученияДанных();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокВыбранныхОрганизаций(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ИмяЭлементаФормыДляЗаполнения", "Организации");
	ПараметрыФормы.Вставить("ИмяРеквизитаЭлементаФормыДляЗаполнения", "Организация");
	ПараметрыФормы.Вставить("ИмяТаблицыВыбора", "Справочник.Организации");
	ПараметрыФормы.Вставить("ЗаголовокФормыВыбора", НСтр("ru = 'Выберите организации для отбора'"));
	ПараметрыФормы.Вставить("МассивВыбранныхЗначений", СформироватьМассивВыбранныхЗначений(ПараметрыФормы));
	ПараметрыФормы.Вставить("ПараметрыВнешнегоСоединения", Неопределено);
	ПараметрыФормы.Вставить("КоллекцияФильтров", Неопределено);
	
	ОткрытьФорму("ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.Форма.ФормаВыбораДополнительныхУсловий",
		ПараметрыФормы,
		ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Процедура ВключениеНастройкиЗагрузкиПослеПодтверждения(Знач Ответ, Знач ТекущиеДанные) Экспорт
	
	Если Ответ <> "Да" Тогда
		ТекущиеДанные.Загружать = Ложь;
		Возврат;
	КонецЕсли;
	
	ОбъектыДляВключенияЗагрузки = ОбъектыДляВключенияЗагрузки(ТекущиеДанные.ИмяМетаданных);
	Если ОбъектыДляВключенияЗагрузки.Метаданные.Количество() > 0 Тогда
		ТекстВопроса = СтрШаблон(
			НСтр("ru = 'Загружать %1 не получится без загрузки следующих объектов: %2.
				|Их загрузка будет включена. Продолжить?'"),
			НРег(ТекущиеДанные.Представление),
			СтрСоединить(ОбъектыДляВключенияЗагрузки.Представления, ", "));
		Ответ = Ждать ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНетАсинх(ТекстВопроса);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Для Каждого ИмяМетаданных Из ОбъектыДляВключенияЗагрузки.Метаданные Цикл
				УстановитьНастройкуЗагрузки(ИмяМетаданных, Истина);
			КонецЦикла;
		Иначе
			ТекущиеДанные.Загружать = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьЗаголовокСтраницыПравилаПолученияДанных();
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьВариантыСинхронизации()
	
	Менеджер = ПланыОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат;
	
	ВариантСинхронизацииДокументовАвтоматическаяСинхронизация =
		Менеджер.ВариантСинхронизацииДокументовАвтоматическаяСинхронизация();
	ВариантСинхронизацииДокументовИнтерактивнаяСинхронизация =
		Менеджер.ВариантСинхронизацииДокументовИнтерактивнаяСинхронизация();
	ВариантСинхронизацииДокументовНеСинхронизировать =
		Менеджер.ВариантСинхронизацииДокументовНеСинхронизировать();
	
	ВариантСинхронизацииСправочниковАвтоматическаяСинхронизация =
		Менеджер.ВариантСинхронизацииСправочниковАвтоматическаяСинхронизация();
	ВариантСинхронизацииСправочниковСинхронизироватьПоНеобходимости =
		Менеджер.ВариантСинхронизацииСправочниковСинхронизироватьПоНеобходимости();
	ВариантСинхронизацииСправочниковНеСинхронизировать =
		Менеджер.ВариантСинхронизацииСправочниковНеСинхронизировать();
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьСоответствиеРеквизитовМетаданным()
	
	// Выгрузка.
	НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
	НоваяСтрока.Направление = НаправлениеВыгрузка();
	НоваяСтрока.ИмяРеквизита = "ВыгружатьБанковскиеСчета";
	НоваяСтрока.ИмяМетаданных = "Справочник.БанковскиеСчета";
	НоваяСтрока.Представление = НСтр("ru = 'банковские счета'");
	НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Банковские счета'");
	
	НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
	НоваяСтрока.Направление = НаправлениеВыгрузка();
	НоваяСтрока.ИмяРеквизита = "ВыгружатьДокументыФизЛиц";
	НоваяСтрока.ИмяМетаданных = "РегистрСведений.ДокументыФизическихЛиц";
	НоваяСтрока.Представление = НСтр("ru = 'документы физ. лиц'");
	НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Документы физ. лиц'");
	
	НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
	НоваяСтрока.Направление = НаправлениеВыгрузка();
	НоваяСтрока.ИмяРеквизита = "ВыгружатьДолжности";
	НоваяСтрока.ИмяМетаданных = "Справочник.Должности";
	НоваяСтрока.Представление = НСтр("ru = 'должности'");
	НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Должности'");
	
	НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
	НоваяСтрока.Направление = НаправлениеВыгрузка();
	НоваяСтрока.ИмяРеквизита = "ВыгружатьКонтактныеЛица";
	НоваяСтрока.ИмяМетаданных = "Справочник.КонтактныеЛица";
	НоваяСтрока.Представление = НСтр("ru = 'контактные лица'");
	НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Контактные лица'");
	
	НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
	НоваяСтрока.Направление = НаправлениеВыгрузка();
	НоваяСтрока.ИмяРеквизита = "ВыгружатьКонтрагентов";
	НоваяСтрока.ИмяМетаданных = "Справочник.Контрагенты";
	НоваяСтрока.Представление = НСтр("ru = 'контрагенты'");
	НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Контрагентов'");
	
	НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
	НоваяСтрока.Направление = НаправлениеВыгрузка();
	НоваяСтрока.ИмяРеквизита = "ВыгружатьОрганизации";
	НоваяСтрока.ИмяМетаданных = "Справочник.Организации";
	НоваяСтрока.Представление = НСтр("ru = 'организации'");
	НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Организации'");
	
	НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
	НоваяСтрока.Направление = НаправлениеВыгрузка();
	НоваяСтрока.ИмяРеквизита = "ВыгружатьСтатьиДДС";
	НоваяСтрока.ИмяМетаданных = "Справочник.СтатьиДвиженияДенежныхСредств";
	НоваяСтрока.Представление = НСтр("ru = 'статьи ДДС'");
	НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Статьи ДДС'");
	
	НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
	НоваяСтрока.Направление = НаправлениеВыгрузка();
	НоваяСтрока.ИмяРеквизита = "ВыгружатьСтруктуруПредприятия";
	НоваяСтрока.ИмяМетаданных = "Справочник.СтруктураПредприятия";
	НоваяСтрока.Представление = НСтр("ru = 'структура предприятия'");
	НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Структуру предприятия'");
	
	НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
	НоваяСтрока.Направление = НаправлениеВыгрузка();
	НоваяСтрока.ИмяРеквизита = "ВыгружатьТоварыИУслуги";
	НоваяСтрока.ИмяМетаданных = "Справочник.Номенклатура";
	НоваяСтрока.Представление = НСтр("ru = 'товары и услуги'");
	НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Товары и услуги'");
	
	НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
	НоваяСтрока.Направление = НаправлениеВыгрузка();
	НоваяСтрока.ИмяРеквизита = "ВыгружатьФизическиеЛица";
	НоваяСтрока.ИмяМетаданных = "Справочник.ФизическиеЛица";
	НоваяСтрока.Представление = НСтр("ru = 'физические лица'");
	НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Физические лица'");
	
	// Загрузка.
	Если Не ЭтоВариантНастройкиОбменаЗУП Тогда
		НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
		НоваяСтрока.Направление = НаправлениеЗагрузка();
		НоваяСтрока.ИмяРеквизита = "ЗагружатьБанки";
		НоваяСтрока.ИмяМетаданных = "Справочник.КлассификаторБанков";
		НоваяСтрока.Представление = НСтр("ru = 'банки'");
		НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Банки'");
	КонецЕсли;
	
	Если Не ЭтоВариантНастройкиОбменаЗУП Тогда
		НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
		НоваяСтрока.Направление = НаправлениеЗагрузка();
		НоваяСтрока.ИмяРеквизита = "ЗагружатьБанковскиеСчета";
		НоваяСтрока.ИмяМетаданных = "Справочник.БанковскиеСчета";
		НоваяСтрока.Представление = НСтр("ru = 'банковские счета'");
		НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Банковские счета'");
	КонецЕсли;
	
	Если Не ЭтоВариантНастройкиОбменаЗУП Тогда
		НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
		НоваяСтрока.Направление = НаправлениеЗагрузка();
		НоваяСтрока.ИмяРеквизита = "ЗагружатьВалюты";
		НоваяСтрока.ИмяМетаданных = "Справочник.Валюты";
		НоваяСтрока.Представление = НСтр("ru = 'валюты'");
		НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Валюты'");
	КонецЕсли;
	
	НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
	НоваяСтрока.Направление = НаправлениеЗагрузка();
	НоваяСтрока.ИмяРеквизита = "ЗагружатьДокументыФизЛиц";
	НоваяСтрока.ИмяМетаданных = "РегистрСведений.ДокументыФизическихЛиц";
	НоваяСтрока.Представление = НСтр("ru = 'документы физ. лиц'");
	НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Документы физ. лиц'");
	
	НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
	НоваяСтрока.Направление = НаправлениеЗагрузка();
	НоваяСтрока.ИмяРеквизита = "ЗагружатьДолжности";
	НоваяСтрока.ИмяМетаданных = "Справочник.Должности";
	НоваяСтрока.Представление = НСтр("ru = 'должности'");
	НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Должности'");
	
	Если Не ЭтоВариантНастройкиОбменаЗУП Тогда
		НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
		НоваяСтрока.Направление = НаправлениеЗагрузка();
		НоваяСтрока.ИмяРеквизита = "ЗагружатьЕдиницыИзмерения";
		НоваяСтрока.ИмяМетаданных = "Справочник.КлассификаторЕдиницИзмерения";
		НоваяСтрока.Представление = НСтр("ru = 'единицы измерения'");
		НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Единицы измерения'");
	КонецЕсли;
	
	Если Не ЭтоВариантНастройкиОбменаЗУП Тогда
		НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
		НоваяСтрока.Направление = НаправлениеЗагрузка();
		НоваяСтрока.ИмяРеквизита = "ЗагружатьКонтактныеЛица";
		НоваяСтрока.ИмяМетаданных = "Справочник.КонтактныеЛица";
		НоваяСтрока.Представление = НСтр("ru = 'контактные лица'");
		НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Контактные лица'");
	КонецЕсли;
	
	Если Не ЭтоВариантНастройкиОбменаЗУП Тогда
		НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
		НоваяСтрока.Направление = НаправлениеЗагрузка();
		НоваяСтрока.ИмяРеквизита = "ЗагружатьКонтрагентов";
		НоваяСтрока.ИмяМетаданных = "Справочник.Контрагенты";
		НоваяСтрока.Представление = НСтр("ru = 'контрагенты'");
		НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Контрагентов'");
	КонецЕсли;
	
	НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
	НоваяСтрока.Направление = НаправлениеЗагрузка();
	НоваяСтрока.ИмяРеквизита = "ЗагружатьОрганизации";
	НоваяСтрока.ИмяМетаданных = "Справочник.Организации";
	НоваяСтрока.Представление = НСтр("ru = 'организации'");
	НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Организации'");
	
	Если Не ЭтоВариантНастройкиОбменаЗУП Тогда
		НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
		НоваяСтрока.Направление = НаправлениеЗагрузка();
		НоваяСтрока.ИмяРеквизита = "ЗагружатьПользователей";
		НоваяСтрока.ИмяМетаданных = "Справочник.Пользователи";
		НоваяСтрока.Представление = НСтр("ru = 'пользователи'");
		НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Пользователей'");
	КонецЕсли;
	
	НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
	НоваяСтрока.Направление = НаправлениеЗагрузка();
	НоваяСтрока.ИмяРеквизита = "ЗагружатьСотрудников";
	НоваяСтрока.ИмяМетаданных = "Справочник.Сотрудники";
	НоваяСтрока.Представление = НСтр("ru = 'сотрудники'");
	НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Сотрудников'");
	
	Если Не ЭтоВариантНастройкиОбменаЗУП Тогда
		НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
		НоваяСтрока.Направление = НаправлениеЗагрузка();
		НоваяСтрока.ИмяРеквизита = "ЗагружатьСтатьиДДС";
		НоваяСтрока.ИмяМетаданных = "Справочник.СтатьиДвиженияДенежныхСредств";
		НоваяСтрока.Представление = НСтр("ru = 'статьи ДДС'");
		НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Статьи ДДС'");
	КонецЕсли;
	
	Если Не ЭтоВариантНастройкиОбменаЗУП Тогда
		НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
		НоваяСтрока.Направление = НаправлениеЗагрузка();
		НоваяСтрока.ИмяРеквизита = "ЗагружатьСтраныМира";
		НоваяСтрока.ИмяМетаданных = "Справочник.СтраныМира";
		НоваяСтрока.Представление = НСтр("ru = 'страны мира'");
		НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Страны мира'");
	КонецЕсли;
	
	НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
	НоваяСтрока.Направление = НаправлениеЗагрузка();
	НоваяСтрока.ИмяРеквизита = "ЗагружатьСтруктуруПредприятия";
	НоваяСтрока.ИмяМетаданных = "Справочник.СтруктураПредприятия";
	НоваяСтрока.Представление = НСтр("ru = 'структура предприятия'");
	НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Структуру предприятия'");
	
	Если Не ЭтоВариантНастройкиОбменаЗУП Тогда
		НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
		НоваяСтрока.Направление = НаправлениеЗагрузка();
		НоваяСтрока.ИмяРеквизита = "ЗагружатьТоварыИУслуги";
		НоваяСтрока.ИмяМетаданных = "Справочник.Номенклатура";
		НоваяСтрока.Представление = НСтр("ru = 'товары и услуги'");
		НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Товары и услуги'");
	КонецЕсли;
	
	НоваяСтрока = СоответствиеРеквизитовМетаданным.Добавить();
	НоваяСтрока.Направление = НаправлениеЗагрузка();
	НоваяСтрока.ИмяРеквизита = "ЗагружатьФизическиеЛица";
	НоваяСтрока.ИмяМетаданных = "Справочник.ФизическиеЛица";
	НоваяСтрока.Представление = НСтр("ru = 'физические лица'");
	НоваяСтрока.ПредставлениеРодительныйПадеж = НСтр("ru = 'Физические лица'");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НайтиСтроку(Таблица, Значение, ИмяКолонки, Направление)
	
	Строки = Таблица.НайтиСтроки(Новый Структура(СтрШаблон("%1, Направление", ИмяКолонки), Значение, Направление));
	Если Строки.Количество() = 1 Тогда
		Возврат Строки[0];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НаправлениеВыгрузка()
	
	Возврат "Выгрузка";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НаправлениеЗагрузка()
	
	Возврат "Загрузка";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НастройкаЗагрузки(Таблица, ИмяМетаданных)
	
	СтрокиНастройки = Таблица.НайтиСтроки(Новый Структура("ИмяМетаданных", ИмяМетаданных));
	
	Возврат СтрокиНастройки[0].Загружать;
	
КонецФункции

&НаКлиенте
Асинх Процедура НастройкиЗагрузкиПриИзменении()
	
	ТекущиеДанные = Элементы.НастройкиЗагрузки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Загружать = Ложь Тогда
		
		ОбъектыДляВыключенияЗагрузки = ОбъектыДляВыключенияЗагрузки(ТекущиеДанные.ИмяМетаданных);
		Если ОбъектыДляВыключенияЗагрузки.Метаданные.Количество() > 0 Тогда
			ТекстВопроса = СтрШаблон(
				НСтр("ru = 'Если не загружать %1, то не получится загрузить следующие объекты: %2.
					|Их загрузка будет выключена. Продолжить?'"),
				НРег(ТекущиеДанные.Представление),
				СтрСоединить(ОбъектыДляВыключенияЗагрузки.Представления, ", "));
			Ответ = Ждать ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНетАсинх(ТекстВопроса);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				Для Каждого ИмяМетаданных Из ОбъектыДляВыключенияЗагрузки.Метаданные Цикл
					УстановитьНастройкуЗагрузки(ИмяМетаданных, Ложь);
				КонецЦикла;
			Иначе
				ТекущиеДанные.Загружать = Истина;
			КонецЕсли;
		КонецЕсли;
		
		ОбновитьЗаголовокСтраницыПравилаПолученияДанных();
		
	Иначе
		
		ОписаниеОповещенияОПодтверждении = Новый ОписаниеОповещения(
			"ВключениеНастройкиЗагрузкиПослеПодтверждения", ЭтотОбъект, ТекущиеДанные);
		
		Если ТекущиеДанные.ИмяМетаданных = "Справочник.Сотрудники" Тогда
			ТекстВопроса = СтрШаблон(
				НСтр("ru = 'Для сопоставления подразделений сотрудников регламентированные подразделения организации из базы %1 будут загружены в управленческую структуру предприятия базы 1С:Документооборота.
					|
					|Если регламентированная и управленческая структура предприятия в вашей организации не совпадают - включать данную настройку не рекомендуется. Продолжить?'"),
				Объект.Наименование);
			ВариантыОтвета = Новый СписокЗначений;
			ВариантыОтвета.Добавить("Да", НСтр("ru = 'Продолжить'"));
			ВариантыОтвета.Добавить("Отмена", НСтр("ru = 'Отмена'"));
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Загрузка сотрудников'"));
			ПараметрыФормы.Вставить("ТекстВопроса", ТекстВопроса);
			ПараметрыФормы.Вставить("СписокДоступныхВариантов", ВариантыОтвета);
			ПараметрыФормы.Вставить("ВариантОтветаПоУмолчанию", "Отмена");
			ПараметрыФормы.Вставить("ЗадержкаВремени", Новый Структура("Да", 9));
			ПараметрыФормы.Вставить("ВысотаТекста", 10);
			
			ОткрытьФорму("ОбщаяФорма.РасширенныйВопрос", ПараметрыФормы,,,,,
				ОписаниеОповещенияОПодтверждении, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Иначе
			ВыполнитьОбработкуОповещения(ОписаниеОповещенияОПодтверждении, "Да");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеОбъекта(Знач СтруктураПараметров)
	
	Объект[СтруктураПараметров.ИмяТаблицыДляЗаполнения].Очистить();
	
	СписокВыбранныхЗначений = ПолучитьИзВременногоХранилища(
		СтруктураПараметров.АдресТаблицыВоВременномХранилище); // ТаблицаЗначений
	
	Если СписокВыбранныхЗначений.Количество() > 0 Тогда
		КолонкаПредставление = СписокВыбранныхЗначений.Колонки.Представление; // КолонкаТаблицыЗначений
		КолонкаПредставление.Имя = СтруктураПараметров.ИмяКолонкиДляЗаполнения;
		Объект[СтруктураПараметров.ИмяТаблицыДляЗаполнения].Загрузить(СписокВыбранныхЗначений);
	КонецЕсли;
	
	ОбновитьНаименованиеКомандФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовокСтраницыПравилаОтправкиДанных()
	
	ВсегоСтрок = 0;
	Выгружается = 0;
	
	Для Каждого ЭлементСоответствия Из СоответствиеРеквизитовМетаданным Цикл
		Если ЭлементСоответствия.Направление = НаправлениеВыгрузка() Тогда
			ВсегоСтрок = ВсегоСтрок + 1;
			Если Объект[ЭлементСоответствия.ИмяРеквизита] = Истина Тогда
				Выгружается = Выгружается + 1;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ПравилаОтправкиДанных.Заголовок = СтрШаблон(Нстр("ru = 'Правила отправки данных (выгружать %1 из %2)'"),
		Выгружается, ВсегоСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовокСтраницыПравилаПолученияДанных()
	
	ВсегоСтрок = НастройкиЗагрузки.Количество();
	Загружается = 0;
	
	Для Каждого Строка Из НастройкиЗагрузки Цикл
		Если Строка.Загружать Тогда
			Загружается = Загружается + 1;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ПравилаПолученияДанных.Заголовок = СтрШаблон(Нстр("ru = 'Правила получения данных (загружать %1 из %2)'"),
		Загружается, ВсегоСтрок);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаименованиеКомандФормы()
	
	// Обновим заголовок выбранных организаций.
	Если Объект.Организации.Количество() > 0 Тогда
		
		ВыбранныеОрганизации = Объект.Организации.Выгрузить().ВыгрузитьКолонку("Организация");
		НовыйЗаголовокОрганизаций = СтрСоединить(ВыбранныеОрганизации, ",");
		
	Иначе
		
		НовыйЗаголовокОрганизаций = НСтр("ru = 'Выбрать организации'");
		
	КонецЕсли;
	
	Элементы.ОткрытьСписокВыбранныхОрганизаций.Заголовок = НовыйЗаголовокОрганизаций;
	
КонецПроцедуры

&НаСервере
Функция ОбъектыДляВключенияЗагрузки(Знач ИмяМетаданных)
	
	Объекты = Новый Структура;
	Объекты.Вставить("Метаданные", Новый Массив);
	Объекты.Вставить("Представления", Новый Массив);
	
	СоответствиеИсходного = НайтиСтроку(
		СоответствиеРеквизитовМетаданным,
		ИмяМетаданных,
		"ИмяМетаданных",
		НаправлениеЗагрузка());
	ОтКогоЗависит = ПланыОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.ОтКогоЗависит(
		СоответствиеИсходного.ИмяМетаданных);
	Для Каждого ЗависимыйОбъект Из ОтКогоЗависит Цикл
		СоответствиеЗависимого = НайтиСтроку(
			СоответствиеРеквизитовМетаданным,
			ЗависимыйОбъект,
			"ИмяМетаданных",
			НаправлениеЗагрузка());
		Если СоответствиеЗависимого <> Неопределено
				И НастройкаЗагрузки(НастройкиЗагрузки, СоответствиеЗависимого.ИмяМетаданных) = Ложь Тогда
			Объекты.Метаданные.Добавить(СоответствиеЗависимого.ИмяМетаданных);
			Объекты.Представления.Добавить(СоответствиеЗависимого.Представление);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Объекты;
	
КонецФункции

&НаСервере
Функция ОбъектыДляВыключенияЗагрузки(Знач ИмяМетаданных)
	
	Объекты = Новый Структура;
	Объекты.Вставить("Метаданные", Новый Массив);
	Объекты.Вставить("Представления", Новый Массив);
	
	СоответствиеИсходного = НайтиСтроку(
		СоответствиеРеквизитовМетаданным,
		ИмяМетаданных,
		"ИмяМетаданных",
		НаправлениеЗагрузка());
	КтоЗависит = ПланыОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.КтоЗависит(
		СоответствиеИсходного.ИмяМетаданных);
	Для Каждого ЗависимыйОбъект Из КтоЗависит Цикл
		СоответствиеЗависимого = НайтиСтроку(
			СоответствиеРеквизитовМетаданным,
			ЗависимыйОбъект,
			"ИмяМетаданных",
			НаправлениеЗагрузка());
		Если СоответствиеЗависимого <> Неопределено
				И НастройкаЗагрузки(НастройкиЗагрузки, СоответствиеЗависимого.ИмяМетаданных) = Истина Тогда
			Объекты.Метаданные.Добавить(СоответствиеЗависимого.ИмяМетаданных);
			Объекты.Представления.Добавить(СоответствиеЗависимого.Представление);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Объекты;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РежимыВыгрузкиВыгружатьВручную()
	
	Возврат ПредопределенноеЗначение("Перечисление.РежимыВыгрузкиОбъектовОбмена.ВыгружатьВручную");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РежимыВыгрузкиВыгружатьПоУсловию()
	
	Возврат ПредопределенноеЗначение("Перечисление.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПоУсловию");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РежимыВыгрузкиВыгружатьПриНеобходимости()
	
	Возврат ПредопределенноеЗначение("Перечисление.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПриНеобходимости");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РежимыВыгрузкиНеВыгружать()
	
	Возврат ПредопределенноеЗначение("Перечисление.РежимыВыгрузкиОбъектовОбмена.НеВыгружать");
	
КонецФункции

&НаСервере
Функция СформироватьМассивВыбранныхЗначений(ПараметрыФормы)
	
	ТабличнаяЧасть = Объект[ПараметрыФормы.ИмяЭлементаФормыДляЗаполнения];
	
	ИмяРеквизита = ПараметрыФормы.ИмяРеквизитаЭлементаФормыДляЗаполнения;
	ТаблицаВыбранныхЗначений = ТабличнаяЧасть.Выгрузить(, ИмяРеквизита);
	
	Возврат ТаблицаВыбранныхЗначений.ВыгрузитьКолонку(ИмяРеквизита);
	
КонецФункции

&НаКлиенте
Процедура УсловияОграниченияСинхронизации()
	
	Объект.ИспользоватьОтборПоОрганизациям =
		(ПравилоОтбораСправочников = "СинхронизироватьДанныеТолькоПоВыбраннымОрганизациям");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьНаСервере()
	
	Если ЭтоВариантНастройкиОбменаЗУП Тогда
		
		Элементы.ГруппаРежимОтправкиНСИ.Доступность = Ложь;
		Элементы.ГруппаСоставПрочихНастроек.Доступность = Ложь;
		Элементы.ГруппаНастройкаОтправкиДокументов.Доступность = Ложь;
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДатаНачалаВыгрузкиДокументов",
			"Доступность",
			Объект.ПравилаОтправкиДокументов = ВариантСинхронизацииДокументовАвтоматическаяСинхронизация);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ПереключательДокументыНеОтправлять",
			"Доступность",
			Объект.ПравилаОтправкиСправочников <> ВариантСинхронизацииСправочниковСинхронизироватьПоНеобходимости);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОписаниеДокументыНеОтправлять",
			"Доступность",
			Объект.ПравилаОтправкиСправочников <> ВариантСинхронизацииСправочниковСинхронизироватьПоНеобходимости);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы.ГруппаДокументы.ПодчиненныеЭлементы,
			"ГруппаРежимОтправкиДокументов",
			"Доступность",
			Объект.ПравилаОтправкиСправочников <> ВариантСинхронизацииСправочниковНеСинхронизировать);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОткрытьСписокВыбранныхОрганизаций",
			"Доступность",
			Объект.ИспользоватьОтборПоОрганизациям);
		
		Для Каждого ЭлементСоответствия Из СоответствиеРеквизитовМетаданным Цикл
			Если ЭлементСоответствия.Направление = НаправлениеВыгрузка() Тогда
				Элементы[ЭлементСоответствия.ИмяРеквизита].Доступность =
					(Объект.ПравилаОтправкиСправочников = ВариантСинхронизацииСправочниковАвтоматическаяСинхронизация);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНастройкуЗагрузки(ИмяМетаданных, Загружать)
	
	СтрокиНастройки = НастройкиЗагрузки.НайтиСтроки(Новый Структура("ИмяМетаданных", ИмяМетаданных));
	СтрокиНастройки[0].Загружать = Загружать;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьРежимВыгрузкиДокументов()
	
	Если Объект.ПравилаОтправкиДокументов = ВариантСинхронизацииДокументовНеСинхронизировать Тогда
		Объект.РежимВыгрузкиДокументов = РежимыВыгрузкиНеВыгружать();
	ИначеЕсли Объект.ПравилаОтправкиДокументов = ВариантСинхронизацииДокументовИнтерактивнаяСинхронизация Тогда
		Объект.РежимВыгрузкиДокументов = РежимыВыгрузкиВыгружатьВручную();
	Иначе
		Объект.РежимВыгрузкиДокументов = РежимыВыгрузкиВыгружатьПоУсловию();
	КонецЕсли;
	
	Если Не Объект.ИспользоватьОтборПоОрганизациям И Объект.Организации.Количество() <> 0 Тогда
		Объект.Организации.Очистить();
	ИначеЕсли Объект.Организации.Количество() = 0 И Объект.ИспользоватьОтборПоОрганизациям Тогда
		Объект.ИспользоватьОтборПоОрганизациям = Ложь;
	КонецЕсли;
	
	Если Объект.ПравилаОтправкиДокументов <> ВариантСинхронизацииДокументовАвтоматическаяСинхронизация Тогда
		Объект.ДатаНачалаВыгрузкиДокументов = ОбщегоНазначенияДокументооборотКлиентСервер.ПустаяДата();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьРежимВыгрузкиСправочников()
	
	// Очистка неиспользуемых реквизитов и заполнение служебных.
	Если Объект.ПравилаОтправкиСправочников = ВариантСинхронизацииСправочниковНеСинхронизировать Тогда
		Объект.ИспользоватьОтборПоОрганизациям = Ложь;
		Объект.РежимВыгрузкиСправочников = РежимыВыгрузкиНеВыгружать();
		Объект.РежимВыгрузкиПриНеобходимости = РежимыВыгрузкиНеВыгружать();
	ИначеЕсли Объект.ПравилаОтправкиСправочников = ВариантСинхронизацииСправочниковСинхронизироватьПоНеобходимости Тогда
		Объект.РежимВыгрузкиСправочников = РежимыВыгрузкиВыгружатьПриНеобходимости();
		Объект.РежимВыгрузкиПриНеобходимости = РежимыВыгрузкиВыгружатьПриНеобходимости();
	Иначе
		Объект.РежимВыгрузкиСправочников = РежимыВыгрузкиВыгружатьПоУсловию();
		Объект.РежимВыгрузкиПриНеобходимости = РежимыВыгрузкиВыгружатьПриНеобходимости();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти