///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ОбменДанными

// Заполняет настройки, влияющие на использование плана обмена.
//
// Параметры:
//   Настройки - см. ОбменДаннымиСервер.НастройкиПланаОбменаПоУмолчанию
//
Процедура ПриПолученииНастроек(Настройки) Экспорт
	
	Настройки.ИмяКонфигурацииИсточника = "Документооборот";
	
	Настройки.ЭтоПланОбменаXDTO = Истина;
	Настройки.ФорматОбмена = "http://v8.1c.ru/edi/edi_stnd/EnterpriseData";
	
	ВерсииФормата = Новый Соответствие;
	ОбменДаннымиПереопределяемый.ПриПолученииДоступныхВерсийФормата(ВерсииФормата);
	Настройки.ВерсииФорматаОбмена = ВерсииФормата;
	
	Настройки.ПланОбменаИспользуетсяВМоделиСервиса = Истина;
	
	Настройки.Алгоритмы.ПриПолученииВариантовНастроекОбмена = Истина;
	Настройки.Алгоритмы.ПриПолученииОписанияВариантаНастройки = Истина;
	Настройки.Алгоритмы.НастроитьИнтерактивнуюВыгрузку = Истина;
	
	Настройки.Глобальный = Истина;
	
КонецПроцедуры

// Заполняет коллекцию вариантов настроек, предусмотренных для плана обмена.
//
// Параметры:
//   ВариантыНастроекОбмена - см. ОбменДаннымиСервер.КоллекцияВариантовНастроекОбмена
//   ПараметрыКонтекста - см. ОбменДаннымиСервер.ПараметрыКонтекстаПолученияВариантовНастроек
//
Процедура ПриПолученииВариантовНастроекОбмена(ВариантыНастроекОбмена, ПараметрыКонтекста) Экспорт
	
	ВариантНастройки = ВариантыНастроекОбмена.Добавить();
	ВариантНастройки.ИдентификаторНастройки = ВариантНастройкиОбменаУП();
	ВариантНастройки.КорреспондентВМоделиСервиса = Истина;
	ВариантНастройки.КорреспондентВЛокальномРежиме = Истина;
	
	ВариантНастройки = ВариантыНастроекОбмена.Добавить();
	ВариантНастройки.ИдентификаторНастройки = ВариантНастройкиОбменаБП();
	ВариантНастройки.КорреспондентВМоделиСервиса = Истина;
	ВариантНастройки.КорреспондентВЛокальномРежиме = Истина;
	
	ВариантНастройки = ВариантыНастроекОбмена.Добавить();
	ВариантНастройки.ИдентификаторНастройки = ВариантНастройкиОбменаЗУП();
	ВариантНастройки.КорреспондентВМоделиСервиса = Истина;
	ВариантНастройки.КорреспондентВЛокальномРежиме = Истина;
	
	ВариантНастройки = ВариантыНастроекОбмена.Добавить();
	ВариантНастройки.ИдентификаторНастройки = ВариантНастройкиОбменУниверсальный();
	ВариантНастройки.КорреспондентВМоделиСервиса = Истина;
	ВариантНастройки.КорреспондентВЛокальномРежиме = Истина;
	
КонецПроцедуры

// Заполняет набор параметров, определяющих вариант настройки обмена.
//
// Параметры:
//   ОписаниеВарианта - см. ОбменДаннымиСервер.ОписаниеВариантаНастройкиОбменаПоУмолчанию
//   ИдентификаторНастройки - Строка - идентификатор варианта настройки обмена.
//   ПараметрыКонтекста - см. ОбменДаннымиСервер.ПараметрыКонтекстаПолученияОписанияВариантаНастройки
//
Процедура ПриПолученииОписанияВариантаНастройки(ОписаниеВарианта, ИдентификаторНастройки, ПараметрыКонтекста) Экспорт
	
	ИмяКонфигурацииКорреспондента = "";
	НаименованиеКонфигурацииКорреспондента = "";
	КраткаяИнформацияПоОбмену = "";
	ЗаголовокКоманды = "";
	ЗаголовокПомощника = "";
	ЗаголовокУзла = "";
	
	Если ИдентификаторНастройки = ВариантНастройкиОбменаУП() Тогда
		
		ИмяКонфигурацииКорреспондента = НСтр("ru = 'УправлениеПредприятием'");
		НаименованиеКонфигурацииКорреспондента = НСтр("ru = '1С:ERP Управление предприятием 2'");
		
		КраткаяИнформацияПоОбмену = НСтр("ru = 'Данная настройка позволит синхронизировать данные с
			|""1С:ERP.Управление холдингом 3"" / ""1С:ERP Управление предприятием 2"" / ""Комплексная автоматизация, редакция 2"" / ""Управление торговлей, редакция 11"".
			|В синхронизации участвуют документы и нормативно-справочная информация.'");
		
		ЗаголовокКоманды = НСтр("ru = '1С:ERP Управление предприятием 2'");
		ЗаголовокПомощника = НСтр("ru = 'Настройка синхронизации с программой ""1С:ERP Управление предприятием 2""'");
		ЗаголовокУзла = НСтр("ru = 'Синхронизация с программой ""1С:ERP Управление предприятием 2""'");
		
	ИначеЕсли ИдентификаторНастройки = ВариантНастройкиОбменаБП() Тогда
		
		ИмяКонфигурацииКорреспондента = НСтр("ru = 'БухгалтерияПредприятия'");
		НаименованиеКонфигурацииКорреспондента = НСтр("ru = 'Бухгалтерия предприятия, редакция 3'");
		
		КраткаяИнформацияПоОбмену = НСтр("ru = 'Данная настройка позволит синхронизировать данные с
			|""1С:Управление холдингом 3"" / ""Бухгалтерия предприятия, редакция 3"".
			|В синхронизации участвуют документы и нормативно-справочная информация.'");
		
		ЗаголовокКоманды = НСтр("ru='Бухгалтерия предприятия, редакция 3'");
		ЗаголовокПомощника = НСтр("ru='Настройка синхронизации с программой ""Бухгалтерия предприятия, редакция 3""'");
		ЗаголовокУзла = НСтр("ru='Синхронизация с программой ""Бухгалтерия предприятия, редакция 3""'");
		
	ИначеЕсли ИдентификаторНастройки = ВариантНастройкиОбменаЗУП() Тогда
		
		ИмяКонфигурацииКорреспондента = НСтр("ru = 'ЗарплатаИУправлениеПерсоналом'");
		НаименованиеКонфигурацииКорреспондента = НСтр("ru = 'Зарплата и Управление Персоналом, редакция 3'");
		
		КраткаяИнформацияПоОбмену = НСтр("ru = 'Данная настройка позволит синхронизировать данные с
			|""Зарплата и Управление Персоналом, редакция 3"" / ""Зарплата и кадры государственного учреждения базовая, редакция 3"".
			|В синхронизации участвуют документы и нормативно-справочная информация.'");
		
		ЗаголовокКоманды = НСтр("ru='Зарплата и Управление Персоналом, редакция 3'");
		ЗаголовокПомощника = НСтр("ru='Настройка синхронизации с программой ""Зарплата и Управление Персоналом, редакция 3""'");
		ЗаголовокУзла = НСтр("ru='Синхронизация с программой ""Зарплата и Управление Персоналом, редакция 3""'");
		
	Иначе
		
		// Другая программа.
		ИмяКонфигурацииКорреспондента = "ДругаяПрограмма";
		НаименованиеКонфигурацииКорреспондента = НСтр("ru = 'Другая программа'");
		
		КраткаяИнформацияПоОбмену =
			НСтр("ru = 'Позволяет синхронизировать данные с любой программой, поддерживающей универсальный формат обмена ""Enterprise Data"".'");
		
		ЗаголовокКоманды = НСтр("ru='Другая программа'");
		ЗаголовокПомощника = НСтр("ru='Настройка синхронизации данных через универсальный формат'");
		ЗаголовокУзла = НСтр("ru='Синхронизация данных через универсальный формат'");
		
	КонецЕсли;
	
	ОписаниеВарианта.ПоддерживаетсяСопоставлениеДанных = Истина;
	
	ОписаниеВарианта.КраткаяИнформацияПоОбмену = КраткаяИнформацияПоОбмену;
	ОписаниеВарианта.ПодробнаяИнформацияПоОбмену =
		"ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.Форма.ПодробнаяИнформация";
	
	ОписаниеВарианта.ИмяКонфигурацииКорреспондента = ИмяКонфигурацииКорреспондента;
	ОписаниеВарианта.НаименованиеКонфигурацииКорреспондента = НаименованиеКонфигурацииКорреспондента;
	ОписаниеВарианта.ИмяФайлаНастроекДляПриемника = НСтр("ru = 'Синхронизация данных через универсальный формат'");
	
	ОписаниеВарианта.ЗаголовокКомандыДляСозданияНовогоОбменаДанными = ЗаголовокКоманды;
	ОписаниеВарианта.ЗаголовокПомощникаСозданияОбмена = ЗаголовокПомощника;
	ОписаниеВарианта.ЗаголовокУзлаПланаОбмена = ЗаголовокУзла;
	
КонецПроцедуры

// Предназначена для настройки вариантов интерактивной настройки выгрузки по сценарию узла.
// Для настройки необходимо установить значения свойств параметров в необходимые значения.
//
// Параметры:
//   Получатель - ПланОбменаСсылка - узел, для которого производится настройка.
//   Параметры  - см. ОбменДаннымиСервер.ОписаниеСтандартныхВариантовДополненияВыгрузки
//
Процедура НастроитьИнтерактивнуюВыгрузку(Получатель, Параметры) Экспорт
	
	Параметры.ВариантВсеДокументы.Использование = Ложь;
	Параметры.ВариантБезДополнения.Использование = Ложь;
	Параметры.ВариантПроизвольныйОтбор.Использование = Ложь;
	Параметры.ВариантДополнительно.Использование = Ложь;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ОбменДанными

#КонецОбласти

#Область ПереходНаУниверсальныйФормат

// Вызывается при первом обмене по настройке синхронизации данных через универсальный формат
// через COM-соединение.
//
// Параметры:
//   ПараметрыНастройкиСинхронизацииДанных - Структура - сведения о настройке синхронизации, с которой происходит переход:
//     * Код - Строка - Код настройки.
//     * ВариантНастройки - Строка - вариант настройки синхронизации данных через универсальный формат базы-корреспондента.
//     * Ошибка - Булево - признак ошибки при выполнении функции.
//     * СообщениеОбОшибке - Строка - текст сообщения об ошибке.
//
// Возвращаемое значение:
//   ПланОбменаСсылка.СинхронизацияДанныхЧерезУниверсальныйФормат - ссылка на настройку синхронизации данных
//     через универсальный формат (при успешном выполнении перехода),
//   Неопределено - в случае критичных ошибок, возникших в процессе перехода.
//
Функция ПереходНаСинхронизациюЧерезУниверсальныйФорматВнешнееСоединение(ПараметрыНастройкиСинхронизацииДанных) Экспорт
	
	
	
КонецФункции

// Функция-обертка, выполняет подготовку параметров и вызывает основную функцию
// (см. ВыполнитьПереходНаСинхронизациюДанныхЧерезУниверсальныйФормат).
// Вызывается при транспорте Интернет: корреспондент выполнил переход на универсальный формат,
// а текущая ИБ нет.
//
// Параметры:
//   КодУзла - Строка - Код настройки.
//   Ошибка - Булево - Признак ошибки при выполнении функции.
//
// Возвращаемое значение:
//   ПланОбменаСсылка.СинхронизацияДанныхЧерезУниверсальныйФормат - ссылка на настройку синхронизации данных
//     через универсальный формат (при успешном выполнении перехода),
//   Неопределено - в случае критичных ошибок, возникших в процессе перехода.
//
Функция ПереходНаСинхронизациюЧерезУниверсальныйФорматИнтернет(КодУзла, Ошибка) Экспорт
	
	
	
КонецФункции

// Процедура выполняет попытку перехода на новый обмен с существующего обмена.
//  Вызывается, если в ходе синхронизации данных получено сообщение не соответствующее формату "старого" обмена.
//
// Параметры:
//   НастройкаСинхронизацииДанных - ПланОбменаСсылка - ссылка на узел плана обмена,
//     с которого выполняется переход на новый обмен.
//   ОбменЧерезВнешнееСоединение - Булево - признак того что текущий обмен выполняется через внешнее соединение.
//
Процедура ВыполнитьПереходНаНовыйОбмен(НастройкаСинхронизацииДанных, ОбменЧерезВнешнееСоединение = Ложь) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#Область ИдентификаторыВариантовНастройкиОбмена

// Возвращает идентификатор варианта настройки обмена для БП и УХ.
//
// Возвращаемое значение:
//   Строка - идентификатор варианта настройки обмена.
//
Функция ВариантНастройкиОбменаБП() Экспорт
	
	Возврат "ОбменДОБП";
	
КонецФункции

// Возвращает идентификатор варианта настройки обмена для ЗУП и ЗКГУ.
//
// Возвращаемое значение:
//   Строка - идентификатор варианта настройки обмена.
//
Функция ВариантНастройкиОбменаЗУП() Экспорт
	
	Возврат "ОбменДОЗУП";
	
КонецФункции

// Возвращает идентификатор варианта настройки обмена для других программ.
//
// Возвращаемое значение:
//   Строка - идентификатор варианта настройки обмена.
//
Функция ВариантНастройкиОбменаУП() Экспорт
	
	Возврат "ОбменДОУП";
	
КонецФункции

// Возвращает идентификатор варианта настройки обмена для БП и УХ.
//
// Возвращаемое значение:
//   Строка - идентификатор варианта настройки обмена.
//
Функция ВариантНастройкиОбменУниверсальный() Экспорт
	
	Возврат "ОбменУниверсальный";
	
КонецФункции

#КонецОбласти

#Область ИдентификаторыВариантовСинхронизацииДокументов

// Возвращает идентификатор варианта синхронизации документов "Автоматическая синхронизация".
//
// Возвращаемое значение:
//   Строка - идентификатор варианта синхронизации документов.
//
Функция ВариантСинхронизацииДокументовАвтоматическаяСинхронизация() Экспорт
	
	Возврат "АвтоматическаяСинхронизация";
	
КонецФункции

// Возвращает идентификатор варианта синхронизации документов "Интерактивная синхронизация".
//
// Возвращаемое значение:
//   Строка - идентификатор варианта синхронизации документов.
//
Функция ВариантСинхронизацииДокументовИнтерактивнаяСинхронизация() Экспорт
	
	Возврат "ИнтерактивнаяСинхронизация";
	
КонецФункции

// Возвращает идентификатор варианта синхронизации документов "Не синхронизировать".
//
// Возвращаемое значение:
//   Строка - идентификатор варианта синхронизации документов.
//
Функция ВариантСинхронизацииДокументовНеСинхронизировать() Экспорт
	
	Возврат "НеСинхронизировать";
	
КонецФункции

#КонецОбласти

#Область ИдентификаторыВариантовСинхронизацииСправочников

// Возвращает идентификатор варианта синхронизации справочников "Автоматическая синхронизация".
//
// Возвращаемое значение:
//   Строка - идентификатор варианта синхронизации справочников.
//
Функция ВариантСинхронизацииСправочниковАвтоматическаяСинхронизация() Экспорт
	
	Возврат "АвтоматическаяСинхронизация";
	
КонецФункции

// Возвращает идентификатор варианта синхронизации справочников "Синхронизировать по необходимости".
//
// Возвращаемое значение:
//   Строка - идентификатор варианта синхронизации справочников.
//
Функция ВариантСинхронизацииСправочниковСинхронизироватьПоНеобходимости() Экспорт
	
	Возврат "СинхронизироватьПоНеобходимости";
	
КонецФункции

// Возвращает идентификатор варианта синхронизации справочников "Не синхронизировать".
//
// Возвращаемое значение:
//   Строка - идентификатор варианта синхронизации справочников.
//
Функция ВариантСинхронизацииСправочниковНеСинхронизировать() Экспорт
	
	Возврат "НеСинхронизировать";
	
КонецФункции

#КонецОбласти

// Возвращает список объектов, которые нельзя загрузить без загрузки исходного объекта.
//
// Параметры:
//   ИсходныхОбъект - Строка - полное имя исходного объекта.
//
// Возвращаемое значение:
//   Массив из Строка
//
Функция КтоЗависит(ИсходныхОбъект) Экспорт
	
	Зависимости = Зависимости();
	
	КтоЗависит = Зависимости[ИсходныхОбъект];
	Индекс = 0;
	Пока Индекс < КтоЗависит.Количество() Цикл
		ЗависимыйОбъект = КтоЗависит[Индекс];
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КтоЗависит, Зависимости[ЗависимыйОбъект], Истина);
		Индекс = Индекс + 1;
	КонецЦикла;
	
	ИндексИсходныхОбъект = КтоЗависит.Найти(ИсходныхОбъект);
	Если ИндексИсходныхОбъект <> Неопределено Тогда
		КтоЗависит.Удалить(ИндексИсходныхОбъект);
	КонецЕсли;
	
	Возврат КтоЗависит;
	
КонецФункции

// Возвращает строковое представление варианта синхронизации документов,
// в зависимости от установленного режима выгрузки документов.
//
// Параметры:
//   РежимВыгрузкиДокументов - ПеречислениеСсылка.РежимыВыгрузкиОбъектовОбмена - режим выгрузки документов.
//
// Возвращаемое значение:
//   Строка - строковое представление варианта выгрузки документов.
//
Функция ОпределитьВариантСинхронизацииДокументов(РежимВыгрузкиДокументов) Экспорт
	
	ВариантСинхронизацииДокументов = "";
	
	Если РежимВыгрузкиДокументов = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПоУсловию Тогда
		
		ВариантСинхронизацииДокументов = ВариантСинхронизацииДокументовАвтоматическаяСинхронизация();
		
	ИначеЕсли РежимВыгрузкиДокументов = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьВручную Тогда
		
		ВариантСинхронизацииДокументов = ВариантСинхронизацииДокументовИнтерактивнаяСинхронизация();
		
	ИначеЕсли РежимВыгрузкиДокументов = Перечисления.РежимыВыгрузкиОбъектовОбмена.НеВыгружать Тогда
		
		ВариантСинхронизацииДокументов = ВариантСинхронизацииДокументовНеСинхронизировать();
		
	КонецЕсли;
	
	Возврат ВариантСинхронизацииДокументов;
	
КонецФункции

// Возвращает строковое представление варианта синхронизации справочников,
// в зависимости от установленного режима выгрузки справочников.
//
// Параметры:
//   РежимВыгрузкиСправочников - ПеречислениеСсылка.РежимыВыгрузкиОбъектовОбмена - режим выгрузки справочников.
//
// Возвращаемое значение:
//   Строка - строковое представление варианта выгрузки справочников.
//
Функция ОпределитьВариантСинхронизацииСправочников(РежимВыгрузкиСправочников) Экспорт
	
	ВариантСинхронизацииСправочников = "";
	
	Если РежимВыгрузкиСправочников = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПоУсловию Тогда
		
		ВариантСинхронизацииСправочников = ВариантСинхронизацииСправочниковАвтоматическаяСинхронизация();
		
	ИначеЕсли РежимВыгрузкиСправочников = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПриНеобходимости Тогда
		
		ВариантСинхронизацииСправочников = ВариантСинхронизацииСправочниковСинхронизироватьПоНеобходимости();
		
	ИначеЕсли РежимВыгрузкиСправочников = Перечисления.РежимыВыгрузкиОбъектовОбмена.НеВыгружать Тогда
		
		ВариантСинхронизацииСправочников = ВариантСинхронизацииСправочниковНеСинхронизировать();
		
	КонецЕсли;
	
	Возврат ВариантСинхронизацииСправочников;
	
КонецФункции

// Возвращает список объектов, которые требуются для загрузки исходного объекта.
//
// Параметры:
//   ИсходныхОбъект - Строка - полное имя исходного объекта.
//
// Возвращаемое значение:
//   Массив из Строка
//
Функция ОтКогоЗависит(ИсходныхОбъект) Экспорт
	
	Зависимости = Зависимости();
	
	ОтКогоЗависит = ОтКогоЗависитПервыйУровень(ИсходныхОбъект, Зависимости);
	Индекс = 0;
	Пока Индекс < ОтКогоЗависит.Количество() Цикл
		ЗависимыйОбъект = ОтКогоЗависит[Индекс];
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			ОтКогоЗависит,
			ОтКогоЗависитПервыйУровень(ЗависимыйОбъект, Зависимости),
			Истина);
		Индекс = Индекс + 1;
	КонецЦикла;
	
	ИндексИсходныхОбъект = ОтКогоЗависит.Найти(ИсходныхОбъект);
	Если ИндексИсходныхОбъект <> Неопределено Тогда
		ОтКогоЗависит.Удалить(ИндексИсходныхОбъект);
	КонецЕсли;
	
	Возврат ОтКогоЗависит;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает список зависимостей объектов при получении данных.
//
// Возвращаемое значение:
//   Соответствие из КлючИЗначение:
//     * Ключ - Строка
//     * Значение - Массив из Строка
//
Функция Зависимости()
	
	Зависимости = Новый Соответствие;
	
	Зависимости.Вставить("Справочник.БанковскиеСчета", Новый Массив);
	Зависимости.Вставить("Справочник.КонтактныеЛица", Новый Массив);
	Зависимости.Вставить("Справочник.Номенклатура", Новый Массив);
	Зависимости.Вставить("Справочник.Сотрудники", Новый Массив);
	Зависимости.Вставить("Справочник.СтатьиДвиженияДенежныхСредств", Новый Массив);
	Зависимости.Вставить("РегистрСведений.ДокументыФизическихЛиц", Новый Массив);
	
	КтоЗависит = Новый Массив;
	КтоЗависит.Добавить("Справочник.КлассификаторБанков");
	КтоЗависит.Добавить("Справочник.СтавкиНДС");
	Зависимости.Вставить("Справочник.СтраныМира", КтоЗависит);
	
	КтоЗависит = Новый Массив;
	КтоЗависит.Добавить("Справочник.БанковскиеСчета");
	Зависимости.Вставить("Справочник.КлассификаторБанков", КтоЗависит);
	
	КтоЗависит = Новый Массив;
	КтоЗависит.Добавить("Справочник.Номенклатура");
	Зависимости.Вставить("Справочник.СтавкиНДС", КтоЗависит);
	
	КтоЗависит = Новый Массив;
	КтоЗависит.Добавить("Справочник.КонтактныеЛица");
	КтоЗависит.Добавить("Справочник.БанковскиеСчета");
	Зависимости.Вставить("Справочник.Контрагенты", КтоЗависит);
	
	КтоЗависит = Новый Массив;
	КтоЗависит.Добавить("Справочник.БанковскиеСчета");
	КтоЗависит.Добавить("Справочник.Сотрудники");
	Зависимости.Вставить("Справочник.Организации", КтоЗависит);
	
	КтоЗависит = Новый Массив;
	КтоЗависит.Добавить("Справочник.Сотрудники");
	Зависимости.Вставить("Справочник.СтруктураПредприятия", КтоЗависит);
	
	КтоЗависит = Новый Массив;
	КтоЗависит.Добавить("Справочник.Сотрудники");
	Зависимости.Вставить("Справочник.Должности", КтоЗависит);
	
	КтоЗависит = Новый Массив;
	КтоЗависит.Добавить("Справочник.БанковскиеСчета");
	Зависимости.Вставить("Справочник.Валюты", КтоЗависит);
	
	КтоЗависит = Новый Массив;
	КтоЗависит.Добавить("Справочник.Номенклатура");
	Зависимости.Вставить("Справочник.КлассификаторЕдиницИзмерения", КтоЗависит);
	
	КтоЗависит = Новый Массив;
	КтоЗависит.Добавить("Справочник.Контрагенты");
	КтоЗависит.Добавить("РегистрСведений.ДокументыФизическихЛиц");
	КтоЗависит.Добавить("Справочник.Пользователи");
	КтоЗависит.Добавить("Справочник.Сотрудники");
	Зависимости.Вставить("Справочник.ФизическиеЛица", КтоЗависит);
	
	КтоЗависит = Новый Массив;
	КтоЗависит.Добавить("Справочник.Контрагенты");
	Зависимости.Вставить("Справочник.Пользователи", КтоЗависит);
	
	Возврат Зависимости;
	
КонецФункции

Функция ОтКогоЗависитПервыйУровень(ИсходныхОбъект, Зависимости)
	
	ОтКогоЗависит = Новый Массив;
	
	Для Каждого ЭлементЗависимости Из Зависимости Цикл
		Если ЭлементЗависимости.Ключ = ИсходныхОбъект Тогда
			Продолжить;
		КонецЕсли;
		Если ЭлементЗависимости.Значение.Найти(ИсходныхОбъект) <> Неопределено Тогда
			ОтКогоЗависит.Добавить(ЭлементЗависимости.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОтКогоЗависит;
	
КонецФункции

#КонецОбласти

#КонецЕсли