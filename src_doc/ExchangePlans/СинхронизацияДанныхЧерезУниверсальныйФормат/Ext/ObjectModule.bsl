///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Проверяет запрещена ли загрузка объекта.
//
// Параметры:
//   ПолноеИмяОбъектаМетаданных - Строка - проверяемый объект.
//
// Возвращаемое значение:
//   Булево
//
Функция ЗагрузкаЗапрещена(ПолноеИмяОбъектаМетаданных) Экспорт
	
	Возврат (ЗапретЗагрузки.Найти(ПолноеИмяОбъектаМетаданных, "ПолноеИмяОбъектаМетаданных") <> Неопределено);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Обновление кэшируемых данных, зависящих от значений реквизитов данного узла обмена.
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		ОбменДаннымиВызовСервера.СброситьКэшМеханизмаРегистрацииОбъектов();
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьУзел(ДанныеЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет настройку отборов на узле.
//
// Параметры:
//   ДанныеЗаполнения - Произвольный - значение, которое используется как основание для заполнения.
//
Процедура ИнициализироватьУзел(ДанныеЗаполнения)
	
	Если Не ДанныеЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Менеджер = ПланыОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат;
	
	ИспользоватьОтборПоОрганизациям = Ложь;
	ДатаНачалаВыгрузкиДокументов = НачалоГода(ТекущаяДатаСеанса());
	
	Если ВариантНастройки = Менеджер.ВариантНастройкиОбменаЗУП() Тогда
		РежимВыгрузкиДокументов = Перечисления.РежимыВыгрузкиОбъектовОбмена.НеВыгружать;
		РежимВыгрузкиСправочников = Перечисления.РежимыВыгрузкиОбъектовОбмена.НеВыгружать;
	Иначе
		РежимВыгрузкиДокументов = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПоУсловию;
		РежимВыгрузкиСправочников = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПоУсловию;
		
		Если ВариантНастройки <> Менеджер.ВариантНастройкиОбменаУП()
				И ВариантНастройки <> Менеджер.ВариантНастройкиОбменаБП() Тогда
			ВыгружатьОрганизации = Истина;
			ВыгружатьСтруктуруПредприятия = Истина;
		КонецЕсли;
		
		ВыгружатьБанковскиеСчета = Истина;
		ВыгружатьДокументыФизЛиц = Истина;
		ВыгружатьДолжности = Истина;
		ВыгружатьКонтактныеЛица = Истина;
		ВыгружатьКонтрагентов = Истина;
		ВыгружатьСтатьиДДС = Истина;
		ВыгружатьТоварыИУслуги = Истина;
		ВыгружатьФизическиеЛица = Истина;
	КонецЕсли;
	
	Если ВариантНастройки = Менеджер.ВариантНастройкиОбменаУП()
			Или ВариантНастройки = Менеджер.ВариантНастройкиОбменаБП()
			Или ВариантНастройки = Менеджер.ВариантНастройкиОбменаЗУП() Тогда
		НоваяСтрокаЗапретЗагрузки = ЗапретЗагрузки.Добавить();
		НоваяСтрокаЗапретЗагрузки.ПолноеИмяОбъектаМетаданных = "Справочник.Сотрудники";
	КонецЕсли;
	
	РежимВыгрузкиПриНеобходимости = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПриНеобходимости;
	
	ПравилаОтправкиДокументов = Менеджер.ОпределитьВариантСинхронизацииДокументов(РежимВыгрузкиДокументов);
	ПравилаОтправкиСправочников = Менеджер.ОпределитьВариантСинхронизацииСправочников(РежимВыгрузкиСправочников);
	
КонецПроцедуры

#КонецОбласти

#Иначе

ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");

#КонецЕсли