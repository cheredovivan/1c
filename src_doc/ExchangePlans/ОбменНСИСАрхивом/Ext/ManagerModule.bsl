#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает узел для регистрации изменений для отправки в 1С:Архив.
// 
// Возвращаемое значение:
//  Неопределено, ПланОбменаСсылка.ОбменНСИСАрхивом - узел плана обмена.
// 
Функция УзелАрхив() Экспорт
	
	УзелАрхив = Неопределено;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ОбменНСИСАрхивом.Ссылка КАК Ссылка
		|ИЗ
		|	ПланОбмена.ОбменНСИСАрхивом КАК ОбменНСИСАрхивом
		|ГДЕ
		|	НЕ ОбменНСИСАрхивом.ЭтотУзел");
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		УзелАрхив = Выборка.Ссылка;
	Иначе
		НачатьТранзакцию();
		Попытка
			Блокировки = Новый БлокировкаДанных;
			Блокировки.Добавить("ПланОбмена.ОбменНСИСАрхивом");
			Блокировки.Заблокировать();
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				УзелАрхив = Выборка.Ссылка;
			Иначе
				УзелАрхивОбъект = СоздатьУзел();
				УзелАрхивОбъект.Код = "Архив";
				УзелАрхивОбъект.Наименование = НСтр("ru = 'Архив'");
				УзелАрхивОбъект.Записать();
				УзелАрхив = УзелАрхивОбъект.Ссылка;
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ВызватьИсключение;
		КонецПопытки;
	КонецЕсли;
	
	Возврат УзелАрхив;
	
КонецФункции

#КонецОбласти

#КонецЕсли
