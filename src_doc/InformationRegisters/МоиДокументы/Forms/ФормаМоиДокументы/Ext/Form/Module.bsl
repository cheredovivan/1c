#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПостоянныеПараметрыСписка();
	
	ВестиУчетСканКопийОригиналовДокументов = 
		ПолучитьФункциональнуюОпцию("ВестиУчетСканКопийОригиналовДокументов");
	
	ЗаголовокСодержаниеФайлы = НСтр("ru = 'Содержание, Файлы'");
	ЗаголовокЗадачиСвязи = НСтр("ru = 'Задачи'");		
	ФорматДатыДляКолонокЗадач = ?(ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач"), 
		"ДФ='dd.MM.yy H:mm'",
		"ДФ='dd.MM.yy'");
	
	Элементы.ЗадачиСрокИсполнения.Формат = ФорматДатыДляКолонокЗадач;
	СортироватьПо = "ДатаДокумента";
	НаправлениеСортировки = "Возр";
	
	Если Не ПравоДоступа("Чтение", Метаданные.Справочники.Проекты) Тогда
		Элементы.СписокСгруппироватьПоПроекту.Видимость = Ложь;
	КонецЕсли;                   
	
	ИспользоватьРолиФайлов = ПолучитьФункциональнуюОпцию("ИспользоватьРолиФайлов");
	
	Если КлючНазначенияИспользования = "ОтображениеИстекающихДокументов" Тогда
		ВсеПричины = Перечисления.ПричиныДобавленияВМоиДокументы.ВсеПричины();
		ОтмеченныеПричины.ЗагрузитьЗначения(ВсеПричины);
	ИначеЕсли КлючНазначенияИспользования = "ОтображениеПросроченныхДокументов" Тогда
		ВсеПричины = Перечисления.ПричиныДобавленияВМоиДокументы.ВсеПричины();
		ОтмеченныеПричины.ЗагрузитьЗначения(ВсеПричины);
	ИначеЕсли КлючНазначенияИспользования = "ОтображениеДокументовБезОтвета" Тогда
		ОтмеченныеПричины.Добавить(Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомДокумента);
		ОтмеченныеПричины.Добавить(Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьОтветственнымЗаДокумент);
	Иначе
		НастройкиФормы = ОбщегоНазначения.ХранилищеСистемныхНастроекЗагрузить(ИмяФормы + "/ТекущиеДанные", "");
		Если НастройкиФормы = Неопределено Или НастройкиФормы.Получить("ОтмеченныеПричины") = Неопределено Тогда
			ВсеПричины = Перечисления.ПричиныДобавленияВМоиДокументы.ВсеПричины();
			ОтмеченныеПричины.ЗагрузитьЗначения(ВсеПричины);
		КонецЕсли;
	КонецЕсли;
	
	Если КлючНазначенияИспользования = "ОтображениеИстекающихДокументов"
		Или КлючНазначенияИспользования = "ОтображениеПросроченныхДокументов"
		Или КлючНазначенияИспользования = "ОтображениеДокументовБезОтвета" Тогда
		
		Элементы.ФормаСоздать.Видимость = Ложь;
		Элементы.ФормаДобавитьВСписок.Видимость = Ложь;
		Элементы.ФормаСкопировать.Видимость = Ложь;
		Элементы.СписокКонтекстноеМенюСкопировать.Видимость = Ложь;
		Элементы.КонтекстноеМенюСоздать.Видимость = Ложь;
		Элементы.СписокКонтекстноеМенюДобавитьВСписок.Видимость = Ложь;
		Для Каждого ЭлементГруппы Из Элементы.СписокГруппаОтбор.ПодчиненныеЭлементы Цикл
			ЭлементГруппы.Доступность = Ложь;
		КонецЦикла;
	КонецЕсли;
	
	УстановитьПометкуРежимуСортировки(ЭтотОбъект);
	
	ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
	ЭтоМобильноеУстройствоСБольшимЭкраном = МК_КлиентСервер.ЭтоМобильныйСБольшимЭкраном();
	Если ЭтоМобильныйКлиент Тогда
		МК_НастроитьЭлементыФормы();
	КонецЕсли;
	
	// Локализация
	// Сервис "1С:Share"
	ИспользоватьСервис1CShare = ИнтеграцияShareДокументооборот.ИспользоватьСервис1СShare();
	// Локализация Конец
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	МассивДокументов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыбранноеЗначение);
	МоиДокументыКлиент.ДобавитьВМоиДокументы(МассивДокументов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()
	
	ЭтоМобильноеУстройствоСБольшимЭкраном = МК_КлиентСервер.ЭтоМобильныйСБольшимЭкраном();
	МК_НастроитьОбластьПредпросмотра();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПрочтениеОбновитьСписок" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
	Если ИмяСобытия = "ДокументСоздан" Тогда
		Если МоиДокументы.ДокументВходитВМоиДокументы(Параметр) Тогда 
			ОбновитьНаСервере();
		КонецЕсли;	
	КонецЕсли;	
	
	Если ИмяСобытия = "ДокументИзменен" И Параметр = ТекущийДокумент Тогда
		ОбновитьМиникарточку();
	КонецЕсли;	
	
	Если ИмяСобытия = "Запись_Файл" И Параметр.Событие = "ДанныеФайлаИзменены" Тогда
		
		Если ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("Владелец")
			 И Параметр.Свойство("Файл") И ЗначениеЗаполнено(Параметр.Владелец)  Тогда
			ВладелецФайла = Параметр.Владелец;
			ФайлСсылка = Параметр.Файл; 
		Иначе	
			ВладелецФайла = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
				Источник, "ВладелецФайла");
			ФайлСсылка = Источник; 
		КонецЕсли;	
		
		Если ВладелецФайла = ТекущийДокумент Тогда
			ОбновитьСтрокуФайла(ФайлСсылка);
			
			ТекущиеДанные = ТекущиеДанныеСпискаФайлов();
			ОбзорСпискаДокументовКлиент.УстановитьДоступностьКоманд(ТекущиеДанные, ЭтотОбъект);
		КонецЕсли;
		
 	КонецЕсли;
	
	Если ИмяСобытия = "ФайлИзменен" Тогда
		Если ТипЗнч(Параметр) = Тип("Структура") Тогда
			Если Параметр.Владелец = ТекущийДокумент Тогда 
				ОбновитьСтрокуФайла(Параметр.Файл);
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;	
	
	Если ИмяСобытия = "БизнесПроцессСтартован" Тогда
		Если Параметр.Свойство("СсылкаНаПредметБизнесПроцесса")
			И Параметр.СсылкаНаПредметБизнесПроцесса = ТекущийДокумент Тогда
			ОбзорСпискаДокументовКлиент.ЗаполнитьСписокЗадач(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_Файл" И Параметр.Событие = "СозданФайл" Тогда
		
		Если Параметр <> Неопределено
		   И Параметр.Свойство("Владелец")
		   И Параметр.Владелец = ТекущийДокумент Тогда
			
			ОбзорСпискаДокументовКлиент.ЗаполнитьСписокФайлов(ЭтотОбъект);
			
			Если Параметр.Свойство("Файл") Тогда
				УстановитьТекущуюСтрокуФайла(Параметр.Файл);
				ОтметитьПризнакОригиналУНовогоФайла(Параметр.Файл);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если (ИмяСобытия = "ИзмененыСвязиДокумента" Или ИмяСобытия = "ИзмененыСвязиПодчиненныхДокументов")
		И Параметр.Документ = ТекущийДокумент Тогда
		ОбновитьМиникарточку();
	КонецЕсли;
	
	Если (ИмяСобытия = "ФайлыДокументаПомеченыНаУдаление" И Параметр = ТекущийДокумент)
		Или (ИмяСобытия = "ИмпортФайловЗавершен" И ТипЗнч(Источник) = Тип("Структура")
		И Источник.ВладелецФайлов = ТекущийДокумент) Тогда
		ОбзорСпискаДокументовКлиент.ЗаполнитьСписокФайлов(ЭтотОбъект);
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_МоиДокументы" Тогда
		ОбновитьНаКлиенте();
	КонецЕсли;
	
	Если ИмяСобытия = "ДанныеПрочтены" И ТипЗнч(Параметр) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ОбновитьЭлементыФормы();
	ОбновитьНаСервере();
	УстановитьЗначениеГруппировкиПоУмолчанию();
	УстановитьПометкуРежимуСортировки(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Обзор
&НаКлиенте
Процедура ОбзорHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда 
		Возврат;
	КонецЕсли;	
	
	ПрефиксСсылки = "v8doc:";
	ДлинаПрефикса = СтрДлина(ПрефиксСсылки);
	
	Если Лев(ДанныеСобытия.Href, ДлинаПрефикса) <> ПрефиксСсылки Тогда 
		Возврат;
	КонецЕсли;	
	
	НавигационнаяСсылкаПоля = Сред(ДанныеСобытия.Href, ДлинаПрефикса + 1);
	
	Если СтрНайти(НавигационнаяСсылкаПоля, "ShowLinks") > 0 Тогда
		
		ОбзорСпискаДокументовКлиент.ПоказатьСвязи(ДанныеСобытия.Href);
		
		Возврат;
	КонецЕсли;
		
	ПерейтиПоНавигационнойСсылке(НавигационнаяСсылкаПоля);
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)

	ОбновитьДеревоПереписки();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанныеДокумент = Неопределено;
	Если Элементы.Список.ТекущиеДанные <> Неопределено Тогда 	
		ТекущиеДанныеДокумент = Элементы.Список.ТекущиеДанные.Документ;
	КонецЕсли;
	
	Если ТекущийДокумент <> ТекущиеДанныеДокумент Тогда 
		ПодключитьОбработчикОжидания("ОбновитьМиникарточку", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если КлючНазначенияИспользования <> "" Тогда
		Возврат;
	КонецЕсли;
	
	Если Копирование Тогда 
		
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначениеКопирования", ТекущиеДанные.Документ);
		ПараметрыФормы.Вставить("МоиДокументыОтразитьНемедленно", Истина);
		
		Открытьформу("Справочник.ДокументыПредприятия.ФормаОбъекта", ПараметрыФормы, Элементы.Список);
		
	Иначе	
		
		ПараметрыФормыШаблона = Неопределено;
		ПараметрыФормыДокумента = Новый Структура;
		ПараметрыФормыДокумента.Вставить("МоиДокументыОтразитьНемедленно", Истина);
		ДелопроизводствоКлиент.СоздатьДокументПредприятия(ПараметрыФормыШаблона, ПараметрыФормыДокумента);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	ПоказатьЗначение(,ТекущиеДанные.Документ);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	УдалитьИзСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаЗапросаОбновления(Элемент)
	
	ОбновитьНаКлиенте();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ОтмеченныеПричины = Настройки.ДополнительныеСвойства.ОтмеченныеПричины;
	
	НужныКонтрагенты = Ложь;
	ДокументыСтрок = Новый Массив; // Массив из СправочникСсылка.ДокументыПредприятия
	ВидыДокументовСтрок = Новый Массив;
	Для Каждого КлючИЗначение Из Строки Цикл
		ДанныеСтроки = КлючИЗначение.Значение.Данные;
		Если ЗначениеЗаполнено(ДанныеСтроки.Документ) Тогда
			ДокументыСтрок.Добавить(ДанныеСтроки.Документ);
		КонецЕсли;
		Если ЗначениеЗаполнено(ДанныеСтроки.ВидДокумента) Тогда
			ВидыДокументовСтрок.Добавить(ДанныеСтроки.ВидДокумента);
		КонецЕсли;
		НужныКонтрагенты = ДанныеСтроки.Свойство("КонтрагентыДляСписков");
	КонецЦикла;
	
	ДанныеСтрокСУчетомПрав = РегистрыСведений.РеестрМоихДокументов.ДанныеСтрокСУчетомПрав(ДокументыСтрок);
	
	ПометкиПрочтения = РаботаСПрочтениями.ПометкиПрочтения(ДокументыСтрок);
	РеквизитыВидовДокументов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		ВидыДокументовСтрок,
		"ЯвляетсяВходящейКорреспонденцией, ЯвляетсяИсходящейКорреспонденцией");
	
	РеквизитыДокументов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		ДокументыСтрок,
		"ДатаСоздания, ДатаРегистрации, Контрагенты, РегистрационныйНомер",
		Истина);
	
	Для Каждого КлючИЗначение Из Строки Цикл
		
		ДанныеСтроки = КлючИЗначение.Значение.Данные;
		
		ДанныеСтрокиСУчетомПрав = ДанныеСтрокСУчетомПрав[ДанныеСтроки.Документ];
		ДанныеСтроки.Наименование = ДанныеСтрокиСУчетомПрав.Наименование;
		ДанныеСтроки.ВидДокумента = ДанныеСтрокиСУчетомПрав.ВидДокумента;
		ДанныеСтроки.Проект = ДанныеСтрокиСУчетомПрав.Проект;
		
		Если ЗначениеЗаполнено(ДанныеСтроки.ВидДокумента) Тогда
			РеквизитыВидаДокументов = РеквизитыВидовДокументов[ДанныеСтроки.ВидДокумента];
			Если РеквизитыВидаДокументов <> Неопределено
				И РеквизитыВидаДокументов.ЯвляетсяВходящейКорреспонденцией = Истина Тогда
				ДанныеСтроки.ВидКорреспонденции = Перечисления.ВидыКорреспонденции.Входящая;
			ИначеЕсли РеквизитыВидаДокументов <> Неопределено
				И РеквизитыВидаДокументов.ЯвляетсяИсходящейКорреспонденцией = Истина Тогда
				ДанныеСтроки.ВидКорреспонденции = Перечисления.ВидыКорреспонденции.Исходящая;
			КонецЕсли;
		КонецЕсли;
		
		Если ДанныеСтроки.Свойство("Причина") Тогда
			МассивПричина = Новый Массив;
			Если ДанныеСтроки.ДобавленВручную И ОтмеченныеПричины.Найти(
				Перечисления.ПричиныДобавленияВМоиДокументы.ДобавленВручную) <> Неопределено Тогда
				ПредставлениеПричины = Строка(Перечисления.ПричиныДобавленияВМоиДокументы.ДобавленВручную);
				МассивПричина.Добавить(ПредставлениеПричины);
			КонецЕсли;
			Если ДанныеСтроки.НазначенаЗадачаМне И ОтмеченныеПричины.Найти(
				Перечисления.ПричиныДобавленияВМоиДокументы.НазначенаЗадачаМне) <> Неопределено Тогда
				ПредставлениеПричины = Строка(Перечисления.ПричиныДобавленияВМоиДокументы.НазначенаЗадачаМне);
				МассивПричина.Добавить(ПредставлениеПричины);
			КонецЕсли;
			Если ДанныеСтроки.ОтправленаЗадачаОтМеня И ОтмеченныеПричины.Найти(
				Перечисления.ПричиныДобавленияВМоиДокументы.ОтправленаЗадачаОтМеня) <> Неопределено Тогда
				ПредставлениеПричины = Строка(Перечисления.ПричиныДобавленияВМоиДокументы.ОтправленаЗадачаОтМеня);
				МассивПричина.Добавить(ПредставлениеПричины);
			КонецЕсли;
			Если ДанныеСтроки.ЯвляюсьАвторомДокумента И ОтмеченныеПричины.Найти(
				Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомДокумента) <> Неопределено Тогда
				ПредставлениеПричины = Строка(Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомДокумента);
				МассивПричина.Добавить(ПредставлениеПричины);
			КонецЕсли;
			Если ДанныеСтроки.ЯвляюсьАвторомУтвержденияДокумента И ОтмеченныеПричины.Найти(
				Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомУтвержденияДокумента) <> Неопределено Тогда
				ПредставлениеПричины =
					Строка(Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомУтвержденияДокумента);
				МассивПричина.Добавить(ПредставлениеПричины);
			КонецЕсли;
			Если ДанныеСтроки.ЯвляюсьОтветственнымЗаДокумент И ОтмеченныеПричины.Найти(
				Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьОтветственнымЗаДокумент) <> Неопределено Тогда
				ПредставлениеПричины =
					Строка(Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьОтветственнымЗаДокумент);
				МассивПричина.Добавить(ПредставлениеПричины);
			КонецЕсли;
			Если ДанныеСтроки.ЯвляюсьПодписантомДокумента И ОтмеченныеПричины.Найти(
				Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьПодписантомДокумента) <> Неопределено Тогда
				ПредставлениеПричины = Строка(Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьПодписантомДокумента);
				МассивПричина.Добавить(ПредставлениеПричины);
			КонецЕсли;
			ДанныеСтроки.Причина = СтрСоединить(МассивПричина, Символы.ПС)
		КонецЕсли;
		
		ДанныеСтроки.Прочтен = ПометкиПрочтения[ДанныеСтроки.Документ];
		
		РеквизитыДокумента = РеквизитыДокументов[ДанныеСтроки.Документ];
		Если РеквизитыДокумента <> Неопределено Тогда
			ДанныеСтроки.РегистрационныйНомер = РеквизитыДокумента.РегистрационныйНомер;
			Если НужныКонтрагенты Тогда
				ДатаУчетаДокумента = Делопроизводство.ДатаУчетаДокумента(РеквизитыДокумента);
				ДанныеСтроки.КонтрагентыДляСписков = Делопроизводство.ПолучитьКонтрагентовДляСписков(
					РеквизитыДокумента.Контрагенты.Выгрузить(),
					"Контрагент",
					ДатаУчетаДокумента);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыФайлыДокумента

&НаКлиенте
Процедура ФайлыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Строка = Файлы.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если Строка = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ДокументыПредприятияКлиентЛокализация.ПриВыбореФайла_ФормаМоиДокументы(Строка) Тогда
		Возврат;
	КонецЕсли;
		
	Если Строка.ПодписанЭП Или ДокументПодписан Тогда
		РаботаСФайламиКлиент.ОткрытьФайлДокумента(Строка.Ссылка, ЭтотОбъект);
	Иначе
		ВыборФайла(Строка.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПриАктивизацииСтроки(Элемент)
	
	ОбзорСпискаДокументовКлиент.УстановитьДоступностьКоманд(Элементы.ФайлыДокумента.ТекущиеДанные,
		ЭтотОбъект);
	Элементы.ОткрытьФайл.Доступность = Элементы.КонтекстноеМенюФайлыОткрытьФайл.Доступность;
	Элементы.Редактировать.Доступность = Элементы.КонтекстноеМенюФайлыРедактировать.Доступность;
	Элементы.ЗакончитьРедактирование.Доступность = Элементы.КонтекстноеМенюФайлыЗакончитьРедактирование.Доступность;
	Элементы.Напечатать.Доступность = Элементы.КонтекстноеМенюФайлыНапечатать.Доступность;
	Элементы.Изменить.Доступность = Элементы.КонтекстноеМенюФайлыИзменить.Доступность;
	Элементы.СохранитьИзменения.Доступность = Элементы.КонтекстноеМенюФайлыСохранитьИзменения.Доступность;
	Элементы.Удалить.Доступность = Элементы.КонтекстноеМенюФайлыУдалить.Доступность;
	Элементы.СохранитьКак.Доступность = Элементы.КонтекстноеМенюФайлыСохранитьКак.Доступность;
	Элементы.ОбновитьИзФайлаНаДиске.Доступность = Элементы.КонтекстноеМенюФайлыОбновитьИзФайлаНаДиске.Доступность;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	ПараметрыДобавления = Новый Структура;
	ФайлыПередНачаломДобавленияКлиент(Элемент, Отказ, Копирование, Родитель, Группа, ПараметрыДобавления);
		
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = ТекущиеДанныеСпискаФайлов();
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ФайлыПередУдалениемПродолжение",
		ЭтотОбъект,
		Новый Структура);
	ДелопроизводствоКлиент.ПометитьФайлыДокументаНаУдаление(ЭтотОбъект, ОписаниеОповещения, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	ТекущиеДанные = Элементы.ФайлыДокумента.ТекущиеДанные;
	ВыделенныеСтроки = Элементы.ФайлыДокумента.ВыделенныеСтроки;
	
	МассивФайловДляПеретаскивания = Новый Массив;
	
	Если ВыделенныеСтроки.Количество() > 1 Тогда
		Для Каждого ВыбраннаяСтрока Из ВыделенныеСтроки Цикл
			ДанныеСтроки = Элементы.ФайлыДокумента.ДанныеСтроки(ВыбраннаяСтрока);	
			МассивФайловДляПеретаскивания.Добавить(ДанныеСтроки.Ссылка);
		КонецЦикла;
	Иначе
		Если ТекущиеДанные = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		МассивФайловДляПеретаскивания.Добавить(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
	ПараметрыПеретаскивания.Значение = МассивФайловДляПеретаскивания;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	ОбработатьПеретаскиваниеФайла(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокЗадачи

&НаКлиенте
Процедура ЗадачиПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.СписокЗадачи.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
		
	РаботаСЗадачамиКлиент.ОткрытьКарточкуИсполнителяЗадачи(
		ТекущиеДанные.ДействиеЗадачи,
		Неопределено,
		Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоПереписки

&НаКлиенте
Процедура ДеревоПерепискиПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;

	ТекущиеДанные = Элементы.ДеревоПереписки.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ПоказатьЗначение( , ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПерепискиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	ТекущиеДанные = Элементы.ДеревоПереписки.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ПоказатьЗначение( , ТекущиеДанные.Ссылка);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыГруппировки

&НаКлиенте
Процедура ГруппировкиПриАктивизацииСтроки(Элемент)
	
	НоваяГруппировка = Неопределено;
	ТекущиеДанные = Элементы.Группировки.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		НоваяГруппировка = ТекущиеДанные.Группировка;
	КонецЕсли;
	
	Если НоваяГруппировка = ТекущаяГруппировка Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяГруппировка = НоваяГруппировка;
	ПодключитьОбработчикОжидания("ОбновитьОтборПоГруппировкамОбработчикОжидания", 0.2, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Команды отбора

&НаКлиенте
Процедура ОтборДобавленВручную(Команда)
	
	Причина = ПредопределенноеЗначение(
		"Перечисление.ПричиныДобавленияВМоиДокументы.ДобавленВручную");
	
	НайденныйЭлемент = ОтмеченныеПричины.НайтиПоЗначению(Причина);
	Если НайденныйЭлемент = Неопределено Тогда 
		ОтмеченныеПричины.Добавить(Причина);
	Иначе
		ОтмеченныеПричины.Удалить(НайденныйЭлемент);
	КонецЕсли;	
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборНазначенаЗадачаМне(Команда)
	
	Причина = ПредопределенноеЗначение(
		"Перечисление.ПричиныДобавленияВМоиДокументы.НазначенаЗадачаМне");
	
	НайденныйЭлемент = ОтмеченныеПричины.НайтиПоЗначению(Причина);
	Если НайденныйЭлемент = Неопределено Тогда 
		ОтмеченныеПричины.Добавить(Причина);
	Иначе
		ОтмеченныеПричины.Удалить(НайденныйЭлемент);
	КонецЕсли;
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтправленаЗадачаОтМеня(Команда)
	
	Причина = ПредопределенноеЗначение(
		"Перечисление.ПричиныДобавленияВМоиДокументы.ОтправленаЗадачаОтМеня");
	
	НайденныйЭлемент = ОтмеченныеПричины.НайтиПоЗначению(Причина);
	Если НайденныйЭлемент = Неопределено Тогда 
		ОтмеченныеПричины.Добавить(Причина);
	Иначе
		ОтмеченныеПричины.Удалить(НайденныйЭлемент);
	КонецЕсли;
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборЯвляюсьАвторомДокумента(Команда)
	
	Причина = ПредопределенноеЗначение(
		"Перечисление.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомДокумента");
	
	НайденныйЭлемент = ОтмеченныеПричины.НайтиПоЗначению(Причина);
	Если НайденныйЭлемент = Неопределено Тогда 
		ОтмеченныеПричины.Добавить(Причина);
	Иначе
		ОтмеченныеПричины.Удалить(НайденныйЭлемент);
	КонецЕсли;
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборЯвляюсьОтветственнымЗаДокумент(Команда)
	
	Причина = ПредопределенноеЗначение(
		"Перечисление.ПричиныДобавленияВМоиДокументы.ЯвляюсьОтветственнымЗаДокумент");
	
	НайденныйЭлемент = ОтмеченныеПричины.НайтиПоЗначению(Причина);
	Если НайденныйЭлемент = Неопределено Тогда 
		ОтмеченныеПричины.Добавить(Причина);
	Иначе
		ОтмеченныеПричины.Удалить(НайденныйЭлемент);
	КонецЕсли;
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборЯвляюсьПодписантомДокумента(Команда)
	
	Причина = ПредопределенноеЗначение(
		"Перечисление.ПричиныДобавленияВМоиДокументы.ЯвляюсьПодписантомДокумента");
	
	НайденныйЭлемент = ОтмеченныеПричины.НайтиПоЗначению(Причина);
	Если НайденныйЭлемент = Неопределено Тогда 
		ОтмеченныеПричины.Добавить(Причина);
	Иначе
		ОтмеченныеПричины.Удалить(НайденныйЭлемент);
	КонецЕсли;
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборЯвляюсьАвторомУтверждения(Команда)
	
	Причина = ПредопределенноеЗначение(
		"Перечисление.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомУтвержденияДокумента");
	
	НайденныйЭлемент = ОтмеченныеПричины.НайтиПоЗначению(Причина);
	Если НайденныйЭлемент = Неопределено Тогда 
		ОтмеченныеПричины.Добавить(Причина);
	Иначе
		ОтмеченныеПричины.Удалить(НайденныйЭлемент);
	КонецЕсли;
	
	ОбновитьНаСервере();
	
КонецПроцедуры

// Команды работы с файлами

&НаКлиенте
Процедура СоздатьФайл(Команда)
	
	ПараметрыДобавления = Новый Структура;
	ФайлыПередНачаломДобавленияКлиент(Элементы.ФайлыДокумента, Ложь, Ложь, Неопределено, Ложь, ПараметрыДобавления);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлВыполнить(Команда)
	
	ТекущиеДанные = ТекущиеДанныеСпискаФайлов();
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	ОткрытьФайл(ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура Редактировать(Команда)
	
	ТекущиеДанные = ТекущиеДанныеСпискаФайлов();
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
			
	КомандыРаботыСФайламиКлиент.Редактировать(ТекущиеДанные.Ссылка);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактирование(Команда)
	
	ТекущиеДанные = ТекущиеДанныеСпискаФайлов();
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыОбновленияФайла = РаботаСФайламиКлиент.ПараметрыОбновленияФайла(Неопределено, 
		ТекущиеДанные.Ссылка, УникальныйИдентификатор);
	ПараметрыОбновленияФайла.ХранитьВерсии = ТекущиеДанные.ХранитьВерсии;
	ПараметрыОбновленияФайла.РедактируетТекущийПользователь = ТекущиеДанные.РедактируетТекущийПользователь;
	ПараметрыОбновленияФайла.Редактирует = ТекущиеДанные.Редактирует;
	РаботаСФайламиКлиент.ЗакончитьРедактированиеСОповещением(ПараметрыОбновленияФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	ТекущиеДанные = Элементы.ФайлыДокумента.ТекущиеДанные;
	ВыделенныеСтроки = Элементы.ФайлыДокумента.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() > 1 Тогда
		СписокФайловДляВыгрузки = Новый СписокЗначений;
		Для Каждого ВыбраннаяСтрока Из ВыделенныеСтроки Цикл
			ДанныеСтроки = Элементы.ФайлыДокумента.ДанныеСтроки(ВыбраннаяСтрока);	
			СписокФайловДляВыгрузки.Добавить(ДанныеСтроки.Ссылка);
		КонецЦикла;
		
		Если СписокФайловДляВыгрузки.Количество() > 0 Тогда
			РаботаСФайламиКлиент.СохранитьФайлыКак(СписокФайловДляВыгрузки, УникальныйИдентификатор);
		КонецЕсли;
	Иначе
		Если ТекущиеДанные = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляСохранения(
			ТекущиеДанные.Ссылка, Неопределено, УникальныйИдентификатор);
		КомандыРаботыСФайламиКлиент.СохранитьКак(ДанныеФайла, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Напечатать(Команда)
	
	#Если ВебКлиент Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'В Веб-клиенте печать файлов не поддерживается.'"));
		Возврат;
	#КонецЕсли
	
	СистемнаяИнфо = Новый СистемнаяИнформация;
	Если СистемнаяИнфо.ТипПлатформы <> ТипПлатформы.Windows_x86 
	   И СистемнаяИнфо.ТипПлатформы <> ТипПлатформы.Windows_x86_64 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Печать файлов возможна только в Windows.'"));
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ФайлыДокумента.ТекущиеДанные;
	ВыделенныеСтроки = Элементы.ФайлыДокумента.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() > 1 Тогда
		МассивФайлов = Новый Массив;
		Для Каждого ВыбраннаяСтрока Из ВыделенныеСтроки Цикл
			ДанныеСтроки = Элементы.ФайлыДокумента.ДанныеСтроки(ВыбраннаяСтрока);	
			МассивФайлов.Добавить(ДанныеСтроки.Ссылка);
		КонецЦикла;
		
		Если МассивФайлов.Количество() > 0 Тогда
			
			ДанныеФайлов = РаботаСФайламиВызовСервера.ДанныеФайловДляОткрытия(
				МассивФайлов, 
				УникальныйИдентификатор);
				
			КомандыРаботыСФайламиКлиент.НапечататьФайлы(ДанныеФайлов);
			
		КонецЕсли;
	Иначе
		Если ТекущиеДанные = Неопределено Тогда 
			Возврат;
		КонецЕсли;	
	
		ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(
			ТекущиеДанные.Ссылка, 
			Неопределено, 
			УникальныйИдентификатор, 
			Неопределено, 
			ПредыдущийАдресФайла);
		
		КомандыРаботыСФайламиКлиент.НапечататьФайл(ДанныеФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзФайлаНаДиске(Команда)
	
	ТекущиеДанные = ТекущиеДанныеСпискаФайлов();
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаИРабочийКаталог(ТекущиеДанные.Ссылка);
	
	РаботаСФайламиКлиент.ОбновитьИзФайлаНаДискеСОповещением(
		Неопределено,
		ДанныеФайла,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокФайлов(Команда)
	
	ОбновитьМиникарточку();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьУдаленныеФайлы(Команда)
	
	ПоказыватьУдаленныеФайлы = Не ПоказыватьУдаленныеФайлы;
	
	ОбновитьМиникарточку();
	
	Элементы.ПоказыватьУдаленныеФайлы.Пометка = ПоказыватьУдаленныеФайлы;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения(Команда)
	
	ТекущиеДанные = ТекущиеДанныеСпискаФайлов();
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	РаботаСФайламиКлиент.СохранитьИзмененияФайлаСОповещением(
		Неопределено,
		ТекущиеДанные.Ссылка,
		УникальныйИдентификатор);
	
КонецПроцедуры

// Команды работы со списком документов

&НаКлиенте
Процедура ДобавитьВСписокВыполнить(Команда)
	
	ДобавитьВСписок();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗадачу(Команда)
	
	ТекущиеДанные = Элементы.СписокЗадачи.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
		
	РаботаСЗадачамиКлиент.ОткрытьКарточкуИсполнителяЗадачи(
		ТекущиеДанные.ДействиеЗадачи,
		Неопределено,
		Ложь);
	
КонецПроцедуры
// Команды сортировки

&НаКлиенте
Процедура СортироватьДатеДокумента(Команда)
	
	СортироватьПоКолонке("ДатаДокумента");
	
КонецПроцедуры

&НаКлиенте
Процедура СортироватьПоНаименованию(Команда)
	
	СортироватьПоКолонке("Наименование");
	
КонецПроцедуры

&НаКлиенте
Процедура СортироватьПоВидуДокумента(Команда)
	
	СортироватьПоКолонке("ВидДокумента");
	
КонецПроцедуры

// Команды группировки

&НаКлиенте
Процедура СгруппироватьПричинеДобавления(Команда)
	
	СгруппироватьПоКолонке("ПричинаДобавления");
	
КонецПроцедуры

&НаКлиенте
Процедура СгруппироватьПоБезГруппировки(Команда)
	
	СгруппироватьПоКолонке("");
	
КонецПроцедуры

&НаКлиенте
Процедура СгруппироватьПоПроекту(Команда)
	
	СгруппироватьПоКолонке("Проект");
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьКакПрочтенные(Команда)
	
	Сообщения = ПолучитьВыбранныеДокументы(Истина);
	КоличествоСообщений = Сообщения.Количество();
	Если Сообщения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЧислоПрочтенных = 0;
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	Для каждого Ссылка Из ВыделенныеСтроки Цикл
		
		Если ТипЗнч(Ссылка) <> Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
			
			Прочтен = Элементы.Список.ДанныеСтроки(Ссылка).Прочтен;
			
			Если Прочтен Тогда
				
				ЧислоПрочтенных = ЧислоПрочтенных + 1;
				
			КонецЕсли;	
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПоставитьПометкуПрочтения = Истина;
	
	Если КоличествоСообщений = ЧислоПрочтенных Тогда
		ПоставитьПометкуПрочтения = Ложь;
	Иначе
		ПоставитьПометкуПрочтения = Истина;
	КонецЕсли;	
	
	Если КоличествоСообщений > 1 Тогда
		Если ПоставитьПометкуПрочтения Тогда
			Состояние(НСтр("ru = 'Документы помечаются как прочтенные. Пожалуйста подождите...'"));
		Иначе
			Состояние(НСтр("ru = 'Снимается пометка прочтенности. Пожалуйста подождите...'"));
		КонецЕсли;	
	КонецЕсли;
	
	ОповеститьОПрочтении = УстановитьПрочтение(Сообщения, ПоставитьПометкуПрочтения);
	
	РаботаСПрочтениямиКлиент.ОповеститьОПрочтении(Сообщения, ОповеститьОПрочтении);
	Если КоличествоСообщений > 1 Тогда
		
		Если ПоставитьПометкуПрочтения Тогда
			ТекстСостояния = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Документы помечены как прочтенные (%1)'"),
				КоличествоСообщений);
		Иначе
			ТекстСостояния = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Сняты пометки прочтенности документов (%1)'"),
				КоличествоСообщений);
		КонецЕсли;
			
		Состояние(ТекстСостояния);
		
	КонецЕсли;
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура МК_СтраницаСодержание(Команда)
	
	МК_ПереключитьНаСтраницу(ЭтотОбъект, Элементы.МК_СтраницаСодержание, Элементы.МК_КнопкаСтраницаСодержание);
	
КонецПроцедуры

&НаКлиенте
Процедура МК_СтраницаЗадачиСвязи(Команда)
	
	МК_ПереключитьНаСтраницу(ЭтотОбъект, Элементы.МК_СтраницаЗадачиСвязи, Элементы.МК_КнопкаСтраницаЗадачиСвязи);
	
КонецПроцедуры

&НаКлиенте
Процедура МК_ПоказатьСкрытьФайлы(Команда)
	
	Если КоличествоФайлов = 0 Тогда
		Элементы.ФайлыДокумента.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.ФайлыДокумента.Видимость = Не Элементы.ФайлыДокумента.Видимость;
	Элементы.СоздатьФайл.Видимость = Элементы.ФайлыДокумента.Видимость;
	
	МК_КлиентСервер.ОформитьДополнительнуюКнопкуПоСостоянию(МК_ЭлементыСтиля,
		Элементы.МК_ПоказатьСкрытьФайлы,
		Элементы.ФайлыДокумента.Видимость);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПереписку(Команда)
	
	ОбновитьДеревоПереписки();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СортироватьПоКолонке(Знач ИмяКолонки)
		
	Если СортироватьПо = ИмяКолонки Тогда
		Если НаправлениеСортировки = "Возр" Тогда
			НаправлениеСортировки = "Убыв";
		Иначе
			НаправлениеСортировки = "Возр";
		КонецЕсли;
	Иначе
		СортироватьПо = ИмяКолонки;
		НаправлениеСортировки = "Возр";
	КонецЕсли;
	
	ОбновитьПорядок();
	УстановитьПометкуРежимуСортировки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПометкуРежимуСортировки(Форма)
	
	Форма.Элементы.СортироватьДатеДокумента.Пометка = Ложь;
	Форма.Элементы.СортироватьДатеДокумента.Заголовок = НСтр("ru = 'Дата документа'");
	
	Форма.Элементы.СортироватьПоНаименованию.Пометка = Ложь;
	Форма.Элементы.СортироватьПоНаименованию.Заголовок = НСтр("ru = 'Наименование'");
	
	Форма.Элементы.СортироватьПоВидуДокумента.Пометка = Ложь;
	Форма.Элементы.СортироватьПоВидуДокумента.Заголовок = НСтр("ru = 'Вид документа'");
	
	Если Форма.СортироватьПо = "ДатаДокумента" Тогда
		Форма.Элементы.СортироватьДатеДокумента.Пометка = Истина;
		Форма.Элементы.СортироватьДатеДокумента.Заголовок = 
			СтрШаблон(НСтр("ru = 'Дата документа (%1)'"),
				?(Форма.НаправлениеСортировки = "Возр", НСтр("ru = 'Возр'"), НСтр("ru = 'Убыв'")));
	ИначеЕсли Форма.СортироватьПо = "Наименование" Тогда
		Форма.Элементы.СортироватьПоНаименованию.Пометка = Истина;
		Форма.Элементы.СортироватьПоНаименованию.Заголовок = 
			СтрШаблон(НСтр("ru = 'Наименование (%1)'"),
				?(Форма.НаправлениеСортировки = "Возр", НСтр("ru = 'Возр'"), НСтр("ru = 'Убыв'")));
	ИначеЕсли Форма.СортироватьПо = "ВидДокумента" Тогда
		Форма.Элементы.СортироватьПоВидуДокумента.Пометка = Истина;
		Форма.Элементы.СортироватьПоВидуДокумента.Заголовок = 
			СтрШаблон(НСтр("ru = 'Вид документа (%1)'"),
				?(Форма.НаправлениеСортировки = "Возр", НСтр("ru = 'Возр'"), НСтр("ru = 'Убыв'")));
	КонецЕсли;
	
	Форма.Элементы.КонтекстноеМенюСортироватьДатеДокумента.Пометка = 
		Форма.Элементы.СортироватьДатеДокумента.Пометка;
	Форма.Элементы.КонтекстноеМенюСортироватьДатеДокумента.Заголовок = 
		Форма.Элементы.СортироватьДатеДокумента.Заголовок;
	
	Форма.Элементы.КонтекстноеМенюСортироватьПоНаименованию.Пометка = 
		Форма.Элементы.СортироватьПоНаименованию.Пометка;
	Форма.Элементы.КонтекстноеМенюСортироватьПоНаименованию.Заголовок = 
		Форма.Элементы.СортироватьПоНаименованию.Заголовок;
	
	Форма.Элементы.КонтекстноеМенюСортироватьПоВидуДокумента.Пометка = 
		Форма.Элементы.СортироватьПоВидуДокумента.Пометка;
	Форма.Элементы.КонтекстноеМенюСортироватьПоВидуДокумента.Заголовок = 
		Форма.Элементы.СортироватьПоВидуДокумента.Заголовок;
	
КонецПроцедуры

&НаКлиенте
Процедура СгруппироватьПоКолонке(НовыйВидГруппировки)
	
	Если ВидГруппировки = НовыйВидГруппировки Тогда
		Возврат;
	КонецЕсли;
	
	ВидГруппировки = НовыйВидГруппировки;
	ПоказыватьОбластьГруппировки = ВидГруппировки <> "";
	
	СгруппироватьПоКолонкеНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СгруппироватьПоКолонкеНаСервере()
	
	ОбновитьЭлементыФормы();
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтборПоГруппировкамОбработчикОжидания()
	
	ОбновитьОтборПоГруппировкамНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтборПоГруппировкамНаСервере()
	
	ОбновитьОтборПоПричинам();
	ОбновитьОтборПоПроекту();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтборПоПроекту()
	
	Если ПоказыватьОбластьГруппировки
		И ВидГруппировки = "Проект" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Проект",
			ТекущаяГруппировка,,,
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "Проект");
	КонецЕсли;
	
КонецПроцедуры

// Работа со списком документов

&НаСервере
Процедура ОбновитьНаСервере()
	
	Элементы.Список.Обновить();
	ОбновитьПометкуПричин();
	ОбновитьЗаголовок();
	ОбновитьГруппировки();
	ОбновитьПорядок();
	ОбновитьОтборПоГруппировкамНаСервере();
	
КонецПроцедуры

&НаСервере
Функция КоличествоПоКлючу(ДанныеИтогов)
	
	Если КлючНазначенияИспользования = "ОтображениеПросроченныхДокументов" Тогда
		КоличествоПоКлючу = ДанныеИтогов.Просроченных;
	ИначеЕсли КлючНазначенияИспользования = "ОтображениеИстекающихДокументов" Тогда
		КоличествоПоКлючу = ДанныеИтогов.Истекающих;
	ИначеЕсли КлючНазначенияИспользования = "ОтображениеДокументовБезОтвета" Тогда
		КоличествоПоКлючу = ДанныеИтогов.БезОтвета;
	Иначе
		КоличествоПоКлючу = ДанныеИтогов.Всего;
	КонецЕсли;
	
	Возврат КоличествоПоКлючу
	
КонецФункции

&НаКлиенте
Процедура УдалитьИзСписка()
	
	НаименованиеДокумента = ""; 
	МассивДокументов = Новый Массив;
	
	Для Каждого ВыбраннаяСтрока Из Элементы.Список.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.Список.ДанныеСтроки(ВыбраннаяСтрока);
		МассивДокументов.Добавить(ДанныеСтроки.Документ);
		НаименованиеДокумента = ДанныеСтроки.Наименование;
	КонецЦикла;
	
	Если МассивДокументов.Количество() = 0 Тогда
		Возврат;
		
	ИначеЕсли МассивДокументов.Количество() > 1 Тогда 
		ТекстВопроса = НСтр("ru = 'Удалить выделенные документы из списка Мои документы?'");
		
	Иначе 
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Удалить документ ""%1"" из списка Мои документы?'"),
			Строка(НаименованиеДокумента));
		
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьИзСпискаПродолжение", 
		ЭтотОбъект, Новый Структура("МассивДокументов", МассивДокументов));
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, 
		РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьИзСпискаПродолжение(Результат, Параметр) Экспорт 
	
	Если Результат <> КодВозвратаДиалога.Да Тогда 
		Возврат;
	КонецЕсли;
	
	МоиДокументыКлиент.УдалитьИзМоихДокументов(Параметр.МассивДокументов);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВСписок()
	
	ОткрытьФорму("Справочник.ДокументыПредприятия.ФормаВыбора", , ЭтотОбъект);
	
КонецПроцедуры

// Заполнение миникарточки

&НаКлиенте
Процедура ОбновитьМиникарточку()
	
	Попытка
		
		ОбзорСпискаДокументовКлиент.ОбновитьМиникарточку(ЭтотОбъект);
		ОбновитьДеревоПереписки();
		Если Файлы.ПолучитьЭлементы().Количество() = 0 Тогда 
			ТекущиеДанные = ТекущиеДанныеСпискаФайлов();
			ОбзорСпискаДокументовКлиент.УстановитьДоступностьКоманд(ТекущиеДанные, ЭтотОбъект);
		КонецЕсли;
		
		Если ЭтоМобильныйКлиент Тогда
			МК_ОбновитьЗаголовкиПредпросмотра(ЭтотОбъект);
		КонецЕсли;
		
	Исключение
		
		ОбзорHTML = "<html><body></body></html>";
		Файлы.ПолучитьЭлементы().Очистить();
		ЗаголовокСодержаниеФайлы = НСтр("ru = 'Содержание, Файлы'");
		ЗаголовокЗадачиСвязи = НСтр("ru = 'Задачи'");
		СписокЗадачи.Очистить();
		ДеревоПереписки.ПолучитьЭлементы().Очистить();
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры	

// Работа с файлами

&НаСервере
Процедура ПометитьФайлыНаУдаление(МассивФайлов, ЗначениеПометкиУдаления)
	
	Делопроизводство.ПометитьФайлыНаУдаление(ЭтотОбъект, МассивФайлов, 
		ЗначениеПометкиУдаления, Истина);
	
КонецПроцедуры

&НаКлиенте
Функция ТекущиеДанныеСпискаФайлов()
	
	ТекущиеДанные = Элементы.ФайлыДокумента.ТекущиеДанные;
	
	Возврат ТекущиеДанные;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФайл(Файл)
	
	Если Не ЗначениеЗаполнено(Файл) Тогда 
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(
		Файл, 
		Неопределено, 
		УникальныйИдентификатор, 
		Неопределено, 
		ПредыдущийАдресФайла);
		
	КомандыРаботыСФайламиКлиент.Открыть(ДанныеФайла);
	
КонецПроцедуры	

&НаКлиенте
Процедура ВыборФайла(Файл)
	
	КакОткрывать = ФайловыеФункцииКлиентПовтИсп.
		ПолучитьПерсональныеНастройкиРаботыСФайлами().
		ДействиеПоДвойномуЩелчкуМыши;
		
	Если КакОткрывать = "ОткрыватьКарточку" Тогда
		ПоказатьЗначение(, Файл);
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(Файл, 
		Неопределено, 
		УникальныйИдентификатор, 
		Неопределено, 
		ПредыдущийАдресФайла);
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("ДанныеФайла", ДанныеФайла);
	Обработчик = Новый ОписаниеОповещения("СписокВыборПослеВыбораРежимаРедактирования", 
		ЭтотОбъект, ПараметрыОбработчика);
	
	РаботаСФайламиКлиент.ВыбратьРежимИРедактироватьФайл(Обработчик, ДанныеФайла, Истина);
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьСтрокуФайла(Файл)
	
	ДелопроизводствоКлиент.ОбновитьСтрокуФайла(Файлы, Файл, Истина);
	
КонецПроцедуры	

&НаКлиенте
Процедура СписокВыборПослеВыбораРежимаРедактирования(Результат, ПараметрыВыполнения) Экспорт
	
	РезультатОткрыть = "Открыть";
	РезультатРедактировать = "Редактировать";
	РезультатОткрытьКарточку = "ОткрытьКарточку";
	
	Если Результат = РезультатРедактировать Тогда
		Обработчик = Новый ОписаниеОповещения("СписокВыборПослеРедактированияФайла", 
			ЭтотОбъект, ПараметрыВыполнения);
		РаботаСФайламиКлиент.РедактироватьФайл(Обработчик, ПараметрыВыполнения.ДанныеФайла);
	ИначеЕсли Результат = РезультатОткрыть Тогда
		РаботаСФайламиКлиент.ОткрытьФайлСОповещением(Неопределено, 
			ПараметрыВыполнения.ДанныеФайла, УникальныйИдентификатор); 
	ИначеЕсли Результат = РезультатОткрытьКарточку Тогда
		ПоказатьЗначение(, ПараметрыВыполнения.ДанныеФайла.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборПослеРедактированияФайла(Результат, ПараметрыВыполнения) Экспорт
	
	ОбновитьСтрокуФайла(ПараметрыВыполнения.ДанныеФайла.Ссылка);
	
	ТекущиеДанные = ТекущиеДанныеСпискаФайлов();
	ОбзорСпискаДокументовКлиент.УстановитьДоступностьКоманд(ТекущиеДанные, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущуюСтрокуФайла(Файл)
	
	СтрокаИдентификатор = Неопределено;
	
	Для Каждого Строка Из Файлы.ПолучитьЭлементы() Цикл
		Если Не Строка.ЭтоРольФайла И Не Строка.ЭтоДокументЭДО Тогда
			Если Строка.Ссылка = Файл Тогда 
				СтрокаИдентификатор = Строка.ПолучитьИдентификатор();
				Прервать;
			КонецЕсли;	
		Иначе
			
			Для Каждого Подстрока Из Строка.ПолучитьЭлементы() Цикл
				Если Подстрока.Ссылка = Файл Тогда 
					СтрокаИдентификатор = Подстрока.ПолучитьИдентификатор();
					Прервать;
				КонецЕсли;	
			КонецЦикла;	
			
		КонецЕсли;	
			
	КонецЦикла;
	
	Если СтрокаИдентификатор = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Элементы.ФайлыДокумента.ТекущаяСтрока = СтрокаИдентификатор;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьПризнакОригиналУНовогоФайла(Файл)
	
	Если Не ВестиУчетСканКопийОригиналовДокументов Тогда 
		Возврат;
	КонецЕсли;	
	
	НайденнаяСтрока = Неопределено;
	Для Каждого Строка Из Файлы.ПолучитьЭлементы() Цикл
		
		Если Строка.Ссылка = Файл Тогда 
			НайденнаяСтрока = Строка;
			Прервать;
		КонецЕсли;	
		
		Для Каждого Подстрока Из Строка.ПолучитьЭлементы() Цикл
			Если Подстрока.Ссылка = Файл Тогда 
				НайденнаяСтрока = Строка;
				Прервать;
			КонецЕсли;	
		КонецЦикла;		
		
	КонецЦикла;	
	
	Если НайденнаяСтрока = Неопределено
	 Или НайденнаяСтрока.СозданИзШаблона
	 Или НайденнаяСтрока.Оригинал Тогда  
	 Возврат;
	КонецЕсли;	
	
	ДоступноТолькоДобавлениеОригинала = ТипЗнч(ДоступныеПоля) = Тип("Структура") 
		И ДоступныеПоля.Свойство("ДобавлениеОригиналов") 
		И Не ДоступныеПоля.Свойство("ДобавлениеФайлов");
	
	Если ДоступноТолькоДобавлениеОригинала Или
		(ДелопроизводствоКлиентСервер.ЭтоРасширениеСканКопии(НайденнаяСтрока.Расширение) 
		И Не Делопроизводство.ЕстьСведенияОбОригиналеФайла(Файл)) Тогда 
		НайденнаяСтрока.Оригинал = Истина;
		Делопроизводство.СохранитьСведенияОбОригиналеФайла(Файл, ТекущийДокумент);
	КонецЕсли;		
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьВыбранныйДокумент(ВыводитьПредупреждение)
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Если ВыводитьПредупреждение Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Не выбран документ.'"));
		КонецЕсли;
		Возврат Неопределено;
	КонецЕсли;
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Если ВыводитьПредупреждение Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Не выбран документ.'"));
		КонецЕсли;
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не Элементы.Список.ТекущиеДанные.Свойство("Документ") Тогда
		Если ВыводитьПредупреждение Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Не выбран документ.'"));
		КонецЕсли;
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеСтроки = Элементы.Список.ДанныеСтроки(ТекущаяСтрока);
	Ссылка = ДанныеСтроки.Документ;
	Возврат Ссылка;
	
КонецФункции

&НаКлиенте
Функция ПолучитьВыбранныеДокументы(ВыводитьПредупреждение)
	
	Результат = Новый Массив;
	
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	Для каждого Строка Из ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.Список.ДанныеСтроки(Строка);
		Если ДанныеСтроки <> Неопределено Тогда
			Ссылка = ДанныеСтроки.Документ;
			Результат.Добавить(Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Если Результат.Количество() = 0 Тогда
		ВыбраннаяСтрока = ПолучитьВыбранныйДокумент(Ложь);
		Если ЗначениеЗаполнено(ВыбраннаяСтрока)
			И Результат.Найти(ВыбраннаяСтрока) = Неопределено Тогда
			Результат.Добавить(ВыбраннаяСтрока);
		КонецЕсли;
	КонецЕсли;
	
	Если Результат.Количество() = 0 Тогда
		Если ВыводитьПредупреждение Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Не выбраны документы.'"));
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#Область СлужебныеПроцедурыИФункции_МобильныйКлиент

&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	МК_ЭлементыСтиля = МК.ЭлементыСтиля();
	
	// Общая адаптация.
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Половинный;
	
	// Нижний блок.
	Элементы.Переместить(Элементы.ФормаСоздать, Элементы.МК_НижнийБлок);
	МК.ОформитьАкцентнуюКнопку(Элементы.ФормаСоздать);
	
	// Список.
	Если ПоказыватьОбластьГруппировки Тогда
		Элементы.ГруппаСписок.Отображение = ОтображениеОбычнойГруппы.ОбычноеВыделение;
	КонецЕсли;
	
	Элементы.СписокКонтекстноеМенюОтменитьПоиск.Видимость = Ложь;
	Элементы.СписокКонтекстноеМенюНайти.Видимость = Ложь;
	Элементы.СписокКонтекстноеМенюКопироватьВБуферОбмена.Видимость = Ложь;
	
	// Командная панель.
	Элементы.СписокУдалить.Видимость = Ложь;
	Элементы.ФормаНайти.Видимость = Ложь;
	Элементы.ФормаОтменитьПоиск.Видимость = Ложь;
	Элементы.СписокОбновить.Видимость = Ложь;
	Элементы.СписокВывестиСписок.Видимость = Ложь;
	Элементы.СтрокаПоиска.Видимость = Ложь;
	
	ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВДополнительномПодменю;
	
	Элементы.ФормаДобавитьВСписок.Заголовок = НСтр("ru = 'Добавить существующий'");
	Элементы.Переместить(Элементы.ФормаПометитьКакПрочтенные, Элементы.Список.КонтекстноеМеню);
	
	Элементы.СписокСправочникДокументыПредприятияСоздатьДокументПредприятияНаОсновании.ПоложениеВКоманднойПанели = ПоложениеВКоманднойПанели;
	Элементы.СписокСправочникМероприятияСоздатьНаОсновании.ПоложениеВКоманднойПанели = ПоложениеВКоманднойПанели;
	Элементы.СписокСправочникЗаписиРабочегоКалендаряСоздатьЗаписьКалендаряНаОсновании.ПоложениеВКоманднойПанели = ПоложениеВКоманднойПанели;
	Элементы.ОтправитьОбъекты.ПоложениеВКоманднойПанели = ПоложениеВКоманднойПанели;
	
	Если Не Элементы.Найти("СписокОбщаяКомандаСоздатьЗаписьЖурналаПередачи") = Неопределено Тогда
		Элементы.СписокОбщаяКомандаСоздатьЗаписьЖурналаПередачи.ПоложениеВКоманднойПанели = ПоложениеВКоманднойПанели;
	КонецЕсли;
	
	МК_НастроитьОбластьПредпросмотра();

КонецПроцедуры

&НаСервере
Процедура МК_НастроитьОбластьПредпросмотра()
	
	Элементы.МК_Миникарточка.Видимость = ЭтоМобильноеУстройствоСБольшимЭкраном;
	Элементы.Страницы.Видимость = Не ЭтоМобильныйКлиент;
	
	Если Не ЭтоМобильноеУстройствоСБольшимЭкраном Тогда
		Элементы.Список.ИспользованиеТекущейСтроки = ИспользованиеТекущейСтрокиТаблицы.Выбор;
		Элементы.ГруппаСписокМиникарточка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаСписокМиникарточка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	Элементы.Список.ИспользованиеТекущейСтроки = ИспользованиеТекущейСтрокиТаблицы.ОтображениеВыделенияИВыбор;

	Элементы.Переместить(Элементы.ОбзорHTML, Элементы.МК_СтраницаСодержание, Элементы.МК_Файлы);
	Элементы.Переместить(Элементы.ФайлыДокумента, Элементы.МК_Файлы);
	Элементы.Переместить(Элементы.СписокЗадачи, Элементы.МК_СтраницаЗадачиСвязи);
	
	МК.ОформитьКнопкуФильтра(Элементы.МК_КнопкаСтраницаСодержание, Истина);
	МК.ОформитьКнопкуФильтра(Элементы.МК_КнопкаСтраницаЗадачиСвязи, Истина);
	
	МК_СброситьОформлениеПереключателейСтраницПредпросмотра(ЭтотОбъект);
	МК_ПереключитьНаСтраницу(ЭтотОбъект, Элементы.МК_СтраницаСодержание, Элементы.МК_КнопкаСтраницаСодержание);
	
	// Файлы.
	Элементы.ФайлыДокумента.Видимость = КоличествоФайлов > 0;
	МК.ОформитьДополнительнуюКнопкуПоСостоянию(Элементы.МК_ПоказатьСкрытьФайлы, Элементы.ФайлыДокумента.Видимость);
	
	Элементы.ФайлыДокумента.Шапка = Ложь;
	Элементы.ФайлыДокумента.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	Элементы.ФайлыДокумента.ПоложениеСтрокиПоиска = ПоложениеСтрокиПоиска.ПотянутьСверху;
	Элементы.Переместить(Элементы.СоздатьФайл, Элементы.МК_ЗаголовокФайлы, Элементы.МК_ФайлыНадпись);
	МК.ОформитьКнопкуФильтра(Элементы.СоздатьФайл, Истина);
	Элементы.ФайлыРольФайла.Видимость = Ложь;
	Элементы.ФайлыОписание.Видимость = Ложь;
	Элементы.Оригинал.Видимость = Ложь;
	Элементы.ФайлыНаименование.Высота = 1;
	
	// Задачи.
	Элементы.СписокЗадачи.Шапка = Ложь;
	Элементы.Переместить(Элементы.ЗадачиГруппа, Элементы.СписокЗадачиГруппа);
	Элементы.ЗадачиНаименование.Высота = 3;
	Элементы.ЗадачиИсполнитель.ЦветТекста = ЦветаСтиля.МК_ЦветАвтораИсполнителя;
	Элементы.ЗадачиИсполнитель.Шрифт = ШрифтыСтиля.МК_ШрифтАвтораИсполнителя;
	
	МК_ОбновитьЗаголовкиПредпросмотра(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура МК_СброситьОформлениеПереключателейСтраницПредпросмотра(Форма)
	
	МК_КлиентСервер.ОформитьДополнительнуюКнопкуПоСостоянию(Форма.МК_ЭлементыСтиля,
		Форма.Элементы.МК_КнопкаСтраницаСодержание, Ложь);
	
	МК_КлиентСервер.ОформитьДополнительнуюКнопкуПоСостоянию(Форма.МК_ЭлементыСтиля,
		Форма.Элементы.МК_КнопкаСтраницаЗадачиСвязи, Ложь);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура МК_ПереключитьНаСтраницу(Форма, Страница, Кнопка)
	
	Форма.Элементы.МК_Страницы.ТекущаяСтраница = Страница;
	МК_СброситьОформлениеПереключателейСтраницПредпросмотра(Форма);
	МК_КлиентСервер.ОформитьДополнительнуюКнопкуПоСостоянию(Форма.МК_ЭлементыСтиля, Кнопка, Истина);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура МК_ОбновитьЗаголовкиПредпросмотра(Форма)
	
	// Для файлов.
	Если Форма.КоличествоФайлов > 0 Тогда 
		Форма.Элементы.МК_ФайлыНадпись.Заголовок = СтрШаблон(НСтр("ru = 'Файлы (%1)'"), Форма.КоличествоФайлов);
	Иначе
		Форма.Элементы.МК_ФайлыНадпись.Заголовок = НСтр("ru = 'Файлы'");
	КонецЕсли;
	
	// Для страницы "Задачи".
	Если Форма.КоличествоЗадач > 0 Тогда 
		Форма.Элементы.МК_КнопкаСтраницаЗадачиСвязи.Заголовок = СтрШаблон(НСтр("ru = 'Задачи (%1)'"), Форма.КоличествоЗадач);
	Иначе
		Форма.Элементы.МК_КнопкаСтраницаЗадачиСвязи.Заголовок = НСтр("ru = 'Задачи'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОбновитьДеревоПереписки()

	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.ВидКорреспонденции) Тогда
		// Есть дерево переписки, обновление только если оно видно на закладке:
		Если Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаПереписка Тогда
			ОбновитьДеревоПерепискиСервер(ТекущиеДанные.Документ);
			ЭлементыДерева = ДеревоПереписки.ПолучитьЭлементы();
			Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
				ИдентификаторСтроки = ЭлементДерева.ПолучитьИдентификатор();
				Элементы.ДеревоПереписки.Развернуть(ИдентификаторСтроки, Истина);
				Если ЭлементДерева.Ссылка = ТекущиеДанные.Документ Тогда
					Элементы.ДеревоПереписки.ТекущаяСтрока = ИдентификаторСтроки;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		// Нет дерева переписки:
		Если Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаПереписка Тогда
			ДеревоПереписки.ПолучитьЭлементы().Очистить();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Обновить дерево переписки на сервере.
// 
// Параметры:
//  Документ - СправочникСсылка.ДокументыПредприятия - Документ, по которому генерируется дерево переписки
&НаСервере
Процедура ОбновитьДеревоПерепискиСервер(Знач Документ)

	Дерево = РеквизитФормыВЗначение("ДеревоПереписки");
	Дерево.Строки.Очистить();
	ВыведенныеПисьма = Новый Массив();
	Делопроизводство.ДобавитьПисьмоВДерево(Дерево.Строки, Документ, ТекущийДокумент, ВыведенныеПисьма);
	ЗначениеВРеквизитФормы(Дерево, "ДеревоПереписки");

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПеретаскиваниеФайла(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	ТекущийЭлемент = Элементы.ФайлыДокумента;
	
	Если ТекущийЭлемент.ТолькоПросмотр Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийДокумент) Тогда 

		РольФайла = Неопределено;
		ТекущиеДанные = Неопределено;
		Если Строка <> Неопределено Тогда
			ТекущиеДанные = Файлы.НайтиПоИдентификатору(Строка);
			Если ТекущиеДанные <> Неопределено Тогда
				РольФайла = ТекущиеДанные.РольФайла;
			КонецЕсли;
		КонецЕсли;
		
		Если РольФайла = Неопределено И Файлы.ПолучитьЭлементы().Количество() <> 0 Тогда	
			СтрФайлы = Файлы.ПолучитьЭлементы()[0];
			Если ЗначениеЗаполнено(СтрФайлы.РольФайла) Тогда
				РольФайла = СтрФайлы.РольФайла;
			КонецЕсли;		
		КонецЕсли;	
		
		Если ТипЗнч(ДоступныеПоля) = Тип("Структура") 
			И Не ДоступныеПоля.Свойство("СоздатьФайлОригинал") 
			И Не ДоступныеПоля.Свойство("СоздатьФайлОбычный") Тогда
			Текст = СтрШаблон(
				НСтр("ru = 'В текущем состоянии ""%1"" файлы добавлять нельзя.'"),
				СостояниеТекст);
			ПоказатьПредупреждение(, Текст);
			
			Возврат;
		
		ИначеЕсли ВестиУчетСканКопийОригиналовДокументов 
			И ТипЗнч(ДоступныеПоля) = Тип("Структура") 
			И ДоступныеПоля.Свойство("СоздатьФайлОригинал") 
			И Не ДоступныеПоля.Свойство("СоздатьФайлОбычный")
			И Не ПеретаскиваниеОригинала Тогда
			
			Текст = СтрШаблон(
				НСтр("ru = 'В текущем состоянии ""%1"" можно добавить только скан-копию оригинала документа. Продолжить?'"),
				СостояниеТекст);
			
			СписокВариантовОтветов = Новый СписокЗначений;
			СписокВариантовОтветов.Добавить(Строка(КодВозвратаДиалога.Да));
			СписокВариантовОтветов.Добавить(Строка(КодВозвратаДиалога.Нет));
			
			ПараметрыОбработчика = Новый Структура;
			ПараметрыОбработчика.Вставить("Элемент", Элемент);
			ПараметрыОбработчика.Вставить("СтандартнаяОбработка", СтандартнаяОбработка);
			ПараметрыОбработчика.Вставить("Строка", Строка);
			ПараметрыОбработчика.Вставить("Поле", Поле);
			ПараметрыОбработчика.Вставить("ПараметрыПеретаскивания", ПараметрыПеретаскивания);
			
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ФайлыПеретаскиваниеПродолжение",
				ЭтотОбъект,
				ПараметрыОбработчика);
			
			ДелопроизводствоКлиент.ПоказатьРасширеннуюФормуВопроса(ЭтотОбъект,
				НСтр("ru = 'Перетаскивание файла'"),
				Текст,
				"ДобавлениеОригинала",
				"ЗадаватьВопросПриДобавленииСканКопииОригинала",
				СписокВариантовОтветов,,
				ОписаниеОповещения);
			Возврат;
		
		КонецЕсли;
		
		Если ПеретаскиваниеОригинала Тогда 
			ТекстПредупреждения = "";
			ПараметрыПеретаскивания.Значение = ДелопроизводствоКлиент.ОтобратьДляПеретаскиванияТолькоФайлыОригиналы(
				ПараметрыПеретаскивания, СостояниеТекст, ТекстПредупреждения);
				
			Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда 
				ПоказатьПредупреждение(, ТекстПредупреждения);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		ВладелецФайлаСписка = ТекущийДокумент;
		НеОткрыватьКарточкуПослеСозданияИзФайла = Истина;
		РаботаСФайламиКлиент.ОбработкаПеретаскиванияВЛинейныйСписок(ПараметрыПеретаскивания,
			ВладелецФайлаСписка, ЭтотОбъект, НеОткрыватьКарточкуПослеСозданияИзФайла, РольФайла);
			
		ОбзорСпискаДокументовКлиент.ЗаполнитьСписокФайлов(ЭтотОбъект);
		
	КонецЕсли;
	
	ПеретаскиваниеОригинала = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередНачаломДобавленияКлиент(Элемент, Отказ, Копирование, Родитель, Группа, ПараметрыДобавления)
	
	ДобавлениеОригинала = Ложь;
	Если ВестиУчетСканКопийОригиналовДокументов Тогда
		Если ТипЗнч(ДоступныеПоля) = Тип("Структура") 
			И ДоступныеПоля.Свойство("СоздатьФайлОригинал") 
			И Не ДоступныеПоля.Свойство("СоздатьФайлОбычный") Тогда 
			
			Если Не ПараметрыДобавления.Свойство("ДобавлениеОригинала") Тогда
				Текст = НСтр("ru = 'В текущем состоянии документа можно добавить только скан-копию оригинала. Продолжить?'");
				
				СписокВариантовОтветов = Новый СписокЗначений;
				СписокВариантовОтветов.Добавить(Строка(КодВозвратаДиалога.Да));
				СписокВариантовОтветов.Добавить(Строка(КодВозвратаДиалога.Нет));
				
				ПараметрыДобавления.Вставить("ДобавлениеОригинала", ДобавлениеОригинала);
				
				ПараметрыОбработчика = Новый Структура;
				ПараметрыОбработчика.Вставить("Элемент", Элемент);
				ПараметрыОбработчика.Вставить("Отказ", Отказ);
				ПараметрыОбработчика.Вставить("Копирование", Копирование);
				ПараметрыОбработчика.Вставить("Родитель", Родитель);
				ПараметрыОбработчика.Вставить("Группа", Группа);
				ПараметрыОбработчика.Вставить("ПараметрыДобавления", ПараметрыДобавления);
				
				ОписаниеОповещения = Новый ОписаниеОповещения(
					"ФайлыПередНачаломДобавленияКлиентПродолжение",
					ЭтотОбъект,
					ПараметрыОбработчика);
				
				ДелопроизводствоКлиент.ПоказатьРасширеннуюФормуВопроса(ЭтотОбъект,
					НСтр("ru = 'Добавление файла'"),
					Текст,
					"ДобавлениеОригинала",
					"ЗадаватьВопросПриДобавленииСканКопииОригинала",
					СписокВариантовОтветов,,
					ОписаниеОповещения);
				Возврат;
			Иначе
				ДобавлениеОригинала = ПараметрыДобавления.ДобавлениеОригинала;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;          
	
	РольФайла = Неопределено;    
	ТекущиеДанные = Элементы.ФайлыДокумента.ТекущиеДанные;
	
	Если Не ЗначениеЗаполнено(РольФайла) Тогда
		Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.РольФайла) Тогда
			РольФайла = ТекущиеДанные.РольФайла;
		ИначеЕсли Файлы.ПолучитьЭлементы().Количество() <> 0 Тогда	
			СтрФайлы = Файлы.ПолучитьЭлементы()[0];
			Если ЗначениеЗаполнено(СтрФайлы.РольФайла) Тогда
				РольФайла = СтрФайлы.РольФайла;
			КонецЕсли;		
		КонецЕсли;	
	КонецЕсли;	  
	
	МожноДобавитьТолькоОдинФайл = Ложь;

	Если (ЗначениеЗаполнено(ТекущийРегистрационныйНомер) 
		Или ТекущееСостояниеОбработки= ПредопределенноеЗначение("Перечисление.СостоянияОбработкиОбъектов.Выполняется")
		Или ТекущееСостояниеОбработки= ПредопределенноеЗначение("Перечисление.СостоянияОбработкиОбъектов.Остановлена"))
		И ТекущийЧислоРолейФайлов <> 0 И ИспользоватьРолиФайлов Тогда

		Если ВВидеЭтаРольТолькоОдинФайл(ВидДокументаКэш.РолиФайлов, РольФайла) Тогда

			НетФайловВРоли = Ложь;
			// найдем число файлов в роли
			Если ТекущиеДанные <> Неопределено Тогда
				Если ТекущиеДанные.ЭтоРольФайла Тогда
					НетФайловВРоли = (ТекущиеДанные.ПолучитьЭлементы().Количество() = 0);
				КонецЕсли;
			КонецЕсли;

			Если Не НетФайловВРоли Тогда

				ПоказатьПредупреждение( , СтрШаблон(НСтр(
					"ru = 'В роли ""%1"" с признаком ""Только один файл"" уже есть файл, поэтому добавить еще один файл в эту роль нельзя.' "),
					РольФайла));

				РольФайла = Неопределено;

				Возврат;

			Иначе

				МожноДобавитьТолькоОдинФайл = Истина;

			КонецЕсли;

		КонецЕсли;

	КонецЕсли;
	
	
	ВладелецФайла = ТекущийДокумент;
	Если Не Копирование Тогда
		Попытка
			РежимСоздания = ?(ДобавлениеОригинала, 2, 1);
			ДополнительныеПараметры = Новый Структура("ДоступнаОтметкаОригинала",
				ТипЗнч(ДоступныеПоля) <> Тип("Структура")
				Или ТипЗнч(ДоступныеПоля) = Тип("Структура") И ДоступныеПоля.Свойство("СоздатьФайлОригинал"));
			ДополнительныеПараметры.Вставить("МожноДобавитьТолькоОдинФайл", МожноДобавитьТолькоОдинФайл);	
			
			РаботаСФайламиКлиент.ДобавитьФайл(Неопределено, ВладелецФайла, 
				ЭтотОбъект, РежимСоздания, Истина, ДобавлениеОригинала,,,,,,РольФайла,, ДополнительныеПараметры);
		Исключение
			ПоказатьПредупреждение(, ФайловыеФункцииСлужебныйКлиентСервер.ОшибкаСозданияНовогоФайла(
				ИнформацияОбОшибке()));
		КонецПопытки;
	Иначе
		ТекущиеДанные = ТекущиеДанныеСпискаФайлов();
		Если ТекущиеДанные = Неопределено Тогда 
			Возврат;
		КонецЕсли;	
		ФайлОснование = ТекущиеДанные.Ссылка;
		
		РаботаСФайламиКлиент.СкопироватьФайл(ВладелецФайла, ФайлОснование);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередНачаломДобавленияКлиентПродолжение(Результат, ПараметрыОбработчика) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Или Результат = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОбработчика.ПараметрыДобавления.ДобавлениеОригинала = Истина;
	ФайлыПередНачаломДобавленияКлиент(
		ПараметрыОбработчика.Элемент, 
		ПараметрыОбработчика.Отказ, 
		ПараметрыОбработчика.Копирование, 
		ПараметрыОбработчика.Родитель, 
		ПараметрыОбработчика.Группа, 
		ПараметрыОбработчика.ПараметрыДобавления);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередУдалениемПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		МассивФайлов = ДополнительныеПараметры.МассивФайлов;
		ПометитьФайлыНаУдаление(МассивФайлов, Не ДополнительныеПараметры.ПометкаУдаления);
		
		Если ПоказыватьУдаленныеФайлы Тогда 
			Для Каждого Файл Из МассивФайлов Цикл 
				ОбновитьСтрокуФайла(Файл);
			КонецЦикла;
		КонецЕсли;
		
		Если ДополнительныеПараметры.ПодписанЭП Тогда
			Оповестить("ПрисоединенныйФайлПодписан", ТекущийДокумент);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПеретаскиваниеПродолжение(Результат, ПараметрыОбработчика) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Или Результат = Ложь Тогда
		ПеретаскиваниеОригинала = Ложь;
		Возврат;
	КонецЕсли;
	
	ПеретаскиваниеОригинала = Истина;
	
	ОбработатьПеретаскиваниеФайла(
		ПараметрыОбработчика.Элемент, 
		ПараметрыОбработчика.ПараметрыПеретаскивания, 
		ПараметрыОбработчика.СтандартнаяОбработка, 
		ПараметрыОбработчика.Строка, 
		ПараметрыОбработчика.Поле);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНаКлиенте()
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаСервере 
Функция УстановитьПрочтение(Сообщение, Прочтен = Истина)
	
	ПрочтениеУстановлено = РаботаСПрочтениями.УстановитьСвойствоПрочтен(Сообщение, Прочтен);
	
	Возврат ПрочтениеУстановлено;

КонецФункции

&НаСервере
Процедура УстановитьПостоянныеПараметрыСписка()
	
	Список.Параметры.УстановитьЗначениеПараметра("Пользователь", Пользователи.ТекущийПользователь());
	
	Если КлючНазначенияИспользования = "ОтображениеПросроченныхДокументов" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Просрочен",
			Истина,,,
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	ИначеЕсли КлючНазначенияИспользования = "ОтображениеИстекающихДокументов" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Истекает",
			Истина,,,
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	ИначеЕсли КлючНазначенияИспользования = "ОтображениеДокументовБезОтвета" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"БезОтвета",
			Истина,,,
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПометкуПричин()
	
	Элементы.ФормаОтборДобавленВручную.Пометка = ОтмеченныеПричины.НайтиПоЗначению(
		Перечисления.ПричиныДобавленияВМоиДокументы.ДобавленВручную) <> Неопределено;
	Элементы.ФормаОтборНазначенаЗадачаМне.Пометка = ОтмеченныеПричины.НайтиПоЗначению(
		Перечисления.ПричиныДобавленияВМоиДокументы.НазначенаЗадачаМне) <> Неопределено;
	Элементы.ФормаОтборОтправленаЗадачаОтМеня.Пометка = ОтмеченныеПричины.НайтиПоЗначению(
		Перечисления.ПричиныДобавленияВМоиДокументы.ОтправленаЗадачаОтМеня) <> Неопределено;
	Элементы.ФормаОтборЯвляюсьАвторомДокумента.Пометка = ОтмеченныеПричины.НайтиПоЗначению(
		Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомДокумента) <> Неопределено;
	Элементы.ФормаОтборЯвляюсьОтветственнымЗаДокумент.Пометка = ОтмеченныеПричины.НайтиПоЗначению(
		Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьОтветственнымЗаДокумент) <> Неопределено;
	Элементы.ФормаОтборЯвляюсьПодписантомДокумента.Пометка = ОтмеченныеПричины.НайтиПоЗначению(
		Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьПодписантомДокумента) <> Неопределено;
	Элементы.ФормаОтборЯвляюсьАвторомУтверждения.Пометка = ОтмеченныеПричины.НайтиПоЗначению(
		Перечисления.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомУтвержденияДокумента) <> Неопределено;
	Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить(
		"ОтмеченныеПричины",
		ОтмеченныеПричины.ВыгрузитьЗначения());
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаголовок()
	
	ОбщиеИтоги = РегистрыНакопления.КоличествоМоихДокументов.ОбщиеИтоги(ОтмеченныеПричины.ВыгрузитьЗначения());
	КоличествоСтрок = КоличествоПоКлючу(ОбщиеИтоги);
	Если КоличествоСтрок > 0 Тогда
		Если КлючНазначенияИспользования = "ОтображениеПросроченныхДокументов" Тогда
			Заголовок = НСтр("ru = 'Просроченные документы (%1)'");
		ИначеЕсли КлючНазначенияИспользования = "ОтображениеИстекающихДокументов" Тогда
			Заголовок = НСтр("ru = 'Документы с истекающим сроком действия/исполнения (%1)'");
		ИначеЕсли КлючНазначенияИспользования = "ОтображениеДокументовБезОтвета" Тогда
			Заголовок = НСтр("ru = 'Документы без ответа (%1)'");
		Иначе
			Заголовок = НСтр("ru = 'Мои документы (%1)'");
		КонецЕсли;
		Заголовок = СтрШаблон(Заголовок, КоличествоСтрок);
	Иначе
		Если КлючНазначенияИспользования = "ОтображениеПросроченныхДокументов" Тогда
			Заголовок = НСтр("ru = 'Просроченные документы'");
		ИначеЕсли КлючНазначенияИспользования = "ОтображениеИстекающихДокументов" Тогда
			Заголовок = НСтр("ru = 'Документы с истекающим сроком действия/исполнения'");
		ИначеЕсли КлючНазначенияИспользования = "ОтображениеДокументовБезОтвета" Тогда
			Заголовок = НСтр("ru = 'Документы без ответа'");
		Иначе
			Заголовок = НСтр("ru = 'Мои документы'");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьГруппировки()
	
	ГруппировкиЗначение = РеквизитФормыВЗначение("Группировки");
	ГруппировкиЗначение.Очистить();
	
	Если ВидГруппировки = "ПричинаДобавления" Тогда
		
		ОтборПричины = ОтмеченныеПричины.ВыгрузитьЗначения();
		ИтогиПоПричинам = РегистрыНакопления.КоличествоМоихДокументов.ИтогиПоПричинам(ОтборПричины);
		Для Каждого ДанныеИтогов Из ИтогиПоПричинам Цикл
			КоличествоПоКлючу = КоличествоПоКлючу(ДанныеИтогов);
			Если КоличествоПоКлючу = 0 Тогда
				Продолжить;
			КонецЕсли;
			СтрокаГруппировки = ГруппировкиЗначение.Добавить();
			СтрокаГруппировки.Группировка = ДанныеИтогов.Причина;
			СтрокаГруппировки.Представление = Строка(ДанныеИтогов.Причина);
			СтрокаГруппировки.Количество = КоличествоПоКлючу;
		КонецЦикла;
		
	ИначеЕсли ВидГруппировки = "Проект" Тогда
		
		ОтборПричины = ОтмеченныеПричины.ВыгрузитьЗначения();
		ИтогиПоПроектам = РегистрыНакопления.КоличествоМоихДокументов.ИтогиПоПроектам(ОтборПричины);
		Для Каждого ДанныеИтогов Из ИтогиПоПроектам Цикл
			КоличествоПоКлючу = КоличествоПоКлючу(ДанныеИтогов);
			Если КоличествоПоКлючу = 0 Тогда
				Продолжить;
			КонецЕсли;
			СтрокаГруппировки = ГруппировкиЗначение.Добавить();
			СтрокаГруппировки.Группировка = ДанныеИтогов.Проект;
			Если ЗначениеЗаполнено(ДанныеИтогов.Проект) Тогда
				СтрокаГруппировки.Представление = Строка(ДанныеИтогов.Проект);
			Иначе
				СтрокаГруппировки.Представление = НСтр("ru = 'Без проекта'");
			КонецЕсли;
			СтрокаГруппировки.Количество = КоличествоПоКлючу;
		КонецЦикла;
	
	КонецЕсли;
	
	ОбщегоНазначенияДокументооборот.ОбновитьТаблицуФормы(Группировки, ГруппировкиЗначение, "Группировка");
	
	Группировки.Сортировать("Представление");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПорядок()
	
	// Порядок.
	Порядок = Список.КомпоновщикНастроек.Настройки.Порядок;
	Порядок.ИдентификаторПользовательскойНастройки = "ОсновнойПорядок";
	
	Порядок.Элементы.Очистить();
	
	Если СортироватьПо = "ДатаДокумента" Тогда
		Поле = Новый ПолеКомпоновкиДанных("ДатаДокумента");
	ИначеЕсли СортироватьПо = "Наименование" Тогда
		Поле = Новый ПолеКомпоновкиДанных("Наименование");
	ИначеЕсли СортироватьПо = "ВидДокумента" Тогда
		Поле = Новый ПолеКомпоновкиДанных("ВидДокумента");
	КонецЕсли;
	
	Если НаправлениеСортировки = "Убыв" Тогда
		ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Убыв;
	Иначе
		ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	КонецЕсли;
	
	ЭлементПорядка = Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементПорядка.Поле = Поле;
	ЭлементПорядка.ТипУпорядочивания = ТипУпорядочивания;
	ЭлементПорядка.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементПорядка.Использование = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтборПоПричинам()
	
	ГруппаОтборПричины = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		Список.Отбор.Элементы,
		НСтр("ru = 'Отбор причины'"),
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	ОбновитьОтборПоПричине(
		ГруппаОтборПричины,
		ПредопределенноеЗначение("Перечисление.ПричиныДобавленияВМоиДокументы.ДобавленВручную"),
		"ДобавленВручную");
	ОбновитьОтборПоПричине(
		ГруппаОтборПричины,
		ПредопределенноеЗначение("Перечисление.ПричиныДобавленияВМоиДокументы.НазначенаЗадачаМне"),
		"НазначенаЗадачаМне");
	ОбновитьОтборПоПричине(
		ГруппаОтборПричины,
		ПредопределенноеЗначение("Перечисление.ПричиныДобавленияВМоиДокументы.ОтправленаЗадачаОтМеня"),
		"ОтправленаЗадачаОтМеня");
	ОбновитьОтборПоПричине(
		ГруппаОтборПричины,
		ПредопределенноеЗначение("Перечисление.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомДокумента"),
		"ЯвляюсьАвторомДокумента");
	ОбновитьОтборПоПричине(
		ГруппаОтборПричины,
		ПредопределенноеЗначение("Перечисление.ПричиныДобавленияВМоиДокументы.ЯвляюсьОтветственнымЗаДокумент"),
		"ЯвляюсьОтветственнымЗаДокумент");
	ОбновитьОтборПоПричине(
		ГруппаОтборПричины,
		ПредопределенноеЗначение("Перечисление.ПричиныДобавленияВМоиДокументы.ЯвляюсьПодписантомДокумента"),
		"ЯвляюсьПодписантомДокумента");
	ОбновитьОтборПоПричине(
		ГруппаОтборПричины,
		ПредопределенноеЗначение("Перечисление.ПричиныДобавленияВМоиДокументы.ЯвляюсьАвторомУтвержденияДокумента"),
		"ЯвляюсьАвторомУтвержденияДокумента");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтборПоПричине(ГруппаОтборПричины, Причина, ИмяПоля)
	
	ЕстьГруппировкаПоПричине = ПоказыватьОбластьГруппировки И ВидГруппировки = "ПричинаДобавления";
	Если ЕстьГруппировкаПоПричине Тогда
		ИсточникПричин = Новый СписокЗначений;
		ИсточникПричин.Добавить(ТекущаяГруппировка);
	Иначе
		ИсточникПричин = ОтмеченныеПричины;
	КонецЕсли;
	
	Если ИсточникПричин.НайтиПоЗначению(Причина) <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ГруппаОтборПричины,
			ИмяПоля,
			ВидСравненияКомпоновкиДанных.Равно,
			Истина,,
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, ИмяПоля);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеГруппировкиПоУмолчанию()
	
	Если Группировки.Количество() > 0 Тогда
		ТекущаяГруппировка = Группировки[0].Группировка;
	Иначе
		ТекущаяГруппировка = Неопределено;
	КонецЕсли;
	ОбновитьОтборПоГруппировкамНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыФормы()
	
	// Группировка.
	Элементы.Группировки.Видимость = ПоказыватьОбластьГруппировки;
	Элементы.СписокСгруппироватьПричинеДобавления.Пометка =
		ПоказыватьОбластьГруппировки И ВидГруппировки = "ПричинаДобавления";
	Элементы.СписокСгруппироватьПоПроекту.Пометка =
		ПоказыватьОбластьГруппировки И ВидГруппировки = "Проект";
	Если ЭтоМобильныйКлиент И ПоказыватьОбластьГруппировки Тогда
		Элементы.ГруппаСписок.Отображение = ОтображениеОбычнойГруппы.ОбычноеВыделение;
	Иначе 
		Элементы.ГруппаСписок.Отображение = ОтображениеОбычнойГруппы.Нет;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ВВидеЭтаРольТолькоОдинФайл(РолиФайлов, Роль)

	Для Каждого ОписаниеРоли Из РолиФайлов Цикл
		Если ОписаниеРоли.Роль = Роль И ОписаниеРоли.ТолькоОдинФайл Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;

	Возврат Ложь;

КонецФункции

#КонецОбласти
