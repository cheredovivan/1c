#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет, чьи является документ, т.е. в чьих "Мои документы" находится.
// 
// Параметры:
//  ДокументПредприятия - СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение:
//  Массив из СправочникСсылка.Сотрудники
// 
Функция ЧейДокумент(ДокументПредприятия) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МоиДокументы.Сотрудник Как Сотрудник
		|ИЗ
		|	РегистрСведений.МоиДокументы КАК МоиДокументы
		|ГДЕ
		|	МоиДокументы.Документ = &ДокументПредприятия");
	Запрос.УстановитьПараметр("ДокументПредприятия", ДокументПредприятия);
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаРезультата = РезультатЗапроса.Выгрузить();
	ЧейДокумент = ТаблицаРезультата.ВыгрузитьКолонку("Сотрудник");
	
	Возврат ЧейДокумент;
	
КонецФункции

// Определяет причины добавления в "Мои документы" для сотрудников пользователя.
// 
// Параметры:
//  ДокументПредприятия - СправочникСсылка.ДокументыПредприятия
//  СотрудникиПользователя - Массив из СправочникСсылка.Сотрудники
// 
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ПричиныДобавленияВМоиДокументы
// 
Функция ПричиныДобавленияВМоиДокументы(ДокументПредприятия, СотрудникиПользователя) Экспорт
	
	ПричиныДобавленияВМоиДокументы = Новый Массив;
	Если Не ЗначениеЗаполнено(ДокументПредприятия) Или СотрудникиПользователя.Количество() = 0 Тогда
		Возврат ПричиныДобавленияВМоиДокументы;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МоиДокументы.Причина КАК Причина
		|ИЗ
		|	РегистрСведений.МоиДокументы КАК МоиДокументы
		|ГДЕ
		|	МоиДокументы.Документ = &ДокументПредприятия
		|	И МоиДокументы.Сотрудник В (&СотрудникиПользователя)");
	Запрос.УстановитьПараметр("ДокументПредприятия", ДокументПредприятия);
	Запрос.УстановитьПараметр("СотрудникиПользователя", СотрудникиПользователя);
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаРезультата = РезультатЗапроса.Выгрузить();
	ПричиныДобавленияВМоиДокументы = ТаблицаРезультата.ВыгрузитьКолонку("Причина");
	
	Возврат ПричиныДобавленияВМоиДокументы;
	
КонецФункции

// Добавляет документа в "Мои документы".
// 
// Параметры:
//  Сотрудник - СправочникСсылка.Сотрудники
//  Документ - СправочникСсылка.ДокументыПредприятия
//  Причина - ПеречислениеСсылка.ПричиныДобавленияВМоиДокументы
//  ИсточникПричины - см. РегистрСведений.МоиДокументы.ИсточникПричины
//  ОтразитьНемедленно - Булево
// 
Процедура ДобавитьВМоиДокументы(Сотрудник, Документ, Причина, ИсточникПричины, ОтразитьНемедленно) Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Сотрудник.Установить(Сотрудник);
		НаборЗаписей.Отбор.Документ.Установить(Документ);
		НаборЗаписей.Отбор.Причина.Установить(Причина);
		НаборЗаписей.Отбор.ИсточникПричины.Установить(ИсточникПричины);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Сотрудник = Сотрудник;
		НоваяЗапись.Документ = Документ;
		НоваяЗапись.Причина = Причина;
		НоваяЗапись.ИсточникПричины = ИсточникПричины;
		
		НаборЗаписей.ДополнительныеСвойства.Вставить("ОтразитьНемедленно", ОтразитьНемедленно);
		
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Удаляет документ из "Моих документов".
// 
// Параметры:
//  Документ - СправочникСсылка.ДокументыПредприятия
//  СотрудникиПользователя - Массив из СправочникСсылка.Сотрудники
//  ОтразитьНемедленно - Булево
// 
Процедура УдалитьИзМоихДокументов(Документ, СотрудникиПользователя, ОтразитьНемедленно) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МоиДокументы.Сотрудник,
		|	МоиДокументы.Документ,
		|	МоиДокументы.Причина,
		|	МоиДокументы.ИсточникПричины
		|ИЗ
		|	РегистрСведений.МоиДокументы КАК МоиДокументы
		|ГДЕ
		|	МоиДокументы.Документ = &Документ
		|	И МоиДокументы.Сотрудник В (&СотрудникиПользователя)");
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("СотрудникиПользователя", СотрудникиПользователя);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	НачатьТранзакцию();
	Попытка
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Сотрудник.Установить(ВыборкаДетальныеЗаписи.Сотрудник);
			НаборЗаписей.Отбор.Документ.Установить(ВыборкаДетальныеЗаписи.Документ);
			НаборЗаписей.Отбор.Причина.Установить(ВыборкаДетальныеЗаписи.Причина);
			НаборЗаписей.Отбор.ИсточникПричины.Установить(ВыборкаДетальныеЗаписи.ИсточникПричины);
			НаборЗаписей.ДополнительныеСвойства.Вставить("ОтразитьНемедленно", ОтразитьНемедленно);
			НаборЗаписей.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Конструктор параметров обновления данных кэширующих объектов.
//
// Возвращаемое значение:
//  Структура:
//   * ДокументыПоСотрудникамДляОбновления - Соответствие из КлючИЗначение:
//      ** Ключ - СправочникСсылка.Сотрудники
//      ** Значение - Массив из СправочникСсылка.ДокументыПредприятия
//
Функция ПараметрыОбновленияДанныхКэширующихОбъектов() Экспорт
	
	ПараметрыОбновления = Новый Структура;
	ПараметрыОбновления.Вставить("ДокументыПоСотрудникамДляОбновления", Новый Соответствие);
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Устанавливает значения параметров обновления данных кэширующих объектов по данным объекта.
//
// Параметры:
//  НаборЗаписей - РегистрСведенийНаборЗаписей.МоиДокументы
//
// Возвращаемое значение:
//  См. РегистрыСведений.МоиДокументы.ПараметрыОбновленияДанныхКэширующихОбъектов
//
Функция ЗначенияПараметровОбновленияДанныхКэширующихОбъектов(НаборЗаписей) Экспорт
	
	ПараметрыОбновления = ПараметрыОбновленияДанныхКэширующихОбъектов();
	
	ПредыдущиеЗаписи = ОбщегоНазначенияДокументооборот.ПредыдущиеЗаписи(НаборЗаписей);
	ТекущиеЗаписи = НаборЗаписей.Выгрузить();
	
	// Влияет на Документы.МоиДокументы.ДанныеМоегоДокумента().
	УдаленныеИДобавленныеЗаписи =
		ОбщегоНазначенияДокументооборот.УдаленныеИДобавленныеЗаписи(НаборЗаписей, ТекущиеЗаписи, ПредыдущиеЗаписи);
	Для Каждого Запись Из УдаленныеИДобавленныеЗаписи Цикл
		
		Если ПараметрыОбновления.ДокументыПоСотрудникамДляОбновления[Запись.Сотрудник] = Неопределено Тогда
			ПараметрыОбновления.ДокументыПоСотрудникамДляОбновления[Запись.Сотрудник] = Новый Массив;
		КонецЕсли;
		ДокументыПоСотрудникуДляОбновления = ПараметрыОбновления.ДокументыПоСотрудникамДляОбновления[Запись.Сотрудник];
		
		Если ДокументыПоСотрудникуДляОбновления.Найти(Запись.Документ) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументыПоСотрудникуДляОбновления.Добавить(Запись.Документ);
		
	КонецЦикла;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Обновляет данные зависимых объектов согласно установленным параметрам.
//
// Параметры:
//  НаборЗаписей - РегистрСведенийНаборЗаписей.МоиДокументы
//  ПараметрыОбновления - См. РегистрыСведений.МоиДокументы.ПараметрыОбновленияДанныхКэширующихОбъектов
//
Процедура ОбновитьДанныеКэширующихОбъектов(НаборЗаписей, ПараметрыОбновления) Экспорт
	
	Для Каждого КлючИЗначение Из ПараметрыОбновления.ДокументыПоСотрудникамДляОбновления Цикл
		
		Сотрудник = КлючИЗначение.Ключ; // СправочникСсылка.Сотрудники
		ДокументыПоСотрудникуДляОбновления = КлючИЗначение.Значение; // Массив из СправочникСсылка.ДокументыПредприятия
		
		Для Каждого ДокументПоСотрудникуДляОбновления Из ДокументыПоСотрудникуДляОбновления Цикл
			Если НаборЗаписей.ДополнительныеСвойства.Свойство("ОтразитьНемедленно")
				И НаборЗаписей.ДополнительныеСвойства.ОтразитьНемедленно Тогда
				Документы.ДанныеМоегоДокумента.ОтразитьПоСотрудникуНемедленно(
					ДокументПоСотрудникуДляОбновления,
					Сотрудник);
			Иначе
				Документы.ДанныеМоегоДокумента.ОтразитьПоСотрудникуОтложенно(
					ДокументПоСотрудникуДляОбновления,
					Сотрудник);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли