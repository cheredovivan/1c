
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Добавляет запись в регистр.
//
// Параметры:
//  Файл - СправочникСсылка.Файлы
//  ТекстХранилище - ХранилищеЗначения
//
Процедура ДобавитьЗапись(Файл, ТекстХранилище) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Файл.Установить(Файл);
	
	Запись = Набор.Добавить();
	Запись.Файл = Файл;
	Запись.ТекстХранилище = ТекстХранилище;
	
	Набор.Записать();
	
КонецПроцедуры

// Получает текст
//
// Параметры:
//  Файл - СправочникСсылка.Файлы
//
// Возвращаемое значение
// Строка
//
Функция ПолучитьТекстФайла(Файл) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);  
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТекстыФайлов.ТекстХранилище КАК ТекстХранилище
	               |ИЗ
	               |	РегистрСведений.ТекстыФайлов КАК ТекстыФайлов
	               |ГДЕ
	               |	ТекстыФайлов.Файл = &Файл";
	Запрос.УстановитьПараметр("Файл", Файл);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат "";
	КонецЕсли;	
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.ТекстХранилище.Получить();
	
КонецФункции

// Удаляет запись из регистра.
//
// Параметры:
//  Файл - СправочникСсылка.Файлы
//
Процедура УдалитьЗапись(Файл) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Файл = Файл;
	МенеджерЗаписи.Удалить();
	
КонецПроцедуры

// Формирует текст файла в формате HTML.
// 
// Параметры:
//  Файл - СправочникСсылка.Файлы.
// 
// Возвращаемое значение:
//  Строка - Текст файла в формате HTML.
// 
Функция ТекстФайлаHTML(Файл) Экспорт
	
	ТекстФайлаHTML = "";
	
	ТекстФайла = ПолучитьТекстФайла(Файл);
	ТекстФайла = ОбработкаСтрокиXML.УдалитьНедопустимыеСимволыXML(ТекстФайла);
	
	Если Не ЗначениеЗаполнено(ТекстФайла) Тогда
		Возврат ТекстФайлаHTML;
	КонецЕсли;
	
	ТекстФайлаHTML = РаботаС_HTML.ПолучитьHTMLИзТекста_ДляПросмотраТекста(ТекстФайла);
	РаботаС_HTML.ДобавитьТегиКСсылкам(ТекстФайлаHTML);
	
	Возврат ТекстФайлаHTML;
	
КонецФункции

#КонецОбласти

#КонецЕсли