
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТолькоПросмотр = Истина;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Объект", Запись.Объект);
	Отбор.Вставить("Операция", Запись.Операция);
	Отбор.Вставить("РодительОбъекта", Запись.РодительОбъекта);
	Отбор.Вставить("Раздел", Запись.Раздел);
	Отбор.Вставить("ОбъектДоступа", Запись.ОбъектДоступа);
	
	ДанныеЗаписи = РегистрыСведений.ОчередьОбновленияАдреснойКниги.Получить(Отбор);
	ПараметрыОбновления = ДанныеЗаписи.ПараметрыОбновления.Получить();
	Если ТипЗнч(ПараметрыОбновления) = Тип("Структура") Тогда
		ПредставлениеПараметровОбновления = ПредставлениеСтруктурыПараметровОбновления(ПараметрыОбновления);
	ИначеЕсли ТипЗнч(ПараметрыОбновления) = Тип("Массив") Тогда
		Для Каждого Элемент Из ПараметрыОбновления Цикл
			ПредставлениеПараметровОбновления =
				ПредставлениеПараметровОбновления + ?(ПредставлениеПараметровОбновления = "", "", Символы.ПС);
			Если ТипЗнч(Элемент) = Тип("Структура") Тогда
				ПредставлениеПараметровОбновления =
					ПредставлениеПараметровОбновления + ПредставлениеСтруктурыПараметровОбновления(Элемент);
			Иначе
				ПредставлениеПараметровОбновления = ПредставлениеПараметровОбновления + Строка(Элемент);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПредставлениеСтруктурыПараметровОбновления(ПараметрыОбновления)
	
	ПредставлениеПараметровОбновления = "";
	
	Для Каждого КлючИЗначение1 Из ПараметрыОбновления Цикл
		ПредставлениеПараметровОбновления =
			ПредставлениеПараметровОбновления + ?(ПредставлениеПараметровОбновления = "", "", Символы.ПС);
		
		Если ТипЗнч(КлючИЗначение1.Значение) = Тип("Структура") Тогда
			ПредставлениеПараметровОбновления =
				ПредставлениеПараметровОбновления + СтрШаблон("%1:", КлючИЗначение1.Ключ);
			Для Каждого КлючИЗначение2 Из КлючИЗначение1.Значение Цикл
				ПредставлениеПараметровОбновления = ПредставлениеПараметровОбновления + Символы.ПС;
				ПредставлениеПараметровОбновления = ПредставлениеПараметровОбновления + 
					СтрШаблон("    %1: %2",
						КлючИЗначение2.Ключ,
						?(ТипЗнч(КлючИЗначение2.Значение) = Тип("Массив"),
							СтрСоединить(КлючИЗначение2.Значение, Символы.ПС + Символы.Таб + Символы.Таб),
							КлючИЗначение2.Значение));
			КонецЦикла;
		Иначе
			ПредставлениеПараметровОбновления = ПредставлениеПараметровОбновления +
				СтрШаблон("%1: %2",
					КлючИЗначение1.Ключ,
					?(ТипЗнч(КлючИЗначение1.Значение) = Тип("Массив"),
							СтрСоединить(КлючИЗначение1.Значение, Символы.ПС + Символы.Таб),
							КлючИЗначение1.Значение));
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПредставлениеПараметровОбновления;
	
КонецФункции

#КонецОбласти
