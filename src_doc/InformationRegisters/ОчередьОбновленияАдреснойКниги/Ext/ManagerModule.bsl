#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет запись в очередь обновления адресной книги.
//
// Параметры:
//	Объект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект-источник.
//	Операция - ПеречислениеСсылка.ВидыОперацийОбновленияАдреснойКниги - Вид операции обновления.
//	РодительОбъекта - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Родитель объекта-источника.
//	Раздел - СправочникСсылка.АдреснаяКнига - Раздел адресной книги.
//	ОбъектДоступа - ОпределяемыйТип.ОбъектДоступаАдреснойКниги - Объект доступа.
//	ОтметкаВремени - Число - Отметка времени.
//	ПараметрыОбновления - ХранилищеЗначения, 
//						  Структура Из КлючИЗначение,
//						  Неопределено - Параметры обновления адресной книги.
//
Процедура Добавить(
		Объект,
		Операция,
		РодительОбъекта = Неопределено,
		Раздел = Неопределено,
		ОбъектДоступа = Неопределено,
		ОтметкаВремени = Неопределено,
		ПараметрыОбновления = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = СоздатьМенеджерЗаписи();
	
	Запись.Объект = Объект;
	Запись.Операция = Операция;
	Запись.РодительОбъекта = РодительОбъекта;
	Запись.ОбъектДоступа = ОбъектДоступа;
	Запись.Раздел = Раздел;
	Запись.ОтметкаВремени =
		?(ОтметкаВремени <> Неопределено, ОтметкаВремени, ОтметкиВремени.Текущая());
	
	Если ТипЗнч(ПараметрыОбновления) = Тип("ХранилищеЗначения") Тогда
		Запись.ПараметрыОбновления = ПараметрыОбновления;
	ИначеЕсли ТипЗнч(ПараметрыОбновления) = Тип("Структура") Тогда
		Запись.ПараметрыОбновления = Новый ХранилищеЗначения(ПараметрыОбновления);
	ИначеЕсли ПараметрыОбновления <> Неопределено Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Неверный тип %1 параметра ПараметрыОбновления'"),
			ТипЗнч(ПараметрыОбновления));
	КонецЕсли;
	
	Запись.Записать();
	
КонецПроцедуры

// Проверяет, есть ли запись с указанными полями ключа.
// 
// Параметры:
//	Объект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект-источник.
//	Операция - ПеречислениеСсылка.ВидыОперацийОбновленияАдреснойКниги - Вид операции обновления.
//	РодительОбъекта - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Родитель объекта-источника.
//	Раздел - СправочникСсылка.АдреснаяКнига - Раздел адресной книги.
//	ОбъектДоступа - ОпределяемыйТип.ОбъектДоступаАдреснойКниги - Объект доступа.
//
// Возвращаемое значение:
//  Булево.
// 
Функция ЕстьЗаписьСПолямиКлюча(Объект, Операция, РодительОбъекта, Раздел, ОбъектДоступа) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Очередь.Объект КАК Объект,
		|	Очередь.Операция КАК Операция,
		|	Очередь.РодительОбъекта КАК РодительОбъекта,
		|	Очередь.Раздел КАК Раздел,
		|	Очередь.ОбъектДоступа КАК ОбъектДоступа,
		|	Очередь.ОтметкаВремени КАК ОтметкаВремени,
		|	Очередь.Попыток КАК Попыток
		|ИЗ
		|	РегистрСведений.ОчередьОбновленияАдреснойКниги КАК Очередь
		|ГДЕ
		|	Очередь.Объект = &Объект
		|	И Очередь.Операция = &Операция
		|	И Очередь.РодительОбъекта = &РодительОбъекта
		|	И Очередь.Раздел = &Раздел
		|	И Очередь.ОбъектДоступа = &ОбъектДоступа");
	
	Запрос.УстановитьПараметр("Объект", Объект);
	Запрос.УстановитьПараметр("Операция", Операция);
	Запрос.УстановитьПараметр("РодительОбъекта", РодительОбъекта);
	Запрос.УстановитьПараметр("Раздел", Раздел);
	Запрос.УстановитьПараметр("ОбъектДоступа", ОбъектДоступа);
	
	ЕстьЗаписьСТакимиПолямиКлюча = Не Запрос.Выполнить().Пустой();
	
	Возврат ЕстьЗаписьСТакимиПолямиКлюча;
	
КонецФункции

// Записывает успешную попытку (удаляет запись из очереди).
//
// Параметры:
//	Объект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект-источник.
//	Операция - ПеречислениеСсылка.ВидыОперацийОбновленияАдреснойКниги - Вид операции обновления.
//	РодительОбъекта - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Родитель объекта-источника.
//	ОбъектДоступа - ОпределяемыйТип.ОбъектДоступаАдреснойКниги - Объект доступа.
//	Раздел - СправочникСсылка.АдреснаяКнига - Раздел адресной книги.
//
Процедура ЗаписатьУспешнуюПопытку(
		Объект,
		Операция,
		РодительОбъекта = Неопределено,
		ОбъектДоступа = Неопределено,
		Раздел = Неопределено) Экспорт
	
	УдалитьЗаписиИзОчереди(
		Объект, Операция, РодительОбъекта, ОбъектДоступа, Раздел);
	
КонецПроцедуры

// Записывает неуспешную попытку (увеличивает счетчик попыток и текст сообщения).
// 
// Параметры:
//	Объект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект-источник.
//	Операция - ПеречислениеСсылка.ВидыОперацийОбновленияАдреснойКниги - Вид операции обновления.
//	РодительОбъекта - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Родитель объекта-источника.
//	ОбъектДоступа - ОпределяемыйТип.ОбъектДоступаАдреснойКниги - Объект доступа.
//	Раздел - СправочникСсылка.АдреснаяКнига - Раздел адресной книги.
//	ИнформацияОбОшибке - Строка - Информация об ошибке.
// 
Процедура ЗаписатьНеуспешнуюПопытку(
		Объект,
		Операция,
		РодительОбъекта,
		ОбъектДоступа,
		Раздел,
		ИнформацияОбОшибке) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОчередьОбновленияАдреснойКниги");
		ЭлементБлокировки.УстановитьЗначение("Объект", Объект);
		ЭлементБлокировки.УстановитьЗначение("Операция", Операция);
		ЭлементБлокировки.УстановитьЗначение("РодительОбъекта", РодительОбъекта);
		ЭлементБлокировки.УстановитьЗначение("Раздел", Раздел);
		ЭлементБлокировки.УстановитьЗначение("ОбъектДоступа", ОбъектДоступа);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Объект.Установить(Объект);
		НаборЗаписей.Отбор.Операция.Установить(Операция);
		НаборЗаписей.Отбор.РодительОбъекта.Установить(РодительОбъекта);
		НаборЗаписей.Отбор.Раздел.Установить(Раздел);
		НаборЗаписей.Отбор.ОбъектДоступа.Установить(ОбъектДоступа);
		
		НаборЗаписей.Прочитать();
		
		ИсчерпаноКоличествоПопытокОбработки = Ложь;
		
		Для Каждого Запись Из НаборЗаписей Цикл
			
			Запись.Попыток = Запись.Попыток + 1;
			Запись.ИнформацияОбОшибке = ИнформацияОбОшибке;
			
			ИсчерпаноКоличествоПопытокОбработки =
				ИсчерпаноКоличествоПопытокОбработки
					Или Запись.Попыток > РаботаСАдреснойКнигойСлужебный.КоличествоПопытокОбработки();
			
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
		ОбъектМетаданных = Неопределено;
		Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Объект)) Тогда
			ОбъектМетаданных = Объект.Метаданные();
		КонецЕсли;
		
		Если ИсчерпаноКоличествоПопытокОбработки Тогда
			ОбщегоНазначенияДокументооборот.УведомитьОтветственныхОПроблеме(
				НСтр("ru = 'Ошибка обработки очереди обновления адресной книги'"),
				ИнформацияОбОшибке,
				РаботаСАдреснойКнигойСлужебный.СобытиеЖурналаРегистрации(),
				ОбъектМетаданных);
		Иначе
			ЗаписьЖурналаРегистрации(
				РаботаСАдреснойКнигойСлужебный.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				ОбъектМетаданных,,
				ИнформацияОбОшибке);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Сбрасывает попытки.
// 
// Параметры:
//	Объект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект-источник.
//	Операция - ПеречислениеСсылка.ВидыОперацийОбновленияАдреснойКниги - Вид операции обновления.
//	РодительОбъекта - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Родитель объекта-источника.
//	ОбъектДоступа - ОпределяемыйТип.ОбъектДоступаАдреснойКниги - Объект доступа.
//	Раздел - СправочникСсылка.АдреснаяКнига - Раздел адресной книги.
// 
Процедура СброситьПопытки(Объект, Операция, РодительОбъекта, ОбъектДоступа, Раздел) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОчередьОбновленияАдреснойКниги");
		ЭлементБлокировки.УстановитьЗначение("Объект", Объект);
		ЭлементБлокировки.УстановитьЗначение("Операция", Операция);
		ЭлементБлокировки.УстановитьЗначение("РодительОбъекта", РодительОбъекта);
		ЭлементБлокировки.УстановитьЗначение("Раздел", Раздел);
		ЭлементБлокировки.УстановитьЗначение("ОбъектДоступа", ОбъектДоступа);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
			
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Объект.Установить(Объект);
		НаборЗаписей.Отбор.Операция.Установить(Операция);
		НаборЗаписей.Отбор.РодительОбъекта.Установить(РодительОбъекта);
		НаборЗаписей.Отбор.Раздел.Установить(Раздел);
		НаборЗаписей.Отбор.ОбъектДоступа.Установить(ОбъектДоступа);
		НаборЗаписей.Прочитать();
		
		Для Каждого Запись Из НаборЗаписей Цикл
			
			Запись.Попыток = 0;
			Запись.ИнформацияОбОшибке = "";
			
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Удаляет запись из очереди.
// 
// Параметры:
//	Объект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект-источник.
//	Операция - ПеречислениеСсылка.ВидыОперацийОбновленияАдреснойКниги - Вид операции обновления.
//	РодительОбъекта - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Родитель объекта-источника.
//	ОбъектДоступа - ОпределяемыйТип.ОбъектДоступаАдреснойКниги - Объект доступа.
//	Раздел - СправочникСсылка.АдреснаяКнига - Раздел адресной книги.
// 
Процедура УдалитьЗаписиИзОчереди(
		Объект,
		Операция,
		РодительОбъекта = Неопределено,
		ОбъектДоступа = Неопределено,
		Раздел = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОчередьОбновленияАдреснойКниги");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Объект", Объект);
		ЭлементБлокировки.УстановитьЗначение("Операция", Операция);
		Если ЗначениеЗаполнено(РодительОбъекта) Тогда
			ЭлементБлокировки.УстановитьЗначение("РодительОбъекта", РодительОбъекта);
		КонецЕсли;
		Если ЗначениеЗаполнено(Раздел) Тогда
			ЭлементБлокировки.УстановитьЗначение("Раздел", Раздел);
		КонецЕсли;
		Если ЗначениеЗаполнено(ОбъектДоступа) Тогда
			ЭлементБлокировки.УстановитьЗначение("ОбъектДоступа", ОбъектДоступа);
		КонецЕсли;
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Объект.Установить(Объект);
		НаборЗаписей.Отбор.Операция.Установить(Операция);
		Если ЗначениеЗаполнено(РодительОбъекта) Тогда
			НаборЗаписей.Отбор.РодительОбъекта.Установить(РодительОбъекта);
		КонецЕсли;
		Если ЗначениеЗаполнено(Раздел) Тогда
			НаборЗаписей.Отбор.Раздел.Установить(Раздел);
		КонецЕсли;
		Если ЗначениеЗаполнено(ОбъектДоступа) Тогда
			НаборЗаписей.Отбор.ОбъектДоступа.Установить(ОбъектДоступа);
		КонецЕсли;
		
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Удаляет неактуальные записи из очереди.
// 
// Параметры:
//	Объект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект-источник.
//	ОтметкаВремени - Число - Отметка времени.
//	Операция - ПеречислениеСсылка.ВидыОперацийОбновленияАдреснойКниги - Вид операции обновления.
//	РодительОбъекта - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Родитель объекта-источника.
//	ОбъектДоступа - ОпределяемыйТип.ОбъектДоступаАдреснойКниги - Объект доступа.
//	Раздел - СправочникСсылка.АдреснаяКнига - Раздел адресной книги.
// 
Процедура УдалитьНеактуальныеЗаписиИзОчереди(
		Объект,
		ОтметкаВремени,
		Операция = Неопределено,
		РодительОбъекта = Неопределено,
		ОбъектДоступа = Неопределено,
		Раздел = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОчередьОбновленияАдреснойКниги");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Объект", Объект);
		Если ЗначениеЗаполнено(Операция) Тогда
			ЭлементБлокировки.УстановитьЗначение("Операция", Операция);
		КонецЕсли;
		Если ЗначениеЗаполнено(РодительОбъекта) Тогда
			ЭлементБлокировки.УстановитьЗначение("РодительОбъекта", РодительОбъекта);
		КонецЕсли;
		Если ЗначениеЗаполнено(Раздел) Тогда
			ЭлементБлокировки.УстановитьЗначение("Раздел", Раздел);
		КонецЕсли;
		Если ЗначениеЗаполнено(ОбъектДоступа) Тогда
			ЭлементБлокировки.УстановитьЗначение("ОбъектДоступа", ОбъектДоступа);
		КонецЕсли;
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Очередь.Объект КАК Объект,
			|	Очередь.Операция КАК Операция,
			|	Очередь.РодительОбъекта КАК РодительОбъекта,
			|	Очередь.Раздел КАК Раздел,
			|	Очередь.ОбъектДоступа КАК ОбъектДоступа
			|ИЗ
			|	РегистрСведений.ОчередьОбновленияАдреснойКниги КАК Очередь
			|ГДЕ
			|	Очередь.Объект = &Объект
			|	И &УсловиеОперация
			|	И &УсловиеРодительОбъекта
			|	И &УсловиеРаздел
			|	И &УсловиеОбъектДоступа
			|	И Очередь.ОтметкаВремени < &ОтметкаВремени");
		
		Запрос.УстановитьПараметр("Объект", Объект);
		Запрос.УстановитьПараметр("ОтметкаВремени", ОтметкаВремени);
		
		УсловиеОперация = "ИСТИНА";
		УсловиеРодительОбъекта = "ИСТИНА";
		УсловиеРаздел = "ИСТИНА";
		УсловиеОбъектДоступа = "ИСТИНА";
		
		Если ЗначениеЗаполнено(Операция) Тогда
			УсловиеОперация = "Очередь.Операция = &Операция";
			Запрос.УстановитьПараметр("Операция", Операция);
		КонецЕсли;
		Если ЗначениеЗаполнено(РодительОбъекта) Тогда
			УсловиеРодительОбъекта = "Очередь.РодительОбъекта = &РодительОбъекта";
			Запрос.УстановитьПараметр("РодительОбъекта", РодительОбъекта);
		КонецЕсли;
		Если ЗначениеЗаполнено(Раздел) Тогда
			УсловиеРаздел = "Очередь.Раздел = &Раздел";
			Запрос.УстановитьПараметр("Раздел", Раздел);
		КонецЕсли;
		Если ЗначениеЗаполнено(ОбъектДоступа) Тогда
			УсловиеОбъектДоступа = "Очередь.ОбъектДоступа = &ОбъектДоступа";
			Запрос.УстановитьПараметр("ОбъектДоступа", ОбъектДоступа);
		КонецЕсли;
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОперация", УсловиеОперация);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРодительОбъекта", УсловиеРодительОбъекта);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРаздел", УсловиеРаздел);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОбъектДоступа", УсловиеОбъектДоступа);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			УдалитьЗаписиИзОчереди(
				Выборка.Объект,
				Выборка.Операция,
				Выборка.РодительОбъекта,
				Выборка.ОбъектДоступа,
				Выборка.Раздел);
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
