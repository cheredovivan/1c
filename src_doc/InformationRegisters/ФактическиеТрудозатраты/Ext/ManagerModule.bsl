#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Заполняет структуру сведений о полях доступа 
//
Процедура ЗаполнитьСведенияОПоляхДоступа(СведенияОПолях) Экспорт 
	
	СведенияОПолях.ОбъектДоступа1 = "Проект";
	СведенияОПолях.ОбъектДоступа2 = "Сотрудник";
	
КонецПроцедуры

// Возвращает запрос для расчета прав доступа по дескрипторам регистра
// 
// Параметры:
//  Дескрипторы - Массив Из СправочникСсылка.ДескрипторыДоступаРегистров - Дескрипторы, чьи права нужно рассчитать.
//  ИдРегистра - СправочникСсылка.ИдентификаторыОбъектовМетаданных - Соответствующий текущему объекту.
//  МенеджерРегистра - РегистрСведенийМенеджер.ФактическиеТрудозатраты -
// 
// Возвращаемое значение:
//	Запрос - Который выберет права доступа.
Функция ЗапросДляРасчетаПрав(Дескрипторы, ИдРегистра, МенеджерРегистра) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДескрипторыДоступаРегистров.Ссылка КАК Дескриптор,
		|	ДескрипторыДоступаРегистров.ОбъектДоступа2 КАК Сотрудник,
		|	ИСТИНА КАК Чтение,
		|	ИСТИНА КАК Добавление,
		|	ИСТИНА КАК Изменение,
		|	ИСТИНА КАК Удаление,
		|	ЛОЖЬ КАК УправлениеПравами
		|ПОМЕСТИТЬ НовыеПрава
		|ИЗ
		|	Справочник.ДескрипторыДоступаРегистров КАК ДескрипторыДоступаРегистров
		|ГДЕ
		|	ДескрипторыДоступаРегистров.Ссылка В (&Дескрипторы)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДескрипторыДоступаРегистров.Ссылка,
		|	Проекты.Руководитель,
		|	ИСТИНА,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	ЛОЖЬ
		|ИЗ
		|	Справочник.Проекты КАК Проекты
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.ДескрипторыДоступаРегистров КАК ДескрипторыДоступаРегистров
		|		ПО ДескрипторыДоступаРегистров.ОбъектДоступа1 = Проекты.Ссылка
		|ГДЕ
		|	ДескрипторыДоступаРегистров.Ссылка В (&Дескрипторы)
		|	И Проекты.Руководитель <> ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)");
	
	ДокументооборотПраваДоступа.ДополнитьТекстЗапросаПоПравам(Запрос.Текст, Истина);
	
	Возврат Запрос;
	
КонецФункции

// Заполняет протокол расчета прав дескрипторов
// 
// Параметры:
//  
//  ПротоколРасчетаПрав - Массив Из Структура - протокол для заполнения
//  ЗапросПоПравам - Запрос - запрос, который использовался для расчета прав дескрипторов
Процедура ЗаполнитьПротоколРасчетаПрав(ПротоколРасчетаПрав, ЗапросПоПравам) Экспорт
	
	ЗаписьПротокола = Новый Структура("Элемент, Описание",
		"Пользователь", НСтр("ru = 'Пользователь'"));
	ПротоколРасчетаПрав.Добавить(ЗаписьПротокола);
	
	ЗаписьПротокола = Новый Структура("Элемент, Описание",
		"РуководительПроекта", НСтр("ru = 'Руководитель проекта'"));
	ПротоколРасчетаПрав.Добавить(ЗаписьПротокола);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

