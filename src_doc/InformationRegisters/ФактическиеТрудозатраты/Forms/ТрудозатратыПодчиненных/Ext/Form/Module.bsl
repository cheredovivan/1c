&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СпособУказанияВремени = УчетВремени.ПолучитьСпособУказанияВремени();
	
	Если СпособУказанияВремени = Перечисления.СпособыУказанияВремени.Длительность Тогда 
		Элементы.Длительность.Видимость = Истина;
		Элементы.Начало.Видимость = Ложь;
		Элементы.Окончание.Видимость = Ложь;
	Иначе
		Элементы.Длительность.Видимость = Ложь;
		Элементы.Начало.Видимость = Истина;
		Элементы.Окончание.Видимость = Истина;
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра(
		"СотрудникиПользователя", Сотрудники.СотрудникиПользователя());
	
	ПериодОтбора = Новый СтандартныйПериод(ВариантСтандартногоПериода.Сегодня);
	УстановитьОтборПериодаНаСервере();
	
	НастроитьФормуДляМобильногоПриНеобходимости();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьОтборПериодаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОтбораПриИзменении(Элемент)
	
	УстановитьОтборПериодаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПериодаНаСервере()
	
	Список.Параметры.УстановитьЗначениеПараметра("ДатаДобавленияНачало", ПериодОтбора.ДатаНачала);
	Список.Параметры.УстановитьЗначениеПараметра("ДатаДобавленияКонец", ПериодОтбора.ДатаОкончания);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаЗапросаОбновления()
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуДляМобильногоПриНеобходимости()
	
	Если Не ПараметрыСеанса.ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Список.Шапка = Ложь;
	Элементы.ФормаОбновить.Видимость = Ложь;
	Элементы.Проект.Видимость = Ложь;
	
	Элементы.ГруппаКолонокМК.Видимость = Истина;
	
	Элементы.Переместить(Элементы.ОписаниеРаботы, Элементы.ГруппаОписаниеДлительностьМК);
	Элементы.Переместить(Элементы.Длительность, Элементы.ГруппаОписаниеДлительностьМК);
	
	Элементы.Переместить(Элементы.ВидРабот, Элементы.ГруппаВидРаботДатаМК);
	Элементы.Переместить(Элементы.ДатаДобавления, Элементы.ГруппаВидРаботДатаМК);
	
	Элементы.Длительность.Ширина = 6;
	Элементы.ДатаДобавления.Ширина = 6;
	
	Элементы.ДатаДобавления.Формат = НСтр("ru='ДФ=''dd.MM.yy'''");
	
КонецПроцедуры

