
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СпособУказанияВремени = УчетВремени.ПолучитьСпособУказанияВремени();
	
	Если СпособУказанияВремени = Перечисления.СпособыУказанияВремени.Длительность Тогда 
		Элементы.Длительность.Видимость = Истина;
		Элементы.Начало.Видимость = Ложь;
		Элементы.Окончание.Видимость = Ложь;
	Иначе
		Элементы.Длительность.Видимость = Ложь;
		Элементы.Начало.Видимость = Истина;
		Элементы.Окончание.Видимость = Истина;
	КонецЕсли;
		
	Список.Параметры.УстановитьЗначениеПараметра("Сотрудники", Сотрудники.СотрудникиПользователя());
	
	ПериодОтбора = Новый СтандартныйПериод(ВариантСтандартногоПериода.Сегодня);
	УстановитьОтборПериодаНаСервере();
	
	НастроитьФормуДляМобильногоПриНеобходимости();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьОтборПериодаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОтбораПриИзменении(Элемент)
	
	УстановитьОтборПериодаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПериодаНаСервере()
	
	Список.Параметры.УстановитьЗначениеПараметра("ДатаДобавленияНачало", ПериодОтбора.ДатаНачала);
	Список.Параметры.УстановитьЗначениеПараметра("ДатаДобавленияКонец", ПериодОтбора.ДатаОкончания);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ЕжедневныйОтчет) Тогда 
		Отказ = Истина;
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Нельзя удалить запись, так как она отражена в ежедневном отчете %1'"),
			Строка(ТекущиеДанные.ЕжедневныйОтчет));
		ПоказатьПредупреждение(, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаЗапросаОбновления()
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуДляМобильногоПриНеобходимости()
	
	Если Не ПараметрыСеанса.ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Список.Шапка = Ложь;
	Элементы.ФормаОбновить.Видимость = Ложь;
	Элементы.Проект.Видимость = Ложь;
	
	Элементы.ГруппаСписокМК.Видимость = Истина;
	Элементы.Переместить(Элементы.Список, Элементы.ГруппаСписокМК);
	
	Элементы.ГруппаКолонокМК.Видимость = Истина;
	
	Элементы.Переместить(Элементы.ОписаниеРаботы, Элементы.ГруппаОписаниеВидРаботМК);
	Элементы.Переместить(Элементы.ВидРабот, Элементы.ГруппаОписаниеВидРаботМК);
	
	Элементы.Переместить(Элементы.Длительность, Элементы.ГруппаДлительностьДатаМК);
	Элементы.Переместить(Элементы.ДатаДобавления, Элементы.ГруппаДлительностьДатаМК);
	
	Элементы.Начало.Видимость = Ложь;
	Элементы.Окончание.Видимость = Ложь;
	
	Элементы.Длительность.Ширина = 6;
	Элементы.ДатаДобавления.Ширина = 6;
	
	Элементы.ДатаДобавления.Формат = НСтр("ru='ДФ=''dd.MM.yy'''");
	Элементы.ОписаниеРаботы.Шрифт = Новый Шрифт(,, Истина,,,);
	
КонецПроцедуры
