#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает организацию, соответствующую переданному подразделению.
//
// Параметры:
//   Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение, для которого нужно получить организацию.
//
// Возвращаемое значение:
//   СправочникСсылка.Организации
//
Функция ОрганизацияПодразделения(Подразделение) Экспорт
	
	ОрганизацииПодразделений = ОрганизацииПодразделений(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Подразделение));
	
	Возврат ОрганизацииПодразделений[Подразделение];
	
КонецФункции

// Возвращает организации, соответствующие переданным подразделениям.
//
// Параметры:
//   Подразделения - Массив из СправочникСсылка.СтруктураПредприятия - подразделения,
//     для которых нужно получить организации.
//
// Возвращаемое значение:
//   Соответствие из КлючИЗначение - соответствие подразделений и организаций:
//     * Ключ - СправочникСсылка.СтруктураПредприятия
//     * Значение - СправочникСсылка.Организации
//
Функция ОрганизацииПодразделений(Подразделения) Экспорт
	
	ОрганизацииПодразделений = Новый Соответствие;
	Для Каждого Подразделение Из Подразделения Цикл
		ОрганизацииПодразделений[Подразделение] = Неопределено;
	КонецЦикла;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПодразделенияОрганизаций.Подразделение КАК Подразделение,
		|	ПодразделенияОрганизаций.Организация КАК Организация
		|ИЗ
		|	РегистрСведений.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|ГДЕ
		|	ПодразделенияОрганизаций.Подразделение В (&Подразделения)");
	Запрос.УстановитьПараметр("Подразделения", Подразделения);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОрганизацииПодразделений[Выборка.Подразделение] = Выборка.Организация;
	КонецЦикла;

	Возврат ОрганизацииПодразделений;
	
КонецФункции

#КонецОбласти

#КонецЕсли