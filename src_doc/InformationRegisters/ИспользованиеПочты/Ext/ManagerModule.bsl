#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает признак использования легкой почты.     
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи
//
// Возвращаемое значение:
//  Булево
//
Функция ЛегкаяПочтаИспользуется(Знач Пользователь = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ИспользованиеПочты.НастройкаИспользоватьЛегкуюПочту КАК НастройкаИспользоватьЛегкуюПочту
	               |ИЗ
	               |	РегистрСведений.ИспользованиеПочты КАК ИспользованиеПочты
	               |ГДЕ
	               |	ИспользованиеПочты.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.НастройкаИспользоватьЛегкуюПочту;
	
КонецФункции

// Возвращает признак использования встроенной почты.
//  
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи
//
// Возвращаемое значение:
//  Булево
//
Функция ВстроеннаяПочтаИспользуется(Знач Пользователь = Неопределено) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ИспользованиеПочты.НастройкаИспользоватьВстроеннуюПочту КАК НастройкаИспользоватьВстроеннуюПочту
	               |ИЗ
	               |	РегистрСведений.ИспользованиеПочты КАК ИспользованиеПочты
	               |ГДЕ
	               |	ИспользованиеПочты.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.НастройкаИспользоватьВстроеннуюПочту;
	
КонецФункции

// Устанавливает признаки использования легкой и встроенной почты.
//   
// Параметры:                                  
//  ИспользоватьЛегкуюПочту - Булево
//  ИспользоватьВстроеннуюПочту - Булево
//  Пользователь - СправочникСсылка.Пользователи 
//
Процедура УстановитьИспользованиеПочты(ИспользоватьЛегкуюПочту, ИспользоватьВстроеннуюПочту, Знач Пользователь = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	КонстантаИспользоватьВстроеннуюПочту = Константы.ИспользоватьВстроеннуюПочту.Получить();
	
	Запись = РегистрыСведений.ИспользованиеПочты.СоздатьМенеджерЗаписи();
	Запись.Пользователь = Пользователь;
	Запись.Прочитать();
	
	Запись.Пользователь = Пользователь;
	Запись.НастройкаИспользоватьЛегкуюПочту = ИспользоватьЛегкуюПочту;
	Запись.НастройкаИспользоватьВстроеннуюПочту = ИспользоватьВстроеннуюПочту;
	Запись.ИспользоватьЛегкуюПочту = ИспользоватьЛегкуюПочту;
	Запись.ИспользоватьВстроеннуюПочту = ИспользоватьВстроеннуюПочту И КонстантаИспользоватьВстроеннуюПочту;
	Запись.Записать(Истина);
	
КонецПроцедуры

// Обновляет записи регистра сведений. Пересчитывает признак использования встроенной почты
// с учетом значения константы ИспользоватьВстроеннуюПочту.
//
Процедура ОбновитьИспользованиеПочты() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонстантаИспользоватьВстроеннуюПочту = Константы.ИспользоватьВстроеннуюПочту.Получить();
	Набор = РегистрыСведений.ИспользованиеПочты.СоздатьНаборЗаписей();
	Набор.Прочитать();
	Для каждого Запись Из Набор Цикл
		Запись.ИспользоватьВстроеннуюПочту = Запись.НастройкаИспользоватьВстроеннуюПочту И КонстантаИспользоватьВстроеннуюПочту;
		Запись.ИспользоватьЛегкуюПочту = Запись.НастройкаИспользоватьЛегкуюПочту;
	КонецЦикла;
	Набор.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли