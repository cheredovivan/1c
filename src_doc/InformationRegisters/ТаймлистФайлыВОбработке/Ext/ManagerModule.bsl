
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет файл в очередь обработки.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  Задание - ПеречислениеСсылка.ТаймлистТипыЗаданий
//  Параметры - Структура
// 
Процедура Добавить(ВерсияФайла, Задание, Параметры = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
	МенеджерЗаписи.Прочитать();
	
	Если Не МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
		Если Параметры <> Неопределено Тогда
			МенеджерЗаписи.Параметры = Новый ХранилищеЗначения(Параметры);
		КонецЕсли;
	Иначе
		Если Параметры <> Неопределено Тогда
			ТекущиеПараметры = МенеджерЗаписи.Параметры.Получить();
			Если ТекущиеПараметры <> Неопределено Тогда
				Для Каждого Параметр Из Параметры Цикл
					ТекущиеПараметры.Вставить(Параметр.Ключ, Параметр.Значение);
				КонецЦикла;
			Иначе
				ТекущиеПараметры = Параметры;
			КонецЕсли;
			МенеджерЗаписи.Параметры = Новый ХранилищеЗначения(ТекущиеПараметры);
		КонецЕсли;
	КонецЕсли;
	
	МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
	МенеджерЗаписи.Задание = Задание;
	Если Задание = Перечисления.ТаймлистТипыЗаданий.ЗагрузитьФайлВСервис
		Или Задание = Перечисления.ТаймлистТипыЗаданий.ПодготовитьДанныеКПолучениюАвтопротокола Тогда
		МенеджерЗаписи.Автор = Пользователи.ТекущийПользователь();
	КонецЕсли;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Удаляет файл из очереди обработки.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
// 
Процедура Удалить(ВерсияФайла) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
	
	МенеджерЗаписи.Удалить();
	
КонецПроцедуры

// Возвращает автора задания на получение расшифровки и автопротокола.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
// 
// Возвращаемое значение:
//  СправочникСсылка.Пользователи
//
Функция АвторЗадания(ВерсияФайла) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		Возврат МенеджерЗаписи.Автор;
	КонецЕсли;
	
	Возврат Справочники.Пользователи.ПустаяСсылка();
	
КонецФункции

#КонецОбласти

#КонецЕсли