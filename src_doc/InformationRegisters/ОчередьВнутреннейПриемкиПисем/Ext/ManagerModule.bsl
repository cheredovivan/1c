#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет письмо для приемки адресату.
//
// Параметры:
//  Письмо          - ДокументСсылка.ИсходящееПисьмо                                   - Исходящее письмо.
//  Адресат         - СправочникСсылка.АдресатыПочтовыхСообщений                       - Адресат для приемки исходящего письма.
//  ДатаОтправки    - Дата                                                             - Дата выполнения отправки письма.
//  ДатаПодготовки  - Дата                                                             - Дата подготовки письма к отправке.
//  ПорядокДоставки - ПеречислениеСсылка.ПорядокДоставкиПисемПоВнутреннейМаршрутизации - Порядок доставки письма.
//
Процедура ДобавитьПисьмоДляАдресата(Письмо, Адресат, ДатаОтправки, ДатаПодготовки, ПорядокДоставки) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Письмо.Установить(Письмо);
	НаборЗаписей.Отбор.Адресат.Установить(Адресат);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Письмо = Письмо;
	НоваяЗапись.Адресат = Адресат;
	НоваяЗапись.ДатаОтправки = ДатаОтправки;
	НоваяЗапись.ДатаПодготовки = ДатаПодготовки;
	НоваяЗапись.ПорядокДоставки = ПорядокДоставки;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Добавляет письмо для приемки списку адресатов.
//
// Параметры:
//  Письмо          - ДокументСсылка.ИсходящееПисьмо                                   - Исходящее письмо.
//  СписокАдресатов - Массив														   - Список получателей исходящего письма.
//  ДатаОтправки    - Дата                                                             - Дата выполнения отправки письма.
//  ДатаПодготовки  - Дата                                                             - Дата подготовки письма к отправке.
//  ПорядокДоставки - ПеречислениеСсылка.ПорядокДоставкиПисемПоВнутреннейМаршрутизации - Порядок доставки письма.
//  Приоритет		- Число - приоритет
//
Процедура ДобавитьПисьмоДляСпискаАдресатов(Письмо, СписокАдресатов, 
	ДатаОтправки, ДатаПодготовки, ПорядокДоставки,
	Приоритет = 0) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Письмо.Установить(Письмо);
	
	Для Каждого Элемент Из СписокАдресатов Цикл
	
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Письмо = Письмо;
		НоваяЗапись.Адресат = Элемент;
		НоваяЗапись.ДатаОтправки = ДатаОтправки;
		НоваяЗапись.ДатаПодготовки = ДатаПодготовки;
		НоваяЗапись.ПорядокДоставки = ПорядокДоставки;
		НоваяЗапись.Приоритет = Приоритет;
		
	КонецЦикла;
		
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Удаляет письмо для адресата, если оно есть, и возвращает удалил ли.
// Если такой записи по письму для адресата нет, то возвращает Ложь.
// 
// Параметры:
//  Письмо  - ДокументСсылка.ИсходящееПисьмо             - Исходящее письмо.
//  Адресат - СправочникСсылка.АдресатыПочтовыхСообщений - Адресат для приемки исходящего письма.
//
// Возвращаемое значение:
//  Булево - Письмо для адресата было удалено.
//
Функция ПисьмоАдресатаБылоУдалено(Письмо, Адресат) Экспорт
	
	ПисьмоБылоУдалено = Ложь;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОчередьВнутреннейПриемкиПисем");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Письмо", Письмо);
		ЭлементБлокировки.УстановитьЗначение("Адресат", Адресат);
		Блокировка.Заблокировать();
		
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Письмо = Письмо;
		МенеджерЗаписи.Адресат = Адресат;
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			ПисьмоБылоУдалено = Истина;
			МенеджерЗаписи.Удалить();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат ПисьмоБылоУдалено;
	
КонецФункции

// Удаляет все записи по данному исх письму
// 
// Параметры:
//  Письмо  - ДокументСсылка.ИсходящееПисьмо             - Исходящее письмо.
//
Процедура УдалитьСведенияОПисьме(Письмо) Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОчередьВнутреннейПриемкиПисем");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Письмо", Письмо);
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Письмо.Установить(Письмо);
		НаборЗаписей.Записать(); // стираем все по письму
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Изменяет поток письма для адресата, если оно есть, и возвращает изменил ли.
// Если такой записи по письму для адресата нет, то возвращает Ложь.
// 
// Параметры:
//  Письмо     - ДокументСсылка.ИсходящееПисьмо             - Исходящее письмо.
//  Адресат    - СправочникСсылка.АдресатыПочтовыхСообщений - Адресат для приемки исходящего письма.
//  НовыйПоток - Число                                      - Новый поток.
//
// Возвращаемое значение:
//  Булево - Поток письма для адресата был изменен.
//
Функция ПотокПисьмаДляАдресатаБылИзменен(Письмо, Адресат, НовыйПоток) Экспорт
	
	ПотокБылИзменен = Ложь;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОчередьВнутреннейПриемкиПисем");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Письмо", Письмо);
		ЭлементБлокировки.УстановитьЗначение("Адресат", Адресат);
		Блокировка.Заблокировать();
		
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Письмо = Письмо;
		МенеджерЗаписи.Адресат = Адресат;
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			ПотокБылИзменен = Истина;
			МенеджерЗаписи.Поток = НовыйПоток;
			МенеджерЗаписи.Записать();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат ПотокБылИзменен;
	
КонецФункции

// Определяет потоки с письмами для указанного порядка дсотавки.
//
// Параметры:
//  ПорядокДоставки - ПеречислениеСсылка.ПорядокДоставкиПисемПоВнутреннейМаршрутизации - Порядок доставки.
// 
// Возвращаемое значение:
//  Массив из Число - Номер потоков с письмами.
//
Функция ПотокиСПисьмами(ПорядокДоставки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОчередьВнутреннейПриемкиПисем.Поток КАК Поток
		|ИЗ
		|	РегистрСведений.ОчередьВнутреннейПриемкиПисем КАК ОчередьВнутреннейПриемкиПисем
		|ГДЕ
		|	ОчередьВнутреннейПриемкиПисем.ПорядокДоставки = &ПорядокДоставки
		|	И ОчередьВнутреннейПриемкиПисем.Поток <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Поток");
	
	Запрос.УстановитьПараметр("ПорядокДоставки", ПорядокДоставки);
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаРезультата = РезультатЗапроса.Выгрузить();
	ПотокиСПисьмами = ТаблицаРезультата.ВыгрузитьКолонку("Поток");
	
	Возврат ПотокиСПисьмами;
	
КонецФункции

// Получает все записи РС с отбором по письму.
//
// Параметры:
//  Письмо  - ДокументСсылка.ИсходящееПисьмо             - Исходящее письмо.
// 
// Возвращаемое значение:
//  таблица значений, колонки Адресат, Адрес
//
Функция АдресатыПоПисьму(Письмо) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ОчередьВнутреннейПриемкиПисем.Адресат КАК Адресат,
		|	ОчередьВнутреннейПриемкиПисем.Адресат.Адрес КАК Адрес
		|ИЗ
		|	РегистрСведений.ОчередьВнутреннейПриемкиПисем КАК ОчередьВнутреннейПриемкиПисем
		|ГДЕ
		|	ОчередьВнутреннейПриемкиПисем.Письмо = &Письмо");
	
	Запрос.УстановитьПараметр("Письмо", Письмо);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Определяет письма, зависшие в процессе отправки по внутренней маршрутизации.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Письма, зависшие в процессе отправки по внутренней маршрутизации.
//   * Письмо       - ДокументСсылка.ИсходящееПисьмо             - Исходящее письмо.
//   * Адресат      - СправочникСсылка.АдресатыПочтовыхСообщений - Адресат, доставку которому зависла.
//   * ДатаОтправки - Дата                                       - Дата отправки письма.
//
Функция ЗависшиеПисьма() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОчередьВнутреннейПриемкиПисем.Письмо КАК Письмо,
		|	ОчередьВнутреннейПриемкиПисем.Адресат КАК Адресат,
		|	ОчередьВнутреннейПриемкиПисем.ДатаОтправки КАК ДатаОтправки,
		|	ОчередьВнутреннейПриемкиПисем.Письмо.УчетнаяЗапись КАК ИсходящееПисьмоУчетнаяЗапись
		|ИЗ
		|	РегистрСведений.ОчередьВнутреннейПриемкиПисем КАК ОчередьВнутреннейПриемкиПисем
		|ГДЕ
		|	ОчередьВнутреннейПриемкиПисем.ПорядокДоставки = &ПорядокДоставки
		|	И ОчередьВнутреннейПриемкиПисем.ДатаОтправки < &ДатаОтправки
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаОтправки";
		
	ПорядокДоставки = Перечисления.ПорядокДоставкиПисемПоВнутреннейМаршрутизации.БыстраяДоставка;
	Запрос.УстановитьПараметр("ПорядокДоставки", ПорядокДоставки);
	Запрос.УстановитьПараметр("ДатаОтправки", ТекущаяДатаСеанса() - 20 * 60);
	
	Результат = Запрос.Выполнить();
	ЗависшиеПисьма = Результат.Выгрузить();
	
	Возврат ЗависшиеПисьма;
	
КонецФункции

// Определяет статистику очереди отправки.
//
// Возвращаемое значение:
//  Структура - Статистика очереди отправки.
//   * ВнешняяМаршрутизацияРазмерОчереди     - Число - Размер очереди (внешняя маршрутизация).
//   * ВнешняяМаршрутизацияВозрастОчереди    - Число - Возраст очереди (внешняя маршрутизация).
//   * ВнутренняяМаршрутизацияРазмерОчереди  - Число - Размер очереди (внутренняя маршрутизация).
//   * ВнутренняяМаршрутизацияВозрастОчереди - Число - Возраст очереди (внутренняя маршрутизация).
//
Функция СтатистикаОчереди() Экспорт
	
	СтатистикаОчереди = Новый Структура();
	СтатистикаОчереди.Вставить("БыстраяДоставкаРазмерОчереди", 0);
	СтатистикаОчереди.Вставить("БыстраяДоставкаВозрастОчереди", 0);
	СтатистикаОчереди.Вставить("ДолгаяДоставкаРазмерОчереди", 0);
	СтатистикаОчереди.Вставить("ДолгаяДоставкаВозрастОчереди", 0);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ОчередьВнутреннейПриемкиПисем.ПорядокДоставки КАК ПорядокДоставки,
		|	КОЛИЧЕСТВО(ОчередьВнутреннейПриемкиПисем.Письмо) КАК КоличествоПисем,
		|	МИНИМУМ(ОчередьВнутреннейПриемкиПисем.ДатаОтправки) КАК МинимумДатаОтправки
		|ИЗ
		|	РегистрСведений.ОчередьВнутреннейПриемкиПисем КАК ОчередьВнутреннейПриемкиПисем
		|
		|СГРУППИРОВАТЬ ПО
		|	ОчередьВнутреннейПриемкиПисем.ПорядокДоставки");
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ПорядокДоставки = Перечисления.ПорядокДоставкиПисемПоВнутреннейМаршрутизации.БыстраяДоставка Тогда
			
			СтатистикаОчереди.БыстраяДоставкаРазмерОчереди = Выборка.КоличествоПисем;
			СтатистикаОчереди.БыстраяДоставкаВозрастОчереди =
				ТекущаяДатаСеанса() - Выборка.МинимумДатаОтправки;
			
		ИначеЕсли Выборка.ПорядокДоставки = Перечисления.ПорядокДоставкиПисемПоВнутреннейМаршрутизации.ДолгаяДоставка Тогда
			
			СтатистикаОчереди.ДолгаяДоставкаРазмерОчереди = Выборка.КоличествоПисем;
			СтатистикаОчереди.ДолгаяДоставкаВозрастОчереди =
				ТекущаяДатаСеанса() - Выборка.МинимумДатаОтправки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтатистикаОчереди;
	
КонецФункции

// Определяет самое старое письмо в очереди.
//
// Параметры:
//  ПорядокДоставки - ПеречислениеСсылка.ПорядокДоставкиПисемПоВнутреннейМаршрутизации - Порядок доставки.
// 
// Возвращаемое значение:
//  ДокументСсылка.ИсходящееПисьмо - Самое старое письмо в очереди.
//
Функция СамоеСтароеПисьмо(ПорядокДоставки) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОчередьВнутреннейПриемкиПисем.Письмо КАК Письмо
		|ИЗ
		|	РегистрСведений.ОчередьВнутреннейПриемкиПисем КАК ОчередьВнутреннейПриемкиПисем
		|ГДЕ
		|	ОчередьВнутреннейПриемкиПисем.ПорядокДоставки = &ПорядокДоставки
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОчередьВнутреннейПриемкиПисем.ДатаОтправки");
	
	Запрос.УстановитьПараметр("ПорядокДоставки", ПорядокДоставки);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	СамоеСтароеПисьмо = ?(Выборка.Следующий(), Выборка.Письмо, Документы.ИсходящееПисьмо.ПустаяСсылка());
	
	Возврат СамоеСтароеПисьмо;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

#КонецОбласти

#КонецОбласти

#КонецЕсли