#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныйПрограммныйИнтерфейс

// Добавляет запись в регистр.
// 
// Параметры: ДокументСсылка - СправочникСсылка.ДокументыПредприятия
// ИмяРеквизита - Строка
//  ПараметрыПроверки - Структура - см. регистр
Процедура ДобавитьПроверяемыйРеквизит(ПараметрыПроверки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, ПараметрыПроверки);

	Запись.Записать(Истина);	
	
КонецПроцедуры


// Возвращает признак необходимости проверки реквизитов.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение:
//  Булево - Требует проверки
Функция НеобходимаПроверкаРеквизитов(ДокументСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ Первые 1
		|	ВерификацияРеквизитовРаспознанныхДокументов.Документ
		|ИЗ
		|	РегистрСведений.ВерификацияРеквизитовРаспознанныхДокументов КАК ВерификацияРеквизитовРаспознанныхДокументов
		|ГДЕ
		|	ВерификацияРеквизитовРаспознанныхДокументов.Документ = &Документ
		|	И ВерификацияРеквизитовРаспознанныхДокументов.ТребуетПроверки = ИСТИНА 
		|		И ВерификацияРеквизитовРаспознанныхДокументов.Проверен = ЛОЖЬ");
	
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Реквизиты требующие проверки.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия - 
//  ВключаяПроверенные - Булево - 
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Реквизиты требующие проверки
Функция РеквизитыТребующиеПроверки(ДокументСсылка, ВключаяПроверенные = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВерификацияРеквизитовРаспознанныхДокументов.ИмяРеквизита КАК ИмяРеквизита,
		|	ВерификацияРеквизитовРаспознанныхДокументов.Проверен КАК Проверен
		|ИЗ
		|	РегистрСведений.ВерификацияРеквизитовРаспознанныхДокументов КАК ВерификацияРеквизитовРаспознанныхДокументов
		|ГДЕ
		|	ВерификацияРеквизитовРаспознанныхДокументов.Документ = &Документ
		|	И ВерификацияРеквизитовРаспознанныхДокументов.ТребуетПроверки = ИСТИНА
		|		И ВерификацияРеквизитовРаспознанныхДокументов.Проверен = ЛОЖЬ");
	
	Если ВключаяПроверенные Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"И ВерификацияРеквизитовРаспознанныхДокументов.Проверен = ЛОЖЬ", "");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	
	ТаблицаРеквизитов = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаРеквизитов;
	
КонецФункции

// Возвращает признак необходимости проверки реквизита.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия - 
//  ИмяРеквизита - Строка - 
// 
// Возвращаемое значение:
//  Булево - Необходима проверка реквизита
Функция НеобходимаПроверкаРеквизита(ДокументСсылка, ИмяРеквизита) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВерификацияРеквизитовРаспознанныхДокументов.ИмяРеквизита 
		|ИЗ
		|	РегистрСведений.ВерификацияРеквизитовРаспознанныхДокументов КАК ВерификацияРеквизитовРаспознанныхДокументов
		|ГДЕ
		|	ВерификацияРеквизитовРаспознанныхДокументов.ИмяРеквизита = &ИмяРеквизита
		|	И ВерификацияРеквизитовРаспознанныхДокументов.Документ = &Документ
		|	И ВерификацияРеквизитовРаспознанныхДокументов.ТребуетПроверки = ИСТИНА
		|		И ВерификацияРеквизитовРаспознанныхДокументов.Проверен = ЛОЖЬ");
	
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	Запрос.УстановитьПараметр("ИмяРеквизита", ИмяРеквизита);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Удалить данные документа.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
Процедура УдалитьДанныеДокумента(ДокументСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(ДокументСсылка);
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Записать данные верификации документа.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
//  ДанныеВерификации - ТаблицаЗначений
//  ДанныеВерифицированы - Булево
Процедура ЗаписатьДанныеДокумента(ДокументСсылка, ДанныеВерификации, ДанныеВерифицированы) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Если ДанныеВерифицированы = Истина Тогда
	
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Документ.Установить(ДокументСсылка);
		НаборЗаписей.Загрузить(ДанныеВерификации.Выгрузить());
		
		Для Каждого Запись Из НаборЗаписей Цикл
			
			Запись.Документ = ДокументСсылка;
			Запись.Ответственный = Пользователи.ТекущийПользователь();
			Запись.ДатаПроверки = ТекущаяДатаСеанса();
			
		КонецЦикла;
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли