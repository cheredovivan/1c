
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ОбработчикиОбновления

// Перенос данных поиска адресной книги в новый регистр
//
// Параметры:
// Параметры - Структура - параметры обновления:
// * ПрогрессВыполнения - Структура:
// ** ОбработаноОбъектов - Число
// ** ВсегоОбъектов - Число
//
Процедура ПеренестиДанныеВНовойРегистр(Параметры) Экспорт
	
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(УдалитьПоискВАдреснойКниге.ОбъектПоиска) КАК ВсегоОбъектов
			|ИЗ
			|	РегистрСведений.УдалитьПоискВАдреснойКниге КАК УдалитьПоискВАдреснойКниге
			|ГДЕ
			|	НЕ УдалитьПоискВАдреснойКниге.ОбъектДоступа ССЫЛКА Справочник.Пользователи
			|	И НЕ УдалитьПоискВАдреснойКниге.ОбъектПоиска ССЫЛКА Справочник.Пользователи
			|	И НЕ УдалитьПоискВАдреснойКниге.КритерийПоиска ССЫЛКА Справочник.Пользователи");
			
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Параметры.ПрогрессВыполнения.ВсегоОбъектов = Выборка.ВсегоОбъектов;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбработаноОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 10000
		|	УдалитьПоискВАдреснойКниге.ОбъектПоиска КАК ОбъектПоиска,
		|	УдалитьПоискВАдреснойКниге.КритерийПоиска КАК КритерийПоиска,
		|	УдалитьПоискВАдреснойКниге.ИспользоватьВПоиске КАК ИспользоватьВПоиске,
		|	УдалитьПоискВАдреснойКниге.ОбъектДоступа КАК ОбъектДоступа,
		|	УдалитьПоискВАдреснойКниге.Слово КАК Слово,
		|	УдалитьПоискВАдреснойКниге.ИдентификаторОтметкиВремени КАК ИдентификаторОтметкиВремени
		|ИЗ
		|	РегистрСведений.УдалитьПоискВАдреснойКниге КАК УдалитьПоискВАдреснойКниге
		|ГДЕ
		|	НЕ УдалитьПоискВАдреснойКниге.ОбъектДоступа ССЫЛКА Справочник.Пользователи
		|	И НЕ УдалитьПоискВАдреснойКниге.ОбъектПоиска ССЫЛКА Справочник.Пользователи
		|	И НЕ УдалитьПоискВАдреснойКниге.КритерийПоиска ССЫЛКА Справочник.Пользователи");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			НовыйНаборЗаписей = РегистрыСведений.ОбъектыПоискаВАдреснойКниге.СоздатьНаборЗаписей();
			
			НовыйНаборЗаписей.Отбор.ОбъектПоиска.Установить(Выборка.ОбъектПоиска);
			НовыйНаборЗаписей.Отбор.КритерийПоиска.Установить(Выборка.КритерийПоиска);
			
			Запись = НовыйНаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НовыйНаборЗаписей);
			
			СтарыйНаборЗаписей = СоздатьНаборЗаписей();
			
			СтарыйНаборЗаписей.Отбор.ОбъектПоиска.Установить(Выборка.ОбъектПоиска);
			СтарыйНаборЗаписей.Отбор.КритерийПоиска.Установить(Выборка.КритерийПоиска);
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(СтарыйНаборЗаписей);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось перенести данные из РС УдалитьПоискВАдреснойКниге по причине:
				|%1. 
				|Критерий поиска: %2
				|ОбъектПоиска: %3'"),
			ПодробноеПредставлениеОшибки, Выборка.КритерийПоиска, Выборка.ОбъектПоиска); 
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				РегистрыСведений.УдалитьПоискВАдреснойКниге,,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла; 
	
	Если ОбработаноОбъектов = 0 И ПроблемныхОбъектов > 0 Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Процедуре ПерейтиНаВерсию_3_0_15_17 не удалось перенести данные адресной книги (пропущены): %1'"),
			ПроблемныхОбъектов);
			
	ИначеЕсли ОбработаноОбъектов = 0 И ПроблемныхОбъектов = 0 Тогда
		
		//Очистим оставшиеся (останутся только записи с пользователями) записи, если нет ошибок
		СтарыеЗаписи = СоздатьНаборЗаписей();
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(СтарыеЗаписи);
		
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = 
		Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ОбработаноОбъектов;
	Параметры.ОбработкаЗавершена = ОбработаноОбъектов = 0;
	
КонецПроцедуры

#КонецОбласти

// При изменении подразделения
Процедура ОбновитьСловаПоискаПоПодразделению(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(Объект.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(Объект.Ссылка);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Сотрудники.Ссылка КАК Ссылка,
	|	НЕ Сотрудники.ПометкаУдаления
	|		И Сотрудники.Действует
	|		И &ИспользоватьВПоиске КАК ИспользоватьВПоиске
	|ПОМЕСТИТЬ Сотрудники
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Подразделение = &Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсполнителиРолей.РольИсполнителя.Владелец КАК ОбъектПоиска,
	|	ИсполнителиРолей.РольИсполнителя.ПометкаУдаления = ЛОЖЬ
	|		И &ИспользоватьВПоиске КАК ИспользоватьВПоиске
	|ИЗ
	|	Сотрудники КАК Сотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсполнителиРолей КАК ИсполнителиРолей
	|		ПО Сотрудники.Ссылка = ИсполнителиРолей.Исполнитель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Сотрудники.Ссылка,
	|	Сотрудники.ИспользоватьВПоиске
	|ИЗ
	|	Сотрудники КАК Сотрудники");
	
	Запрос.УстановитьПараметр("Объект", Объект.Ссылка);
	Запрос.УстановитьПараметр("ИспользоватьВПоиске", Не Объект.ПометкаУдаления);
	
	ОбъектыПоиска = Запрос.Выполнить().Выгрузить();
	
	ОбъектыПоиска.Колонки.Добавить("Слово");
	ОбъектыПоиска.Колонки.Добавить("ОбъектДоступа");
	ОбъектыПоиска.Колонки.Добавить("КритерийПоиска");
	
	ОбъектыПоиска.ЗаполнитьЗначения(Объект.Наименование, "Слово");
	ОбъектыПоиска.ЗаполнитьЗначения(Неопределено, "ОбъектДоступа");
	ОбъектыПоиска.ЗаполнитьЗначения(Объект.Ссылка, "КритерийПоиска");
	
	НаборЗаписей.Загрузить(ОбъектыПоиска);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = Объект.Ссылка;
	НоваяЗапись.КритерийПоиска = Объект.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = НЕ Объект.ПометкаУдаления;
	НоваяЗапись.Слово = Объект.Наименование;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// При изменении группы пользователей
Процедура ОбновитьСловаПоискаПоРабочейГруппе(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(Объект.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(Объект.Ссылка);
		
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РабочиеГруппыСостав.Участник КАК ОбъектПоиска,
	|	РабочиеГруппыСостав.Участник.ПометкаУдаления = ЛОЖЬ
	|		И РабочиеГруппыСостав.Участник.Действует = ИСТИНА
	|		И &ИспользоватьВПоиске КАК ИспользоватьВПоиске
	|ИЗ
	|	Справочник.РабочиеГруппы.Состав КАК РабочиеГруппыСостав
	|ГДЕ
	|	РабочиеГруппыСостав.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("ИспользоватьВПоиске", Не Объект.ПометкаУдаления);
	
	ОбъектыПоиска = Запрос.Выполнить().Выгрузить();
	
	ОбъектыПоиска.Колонки.Добавить("Слово");
	ОбъектыПоиска.Колонки.Добавить("ОбъектДоступа");
	ОбъектыПоиска.Колонки.Добавить("КритерийПоиска");
	
	ОбъектыПоиска.ЗаполнитьЗначения(Объект.Наименование, "Слово");
	ОбъектыПоиска.ЗаполнитьЗначения(Неопределено, "ОбъектДоступа");
	ОбъектыПоиска.ЗаполнитьЗначения(Объект.Ссылка, "КритерийПоиска");
	
	НаборЗаписей.Загрузить(ОбъектыПоиска);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = Объект.Ссылка;
	НоваяЗапись.КритерийПоиска = Объект.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = НЕ Объект.ПометкаУдаления;
	НоваяЗапись.Слово = Объект.Наименование;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// При изменении проекта
Процедура ОбновитьСловаПоискаПоПроекту(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(Объект.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(Объект.Ссылка);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПроектнаяКоманда.Исполнитель КАК ОбъектПоиска,
	|	ВЫБОР
	|		КОГДА ПроектнаяКоманда.Исполнитель ССЫЛКА Справочник.Пользователи
	|			ТОГДА НЕ ПроектнаяКоманда.Исполнитель.ПометкаУдаления
	|					И НЕ ПроектнаяКоманда.Исполнитель.Служебный
	|					И НЕ ПроектнаяКоманда.Исполнитель.Недействителен
	|					И &ИспользоватьВПоиске
	|		КОГДА ПроектнаяКоманда.Исполнитель ССЫЛКА Справочник.Сотрудники
	|			ТОГДА НЕ ПроектнаяКоманда.Исполнитель.ПометкаУдаления
	|					И ПроектнаяКоманда.Исполнитель.Действует
	|					И &ИспользоватьВПоиске
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьВПоиске
	|ИЗ
	|	Справочник.Проекты.ПроектнаяКоманда КАК ПроектнаяКоманда
	|ГДЕ
	|	(ПроектнаяКоманда.Исполнитель ССЫЛКА Справочник.Пользователи
	|			ИЛИ ПроектнаяКоманда.Исполнитель ССЫЛКА Справочник.Сотрудники)");
	Запрос.УстановитьПараметр("Проект", Объект.Ссылка);
	Запрос.УстановитьПараметр("ИспользоватьВПоиске", Не Объект.ПометкаУдаления);
	
	ОбъектыПоиска = Запрос.Выполнить().Выгрузить();
	
	ОбъектыПоиска.Колонки.Добавить("Слово");
	ОбъектыПоиска.Колонки.Добавить("ОбъектДоступа");
	ОбъектыПоиска.Колонки.Добавить("КритерийПоиска");
	
	ОбъектыПоиска.ЗаполнитьЗначения(Объект.Наименование, "Слово");
	ОбъектыПоиска.ЗаполнитьЗначения(Объект.Ссылка, "ОбъектДоступа");
	ОбъектыПоиска.ЗаполнитьЗначения(Объект.Ссылка, "КритерийПоиска");
	
	НаборЗаписей.Загрузить(ОбъектыПоиска);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = Объект.Ссылка;
	НоваяЗапись.ОбъектДоступа = Объект.Ссылка;
	НоваяЗапись.КритерийПоиска = Объект.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = НЕ Объект.ПометкаУдаления;
	НоваяЗапись.Слово = Объект.Наименование;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// При изменении сотрудника
Процедура ОбновитьСловаПоискаПоСотруднику(Объект, ОбновитьКритерийПоиска = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(Объект.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	СтрокиДляПоиска = Объект.КонтактнаяИнформация.ВыгрузитьКолонку("Представление");
	
	СтрокиДляПоиска.Добавить(Строка(Объект.Владелец));
	СтрокиДляПоиска.Добавить(Строка(Объект.ПредставлениеВПереписке));
	
	КоличествоСтрок = СтрокиДляПоиска.Количество();
	Для Индекс = 1 По КоличествоСтрок Цикл
		ОбратныйИндекс = КоличествоСтрок - Индекс;
		Если ПустаяСтрока(СтрокиДляПоиска[ОбратныйИндекс]) Тогда
			СтрокиДляПоиска.Удалить(ОбратныйИндекс);
		КонецЕсли;
	КонецЦикла;
	
	Слово = СтрСоединить(СтрокиДляПоиска, "~");
	
	Если ОбновитьКритерийПоиска Тогда
		Блокировка = Новый БлокировкаДанных;
		Блокировка.Добавить("РегистрСведений.УдалитьПоискВАдреснойКниге").УстановитьЗначение("ОбъектПоиска", Объект.Ссылка);
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ОбъектПоиска.Установить(Объект.Ссылка);
		НаборЗаписей.Прочитать();
		
		ЕстьСотрудник = Ложь;
		ЕстьПодразделение = Ложь;
		ЕстьДолжность = Ложь;
		Корзина = Новый Массив;
		
		Для Каждого Запись Из НаборЗаписей Цикл
			Если ТипЗнч(Запись.КритерийПоиска) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
				Запись.КритерийПоиска = Объект.Подразделение;
				ЕстьПодразделение = Истина;
			ИначеЕсли ТипЗнч(Запись.КритерийПоиска) = Тип("СправочникСсылка.Должности") Тогда
				Запись.КритерийПоиска = Объект.Должность;
				ЕстьДолжность = Истина;
			ИначеЕсли Запись.КритерийПоиска = Объект.Ссылка Тогда
				Запись.Слово = Слово;
				ЕстьСотрудник = Истина;
			КонецЕсли;
			Запись.ИспользоватьВПоиске = НЕ Объект.ПометкаУдаления И Объект.Действует;
			Если Не ЗначениеЗаполнено(Запись.КритерийПоиска) Тогда
				Корзина.Добавить(Запись);
			КонецЕсли;
		КонецЦикла;
		Для Каждого Запись Из Корзина Цикл
			НаборЗаписей.Удалить(Запись);
		КонецЦикла;
		
		Если Не ЕстьПодразделение И ЗначениеЗаполнено(Объект.Подразделение) Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.ОбъектПоиска = Объект.Ссылка;
			НоваяЗапись.КритерийПоиска = Объект.Подразделение;
			НоваяЗапись.ИспользоватьВПоиске = НЕ Объект.ПометкаУдаления И Объект.Действует;
			НоваяЗапись.Слово = Строка(НоваяЗапись.КритерийПоиска);
		КонецЕсли;
		
		Если Не ЕстьДолжность И ЗначениеЗаполнено(Объект.Должность) Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.ОбъектПоиска = Объект.Ссылка;
			НоваяЗапись.КритерийПоиска = Объект.Должность;
			НоваяЗапись.ИспользоватьВПоиске = НЕ Объект.ПометкаУдаления И Объект.Действует;
			НоваяЗапись.Слово = Строка(НоваяЗапись.КритерийПоиска);
		КонецЕсли;
		
		Если Не ЕстьСотрудник Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.ОбъектПоиска = Объект.Ссылка;
			НоваяЗапись.КритерийПоиска = Объект.Ссылка;
			НоваяЗапись.ИспользоватьВПоиске = НЕ Объект.ПометкаУдаления И Объект.Действует;
			НоваяЗапись.Слово = Слово;
		КонецЕсли;
		
	Иначе
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.КритерийПоиска.Установить(Объект.Ссылка);
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ОбъектПоиска = Объект.Ссылка;
		НоваяЗапись.КритерийПоиска = Объект.Ссылка;
		НоваяЗапись.ИспользоватьВПоиске = НЕ Объект.ПометкаУдаления И Объект.Действует;
		НоваяЗапись.Слово = Слово;
		
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// При изменении должности
Процедура ОбновитьСловаПоискаПоДолжности(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(Объект.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(Объект.Ссылка);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Сотрудники.Ссылка КАК ОбъектПоиска,
	|	НЕ Сотрудники.ПометкаУдаления
	|		И Сотрудники.Действует
	|		И &ИспользоватьВПоиске КАК ИспользоватьВПоиске
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Должность = &Должность");
	Запрос.УстановитьПараметр("Должность", Объект.Ссылка);
	Запрос.УстановитьПараметр("ИспользоватьВПоиске", Не Объект.ПометкаУдаления);
	
	ОбъектыПоиска = Запрос.Выполнить().Выгрузить();
	
	ОбъектыПоиска.Колонки.Добавить("Слово");
	ОбъектыПоиска.Колонки.Добавить("ОбъектДоступа");
	ОбъектыПоиска.Колонки.Добавить("КритерийПоиска");
	
	ОбъектыПоиска.ЗаполнитьЗначения(Объект.Наименование, "Слово");
	ОбъектыПоиска.ЗаполнитьЗначения(Объект.Ссылка, "КритерийПоиска");
	ОбъектыПоиска.ЗаполнитьЗначения(Неопределено, "ОбъектДоступа");
	
	НаборЗаписей.Загрузить(ОбъектыПоиска);
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// При изменении роли
Процедура ОбновитьСловаПоискаПоРоли(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(Объект.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(Объект.Ссылка);
	
	СтрокиДляПоиска = Объект.КонтактнаяИнформация.ВыгрузитьКолонку("Представление");
	
	СтрокиДляПоиска.Добавить(Объект.Наименование);
	
	КоличествоСтрок = СтрокиДляПоиска.Количество();
	Для Индекс = 1 По КоличествоСтрок Цикл
		ОбратныйИндекс = КоличествоСтрок - Индекс;
		Если ПустаяСтрока(СтрокиДляПоиска[ОбратныйИндекс]) Тогда
			СтрокиДляПоиска.Удалить(ОбратныйИндекс);
		КонецЕсли;
	КонецЦикла;
	
	Слово = СтрСоединить(СтрокиДляПоиска, "~");
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = Объект.Ссылка;
	НоваяЗапись.КритерийПоиска = Объект.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = НЕ Объект.ПометкаУдаления;
	НоваяЗапись.Слово = Слово;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// При изменении Контрагента
Процедура ОбновитьСловаПоискаПоКонтрагенту(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(Объект.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(Объект.Ссылка);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КонтактныеЛица.Ссылка КАК ОбъектПоиска,
		|	НЕ КонтактныеЛица.ПометкаУдаления
		|		И &ИспользоватьВПоиске КАК ИспользоватьВПоиске
		|ИЗ
		|	Справочник.КонтактныеЛица КАК КонтактныеЛица
		|ГДЕ
		|	КонтактныеЛица.Владелец = &Контрагент";
	Запрос.УстановитьПараметр("Контрагент", Объект.Ссылка);
	Запрос.УстановитьПараметр("ИспользоватьВПоиске", Не Объект.ПометкаУдаления);
	
	ОбъектыПоиска = Запрос.Выполнить().Выгрузить();
	
	ОбъектыПоиска.Колонки.Добавить("Слово");
	ОбъектыПоиска.Колонки.Добавить("ОбъектДоступа");
	ОбъектыПоиска.Колонки.Добавить("КритерийПоиска");
	
	ОбъектыПоиска.ЗаполнитьЗначения(Объект.Наименование, "Слово");
	ОбъектыПоиска.ЗаполнитьЗначения(Объект.Ссылка, "ОбъектДоступа");
	ОбъектыПоиска.ЗаполнитьЗначения(Объект.Ссылка, "КритерийПоиска");
	
	НаборЗаписей.Загрузить(ОбъектыПоиска);
	
	СтрокиДляПоиска = Объект.КонтактнаяИнформация.ВыгрузитьКолонку("Представление");
	СтрокиДляПоиска.Добавить(Объект.Наименование);
	КоличествоСтрок = СтрокиДляПоиска.Количество();
	Для Индекс = 1 По КоличествоСтрок Цикл
		ОбратныйИндекс = КоличествоСтрок - Индекс;
		Если ПустаяСтрока(СтрокиДляПоиска[ОбратныйИндекс]) Тогда
			СтрокиДляПоиска.Удалить(ОбратныйИндекс);
		КонецЕсли;
	КонецЦикла;
	Слово = СтрСоединить(СтрокиДляПоиска, "~");
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = Объект.Ссылка;
	НоваяЗапись.ОбъектДоступа = Объект.Ссылка;
	НоваяЗапись.КритерийПоиска = Объект.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = НЕ Объект.ПометкаУдаления;
	НоваяЗапись.Слово = Слово;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// При изменении Контактного лица
Процедура ОбновитьСловаПоискаПоКонтактномуЛицу(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(Объект.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(Объект.Ссылка);
	
	СтрокиДляПоиска = Объект.КонтактнаяИнформация.ВыгрузитьКолонку("Представление");
	
	СтрокиДляПоиска.Добавить(Объект.Наименование);
	КоличествоСтрок = СтрокиДляПоиска.Количество();
	Для Индекс = 1 По КоличествоСтрок Цикл
		ОбратныйИндекс = КоличествоСтрок - Индекс;
		Если ПустаяСтрока(СтрокиДляПоиска[ОбратныйИндекс]) Тогда
			СтрокиДляПоиска.Удалить(ОбратныйИндекс);
		КонецЕсли;
	КонецЦикла;
	Слово = СтрСоединить(СтрокиДляПоиска, "~");
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = Объект.Ссылка;
	НоваяЗапись.КритерийПоиска = Объект.Ссылка;
	НоваяЗапись.ОбъектДоступа = Объект.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = НЕ Объект.ПометкаУдаления;
	НоваяЗапись.Слово = Слово;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// При изменении личного адресата
Процедура ОбновитьСловаПоискаПоЛичномуАдресату(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(Объект.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(Объект.Ссылка);
	
	СтрокиДляПоиска = Объект.КонтактнаяИнформация.ВыгрузитьКолонку("Представление");
	
	СтрокиДляПоиска.Добавить(Объект.Наименование);
	КоличествоСтрок = СтрокиДляПоиска.Количество();
	Для Индекс = 1 По КоличествоСтрок Цикл
		ОбратныйИндекс = КоличествоСтрок - Индекс;
		Если ПустаяСтрока(СтрокиДляПоиска[ОбратныйИндекс]) Тогда
			СтрокиДляПоиска.Удалить(ОбратныйИндекс);
		КонецЕсли;
	КонецЦикла;
	Слово = СтрСоединить(СтрокиДляПоиска, "~");
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = Объект.Ссылка;
	НоваяЗапись.КритерийПоиска = Объект.Ссылка;
	
	НоваяЗапись.ОбъектДоступа = Объект.Сотрудник;
	
	НоваяЗапись.ИспользоватьВПоиске = НЕ Объект.ПометкаУдаления;
	НоваяЗапись.Слово = Слово;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// При изменении критерия использования в поиске
Процедура ОбновитьДоступностьВПоиске(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(Объект.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Объект) = Тип("СправочникОбъект.Пользователи") Тогда
		ИспользоватьВПоиске = Не Объект.ПометкаУдаления
			И Не Объект.Служебный
			И Не Объект.Недействителен;
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.Сотрудники") Тогда
		ИспользоватьВПоиске = Не Объект.ПометкаУдаления
			И Объект.Действует;
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.КонтактныеЛица") Тогда
		ИспользоватьВПоиске = Не Объект.ПометкаУдаления 
			И Не Объект.НеДействует
	Иначе
		ИспользоватьВПоиске = Не Объект.ПометкаУдаления;
	КонецЕсли;
	
	// Обновление по критерию поиска.
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.УдалитьПоискВАдреснойКниге");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("КритерийПоиска", Объект.Ссылка);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	УдалитьПоискВАдреснойКниге.ОбъектПоиска КАК ОбъектПоиска,
		|	УдалитьПоискВАдреснойКниге.КритерийПоиска КАК КритерийПоиска,
		|	УдалитьПоискВАдреснойКниге.Слово КАК Слово,
		|	УдалитьПоискВАдреснойКниге.ОбъектДоступа КАК ОбъектДоступа,
		|	ВЫБОР
		|		КОГДА УдалитьПоискВАдреснойКниге.ОбъектПоиска ССЫЛКА Справочник.Пользователи
		|			ТОГДА НЕ УдалитьПоискВАдреснойКниге.ОбъектПоиска.ПометкаУдаления
		|					И НЕ УдалитьПоискВАдреснойКниге.ОбъектПоиска.Служебный
		|					И НЕ УдалитьПоискВАдреснойКниге.ОбъектПоиска.Недействителен
		|					И &ИспользоватьВПоиске
		|		КОГДА УдалитьПоискВАдреснойКниге.ОбъектПоиска ССЫЛКА Справочник.Сотрудники
		|			ТОГДА НЕ УдалитьПоискВАдреснойКниге.ОбъектПоиска.ПометкаУдаления
		|					И УдалитьПоискВАдреснойКниге.ОбъектПоиска.Действует
		|					И &ИспользоватьВПоиске
		|		КОГДА УдалитьПоискВАдреснойКниге.ОбъектПоиска ССЫЛКА Справочник.КонтактныеЛица
		|			ТОГДА НЕ УдалитьПоискВАдреснойКниге.ОбъектПоиска.ПометкаУдаления
		|					И НЕ УдалитьПоискВАдреснойКниге.ОбъектПоиска.НеДействует
		|					И &ИспользоватьВПоиске
		|		ИНАЧЕ НЕ УдалитьПоискВАдреснойКниге.ОбъектПоиска.ПометкаУдаления
		|				И &ИспользоватьВПоиске
		|	КОНЕЦ КАК ИспользоватьВПоиске
		|ИЗ
		|	РегистрСведений.УдалитьПоискВАдреснойКниге КАК УдалитьПоискВАдреснойКниге
		|ГДЕ
		|	УдалитьПоискВАдреснойКниге.КритерийПоиска = &КритерийПоиска";
	Запрос.УстановитьПараметр("КритерийПоиска", Объект.Ссылка);
	Запрос.УстановитьПараметр("ИспользоватьВПоиске", ИспользоватьВПоиске);
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.КритерийПоиска.Установить(Объект.Ссылка);
		НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	// Обновление по объекту поиска.
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.УдалитьПоискВАдреснойКниге");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("КритерийПоиска", Объект.Ссылка);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	УдалитьПоискВАдреснойКниге.ОбъектПоиска КАК ОбъектПоиска,
		|	УдалитьПоискВАдреснойКниге.КритерийПоиска КАК КритерийПоиска,
		|	УдалитьПоискВАдреснойКниге.Слово КАК Слово,
		|	УдалитьПоискВАдреснойКниге.ОбъектДоступа КАК ОбъектДоступа,
		|	ВЫБОР
		|		КОГДА УдалитьПоискВАдреснойКниге.КритерийПоиска ССЫЛКА Справочник.Пользователи
		|			ТОГДА НЕ УдалитьПоискВАдреснойКниге.КритерийПоиска.ПометкаУдаления
		|					И НЕ УдалитьПоискВАдреснойКниге.КритерийПоиска.Служебный
		|					И НЕ УдалитьПоискВАдреснойКниге.КритерийПоиска.Недействителен
		|					И &ИспользоватьВПоиске
		|		КОГДА УдалитьПоискВАдреснойКниге.КритерийПоиска ССЫЛКА Справочник.Сотрудники
		|			ТОГДА НЕ УдалитьПоискВАдреснойКниге.КритерийПоиска.ПометкаУдаления
		|					И УдалитьПоискВАдреснойКниге.КритерийПоиска.Действует
		|					И &ИспользоватьВПоиске
		|		КОГДА УдалитьПоискВАдреснойКниге.КритерийПоиска ССЫЛКА Справочник.КонтактныеЛица
		|			ТОГДА НЕ УдалитьПоискВАдреснойКниге.КритерийПоиска.ПометкаУдаления
		|					И НЕ УдалитьПоискВАдреснойКниге.КритерийПоиска.НеДействует
		|					И &ИспользоватьВПоиске
		|		ИНАЧЕ НЕ УдалитьПоискВАдреснойКниге.КритерийПоиска.ПометкаУдаления
		|				И &ИспользоватьВПоиске
		|	КОНЕЦ КАК ИспользоватьВПоиске
		|ИЗ
		|	РегистрСведений.УдалитьПоискВАдреснойКниге КАК УдалитьПоискВАдреснойКниге
		|ГДЕ
		|	УдалитьПоискВАдреснойКниге.ОбъектПоиска = &ОбъектПоиска";
	Запрос.УстановитьПараметр("ОбъектПоиска", Объект.Ссылка);
	Запрос.УстановитьПараметр("ИспользоватьВПоиске", ИспользоватьВПоиске);
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ОбъектПоиска.Установить(Объект.Ссылка);
		НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// При изменении Организации
Процедура ОбновитьСловаПоискаПоОрганизации(Объект) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(Объект.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(Объект.Ссылка);
	
	СтрокиДляПоиска = Объект.КонтактнаяИнформация.ВыгрузитьКолонку("Представление");
	
	СтрокиДляПоиска.Добавить(Объект.Наименование);
	
	КоличествоСтрок = СтрокиДляПоиска.Количество();
	Для Индекс = 1 По КоличествоСтрок Цикл
		ОбратныйИндекс = КоличествоСтрок - Индекс;
		Если ПустаяСтрока(СтрокиДляПоиска[ОбратныйИндекс]) Тогда
			СтрокиДляПоиска.Удалить(ОбратныйИндекс);
		КонецЕсли;
	КонецЦикла;
	
	Слово = СтрСоединить(СтрокиДляПоиска, "~");
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = Объект.Ссылка;
	НоваяЗапись.КритерийПоиска = Объект.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = НЕ Объект.ПометкаУдаления;
	НоваяЗапись.Слово = Слово;
	
	НаборЗаписей.Записать();	

КонецПроцедуры

// При изменении Автоподстановки для объектов.
Процедура ОбновитьСловаПоискаПоАвтоподстановкеДляОбъектов(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(Объект.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(Объект.Ссылка);
	
	СтрокиДляПоиска = Новый Массив;
	СтрокиДляПоиска.Добавить(Строка(Объект));
	
	Слово = СтрСоединить(СтрокиДляПоиска, "~");
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = Объект.Ссылка;
	НоваяЗапись.КритерийПоиска = Объект.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = НЕ Объект.ПометкаУдаления;
	НоваяЗапись.Слово = Слово;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// При изменении Автоподстановки для процессов.
Процедура ОбновитьСловаПоискаПоАвтоподстановкеДляПроцессов(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(Объект.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(Объект.Ссылка);
	
	СтрокиДляПоиска = Новый Массив;
	СтрокиДляПоиска.Добавить(Объект.Наименование);
	
	Слово = СтрСоединить(СтрокиДляПоиска, "~");
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = Объект.Ссылка;
	НоваяЗапись.КритерийПоиска = Объект.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = НЕ Объект.ПометкаУдаления;
	НоваяЗапись.Слово = Слово;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет заполнение слов поиска в регистре для всех объектов адр. книги. 
//
Процедура ЗаполнитьСловаПоиска() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Подразделение = Справочники.СтруктураПредприятия.ВыбратьИерархически();
	Пока Подразделение.Следующий() Цикл
		ОбновитьСловаПоискаПоПодразделению(Подразделение);
	КонецЦикла;
	
	РабочиеГруппы = Справочники.РабочиеГруппы.ВыбратьИерархически();
	Пока РабочиеГруппы.Следующий() Цикл
		ОбновитьСловаПоискаПоРабочейГруппе(РабочиеГруппы);
	КонецЦикла;
	
	Проекты = Справочники.Проекты.ВыбратьИерархически();
	Пока Проекты.Следующий() Цикл
		Если Проекты.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		ОбновитьСловаПоискаПоПроекту(Проекты);
	КонецЦикла;
	
	ВсеСотрудники = Справочники.Сотрудники.Выбрать();
	Пока ВсеСотрудники.Следующий() Цикл
		ОбновитьСловаПоискаПоСотруднику(ВсеСотрудники);
	КонецЦикла;
		
	Должности = Справочники.Должности.Выбрать();
	Пока Должности.Следующий() Цикл
		ОбновитьСловаПоискаПоДолжности(Должности);
	КонецЦикла;
	
	ВсеРоли = Справочники.РолиИсполнителей.Выбрать();
	Пока ВсеРоли.Следующий() Цикл
		ОбновитьСловаПоискаПоРоли(ВсеРоли);
	КонецЦикла;
	
	Контрагенты = Справочники.Контрагенты.ВыбратьИерархически();
	Пока Контрагенты.Следующий() Цикл
		Если Контрагенты.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		ОбновитьСловаПоискаПоКонтрагенту(Контрагенты);
	КонецЦикла;
	
	КонтактныеЛица = Справочники.КонтактныеЛица.Выбрать();
	Пока КонтактныеЛица.Следующий() Цикл
		ОбновитьСловаПоискаПоКонтактномуЛицу(КонтактныеЛица);
	КонецЦикла;
	
	ЛичныеАдресаты = Справочники.ЛичныеАдресаты.Выбрать();
	Пока ЛичныеАдресаты.Следующий() Цикл
		ОбновитьСловаПоискаПоЛичномуАдресату(ЛичныеАдресаты);
	КонецЦикла;
	
	АвтоподстановкиДляДокументов = Справочники.АвтоподстановкиДляОбъектов.Выбрать();
	Пока АвтоподстановкиДляДокументов.Следующий() Цикл
		ОбновитьСловаПоискаПоАвтоподстановкеДляОбъектов(АвтоподстановкиДляДокументов);
	КонецЦикла;
	
	АвтоподстановкиДляПроцессов = Справочники.АвтоподстановкиДляПроцессов.Выбрать();
	Пока АвтоподстановкиДляПроцессов.Следующий() Цикл
		ОбновитьСловаПоискаПоАвтоподстановкеДляПроцессов(АвтоподстановкиДляПроцессов);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

