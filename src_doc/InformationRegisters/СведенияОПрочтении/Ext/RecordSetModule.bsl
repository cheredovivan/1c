#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ПараметрыСеанса.ЗагрузкаОбработанныхДанныхИзДругойСистемы Тогда          
		
		// добавление, и еще удаление   
		ЭтоИзменения = Истина;              
		Если ДополнительныеСвойства.Свойство("ЭтоИзменения") Тогда
			ЭтоИзменения = ДополнительныеСвойства.ЭтоИзменения;
		КонецЕсли;	
		
		Если ЭтотОбъект.Количество() <> 0 Тогда
		
			Для Каждого Запись Из ЭтотОбъект Цикл
				
				Если ТипЗнч(Запись.Объект) = Тип("ДокументСсылка.ВходящееПисьмо")
						Или ТипЗнч(Запись.Объект) = Тип("ДокументСсылка.ИсходящееПисьмо") Тогда     
						
					// ЭтоИзменения = Ложь - т.е. исторические  - не пишем количество писем	
					Если ЭтоИзменения = Ложь Тогда
						Продолжить;
					КонецЕсли;	
						
					Если Не ОбщегоНазначения.СсылкаСуществует(Запись.Объект) Тогда    
							
						Письмо = Запись.Объект;       
						Прочтен = Запись.Прочтен;
						
						ПрочтенЗначениеВРегистре = РегистрыСведений.СведенияОПрочтении.ОбъектБылПрочтен(Письмо);
						Если Прочтен <> ПрочтенЗначениеВРегистре Тогда
							
							РегистрыСведений.ОчередьПостОбработкиЗагрузки.ДобавитьВОчередьКоличествоПисем(
								Запись.Объект, Запись.Прочтен, Запись.Пользователь);
						КонецЕсли;	 
							
					Иначе		
							
						РаботаСПрочтениями.УстановитьСвойствоПрочтенОбъект(Запись.Объект, Запись.Прочтен, Ложь, Запись.Пользователь);
							
					КонецЕсли;	
					
				КонецЕсли;
						
			КонецЦикла;	  
			
		Иначе	   
			
			Если ЗначениеЗаполнено(ЭтотОбъект.Отбор.Пользователь.Значение) И 
				ЗначениеЗаполнено(ЭтотОбъект.Отбор.Объект.Значение) Тогда

				Если ТипЗнч(ЭтотОбъект.Отбор.Объект.Значение) = Тип("ДокументСсылка.ВходящееПисьмо")
						Или ТипЗнч(ЭтотОбъект.Отбор.Объект.Значение) = Тип("ДокументСсылка.ИсходящееПисьмо") Тогда
						
					// ЭтоИзменения = Ложь - т.е. исторические  - не пишем количество писем	
					Если ЭтоИзменения = Истина Тогда
						
						Прочтен = Ложь; // удалили  запись РС - сделали не-прочтенным (Прочтен = Ложь)     
						Письмо = ЭтотОбъект.Отбор.Объект.Значение;
						Пользователь = ЭтотОбъект.Отбор.Пользователь.Значение;
							
						Если Не ОбщегоНазначения.СсылкаСуществует(ЭтотОбъект.Отбор.Объект.Значение) Тогда  
							
							ПрочтенЗначениеВРегистре = РегистрыСведений.СведенияОПрочтении.ОбъектБылПрочтен(Письмо);
							Если Прочтен <> ПрочтенЗначениеВРегистре Тогда
								
								РегистрыСведений.ОчередьПостОбработкиЗагрузки.ДобавитьВОчередьКоличествоПисем(
									Письмо, Прочтен, Пользователь);
								
							КонецЕсли; 
						Иначе		
							
							РаботаСПрочтениями.УстановитьСвойствоПрочтенОбъект(
								ЭтотОбъект.Отбор.Объект.Значение, Прочтен, Ложь, ЭтотОбъект.Отбор.Пользователь.Значение);
							
						КонецЕсли;	
						
					КонецЕсли;	
						
				КонецЕсли;		
				
			КонецЕсли;	
		
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
