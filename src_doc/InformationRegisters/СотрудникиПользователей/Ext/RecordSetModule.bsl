#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить(
		"ПараметрыОбновленияДанныхКэширующихОбъектов",
		РегистрыСведений.СотрудникиПользователей.ЗначенияПараметровОбновленияДанныхКэширующихОбъектов(ЭтотОбъект));
	
	ЭтоУдаление = Количество() = 0;	
	Если ЭтоУдаление Тогда
		
		СтарыеЗаписи = НаборСтарыхЗаписей();
		ДополнительныеСвойства.Вставить("СтарыеЗаписи", СтарыеЗаписи);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьЕстьЛиНесколькоПользователейУОдногоСотрудника();
	
	РегистрыСведений.СотрудникиПользователей.ОбновитьДанныеКэширующихОбъектов(
		ЭтотОбъект, ДополнительныеСвойства.ПараметрыОбновленияДанныхКэширующихОбъектов);
	
	ЗапротоколироватьДобавлениеВСписокСотрудников();
	ЗапротоколироватьУдалениеИзСпискаСотрудников();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НаборСтарыхЗаписей()
	
	НаборСтарыхЗаписей = РегистрыСведений.СотрудникиПользователей.СоздатьНаборЗаписей();
		
	Для Каждого ЭлементОтбора Из Отбор Цикл
		НаборСтарыхЗаписей.Отбор[ЭлементОтбора.Имя].Установить(ЭлементОтбора.Значение);
	КонецЦикла;
		
	НаборСтарыхЗаписей.Прочитать();
	Возврат НаборСтарыхЗаписей;
			
КонецФункции

// Нельзя иметь нескольких пользователей у одного сотрудника. Наоборот, несколько сотрудников у одного пользователя -
// можно. Система прав доступа такое не предусматривает. В частности сломается получение данных из регистров
// ПраваПоДескрипторамДоступаОбъектов, ПраваПоДескрипторамДоступаРегистров и СоставСубъектовПравДоступа.
Процедура ПроверитьЕстьЛиНесколькоПользователейУОдногоСотрудника()
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Сотрудник,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Пользователь) КАК КолвоПользователей
		|ИЗ
		|	РегистрСведений.СотрудникиПользователей
		|ГДЕ
		|	Сотрудник В (&ПроверяемыеСотрудники)
		|СГРУППИРОВАТЬ ПО
		|	Сотрудник");
	// Проверяем только набор, который сейчас записывается:
	Запрос.УстановитьПараметр(
		"ПроверяемыеСотрудники", 
		ОбщегоНазначенияКлиентСервер.СвернутьМассив(ВыгрузитьКолонку("Сотрудник")));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.КолвоПользователей > 1 Тогда
			Фраза = СтрШаблон(
				НСтр("ru = 'Для сотрудника %1 существует %2. Такого быть не должно'"),
				Выборка.Сотрудник,
				СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
					НСтр("ru = ';%1 пользователь;;%1 пользователя;%1 пользователей;%1 пользователя'"),
					Выборка.КолвоПользователей));
			ВызватьИсключение Фраза;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗапротоколироватьДобавлениеВСписокСотрудников()
	
	Для Каждого НоваяЗапись Из ЭтотОбъект Цикл
		
		ПротоколированиеРаботыСотрудников.ЗаписатьДобавлениеВСписокСотрудниковПользователя(
			НоваяЗапись.Сотрудник, НоваяЗапись.Пользователь);
			
	КонецЦикла;
			
КонецПроцедуры

Процедура ЗапротоколироватьУдалениеИзСпискаСотрудников()
	
	Если ДополнительныеСвойства.Свойство("СтарыеЗаписи") Тогда
	
		Для Каждого СтараяЗапись Из ДополнительныеСвойства.СтарыеЗаписи Цикл
		
			ПротоколированиеРаботыСотрудников.ЗаписатьУдалениеИзСпискаСотрудниковПользователя(
				СтараяЗапись.Сотрудник, СтараяЗапись.Пользователь);
		
		КонецЦикла;
	
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли