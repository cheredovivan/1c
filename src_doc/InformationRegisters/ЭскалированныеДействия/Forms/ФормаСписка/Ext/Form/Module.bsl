#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("ПравилоЭскалации") Тогда
		Элементы.ПравилоЭскалации.Видимость = Ложь;
		АвтоЗаголовок = Ложь;
		Заголовок = СтрШаблон(
			НСтр("ru = 'История срабатывания правила эскалации ""%1""'"),
			Параметры.Отбор.ПравилоЭскалации);
	КонецЕсли;

	МассивКолонок = Новый Массив;
	МассивКолонок.Добавить("Предмет");
	МассивКолонок.Добавить("Участник");
	Список.УстановитьОграниченияИспользованияВПорядке(МассивКолонок);
	Список.УстановитьОграниченияИспользованияВГруппировке(МассивКолонок);
	Список.УстановитьОграниченияИспользованияВОтборе(МассивКолонок);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДействияКлиентСервер.ЭтоДействиеОзнакомления(ТекущиеДанные.Действие) Тогда
		
		ДействиеЗадачи = ДействияВызовСервера.ДействиеЗадачиДляСтрокиОзнакомления(
			ТекущиеДанные.Действие, ТекущиеДанные.ИдентификаторУчастника);
		Если ЗначениеЗаполнено(ДействиеЗадачи) Тогда
			ПоказатьЗначение(, ДействиеЗадачи);	
			Возврат;     
		КонецЕсли;	
		
	Иначе	
	
		КлючОткрытия = Новый Структура;
		КлючОткрытия.Вставить("Действие", ТекущиеДанные.Действие);
		КлючОткрытия.Вставить("ИдентификаторУчастника", ТекущиеДанные.ИдентификаторУчастника);
			
		КарточкаОткрыта = РаботаСЗадачамиКлиент.ОткрытьКарточкуИсполнителяЗадачи(КлючОткрытия);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьОтчеты(Команда)
	
	Раздел = ПредопределенноеЗначение("Перечисление.РазделыОтчетов.ЭскалацияЗадач");
	
	ЗаголовокФормы = НСтр("ru = 'Отчеты по эскалации задач'");
	
	РазделГипперссылка = НастройкиВариантовОтчетовДокументооборот.ПолучитьРазделОтчетаПоИмени(
		"НСИ");
	
	ПараметрыФормы = Новый Структура(
		"Раздел, ЗаголовокФормы, НеОтображатьИерархию, РазделГипперссылка", 
		Раздел, ЗаголовокФормы, Истина, РазделГипперссылка);
	
	ОткрытьФорму(
		"Обработка.ВсеОтчеты.Форма.ФормаПоКатегориям",
		ПараметрыФормы,
		ЭтаФорма,
		"ЭскалацияЗадач");
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	// сгруппировать по действию, для одного получить ТЧ Участники + Предмет.
	
	УникальныеДействия = Новый Соответствие; // Ключ - Действие, Значение - Структура
	
	Для Каждого КлючИЗначение Из Строки Цикл
		
		ДанныеСтроки = КлючИЗначение.Значение.Данные;
		
		// Действие
		Если ДанныеСтроки.Свойство("Действие") Тогда
			
			ЗначениеДействия = УникальныеДействия.Получить(ДанныеСтроки.Действие);
			Если ЗначениеДействия = Неопределено Тогда
				УникальныеДействия.Вставить(ДанныеСтроки.Действие, Новый Структура);
			КонецЕсли;	
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого КлючИЗначение Из УникальныеДействия Цикл
		
		Действие =  КлючИЗначение.Ключ;
		
		РеквизитыДействия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Действие, "Предмет, Участники");
		РеквизитыДействия.Участники = РеквизитыДействия.Участники.Выгрузить(); 
		
		КлючИЗначение.Значение.Вставить("Предмет", РеквизитыДействия.Предмет);
		КлючИЗначение.Значение.Вставить("Участники", РеквизитыДействия.Участники);
		
	КонецЦикла;	
	
	Для Каждого КлючИЗначение Из Строки Цикл
		
		ДанныеСтроки = КлючИЗначение.Значение.Данные;  
		
		Если ДанныеСтроки.Свойство("Действие") Тогда

			ЗначениеДействия = УникальныеДействия.Получить(ДанныеСтроки.Действие);
			
			Если ЗначениеДействия <> Неопределено Тогда
				
				ДанныеСтроки.Предмет = ЗначениеДействия.Предмет;
				
				СтрокаНашли = ЗначениеДействия.Участники.Найти(ДанныеСтроки.ИдентификаторУчастника, "Идентификатор");
				Если СтрокаНашли <> Неопределено Тогда
					ДанныеСтроки.Участник = СтрокаНашли.Участник;
				КонецЕсли;	
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти