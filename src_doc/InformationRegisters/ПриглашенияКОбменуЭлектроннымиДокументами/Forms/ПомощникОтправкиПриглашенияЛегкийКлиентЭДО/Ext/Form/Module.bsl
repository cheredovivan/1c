#Область ОписаниеПеременных

&НаКлиенте
Перем НачалоОтправкиПриглашения;

&НаКлиенте
Перем ПриглашениеОтправлено;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не НастройкиЭДО.ЕстьПравоНастройкиОбмена() Тогда
		ВызватьИсключение НСтр("ru = 'Недостаточно прав для выполнения операции'");
	КонецЕсли;
	
	ОбщегоНазначенияБЭД.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);

	Элементы.Контрагент.ОграничениеТипа = Метаданные.ОпределяемыеТипы.КонтрагентБЭД.Тип;
	
	ЗаполнитьРеквизитыИзПараметров();
	ЗаполнитьСоответствиеОператоровЭДО();
	
	Если Не ЗначениеЗаполнено(Организация)
		И Не ИнтеграцияЭДО.ИспользуетсяНесколькоОрганизаций() Тогда
		Организация = ИнтеграцияЭДО.ОрганизацияПоУмолчанию();
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ЗаполнитьИдентификаторыОрганизации();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(ИдентификаторОрганизации) И ЗначениеЗаполнено(Контрагент) Тогда
		ЗаполнитьДанныеДляНастройкиОтправкиПриглашения();
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриглашениеОтправлено = Ложь;
	
	ОжидатьПолучениеДанныхДляНастройкиОтправкиПриглашения();
	ПриИзмененииИдентификатораОрганизации();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСостояниеПриглашений"
		И ПриглашениеОтправлено <> Истина Тогда
		УправлениеФормой();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ЗаполнитьИдентификаторыОрганизации();
	ПриИзмененииИдентификатораОрганизации();
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(ИдентификаторОрганизации) И ЗначениеЗаполнено(Контрагент) Тогда
		ЗаполнитьДанныеДляНастройкиОтправкиПриглашения();
		ОжидатьПолучениеДанныхДляНастройкиОтправкиПриглашения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторОрганизацииПриИзменении(Элемент)
	ПриИзмененииИдентификатораИнтерактивно();
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторОрганизацииОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
		Возврат;
	КонецЕсли;
	
	УчетныеЗаписиЭДОКлиент.ОткрытьУчетнуюЗапись(ИдентификаторОрганизации);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ИдентификаторКонтрагента = "";
	Элементы.ОператорКонтрагентаНаСтраницеВыбора.Видимость = Ложь;
	Элементы.ОператорКонтрагента.Видимость = Ложь;
	
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(ИдентификаторОрганизации) И ЗначениеЗаполнено(Контрагент) Тогда
		ЗаполнитьДанныеДляНастройкиОтправкиПриглашения();
		ОжидатьПолучениеДанныхДляНастройкиОтправкиПриглашения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторКонтрагентаВводПриИзменении(Элемент)
	ПриИзмененииИдентификатораКонтрагента();
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторКонтрагентаВводИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ПроверитьВводАсинхронно();
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторКонтрагентаВводНаСтраницеВыбораПриИзменении(Элемент)
	ПриИзмененииИдентификатораКонтрагента();
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторКонтрагентаВводНаСтраницеВыбораИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ПроверитьВводАсинхронноНаСтраицеВыбора();
КонецПроцедуры

&НаКлиенте
Процедура НадписьОшибкаПерейтиНажатие(Элемент)
	
	ОтборЖурнала = Новый Структура;
	Если ЗначениеЗаполнено(НачалоОтправкиПриглашения) Тогда
		ОтборЖурнала.Вставить("ДатаНачала", НачалоОтправкиПриглашения);
	КонецЕсли;
	ОтборЖурнала.Вставить("Уровень", "Ошибка");
	ЖурналРегистрацииКлиент.ОткрытьЖурналРегистрации(ОтборЖурнала);

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНадписьПредупреждениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыОбработкиОшибок = ОбработкаНеисправностейБЭДКлиент.НовыеПараметрыОбработкиОшибок();
	ПараметрыОбработкиОшибок.ГруппаПредупреждения = Элементы.ГруппаПредупреждения;
	КонтекстДиагностики.ОшибкиОбработаны = Ложь;
	ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(КонтекстДиагностики, ПараметрыОбработкиОшибок);
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПолучательСпособВыбораПриСменеСтраницы(Элемент, ТекущаяСтраница)
	ИдентификаторКонтрагента = "";
	
	Если ТекущаяСтраница = Элементы.СтраницаПолучательВводВручную Тогда
		ПроверитьВводАсинхронноНаСтраицеВыбора();
	Иначе
		
		ВсеВыбранныеСроки = УчетныеЗаписиКонтрагента.НайтиСтроки(Новый Структура("Выбран", 1));
		Для Каждого ВыбраннаяСтрокаУЗ Из ВсеВыбранныеСроки Цикл
			Элементы.УчетныеЗаписиКонтрагента.ТекущаяСтрока = ВыбраннаяСтрокаУЗ.ПолучитьИдентификатор();
			ИдентификаторКонтрагента = ВыбраннаяСтрокаУЗ.Идентификатор;
		КонецЦикла;
		
		УстановитьДоступностьИВидимостьКоманднойПанелиОсновнойСтраницы(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУчетныеЗаписиКонтрагента

&НаКлиенте
Процедура УчетныеЗаписиКонтрагентаПриАктивизацииСтроки(Элемент)

	Если Не Элемент.ТекущиеДанные = Неопределено Тогда
		ОтменитьВыборУчетныхЗаписей();
	
		Элемент.ТекущиеДанные.Выбран = 1;
		ИдентификаторКонтрагента = Элемент.ТекущиеДанные.Идентификатор;
		
		УстановитьДоступностьИВидимостьКоманднойПанелиОсновнойСтраницы(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УчетныеЗаписиКонтрагентаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Элемент.ТекущиеДанные = Неопределено Или Не ЗначениеЗаполнено(Элемент.ТекущиеДанные.Приглашение) Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, Элемент.ТекущиеДанные.Приглашение);
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отправить(Команда)
	
	ОтправитьПриглашение();
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ПриЗакрытииОтмене();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПослеОтправки(Команда)
	ПриЗакрытииОтмене();
КонецПроцедуры

&НаКлиенте
Функция РезультатОтправкиПриглашения()
	
	Результат = Новый Структура;
	Результат.Вставить("Организация", Организация);
	Результат.Вставить("Контрагент", Контрагент);
	Результат.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
	Результат.Вставить("ТекстПриглашения", СтандартныйШаблонПриглашения());
	Результат.Вставить("ОператорКонтрагента");
	Результат.Вставить("ОткрылиПисьмоОВыбореОператора", Ложь);
	Результат.Вставить("ИдентификаторКонтрагента", ИдентификаторКонтрагента);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура Применить(Команда)
	
	Результат = РезультатОтправкиПриглашения();
	
	Закрыть(Результат);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РедактированиеИдентификаторовКонтрагента
&НаКлиенте
Процедура ОтменитьВыборУчетныхЗаписей()
	ВсеВыбранныеСроки = УчетныеЗаписиКонтрагента.НайтиСтроки(Новый Структура("Выбран", 1));
	Для Каждого ВыбраннаяСтрока Из ВсеВыбранныеСроки Цикл
		ВыбраннаяСтрока.Выбран = 0;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииИдентификатораКонтрагента()
	
	Если ЗначениеЗаполнено(ИдентификаторКонтрагента) Тогда
		ИдентификаторАбонентаКорректный = РаботаСАбонентамиЭДОКлиентСервер.ИдентификаторАбонентаКорректный(ИдентификаторКонтрагента);
	
		Элементы.ИдентификаторКонтрагентаВводНеКорректен.Видимость = Не ИдентификаторАбонентаКорректный;
		Элементы.ИдентификаторКонтрагентаВводНеКорректенНаСтраницеВыбора.Видимость = Не ИдентификаторАбонентаКорректный;
		
		Если ИдентификаторАбонентаКорректный Тогда
			СтрокиОператора = ТаблицаОператоровЭДО.НайтиСтроки(Новый Структура("ИдентификаторОператора", Лев(ИдентификаторКонтрагента, 3)));
			Если ЗначениеЗаполнено(СтрокиОператора) Тогда
				ЗаголовокОператора = СтрШаблон(НСтр("ru = 'Оператор %1'"), СтрокиОператора[0].Представление);
				Элементы.ОператорКонтрагентаНаСтраницеВыбора.Видимость = Истина;
				Элементы.ОператорКонтрагента.Видимость = Истина;
				Элементы.ОператорКонтрагентаНаСтраницеВыбора.Заголовок = ЗаголовокОператора;
				Элементы.ОператорКонтрагента.Заголовок = ЗаголовокОператора;
			Иначе
				ЗаголовокОператора = НСтр("ru = 'Оператор не определён'");
				Элементы.ОператорКонтрагентаНаСтраницеВыбора.Видимость = Истина;
				Элементы.ОператорКонтрагента.Видимость = Истина;
				Элементы.ОператорКонтрагентаНаСтраницеВыбора.Заголовок = ЗаголовокОператора;
				Элементы.ОператорКонтрагента.Заголовок = ЗаголовокОператора;
			КонецЕсли;
			
		Иначе
			Элементы.ОператорКонтрагентаНаСтраницеВыбора.Видимость = Ложь;
			Элементы.ОператорКонтрагента.Видимость = Ложь;
			Элементы.ОператорКонтрагентаНаСтраницеВыбора.Заголовок = "";
			Элементы.ОператорКонтрагента.Заголовок = "";
		КонецЕсли;
		
	Иначе
		Элементы.ИдентификаторКонтрагентаВводНеКорректен.Видимость = Ложь;
		Элементы.ИдентификаторКонтрагентаВводНеКорректенНаСтраницеВыбора.Видимость = Ложь;
		Элементы.ОператорКонтрагентаНаСтраницеВыбора.Видимость = Ложь;
		Элементы.ОператорКонтрагента.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьДоступностьИВидимостьКоманднойПанелиОсновнойСтраницы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПроверитьВводАсинхронно()
	ИдентификаторКонтрагента = Элементы.ИдентификаторКонтрагентаВвод.ТекстРедактирования;
	ПриИзмененииИдентификатораКонтрагента();
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПроверитьВводАсинхронноНаСтраицеВыбора()
	ИдентификаторКонтрагента = Элементы.ИдентификаторКонтрагентаВводНаСтраницеВыбора.ТекстРедактирования;
	ПриИзмененииИдентификатораКонтрагента();
КонецПроцедуры

#КонецОбласти

#Область ПолучениеДанныхСпискаИдентификаторовКонтрагента

&НаСервере
Процедура ЗаполнитьДанныеДляНастройкиОтправкиПриглашения()
	ВыводитьОжиданиеДанныхПолучателяИзСервиса = Истина;
	ДлительнаяОперация = НачатьПолучениеДанныхДляНастройкиОтправкиПриглашения();
	Если ДлительнаяОперация = Неопределено
		ИЛИ ДлительнаяОперация.Статус = "Ошибка" Тогда
		ОписаниеОшибки = НСтр("ru = 'Не удалось загрузить данные из сервиса настроек.'");
		НастроитьСтраницуОшибки(ЭтотОбъект, ОписаниеОшибки);
		ДлительнаяОперация = Неопределено;
		ВыводитьОжиданиеДанныхПолучателяИзСервиса = Ложь;
		Возврат;
	КонецЕсли;
	
	УправлениеФормой();

КонецПроцедуры

&НаСервере
Функция НачатьПолучениеДанныхДляНастройкиОтправкиПриглашения()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение данных по участникам ЭДО для отправки приглашения.'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ПараметрыДанных = ИнтерфейсДокументовЭДО.НовыеПараметрыДанныхЗаполненияНастройкиОтправкиПриглашений();
	ПараметрыДанных.Организация = Организация;
	ПараметрыДанных.ОбновитьКэшНастроек = Истина;
	
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Контрагент) Тогда
			
		ПараметрыПолученияСвязиИдентификаторов = ИнтерфейсДокументовЭДО.НовыеПараметрыПолученияСвязиИдентификаторов();
		ПараметрыПолученияСвязиИдентификаторов.Организация = Организация;
		ПараметрыПолученияСвязиИдентификаторов.Контрагент = Контрагент;
		ПараметрыПолученияСвязиИдентификаторов.ФормироватьСписокВыбора = Истина;
		
		Если ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
			ПараметрыПолученияСвязиИдентификаторов.ИдентификаторыОрганизации.Добавить(ИдентификаторОрганизации);
		КонецЕсли;
		
		ПараметрыДанных.ПараметрыПолученияСвязиИдентификаторов = ПараметрыПолученияСвязиИдентификаторов;
	КонецЕсли;
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"ИнтерфейсДокументовЭДО.ДанныеЗаполненияНастройкиОтправкиПриглашений", ПараметрыДанных);
	
КонецФункции

&НаСервере
Процедура ПодготовитьФормуПослеПолученияДанныхДляНастройкиОтправкиПриглашения(Знач АдресРезультата)
	
	УчетныеЗаписиКонтрагента.Очистить();
	ИдентификаторКонтрагента = "";
	
	ДанныхДляОтправкиПриглашений = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	КонтекстДиагностики = ДанныхДляОтправкиПриглашений.КонтекстДиагностики;
	
	ПараметрыСвязиИдентификаторов = ДанныхДляОтправкиПриглашений.ПараметрыСвязиИдентификаторовЭДО;
	Если ЗначениеЗаполнено(ПараметрыСвязиИдентификаторов) Тогда
		ПараметрыТекущейСвязи = ПараметрыСвязиИдентификаторов[0];
		ИдентификаторКонтрагента = ПараметрыТекущейСвязи.ИдентификаторКонтрагента;

		ОтправленныеПриглашения = ОтправленныеПриглашения(ИдентификаторОрганизации);
		
		Для Каждого ИдентификаторДляВыбора Из ПараметрыТекущейСвязи.ДополнительныеДанныеСпискаВыбора Цикл
			Если ИдентификаторКонтрагента = ИдентификаторДляВыбора.Ключ Тогда
				ОписаниеУчетнойЗаписи = УчетныеЗаписиКонтрагента.Вставить(0);
				ОписаниеУчетнойЗаписи.Выбран = 1;
			Иначе
				ОписаниеУчетнойЗаписи = УчетныеЗаписиКонтрагента.Добавить();
			КонецЕсли;
			
			СвойстваУчетнойЗаписи = ИдентификаторДляВыбора.Значение;
			
			ЗаполнитьЗначенияСвойств(ОписаниеУчетнойЗаписи, СвойстваУчетнойЗаписи);
			ОписаниеУчетнойЗаписи.Идентификатор = ИдентификаторДляВыбора.Ключ;
			ОписаниеУчетнойЗаписи.Оператор = СвойстваУчетнойЗаписи.ПредставлениеОператора;
			ОписаниеУчетнойЗаписи.Комментарий = СвойстваУчетнойЗаписи.КраткоеОписание;
			ОписаниеУчетнойЗаписи.Представление = СвойстваУчетнойЗаписи.ПредставлениеОператора + " ," 
				+ СвойстваУчетнойЗаписи.КраткоеОписание
				+ Символы.ПС
				+ ОписаниеУчетнойЗаписи.Идентификатор;
			
			СсылкаНаОтправленноеПриглашение = ОтправленныеПриглашения.Получить(ИдентификаторДляВыбора.Ключ);
			Если СсылкаНаОтправленноеПриглашение <> Неопределено Тогда
				ОписаниеУчетнойЗаписи.Приглашение = СсылкаНаОтправленноеПриглашение.КлючЗаписиПриглашения;
				ОписаниеУчетнойЗаписи.Статус = СсылкаНаОтправленноеПриглашение.Статус;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	УправлениеФормой();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтправленныеПриглашения(ИдентификаторОрганизации)
	Запросы = Новый Массив;
	
	СтатусыПриглашений = Новый Массив;
	СтатусыПриглашений.Добавить(Перечисления.СтатусыПриглашений.Принято);
	СтатусыПриглашений.Добавить(Перечисления.СтатусыПриглашений.ОжидаемСогласия);
	
	Отбор = ПриглашенияЭДО.НовыйОтборПриглашений();
	Отбор.ИдентификаторОрганизации = "&ИдентификаторОтправителя";
	Отбор.Тип = "&ТипПриглашенияНаИдентификатор";
	Отбор.Статус = "&СтатусыУспешныхПриглашений";
	
	ЗапросПриглашений = ПриглашенияЭДО.ЗапросПриглашений("Приглашения", Отбор);
	
	Запросы.Добавить(ЗапросПриглашений);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Приглашения.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
	|	Приглашения.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
	|	Приглашения.КлючПриглашения КАК КлючПриглашения,
	|	Приглашения.Статус КАК Статус,
	|	Представление(Приглашения.Статус) КАК ПредставлениеСтатуса
	|ИЗ
	|	Приглашения КАК Приглашения";
	
	ИтоговыйЗапрос = ОбщегоНазначенияБЭД.СоединитьЗапросы(Запрос, Запросы);
	ИтоговыйЗапрос.УстановитьПараметр("ИдентификаторОтправителя", ИдентификаторОрганизации);
	ИтоговыйЗапрос.УстановитьПараметр("ТипПриглашенияНаИдентификатор", Перечисления.ТипыПриглашений.НаИдентификатор);
	ИтоговыйЗапрос.УстановитьПараметр("СтатусыУспешныхПриглашений", СтатусыПриглашений);
	
	РезультатЗапроса = ИтоговыйЗапрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Результат = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		
		КлючОтправленногоПриглашения = ПриглашенияЭДОКлиентСервер.НовыйКлючПриглашения();
		КлючОтправленногоПриглашения.Ключ = Выборка.КлючПриглашения;
		КлючОтправленногоПриглашения.ИдентификаторОрганизации = Выборка.ИдентификаторОрганизации;
		
		КлючЗаписиПриглашения = ПриглашенияЭДО.КлючЗаписиПриглашения(КлючОтправленногоПриглашения);
		
		Если Выборка.Статус = Перечисления.СтатусыПриглашений.ОжидаемСогласия Тогда
			ПредставлениеСтатуса = НСтр("ru = 'Приглашение отправлено'");
		ИначеЕсли Выборка.Статус = Перечисления.СтатусыПриглашений.Принято Тогда
			ПредставлениеСтатуса = НСтр("ru = 'Приглашение принято'");
		Иначе
			ПредставлениеСтатуса = Выборка.ПредставлениеСтатуса;
		КонецЕсли;
		Результат.Вставить(Выборка.ИдентификаторКонтрагента, 
			Новый Структура("КлючЗаписиПриглашения, Статус", КлючЗаписиПриглашения, ПредставлениеСтатуса));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОжидатьПолучениеДанныхДляНастройкиОтправкиПриглашения()
	
	Если ДлительнаяОперация = Неопределено Тогда
		ПоказатьГруппуПредупреждения();
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьДанныеДляНастройкиОтправкиПриглашенияПослеПолучения", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеДляНастройкиОтправкиПриглашенияПослеПолучения(Результат, Контекст) Экспорт
	
	ДлительнаяОперация = Неопределено;
	ВыводитьОжиданиеДанныхПолучателяИзСервиса = Ложь;
	
	Если Результат = Неопределено Тогда
		Возврат;
	ИначеЕсли Результат.Статус <> "Выполнено" Тогда
		ОписаниеОшибки = НСтр("ru = 'Не удалось загрузить данные из сервиса настроек.'");
		НастроитьСтраницуОшибки(ЭтотОбъект, ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	ПодготовитьФормуПослеПолученияДанныхДляНастройкиОтправкиПриглашения(Результат.АдресРезультата);
	ПоказатьГруппуПредупреждения();
	
КонецПроцедуры

#КонецОбласти

#Область ИдентификаторыОрганизации

&НаКлиенте
Процедура УстановитьИдентификаторПослеСозданияУчетнойЗаписи(Идентификатор, Контекст) Экспорт
	
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторОрганизации = Идентификатор;
	
	ПриИзмененииИдентификатораИнтерактивно();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииИдентификатораИнтерактивно()
	ПриИзмененииИдентификатораОрганизации();
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(ИдентификаторОрганизации) И ЗначениеЗаполнено(Контрагент) Тогда
		ЗаполнитьДанныеДляНастройкиОтправкиПриглашения();
		ОжидатьПолучениеДанныхДляНастройкиОтправкиПриглашения();
	КонецЕсли;
	УстановитьДоступностьИВидимостьКоманднойПанелиОсновнойСтраницы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИдентификаторыОрганизации()
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторОрганизации = "";
	СписокВыбора = Элементы.ИдентификаторОрганизации.СписокВыбора;
	
	СписокВыбора.Очистить();
	
	ИдентификаторыОрганизации = УчетныеЗаписиЭДО.СписокИдентификаторовУчетныхЗаписейОрганизацииСервисаЭДО(Организация);
	
	Если ЗначениеЗаполнено(ИдентификаторыОрганизации) Тогда
		Если ИдентификаторыОрганизации.Количество() = 1 Тогда
			ИдентификаторОрганизации = ИдентификаторыОрганизации[0].Значение;
		КонецЕсли;
		
		Для Каждого ЭлементСписка Из ИдентификаторыОрганизации Цикл
			ЗаполнитьЗначенияСвойств(СписокВыбора.Добавить(), ЭлементСписка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииИдентификатораОрганизации()
	
	СтрокиОператора = ТаблицаОператоровЭДО.НайтиСтроки(
		Новый Структура("ИдентификаторОператора", Лев(ИдентификаторОрганизации, 3)));
	Если ЗначениеЗаполнено(СтрокиОператора) Тогда
		Элементы.ОператорОрганизации.Заголовок = СтрШаблон(НСтр("ru = 'Оператор %1'"), СтрокиОператора[0].Представление);
	
	Иначе
		Элементы.ОператорОрганизации.Заголовок = "";
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОтправкаПриглашения

&НаКлиенте
Процедура ОтправитьПриглашение()
	
	НачалоОтправкиПриглашения = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	НастроитьСтраницуОжидания();
	
	Приглашение = ПриглашенияЭДОКлиент.НовоеПриглашениеНаИдентификатор();
	Приглашение.ИдентификаторКонтрагента = ИдентификаторКонтрагента;
	Приглашение.Контрагент = Контрагент;
	УстановитьРеквизитыКонтрагентаВПриглашение(Приглашение);
	
	Приглашение.ИдентификаторОрганизации = ИдентификаторОрганизации;
	Приглашение.Организация = Организация;
	
	Приглашение.ТекстПриглашения = СтандартныйШаблонПриглашения();
		
	Если ЗначениеЗаполнено(КлючПриглашения) Тогда
		Приглашение.КлючПриглашения = КлючПриглашения;
	Иначе
		ПриглашенияЭДОСлужебныйКлиент.АктуализироватьКлючПриглашенияПоНатуральнымКлючам(Приглашение);
	КонецЕсли;
	
	Приглашение.ЭтоОблачныйЭДО = ЭтоОблачныйЭДО;
	
	Приглашения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Приглашение);
	
	Оповещение = Новый ОписаниеОповещения("ОтправитьПриглашениеЗавершение", ЭтотОбъект);
	ПриглашенияЭДОСлужебныйКлиент.ОтправитьПриглашения(Приглашения, ЭтотОбъект, Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПриглашениеЗавершение(РезультатОтправки, Контекст) Экспорт
	
	Если Не РезультатОтправки.Успех Тогда
		НастроитьСтраницуОшибки(ЭтотОбъект);
		ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(РезультатОтправки.КонтекстДиагностики);
		Возврат;
	КонецЕсли;
	
	ПриглашениеОтправлено = Истина;
	
	НастроитьСтраницуВыполнено();
	
	Оповестить("ОбновитьСостояниеПриглашений");
	
	ОповеститьОбИзмененииПриглашения();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьРеквизитыКонтрагентаВПриглашение(Приглашение)
	ОтборПоИдентификатору = Новый Структура("Идентификатор", ИдентификаторКонтрагента);
	СтрокиИдентификатора = УчетныеЗаписиКонтрагента.НайтиСтроки(ОтборПоИдентификатору);
	Если ЗначениеЗаполнено(СтрокиИдентификатора) Тогда
		СтрокаИдентификатора = СтрокиИдентификатора[0];
		
		Приглашение.ПолучательИНН = СтрокаИдентификатора.ИНН;
		Приглашение.ПолучательКПП = СтрокаИдентификатора.КПП;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Приглашение.ПолучательИНН) Тогда
		КлючевыеРеквизитыКонтрагента = КлючевыеРеквизитыКонтрагента(Контрагент);
		Приглашение.ПолучательИНН = КлючевыеРеквизитыКонтрагента.ИНН;
		Приглашение.ПолучательКПП = КлючевыеРеквизитыКонтрагента.КПП;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция КлючевыеРеквизитыКонтрагента(Знач Контрагент)
	Возврат ИнтеграцияЭДО.КлючевыеРеквизитыКонтрагента(Контрагент);
КонецФункции

&НаКлиенте
Процедура ОповеститьОбИзмененииПриглашения()

	Если ЗначениеЗаполнено(КлючПриглашения) Тогда
		ПолныйКлючПриглашения = ПриглашенияЭДОКлиентСервер.НовыйКлючПриглашения();
		ПолныйКлючПриглашения.Ключ = КлючПриглашения;
		ПолныйКлючПриглашения.ИдентификаторОрганизации = ИдентификаторОрганизации;
		
		КлючЗаписи = ПриглашенияЭДОСлужебныйКлиент.КлючЗаписиПриглашения(ПолныйКлючПриглашения);
		ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(КлючЗаписи);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СтандартныйШаблонПриглашения()
	Возврат ПриглашенияЭДОСлужебный.СтандартныйШаблонПриглашения();	
КонецФункции

#КонецОбласти

#Область УправлениеЭлементамиФормы

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьИВидимостьКоманднойПанелиОсновнойСтраницы(Форма)
	
	ПолучательКорректен = Ложь;
	ЭлементыФормы = Форма.Элементы;
	
	ИдентификаторПолучателяКорректный = РаботаСАбонентамиЭДОКлиентСервер.ИдентификаторАбонентаКорректный(
		Форма.ИдентификаторКонтрагента);
		
	СвязьОператоровДоступна = СвязьОператоровДоступна(Форма.ИдентификаторОрганизации, Форма.ИдентификаторКонтрагента);
	ЭлементыФормы.ГруппаНетСвязиОператоров.Видимость = ИдентификаторПолучателяКорректный 
		И Не СвязьОператоровДоступна;
		
	ПолучательКорректен = ИдентификаторПолучателяКорректный
		И ЗначениеЗаполнено(Форма.ИдентификаторОрганизации)
		И СвязьОператоровДоступна;
	
	ДоступнаОтправка = ПолучательКорректен И Не Форма.ВыводитьОжиданиеДанныхПолучателяИзСервиса;
	Если Форма.РежимНастройки Тогда
		ЭлементыФормы.КнопкаПрименить.Видимость = Истина;
		ЭлементыФормы.КнопкаПрименить.Доступность = ДоступнаОтправка;
		
		ЭлементыФормы.КнопкаОтправить.Видимость = Ложь;
	Иначе
		ЭлементыФормы.КнопкаОтправить.Видимость = Истина;
		ЭлементыФормы.КнопкаОтправить.Доступность = ДоступнаОтправка;
		
		ЭлементыФормы.КнопкаПрименить.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()

	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОсновная;
	Элементы.ГруппаПредупреждения.Видимость = Ложь;
	Элементы.ГруппаНетСвязиОператоров.Видимость = Ложь;
	Если Не ИнтеграцияЭДО.ИспользуетсяНесколькоОрганизаций() И ЗначениеЗаполнено(Организация) Тогда
		Элементы.ГруппаОтправительОрганизация.Видимость = Ложь;
		Если ИдентификаторыОрганизации.Количество() = 1 И ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
			Элементы.ГруппаОтправитель.Видимость = Ложь;
		КонецЕсли;
	Иначе
		Элементы.ГруппаОтправитель.Видимость = Истина;
		Элементы.ГруппаОтправительОрганизация.Видимость = Истина;
	КонецЕсли;
	
	Элементы.СтраницаПолучательВводИдентификатора.Видимость = Ложь;
	Элементы.СтраницаПолучательВыборИдентификатора.Видимость = Ложь;
	Элементы.СтраницаПолучательОжидание.Видимость = Ложь;
	
	Элементы.ГруппаПолучательКонтрагент.ТолькоПросмотр = Ложь;
	Элементы.ГруппаОтправитель.ТолькоПросмотр = Ложь;
		
	Если ВыводитьОжиданиеДанныхПолучателяИзСервиса Тогда
		Элементы.СтраницыВариантыВводаИдентификатораПолучателя.ТекущаяСтраница = Элементы.СтраницаПолучательОжидание;
		Элементы.СтраницаПолучательОжидание.Видимость = Истина;
		
		Элементы.ГруппаПолучательКонтрагент.ТолькоПросмотр = Истина;
		Элементы.ГруппаОтправитель.ТолькоПросмотр = Истина;
		
		Если ЗначениеЗаполнено(УчетныеЗаписиКонтрагента) Тогда
			Элементы.ГруппаОжиданиеПолучатель.Высота = 14;
		Иначе
			Элементы.ГруппаОжиданиеПолучатель.Высота = 0;
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(УчетныеЗаписиКонтрагента) Тогда 
		Элементы.СтраницыВариантыВводаИдентификатораПолучателя.ТекущаяСтраница = Элементы.СтраницаПолучательВыборИдентификатора;
		Элементы.СтраницыПолучательСпособВыбора.ТекущаяСтраница = Элементы.СтраницаПолучательВыборОператора;
		Элементы.СтраницаПолучательВыборИдентификатора.Видимость = Истина;
	Иначе
		Элементы.СтраницыВариантыВводаИдентификатораПолучателя.ТекущаяСтраница = Элементы.СтраницаПолучательВводИдентификатора;
		Элементы.СтраницаПолучательВводИдентификатора.Видимость = Истина;
	КонецЕсли;
	
	УстановитьДоступностьИВидимостьКоманднойПанелиОсновнойСтраницы(ЭтотОбъект);
	
	Элементы.КнопкаНазад.Видимость = Ложь;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьСтраницуОшибки(Форма, ОписаниеОшибки = Неопределено)
	
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОшибка;
	
	Элементы.КнопкаОтправить.Видимость = Ложь;
	Элементы.КнопкаПрименить.Видимость = Ложь;
	
	Если ОписаниеОшибки = Неопределено Тогда
		Элементы.КнопкаНазад.Видимость = Истина;
	Иначе
		Элементы.КнопкаНазад.Видимость = Ложь;
		Элементы.НадписьОшибка.Заголовок = ОписаниеОшибки;
		Элементы.КнопкаОтмена.Видимость = Ложь;
		Элементы.КнопкаЗакрыть.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСтраницуВыполнено()

	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаВыполнено;
	
	Элементы.КнопкаНазад.Видимость = Ложь;
	Элементы.КнопкаОтмена.Видимость = Ложь;
	Элементы.КнопкаЗакрыть.Видимость = Истина;
	Элементы.КнопкаОтправить.Видимость = Ложь;
	Элементы.КнопкаПрименить.Видимость = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура НастроитьСтраницуОжидания()
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОжидание;

	Элементы.КнопкаНазад.Видимость = Ложь;
	Элементы.КнопкаЗакрыть.Видимость = Ложь;
	Элементы.КнопкаОтправить.Видимость = Ложь;
	Элементы.КнопкаПрименить.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьГруппуПредупреждения()
	
	Элементы.ГруппаПредупреждения.Видимость = Ложь;
	
	Если КонтекстДиагностики = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстДиагностики.ЗаголовокОперации = НСтр("ru = 'При получении данных участников ЭДО'");
	ПараметрыОбработкиОшибок = ОбработкаНеисправностейБЭДКлиент.НовыеПараметрыОбработкиОшибок();
	ПараметрыОбработкиОшибок.КонтекстныйРежимОбработки = Истина;
	ПараметрыОбработкиОшибок.ГруппаПредупреждения = Элементы.ГруппаПредупреждения;
	ПараметрыОбработкиОшибок.НадписьПредупреждение = Элементы.ДекорацияНадписьПредупреждение;
	ПараметрыОбработкиОшибок.ТекстПредупреждения = ДиагностикаЭДОКлиент.ТекстПредупреждения(КонтекстДиагностики);
	ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(КонтекстДиагностики, ПараметрыОбработкиОшибок);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура ПриЗакрытииОтмене()
	
	Если ДлительнаяОперация <> Неопределено Тогда
		ИнтеграцияБСПБЭДВызовСервера.ОтменитьВыполнениеЗадания(ДлительнаяОперация.ИдентификаторЗадания);
	КонецЕсли;
	
	Результат = Неопределено;
	Если ПриглашениеОтправлено Тогда
		Результат = РезультатОтправкиПриглашения();
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СвязьОператоровДоступна(ИдентификаторОрганизации, ИдентификаторКонтрагента)
	
	Если Не ЗначениеЗаполнено(ИдентификаторКонтрагента) 
		ИЛИ Не ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
			
		Возврат Ложь;
		
	КонецЕсли;
	
	ОператорОрганизации = Лев(ИдентификаторОрганизации, 3);
	ОператорКонтрагента = Лев(ИдентификаторКонтрагента, 3);
	Если ОператорОрганизации = ОператорКонтрагента Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СовместимостьОператоровЭДО.Оператор1 КАК ОператорОрганизации,
		|	СовместимостьОператоровЭДО.Оператор2 КАК ОператорКонтрагента,
		|	СовместимостьОператоровЭДО.СвязьДоступна КАК СвязьДоступна
		|ИЗ
		|	РегистрСведений.СовместимостьОператоровЭДО КАК СовместимостьОператоровЭДО
		|ГДЕ
		|	СовместимостьОператоровЭДО.Оператор1 = &ОператорОрганизации
		|	И СовместимостьОператоровЭДО.Оператор2 = &ОператорКонтрагента
		|	И СовместимостьОператоровЭДО.СвязьДоступна = Истина
		|	И Не СовместимостьОператоровЭДО.ПотребностьСоглашения = &ОбязательноСоглашение";
	
	Запрос.УстановитьПараметр("ОбязательноСоглашение", Перечисления.ВариантыПотребностиСоглашенияНаРоуминг.Обязательно);
	Запрос.УстановитьПараметр("ОператорОрганизации", ОператорОрганизации);
	Запрос.УстановитьПараметр("ОператорКонтрагента",  ОператорКонтрагента);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
КонецФункции

&НаСервере
Процедура ЗаполнитьРеквизитыИзПараметров()
	Параметры.Свойство("Организация", Организация);
	Параметры.Свойство("Контрагент", Контрагент);
	Параметры.Свойство("РежимНастройки", РежимНастройки);
	
	КлючЗаписиПриглашения = Неопределено;
	Параметры.Свойство("Ключ", КлючЗаписиПриглашения);
	Если Не КлючЗаписиПриглашения = Неопределено Тогда
		КлючПриглашения = КлючЗаписиПриглашения.Ключ;
	Иначе
		Параметры.Свойство("КлючПриглашения", КлючПриглашения);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСоответствиеОператоровЭДО()
	
	ТаблицаОператоровЭДОПолная = РегистрыСведений.ОператорыЭДО.ТаблицаОператоровЭДО();
	Для Каждого СтрокаОператора Из ТаблицаОператоровЭДОПолная Цикл
		НоваяСтрокаОператора = ТаблицаОператоровЭДО.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаОператора, СтрокаОператора);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
