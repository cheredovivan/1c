#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Устанавливает блокировку в регистре по файлу.
//
// Параметры:
//  Файл - СправочникСсылка.ВерсииФайлов
Процедура ЗаблокироватьФайл(Файл) Экспорт
	
	ЗаблокироватьДанныеДляРедактирования(
		СоздатьКлючЗаписи(Новый Структура("Файл", Файл)));
	
КонецПроцедуры

// Снимает блокировку в регистре по файлу.
//
// Параметры:
//  Файл - ОпределяемыйТип.ПрисоединенныйФайл
Процедура РазблокироватьФайл(Файл) Экспорт
	
	РазблокироватьДанныеДляРедактирования(
		СоздатьКлючЗаписи(Новый Структура("Файл", Файл)));
	
КонецПроцедуры

// Добавляет запись в регистр
// 
// Параметры:
//ВерсияФайлаСсылка - СправочникСсылка.ВерсииФайлов
//ЗаписыватьХеш - Булево
Процедура ДобавитьФайл(ВерсияФайлаСсылка, ЗаписыватьХеш = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Файл.Установить(ВерсияФайлаСсылка);
	
	Запись = НаборЗаписей.Добавить();
	Запись.Файл = ВерсияФайлаСсылка;
	Запись.ДатаПередачиНаРаспознавание = ТекущаяУниверсальнаяДата();
	Запись.Ответственный = Пользователи.ТекущийПользователь();
	
	Если ЗаписыватьХеш = Истина Тогда
		ДанныеФайла = РаботаСФайлами.ДвоичныеДанныеФайла(ВерсияФайлаСсылка);
		ХешФайла = ОбщегоНазначения.КонтрольнаяСуммаСтрокой(ДанныеФайла, ХешФункция.SHA256);
		Запись.ХешФайла = ХешФайла;
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Добавляет запись в регистр
// 
// Параметры:
//ВерсияФайлаСсылка - СправочникСсылка.ВерсииФайлов
Процедура СброситьОшибку(ВерсияФайлаСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ФайлыДокументовНаРаспознавании");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Файл", ВерсияФайлаСсылка);
		Блокировка.Заблокировать();	
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Файл.Установить(ВерсияФайлаСсылка);
		НаборЗаписей.Прочитать();
		НаборЗаписей[0].КоличествоПопыток = 0;
		НаборЗаписей[0].ОписаниеОшибки = "";
		НаборЗаписей[0].ИдентификаторЗадания = "";
		НаборЗаписей[0].ИдентификаторФайла = "";
		НаборЗаписей[0].ДатаПередачиНаРаспознавание = ТекущаяДатаСеанса();
		НаборЗаписей[0].ДатаПоследнейПроверкиРезультата = Дата(1,1,1);
		НаборЗаписей[0].Ответственный = Пользователи.ТекущийПользователь();
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
	
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры
	
// Удаляет запись из регистра
// 
// Параметры:
//  ВерсияФайлаСсылка - СправочникСсылка.ВерсииФайлов
Процедура УдалитьФайл(ВерсияФайлаСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Файл.Установить(ВерсияФайлаСсылка);
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Удаляет запись из регистра
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
Процедура УдалитьЗаписьПоДокументу(ДокументСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ФайлСсылка = ПолучитьФайлПоДокументу(ДокументСсылка);
	Если ФайлСсылка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Файл.Установить(ФайлСсылка);
	НаборЗаписей.Записать();
	
КонецПроцедуры
	

// Изменить состояние.
// 
// Параметры:
//  ВерсияФайлаСсылка - СправочникСсылка.ВерсииФайлов
//  ИдентификаторФайла - Неопределено - Идентификатор файла
//  ИдентификаторЗадания - Неопределено - Идентификатор задания
//  ОписаниеОшибки - Строка - Описание ошибки
Процедура ИзменитьСостояние(ВерсияФайлаСсылка, ИдентификаторФайла = Неопределено, 
		ИдентификаторЗадания = Неопределено, ОписаниеОшибки = "") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ФайлыДокументовНаРаспознавании");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Файл", ВерсияФайлаСсылка);
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Файл.Установить(ВерсияФайлаСсылка);
		НаборЗаписей.Прочитать();
		
		Если ИдентификаторФайла <> Неопределено Тогда
			НаборЗаписей[0].ИдентификаторФайла = ИдентификаторФайла;
		КонецЕсли;
		
		Если ИдентификаторЗадания <> Неопределено Тогда
			НаборЗаписей[0].ИдентификаторЗадания = ИдентификаторЗадания;
		КонецЕсли;
		
		Если ОписаниеОшибки<>"" Тогда
			НаборЗаписей[0].КоличествоПопыток = НаборЗаписей[0].КоличествоПопыток+1;
			НаборЗаписей[0].ОписаниеОшибки = ОписаниеОшибки;
		КонецЕсли;
		
		НаборЗаписей[0].ДатаПоследнейПроверкиРезультата = ТекущаяДатаСеанса();
		
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Количество документов на распознавании.
// 
// Возвращаемое значение:
//  Число - Количество документов на распознавании
Функция КоличествоДокументовНаРаспознавании() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(*) КАК Количество
		|ИЗ
		|	РегистрСведений.ФайлыДокументовНаРаспознавании КАК ФайлыДокументовНаРаспознавании";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Количество;
	
КонецФункции

// Фиксирует ошибку распознавания файла, увеличивая количество попыток.
//
// Параметры:
//  Файл - ОпределяемыйТип.ПрисоединенныйФайл
//  ОписаниеОшибки - Строка
Процедура ЗафиксироватьОшибкуПриРаспознавании(Файл, ОписаниеОшибки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ФайлыДокументовНаРаспознавании");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Файл", Файл);
		Блокировка.Заблокировать();
	
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Файл.Установить(Файл);
		НаборЗаписей.Прочитать();
		
		НаборЗаписей[0].КоличествоПопыток = НаборЗаписей[0].КоличествоПопыток + 1;
		НаборЗаписей[0].ОписаниеОшибки = ОписаниеОшибки;
		
		НаборЗаписей[0].ДатаПоследнейПроверкиРезультата = ТекущаяДатаСеанса();
		
		НаборЗаписей.Записать();
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает максимально допустимое количество неудачных попыток обработки файла
// на распознавании.
//
// Возвращаемое значение:
//  Число
Функция МаксимальноеКоличествоНеудачныхПопытокОбработки() Экспорт
	
	Возврат 3;
	
КонецФункции

// Получить файлы по документу.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение: 
//  СправочникСсылка.ВерсииФайлов
Функция ПолучитьФайлПоДокументу(ДокументСсылка)
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ФайлыДокументовНаРаспознавании.Файл КАК Файл
		|ИЗ
		|	РегистрСведений.ФайлыДокументовНаРаспознавании КАК ФайлыДокументовНаРаспознавании
		|ГДЕ
		|	ФайлыДокументовНаРаспознавании.Файл.Владелец.ВладелецФайла = &ВладелецФайла");
		
	Запрос.УстановитьПараметр("ВладелецФайла", ДокументСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Файл;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#КонецЕсли