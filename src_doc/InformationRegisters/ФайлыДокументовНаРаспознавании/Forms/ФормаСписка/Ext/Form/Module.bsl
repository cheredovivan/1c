#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПовторитьРаспознавание(Команда)
	
	ВыделенныеФайлы = ВыделенныеФайлыВСписке();
	Если ВыделенныеФайлы.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Для повторения следует выделить файлы в списке.'"));
		Возврат;
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Выполняется возобновление распознавания файлов в сервисе.'"));
	
	ВозобновитьРаспознаваниеФайловВСервисе(ВыделенныеФайлы);
	
	Состояние(НСтр("ru = 'Возобновление распознавания файлов в сервисе завершено.'"));
	
	Элементы.Список.Обновить();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ВыделенныеФайлыВСписке()
	
	ВыделенныеФайлы = Новый Массив;
	
	Для Каждого СтрокаСписка Из Элементы.Список.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.Список.ДанныеСтроки(СтрокаСписка);
		ВыделенныеФайлы.Добавить(ДанныеСтроки.Файл);
	КонецЦикла;
	
	Возврат ВыделенныеФайлы;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ВозобновитьРаспознаваниеФайловВСервисе(Знач Файлы)
	
	Для Каждого Файл Из Файлы Цикл
		РаспознаваниеДокументов.ВозобновитьРаспознаваниеФайлаДокумента(Файл);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
