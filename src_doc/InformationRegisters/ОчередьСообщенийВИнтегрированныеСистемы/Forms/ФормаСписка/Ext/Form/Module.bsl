#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПредельноеКоличество = ОбработкаЗапросовXDTOОбмен.ПредельноеКоличествоПопытокОтправкиСообщенияОбмена();
	
	Элементы.ИнформацияОписание.Заголовок = СтрШаблон(
		НСтр("ru = 'Максимальное количество попыток отправки сообщения равно %1. При достижении этого количества система не будет предпринимать новых попыток отправки. В такой ситуации нужно устранить ошибку отправки, затем либо удалить сообщение из очереди, либо сбросить счетчик попыток отправки.'"),
		ПредельноеКоличество);
	
	ЭлементОформления = Список.УсловноеОформление.Элементы.Добавить();
	
	ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИнтегрированнаяСистема.Имя);
	ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(Элементы.Идентификатор.Имя);
	ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДатаСоздания.Имя);
	ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(Элементы.КоличествоПопытокОтправки.Имя);
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КоличествоПопытокОтправки");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ЭлементОтбора.ПравоеЗначение = ПредельноеКоличество;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Красный);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоИдентификаторуПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ПоИдентификатору) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
			"Идентификатор",
			Неопределено,
			ВидСравненияКомпоновкиДанных.Равно,,
			Ложь);
		Возврат;
	КонецЕсли;
	
	Попытка
		Идентификатор = Новый УникальныйИдентификатор(ПоИдентификатору);
	Исключение
		ПоказатьПредупреждение(, НСтр("ru = 'Идентификатор неверен'"));
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
			"Идентификатор",
			Неопределено,
			ВидСравненияКомпоновкиДанных.Равно,,
			Ложь);
		Возврат;
	КонецПопытки;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"Идентификатор",
		Идентификатор,
		ВидСравненияКомпоновкиДанных.Равно,,
		Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура СброситьСчетчикПопытокОтправки(Знач Команда)
	
	КоличествоСообщений = Элементы.Список.ВыделенныеСтроки.Количество();
	
	Если КоличествоСообщений = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбрано ни одного сообщения.'"));
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = СтрШаблон(
		НСтр("ru = 'Сбросить счетчик попыток отправки для %1?'"),
		ОбщегоНазначенияДокументооборотВызовСервера.СклоненияСтрокиПоЧислу(
			НСтр("ru = 'сообщение'"),
			КоличествоСообщений,
			Неопределено,
			"ЧС=Количественное",
			"ПД=Родительный",
			Неопределено));
	Ответ = Ждать ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНетАсинх(ТекстВопроса);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		СброситьСчетчикПопытокОтправкиНаСервере(Элементы.Список.ВыделенныеСтроки);
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СброситьСчетчикПопытокОтправкиНаСервере(Знач СписокВыбранныхСообщений)
	
	Для Каждого Сообщение Из СписокВыбранныхСообщений Цикл
		Запись = РегистрыСведений.ОчередьСообщенийВИнтегрированныеСистемы.СоздатьМенеджерЗаписи();
		Запись.ИнтегрированнаяСистема = Сообщение.ИнтегрированнаяСистема;
		Запись.МоментВремени = Сообщение.МоментВремени;
		Запись.Идентификатор = Сообщение.Идентификатор;
		Запись.Прочитать();
		Если Запись.Выбран() Тогда
			Запись.КоличествоПопытокОтправки = 0;
			Запись.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти