
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет версии файлов документа в регистр.
// Фиксирует все версии файлов, на момент установки результат для визы.
//
// Параметры:
//  ВизаСогласования - СправочникСсылка.ВизыСогласования - ссылка на визу.
//  Документ - СправочникСсылка.ДокументыПредприятия - ссылка на документ.
//
Процедура ДобавитьВерсииФайловПоВизе(ВизаСогласования, Документ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Файлы.Ссылка КАК Файл,
		|	Файлы.ТекущаяВерсия КАК ВерсияФайла
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СлужебныеФайлыДокументов КАК СлужебныеФайлыДокументов
		|		ПО Файлы.Ссылка = СлужебныеФайлыДокументов.Файл
		|ГДЕ
		|	Файлы.ВладелецФайла = &Документ
		|	И СлужебныеФайлыДокументов.Файл ЕСТЬ NULL
		|	И Не Файлы.ПометкаУдаления";
		
	Запрос.Параметры.Вставить("Документ", Документ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Запись = СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		Запись.ВизаСогласования = ВизаСогласования;
		Запись.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Удаляет версии согласованных файлов из регистра.
// Удаляет сохраненные версии файлов, на момент снятия результата согласования визы.
//
// Параметры:
//  ВизаСогласования - СправочникСсылка.ВизыСогласования - ссылка на визу.
//
Процедура УдалитьВерсииФайловПоВизе(ВизаСогласования) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ВизаСогласования.Установить(ВизаСогласования);
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Возвращает все версии согласованных файлов по списку виз согласования.
// 
// Параметры:
//  Документ - СправочникССылка.ДокументыПредприятия
//  ВизыСогласования - Массив - Визы согласования в этом документе
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Все версии по списку виз
//
Функция ВсеВерсииПоСпискуВиз(Документ, ВизыСогласования) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВизыСогласования.Документ КАК ДокументПредприятия,
		|	ВизыСогласования.Ссылка КАК Виза,
		|	ВизыСогласования.ДатаИсполнения КАК ДатаИсполнения
		|ПОМЕСТИТЬ ДокументыПоВизе
		|ИЗ
		|	Справочник.ВизыСогласования КАК ВизыСогласования
		|ГДЕ
		|	ВизыСогласования.Ссылка В(&ВизыСогласования)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументПредприятия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Файлы.Ссылка КАК Ссылка,
		|	Файлы.ТекущаяВерсияРасширение КАК Расширение,
		|	Файлы.ВладелецФайла КАК ВладелецФайла,
		|	Файлы.ТекущаяВерсия КАК ТекущаяВерсия,
		|	Файлы.ДатаСоздания КАК ДатаСоздания
		|ПОМЕСТИТЬ ФайлыДокумента
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СлужебныеФайлыДокументов КАК СлужебныеФайлыДокументов
		|		ПО (СлужебныеФайлыДокументов.Файл = Файлы.Ссылка)
		|ГДЕ
		|	Файлы.ВладелецФайла = &ВладелецФайла
		|	И Файлы.ПометкаУдаления = ЛОЖЬ
		|	И СлужебныеФайлыДокументов.Файл ЕСТЬ NULL
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка,
		|	ВладелецФайла
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВерсииСогласованныхФайлов.ВерсияФайла КАК ВерсияФайла,
		|	ВерсииСогласованныхФайлов.Файл КАК Файл,
		|	ВерсииСогласованныхФайлов.ВизаСогласования КАК ВизаСогласования
		|ПОМЕСТИТЬ ВерсииСогласованныхФайловВизы
		|ИЗ
		|	РегистрСведений.ВерсииСогласованныхФайлов КАК ВерсииСогласованныхФайлов
		|ГДЕ
		|	ВерсииСогласованныхФайлов.ВизаСогласования В(&ВизыСогласования)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Файл
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВерсииСогласованныхФайлов.ВерсияФайла КАК ВерсияФайла,
		|	ВерсииСогласованныхФайлов.Файл КАК Файл,
		|	ВерсииСогласованныхФайлов.ВизаСогласования КАК ВизаСогласования,
		|	ВЫБОР
		|		КОГДА Файлы.ТекущаяВерсия <> ВерсииСогласованныхФайлов.ВерсияФайла
		|				ИЛИ Файлы.ПометкаУдаления
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ВерсияФайлаОтличается,
		|	Файлы.ТекущаяВерсия КАК ТекущаяВерсияФайла,
		|	Файлы.ТекущаяВерсия.НеМеняетСути КАК ВерсияНеМеняетСути,
		|	ВерсииСогласованныхФайлов.ВерсияФайла.Расширение КАК Расширение,
		|	ЛОЖЬ КАК ЭтоСканКопия,
		|	ИСТИНА КАК ОтслеживаниеВерсийПроизводилось
		|ИЗ
		|	ВерсииСогласованныхФайловВизы КАК ВерсииСогласованныхФайлов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Файлы КАК Файлы
		|		ПО (Файлы.Ссылка = ВерсииСогласованныхФайлов.Файл)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Файлы.ТекущаяВерсия,
		|	Файлы.Ссылка,
		|	ДокументыПоВизе.Виза,
		|	ИСТИНА,
		|	Файлы.ТекущаяВерсия,
		|	ЛОЖЬ,
		|	Файлы.ТекущаяВерсия.Расширение,
		|	ЛОЖЬ,
		|	ДокументыПоВизе.ДатаИсполнения < Файлы.ДатаСоздания
		|ИЗ
		|	ФайлыДокумента КАК Файлы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПоВизе КАК ДокументыПоВизе
		|		ПО (ДокументыПоВизе.ДокументПредприятия = Файлы.ВладелецФайла)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВерсииСогласованныхФайловВизы КАК ВерсииСогласованныхФайловВизы
		|		ПО Файлы.Ссылка = ВерсииСогласованныхФайловВизы.Файл
		|			И (ВерсииСогласованныхФайловВизы.ВизаСогласования = ДокументыПоВизе.Виза)
		|ГДЕ
		|	ВерсииСогласованныхФайловВизы.Файл ЕСТЬ NULL";
		
	Запрос.Параметры.Вставить("ВизыСогласования", ВизыСогласования); 
	Запрос.Параметры.Вставить("ВладелецФайла", Документ); 
	
	ВсеВерсии = Запрос.Выполнить().Выгрузить();
	УчетСканКопийОригиналов = ПолучитьФункциональнуюОпцию("ВестиУчетСканКопийОригиналовДокументов"); 
	Для Каждого СтрВерсия Из ВсеВерсии Цикл
		
		Если УчетСканКопийОригиналов 
			И ДелопроизводствоПовтИсп.ФайлЯвляетсяОригиналом(СтрВерсия.Файл) Тогда
			СтрВерсия.ЭтоСканКопия = Истина;
		КонецЕсли;
		
		Если Не СтрВерсия.ВерсияФайлаОтличается Или Не СтрВерсия.ВерсияНеМеняетСути Тогда
			Продолжить;
		КонецЕсли;
		
		// Требуется проверить, не являются ли данные версии Служебными.
		// Если версия НеМеняетСути, то проверяем ее Родителя. 
		// Если Родитель равен согласованной версии, значит отличий нет.
		ТекущаяВерсияФайла = СтрВерсия.ТекущаяВерсияФайла;
		Пока ЗначениеЗаполнено(ТекущаяВерсияФайла) Цикл
			РеквизитыВерсии = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущаяВерсияФайла, 
				"РодительскаяВерсия, РодительскаяВерсия.НеМеняетСути");
			
			Если ЗначениеЗаполнено(РеквизитыВерсии.РодительскаяВерсия) 
				И РеквизитыВерсии.РодительскаяВерсия = СтрВерсия.ВерсияФайла Тогда
				СтрВерсия.ВерсияФайлаОтличается = Ложь;
				Прервать;
			КонецЕсли;
			
			Если РеквизитыВерсии.РодительскаяВерсияНеМеняетСути = Ложь Тогда
				Прервать;
			КонецЕсли;
			
			ТекущаяВерсияФайла = РеквизитыВерсии.РодительскаяВерсия;	
		КонецЦикла;
	КонецЦикла;
	
	Возврат ВсеВерсии;
	
КонецФункции

#КонецОбласти

#КонецЕсли