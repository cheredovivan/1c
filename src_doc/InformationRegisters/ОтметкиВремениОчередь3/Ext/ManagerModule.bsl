#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Удаляет порцию устаревших данных.
//
// Возвращаемое значение:
//	Булево - Истина, если были найдены устаревшие данные, в противном случае Ложь.
//
Функция УдалитьПорциюУстаревшихДанных() Экспорт
	
	//если отметки времени еще используем - не чистим.  
	ИспользоватьОтметкиВремени = Константы.ИспользоватьОтметкиВремени.Получить();
	Если ИспользоватьОтметкиВремени Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 10000
		|	ОтметкиВремениОчередь.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ОтметкиВремениОчередь.ТипКлюча КАК ТипКлюча,
		|	ОтметкиВремениОчередь.Объект КАК Объект,
		|	ОтметкиВремениОчередь.Отметка КАК Отметка
		|ИЗ
		|	РегистрСведений.ОтметкиВремениОчередь3 КАК ОтметкиВремениОчередь");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл 
		
		НаборЗаписей = СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.ИдентификаторКлюча.Установить(Выборка.ИдентификаторКлюча);
		НаборЗаписей.Отбор.ТипКлюча.Установить(Выборка.ТипКлюча);
		НаборЗаписей.Отбор.Объект.Установить(Выборка.Объект);
		НаборЗаписей.Отбор.Отметка.Установить(Выборка.Отметка);
		
		НаборЗаписей.Записать(); // стираем
		
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru='Удаление устаревших данных'"), 
		УровеньЖурналаРегистрации.Информация,
		Метаданные.РегистрыСведений.ОтметкиВремениОчередь3,, 
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедура завершена успешно, обработано %1 записей'"), Выборка.Количество()));
	
	Возврат Выборка.Количество() > 0;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)

	Если ВидФормы = "ФормаСписка" Тогда
		
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = Метаданные.РегистрыСведений.ОтметкиВремениОчередь1.Формы.ФормаСписка;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
