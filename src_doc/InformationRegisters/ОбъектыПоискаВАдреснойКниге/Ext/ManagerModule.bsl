
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ОбновлениеИнформационнойБазы

// Заполнение реквизита "ПредставлениеОбъекта"
// 
// Параметры:
//  Параметры - Структура - Параметры обработчика:
//   * ПрогрессВыполнения - Структура:
//      ** ОбработаноОбъектов - Число.
//      ** ВсегоОбъектов - Число.
//
Процедура ЗаполнитьПредставлениеОбъекта(Параметры) Экспорт
	
	ВозможныеПустыеЗначенияОбъектаПоиска = Новый Массив;
	ВозможныеПустыеЗначенияОбъектаПоиска.Добавить(Неопределено);
	ВозможныеПустыеЗначенияОбъектаПоиска.Добавить(Справочники.Сотрудники.ПустаяСсылка());
	ВозможныеПустыеЗначенияОбъектаПоиска.Добавить(Справочники.Контрагенты.ПустаяСсылка());
	ВозможныеПустыеЗначенияОбъектаПоиска.Добавить(Справочники.Проекты.ПустаяСсылка());
	ВозможныеПустыеЗначенияОбъектаПоиска.Добавить(Справочники.ЛичныеАдресаты.ПустаяСсылка());
	ВозможныеПустыеЗначенияОбъектаПоиска.Добавить(Справочники.СтруктураПредприятия.ПустаяСсылка());
	ВозможныеПустыеЗначенияОбъектаПоиска.Добавить(Справочники.РолиИсполнителей.ПустаяСсылка());
	ВозможныеПустыеЗначенияОбъектаПоиска.Добавить(Справочники.АвтоподстановкиДляОбъектов.ПустаяСсылка());
	ВозможныеПустыеЗначенияОбъектаПоиска.Добавить(Справочники.РабочиеГруппы.ПустаяСсылка());
	ВозможныеПустыеЗначенияОбъектаПоиска.Добавить(Справочники.Организации.ПустаяСсылка());
	ВозможныеПустыеЗначенияОбъектаПоиска.Добавить(Справочники.АвтоподстановкиДляПроцессов.ПустаяСсылка());
	ВозможныеПустыеЗначенияОбъектаПоиска.Добавить(Справочники.Мероприятия.ПустаяСсылка());
	ВозможныеПустыеЗначенияОбъектаПоиска.Добавить(Справочники.КонтактныеЛица.ПустаяСсылка());
	
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(ОбъектыПоискаВАдреснойКниге.ОбъектПоиска) КАК ВсегоОбъектов
			|ИЗ
			|	РегистрСведений.ОбъектыПоискаВАдреснойКниге КАК ОбъектыПоискаВАдреснойКниге
			|ГДЕ
			|	ОбъектыПоискаВАдреснойКниге.ПредставлениеОбъекта = """"
			|	И НЕ ОбъектыПоискаВАдреснойКниге.ОбъектПоиска В (&ПустыеЗначения)");
		
		Запрос.УстановитьПараметр("ПустыеЗначения", ВозможныеПустыеЗначенияОбъектаПоиска);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Параметры.ПрогрессВыполнения.ВсегоОбъектов = Выборка.ВсегоОбъектов;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбработаноОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ОбъектыПоискаВАдреснойКниге.ОбъектПоиска КАК ОбъектПоиска,
		|	ОбъектыПоискаВАдреснойКниге.КритерийПоиска КАК КритерийПоиска,
		|	ПРЕДСТАВЛЕНИЕ(ОбъектыПоискаВАдреснойКниге.ОбъектПоиска) КАК ПредставлениеОбъектаПоиска
		|ИЗ
		|	РегистрСведений.ОбъектыПоискаВАдреснойКниге КАК ОбъектыПоискаВАдреснойКниге
		|ГДЕ
		|	ОбъектыПоискаВАдреснойКниге.ПредставлениеОбъекта = """"
		|	И НЕ ОбъектыПоискаВАдреснойКниге.ОбъектПоиска В (&ПустыеЗначения)"); 
	
	Запрос.УстановитьПараметр("ПустыеЗначения", ВозможныеПустыеЗначенияОбъектаПоиска);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ПредставлениеОбъектаПоиска = Выборка.ПредставлениеОбъектаПоиска;
		Если ПредставлениеОбъектаПоиска = "" Тогда
			
			ПредставлениеОбъектаПоиска = НСтр("ru='<Нет представления>'");
			
		КонецЕсли;
		
		НачатьТранзакцию();
		Попытка
			
			Набор = СоздатьНаборЗаписей();
			Набор.Отбор.ОбъектПоиска.Установить(Выборка.ОбъектПоиска);
			Набор.Отбор.КритерийПоиска.Установить(Выборка.КритерийПоиска);
			Набор.Прочитать();
			
			Для Каждого Запись Из Набор Цикл
				
				Запись.ПредставлениеОбъекта = ПредставлениеОбъектаПоиска;
				
			КонецЦикла;
			
			ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные(Набор);
			
			ОбработаноОбъектов = ОбработаноОбъектов + 1;
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				Выборка.ОбъектПоиска.Метаданные(),
				Выборка.ОбъектПоиска,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбработаноОбъектов = 0 И ПроблемныхОбъектов > 0 Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Процедуре ПерейтиНаВерсию_3_0_16_13 не удалось добавить представление %1 объектам
			|в ОбъектыПоискаВАдреснойКниге'"),
			ПроблемныхОбъектов);
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = 
		Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ОбработаноОбъектов;
	Параметры.ОбработкаЗавершена = ОбработаноОбъектов = 0;
	
КонецПроцедуры

#КонецОбласти

// Выполняет обновление слов поиска в адресной книге.
//
// Параметры:
//	СсылкаНаОбъект - ОпределяемыйТип.ИсточникДанныхАдреснойКниги - Объект, по которому нужно обновить слова поиска.
//	ПараметрыОбновления - Структура Из КлючИЗначение - Данные, для обновления слов поиска.
//
Процедура ОбновитьСловаПоиска(СсылкаНаОбъект, ПараметрыОбновления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
		ОбновитьСловаПоискаПоПодразделению(ПараметрыОбновления);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.РабочиеГруппы") Тогда
		ОбновитьСловаПоискаПоРабочейГруппе(ПараметрыОбновления);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Проекты") Тогда
		ОбновитьСловаПоискаПоПроекту(ПараметрыОбновления);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Сотрудники") Тогда
		ОбновитьСловаПоискаПоСотруднику(ПараметрыОбновления);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Должности") Тогда
		ОбновитьСловаПоискаПоДолжности(ПараметрыОбновления);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.РолиИсполнителей") Тогда
		ОбновитьСловаПоискаПоРоли(ПараметрыОбновления);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Контрагенты") Тогда
		ОбновитьСловаПоискаПоКонтрагенту(ПараметрыОбновления);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
		ОбновитьСловаПоискаПоКонтактномуЛицу(ПараметрыОбновления);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.ЛичныеАдресаты") Тогда
		ОбновитьСловаПоискаПоЛичномуАдресату(ПараметрыОбновления);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Организации") Тогда
		ОбновитьСловаПоискаПоОрганизации(ПараметрыОбновления);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.АвтоподстановкиДляОбъектов") Тогда
		ОбновитьСловаПоискаПоАвтоподстановкеДляОбъектов(ПараметрыОбновления);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.АвтоподстановкиДляПроцессов") Тогда
		ОбновитьСловаПоискаПоАвтоподстановкеДляПроцессов(ПараметрыОбновления);
	Иначе
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Неизвестный тип объекта: %1'"),
			ТипЗнч(СсылкаНаОбъект));
	КонецЕсли;
	
КонецПроцедуры

// Возвращает данные, необходимые для обновления слов поиска.
//
// Параметры:
//	Объект - СправочникОбъект, СправочникВыборкаИмяСправочника - Объект, данные которого необходимо получить.
//	ОбновитьКритерийПоиска - Булево - Признак необходимости обновления критерия поиска.
//
// Возвращаемое значение:
//	Структура:
//		* Ссылка - СправочникСсылка - ,
//				 - Неопределено - .
//		* Наименование - Строка - ,
//					   - Неопределено - .
//		* ПометкаУдаления - Булево - ,
//						  - Неопределено - .
//		* ОбновитьКритерийПоиска - Булево -,
//								 - Неопределено -.
//		* Владелец - СправочникСсылка.ФизическиеЛица - ,
//				   - Неопределено - ,
//		* ПредставлениеВПереписке - Строка -,
//								  - Неопределено - .
//		* Подразделение - СправочникСсылка.СтруктураПредприятия - ,
//						- Неопределено - .
//		* Должность - СправочникСсылка.Должности - ,
//					- Неопределено - .
//		* Действует - Булево - ,
//					- Неопределено -.
//		* Сотрудник - СправочникСсылка.Сотрудники - ,
//					- Неопределено - .
//		* КонтактнаяИнформация - ТаблицаЗначений - ,
//							   - Неопределено - .
//
Функция ПараметрыОбновленияСловПоиска(Объект, ОбновитьКритерийПоиска = Ложь) Экспорт
	
	ПараметрыОбновленияСловПоиска = НовыеПараметрыОбновленияСловПоиска(Объект);
	
	ИсточникДанных = Объект;
	Если ОбщегоНазначения.ЗначениеСсылочногоТипа(Объект) Тогда
		ИсточникДанных = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект, ПараметрыОбновленияСловПоиска);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ПараметрыОбновленияСловПоиска, ИсточникДанных);
	
	КонтактнаяИнформация =
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОбновленияСловПоиска, "КонтактнаяИнформация");
	Если КонтактнаяИнформация <> Неопределено Тогда
		ПараметрыОбновленияСловПоиска.КонтактнаяИнформация =
			ПараметрыОбновленияСловПоиска.КонтактнаяИнформация.Выгрузить();
	КонецЕсли;
	
	ПараметрыОбновленияСловПоиска.Вставить("ОбновитьКритерийПоиска", ОбновитьКритерийПоиска);
	
	Возврат ПараметрыОбновленияСловПоиска;
	
КонецФункции

// Возвращает данные, необходимые для обновления слов поиска по подразделени. сотрудника.
//
// Параметры:
//	Сотрудник - СправочникОбъект.Сотрудники, СправочникВыборка.Сотрудники - Сотрудник.
//	ДанныеПодразделения - Структура:
//		* Ссылка - СправочникСсылка.СтруктураПредприятия.
//		* Родитель - СправочникСсылка.СтруктураПредприятия.
//		* ПометкаУдаления - Булево.
//		* Наименование - Строка.
//
// Возвращаемое значение:
//	Структура:
//		* Ссылка - СправочникСсылка.Сотрудники.
//		* ПометкаУдаления - Булево.
//		* Наименование - Строка.
//		* ПодразделениеСотрудника - СправочникСсылка.СтруктураПредприятия.
//		* Подразделение - Структура:
//			** Ссылка - СправочникСсылка.Сотрудники.
//			** Родитель - СправочникСсылка.Сотрудники.
//			** ПометкаУдаления - Булево.
//			** Наименование - Строка.
//
Функция ПараметрыОбновленияСловПоискаПоПодразделениюСотрудника(Сотрудник, ДанныеПодразделения) Экспорт
	
	СведенияОПодразделении = Новый Структура;
	
	СведенияОПодразделении.Вставить("Ссылка", ДанныеПодразделения.Ссылка);
	СведенияОПодразделении.Вставить("Родитель", ДанныеПодразделения.Родитель);
	СведенияОПодразделении.Вставить("ПометкаУдаления", ДанныеПодразделения.ПометкаУдаления);
	СведенияОПодразделении.Вставить("Наименование", ДанныеПодразделения.Наименование);
	
	СведенияДляОбновленияСловПоиска = Новый Структура;
	
	СведенияДляОбновленияСловПоиска.Вставить("Ссылка", Сотрудник.Ссылка);
	СведенияДляОбновленияСловПоиска.Вставить("ПометкаУдаления", Сотрудник.ПометкаУдаления);
	СведенияДляОбновленияСловПоиска.Вставить("Наименование", Сотрудник.Наименование);
	СведенияДляОбновленияСловПоиска.Вставить("ПодразделениеСотрудника", Сотрудник.Подразделение);
	СведенияДляОбновленияСловПоиска.Вставить("Подразделение", СведенияОПодразделении);
		
	Возврат СведенияДляОбновленияСловПоиска;
	
КонецФункции

// Возвращает данные, необходимые для обновления слов поиска по должности сотруднику.
//
// Параметры:
//	Сотрудник - СправочникОбъект.Сотрудники, СправочникВыборка.Сотрудники - Сотрудник.
//	ДанныеДолжности - Структура:
//		* Ссылка - СправочникСсылка.Должности.
//		* ПометкаУдаления - Булево.
//		* Наименование - Строка.
//
// Возвращаемое значение:
//	Структура:
//		* Ссылка - СправочникСсылка.Сотрудники.
//		* ПометкаУдаления - Булево.
//		* Наименование - Строка.
//		* ДолжностьСотрудника - СправочникСсылка.Должности.
//		* Должность - Структура:
//			** Ссылка - СправочникСсылка.Должности.
//			** ПометкаУдаления - Булево.
//			** Наименование - Строка.
//
Функция ПараметрыОбновленияСловПоискаПоДолжностиСотрудника(Сотрудник, ДанныеДолжности) Экспорт
	
	СведенияОДолжности = Новый Структура;
	
	СведенияОДолжности.Вставить("Ссылка", ДанныеДолжности.Ссылка);
	СведенияОДолжности.Вставить("ПометкаУдаления", ДанныеДолжности.ПометкаУдаления);
	СведенияОДолжности.Вставить("Наименование", ДанныеДолжности.Наименование);
	
	СведенияДляОбновленияСловПоиска = Новый Структура;
	
	СведенияДляОбновленияСловПоиска.Вставить("Ссылка", Сотрудник.Ссылка);
	СведенияДляОбновленияСловПоиска.Вставить("ПометкаУдаления", Сотрудник.ПометкаУдаления);
	СведенияДляОбновленияСловПоиска.Вставить("Наименование", Сотрудник.Наименование);
	СведенияДляОбновленияСловПоиска.Вставить("ДолжностьСотрудника", Сотрудник.Должность);
	СведенияДляОбновленияСловПоиска.Вставить("Должность", СведенияОДолжности);
		
	Возврат СведенияДляОбновленияСловПоиска;
	
КонецФункции

// Возвращает данные, необходимые для обновления доступности в поиске.
//
// Параметры:
//	Объект - СправочникОбъект, СправочникВыборкаИмяСправочника - Объект, данные которого необходимо получить.
//
// Возвращаемое значение:
//	Структура:
//		* Ссылка - СправочникСсылка.
//		* ИспользоватьВПоиске - Булево.
//
Функция ПараметрыОбновленияДоступностиВПоиске(Объект) Экспорт
	
	СведенияДляОбновленияДоступности = Новый Структура;
	
	СведенияДляОбновленияДоступности.Вставить("Ссылка", Объект.Ссылка);
	
	Если ТипЗнч(Объект) = Тип("СправочникОбъект.Пользователи")
			Или ТипЗнч(Объект) = Тип("СправочникВыборка.Пользователи") Тогда
			
		СведенияДляОбновленияДоступности.Вставить(
			"ИспользоватьВПоиске", Не Объект.ПометкаУдаления И Не Объект.Служебный И Не Объект.Недействителен);
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.Сотрудники")
			Или ТипЗнч(Объект) = Тип("СправочникВыборка.Сотрудники") Тогда
			
		СведенияДляОбновленияДоступности.Вставить(
			"ИспользоватьВПоиске", Не Объект.ПометкаУдаления И Объект.Действует);
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.КонтактныеЛица")
			Или ТипЗнч(Объект) = Тип("СправочникВыборка.КонтактныеЛица") Тогда
			
		СведенияДляОбновленияДоступности.Вставить(
			"ИспользоватьВПоиске", Не Объект.ПометкаУдаления И Не Объект.НеДействует);
	Иначе
		СведенияДляОбновленияДоступности.Вставить("ИспользоватьВПоиске", Не Объект.ПометкаУдаления);
	КонецЕсли;
	
	Возврат СведенияДляОбновленияДоступности;
	
КонецФункции

// Представление объекта поиска
//
// Параметры:
//	Объект - СправочникСсылка.Сотрудники,
//			 СправочникСсылка.Контрагенты,
//			 СправочникСсылка.Проекты,
//			 СправочникСсылка.ЛичныеАдресаты,
//			 СправочникСсылка.СтруктураПредприятия,
//			 СправочникСсылка.РолиИсполнителей,
//			 СправочникСсылка.АвтоподстановкиДляОбъектов,
//			 СправочникСсылка.РабочиеГруппы,
//			 СправочникСсылка.Организации,
//			 СправочникСсылка.АвтоподстановкиДляПроцессов,
//			 СправочникСсылка.Мероприятия,
//			 СправочникСсылка.КонтактныеЛица - Объект для формирования представления.
// 
// Возвращаемое значение:
//	Строка.
//
Функция ПредставлениеОбъектаПоиска(Объект) Экспорт
	
	Возврат Строка(Объект);
	
КонецФункции

// Выполняет обновление слов поиска по подразделению.
//
// Параметры:
//	ПараметрыОбновления - см. ПараметрыОбновленияСловПоиска.
//
Процедура ОбновитьСловаПоискаПоПодразделению(ПараметрыОбновления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ПараметрыОбновления.Ссылка)
			Или Не ОбщегоНазначения.СсылкаСуществует(ПараметрыОбновления.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(ПараметрыОбновления.Ссылка);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Ссылка,
		|	НЕ Сотрудники.ПометкаУдаления
		|		И Сотрудники.Действует
		|		И &ИспользоватьВПоиске КАК ИспользоватьВПоиске
		|ПОМЕСТИТЬ Сотрудники
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Подразделение = &Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИсполнителиРолей.РольИсполнителя.Владелец КАК ОбъектПоиска,
		|	ИсполнителиРолей.РольИсполнителя.ПометкаУдаления = ЛОЖЬ
		|		И &ИспользоватьВПоиске КАК ИспользоватьВПоиске
		|ИЗ
		|	Сотрудники КАК Сотрудники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсполнителиРолей КАК ИсполнителиРолей
		|		ПО Сотрудники.Ссылка = ИсполнителиРолей.Исполнитель
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Сотрудники.Ссылка,
		|	Сотрудники.ИспользоватьВПоиске
		|ИЗ
		|	Сотрудники КАК Сотрудники");
	
	Запрос.УстановитьПараметр("Объект", ПараметрыОбновления.Ссылка);
	Запрос.УстановитьПараметр("ИспользоватьВПоиске", Не ПараметрыОбновления.ПометкаУдаления);
	
	ОбъектыПоиска = Запрос.Выполнить().Выгрузить();
	
	ОбъектыПоиска.Колонки.Добавить("Слово");
	ОбъектыПоиска.Колонки.Добавить("ОбъектДоступа");
	ОбъектыПоиска.Колонки.Добавить("КритерийПоиска");
	
	ОбъектыПоиска.ЗаполнитьЗначения(ПараметрыОбновления.Наименование, "Слово");
	ОбъектыПоиска.ЗаполнитьЗначения(Неопределено, "ОбъектДоступа");
	ОбъектыПоиска.ЗаполнитьЗначения(ПараметрыОбновления.Ссылка, "КритерийПоиска");
	
	НаборЗаписей.Загрузить(ОбъектыПоиска);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.КритерийПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = Не ПараметрыОбновления.ПометкаУдаления;
	НоваяЗапись.Слово = ПараметрыОбновления.Наименование;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Выполняет обновление слов поиска по подразделению сотрудника.
//
// Параметры:
//	Сотрудник - СправочникСсылка.Сотрудники - Сотрудник.
//	ПараметрыОбновления - см. ПараметрыОбновленияСловПоискаПоПодразделениюСотрудника.
//
Процедура ОбновитьСловаПоискаПоПодразделениюСотрудника(Сотрудник, ПараметрыОбновления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ПараметрыОбновления.ПодразделениеСотрудника <> ПараметрыОбновления.Подразделение.Ссылка Тогда
		
		Запись = СоздатьМенеджерЗаписи();
		Запись.ОбъектПоиска = Сотрудник;
		Запись.КритерийПоиска = ПараметрыОбновления.Подразделение.Ссылка;
		Запись.Удалить();
		
	Иначе
		
		Запись = СоздатьМенеджерЗаписи();
		Запись.ОбъектПоиска = Сотрудник;
		Запись.КритерийПоиска = ПараметрыОбновления.Подразделение.Ссылка;
		Запись.ИспользоватьВПоиске = ПараметрыОбновления.ПометкаУдаления;
		Запись.Слово = ПараметрыОбновления.Подразделение.Наименование;
		Запись.Записать(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет обновление слов поиска по рабочей группе.
//
// Параметры:
//	ПараметрыОбновления - см. ПараметрыОбновленияСловПоиска.
//
Процедура ОбновитьСловаПоискаПоРабочейГруппе(ПараметрыОбновления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ПараметрыОбновления.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(ПараметрыОбновления.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(ПараметрыОбновления.Ссылка);
		
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РабочиеГруппыСостав.Участник КАК ОбъектПоиска,
		|	РабочиеГруппыСостав.Участник.ПометкаУдаления = ЛОЖЬ
		|		И РабочиеГруппыСостав.Участник.Действует = ИСТИНА
		|		И &ИспользоватьВПоиске КАК ИспользоватьВПоиске
		|ИЗ
		|	Справочник.РабочиеГруппы.Состав КАК РабочиеГруппыСостав
		|ГДЕ
		|	РабочиеГруппыСостав.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", ПараметрыОбновления.Ссылка);
	Запрос.УстановитьПараметр("ИспользоватьВПоиске", Не ПараметрыОбновления.ПометкаУдаления);
	
	ОбъектыПоиска = Запрос.Выполнить().Выгрузить();
	
	ОбъектыПоиска.Колонки.Добавить("Слово");
	ОбъектыПоиска.Колонки.Добавить("ОбъектДоступа");
	ОбъектыПоиска.Колонки.Добавить("КритерийПоиска");
	
	ОбъектыПоиска.ЗаполнитьЗначения(ПараметрыОбновления.Наименование, "Слово");
	ОбъектыПоиска.ЗаполнитьЗначения(Неопределено, "ОбъектДоступа");
	ОбъектыПоиска.ЗаполнитьЗначения(ПараметрыОбновления.Ссылка, "КритерийПоиска");
	
	НаборЗаписей.Загрузить(ОбъектыПоиска);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.КритерийПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = Не ПараметрыОбновления.ПометкаУдаления;
	НоваяЗапись.Слово = ПараметрыОбновления.Наименование;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Выполняет обновление слов поиска по проекту.
//
// Параметры:
//	ПараметрыОбновления - см. ПараметрыОбновленияСловПоиска.
//
Процедура ОбновитьСловаПоискаПоПроекту(ПараметрыОбновления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ПараметрыОбновления.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(ПараметрыОбновления.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(ПараметрыОбновления.Ссылка);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПроектнаяКоманда.Исполнитель КАК ОбъектПоиска,
		|	ВЫБОР
		|		КОГДА ПроектнаяКоманда.Исполнитель ССЫЛКА Справочник.Пользователи
		|			ТОГДА НЕ ПроектнаяКоманда.Исполнитель.ПометкаУдаления
		|					И НЕ ПроектнаяКоманда.Исполнитель.Служебный
		|					И НЕ ПроектнаяКоманда.Исполнитель.Недействителен
		|					И &ИспользоватьВПоиске
		|		КОГДА ПроектнаяКоманда.Исполнитель ССЫЛКА Справочник.Сотрудники
		|			ТОГДА НЕ ПроектнаяКоманда.Исполнитель.ПометкаУдаления
		|					И ПроектнаяКоманда.Исполнитель.Действует
		|					И &ИспользоватьВПоиске
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ИспользоватьВПоиске
		|ИЗ
		|	Справочник.Проекты.ПроектнаяКоманда КАК ПроектнаяКоманда
		|ГДЕ
		|	(ПроектнаяКоманда.Исполнитель ССЫЛКА Справочник.Пользователи
		|			ИЛИ ПроектнаяКоманда.Исполнитель ССЫЛКА Справочник.Сотрудники)");
	Запрос.УстановитьПараметр("Проект", ПараметрыОбновления.Ссылка);
	Запрос.УстановитьПараметр("ИспользоватьВПоиске", Не ПараметрыОбновления.ПометкаУдаления);
	
	ОбъектыПоиска = Запрос.Выполнить().Выгрузить();
	
	ОбъектыПоиска.Колонки.Добавить("Слово");
	ОбъектыПоиска.Колонки.Добавить("ОбъектДоступа");
	ОбъектыПоиска.Колонки.Добавить("КритерийПоиска");
	
	ОбъектыПоиска.ЗаполнитьЗначения(ПараметрыОбновления.Наименование, "Слово");
	ОбъектыПоиска.ЗаполнитьЗначения(ПараметрыОбновления.Ссылка, "ОбъектДоступа");
	ОбъектыПоиска.ЗаполнитьЗначения(ПараметрыОбновления.Ссылка, "КритерийПоиска");
	
	НаборЗаписей.Загрузить(ОбъектыПоиска);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.ОбъектДоступа = ПараметрыОбновления.Ссылка;
	НоваяЗапись.КритерийПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = Не ПараметрыОбновления.ПометкаУдаления;
	НоваяЗапись.Слово = ПараметрыОбновления.Наименование;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Выполняет обновление слов поиска по сотруднику.
//
// Параметры:
//	ПараметрыОбновления - см. ПараметрыОбновленияСловПоиска.
//
Процедура ОбновитьСловаПоискаПоСотруднику(ПараметрыОбновления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ПараметрыОбновления.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(ПараметрыОбновления.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	СтрокиДляПоиска = ПараметрыОбновления.КонтактнаяИнформация.ВыгрузитьКолонку("Представление");
	
	СтрокиДляПоиска.Добавить(Строка(ПараметрыОбновления.Владелец));
	СтрокиДляПоиска.Добавить(Строка(ПараметрыОбновления.ПредставлениеВПереписке));
	
	КоличествоСтрок = СтрокиДляПоиска.Количество();
	Для Индекс = 1 По КоличествоСтрок Цикл
		ОбратныйИндекс = КоличествоСтрок - Индекс;
		Если ПустаяСтрока(СтрокиДляПоиска[ОбратныйИндекс]) Тогда
			СтрокиДляПоиска.Удалить(ОбратныйИндекс);
		КонецЕсли;
	КонецЦикла;
	
	Слово = СтрСоединить(СтрокиДляПоиска, "~");
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(ПараметрыОбновления.Ссылка);
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.КритерийПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = НЕ ПараметрыОбновления.ПометкаУдаления И ПараметрыОбновления.Действует;
	НоваяЗапись.Слово = Слово;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Выполняет обновление слов поиска по должности.
//
// Параметры:
//	ПараметрыОбновления - см. ПараметрыОбновленияСловПоиска.
//
Процедура ОбновитьСловаПоискаПоДолжности(ПараметрыОбновления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ПараметрыОбновления.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(ПараметрыОбновления.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(ПараметрыОбновления.Ссылка);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК ОбъектПоиска,
		|	НЕ Сотрудники.ПометкаУдаления
		|		И Сотрудники.Действует
		|		И &ИспользоватьВПоиске КАК ИспользоватьВПоиске
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Должность = &Должность");
	Запрос.УстановитьПараметр("Должность", ПараметрыОбновления.Ссылка);
	Запрос.УстановитьПараметр("ИспользоватьВПоиске", Не ПараметрыОбновления.ПометкаУдаления);
	
	ОбъектыПоиска = Запрос.Выполнить().Выгрузить();
	
	ОбъектыПоиска.Колонки.Добавить("Слово");
	ОбъектыПоиска.Колонки.Добавить("ОбъектДоступа");
	ОбъектыПоиска.Колонки.Добавить("КритерийПоиска");
	
	ОбъектыПоиска.ЗаполнитьЗначения(ПараметрыОбновления.Наименование, "Слово");
	ОбъектыПоиска.ЗаполнитьЗначения(ПараметрыОбновления.Ссылка, "КритерийПоиска");
	ОбъектыПоиска.ЗаполнитьЗначения(Неопределено, "ОбъектДоступа");
	
	НаборЗаписей.Загрузить(ОбъектыПоиска);
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Выполняет обновление слов поиска по должности сотрудника.
//
// Параметры:
//	ПараметрыОбновления - см. ПараметрыОбновленияСловПоискаПоДолжностиСотрудника.
//
Процедура ОбновитьСловаПоискаПоДолжностиСотрудника(ПараметрыОбновления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ПараметрыОбновления.ДолжностьСотрудника <> ПараметрыОбновления.Должность.Ссылка Тогда
		
		Запись = СоздатьМенеджерЗаписи();
		Запись.ОбъектПоиска = ПараметрыОбновления.Ссылка;
		Запись.КритерийПоиска = ПараметрыОбновления.Должность.Ссылка;
		Запись.Удалить();
		
	Иначе
		
		Запись = СоздатьМенеджерЗаписи();
		Запись.ОбъектПоиска = ПараметрыОбновления.Ссылка;
		Запись.КритерийПоиска = ПараметрыОбновления.Должность.Ссылка;
		Запись.ИспользоватьВПоиске = ПараметрыОбновления.ПометкаУдаления;
		Запись.Слово = ПараметрыОбновления.Должность.Наименование;
		Запись.Записать(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет обновление слов поиска по роли.
//
// Параметры:
//	ПараметрыОбновления - см. ПараметрыОбновленияСловПоиска.
//
Процедура ОбновитьСловаПоискаПоРоли(ПараметрыОбновления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ПараметрыОбновления.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(ПараметрыОбновления.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(ПараметрыОбновления.Ссылка);
	
	СтрокиДляПоиска = ПараметрыОбновления.КонтактнаяИнформация.ВыгрузитьКолонку("Представление");
	
	СтрокиДляПоиска.Добавить(ПараметрыОбновления.Наименование);
	
	КоличествоСтрок = СтрокиДляПоиска.Количество();
	Для Индекс = 1 По КоличествоСтрок Цикл
		ОбратныйИндекс = КоличествоСтрок - Индекс;
		Если ПустаяСтрока(СтрокиДляПоиска[ОбратныйИндекс]) Тогда
			СтрокиДляПоиска.Удалить(ОбратныйИндекс);
		КонецЕсли;
	КонецЦикла;
	
	Слово = СтрСоединить(СтрокиДляПоиска, "~");
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.КритерийПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = НЕ ПараметрыОбновления.ПометкаУдаления;
	НоваяЗапись.Слово = Слово;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Выполняет обновление слов поиска по контрагенту.
//
// Параметры:
//	ПараметрыОбновления - см. ПараметрыОбновленияСловПоиска.
//
Процедура ОбновитьСловаПоискаПоКонтрагенту(ПараметрыОбновления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ПараметрыОбновления.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(ПараметрыОбновления.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(ПараметрыОбновления.Ссылка);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КонтактныеЛица.Ссылка КАК ОбъектПоиска,
		|	НЕ КонтактныеЛица.ПометкаУдаления
		|		И &ИспользоватьВПоиске КАК ИспользоватьВПоиске
		|ИЗ
		|	Справочник.КонтактныеЛица КАК КонтактныеЛица
		|ГДЕ
		|	КонтактныеЛица.Владелец = &Контрагент";
	Запрос.УстановитьПараметр("Контрагент", ПараметрыОбновления.Ссылка);
	Запрос.УстановитьПараметр("ИспользоватьВПоиске", Не ПараметрыОбновления.ПометкаУдаления);
	
	ОбъектыПоиска = Запрос.Выполнить().Выгрузить();
	
	ОбъектыПоиска.Колонки.Добавить("Слово");
	ОбъектыПоиска.Колонки.Добавить("ОбъектДоступа");
	ОбъектыПоиска.Колонки.Добавить("КритерийПоиска");
	
	ОбъектыПоиска.ЗаполнитьЗначения(ПараметрыОбновления.Наименование, "Слово");
	ОбъектыПоиска.ЗаполнитьЗначения(ПараметрыОбновления.Ссылка, "ОбъектДоступа");
	ОбъектыПоиска.ЗаполнитьЗначения(ПараметрыОбновления.Ссылка, "КритерийПоиска");
	
	НаборЗаписей.Загрузить(ОбъектыПоиска);
	
	СтрокиДляПоиска = ПараметрыОбновления.КонтактнаяИнформация.ВыгрузитьКолонку("Представление");
	
	Если СтрокиДляПоиска.Количество() = 0 Тогда
		СтрокиДляПоиска.Добавить(ПараметрыОбновления.Наименование);
	Иначе	
		СтрокиДляПоиска.Вставить(0, ПараметрыОбновления.Наименование);
	КонецЕсли;   
	
	Если ПараметрыОбновления.Свойство("ИНН") И ЗначениеЗаполнено(ПараметрыОбновления.ИНН) Тогда
		СтрокиДляПоиска.Вставить(0, ПараметрыОбновления.ИНН);
	КонецЕсли;	
	
	КоличествоСтрок = СтрокиДляПоиска.Количество();
	Для Индекс = 1 По КоличествоСтрок Цикл
		ОбратныйИндекс = КоличествоСтрок - Индекс;
		Если ПустаяСтрока(СтрокиДляПоиска[ОбратныйИндекс]) Тогда
			СтрокиДляПоиска.Удалить(ОбратныйИндекс);
		КонецЕсли;
	КонецЦикла;
	Слово = СтрСоединить(СтрокиДляПоиска, "~");
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.ОбъектДоступа = ПараметрыОбновления.Ссылка;
	НоваяЗапись.КритерийПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = НЕ ПараметрыОбновления.ПометкаУдаления;
	НоваяЗапись.Слово = Слово;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Выполняет обновление слов поиска по контактному лицу.
//
// Параметры:
//	ПараметрыОбновления - см. ПараметрыОбновленияСловПоиска.
//
Процедура ОбновитьСловаПоискаПоКонтактномуЛицу(ПараметрыОбновления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ПараметрыОбновления.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(ПараметрыОбновления.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(ПараметрыОбновления.Ссылка);
	
	СтрокиДляПоиска = ПараметрыОбновления.КонтактнаяИнформация.ВыгрузитьКолонку("Представление");
	
	СтрокиДляПоиска.Добавить(ПараметрыОбновления.Наименование);
	КоличествоСтрок = СтрокиДляПоиска.Количество();
	Для Индекс = 1 По КоличествоСтрок Цикл
		ОбратныйИндекс = КоличествоСтрок - Индекс;
		Если ПустаяСтрока(СтрокиДляПоиска[ОбратныйИндекс]) Тогда
			СтрокиДляПоиска.Удалить(ОбратныйИндекс);
		КонецЕсли;
	КонецЦикла;
	Слово = СтрСоединить(СтрокиДляПоиска, "~");
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.КритерийПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.ОбъектДоступа = ПараметрыОбновления.Владелец;
	НоваяЗапись.ИспользоватьВПоиске = НЕ ПараметрыОбновления.ПометкаУдаления;
	НоваяЗапись.Слово = Слово;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Выполняет обновление слов поиска по личному адресату.
//
// Параметры:
//	ПараметрыОбновления - см. ПараметрыОбновленияСловПоиска.
//
Процедура ОбновитьСловаПоискаПоЛичномуАдресату(ПараметрыОбновления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ПараметрыОбновления.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(ПараметрыОбновления.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(ПараметрыОбновления.Ссылка);
	
	СтрокиДляПоиска = ПараметрыОбновления.КонтактнаяИнформация.ВыгрузитьКолонку("Представление");
	
	СтрокиДляПоиска.Добавить(ПараметрыОбновления.Наименование);
	КоличествоСтрок = СтрокиДляПоиска.Количество();
	Для Индекс = 1 По КоличествоСтрок Цикл
		ОбратныйИндекс = КоличествоСтрок - Индекс;
		Если ПустаяСтрока(СтрокиДляПоиска[ОбратныйИндекс]) Тогда
			СтрокиДляПоиска.Удалить(ОбратныйИндекс);
		КонецЕсли;
	КонецЦикла;
	Слово = СтрСоединить(СтрокиДляПоиска, "~");
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.КритерийПоиска = ПараметрыОбновления.Ссылка;
	
	НоваяЗапись.ОбъектДоступа = ПараметрыОбновления.Сотрудник;
	
	НоваяЗапись.ИспользоватьВПоиске = НЕ ПараметрыОбновления.ПометкаУдаления;
	НоваяЗапись.Слово = Слово;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Выполняет обновление доступности в поиске.
//
// Параметры:
//	СсылкаНаОбъект - СправочникСсылка - Ссылка на объект, доступность которого необходимо обновить.
//	ПараметрыОбновления - Структура:
//		* Ссылка - СправочникСсылка - Ссылка на объект, доступность которого необходимо обновить.
//		* ИспользоватьВПоиске - Булево - Признак использования в поиске.
//
Процедура ОбновитьДоступностьВПоиске(СсылкаНаОбъект, ПараметрыОбновления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(СсылкаНаОбъект)
		Или Не ОбщегоНазначения.СсылкаСуществует(СсылкаНаОбъект) Тогда
		
		Возврат;
	КонецЕсли;
	
	// Обновление по критерию поиска.
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОбъектыПоискаВАдреснойКниге");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("КритерийПоиска", СсылкаНаОбъект);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОбъектыПоискаВАдреснойКниге.ОбъектПоиска КАК ОбъектПоиска,
		|	ОбъектыПоискаВАдреснойКниге.КритерийПоиска КАК КритерийПоиска,
		|	ОбъектыПоискаВАдреснойКниге.Слово КАК Слово,
		|	ОбъектыПоискаВАдреснойКниге.ОбъектДоступа КАК ОбъектДоступа,
		|	ВЫБОР
		|		КОГДА ОбъектыПоискаВАдреснойКниге.ОбъектПоиска ССЫЛКА Справочник.Сотрудники
		|			ТОГДА НЕ ОбъектыПоискаВАдреснойКниге.ОбъектПоиска.ПометкаУдаления
		|					И ОбъектыПоискаВАдреснойКниге.ОбъектПоиска.Действует
		|					И &ИспользоватьВПоиске
		|		КОГДА ОбъектыПоискаВАдреснойКниге.ОбъектПоиска ССЫЛКА Справочник.КонтактныеЛица
		|			ТОГДА НЕ ОбъектыПоискаВАдреснойКниге.ОбъектПоиска.ПометкаУдаления
		|					И НЕ ОбъектыПоискаВАдреснойКниге.ОбъектПоиска.НеДействует
		|					И &ИспользоватьВПоиске
		|		ИНАЧЕ НЕ ОбъектыПоискаВАдреснойКниге.ОбъектПоиска.ПометкаУдаления
		|				И &ИспользоватьВПоиске
		|	КОНЕЦ КАК ИспользоватьВПоиске
		|ИЗ
		|	РегистрСведений.ОбъектыПоискаВАдреснойКниге КАК ОбъектыПоискаВАдреснойКниге
		|ГДЕ
		|	ОбъектыПоискаВАдреснойКниге.КритерийПоиска = &КритерийПоиска";
	Запрос.УстановитьПараметр("КритерийПоиска", СсылкаНаОбъект);
	Запрос.УстановитьПараметр("ИспользоватьВПоиске", ПараметрыОбновления.ИспользоватьВПоиске);
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.КритерийПоиска.Установить(СсылкаНаОбъект);
		НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	// Обновление по объекту поиска.
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОбъектыПоискаВАдреснойКниге");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("КритерийПоиска", СсылкаНаОбъект);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОбъектыПоискаВАдреснойКниге.ОбъектПоиска КАК ОбъектПоиска,
		|	ОбъектыПоискаВАдреснойКниге.КритерийПоиска КАК КритерийПоиска,
		|	ОбъектыПоискаВАдреснойКниге.Слово КАК Слово,
		|	ОбъектыПоискаВАдреснойКниге.ОбъектДоступа КАК ОбъектДоступа,
		|	ВЫБОР
		|		КОГДА ОбъектыПоискаВАдреснойКниге.КритерийПоиска ССЫЛКА Справочник.Сотрудники
		|			ТОГДА НЕ ОбъектыПоискаВАдреснойКниге.КритерийПоиска.ПометкаУдаления
		|					И ОбъектыПоискаВАдреснойКниге.КритерийПоиска.Действует
		|					И &ИспользоватьВПоиске
		|		КОГДА ОбъектыПоискаВАдреснойКниге.КритерийПоиска ССЫЛКА Справочник.КонтактныеЛица
		|			ТОГДА НЕ ОбъектыПоискаВАдреснойКниге.КритерийПоиска.ПометкаУдаления
		|					И НЕ ОбъектыПоискаВАдреснойКниге.КритерийПоиска.НеДействует
		|					И &ИспользоватьВПоиске
		|		ИНАЧЕ НЕ ОбъектыПоискаВАдреснойКниге.КритерийПоиска.ПометкаУдаления
		|				И &ИспользоватьВПоиске
		|	КОНЕЦ КАК ИспользоватьВПоиске
		|ИЗ
		|	РегистрСведений.ОбъектыПоискаВАдреснойКниге КАК ОбъектыПоискаВАдреснойКниге
		|ГДЕ
		|	ОбъектыПоискаВАдреснойКниге.ОбъектПоиска = &ОбъектПоиска";
	Запрос.УстановитьПараметр("ОбъектПоиска", СсылкаНаОбъект);
	Запрос.УстановитьПараметр("ИспользоватьВПоиске", ПараметрыОбновления.ИспользоватьВПоиске);
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ОбъектПоиска.Установить(СсылкаНаОбъект);
		НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Выполняет обновление слов поиска по организации.
//
// Параметры:
//	ПараметрыОбновления - см. ПараметрыОбновленияСловПоиска.
//
Процедура ОбновитьСловаПоискаПоОрганизации(ПараметрыОбновления) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ПараметрыОбновления.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(ПараметрыОбновления.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(ПараметрыОбновления.Ссылка);
	
	СтрокиДляПоиска = ПараметрыОбновления.КонтактнаяИнформация.ВыгрузитьКолонку("Представление");
	
	СтрокиДляПоиска.Добавить(ПараметрыОбновления.Наименование);
	
	КоличествоСтрок = СтрокиДляПоиска.Количество();
	Для Индекс = 1 По КоличествоСтрок Цикл
		ОбратныйИндекс = КоличествоСтрок - Индекс;
		Если ПустаяСтрока(СтрокиДляПоиска[ОбратныйИндекс]) Тогда
			СтрокиДляПоиска.Удалить(ОбратныйИндекс);
		КонецЕсли;
	КонецЦикла;
	
	Слово = СтрСоединить(СтрокиДляПоиска, "~");
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.КритерийПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = НЕ ПараметрыОбновления.ПометкаУдаления;
	НоваяЗапись.Слово = Слово;
	
	НаборЗаписей.Записать();	

КонецПроцедуры

// Выполняет обновление слов поиска по автоподстановке для объектов.
//
// Параметры:
//	ПараметрыОбновления - см. ПараметрыОбновленияСловПоиска.
//
Процедура ОбновитьСловаПоискаПоАвтоподстановкеДляОбъектов(ПараметрыОбновления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ПараметрыОбновления.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(ПараметрыОбновления.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(ПараметрыОбновления.Ссылка);
	
	СтрокиДляПоиска = Новый Массив;
	СтрокиДляПоиска.Добавить(ПараметрыОбновления.Наименование);
	
	Слово = СтрСоединить(СтрокиДляПоиска, "~");
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.КритерийПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = НЕ ПараметрыОбновления.ПометкаУдаления;
	НоваяЗапись.Слово = Слово;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Выполняет обновление слов поиска по автоподстановке для процессов.
//
// Параметры:
//	ПараметрыОбновления - см. ПараметрыОбновленияСловПоиска.
//
Процедура ОбновитьСловаПоискаПоАвтоподстановкеДляПроцессов(ПараметрыОбновления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ПараметрыОбновления.Ссылка)
		Или Не ОбщегоНазначения.СсылкаСуществует(ПараметрыОбновления.Ссылка) Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КритерийПоиска.Установить(ПараметрыОбновления.Ссылка);
	
	СтрокиДляПоиска = Новый Массив;
	СтрокиДляПоиска.Добавить(ПараметрыОбновления.Наименование);
	
	Слово = СтрСоединить(СтрокиДляПоиска, "~");
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ОбъектПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.КритерийПоиска = ПараметрыОбновления.Ссылка;
	НоваяЗапись.ИспользоватьВПоиске = НЕ ПараметрыОбновления.ПометкаУдаления;
	НоваяЗапись.Слово = Слово;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет заполнение слов поиска в регистре для всех объектов адр. книги. 
//
Процедура ЗаполнитьСловаПоиска() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Подразделение = Справочники.СтруктураПредприятия.ВыбратьИерархически();
	Пока Подразделение.Следующий() Цикл
		РаботаСАдреснойКнигой.ОбновитьСловаПоискаПоОбъекту(Подразделение);
	КонецЦикла;
	
	РабочиеГруппы = Справочники.РабочиеГруппы.ВыбратьИерархически();
	Пока РабочиеГруппы.Следующий() Цикл
		РаботаСАдреснойКнигой.ОбновитьСловаПоискаПоОбъекту(РабочиеГруппы);
	КонецЦикла;
	
	Проекты = Справочники.Проекты.ВыбратьИерархически();
	Пока Проекты.Следующий() Цикл
		Если Проекты.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		РаботаСАдреснойКнигой.ОбновитьСловаПоискаПоОбъекту(Проекты);
	КонецЦикла;
	
	ВсеСотрудники = Справочники.Сотрудники.Выбрать();
	Пока ВсеСотрудники.Следующий() Цикл
		РаботаСАдреснойКнигой.ОбновитьСловаПоискаПоОбъекту(ВсеСотрудники);
	КонецЦикла;
		
	Должности = Справочники.Должности.Выбрать();
	Пока Должности.Следующий() Цикл
		РаботаСАдреснойКнигой.ОбновитьСловаПоискаПоОбъекту(Должности);
	КонецЦикла;
	
	ВсеРоли = Справочники.РолиИсполнителей.Выбрать();
	Пока ВсеРоли.Следующий() Цикл
		РаботаСАдреснойКнигой.ОбновитьСловаПоискаПоОбъекту(ВсеРоли);
	КонецЦикла;
	
	Контрагенты = Справочники.Контрагенты.ВыбратьИерархически();
	Пока Контрагенты.Следующий() Цикл
		Если Контрагенты.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		РаботаСАдреснойКнигой.ОбновитьСловаПоискаПоОбъекту(Контрагенты);
	КонецЦикла;
	
	КонтактныеЛица = Справочники.КонтактныеЛица.Выбрать();
	Пока КонтактныеЛица.Следующий() Цикл
		РаботаСАдреснойКнигой.ОбновитьСловаПоискаПоОбъекту(КонтактныеЛица);
	КонецЦикла;
	
	ЛичныеАдресаты = Справочники.ЛичныеАдресаты.Выбрать();
	Пока ЛичныеАдресаты.Следующий() Цикл
		РаботаСАдреснойКнигой.ОбновитьСловаПоискаПоОбъекту(ЛичныеАдресаты);
	КонецЦикла;
	
	АвтоподстановкиДляДокументов = Справочники.АвтоподстановкиДляОбъектов.Выбрать();
	Пока АвтоподстановкиДляДокументов.Следующий() Цикл
		РаботаСАдреснойКнигой.ОбновитьСловаПоискаПоОбъекту(АвтоподстановкиДляДокументов);
	КонецЦикла;
	
	АвтоподстановкиДляПроцессов = Справочники.АвтоподстановкиДляПроцессов.Выбрать();
	Пока АвтоподстановкиДляПроцессов.Следующий() Цикл
		РаботаСАдреснойКнигой.ОбновитьСловаПоискаПоОбъекту(АвтоподстановкиДляПроцессов);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает описание параметров обновления слов поиска.
//
// Параметры:
//	Объект - СправочникОбъект,
//			 СправочникВыборкаИмяСправочника,
//			 СправочникСсылка - Объект, данные которого необходимо получить.
//
// Возвращаемое значение:
//	Структура:
//		* Ссылка - СправочникСсылка - ,
//				 - Неопределено - .
//		* Наименование - Строка - ,
//					   - Неопределено - .
//		* ПометкаУдаления - Булево - ,
//						  - Неопределено - .
//		* Владелец - СправочникСсылка.ФизическиеЛица - ,
//				   - Неопределено - ,
//		* ПредставлениеВПереписке - Строка -,
//								  - Неопределено - .
//		* Подразделение - СправочникСсылка.СтруктураПредприятия - ,
//						- Неопределено - .
//		* Должность - СправочникСсылка.Должности - ,
//					- Неопределено - .
//		* Действует - Булево - ,
//					- Неопределено -.
//		* Сотрудник - СправочникСсылка.Сотрудники - ,
//					- Неопределено - .
//		* КонтактнаяИнформация - ТаблицаЗначений - ,
//							   - Неопределено - .
//
Функция НовыеПараметрыОбновленияСловПоиска(Объект)
	
	ПараметрыОбновления = Новый Структура;
	
	ПараметрыОбновления.Вставить("Ссылка", Неопределено);
	ПараметрыОбновления.Вставить("Наименование", Неопределено);
	ПараметрыОбновления.Вставить("ПометкаУдаления", Неопределено);
	
	Если ЭтоСотрудник(Объект) Тогда
		ПараметрыОбновления.Вставить("Владелец", Неопределено);
		ПараметрыОбновления.Вставить("ПредставлениеВПереписке", Неопределено);
		ПараметрыОбновления.Вставить("Подразделение", Неопределено);
		ПараметрыОбновления.Вставить("Должность", Неопределено);
		ПараметрыОбновления.Вставить("Действует", Неопределено);
		ПараметрыОбновления.Вставить("КонтактнаяИнформация", Неопределено);
	КонецЕсли;
	
	Если ЭтоКонтактноеЛицо(Объект) Тогда
		ПараметрыОбновления.Вставить("Владелец", Неопределено);
		ПараметрыОбновления.Вставить("КонтактнаяИнформация", Неопределено);
	КонецЕсли;

	Если ЭтоКонтрагент(Объект) Тогда
		ПараметрыОбновления.Вставить("ИНН", Неопределено);
	КонецЕсли;
	
	Если ЭтоРольИсполнителя(Объект)
			Или ЭтоКонтрагент(Объект)
			Или ЭтоОрганизация(Объект) Тогда
		
		ПараметрыОбновления.Вставить("КонтактнаяИнформация", Неопределено);
	КонецЕсли;
	
	Если ЭтоЛичныйАдресат(Объект) Тогда
		ПараметрыОбновления.Вставить("Сотрудник", Неопределено);
		ПараметрыОбновления.Вставить("КонтактнаяИнформация", Неопределено);
	КонецЕсли;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Проверяет, является ли объект одним из типов справочника Сотрудники.
//
// Параметры:
//	Объект - Произвольный - Объект, который необходимо проверить.
//
// Возвращаемое значение:
//	Булево - Истина, если объект является типом справочника Сотрудники.
//
Функция ЭтоСотрудник(Объект)
	
	Возврат ТипЗнч(Объект) = Тип("СправочникОбъект.Сотрудники")
			Или ТипЗнч(Объект) = Тип("СправочникВыборка.Сотрудники")
				Или ТипЗнч(Объект) = Тип("СправочникСсылка.Сотрудники");
	
КонецФункции

// Проверяет, является ли объект одним из типов справочника РолиИсполнителей.
//
// Параметры:
//	Объект - Произвольный - Объект, который необходимо проверить.
//
// Возвращаемое значение:
//	Булево - Истина, если объект является типом справочника РолиИсполнителей.
//
Функция ЭтоРольИсполнителя(Объект)
	
	Возврат ТипЗнч(Объект) = Тип("СправочникОбъект.РолиИсполнителей")
			Или ТипЗнч(Объект) = Тип("СправочникВыборка.РолиИсполнителей")
				Или ТипЗнч(Объект) = Тип("СправочникСсылка.РолиИсполнителей");
	
КонецФункции

// Проверяет, является ли объект одним из типов справочника КонтактныеЛица.
//
// Параметры:
//	Объект - Произвольный - Объект, который необходимо проверить.
//
// Возвращаемое значение:
//	Булево - Истина, если объект является типом справочника КонтактныеЛица.
//
Функция ЭтоКонтактноеЛицо(Объект)
	
	Возврат ТипЗнч(Объект) = Тип("СправочникОбъект.КонтактныеЛица")
			Или ТипЗнч(Объект) = Тип("СправочникВыборка.КонтактныеЛица")
				Или ТипЗнч(Объект) = Тип("СправочникСсылка.КонтактныеЛица");
	
КонецФункции

// Проверяет, является ли объект одним из типов справочника Контрагенты.
//
// Параметры:
//	Объект - Произвольный - Объект, который необходимо проверить.
//
// Возвращаемое значение:
//	Булево - Истина, если объект является типом справочника Контрагенты.
//
Функция ЭтоКонтрагент(Объект)
	
	Возврат ТипЗнч(Объект) = Тип("СправочникОбъект.Контрагенты")
			Или ТипЗнч(Объект) = Тип("СправочникВыборка.Контрагенты")
				Или ТипЗнч(Объект) = Тип("СправочникСсылка.Контрагенты");
	
КонецФункции

// Проверяет, является ли объект одним из типов справочника Организации.
//
// Параметры:
//	Объект - Произвольный - Объект, который необходимо проверить.
//
// Возвращаемое значение:
//	Булево - Истина, если объект является типом справочника Организации.
//
Функция ЭтоОрганизация(Объект)
	
	Возврат ТипЗнч(Объект) = Тип("СправочникОбъект.Организации")
			Или ТипЗнч(Объект) = Тип("СправочникВыборка.Организации")
				Или ТипЗнч(Объект) = Тип("СправочникСсылка.Организации");
	
КонецФункции

// Проверяет, является ли объект одним из типов справочника ЛичныеАдресаты.
//
// Параметры:
//	Объект - Произвольный - Объект, который необходимо проверить.
//
// Возвращаемое значение:
//	Булево - Истина, если объект является типом справочника ЛичныеАдресаты.
//
Функция ЭтоЛичныйАдресат(Объект)
	
	Возврат ТипЗнч(Объект) = Тип("СправочникОбъект.ЛичныеАдресаты")
			Или ТипЗнч(Объект) = Тип("СправочникВыборка.ЛичныеАдресаты")
				Или ТипЗнч(Объект) = Тип("СправочникСсылка.ЛичныеАдресаты");
	
КонецФункции

#КонецОбласти

#КонецЕсли


