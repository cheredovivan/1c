
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает данные обработки файла сервисом.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  Поля - Строка - список полей через разделитель ",". Если не указаны, то берутся все поля.
// 
// Возвращаемое значение:
//  Структура - см. ОписаниеДанных
// 
Функция Данные(ВерсияФайла, Поля = "") Экспорт
	
	Данные = ОписаниеДанных();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВерсияФайла", ВерсияФайла);
	Запрос.Текст =
		"ВЫБРАТЬ
		|%ПоляЗапроса
		|ИЗ
		|	РегистрСведений.ТаймлистДанныеРаботыСервиса КАК ТаймлистДанныеРаботыСервиса
		|ГДЕ
		|	ТаймлистДанныеРаботыСервиса.ВерсияФайла = &ВерсияФайла";
	
	Если Не ЗначениеЗаполнено(Поля) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляЗапроса",
			"ТаймлистДанныеРаботыСервиса.Автопротокол,
			|ТаймлистДанныеРаботыСервиса.ИдентификаторАвтопротокола,
			|ТаймлистДанныеРаботыСервиса.ИдентификаторЗадания,
			|ТаймлистДанныеРаботыСервиса.Расшифровка,
			|ТаймлистДанныеРаботыСервиса.СледующиеШаги,
			|ТаймлистДанныеРаботыСервиса.Спикеры,
			|ТаймлистДанныеРаботыСервиса.Статус");
		
	Иначе
		
		Поля = СтрЗаменить(Поля, " ", "");
		МассивПолей = СтрРазделить(СокрЛП(Поля), ",");
		МассивСтрокПолейЗапроса = Новый Массив;
		Для Каждого Поле Из МассивПолей Цикл
			МассивСтрокПолейЗапроса.Добавить(СтрШаблон("ТаймлистДанныеРаботыСервиса.%1", Поле));
		КонецЦикла;
		
		СтрокаПоляЗапроса = СтрСоединить(МассивСтрокПолейЗапроса, "," + Символы.ПС);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляЗапроса", СтрокаПоляЗапроса);
		
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Данные, Выборка);
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

// Фиксирует событие начала загрузки файла в сервис.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  ИдентификаторЗадания - Строка
// 
Процедура ЗаписатьНачалоЗагрузкиФайлаВСервис(ВерсияФайла, ИдентификаторЗадания) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
	МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
	МенеджерЗаписи.ИдентификаторЗадания = ИдентификаторЗадания;
	МенеджерЗаписи.Статус = Перечисления.ТаймлистСтатусы.ОтправленНаЗагрузку;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Записывает расшифровку и спикеров, полученных из сервиса.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  Расшифровка - ХранилищеЗначения
//  Спикеры - ХранилищеЗначения
//  Статус - ПеречислениеСсылка.ТаймлистСтатусы
// 
Процедура ЗаписатьРасшифровкуИСпикеров(ВерсияФайла, Расшифровка, Спикеры) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		
		МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
		МенеджерЗаписи.Расшифровка = Расшифровка;
		МенеджерЗаписи.Спикеры = Спикеры;
		МенеджерЗаписи.Статус = Перечисления.ТаймлистСтатусы.Расшифрован;
		МенеджерЗаписи.Записать();
		
		Автор = РегистрыСведений.ТаймлистФайлыВОбработке.АвторЗадания(ВерсияФайла);
		Если ЗначениеЗаполнено(Автор) Тогда
			ДанныеУведомления = ДанныеУведомления(ВерсияФайла);
			Если ТипЗнч(ДанныеУведомления.ВладелецФайла) = Тип("СправочникСсылка.ДокументыПредприятия")
				Или ТипЗнч(ДанныеУведомления.ВладелецФайла) = Тип("СправочникСсылка.Мероприятия") Тогда
				ОбъектУведомления = ДанныеУведомления.ВладелецФайла;
			Иначе
				ОбъектУведомления = ДанныеУведомления.Файл;
			КонецЕсли;
			ВидСобытия = Перечисления.СобытияУведомлений.ТаймлистПолучениеРасшифровкиАвтопротокола;
			РегистрыСведений.ОчередьУведомлений.ДобавитьУведомлениеПоСобытию(Автор, ВидСобытия, ОбъектУведомления,
				ДанныеУведомления.Файл);
		КонецЕсли;
		
		РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Удалить(ВерсияФайла);
		
	КонецЕсли;
	
КонецПроцедуры

// Записывает расшифровку.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  Расшифровка - ХранилищеЗначения
//  Статус - ПеречислениеСсылка.ТаймлистСтатусы
// 
Процедура ЗаписатьРасшифровку(ВерсияФайла, Расшифровка, Статус = Неопределено) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		
		МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
		МенеджерЗаписи.Расшифровка = Расшифровка;
		Если ЗначениеЗаполнено(Статус) Тогда
			МенеджерЗаписи.Статус = Статус;
		Иначе
			МенеджерЗаписи.Статус = Перечисления.ТаймлистСтатусы.Расшифрован;
		КонецЕсли;
		МенеджерЗаписи.Записать();
		РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Удалить(ВерсияФайла);
	КонецЕсли;
	
КонецПроцедуры

// Записывает расшифровку, полученную из сервиса.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  Спикеры - ХранилищеЗначения
// 
Процедура ЗаписатьСпикеров(ВерсияФайла, Спикеры) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
		МенеджерЗаписи.Спикеры = Спикеры;
		МенеджерЗаписи.Записать();
		РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Удалить(ВерсияФайла);
	КонецЕсли;
	
КонецПроцедуры

// Фиксирует событие начала формирования автопротокола.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  ИдентификаторАвтопротокола - Строка
// 
Процедура ЗаписатьНачалоФормированияАвтопротокола(ВерсияФайла, ИдентификаторАвтопротокола) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
		МенеджерЗаписи.ИдентификаторАвтопротокола = ИдентификаторАвтопротокола;
		МенеджерЗаписи.Статус = Перечисления.ТаймлистСтатусы.ПолучениеАвтопротокола;
		МенеджерЗаписи.Записать();
		РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Удалить(ВерсияФайла);
	КонецЕсли;
	
КонецПроцедуры

// Записывает автопротокол.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  ДанныеАвтопротокола - Структура:
//   * Автопротокол - ХранилищеЗначения
//   * СледующиеШаги - Строка
// 
Процедура ЗаписатьАвтопротокол(ВерсияФайла, ДанныеАвтопротокола) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		
		МенеджерЗаписи.Автопротокол = ДанныеАвтопротокола.Автопротокол;
		МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
		МенеджерЗаписи.СледующиеШаги = ДанныеАвтопротокола.СледующиеШаги;
		МенеджерЗаписи.Статус = Перечисления.ТаймлистСтатусы.ПолученАвтопротокол;
		МенеджерЗаписи.Записать();
		
		Автор = РегистрыСведений.ТаймлистФайлыВОбработке.АвторЗадания(ВерсияФайла);
		Если ЗначениеЗаполнено(Автор) Тогда
			ДанныеУведомления = ДанныеУведомления(ВерсияФайла);
			Если ТипЗнч(ДанныеУведомления.ВладелецФайла) = Тип("СправочникСсылка.ДокументыПредприятия")
				Или ТипЗнч(ДанныеУведомления.ВладелецФайла) = Тип("СправочникСсылка.Мероприятия") Тогда
				ОбъектУведомления = ДанныеУведомления.ВладелецФайла;
			Иначе
				ОбъектУведомления = ДанныеУведомления.Файл;
			КонецЕсли;
			ВидСобытия = Перечисления.СобытияУведомлений.ТаймлистПолучениеРасшифровкиАвтопротокола;
			РегистрыСведений.ОчередьУведомлений.ДобавитьУведомлениеПоСобытию(Автор, ВидСобытия, ОбъектУведомления,
				ДанныеУведомления.Файл);
		КонецЕсли;
		
		РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Удалить(ВерсияФайла);
		
	КонецЕсли;
	
КонецПроцедуры

// Фиксирует статус обработки файла сервисом.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  Статус - ПеречислениеСсылка.ТаймлистСтатусы
// 
Процедура ЗаписатьСтатусРаботыССервисом(ВерсияФайла, Статус) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
		МенеджерЗаписи.Статус = Статус;
		МенеджерЗаписи.Записать();
		РегистрыСведений.ТаймлистОшибкиРаботыССервисом.Удалить(ВерсияФайла);
	КонецЕсли;
	
КонецПроцедуры

// Записывает пользователя, начавшего редактирование данных.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  Пользователь - СправочникСсылка.Пользователи
//
Процедура ЗаписатьПолеРедактирует(ВерсияФайла, Пользователь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
		МенеджерЗаписи.Редактирует = Пользователь;
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Записать результат редактирования.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  ПараметрыЗаписи - см. Таймлист.ПараметрыЗаписиДанныхРедактирования
//
Процедура ЗаписатьРезультатРедактирования(ВерсияФайла, ПараметрыЗаписи) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		Если ПараметрыЗаписи.Расшифровка <> Неопределено Тогда
			МенеджерЗаписи.РасшифровкаИзмененная = ПараметрыЗаписи.Расшифровка;
		КонецЕсли;
		Если ПараметрыЗаписи.Спикеры <> Неопределено Тогда
			МенеджерЗаписи.Спикеры = ПараметрыЗаписи.Спикеры;
		КонецЕсли;
		Если ПараметрыЗаписи.Автопротокол <> Неопределено Тогда
			МенеджерЗаписи.АвтопротоколИзмененный = ПараметрыЗаписи.Автопротокол;
		КонецЕсли;
		Если ПараметрыЗаписи.СледующиеШаги <> Неопределено Тогда
			МенеджерЗаписи.СледующиеШаги = ПараметрыЗаписи.СледующиеШаги;
		КонецЕсли;
		МенеджерЗаписи.Редактирует = Справочники.Пользователи.ПустаяСсылка();
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Снимает признак редактирования данных.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//
Процедура ОтменитьРедактированиеДанных(ВерсияФайла) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Редактирует = Справочники.Пользователи.ПустаяСсылка();
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Удаляет запись с отбором по переданной версии файла.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
// 
Процедура Удалить(ВерсияФайла) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
	МенеджерЗаписи.Удалить();
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

#Область ОбработчикиОбновления

// Регистрирует данные к обновлению в плане обмена ОбновлениеИнформационнойБазы.
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.РегистрыСведений.ТаймлистДанныеРаботыСервиса;
	ПолноеИмяРегистра = МетаданныеОбъекта.ПолноеИмя();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяРегистра;
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ТаймлистДанныеРаботыСервиса.ВерсияФайла КАК ВерсияФайла
		|ИЗ
		|	РегистрСведений.ТаймлистДанныеРаботыСервиса КАК ТаймлистДанныеРаботыСервиса
		|ГДЕ
		|	ТаймлистДанныеРаботыСервиса.ВерсияФайла >= &ВерсияФайла
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВерсияФайла");
	
	ВерсияФайла = МетаданныеОбъекта.Измерения.ВерсияФайла.Тип.ПривестиЗначение();
	
	Пока Истина Цикл
		
		Запрос.УстановитьПараметр("ВерсияФайла", ВерсияФайла);
		
		//@skip-check query-in-loop
		Выгрузка = Запрос.Выполнить().Выгрузить();
		КоличествоСтрок = Выгрузка.Количество();
		
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Выгрузка, ДополнительныеПараметры);
		
		Если КоличествоСтрок < 1000 Тогда
			Прервать;
		Иначе
			ВерсияФайла = Выгрузка[КоличествоСтрок - 1].ВерсияФайла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Обрабатывает данные, зарегистрированные в плане обмена ОбновлениеИнформационнойБазы.
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.РегистрыСведений.ТаймлистДанныеРаботыСервиса;
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	ТаблицаКОбновления = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Для Каждого СтрокаКОбновлению Из ТаблицаКОбновления Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных();
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ТаймлистДанныеРаботыСервиса");
			ЭлементБлокировки.УстановитьЗначение("ВерсияФайла", СтрокаКОбновлению.ВерсияФайла);
			Блокировка.Заблокировать();
			
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ВерсияФайла.Установить(СтрокаКОбновлению.ВерсияФайла);
			НаборЗаписей.Прочитать();
			
			ОбработкаДанных_1_0_1_3(НаборЗаписей);
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				МетаданныеОбъекта,
				СтрокаКОбновлению.ВерсияФайла,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Не удалось обработать некоторые объекты (пропущены): %1'"), 
			ПроблемныхОбъектов);
	Иначе
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			МетаданныеОбъекта,,
			СтрШаблон(
				НСтр("ru = 'Обработана очередная порция: %1'"),
				ОбъектовОбработано));
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = ОбъектовОбработано;
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
		Параметры.Очередь,
		ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОписаниеДанных()
	
	НовыйОписаниеДанных = Новый Структура;
	НовыйОписаниеДанных.Вставить("Автопротокол");
	НовыйОписаниеДанных.Вставить("ИдентификаторАвтопротокола");
	НовыйОписаниеДанных.Вставить("ИдентификаторЗадания");
	НовыйОписаниеДанных.Вставить("Расшифровка");
	НовыйОписаниеДанных.Вставить("Редактирует");
	НовыйОписаниеДанных.Вставить("СледующиеШаги");
	НовыйОписаниеДанных.Вставить("Спикеры");
	НовыйОписаниеДанных.Вставить("Статус");
	
	Возврат НовыйОписаниеДанных;
	
КонецФункции

// Удаляет тег команды-гиперссылки "TimelistGetAutoprotocol" из расшифровки.
//
Процедура ОбработкаДанных_1_0_1_3(НаборЗаписей)
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запись = НаборЗаписей[0];
	
	РасшифровкаHTML = Запись.Расшифровка.Получить();
	ДокументHTML = РаботаС_HTML.ПолучитьДокументHTMLИзТекстаHTML(РасшифровкаHTML);
	
	Если РасшифровкаHTML = Неопределено
		Или ДокументHTML.Тело.ДочерниеУзлы[0].ТекстовоеСодержимое <> "TimelistGetAutoprotocol" Тогда
		Возврат;
	КонецЕсли;
	
	ДокументHTML.Тело.УдалитьДочерний(ДокументHTML.Тело.ДочерниеУзлы[0]);
	ДокументHTML.Тело.УдалитьДочерний(ДокументHTML.Тело.ДочерниеУзлы[0]);
	
	Результат = РаботаС_HTML.ПолучитьТекстHTMLИзОбъектаДокументHTML(ДокументHTML);
	Запись.Расшифровка = Новый ХранилищеЗначения(Результат);
	
	АвтопротоколHTML = Запись.Автопротокол.Получить();
	
	Если АвтопротоколHTML <> Неопределено Тогда
		
		ДокументHTML = РаботаС_HTML.ПолучитьДокументHTMLИзТекстаHTML(АвтопротоколHTML);
		ДокументHTML.Тело.УдалитьДочерний(ДокументHTML.Тело.ДочерниеУзлы[0]);
		
		Результат = РаботаС_HTML.ПолучитьТекстHTMLИзОбъектаДокументHTML(ДокументHTML);
		Запись.Автопротокол = Новый ХранилищеЗначения(Результат);
		
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные(НаборЗаписей);
	
КонецПроцедуры

Функция ДанныеУведомления(ВерсияФайла)
	
	НовыеДанныеУведомления = Новый Структура("Файл, ВладелецФайла");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Файлы.Ссылка КАК Файл,
	|	Файлы.ВладелецФайла КАК ВладелецФайла
	|ИЗ
	|	Справочник.Файлы КАК Файлы
	|ГДЕ
	|	Файлы.ТекущаяВерсия = &ВерсияФайла";
	Запрос.УстановитьПараметр("ВерсияФайла", ВерсияФайла);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Не Выборка.Следующий() Тогда
		Возврат НовыеДанныеУведомления;
	КонецЕсли;
	
	НовыеДанныеУведомления.Файл = Выборка.Файл;
	НовыеДанныеУведомления.ВладелецФайла = Выборка.ВладелецФайла;
	
	Возврат НовыеДанныеУведомления;
	
КонецФункции

#КонецОбласти

#КонецЕсли