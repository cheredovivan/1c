#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает доступные текущему пользователю приглашения.
//
// Параметры:
//  Мероприятие - СправочникСсылка.Мероприятия - Мероприятие.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Доступные приглашения.
//
Функция ДоступныеПриглашения(Мероприятие) Экспорт
	
	ДоступныеПриглашения = Новый ТаблицаЗначений;
	ДоступныеПриглашения.Колонки.Добавить("Исполнитель");
	ДоступныеПриглашения.Колонки.Добавить("ОтКогоДелегирование");
	ДоступныеПриглашения.Колонки.Добавить("Представление");
	ДоступныеПриглашения.Колонки.Добавить("СостояниеПриглашения");
	Если Не ЗначениеЗаполнено(Мероприятие) Тогда
		Возврат ДоступныеПриглашения;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	УчастникиМероприятия.Исполнитель КАК Исполнитель,
		|	ВЫБОР
		|		КОГДА УчастникиМероприятия.Исполнитель ССЫЛКА Справочник.ПолныеРоли
		|			ТОГДА ЕСТЬNULL(ЗамещающиеИПомощники.Сотрудник, ИсполнителиРолейИДелегаты.ИсполнительДелегат)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
		|	КОНЕЦ КАК ОтКогоДелегирование,
		|	УчастникиМероприятия.СостояниеПриглашения КАК СостояниеПриглашения
		|ИЗ
		|	РегистрСведений.УчастникиМероприятия КАК УчастникиМероприятия
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсполнителиРолейИДелегаты КАК ИсполнителиРолейИДелегаты
		|		ПО УчастникиМероприятия.Исполнитель = ИсполнителиРолейИДелегаты.РольСотрудник
		|		И ИсполнителиРолейИДелегаты.ИсполнительДелегат В (&СотрудникиПользователя)
		|		И ИсполнителиРолейИДелегаты.ИмяОбластиДелегирования В ("""", ""Мероприятия"")
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗамещающиеИПомощники КАК ЗамещающиеИПомощники
		|		ПО ИсполнителиРолейИДелегаты.НастройкаДелегирования = ЗамещающиеИПомощники.Ссылка
		|ГДЕ
		|	УчастникиМероприятия.Мероприятие = &Мероприятие
		|
		|УПОРЯДОЧИТЬ ПО
		|	Исполнитель,
		|	ОтКогоДелегирование");
	
	Запрос.УстановитьПараметр("Мероприятие", Мероприятие);
	
	Запрос.УстановитьПараметр("СотрудникиПользователя", Сотрудники.СотрудникиПользователя());
	
	ДоступныеПриглашения = Запрос.Выполнить().Выгрузить();
	
	ДоступныеПриглашения.Колонки.Добавить("Представление");
	
	Для Каждого ДоступноеПриглашение Из ДоступныеПриглашения Цикл
		
		Если Не ЗначениеЗаполнено(ДоступноеПриглашение.ОтКогоДелегирование) Тогда
			ДоступноеПриглашение.Представление = Строка(ДоступноеПриглашение.Исполнитель);
		Иначе
			ДоступноеПриглашение.Представление = СтрШаблон(
				"%1 (%2)",
				Строка(ДоступноеПриглашение.Исполнитель),
				Строка(ДоступноеПриглашение.ОтКогоДелегирование));
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДоступныеПриглашения;
	
КонецФункции

// Изменяет состояние приглашения на мероприятие.
//
// Параметры:
//  Мероприятие - СправочникСсылка.Мероприятия - Мероприятие.
//  Исполнитель - СправочникСсылка.ПолныеРоли,
//                СправочникСсылка.Пользователи,
//                СправочникСсылка.Сотрудники - Исполнитель.
//  СостояниеПриглашения - ПеречислениеСсылка.СостоянияПриглашения - Состояние приглашения.
//  ОтКогоДелегирование - СправочникСсылка.Сотрудники.
//
Процедура ИзменитьСостояниеПриглашения(
	Мероприятие,
	Исполнитель,
	СостояниеПриглашения,
	ОтКогоДелегирование = Неопределено) Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		Если Не ПривилегированныйРежим() Тогда
			ДоступныеПриглашения = ДоступныеПриглашения(Мероприятие);
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("Исполнитель", Исполнитель);
			ПараметрыОтбора.Вставить("ОтКогоДелегирование", ОтКогоДелегирование);
			НайденныеСтроки = ДоступныеПриглашения.НайтиСтроки(ПараметрыОтбора);
			Если НайденныеСтроки.Количество() = 0 Тогда
				ПредставлениеУчастника = Строка(Исполнитель);
				ТекстОшибки = СтрШаблон(
					НСтр("ru = 'Недостаточно прав для изменения состояние приглашения участника %1 на мероприятие %2.'"),
					ПредставлениеУчастника,
					Мероприятие);
				ВызватьИсключение ТекстОшибки;
			КонецЕсли;
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
		
		УстановитьСостояниеПриглашения(
			Мероприятие,
			Исполнитель,
			СостояниеПриглашения,
			Неопределено,
			ОтКогоДелегирование,
			Истина);
		
		УправлениеМероприятиями.ОбновитьСостояниеПриглашений(Мероприятие);
		
		Если СостояниеПриглашения = Перечисления.СостоянияПриглашения.Принято Тогда
			
			РаботаСУведомлениями.ОбработатьСобытиеУведомления(
				Мероприятие,
				Перечисления.СобытияУведомлений.МероприятияПринятоПриглашение,
				ПользователиДокументооборот.ОлицетворяемыйПользователь(),
				Новый ХранилищеЗначения(Исполнитель));
			
		ИначеЕсли СостояниеПриглашения = Перечисления.СостоянияПриглашения.НеПринято Тогда
			
			РаботаСУведомлениями.ОбработатьСобытиеУведомления(
				Мероприятие,
				Перечисления.СобытияУведомлений.МероприятияОтклоненоПриглашение,
				ПользователиДокументооборот.ОлицетворяемыйПользователь(),
				Новый ХранилищеЗначения(Исполнитель));
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Проверяет, является ли переданный исполнитель участником мероприятия.
//
// Параметры:
//  Мероприятие - СправочникСсылка.Мероприятие - Мероприятие.
//  Исполнитель - СправочникСсылка.Контрагенты,
//                СправочникСсылка.ЛичныеАдресаты,
//                СправочникСсылка.ПолныеРоли,
//                СправочникСсылка.Пользователи,
//                СправочникСсылка.Сотрудники,
//                СправочникСсылка.КонтактныеЛица - Исполнитель.
// 
// Возвращаемое значение:
//  Булево - Исполнитель является участником.
//
Функция ЯвляетсяУчастникомМероприятия(Мероприятие, Исполнитель) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	УчастникиМероприятия.Исполнитель
		|ИЗ
		|	РегистрСведений.УчастникиМероприятия КАК УчастникиМероприятия
		|ГДЕ
		|	УчастникиМероприятия.Мероприятие = &Мероприятие
		|	И УчастникиМероприятия.Исполнитель = &Исполнитель");
	
	Запрос.УстановитьПараметр("Мероприятие", Мероприятие);
	Запрос.УстановитьПараметр("Исполнитель", Исполнитель);
	
	ЯвляетсяУчастникомМероприятия = Не Запрос.Выполнить().Пустой();
	
	Возврат ЯвляетсяУчастникомМероприятия;
	
КонецФункции

// Устанавливает состояние приглашения на мероприятие.
//
// Параметры:
//  Мероприятие - СправочникСсылка.Мероприятия
//  Исполнитель - СправочникСсылка.ПолныеРоли
//              - СправочникСсылка.Пользователи
//              - СправочникСсылка.Сотрудники
//  СостояниеПриглашения - ПеречислениеСсылка.СостоянияПриглашения
//  КонтрольнаяСуммаПриглашения - Строка
//  ОтКогоДелегирование - СправочникСсылка.Сотрудники
//  ОтключитьОповещения - Булево
//  ДополнительныеПараметры - Структура
//                          - Неопределено
//
Процедура УстановитьСостояниеПриглашения(
	Мероприятие,
	Исполнитель,
	СостояниеПриглашения,
	КонтрольнаяСуммаПриглашения = Неопределено,
	ОтКогоДелегирование = Неопределено,
	ОтключитьОповещения = Ложь,
	ДополнительныеПараметры = Неопределено) Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		ЭтоРолевоеПриглашение =
			ТипЗнч(Исполнитель) = Тип("СправочникСсылка.ПолныеРоли") И ЗначениеЗаполнено(ОтКогоДелегирование);
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.УчастникиМероприятия");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Мероприятие", Мероприятие);
		ЭлементБлокировки.УстановитьЗначение("Исполнитель", Исполнитель);
		Если ЭтоРолевоеПриглашение Тогда
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.УчастникиМероприятия");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Мероприятие", Мероприятие);
			ЭлементБлокировки.УстановитьЗначение("Исполнитель", ОтКогоДелегирование);
		КонецЕсли;
		Блокировка.Заблокировать();
		
		ЗначенияКлюча = Новый Структура;
		ЗначенияКлюча.Вставить("Мероприятие", Мероприятие);
		ЗначенияКлюча.Вставить("Исполнитель", Исполнитель);
		КлючЗаписи = СоздатьКлючЗаписи(ЗначенияКлюча);
		ЗаблокироватьДанныеДляРедактирования(КлючЗаписи);
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Мероприятие.Установить(Мероприятие);
		НаборЗаписей.Отбор.Исполнитель.Установить(Исполнитель);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда
			ПредставлениеУчастника = Строка(Исполнитель);
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'Участник %1 мероприятия %2 не найден.'"),
				ПредставлениеУчастника,
				Мероприятие);
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
		
		Для Каждого Запись Из НаборЗаписей Цикл
			Запись.СостояниеПриглашения = СостояниеПриглашения;
			Если КонтрольнаяСуммаПриглашения <> Неопределено Тогда
				Запись.КонтрольнаяСуммаПриглашения = КонтрольнаяСуммаПриглашения;
			КонецЕсли;
			Если ЭтоРолевоеПриглашение Тогда
				Запись.Исполнитель = ОтКогоДелегирование;
			КонецЕсли;
		КонецЦикла;
		Если Не ЭтоРолевоеПриглашение Тогда
			НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьОпределениеЗависимыхПрав", Истина);
			НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьРасширениеРабочейГруппыМероприятия", Истина);
			НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьОбновлениеДанныхМероприятия", Истина);
			НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьАвтообновлениеОбсуждений", Истина);
			Если ДополнительныеПараметры <> Неопределено Тогда
				НаборЗаписей.ДополнительныеСвойства.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
			КонецЕсли;
			НаборЗаписей.Записать();
		Иначе
			НаборЗаписейРолевой = СоздатьНаборЗаписей();
			НаборЗаписейРолевой.Отбор.Мероприятие.Установить(Мероприятие);
			НаборЗаписейРолевой.Отбор.Исполнитель.Установить(ОтКогоДелегирование);
			Для Каждого Запись Из НаборЗаписей Цикл
				ЗаписьРолевая = НаборЗаписейРолевой.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьРолевая, Запись);
			КонецЦикла;
			// Дополнительные свойства не переносим, чтобы отработала стандартная логика при смене участника.
			НаборЗаписей.Очистить();
			НаборЗаписей.Записать();
			НаборЗаписейРолевой.Записать();
		КонецЕсли;
		
		РаботаСРабочимКалендаремСервер.ОбновитьЗаписиКалендаряПоПредмету(
			Мероприятие,
			ОтключитьОповещения,
			Исполнитель,
			ДополнительныеПараметры);
		Если ЭтоРолевоеПриглашение Тогда
			РаботаСРабочимКалендаремСервер.ОбновитьЗаписиКалендаряПоПредмету(
				Мероприятие,
				ОтключитьОповещения,
				ОтКогоДелегирование,
				ДополнительныеПараметры);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли