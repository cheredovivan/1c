#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ВидДокумента) Тогда
		Заголовок = СтрШаблон(НСтр(
			"ru = 'Настройки доступности по состоянию для вида документа ""%1""'"),
			Параметры.ВидДокумента);
		ВидыДокументов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Параметры.ВидДокумента);

	ИначеЕсли ЗначениеЗаполнено(Параметры.ВидДействия) Тогда
		Заголовок = СтрШаблон(НСтр(
			"ru = 'Настройки доступности по состоянию для вида действия ""%1""'"),
			Параметры.ВидДействия);
		ВидыДокументов = Справочники.НастройкиОбработкиВидовОбъектов.ВидыОбъектовПоВидуДействия(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Параметры.ВидДействия));
	Иначе
		ВидыДокументов = Новый Массив;

	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НастройкиДоступностиПоСостоянию.Ссылка КАК НастройкаДоступностиПоСостоянию
		|ИЗ
		|	Справочник.НастройкиДоступностиПоСостоянию КАК НастройкиДоступностиПоСостоянию
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиДоступностиДляВидовДокументов КАК
		|			РегистрСведенийНастройкиДоступностиДляВидовДокументов
		|		ПО НастройкиДоступностиПоСостоянию.Ссылка = РегистрСведенийНастройкиДоступностиДляВидовДокументов.НастройкаДоступностиПоСостоянию
		|ГДЕ
		|	НЕ НастройкиДоступностиПоСостоянию.ПометкаУдаления
		|	И НастройкиДоступностиПоСостоянию.ТипДокумента = &ТипДокумента
		|	И РегистрСведенийНастройкиДоступностиДляВидовДокументов.НастройкаДоступностиПоСостоянию ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РегистрСведенийНастройкиДоступностиДляВидовДокументов.НастройкаДоступностиПоСостоянию
		|ИЗ
		|	РегистрСведений.НастройкиДоступностиДляВидовДокументов КАК РегистрСведенийНастройкиДоступностиДляВидовДокументов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиДоступностиПоСостоянию КАК НастройкиДоступностиПоСостоянию
		|		ПО РегистрСведенийНастройкиДоступностиДляВидовДокументов.НастройкаДоступностиПоСостоянию = НастройкиДоступностиПоСостоянию.Ссылка
		|		И Не НастройкиДоступностиПоСостоянию.ПометкаУдаления И НастройкиДоступностиПоСостоянию.ТипДокумента = &ТипДокумента
		|ГДЕ
		|	РегистрСведенийНастройкиДоступностиДляВидовДокументов.ВидДокумента В (&ВидДокумента)";
		
	Запрос.Параметры.Вставить("ТипДокумента", Перечисления.ТипыОбъектов.ДокументыПредприятия);
	Запрос.Параметры.Вставить("ВидДокумента", ВидыДокументов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		
		НоваяСтрока = СписокНастроек.Добавить();
		НоваяСтрока.НастройкаДоступностиПоСостоянию = Выборка.НастройкаДоступностиПоСостоянию;
		
		УчастникиДляСписка = Новый Массив;
		Для Каждого Строка Из Выборка.НастройкаДоступностиПоСостоянию.ИспользоватьДля Цикл 
			УчастникиДляСписка.Добавить(Строка.Участник);
		КонецЦикла;
		НоваяСтрока.Участники = СтрСоединить(УчастникиДляСписка, ", ");
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокНастроек

&НаКлиенте
Процедура СписокНастроекПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.СписокНастроек.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	Если ЗначениеЗаполнено(Параметры.ВидДокумента) Тогда 
		ПараметрыОткрытия.Вставить("ВидДокумента", Параметры.ВидДокумента);
	ИначеЕсли ЗначениеЗаполнено(Параметры.ВидДействия) Тогда
		ПараметрыОткрытия.Вставить("ВидДействия", Параметры.ВидДействия);
	КонецЕсли;
	ПараметрыОткрытия.Вставить("Ключ", ТекущиеДанные.НастройкаДоступностиПоСостоянию);
	
	КлючеваяОперация = ДелопроизводствоКлиентСервер.КлючеваяОперацияОткрытиеНДПС();
	ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
	ОткрытьФорму("Справочник.НастройкиДоступностиПоСостоянию.ФормаОбъекта", ПараметрыОткрытия);
	
КонецПроцедуры

#КонецОбласти