//@skip-check query-in-loop
// (В модуле сложная логика и обработка порциями, делающая невозможным полный отказ от запросов в цикле).

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Удаляет все данные регистра и заполняет заново.
// 
// Параметры:
//  Немедленно - Булево - Немедленно
//  ВТранзакции - Булево - В транзакции
Процедура ОбновитьВсеДанные(Немедленно = Ложь, ВТранзакции = Истина) Экспорт
	
	Если Немедленно <> Истина
		И ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() Тогда
		
		Модуль = РегистрыСведений.ОчередьОбновленияПравДоступа;
		Модуль.Добавить(
			Модуль.ПриоритетДолгая(),
			Перечисления.ЗаданияОчередиОбновленияПрав.ЗаполнитьСоставСубъектовПравДоступа,
			'00010101');
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ВТранзакции Тогда
		НачатьТранзакцию();
	КонецЕсли;
	
	Попытка
		// Сотрудники
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Сотрудники.Ссылка КАК Субъект,
			|	Сотрудники.Ссылка КАК Сотрудник,
			|	ЕСТЬNULL(СотрудникиПользователей.Пользователь, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) КАК Пользователь,
			|	Сотрудники.ПометкаУдаления КАК СубъектПометкаУдаления,
			|	Сотрудники.ПометкаУдаления КАК СотрудникПометкаУдаления
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|		ПО СотрудникиПользователей.Сотрудник = Сотрудники.Ссылка");
		ЗаписатьТаблицуПорциями(Запрос.Выполнить().Выгрузить(), Новый Структура(), Истина);
		
		// Роли исполнителей.
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ИсполнителиРолей.РольИсполнителя КАК Субъект,
			|	ИсполнителиРолей.Исполнитель КАК Сотрудник,
			|	ЕСТЬNULL(СотрудникиПользователей.Пользователь, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) КАК Пользователь,
			|	ИсполнителиРолей.РольИсполнителя.ПометкаУдаления КАК СубъектПометкаУдаления,
			|	ИсполнителиРолей.Исполнитель.ПометкаУдаления КАК СотрудникПометкаУдаления
			|ИЗ
			|	РегистрСведений.ИсполнителиРолей КАК ИсполнителиРолей
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|		ПО СотрудникиПользователей.Сотрудник = ИсполнителиРолей.Исполнитель
			|ГДЕ
			|	ИсполнителиРолей.РольИсполнителя <> ЗНАЧЕНИЕ(Справочник.ПолныеРоли.ПустаяСсылка)");
		ЗаписатьТаблицуПорциями(Запрос.Выполнить().Выгрузить(), Новый Структура(), Ложь);
		
		// Проекты
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Проекты.Ссылка КАК Субъект,
			|	Проекты.Руководитель КАК Сотрудник,
			|	ЕСТЬNULL(СотрудникиПользователей.Пользователь, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) КАК Пользователь,
			|	Проекты.ПометкаУдаления КАК СубъектПометкаУдаления,
			|	Сотрудники.ПометкаУдаления КАК СотрудникПометкаУдаления
			|ИЗ
			|	Справочник.Проекты КАК Проекты
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	Справочник.Сотрудники КАК Сотрудники
			|		ПО Проекты.Руководитель = Сотрудники.Ссылка
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|		ПО СотрудникиПользователей.Сотрудник = Проекты.Руководитель");
		ЗаписатьТаблицуПорциями(Запрос.Выполнить().Выгрузить(), Новый Структура(), Ложь);
		
		// Руководители
		Если РаботаСЗадачамиПовтИсп.ДобавлятьРуководителямДоступПодчиненных() Тогда
			ЗаполнитьВсехРуководителей();
		КонецЕсли;
		
		// Делегаты
		ЗаполнитьВсехДелегатов();
		
		Если ВТранзакции Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
		
	Исключение
		
		Если ВТранзакции Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Перезаполняет данные по всем руководителям подразделений.
// 
// Параметры:
//	Подразделения - Массив Из СправочникСсылка.СтруктураПредприятия -
//				  - Неопределено - Если не указаны, то по всем.
Процедура ЗаполнитьВсехРуководителей(Знач Подразделения = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	УдалитьВсехРуководителей(Подразделения);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Субъект,
		|	ПодчиненностьПодразделений.РуководительВышестоящего КАК Сотрудник,
		|	ЕСТЬNULL(СотрудникиПользователей.Пользователь, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) КАК Пользователь,
		|	ПодчиненностьПодразделений.Вышестоящее КАК ОбъектОснование,
		|	Сотрудники.ПометкаУдаления КАК СубъектПометкаУдаления,
		|	ПодчиненностьПодразделений.РуководительВышестоящего.ПометкаУдаления КАК СотрудникПометкаУдаления
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ПодчиненностьПодразделений КАК ПодчиненностьПодразделений
		|		ПО Сотрудники.Подразделение = ПодчиненностьПодразделений.Подчиненное
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
		|		ПО СотрудникиПользователей.Сотрудник = ПодчиненностьПодразделений.РуководительВышестоящего
		|ГДЕ
		|	ПодчиненностьПодразделений.РуководительВышестоящего <> ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
		|	И ПодчиненностьПодразделений.Вышестоящее В ИЕРАРХИИ (&Подразделения)
		|ИТОГИ
		|ПО
		|	ОбъектОснование");
	
	Если Подразделения = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"И ПодчиненностьПодразделений.Вышестоящее В ИЕРАРХИИ (&Подразделения)", "")
	КонецЕсли;
	Запрос.УстановитьПараметр("Подразделения", Подразделения);
	Запрос.УстановитьПараметр("ПустыеСсылкиПользователя", Сотрудники.МассивПустыхСсылокПользователя());
	
	ВыборкаПоПодразделениям = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоПодразделениям.Следующий() Цикл
		
		СоставСубъектов = СоздатьНаборЗаписей();
		ТаблицаНабора = СоставСубъектов.ВыгрузитьКолонки();
		
		Выборка = ВыборкаПоПодразделениям.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаНабора.Добавить(), Выборка);
		КонецЦикла;
		
		РасширитьТаблицуНабораСоставомСубъектовНижнихУровней(ТаблицаНабора, Ложь);
		ЗаписатьТаблицуПорциями(
			ТаблицаНабора, Новый Структура("ОбъектОснование", ВыборкаПоПодразделениям.ОбъектОснование), Истина);
		РасширитьСоставСубъектовВерхнихУровней(ТаблицаНабора, Ложь);
		
	КонецЦикла;
	
КонецПроцедуры

// Удаляет всех руководителей из состава субъектов-пользователей.
// 
// Параметры:
//	Подразделения - Массив Из СправочникСсылка.СтруктураПредприятия -
//				  - Неопределено - Если не указаны, то по всем.
Процедура УдалитьВсехРуководителей(Подразделения = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СоставСубъектовПравДоступа.ОбъектОснование
		|ИЗ
		|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектовПравДоступа
		|ГДЕ
		|	СоставСубъектовПравДоступа.ОбъектОснование ССЫЛКА Справочник.СтруктураПредприятия
		|	И СоставСубъектовПравДоступа.ОбъектОснование В ИЕРАРХИИ (&Подразделения)");
	
	Если Подразделения = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"И СоставСубъектовПравДоступа.ОбъектОснование В ИЕРАРХИИ (&Подразделения)", "")
	КонецЕсли;
	Запрос.УстановитьПараметр("Подразделения", Подразделения);
	ВыборкаПоПодразделениям = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаПоПодразделениям.Следующий() Цикл
		СоставСубъектов = СоздатьНаборЗаписей();
		СоставСубъектов.Отбор.ОбъектОснование.Установить(ВыборкаПоПодразделениям.ОбъектОснование);
		СоставСубъектов.Записать();
	КонецЦикла;
	
	УдалитьНеактуальныеЗаписиВерхнихУровней();
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

#Область ОбработчикиОбновления

// Монопольный обработчик обновления, заполняет регистр для перехода на новый формат прав доступа.
Процедура ЗаполнитьПользователяДляПереходаНаНовуюВерсию() Экспорт
	
	// Обработчик монопольный, пишем как можно быстрее, без установок блокировок.
	
	// Оббегаем пары сотрудник + субъект
	// Меняем только те записи, где у Сотрудника есть Пользователь, остальные оставляем с пустым пользователем:
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РегистрПрав.Сотрудник,
		|	РегистрПрав.Субъект,
		|	МАКСИМУМ(СотрудникиПользователей.Пользователь) КАК Пользователь
		|ИЗ
		|	РегистрСведений.СоставСубъектовПравДоступа КАК РегистрПрав
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
		|		ПО СотрудникиПользователей.Сотрудник = РегистрПрав.Сотрудник
		|ГДЕ
		|	РегистрПрав.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	РегистрПрав.Сотрудник,
		|	РегистрПрав.Субъект");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НадоЗаписать = Ложь;
		Набор = Неопределено;
		Набор = СоздатьНаборЗаписей();
		Набор.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
		Набор.Отбор.Субъект.Установить(Выборка.Субъект);
		Набор.Прочитать();
		
		Для Каждого Запись Из Набор Цикл
			Если ЗначениеЗаполнено(Запись.Пользователь) Тогда
				Продолжить; // Возможно, весь набор не придется перезаписывать.
			КонецЕсли;
			
			Запись.Пользователь = Выборка.Пользователь;
			НадоЗаписать = Истина;
		КонецЦикла;
		
		Если НадоЗаписать Тогда
			Набор.ДополнительныеСвойства.Вставить("ПропуститьОпределениеДескриптораДоступаИПроверкуПрав", Истина);
			Набор.ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов", Истина);
			ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные(Набор);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиОбновления

#КонецОбласти // ДляВызоваИзДругихПодсистем

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Отрабатывает изменения в регистре при изменении пометки удаления подразделения.
//
// Параметры:
//	Источник - СправочникОбъект.СтруктураПредприятия
//
Процедура ИзменениеПометкиУдаленияПодразделения(Источник) Экспорт
	
	Подразделения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Источник.Ссылка);
	Если Источник.ПометкаУдаления Тогда
		УдалитьВсехРуководителей(Подразделения);
	Иначе
		ЗаполнитьВсехРуководителей(Подразделения);
	КонецЕсли;
	
КонецПроцедуры

// Добавляет/удаляет руководителей в состав субъектов. При записи сотрудника.
//
// Параметры:
//	Источник - СправочникОбъект.Сотрудники
//
Процедура ЗаписьСотрудника(Источник) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПользовательСотрудника = Сотрудники.ПользовательСотрудника(Источник.Ссылка);
	ПользовательПометкаУдаления = Ложь;
	Если Значениезаполнено(ПользовательСотрудника) Тогда
		ПользовательПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ПользовательСотрудника, "ПометкаУдаления");
	КонецЕсли;
	
	// Запись по "самому себе". Если пользователя нет, то все равно нужна запись с сотрудником, но пустым пользователем:
	Запись = СоздатьМенеджерЗаписи();
	Запись.Субъект = Источник.Ссылка;
	Запись.Сотрудник = Источник.Ссылка;
	// Если пользователь помечен на удаление - запись все равно нужна по Сотруднику - пишем с пустым пользователем:
	Запись.Пользователь = ?(
		ПользовательПометкаУдаления, Справочники.Пользователи.ПустаяСсылка(), ПользовательСотрудника);
	Запись.СубъектПометкаУдаления = Источник.ПометкаУдаления;
	Запись.СотрудникПометкаУдаления = Источник.ПометкаУдаления;
	Запись.Записать();
	
	Если Не РаботаСЗадачамиПовтИсп.ДобавлятьРуководителямДоступПодчиненных() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СоставСубъектовПравДоступа.Субъект КАК Сотрудник,
		|	ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка) КАК Пользователь,
		|	СоставСубъектовПравДоступа.ОбъектОснование КАК Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Руководитель,
		|	ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка) КАК РуководительПользователь,
		|	ЛОЖЬ КАК Актуальна,
		|	СоставСубъектовПравДоступа.СотрудникПометкаУдаления,
		|	СоставСубъектовПравДоступа.СубъектПометкаУдаления
		|ИЗ
		|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектовПравДоступа
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	Справочник.Сотрудники КАК Сотрудники
		|		ПО СоставСубъектовПравДоступа.Субъект = Сотрудники.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ПодчиненностьПодразделений КАК ПодчиненностьПодразделений
		|		ПО (Сотрудники.Подразделение = ПодчиненностьПодразделений.Подчиненное)
		|		И СоставСубъектовПравДоступа.ОбъектОснование = ПодчиненностьПодразделений.Вышестоящее
		|ГДЕ
		|	СоставСубъектовПравДоступа.Субъект = &Сотрудник
		|	И СоставСубъектовПравДоступа.ОбъектОснование ССЫЛКА Справочник.СтруктураПредприятия
		|	И ПодчиненностьПодразделений.Подчиненное ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Сотрудники.Ссылка,
		|	&ПользовательСотрудника,
		|	ПодчиненностьПодразделений.Вышестоящее,
		|	ПодчиненностьПодразделений.РуководительВышестоящего,
		|	ЕСТЬNULL(СотрудникиПользователейРуководителей.Пользователь, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)),
		|	ИСТИНА,
		|	ПодчиненностьПодразделений.РуководительВышестоящего.ПометкаУдаления,
		|	Сотрудники.ПометкаУдаления
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ПодчиненностьПодразделений КАК ПодчиненностьПодразделений
		|		ПО Сотрудники.Подразделение = ПодчиненностьПодразделений.Подчиненное
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектовПравДоступа
		|		ПО (СоставСубъектовПравДоступа.Субъект = Сотрудники.Ссылка)
		|		И (СоставСубъектовПравДоступа.ОбъектОснование = ПодчиненностьПодразделений.Вышестоящее)
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователейРуководителей
		|		ПО СотрудникиПользователейРуководителей.Сотрудник = ПодчиненностьПодразделений.РуководительВышестоящего
		|	
		|ГДЕ
		|	Сотрудники.Ссылка = &Сотрудник
		|	И СоставСубъектовПравДоступа.Субъект ЕСТЬ NULL
		|	И Сотрудники.Ссылка <> ПодчиненностьПодразделений.РуководительВышестоящего
		|	И ПодчиненностьПодразделений.РуководительВышестоящего <> ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)");
	Запрос.УстановитьПараметр("Сотрудник", Источник.Ссылка);
	Запрос.УстановитьПараметр("ПользовательСотрудника", ПользовательСотрудника);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Актуальна Тогда
			
			СоставСубъектов = СоздатьНаборЗаписей();
			СоставСубъектов.Отбор.Субъект.Установить(Выборка.Сотрудник);
			СоставСубъектов.Отбор.ОбъектОснование.Установить(Выборка.Подразделение);
			
			Запись = СоставСубъектов.Добавить();
			Запись.Субъект = Выборка.Сотрудник;
			Запись.Сотрудник = Выборка.Руководитель;
			Запись.Пользователь = Выборка.Пользователь;
			Запись.ОбъектОснование = Выборка.Подразделение;
			Запись.СотрудникПометкаУдаления = Выборка.СотрудникПометкаУдаления;
			Запись.СубъектПометкаУдаления = Выборка.СубъектПометкаУдаления; 
			
			СоставСубъектов.Записать();
			
			// Записи верхнего уровня.
			ТаблицаНабора = СоставСубъектов.Выгрузить();
			РасширитьСоставСубъектовВерхнихУровней(ТаблицаНабора, Ложь);
			
			// Записи нижнего уровня.
			ЗапросНижнегоУровня = Новый Запрос(
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	СоставСубъектовПравДоступа.Субъект,
				|	&Руководитель КАК Сотрудник,
				|	&РуководительПользователь КАК Пользователь,
				|	СоставСубъектовПравДоступа.ОбъектМетаданных,
				|	&ОбъектОснование КАК ОбъектОснование,
				|	СоставСубъектовПравДоступа.Сотрудник КАК СотрудникОснование,
				|	СоставСубъектовПравДоступа.ОбластьДелегирования,
				|	СоставСубъектовПравДоступа.ИмяОбластиДелегирования,
				|	СоставСубъектовПравДоступа.Субъект.ПометкаУдаления КАК СубъектПометкаУдаления,
				|	&СотрудникПометкаУдаления КАК СотрудникПометкаУдаления
				|ИЗ
				|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектовПравДоступа
				|ГДЕ
				|	СоставСубъектовПравДоступа.Сотрудник = &Сотрудник
				|	И СоставСубъектовПравДоступа.Субъект <> СоставСубъектовПравДоступа.Сотрудник");
			
			ЗапросНижнегоУровня.УстановитьПараметр("Сотрудник", Выборка.Сотрудник);
			ЗапросНижнегоУровня.УстановитьПараметр("Руководитель", Выборка.Руководитель);
			ЗапросНижнегоУровня.УстановитьПараметр("РуководительПользователь", Выборка.РуководительПользователь);
			ЗапросНижнегоУровня.УстановитьПараметр("ОбъектОснование", Выборка.Подразделение);
			ЗапросНижнегоУровня.УстановитьПараметр("СотрудникПометкаУдаления", Выборка.СотрудникПометкаУдаления);
			
			НачальныйОтбор = Новый Структура();
			НачальныйОтбор.Вставить("Сотрудник", Выборка.Руководитель);
			НачальныйОтбор.Вставить("Пользователь", Выборка.РуководительПользователь);
			НачальныйОтбор.Вставить("ОбъектОснование", Выборка.Подразделение);
			НачальныйОтбор.Вставить("СотрудникОснование", Выборка.Сотрудник);
			ЗаписатьТаблицуПорциями(ЗапросНижнегоУровня.Выполнить().Выгрузить(), НачальныйОтбор, Истина);
			
		Иначе
			// Удаление неактуальных записей:
			НачатьТранзакцию();
			Попытка
				Блокировка = Новый БлокировкаДанных();
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СоставСубъектовПравДоступа");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.УстановитьЗначение("Субъект", Выборка.Сотрудник);
				ЭлементБлокировки.УстановитьЗначение("ОбъектОснование", Выборка.Подразделение);
				Блокировка.Заблокировать();
				
				СоставСубъектов = СоздатьНаборЗаписей();
				СоставСубъектов.Отбор.Субъект.Установить(Выборка.Сотрудник);
				СоставСубъектов.Отбор.ОбъектОснование.Установить(Выборка.Подразделение);
				СоставСубъектов.Записать();
				
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ВызватьИсключение;
			КонецПопытки;
		КонецЕсли;
		
	КонецЦикла;
	
	
	УдалитьНеактуальныеЗаписиВерхнихУровней();
	ОбновитьПометкиУдаленияПоСотруднику(Источник);
	
КонецПроцедуры

// Обновляет регистр при изменении пользователя
// 
// Параметры:
//  ДанныеПользователя - Структура - Данные из справочника Пользователи:
//	* Ссылка - СправочникСсылка.Пользователи -
//	* Недействителен - Булево -
//	* ПометкаУдаления - Булево -
Процедура ЗаписьПользователя(ДанныеПользователя) Экспорт
	
	СотрудникиПользователя = Сотрудники.СотрудникиПользователя(ДанныеПользователя.Ссылка);
	Для Каждого Сотрудник Из СотрудникиПользователя Цикл
		
		Набор = СоздатьНаборЗаписей();
		Набор.Отбор.Сотрудник.Установить(Сотрудник);
		Набор.Прочитать();
		ТаблицаНабора = Набор.Выгрузить();
		НадоЗаписать = Ложь;
		ПользовательНедействителен = (ДанныеПользователя.Недействителен Или ДанныеПользователя.ПометкаУдаления);
		Для Каждого СтрокаТаблицы Из ТаблицаНабора Цикл
			Если ЗначениеЗаполнено(СтрокаТаблицы.Пользователь) И ПользовательНедействителен Тогда
				СтрокаТаблицы.Пользователь = Справочники.Пользователи.ПустаяСсылка();
				НадоЗаписать = Истина;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.Пользователь) И Не ПользовательНедействителен Тогда
				СтрокаТаблицы.Пользователь = ДанныеПользователя.Ссылка;
				НадоЗаписать = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если НадоЗаписать Тогда
			ЗаписатьТаблицуПорциями(ТаблицаНабора, Новый Структура("Сотрудник", Сотрудник), Истина);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

// Добавляет/удаляет исполнителей ролей в состав субъектов.
//
// Параметры:
//	НаборИсточник - РегистрСведенийНаборЗаписей.ИсполнителиРолей
//
Процедура ЗаписьИсполнителейРолей(НаборИсточник) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЭтоУдаление = НаборИсточник.Количество() = 0;
	Роль = НаборИсточник.Отбор.РольИсполнителя.Значение;
	
	Если Не ЗначениеЗаполнено(Роль) Тогда
		Если ЭтоУдаление Тогда
			Возврат;
		Иначе
			ВызватьИсключение НСтр("ru = 'Не заполнен отбор по роли исполнителей'");
		КонецЕсли;
	КонецЕсли;
	
	СоставСубъектов = СоздатьНаборЗаписей();
	СоставСубъектов.Отбор.Субъект.Установить(Роль);
	СоставСубъектов.Отбор.ОбъектОснование.Установить(Неопределено);
	СоставСубъектов.Отбор.СотрудникОснование.Установить(Неопределено);
	
	ОтборИсточника = НаборИсточник.Отбор.Исполнитель;
	Если ОтборИсточника.Использование Тогда
		СоставСубъектов.Отбор.Сотрудник.Установить(ОтборИсточника.Значение);
	КонецЕсли;
	
	ТаблицаИсточник = НаборИсточник.Выгрузить();
	ТаблицаИсточник.Свернуть("Исполнитель");
	
	ИсполнителиИРоли = ТаблицаИсточник.ВыгрузитьКолонку("Исполнитель");
	ПользователиПоСотрудникам = Сотрудники.ПользователиПоСотрудникам(ИсполнителиИРоли);
	ИсполнителиИРоли.Добавить(Роль);
	ПометкиУдаления = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ИсполнителиИРоли, "ПометкаУдаления");
	
	Для Каждого СтрИсточника Из ТаблицаИсточник Цикл
		Запись = СоставСубъектов.Добавить();
		Запись.Субъект = Роль;
		Запись.Сотрудник = СтрИсточника.Исполнитель;
		
		ПользователиСотрудника = ПользователиПоСотрудникам[Запись.Сотрудник];
		Если ПользователиСотрудника.Количество() > 0 Тогда
			Запись.Пользователь = ПользователиСотрудника[0];
		КонецЕсли;
		
		Если ПометкиУдаления[Запись.Субъект] <> Неопределено Тогда
			Запись.СубъектПометкаУдаления = ПометкиУдаления[Запись.Субъект].ПометкаУдаления;
		КонецЕсли;
		Если ПометкиУдаления[Запись.Сотрудник] <> Неопределено Тогда
			Запись.СотрудникПометкаУдаления = ПометкиУдаления[Запись.Сотрудник].ПометкаУдаления;
		КонецЕсли;
	КонецЦикла;
	
	СоставСубъектов.Записать();
	ТаблицаНабора = СоставСубъектов.Выгрузить();
	РасширитьСоставСубъектовВерхнихУровней(ТаблицаНабора);
	
	УдалитьНеактуальныеЗаписиВерхнихУровней();
	
КонецПроцедуры

// Вызывается при записи замещающих и помощников.
// Добавляет/удаляет делегата как строку состава субъекта.
// 
// Параметры:
//  Делегирование - СправочникСсылка.ЗамещающиеИПомощники
//
Процедура ЗаписьЗамещения(Знач Делегирование) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	РеквизитыЗамещения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Делегирование, "Действует, Сотрудник, Замещающий, ВопросыЗамещения");
	ПользовательЗамещающего = Сотрудники.ПользовательСотрудника(РеквизитыЗамещения.Замещающий);
	
	СоставСубъектов = СоздатьНаборЗаписей();
	
	Если РеквизитыЗамещения.Действует Тогда
		
		ТаблицаНабора = СоставСубъектов.ВыгрузитьКолонки();
		ВопросыЗамещения = РеквизитыЗамещения.ВопросыЗамещения.Выгрузить();
		
		Если ВопросыЗамещения.Найти(Справочники.ОбластиЗамещения.ВсеОбласти) <> Неопределено Тогда
			
			СтрокаТЗ = ТаблицаНабора.Добавить();
			СтрокаТЗ.Субъект = РеквизитыЗамещения.Сотрудник;
			СтрокаТЗ.Сотрудник = РеквизитыЗамещения.Замещающий;
			СтрокаТЗ.Пользователь = ПользовательЗамещающего;
			СтрокаТЗ.ОбъектОснование = Делегирование;
			
		Иначе
			
			Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	ЗамещающиеИПомощники.Замещающий КАК Сотрудник,
				|	&ПользовательЗамещающего КАК Пользователь,
				|	ЗамещающиеИПомощники.Сотрудник КАК Субъект,
				|	ОбластиЗамещения.ИмяПредопределенныхДанных КАК ИмяОбластиДелегирования,
				|	ОбластиЗамещенияСостав.ОбъектМетаданных КАК ОбъектМетаданных,
				|	ЗамещающиеИПомощники.Ссылка КАК ОбъектОснование,
				|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК СотрудникОснование,
				|	ЗамещающиеИПомощникиВопросыЗамещения.Область КАК ОбластьДелегирования,
				|	ЗамещающиеИПомощники.Замещающий.ПометкаУдаления КАК СотрудникПометкаУдаления,
				|	ЗамещающиеИПомощники.Сотрудник.ПометкаУдаления КАК СубъектПометкаУдаления
				|ИЗ
				|	Справочник.ЗамещающиеИПомощники КАК ЗамещающиеИПомощники
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗамещающиеИПомощники.ВопросыЗамещения КАК ЗамещающиеИПомощникиВопросыЗамещения
				|		ПО ЗамещающиеИПомощники.Ссылка = ЗамещающиеИПомощникиВопросыЗамещения.Ссылка
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбластиЗамещения.Состав КАК ОбластиЗамещенияСостав
				|		ПО ЗамещающиеИПомощникиВопросыЗамещения.Область = ОбластиЗамещенияСостав.Ссылка
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
				|		ПО ОбластиЗамещенияСостав.ОбъектМетаданных = ИдентификаторыОбъектовМетаданных.Ссылка
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбластиЗамещения КАК ОбластиЗамещения
				|		ПО ОбластиЗамещенияСостав.Ссылка = ОбластиЗамещения.Ссылка
				|ГДЕ
				|	ЗамещающиеИПомощники.Ссылка = &Делегирование
				|	И НЕ ОбластиЗамещения.ПометкаУдаления
				|	И НЕ ИдентификаторыОбъектовМетаданных.ПометкаУдаления");
			Запрос.УстановитьПараметр("Делегирование", Делегирование);
			Запрос.УстановитьПараметр("ПользовательЗамещающего", ПользовательЗамещающего);
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ТаблицаНабора.Добавить(), Выборка);
			КонецЦикла;
			
		КонецЕсли;
		
		РасширитьТаблицуНабораСоставомСубъектовНижнихУровней(ТаблицаНабора, Истина);
		
		ЗаписатьТаблицуПорциями(ТаблицаНабора, Новый Структура("ОбъектОснование", Делегирование), Истина);
		
	Иначе
		// Не действует - очистка всех записей по замещению:
		СоставСубъектов.Отбор.ОбъектОснование.Установить(Делегирование);
		СоставСубъектов.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при записи области делегирования.
// Актуализирует состав строки субъектов, связанных с переданной областью.
// 
// Параметры:
//  ОбластьЗамещения - СправочникОбъект.ОбластиЗамещения
//
Процедура ЗаписьОбластиЗамещения(Знач ОбластьЗамещения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СоставСубъектовПравДоступа.Субъект,
		|	СоставСубъектовПравДоступа.Сотрудник,
		|	СоставСубъектовПравДоступа.Пользователь,
		|	СоставСубъектовПравДоступа.ОбъектОснование,
		|	СоставСубъектовПравДоступа.СотрудникОснование,
		|	СоставСубъектовПравДоступа.ОбластьДелегирования,
		|	СоставСубъектовПравДоступа.ИмяОбластиДелегирования,
		|	СоставСубъектовПравДоступа.СубъектПометкаУдаления,
		|	СоставСубъектовПравДоступа.СотрудникПометкаУдаления
		|ПОМЕСТИТЬ ЗаписиПоОбластиБезРасшифровки
		|ИЗ
		|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектовПравДоступа
		|ГДЕ
		|	СоставСубъектовПравДоступа.ОбластьДелегирования = &ОбластьДелегирования
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаписиПоОбластиБезРасшифровки.Субъект,
		|	ЗаписиПоОбластиБезРасшифровки.Сотрудник,
		|	ЗаписиПоОбластиБезРасшифровки.Пользователь КАК Пользователь,
		|	ЗаписиПоОбластиБезРасшифровки.ОбъектОснование КАК ОбъектОснование,
		|	ЗаписиПоОбластиБезРасшифровки.СотрудникОснование,
		|	ЗаписиПоОбластиБезРасшифровки.ОбластьДелегирования,
		|	ЗаписиПоОбластиБезРасшифровки.ИмяОбластиДелегирования,
		|	ИдентификаторыОбъектовМетаданных.Ссылка КАК ОбъектМетаданных,
		|	ЗаписиПоОбластиБезРасшифровки.СубъектПометкаУдаления,
		|	ЗаписиПоОбластиБезРасшифровки.СотрудникПометкаУдаления
		|ИЗ
		|	ЗаписиПоОбластиБезРасшифровки КАК ЗаписиПоОбластиБезРасшифровки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбластиЗамещения.Состав КАК ОбластиЗамещенияСостав
		|		ПО ЗаписиПоОбластиБезРасшифровки.ОбластьДелегирования = ОбластиЗамещенияСостав.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
		|		ПО ОбластиЗамещенияСостав.ОбъектМетаданных = ИдентификаторыОбъектовМетаданных.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбластиЗамещения КАК ОбластиЗамещения
		|		ПО ОбластиЗамещенияСостав.Ссылка = ОбластиЗамещения.Ссылка
		|ГДЕ
		|	ЗаписиПоОбластиБезРасшифровки.ОбластьДелегирования = &ОбластьДелегирования
		|	И НЕ ОбластиЗамещения.ПометкаУдаления
		|	И НЕ ИдентификаторыОбъектовМетаданных.ПометкаУдаления
		|ИТОГИ
		|ПО
		|	ОбъектОснование");
		
	Запрос.УстановитьПараметр("ОбластьДелегирования", ОбластьЗамещения.Ссылка);
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		Если ОбластьЗамещения.ПометкаУдаления Тогда
			
			СоставСубъектов = СоздатьНаборЗаписей();
			СоставСубъектов.Отбор.ОбластьДелегирования.Установить(ОбластьЗамещения.Ссылка);
			СоставСубъектов.Записать();
			
		Иначе
			
			// Если записей по этой области в базе нет - выполняется поиск элементов делегирования, 
			// использующих область, и перезаполняются данные регистра по этим элементам.
			
			Запрос = Новый Запрос(
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	ЗамещающиеИПомощникиВопросыЗамещения.Ссылка КАК Замещение
				|ИЗ
				|	Справочник.ЗамещающиеИПомощники.ВопросыЗамещения КАК ЗамещающиеИПомощникиВопросыЗамещения
				|ГДЕ
				|	ЗамещающиеИПомощникиВопросыЗамещения.Область = &ОбластьДелегирования");
				
			Запрос.УстановитьПараметр("ОбластьДелегирования", ОбластьЗамещения.Ссылка);
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				ЗаписьЗамещения(Выборка.Замещение);
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		
		ВыборкаПоДелегированиям = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоДелегированиям.Следующий() Цикл
			
			СоставСубъектов = СоздатьНаборЗаписей();
			ТаблицаНабора = СоставСубъектов.ВыгрузитьКолонки();
			Выборка = ВыборкаПоДелегированиям.Выбрать();
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ТаблицаНабора.Добавить(), Выборка);
			КонецЦикла;
			НачальныйОтбор = Новый Структура(
				"ОбластьДелегирования, ОбъектОснование",
				ОбластьЗамещения.Ссылка, ВыборкаПоДелегированиям.ОбъектОснование);
			ЗаписатьТаблицуПорциями(ТаблицаНабора, НачальныйОтбор, Истина);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при записи проекта - нового, или с измененным руководителем.
// Добавляет/удаляет руководителя проекта как строку состава субъекта.
// 
// Параметры:
//  Проект - СправочникОбъект.Проекты
//
Процедура ЗаписьПроекта(Проект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СоставСубъектов = СоздатьНаборЗаписей();
	СоставСубъектов.Отбор.Субъект.Установить(Проект.Ссылка);
	
	Если ЗначениеЗаполнено(Проект.Руководитель) Тогда
		Запись = СоставСубъектов.Добавить();
		Запись.Субъект = Проект.Ссылка;
		Запись.СубъектПометкаУдаления = Проект.ПометкаУдаления;
		Запись.Сотрудник = Проект.Руководитель;
		Запись.Пользователь = Сотрудники.ПользовательСотрудника(Запись.Сотрудник);
		Запись.СотрудникПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Проект.Руководитель,
			"ПометкаУдаления");
	КонецЕсли;
	
	СоставСубъектов.Записать();
	
	ТаблицаНабора = СоставСубъектов.Выгрузить();
	РасширитьСоставСубъектовВерхнихУровней(ТаблицаНабора);
	
КонецПроцедуры

// Обновить пометки удаления по полной роли.
// 
// Параметры:
//  ПолнаяРоль - СправочникОбъект.ПолныеРоли - Записываемая полная роль
Процедура ОбновитьПометкиУдаленияПоПолнойРоли(ПолнаяРоль) Экспорт
	
	Если ПолнаяРоль.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Субъект.Установить(ПолнаяРоль.Ссылка);
	Набор.Прочитать();
	
	Для Каждого Запись Из Набор Цикл
		Запись.СубъектПометкаУдаления = ПолнаяРоль.ПометкаУдаления;
	КонецЦикла;
	
	Набор.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Записать таблицу порциями, т.к. она может быть огромной.
// 
// Параметры:
//  ТаблицаНабора - ТаблицаЗначений - Таблица набора, которую надо загрузить.
//  НачальныйОтбор - Структура - Начальный отбор, который должен быть у каждой порции.
//  Замещать - Булево - Писать с замещением текущих или без.
// 
// Возвращаемое значение:
//  Число - Количество записей.
Функция ЗаписатьТаблицуПорциями(ТаблицаНабора, НачальныйОтбор, Замещать)
	
	КолвоСтрок = ТаблицаНабора.Количество();
	Если КолвоСтрок < ДокументооборотПраваДоступаПереопределяемый.РазмерПорцииДляЗаписиССПД() Тогда
		
		// Пишем все разом:
		Набор = СоздатьНаборЗаписей();
		Для Каждого КлючЗначение Из НачальныйОтбор Цикл
			Набор.Отбор[КлючЗначение.Ключ].Установить(КлючЗначение.Значение);
		КонецЦикла;
		Набор.Загрузить(ТаблицаНабора);
		Набор.Записать(Замещать);
		Возврат КолвоСтрок;
		
	КонецЕсли;
	
	Если ТаблицаНабора.Колонки.Найти("ОбъектОснование") = Неопределено Тогда
		// нужна пустая, но типизированная колонка:
		ТаблицаНабора.Колонки.Добавить(
			"ОбъектОснование",
			Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия, СправочникСсылка.ЗамещающиеИПомощники"));
	КонецЕсли;
	Если ТаблицаНабора.Колонки.Найти("ОбластьДелегирования") = Неопределено Тогда
		// нужны пустая, но типизированная колонка:
		ТаблицаНабора.Колонки.Добавить(
			"ОбластьДелегирования",
			Новый ОписаниеТипов("СправочникСсылка.ОбластиЗамещения"));
	КонецЕсли;
	
	// Пишем порциями:
	Запрос = Новый Запрос(
		"ВЫБРАТЬ *
		|ПОМЕСТИТЬ ВТ_ТаблицаНабора
		|ИЗ
		|	&ТаблицаНабора КАК Таб
		|;
		|
		|ВЫБРАТЬ *
		|ИЗ
		|	ВТ_ТаблицаНабора КАК Т
		|ИТОГИ ПО
		|	Субъект, ОбъектОснование");
	Запрос.УстановитьПараметр("ТаблицаНабора", ТаблицаНабора);
	Выборка1 = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Счетчик = 0;
	Пока Выборка1.Следующий() Цикл // по Субъект
		Выборка2 = Выборка1.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Выборка2.Следующий() Цикл // по ОбъектОснование
			Набор = СоздатьНаборЗаписей();
			Для Каждого КлючЗначение Из НачальныйОтбор Цикл
				Набор.Отбор[КлючЗначение.Ключ].Установить(КлючЗначение.Значение);
			КонецЦикла;
			Набор.Отбор.Субъект.Установить(Выборка2.Субъект);
			Набор.Отбор.ОбъектОснование.Установить(Выборка2.ОбъектОснование);
			ВыборкаДетальные = Выборка2.Выбрать();
			Пока ВыборкаДетальные.Следующий() Цикл
				Запись = Набор.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, ВыборкаДетальные);
				Счетчик = Счетчик + 1;
			КонецЦикла;
			Набор.Записать(Замещать);
		КонецЦикла;
	КонецЦикла;
	
	Возврат Счетчик;
	
КонецФункции

// Перезаполняет все записи регистра, относящиеся к делегированию прав.
// 
Процедура ЗаполнитьВсехДелегатов()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗамещающиеИПомощники.Ссылка
		|ИЗ
		|	Справочник.ЗамещающиеИПомощники КАК ЗамещающиеИПомощники
		|ГДЕ
		|	ЗамещающиеИПомощники.Действует");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаписьЗамещения(Выборка.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

Процедура РасширитьТаблицуНабораСоставомСубъектовНижнихУровней(ТаблицаНабора, ОбрабатыватьРуководителей = Истина)
	
	// Дополнение таблицы набора составом субъектов нижних уровней.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Таб.Субъект,
		|	Таб.Сотрудник,
		|	Таб.Пользователь,
		|	Таб.ОбъектОснование,
		|	Таб.СотрудникОснование,
		|	Таб.ОбластьДелегирования,
		|	Таб.ИмяОбластиДелегирования,
		|	Таб.ОбъектМетаданных,
		|	Таб.СубъектПометкаУдаления,
		|	Таб.СотрудникПометкаУдаления
		|ПОМЕСТИТЬ ТаблицаНабора
		|ИЗ
		|	&ТаблицаНабора КАК Таб
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СоставСубъектовПравДоступа.Субъект,
		|	ТаблицаНабора.Сотрудник,
		|	ТаблицаНабора.Пользователь,
		|	ТаблицаНабора.ОбъектОснование,
		|	ТаблицаНабора.Субъект КАК СотрудникОснование,
		|	ТаблицаНабора.ОбластьДелегирования,
		|	ТаблицаНабора.ИмяОбластиДелегирования,
		|	ТаблицаНабора.ОбъектМетаданных,
		|	СоставСубъектовПравДоступа.Субъект.ПометкаУдаления КАК СубъектПометкаУдаления,
		|	ТаблицаНабора.СотрудникПометкаУдаления
		|ИЗ
		|	ТаблицаНабора КАК ТаблицаНабора
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектовПравДоступа
		|		ПО ТаблицаНабора.Субъект = СоставСубъектовПравДоступа.Сотрудник
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗамещающиеИПомощники КАК ЗамещающиеИПомощники
		|			ПО СоставСубъектовПравДоступа.ОбъектОснование = ЗамещающиеИПомощники.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|			ПО СоставСубъектовПравДоступа.ОбъектОснование = СтруктураПредприятия.Ссылка
		|ГДЕ
		|	(СтруктураПредприятия.Ссылка ЕСТЬ NULL 
		|			ИЛИ &ОбрабатыватьРуководителей = ИСТИНА)
		|	И ЗамещающиеИПомощники.Ссылка ЕСТЬ NULL 
		|	И СоставСубъектовПравДоступа.Субъект <> СоставСубъектовПравДоступа.Сотрудник
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ТаблицаНабора.Субъект,
		|	ТаблицаНабора.Сотрудник,
		|	ТаблицаНабора.Пользователь,
		|	ТаблицаНабора.ОбъектОснование,
		|	ТаблицаНабора.СотрудникОснование,
		|	ТаблицаНабора.ОбластьДелегирования,
		|	ТаблицаНабора.ИмяОбластиДелегирования,
		|	ТаблицаНабора.ОбъектМетаданных,
		|	ТаблицаНабора.СубъектПометкаУдаления,
		|	ТаблицаНабора.СотрудникПометкаУдаления
		|ИЗ
		|	ТаблицаНабора КАК ТаблицаНабора");
	
	Запрос.УстановитьПараметр("ТаблицаНабора", ТаблицаНабора);
	Запрос.УстановитьПараметр("ОбрабатыватьРуководителей", ОбрабатыватьРуководителей);
	ТаблицаНабора = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры
	
Процедура РасширитьСоставСубъектовВерхнихУровней(ТаблицаНабора, ОбрабатыватьРуководителей = Истина)
	
	// Расширение по руководителям и делегатам.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Таб.Субъект,
		|	Таб.Сотрудник,
		|	Таб.Пользователь,
		|	Таб.ОбъектОснование,
		|	Таб.СотрудникОснование,
		|	Таб.ОбластьДелегирования,
		|	Таб.ИмяОбластиДелегирования,
		|	Таб.ОбъектМетаданных,
		|	Таб.СотрудникПометкаУдаления,
		|	Таб.СубъектПометкаУдаления
		|ПОМЕСТИТЬ ТаблицаНабора
		|ИЗ
		|	&ТаблицаНабора КАК Таб
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаНабора.Субъект,
		|	СоставСубъектовПравДоступа.Сотрудник,
		|	СоставСубъектовПравДоступа.Пользователь,
		|	СоставСубъектовПравДоступа.ОбъектОснование,
		|	СоставСубъектовПравДоступа.Субъект КАК СотрудникОснование,
		|	СоставСубъектовПравДоступа.ОбластьДелегирования,
		|	СоставСубъектовПравДоступа.ИмяОбластиДелегирования,
		|	СоставСубъектовПравДоступа.ОбъектМетаданных,
		|	ТаблицаНабора.СубъектПометкаУдаления КАК СубъектПометкаУдаления,
		|	СоставСубъектовПравДоступа.Сотрудник.ПометкаУдаления КАК СотрудникПометкаУдаления
		|ПОМЕСТИТЬ ТаблицаСРуководителями
		|ИЗ
		|	ТаблицаНабора КАК ТаблицаНабора
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектовПравДоступа
		|		ПО ТаблицаНабора.Сотрудник = СоставСубъектовПравДоступа.Субъект
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|		ПО СоставСубъектовПравДоступа.ОбъектОснование = СтруктураПредприятия.Ссылка
		|ГДЕ
		|	&ОбрабатыватьРуководителей = ИСТИНА
		|	И ТаблицаНабора.Субъект <> ТаблицаНабора.Сотрудник
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ТаблицаНабора.Субъект,
		|	ТаблицаНабора.Сотрудник,
		|	ТаблицаНабора.Пользователь,
		|	ТаблицаНабора.ОбъектОснование,
		|	ТаблицаНабора.СотрудникОснование,
		|	ТаблицаНабора.ОбластьДелегирования,
		|	ТаблицаНабора.ИмяОбластиДелегирования,
		|	ТаблицаНабора.ОбъектМетаданных,
		|	ТаблицаНабора.СубъектПометкаУдаления,
		|	ТаблицаНабора.СотрудникПометкаУдаления
		|ИЗ
		|	ТаблицаНабора КАК ТаблицаНабора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаНабораСРуководителями.Субъект,
		|	СоставСубъектовПравДоступа.Сотрудник,
		|	СоставСубъектовПравДоступа.Пользователь,
		|	СоставСубъектовПравДоступа.ОбъектОснование,
		|	СоставСубъектовПравДоступа.Субъект КАК СотрудникОснование,
		|	СоставСубъектовПравДоступа.ОбластьДелегирования,
		|	СоставСубъектовПравДоступа.ИмяОбластиДелегирования,
		|	СоставСубъектовПравДоступа.ОбъектМетаданных,
		|	ТаблицаНабораСРуководителями.СубъектПометкаУдаления КАК СубъектПометкаУдаления,
		|	СоставСубъектовПравДоступа.Сотрудник.ПометкаУдаления КАК СотрудникПометкаУдаления
		|ПОМЕСТИТЬ ТаблицаСДелегатами
		|ИЗ
		|	ТаблицаСРуководителями КАК ТаблицаНабораСРуководителями
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектовПравДоступа
		|		ПО ТаблицаНабораСРуководителями.Сотрудник = СоставСубъектовПравДоступа.Субъект
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗамещающиеИПомощники КАК ЗамещающиеИПомощники
		|		ПО СоставСубъектовПравДоступа.ОбъектОснование = ЗамещающиеИПомощники.Ссылка
		|			И СоставСубъектовПравДоступа.Субъект = ЗамещающиеИПомощники.Сотрудник
		|ГДЕ
		|	ТаблицаНабораСРуководителями.Субъект <> ТаблицаНабораСРуководителями.Сотрудник
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ТаблицаСРуководителями.Субъект,
		|	ТаблицаСРуководителями.Сотрудник,
		|	ТаблицаСРуководителями.Пользователь,
		|	ТаблицаСРуководителями.ОбъектОснование,
		|	ТаблицаСРуководителями.СотрудникОснование,
		|	ТаблицаСРуководителями.ОбластьДелегирования,
		|	ТаблицаСРуководителями.ИмяОбластиДелегирования,
		|	ТаблицаСРуководителями.ОбъектМетаданных,
		|	ТаблицаСРуководителями.СубъектПометкаУдаления,
		|	ТаблицаСРуководителями.СотрудникПометкаУдаления
		|ИЗ
		|	ТаблицаСРуководителями КАК ТаблицаСРуководителями
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаСДелегатами.Субъект,
		|	ТаблицаСДелегатами.Сотрудник,
		|	ТаблицаСДелегатами.Пользователь,
		|	ТаблицаСДелегатами.ОбъектОснование,
		|	ТаблицаСДелегатами.СотрудникОснование,
		|	ТаблицаСДелегатами.ОбластьДелегирования,
		|	ТаблицаСДелегатами.ИмяОбластиДелегирования,
		|	ТаблицаСДелегатами.ОбъектМетаданных,
		|	ТаблицаСДелегатами.СубъектПометкаУдаления,
		|	ТаблицаСДелегатами.СотрудникПометкаУдаления
		|ИЗ
		|	ТаблицаСДелегатами КАК ТаблицаСДелегатами
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаНабора КАК ТаблицаНабора
		|		ПО ТаблицаСДелегатами.Субъект = ТаблицаНабора.Субъект
		|			И ТаблицаСДелегатами.Сотрудник = ТаблицаНабора.Сотрудник
		|			И ТаблицаСДелегатами.Пользователь = ТаблицаНабора.Пользователь
		|			И ТаблицаСДелегатами.ОбъектОснование = ТаблицаНабора.ОбъектОснование
		|			И ТаблицаСДелегатами.СотрудникОснование = ТаблицаНабора.СотрудникОснование
		|			И ТаблицаСДелегатами.ОбластьДелегирования = ТаблицаНабора.ОбластьДелегирования
		|			И ТаблицаСДелегатами.ОбъектМетаданных = ТаблицаНабора.ОбъектМетаданных
		|ГДЕ
		|	ТаблицаНабора.Субъект ЕСТЬ NULL ");
	
	Запрос.УстановитьПараметр("ТаблицаНабора", ТаблицаНабора);
	Запрос.УстановитьПараметр("ОбрабатыватьРуководителей", ОбрабатыватьРуководителей);
	ТаблицаДопЗаписей = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТаблицы Из ТаблицаДопЗаписей Цикл
		Запись = СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
		Запись.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Обновить пометки удаления по сотруднику.
// 
// Параметры:
//  Сотрудник - СправочникОбъект.Сотрудники - Сотрудник для обновления пометок
Процедура ОбновитьПометкиУдаленияПоСотруднику(Сотрудник)
	
	Если Сотрудник.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	Если Сотрудник.ДополнительныеСвойства.ПредыдущаяПометкаУдаления = Сотрудник.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
		
	// Нужно обновить СотрудникПометкаУдаления в записях по сотруднику
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Сотрудник.Установить(Сотрудник.Ссылка);
	Набор.Прочитать();
	
	Для Каждого Запись Из Набор Цикл
		Запись.СотрудникПометкаУдаления = Сотрудник.ПометкаУдаления;
	КонецЦикла;
	
	Набор.Записать();
	

	// Нужно обновить СубъектПометкаУдаления в записях по сотруднику
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Субъект.Установить(Сотрудник.Ссылка);
	Набор.Прочитать();
	
	Для Каждого Запись Из Набор Цикл
		Запись.СубъектПометкаУдаления = Сотрудник.ПометкаУдаления;
	КонецЦикла;
	
	Набор.Записать();
	
КонецПроцедуры

// Удаляет неактуальные записи верхних уровней.
// Запись неактуальна, если СотрудникОснование не входит в состав субъекта.
// 
// Параметры:
//  НомерИтерации - Число -
Процедура УдалитьНеактуальныеЗаписиВерхнихУровней(НомерИтерации = 1)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СоставСубъектов_ВерхнийУровень.Сотрудник,
		|	СоставСубъектов_ВерхнийУровень.Пользователь,
		|	СоставСубъектов_ВерхнийУровень.Субъект,
		|	СоставСубъектов_ВерхнийУровень.ИмяОбластиДелегирования,
		|	СоставСубъектов_ВерхнийУровень.ОбъектМетаданных,
		|	СоставСубъектов_ВерхнийУровень.ОбъектОснование,
		|	СоставСубъектов_ВерхнийУровень.СотрудникОснование,
		|	СоставСубъектов_ВерхнийУровень.ОбластьДелегирования
		|ИЗ
		|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов_ВерхнийУровень
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов_НижнийУровень
		|		ПО СоставСубъектов_ВерхнийУровень.Субъект = СоставСубъектов_НижнийУровень.Субъект
		|			И СоставСубъектов_ВерхнийУровень.СотрудникОснование = СоставСубъектов_НижнийУровень.Сотрудник
		|ГДЕ
		|	СоставСубъектов_НижнийУровень.Сотрудник ЕСТЬ NULL
		|	И СоставСубъектов_ВерхнийУровень.СотрудникОснование <> ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Запись = СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		Запись.Удалить();
	КонецЦикла;
	
	// Выполнение второго прохода - на первом этапе могли не удалиться записи самого верхнего уровня, 
	// т.е. делегаты руководителей.
	НеобходимоеКоличествоИтераций = 2;
	Если НомерИтерации < НеобходимоеКоличествоИтераций Тогда
		УдалитьНеактуальныеЗаписиВерхнихУровней(НомерИтерации + 1);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
