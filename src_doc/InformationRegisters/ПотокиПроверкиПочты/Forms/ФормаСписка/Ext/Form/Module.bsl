#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если Не ВебКлиент Тогда
		ПодключитьОбработчикОжидания("Автообновление", 300);
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАвтоматически(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗаполнитьАвтоматическиПродолжение",
		ЭтотОбъект);

	ОткрытьФорму("РегистрСведений.ПотокиПроверкиПочты.Форма.ФормаВводаЧислаЗаданий",,,,,,
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАвтоматическиПродолжение(Результат, Параметры) Экспорт 

	Если ТипЗнч(Результат) <> Тип("Число") Тогда
		Возврат;
	КонецЕсли;	
	
	ЧислоЗаданий = Результат;
	
	ВстроеннаяПочтаСервер.ПерезаполнитьРегистрОбрабатываемыеРегламентнымиЗаданиямиУчетныеЗаписи(ЧислоЗаданий);
	Элементы.Список.Обновить();

КонецПроцедуры

&НаКлиенте
Процедура ПоменятьПоток(Команда)
	
	МассивУчетныхЗаписей = Новый Массив;
	
	Если Элементы.Список.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	ИначеЕсли Элементы.Список.ВыделенныеСтроки.Количество() = 1 Тогда
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		МассивУчетныхЗаписей.Добавить(ТекущиеДанные.УчетнаяЗапись);
	Иначе
		Для Каждого ВыбраннаяСтрока Из Элементы.Список.ВыделенныеСтроки Цикл
			ДанныеСтроки = Элементы.Список.ДанныеСтроки(ВыбраннаяСтрока);
			МассивУчетныхЗаписей.Добавить(ДанныеСтроки.УчетнаяЗапись);
		КонецЦикла;
	КонецЕсли;
	
	Если МассивУчетныхЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("МассивУчетныхЗаписей", МассивУчетныхЗаписей);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПоменятьПотокЗавершение", ЭтотОбъект, ПараметрыФормы);
	
	ОткрытьФорму("РегистрСведений.ПотокиПроверкиПочты.Форма.ФормаВводаНомераПотока",
		ПараметрыФормы, ЭтаФорма,,,,
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоменятьПотокЗавершение(Результат, Параметры) Экспорт 
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоменятьПотокСервер(Параметры.МассивУчетныхЗаписей, Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Автообновление()
	
	ОбновитьДанные();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанные()
	
	Таблица.Очистить();
	Для Каждого ДанныеПотока Из РегистрыСведений.ПотокиПроверкиПочты.ДанныеПотоков() Цикл
		
		Строка = Таблица.Добавить();
		Строка.Поток                  = ДанныеПотока.НомерПотока;
		Строка.ЧислоУчетныхЗаписей    = ДанныеПотока.КоличествоУчетныхЗаписей;
		Строка.ДатаПоследнейОбработки = ДанныеПотока.ДатаПоследнейОбработки;
		Строка.СрокПревышен           = ДанныеПотока.СрокПревышен;
		
	КонецЦикла;
	
	ГраницаПросрочки = РегистрыСведений.ПотокиПроверкиПочты.ГраницаПросрочки();
	Список.Параметры.УстановитьЗначениеПараметра("ГраницаПросрочки", ГраницаПросрочки);
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ПоменятьПотокСервер(МассивУчетныхЗаписей, НомерПотока)
	
	УстановитьПривилегированныйРежим(Истина);
	ЕстьИзменения = Ложь;
	
	Для Каждого УчетнаяЗапись Из МассивУчетныхЗаписей Цикл
		
		МенеджерЗаписи = РегистрыСведений.ПотокиПроверкиПочты.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
		МенеджерЗаписи.Прочитать();
		
		Если МенеджерЗаписи.НомерЗадания <> НомерПотока Тогда 
			МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
			МенеджерЗаписи.НомерЗадания = НомерПотока;
			МенеджерЗаписи.Записать();
			ЕстьИзменения = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьИзменения Тогда 
		ОбновитьДанные();
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
