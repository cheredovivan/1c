#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет дату, после которой считается что просрочена обработка учетной записи потоком.
// 
// Возвращаемое значение:
//  Дата - Граница просрочки.
//
Функция ГраницаПросрочки() Экспорт
	
	Дата20Минут = ТекущаяДатаСеанса() - 20 * 60;
	
	Возврат Дата20Минут;
	
КонецФункции

// Формирует данные потоков, обрабатывающих учетные записи.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные потоков.
//   * НомерПотока              - Число  - Номер потока.
//   * КоличествоУчетныхЗаписей - Число  - Количество учетных записей, назначенных на поток.
//   * ДатаПоследнейОбработки   - Дата   - Дата последней обработки потока.
//   * СрокПревышен             - Булево - Просрочена обработка потока.
//
Функция ДанныеПотоков() Экспорт
	
	ДанныеПотоков = Новый ТаблицаЗначений;
	ДанныеПотоков.Колонки.Добавить("НомерПотока",              Новый ОписаниеТипов("Число"));
	ДанныеПотоков.Колонки.Добавить("КоличествоУчетныхЗаписей", Новый ОписаниеТипов("Число"));
	ДанныеПотоков.Колонки.Добавить("ДатаПоследнейОбработки",   Новый ОписаниеТипов("Дата"));
	ДанныеПотоков.Колонки.Добавить("СрокПревышен",             Новый ОписаниеТипов("Булево"));
	
	ГраницаПросрочки = ГраницаПросрочки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПотокиПроверкиПочты.НомерЗадания КАК НомерЗадания,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПотокиПроверкиПочты.УчетнаяЗапись) КАК ЧислоУчетныхЗаписей
		|ИЗ
		|	РегистрСведений.ПотокиПроверкиПочты КАК ПотокиПроверкиПочты
		|
		|СГРУППИРОВАТЬ ПО
		|	ПотокиПроверкиПочты.НомерЗадания
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерЗадания";
		
	ТаблицаНомеровЗаданий = Запрос.Выполнить().Выгрузить();
	МассивНомеровЗаданий = ТаблицаНомеровЗаданий.ВыгрузитьКолонку("НомерЗадания");
	
	Для Каждого НомерЗадания Из МассивНомеровЗаданий Цикл
		
		НайденнаяСтрока = ТаблицаНомеровЗаданий.Найти(НомерЗадания, "НомерЗадания");
		ЧислоУчетныхЗаписейВПотоке = 0;
		Если НайденнаяСтрока <> Неопределено Тогда
			ЧислоУчетныхЗаписейВПотоке = НайденнаяСтрока.ЧислоУчетныхЗаписей;
		КонецЕсли;
		
		Количество = ЧислоУчетныхЗаписейВПотоке;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЕСТЬNULL(МАКСИМУМ(ОбработанныеУчетныеЗаписи.ДатаПоследнейОбработки), 0) КАК МаксВремя
			|ИЗ
			|	РегистрСведений.ПотокиПроверкиПочты КАК ПотокиПроверкиПочты
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОбработанныеУчетныеЗаписи КАК ОбработанныеУчетныеЗаписи
			|		ПО ПотокиПроверкиПочты.УчетнаяЗапись = ОбработанныеУчетныеЗаписи.УчетнаяЗапись
			|ГДЕ
			|	ПотокиПроверкиПочты.НомерЗадания = &НомерЗадания";
			
		Запрос.УстановитьПараметр("НомерЗадания", НомерЗадания);
		ТаблицаПотоков = Запрос.Выполнить().Выгрузить();
		
		Если ТаблицаПотоков.Количество() <> 0 Тогда
			
			МаксВремя = ТаблицаПотоков[0].МаксВремя;
			
			Если ТипЗнч(МаксВремя) = Тип("Дата") Тогда
				
				Строка = ДанныеПотоков.Добавить();
				Строка.НомерПотока = НомерЗадания;
				Строка.КоличествоУчетныхЗаписей = Количество;
				Строка.ДатаПоследнейОбработки = МаксВремя;
				Строка.СрокПревышен = (МаксВремя < ГраницаПросрочки);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеПотоков;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли