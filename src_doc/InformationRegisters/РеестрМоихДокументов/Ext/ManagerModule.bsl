#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Данные строк с учетом прав.
// 
// Параметры:
//  МассивДокументов - Массив из СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение:
//  Соответствие из Структура:
//   * Наименование - Строка
//   * ВидДокумента - СправочникСсылка.ВидыДокументов
//   * Проект - СправочникСсылка.Проекты
// 
Функция ДанныеСтрокСУчетомПрав(МассивДокументов) Экспорт
	
	ДанныеСтрокСУчетомПрав = Новый Соответствие;
	Если МассивДокументов.Количество() = 0 Тогда
		Возврат ДанныеСтрокСУчетомПрав;
	КонецЕсли;
	
	Для Каждого ДокументПредприятия Из МассивДокументов Цикл
		ДанныеСтрокиСУчетомПрав = Новый Структура;
		ДанныеСтрокиСУчетомПрав.Вставить("Наименование", "");
		ДанныеСтрокиСУчетомПрав.Вставить("ВидДокумента", Справочники.ВидыДокументов.ПустаяСсылка());
		ДанныеСтрокиСУчетомПрав.Вставить("Проект", Справочники.Проекты.ПустаяСсылка());
		ДанныеСтрокСУчетомПрав[ДокументПредприятия] = ДанныеСтрокиСУчетомПрав;
	КонецЦикла;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РеестрМоихДокументов.ДокументПредприятия КАК ДокументПредприятия,
		|	ПРЕДСТАВЛЕНИЕ(РеестрМоихДокументов.ДокументПредприятия) КАК Наименование,
		|	ЕСТЬNULL(ДокументыПредприятия.ВидДокумента, ЗНАЧЕНИЕ(Справочник.ВидыДокументов.ПустаяСсылка)) КАК ВидДокумента,
		|	ЕСТЬNULL(ДокументыПредприятия.Проект, ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)) КАК Проект
		|ИЗ
		|	РегистрСведений.РеестрМоихДокументов КАК РеестрМоихДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДокументыПредприятия КАК ДокументыПредприятия
		|		ПО РеестрМоихДокументов.ДокументПредприятия = ДокументыПредприятия.Ссылка
		|ГДЕ
		|	РеестрМоихДокументов.Пользователь = &Пользователь
		|	И РеестрМоихДокументов.ДокументПредприятия В (&МассивДокументов)");
	
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ДанныеСтрокиСУчетомПрав = ДанныеСтрокСУчетомПрав[ВыборкаДетальныеЗаписи.ДокументПредприятия];
		ДанныеСтрокиСУчетомПрав.Наименование = ВыборкаДетальныеЗаписи.Наименование;
		ДанныеСтрокиСУчетомПрав.ВидДокумента = ВыборкаДетальныеЗаписи.ВидДокумента;
		ДанныеСтрокиСУчетомПрав.Проект = ВыборкаДетальныеЗаписи.Проект;
	КонецЦикла;
	
	Возврат ДанныеСтрокСУчетомПрав;
	
КонецФункции

#КонецОбласти

#КонецЕсли