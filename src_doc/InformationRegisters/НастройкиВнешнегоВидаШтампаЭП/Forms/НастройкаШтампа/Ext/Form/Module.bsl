#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// читаем настройки.
	ВсеНастройки = РегистрыСведений.НастройкиВнешнегоВидаШтампаЭП.ВсеНастройки();
	
	Если ВсеНастройки.Количество() = 0 Тогда
		// если настроек нет в СУБД, заполним умолчание.   
		ЗаполнитьПоУмолчанию();
		
	Иначе
		
		ЗаполнитьПоНастройкам(ВсеНастройки);
		
	КонецЕсли;
	
	ЗаполнитьВсеСпискиВыбора();
	
	Элементы.Логотип.СписокВыбора.Добавить("[ЛоготипОрганизации]");  
	
	ОбновитьПревьюНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура ЛоготипПриИзменении(Элемент)
	
	ОбновитьПревьюПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновнаяНадписьКЭППриИзменении(Элемент)
	
	ОбновитьПревьюПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновнаяНадписьПЭППриИзменении(Элемент)
	
	ОбновитьПревьюПриИзменении();
		
КонецПроцедуры

&НаКлиенте
Процедура Поле1ПриИзменении(Элемент)
	
	ОбновитьПревьюПриИзменении();
	ЗаполнитьВсеСпискиВыбора();
	
КонецПроцедуры

&НаКлиенте
Процедура Поле2ПриИзменении(Элемент)
	
	ОбновитьПревьюПриИзменении(); 
	ЗаполнитьВсеСпискиВыбора();
	
КонецПроцедуры

&НаКлиенте
Процедура Поле3ПриИзменении(Элемент)
	
	ОбновитьПревьюПриИзменении(); 
	ЗаполнитьВсеСпискиВыбора();
	
КонецПроцедуры

&НаКлиенте
Процедура Поле4ПриИзменении(Элемент)
	
	ОбновитьПревьюПриИзменении();         
	ЗаполнитьВсеСпискиВыбора();
	
КонецПроцедуры

&НаКлиенте
Процедура Поле5ПриИзменении(Элемент)
	
	ОбновитьПревьюПриИзменении();  
	ЗаполнитьВсеСпискиВыбора();
	
КонецПроцедуры

&НаКлиенте
Процедура Поле6ПриИзменении(Элемент)
	
	ОбновитьПревьюПриИзменении();  
	ЗаполнитьВсеСпискиВыбора();
	
КонецПроцедуры

&НаКлиенте
Процедура Поле7ПриИзменении(Элемент)
	
	ОбновитьПревьюПриИзменении();  
	ЗаполнитьВсеСпискиВыбора();
	
КонецПроцедуры

&НаКлиенте
Процедура РазмерШтампаПриИзменении(Элемент)
	
	ОбновитьПревьюПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ШрифтИмяПриИзменении(Элемент)

	ОбновитьПревьюПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ШрифтИмяНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДиалогВыбораШрифта = Новый ДиалогВыбораШрифта;  
	ДиалогВыбораШрифта.Шрифт = Новый Шрифт(ШрифтИмя, ШрифтРазмер);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеИзменитьШрифт", ЭтотОбъект);
	ДиалогВыбораШрифта.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ШрифтРазмерПриИзменении(Элемент)
	
	ОбновитьПревьюПриИзменении();

КонецПроцедуры

&НаКлиенте
Процедура СкругленныеУглыПриИзменении(Элемент)
	
	ОбновитьПревьюПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ЦветПриИзменении(Элемент)
	
	ОбновитьПревьюПриИзменении();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьФайлКартинки(Команда)
	
	ИмяФормыВыбора = "Справочник.Файлы.Форма.ФормаВыбораФайлаВПапках";

	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения(
		"ВыбратьФайлКартинкиПродолжение",
		ЭтотОбъект);

	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Заголовок", НСтр("ru = 'Выбрать файл логотипа'"));
		
	ОткрытьФорму(ИмяФормыВыбора, ПараметрыОткрытия,,,,,ОписаниеОповещенияОЗакрытии, 
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоУмолчанию(Команда)
	
	ЗаполнитьПоУмолчанию(); 
	ОбновитьПревьюНаСервере();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	ГотовоНаСервере();
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ГотовоНаСервере()      
	
	ВсеНастройки = ВсеНастройки();
	РегистрыСведений.НастройкиВнешнегоВидаШтампаЭП.ЗаписатьВсеНастройки(ВсеНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПревьюПриИзменении()

	ПоУмолчанию = Ложь;       
	ОбновитьПревьюНаСервере();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПревьюНаСервере()
	
	ВсеНастройки = ВсеНастройки(); 
	
	ИтоговыйФорматPdf = Ложь;
	НастройкиШтампаЭП = ФайловыеФункцииПовтИсп.ПолучитьОбщиеНастройкиШтампаЭП();
	Если НастройкиШтампаЭП.ИтоговыйФорматФайлаСоШтампомЭП = Перечисления.ИтоговыйФорматФайлаСоШтампомЭП.PDFA Тогда
		ИтоговыйФорматPdf = Истина;
	КонецЕсли;	     
	
	Организация = НСтр("ru = 'ООО МеркурийТрейдинг'");
	ДатаПодписания = ТекущаяДатаСеанса();

	Превью.Очистить();
	ОтступY = 0;
	
	ЛоготипДанные = Неопределено;   
	Если ВсеНастройки.Логотип = "[ЛоготипОрганизации]" Тогда
		
		ЛоготипДанные = БиблиотекаКартинок.ОбразецКартинкиОрганизации.ПолучитьДвоичныеДанные();
		
	ИначеЕсли  СтрНайти(ВсеНастройки.Логотип, "Справочник.Файлы") <> 0 Тогда	  // e1cib/data/Справочник.Файлы?ref=81664cedfb95098411efe87aaf29411e
		
		ФайлСсылка = ОбщегоНазначенияДокументооборот.СсылкаПоНавигационной(ВсеНастройки.Логотип);
		ЛоготипДанные = РаботаСФайламиВызовСервера.ПолучитьДвоичныеДанныеФайла(ФайлСсылка);
		
	КонецЕсли;	
	
	// ПЭП
	ДанныеПодписанта = Новый Структура;
	ДанныеПодписанта.Вставить("Подписант", 
		НСтр("ru = 'Яковлев Сергей Петрович (Управление информационных технологий и развития, Руководитель управления)'"));
	ДанныеПодписанта.Вставить("Дата", ТекущаяДата());
	ДанныеПодписанта.Вставить("Логотип", ЛоготипДанные);
	ДанныеПодписанта.Вставить("Организация", Организация);
	ДанныеПодписанта.Вставить("ДатаПодписания", ДатаПодписания);
		
	ПутьКОтметкеЭП = РаботаСЭП.ПутьОтметкиПЭП(ДанныеПодписанта, ИтоговыйФорматPdf, ВсеНастройки);
	
	КартинкаПЭП = Новый Картинка(ПутьКОтметкеЭП);
	
	Картинка = Превью.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
	Картинка.ГраницаСверху = Ложь;
	Картинка.ГраницаСлева = Ложь;
	Картинка.ГраницаСнизу = Ложь;
	Картинка.ГраницаСправа = Ложь;
	Картинка.Картинка = КартинкаПЭП;
	Картинка.Верх = ОтступY;
	Картинка.Лево = 0;            
	
	Картинка.Ширина  = КартинкаПЭП.Ширина() / 10;
	Картинка.Высота  = КартинкаПЭП.Высота() / 10; 
	
	ОтступY = ОтступY + Картинка.Высота + 5;

	
	// КЭП                      
	ДанныеДоверенности = Новый Структура;                  
	ДанныеДоверенности.Вставить("ПодписьПоДоверенности", Ложь);
	
	ДанныеПодписанта = Новый Структура;	    
	ДанныеПодписанта.Вставить("Владелец", 
		НСтр("ru = 'Яковлев Сергей Петрович выдан ""ООО МеркурийТрейдинг Строительство"", Аппарат управления, Заместитель генерального директора по проектной работе'"));
	ДанныеПодписанта.Вставить("ДанныеДоверенности", ДанныеДоверенности);
	ДанныеПодписанта.Вставить("ДатаНачала", ТекущаяДата());
	ДанныеПодписанта.Вставить("ДатаОкончания", ТекущаяДата() + 86400 * 365);
	ДанныеПодписанта.Вставить("Номер", "6A00000316A818B89413C7D7C7000300000316");
	ДанныеПодписанта.Вставить("Логотип", ЛоготипДанные);
	ДанныеПодписанта.Вставить("Организация", Организация);
	ДанныеПодписанта.Вставить("ДатаПодписания", ДатаПодписания);
	ДанныеПодписанта.Вставить("ДолжностьПодписывающего", НСтр("ru = 'Заместитель генерального директора по проектной работе'"));
	
	ПутьКОтметкеЭП = РаботаСЭП.ПутьОтметкиЭП(ДанныеПодписанта, ИтоговыйФорматPdf, ВсеНастройки);
	
	КартинкаКЭП = Новый Картинка(ПутьКОтметкеЭП);
	
	Картинка = Превью.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
	Картинка.ГраницаСверху = Ложь;
	Картинка.ГраницаСлева = Ложь;
	Картинка.ГраницаСнизу = Ложь;
	Картинка.ГраницаСправа = Ложь;
	
	Картинка.Картинка = КартинкаКЭП;
	Картинка.Верх = ОтступY;
	Картинка.Лево = 0;            
	
	Картинка.Ширина  = КартинкаКЭП.Ширина() / 10;
	Картинка.Высота  = КартинкаКЭП.Высота() / 10; 
	
	ОтступY = ОтступY + Картинка.Высота + 5;

	
	
	// КЭП с МЧД                   
	ДанныеДоверенности = Новый Структура;
	ДанныеДоверенности.Вставить("ДатаВыдачи", ТекущаяДата());
	ДанныеДоверенности.Вставить("ДоверительПредставление", НСтр("ru = 'ООО МеркурийТрейдинг Строительные материалы и техника, отдел продаж и обслуживания'"));
	ДанныеДоверенности.Вставить("НомерДоверенности", "43572dad-cb80-424a-a611-035c554fdb56");
	ДанныеДоверенности.Вставить("ПодписьПоДоверенности", Истина);
	
	ДанныеПодписанта = Новый Структура;	    
	ДанныеПодписанта.Вставить("Владелец", 
		НСтр("ru = 'Яковлев Сергей Петрович выдан ""ООО МеркурийТрейдинг Строительство"", Аппарат управления, Заместитель генерального директора по проектной работе'"));
	ДанныеПодписанта.Вставить("ДанныеДоверенности", ДанныеДоверенности);
	ДанныеПодписанта.Вставить("ДатаНачала", ТекущаяДата());
	ДанныеПодписанта.Вставить("ДатаОкончания", ТекущаяДата() + 86400 * 365);
	ДанныеПодписанта.Вставить("Номер", "6A00000316A818B89413C7D7C7000300000316");
	ДанныеПодписанта.Вставить("Логотип", ЛоготипДанные);
	ДанныеПодписанта.Вставить("Организация", Организация);
	ДанныеПодписанта.Вставить("ДатаПодписания", ДатаПодписания);  
	ДанныеПодписанта.Вставить("ДолжностьПодписывающего", НСтр("ru = 'Заместитель генерального директора по проектной работе'"));
	
	ПутьКОтметкеЭП = РаботаСЭП.ПутьОтметкиЭП(ДанныеПодписанта, ИтоговыйФорматPdf, ВсеНастройки);
	
	КартинкаКЭП = Новый Картинка(ПутьКОтметкеЭП);
	
	Картинка = Превью.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
	Картинка.ГраницаСверху = Ложь;
	Картинка.ГраницаСлева = Ложь;
	Картинка.ГраницаСнизу = Ложь;
	Картинка.ГраницаСправа = Ложь;
	Картинка.Картинка = КартинкаКЭП;
	Картинка.Верх = ОтступY;
	Картинка.Лево = 0;            
	
	Картинка.Ширина  = КартинкаКЭП.Ширина() / 10;
	Картинка.Высота  = КартинкаКЭП.Высота() / 10; 
	
	ОтступY = ОтступY + Картинка.Высота + 5;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоНастройкам(ВсеНастройки)

	РазмерШтампа = Число(ВсеНастройки.РазмерШтампа);
	ОсновнаяНадписьКЭП = ВсеНастройки.ОсновнаяНадписьКЭП;
	ОсновнаяНадписьПЭП = ВсеНастройки.ОсновнаяНадписьПЭП;
	Поле1 = ВсеНастройки.Поле1;
	Поле2 = ВсеНастройки.Поле2;
	Поле3 = ВсеНастройки.Поле3;
	Поле4 = ВсеНастройки.Поле4;
	Поле5 = ВсеНастройки.Поле5;
	Поле6 = ВсеНастройки.Поле6;
	Поле7 = ВсеНастройки.Поле7;
	
	Цвет = Новый Цвет(Число(ВсеНастройки.ЦветКрасный), 
		Число(ВсеНастройки.ЦветЗеленый), 
		Число(ВсеНастройки.ЦветСиний));
	
	ШрифтИмя = ВсеНастройки.ШрифтИмя;
	ШрифтРазмер = Число(ВсеНастройки.ШрифтРазмер); 
	
	Логотип = ВсеНастройки.Логотип;
	
	ПоУмолчанию = Булево(ВсеНастройки.ПоУмолчанию);    
	
	СкругленныеУглы = Булево(ВсеНастройки.СкругленныеУглы);
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьПоУмолчанию() 
	
	РаботаСЭПЛокализация.ЗаполнитьШтампЭППоУмолчанию(ЭтотОбъект);  
	ЗаполнитьВсеСпискиВыбора();

КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьВсеСпискиВыбора()
	
	ЗаполнитьСписокВыбора(Элементы.Поле1, Поле1);
	ЗаполнитьСписокВыбора(Элементы.Поле2, Поле2);
	ЗаполнитьСписокВыбора(Элементы.Поле3, Поле3);
	ЗаполнитьСписокВыбора(Элементы.Поле4, Поле4);
	ЗаполнитьСписокВыбора(Элементы.Поле5, Поле5);
	ЗаполнитьСписокВыбора(Элементы.Поле6, Поле6);
	ЗаполнитьСписокВыбора(Элементы.Поле7, Поле7);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбора(Элемент, ЗначениеПоля) 
	
	МассивПолей = Новый Массив;
	МассивПолей.Добавить("[Сертификат]");
	МассивПолей.Добавить("[Владелец]");
	МассивПолей.Добавить("[Подписант]");
	МассивПолей.Добавить("[СрокДействия]");
	МассивПолей.Добавить("[Доверитель]");
	МассивПолей.Добавить("[Доверенность]");
	МассивПолей.Добавить("[Организация]");
	МассивПолей.Добавить("[ДатаПодписания]");   
	МассивПолей.Добавить("[ДолжностьПодписывающего]");   
	
	ИспользованныеПоля.Очистить();
	
	Если МассивПолей.Найти(Поле1) <> Неопределено Тогда
		ИспользованныеПоля.Добавить(Поле1);
	КонецЕсли;	
	Если МассивПолей.Найти(Поле2) <> Неопределено Тогда
		ИспользованныеПоля.Добавить(Поле2);
	КонецЕсли;	
	Если МассивПолей.Найти(Поле3) <> Неопределено Тогда
		ИспользованныеПоля.Добавить(Поле3);
	КонецЕсли;	
	Если МассивПолей.Найти(Поле4) <> Неопределено Тогда
		ИспользованныеПоля.Добавить(Поле4);
	КонецЕсли;	
	Если МассивПолей.Найти(Поле5) <> Неопределено Тогда
		ИспользованныеПоля.Добавить(Поле5);
	КонецЕсли;	
	Если МассивПолей.Найти(Поле6) <> Неопределено Тогда
		ИспользованныеПоля.Добавить(Поле6);
	КонецЕсли;	
	Если МассивПолей.Найти(Поле7) <> Неопределено Тогда
		ИспользованныеПоля.Добавить(Поле7);
	КонецЕсли;	
	
	Элемент.СписокВыбора.Очистить();          
	
	Если ЗначениеЗаполнено(ЗначениеПоля) Тогда
		Элемент.СписокВыбора.Добавить(ЗначениеПоля);
	КонецЕсли;
		
	Для Каждого ИмяПоля Из МассивПолей Цикл  
		
		Если ИспользованныеПоля.НайтиПоЗначению(ИмяПоля) = Неопределено Тогда
			Элемент.СписокВыбора.Добавить(ИмяПоля);
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры

//  Продолжение ВыбратьФайлКартинки
&НаКлиенте
Процедура ВыбратьФайлКартинкиПродолжение(РезультатОткрытия, Параметры) Экспорт
	
	ТипРезультатаОткрытия = ТипЗнч(РезультатОткрытия);
	
	Если ТипРезультатаОткрытия <> Тип("СправочникСсылка.Файлы") Тогда
		
		// файл не выбран
		Возврат;
	КонецЕсли;  
	
	Логотип = ПолучитьНавигационнуюСсылку(РезультатОткрытия);
	
	ОбновитьПревьюПриИзменении();
	
КонецПроцедуры

&НаСервере
Функция ВсеНастройки()
	
	ВсеНастройки = Новый Структура;
	ВсеНастройки.Вставить("РазмерШтампа", Строка(РазмерШтампа));
	ВсеНастройки.Вставить("ОсновнаяНадписьКЭП", ОсновнаяНадписьКЭП);
	ВсеНастройки.Вставить("ОсновнаяНадписьПЭП", ОсновнаяНадписьПЭП);
	ВсеНастройки.Вставить("Поле1", Поле1);
	ВсеНастройки.Вставить("Поле2", Поле2);
	ВсеНастройки.Вставить("Поле3", Поле3);
	ВсеНастройки.Вставить("Поле4", Поле4);
	ВсеНастройки.Вставить("Поле5", Поле5);
	ВсеНастройки.Вставить("Поле6", Поле6);
	ВсеНастройки.Вставить("Поле7", Поле7);
	ВсеНастройки.Вставить("Логотип", Логотип);
	ВсеНастройки.Вставить("ПоУмолчанию", Строка(ПоУмолчанию));
	
	ЦветАбс = Цвет.ПолучитьАбсолютный();
	ВсеНастройки.Вставить("ЦветКрасный", Строка(ЦветАбс.Красный));
	ВсеНастройки.Вставить("ЦветЗеленый", Строка(ЦветАбс.Зеленый));
	ВсеНастройки.Вставить("ЦветСиний", Строка(ЦветАбс.Синий));
	
	ВсеНастройки.Вставить("ШрифтИмя", ШрифтИмя);
	ВсеНастройки.Вставить("ШрифтРазмер", ШрифтРазмер);  
	
	ВсеНастройки.Вставить("СкругленныеУглы", Строка(СкругленныеУглы));
	
	Возврат ВсеНастройки;
	
КонецФункции

&НаКлиенте
Процедура ЗавершениеИзменитьШрифт(Шрифт, Параметры) Экспорт

	Если Шрифт = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПустаяСтрока(Шрифт.Имя) Тогда   
		
		ШрифтИмя = Шрифт.Имя;
		
	КонецЕсли;
	
	ШрифтРазмер = Шрифт.Размер;
	
	ОбновитьПревьюПриИзменении();
	
КонецПроцедуры

#КонецОбласти
