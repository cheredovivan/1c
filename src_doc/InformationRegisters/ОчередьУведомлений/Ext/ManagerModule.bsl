#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет уведомление по объекту.
//
// Параметры:
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события.
//  Объект - ЛюбаяСсылка - Объект.
//  ОбъектПодписки - ЛюбаяСсылка - Объект подписки.
//  ДополнительноеОписание - Строка - Дополнительное описание.
//  Пользователь - СправочникСсылка.Пользователи - Пользователь.
//
Процедура ДобавитьУведомлениеПоОбъекту(
	ВидСобытия,
	Объект,
	ОбъектПодписки,
	ДополнительноеОписание = "",
	Пользователь = Неопределено) Экспорт
	
	Подписчики = РегистрыСведений.НастройкиУведомлений.ПодписчикиПоОбъекту(ВидСобытия, ОбъектПодписки);
	Для Каждого Подписчик Из Подписчики Цикл
		Если ЗначениеЗаполнено(Пользователь) И Подписчик.Пользователь <> Пользователь Тогда
			Продолжить;
		КонецЕсли;
		ДобавитьУведомление(
			Подписчик.Пользователь,
			ВидСобытия,
			Подписчик.СпособУведомления,
			Объект,
			ОбъектПодписки,
			ДополнительноеОписание);
	КонецЦикла;
	
КонецПроцедуры

// Добавляет уведомление по событию.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь.
//  ВидСобытияУведомления - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события, о котором будет выполнено уведомление.
//  Объект - ЛюбаяСсылка - Объект.
//  ОбъектПодписки - ЛюбаяСсылка - Объект подписки.
//  ДополнительноеОписание - Строка - Дополнительное описание.
//  ВключаяДелегатов - Булево - Добавлять уведомление для делегатов.
//  ВидСобытияПодписки - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события, по которому будет проверяться подписка.
//
Процедура ДобавитьУведомлениеПоСобытию(
	Пользователь,
	ВидСобытияУведомления,
	Объект,
	ОбъектПодписки,
	ДополнительноеОписание = "",
	ВключаяДелегатов = Истина,
	Знач ВидСобытияПодписки = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Пользователь) <> Тип("СправочникСсылка.Пользователи") Тогда
		Возврат;
	КонецЕсли;
	
	Если ВидСобытияПодписки = Неопределено Тогда
		ВидСобытияПодписки = ВидСобытияУведомления;
	КонецЕсли;
	
	ВозможныеПодписчики = Новый Массив;
	ВозможныеПодписчики.Добавить(Пользователь);
	Если ВключаяДелегатов Тогда
		Делегаты = ЗамещающиеИПомощники.ДелегатыДляУведомления(Пользователь, Объект);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВозможныеПодписчики, Делегаты);
	КонецЕсли;
	
	Подписчики = РегистрыСведений.НастройкиУведомлений.ПодписчикиПоСобытию(ВозможныеПодписчики, ВидСобытияПодписки, Объект);
	Для Каждого Подписчик Из Подписчики Цикл
		ДобавитьУведомление(
			Подписчик.Пользователь,
			ВидСобытияУведомления,
			Подписчик.СпособУведомления,
			Объект,
			ОбъектПодписки,
			ДополнительноеОписание);
	КонецЦикла;
	
КонецПроцедуры

// Добавляет уведомление по состоянию.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события.
//  Объект - ЛюбаяСсылка - Объект.
//  ОбъектПодписки - ЛюбаяСсылка - Объект подписки.
//  ДополнительноеОписание - Строка - Дополнительное описание.
//  ДатаОбработк - Дата
//
Процедура ДобавитьУведомлениеПоСостоянию(
	Пользователь,
	ВидСобытия,
	Объект,
	ОбъектПодписки,
	ДополнительноеОписание = "",
	ДатаОбработки = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Возврат;
	КонецЕсли;
	
	// Уведомления по состоянию обрабатываются только в узле пользователя.
	Если Не ОбменДаннымиДокументооборотПовтИсп.ЭтоОсновнойУзелПользователя(Пользователь) Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		ДобавитьУведомлениеПоСобытию(
			Пользователь,
			ВидСобытия,
			Объект,
			ОбъектПодписки,
			ДополнительноеОписание,
			Ложь);
		
		РегистрыСведений.ОбработанныеУведомления.ДобавитьОбработанноеУведомление(
			ВидСобытия,
			Объект,
			Пользователь,
			ДатаОбработки);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Возвращает все уведомления из очереди, отправку которых необходимо выполнить.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Уведомления.
//
Функция ПолучитьУведомления() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ОчередьУведомлений.Пользователь КАК Пользователь,
		|	Пользователи.ПометкаУдаления КАК ПользовательПометкаУдаления,
		|	Пользователи.Недействителен КАК ПользовательНедействителен,
		|	ОчередьУведомлений.ВидСобытия КАК ВидСобытия,
		|	ОчередьУведомлений.СпособУведомления КАК СпособУведомления,
		|	ОчередьУведомлений.Объект КАК Объект,
		|	ОчередьУведомлений.ОбъектПодписки КАК ОбъектПодписки,
		|	ОчередьУведомлений.ДополнительноеОписание КАК ДополнительноеОписание,
		|	ОчередьУведомлений.КоличествоПопытокОтправки КАК КоличествоПопытокОтправки
		|ИЗ
		|	РегистрСведений.ОчередьУведомлений КАК ОчередьУведомлений
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО ОчередьУведомлений.Пользователь = Пользователи.Ссылка";
	Уведомления = Запрос.Выполнить().Выгрузить();
	
	КоличествоЭлементов = Уведомления.Количество();
	Для Индекс = 1 По КоличествоЭлементов Цикл
		Уведомление = Уведомления[КоличествоЭлементов - Индекс];
		
		Если Не ОбменДаннымиДокументооборотПовтИсп.ЭтоОсновнойУзелПользователя(Уведомление.Пользователь) Тогда
			Уведомления.Удалить(Уведомление);
			Продолжить;
		КонецЕсли;
		
		Если Уведомление.ПользовательПометкаУдаления Или Уведомление.ПользовательНедействителен Тогда
			ПричинаУдаления = НСтр("ru = 'Пользователь помечен на удаление или является недействительным.'",
				ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			УдалитьУведомление(
				Уведомление.Пользователь,
				Уведомление.ВидСобытия,
				Уведомление.СпособУведомления,
				Уведомление.Объект,
				Уведомление.ОбъектПодписки,
				ПричинаУдаления);
			Уведомления.Удалить(Уведомление);
			Продолжить;
		КонецЕсли;
		
		Если Уведомление.КоличествоПопытокОтправки >= 3 Тогда
			ПричинаУдаления = НСтр("ru = 'Превышено количество попыток отправки.'",
				ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			УдалитьУведомление(
				Уведомление.Пользователь,
				Уведомление.ВидСобытия,
				Уведомление.СпособУведомления,
				Уведомление.Объект,
				Уведомление.ОбъектПодписки,
				ПричинаУдаления);
			Уведомления.Удалить(Уведомление);
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Уведомление.Объект) Тогда
			ЕстьПраваНаОбъект = ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(
				Уведомление.Объект,
				Уведомление.Пользователь).Чтение;
			Если Не ЕстьПраваНаОбъект Тогда
				ПричинаУдаления = НСтр("ru = 'Недостаточно прав для получения уведомления.'",
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
				УдалитьУведомление(
					Уведомление.Пользователь,
					Уведомление.ВидСобытия,
					Уведомление.СпособУведомления,
					Уведомление.Объект,
					Уведомление.ОбъектПодписки,
					ПричинаУдаления);
				Уведомления.Удалить(Уведомление);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		// Подписка по источнику вытесняет уведомление с подпиской по папке.
		Если Уведомление.ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеДокумента
			И ТипЗнч(Уведомление.ОбъектПодписки) = Тип("СправочникСсылка.ПапкиДокументов")
			И ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Уведомление.Объект, "Источник")) Тогда
			
			ОтборВытесняющего = Новый Структура("Пользователь, ВидСобытия, СпособУведомления, Объект");
			ЗаполнитьЗначенияСвойств(ОтборВытесняющего, Уведомление, "Пользователь, ВидСобытия, СпособУведомления, Объект");
			
			ВытесняющиеУведомления = Уведомления.НайтиСтроки(ОтборВытесняющего);
			Если ВытесняющиеУведомления.Количество() > 1 Тогда
				
				ПричинаУдаления = НСтр("ru = 'Вместо уведомления с подпиской по папке будет отправлено уведомление по источнику.'",
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
				
				УдалитьУведомление(
					Уведомление.Пользователь,
					Уведомление.ВидСобытия,
					Уведомление.СпособУведомления,
					Уведомление.Объект,
					Уведомление.ОбъектПодписки,
					ПричинаУдаления);
				Уведомления.Удалить(Уведомление);
				
				Продолжить;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Уведомление.ВидСобытия = Справочники.ВидыБизнесСобытий.НоваяЗадача
			И Не РаботаСЗадачами.ЭтоИсполнительОжидаемогоДействия(Уведомление.Объект, Уведомление.Пользователь) Тогда
			ПричинаУдаления = НСтр(
				"ru = 'Уведомление устарело - получатель не является исполнителем, или задача уже выполнена.'",
				ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			УдалитьУведомление(
				Уведомление.Пользователь,
				Уведомление.ВидСобытия,
				Уведомление.СпособУведомления,
				Уведомление.Объект,
				Уведомление.ОбъектПодписки,
				ПричинаУдаления);
			Уведомления.Удалить(Уведомление);
			Продолжить;
		КонецЕсли;
		
		Если Уведомление.ВидСобытия = Справочники.ВидыБизнесСобытий.ОтменаБрони Тогда
			
			РеквизитыБрони = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				Уведомление.Объект,
				"ДатаОкончания, СостояниеБрони");
			Если РеквизитыБрони.СостояниеБрони <> Перечисления.СостоянияБроней.Отменена Тогда
				ПричинаУдаления = НСтр("ru = 'Уведомление устарело - бронь уже не отменена.'",
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
				УдалитьУведомление(
					Уведомление.Пользователь,
					Уведомление.ВидСобытия,
					Уведомление.СпособУведомления,
					Уведомление.Объект,
					Уведомление.ОбъектПодписки,
					ПричинаУдаления);
				Уведомления.Удалить(Уведомление);
				Продолжить;
			КонецЕсли;
			
			Если РеквизитыБрони.ДатаОкончания < ТекущаяДатаСеанса() Тогда
				ПричинаУдаления = НСтр("ru = 'Уведомление устарело - бронь уже закончилась.'",
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
				УдалитьУведомление(
					Уведомление.Пользователь,
					Уведомление.ВидСобытия,
					Уведомление.СпособУведомления,
					Уведомление.Объект,
					Уведомление.ОбъектПодписки,
					ПричинаУдаления);
				Уведомления.Удалить(Уведомление);
				Продолжить;
			КонецЕсли;
			
			ДатаОбработки = РегистрыСведений.ОбработанныеУведомления.ДатаОбработки(
				Уведомление.ВидСобытия,
				Уведомление.Объект,
				Уведомление.Пользователь);
			Если ЗначениеЗаполнено(ДатаОбработки) Тогда
				ПричинаУдаления = НСтр("ru = 'Уведомление устарело - получатель уже знает, что бронь отменена.'",
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
				УдалитьУведомление(
					Уведомление.Пользователь,
					Уведомление.ВидСобытия,
					Уведомление.СпособУведомления,
					Уведомление.Объект,
					Уведомление.ОбъектПодписки,
					ПричинаУдаления);
				Уведомления.Удалить(Уведомление);
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Уведомления;
	
КонецФункции

// Увеличивает число попыток отправки уведомления.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ уведомления.
//  Объект - ЛюбаяСсылка - Объект.
//  ОбъектПодписки - ЛюбаяСсылка - Объект подписки.
//
Процедура УвеличитьЧислоПопытокОтправки(
	Пользователь,
	ВидСобытия,
	СпособУведомления,
	Объект,
	ОбъектПодписки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.ОчередьУведомлений.СоздатьМенеджерЗаписи();
	Запись.Пользователь = Пользователь;
	Запись.ВидСобытия = ВидСобытия;
	Запись.СпособУведомления = СпособУведомления;
	Запись.Объект = Объект;
	Запись.ОбъектПодписки = ОбъектПодписки;
	Запись.Прочитать();
	
	Запись.Пользователь = Пользователь;
	Запись.ВидСобытия = ВидСобытия;
	Запись.СпособУведомления = СпособУведомления;
	Запись.Объект = Объект;
	Запись.ОбъектПодписки = ОбъектПодписки;
	Запись.ДополнительноеОписание = Запись.ДополнительноеОписание;
	Запись.КоличествоПопытокОтправки = Запись.КоличествоПопытокОтправки + 1;
	Запись.Записать();
	
КонецПроцедуры

// Удаляет уведомление из очереди уведомлений.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ уведомления.
//  Объект - ЛюбаяСсылка - Объект.
//  ОбъектПодписки - ЛюбаяСсылка - Объект подписки.
//  ПричинаУдаления - Строка - Причина удаления уведомления из очереди.
//
Процедура УдалитьУведомление(
	Пользователь,
	ВидСобытия,
	СпособУведомления,
	Объект,
	ОбъектПодписки,
	ПричинаУдаления = "") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.ОчередьУведомлений.СоздатьМенеджерЗаписи();
	Запись.Пользователь = Пользователь;
	Запись.ВидСобытия = ВидСобытия;
	Запись.СпособУведомления = СпособУведомления;
	Запись.Объект = Объект;
	Запись.ОбъектПодписки = ОбъектПодписки;
	Запись.Удалить();
	
	Если ЗначениеЗаполнено(ПричинаУдаления) Тогда
		ТекстЗаписи = СтрШаблон(
			НСтр("ru = 'Уведомление не было отправлено
				|Пользователь: %1
				|Вид события: %2
				|Способ уведомления: %3
				|Объект: %4
				|Объект подписки: %5
				|Причина: %6'",
				ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			Пользователь,
			ВидСобытия,
			СпособУведомления,
			Объект,
			ОбъектПодписки,
			ПричинаУдаления);
		ЗаписьЖурналаРегистрации(
			РаботаСУведомлениями.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			ТекстЗаписи);
	КонецЕсли;
	
КонецПроцедуры

// Удаляет порцию устаревших данных.
// 
// Возвращаемое значение:
//  Булево - Истина, если были найдены устаревшие данные, в противном случае Ложь.
//
Функция УдалитьПорциюУстаревшихДанных() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// В днях.
	СрокХраненияУведомленийПрограммы =
		Константы.СрокХраненияУведомленийПрограммы.Получить();
	
	Если СрокХраненияУведомленийПрограммы <= 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ГраницаОчисткиЗаписей = НачалоДня(ТекущаяДатаСеанса())
		- СрокХраненияУведомленийПрограммы * 24 * 3600;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 10000
		|	ОчередьУведомлений.ВидСобытия,
		|	ОчередьУведомлений.Объект,
		|	ОчередьУведомлений.ОбъектПодписки,
		|	ОчередьУведомлений.Пользователь,
		|	ОчередьУведомлений.СпособУведомления,
		|	ОчередьУведомлений.КоличествоПопытокОтправки
		|ИЗ
		|	РегистрСведений.ОчередьУведомлений КАК ОчередьУведомлений
		|ГДЕ
		|	ОчередьУведомлений.ДатаПомещенияВОчередь < &ГраницаОчисткиЗаписей
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОчередьУведомлений.ДатаПомещенияВОчередь УБЫВ");
	
	Запрос.УстановитьПараметр("ГраницаОчисткиЗаписей", ГраницаОчисткиЗаписей);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ОбработаноЗаписей = 0;
	ОбработаноУспешно = 0;
	ОбработаноСОшибками = 0;
	
	Пока Выборка.Следующий() Цикл
		
		// Обрабатываем только записи этого узла.
		ЭтоПользовательЭтогоУзла = ОбменДаннымиДокументооборотПовтИсп.ЭтоОсновнойУзелПользователя(Выборка.Пользователь);
		УведомлениеОбработано = Выборка.КоличествоПопытокОтправки >= 3;
		
		Если Не ЭтоПользовательЭтогоУзла Тогда
			
			// В центральном узле обработаем те устаревшие записи, которые никто не пытался отправить.
			ЭтоЦентральныйУзел = ОбщегоНазначенияДокументооборот.ЭтоЦентральныйУзел();
			
			Если Не ЭтоЦентральныйУзел Или УведомлениеОбработано Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		ОбработаноЗаписей = ОбработаноЗаписей + 1;
		
		Попытка
			
			Если УведомлениеОбработано Тогда
				ПричинаУдаления = "";
			Иначе
				ПричинаУдаления = НСтр("ru = 'Удаление устаревших данных.'",
					ОбщегоНазначения.КодОсновногоЯзыка());
			КонецЕсли;
			
			УдалитьУведомление(
				Выборка.Пользователь,
				Выборка.ВидСобытия,
				Выборка.СпособУведомления,
				Выборка.Объект,
				Выборка.ОбъектПодписки,
				ПричинаУдаления);
			
			ОбработаноУспешно = ОбработаноУспешно + 1;
		
		Исключение
			
			ОбработаноСОшибками = ОбработаноСОшибками + 1;
			
			ТекстСообщения = СтрШаблон(
				"%1 РегистрСведений.ОчередьУведомлений:
				|%2",
				НСтр("ru = 'Ошибка при удалении'", ОбщегоНазначения.КодОсновногоЯзыка()),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru='Удаление устаревших данных'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.ОчередьУведомлений,,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	ТекстСообщения = СтрШаблон(
		НСтр("ru = 'Процедура завершена.
			|Обработано записей: %1
			|Из них:
			|	Успешно - %2
			|	С ошибками - %3'"),
		ОбработаноЗаписей,
		ОбработаноУспешно,
		ОбработаноСОшибками);
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru='Удаление устаревших данных'", ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация,
		Метаданные.РегистрыСведений.ОчередьУведомлений,,
		ТекстСообщения);
	
	Возврат ОбработаноЗаписей > 0;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет уведомление в очередь уведомлений. Уведомление добавляет только для дейстительных пользователей.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ уведомления.
//  Объект - ЛюбаяСсылка - Объект.
//  ОбъектПодписки - ЛюбаяСсылка - Объект подписки.
//  ДополнительноеОписание - Строка - Дополнительное описание.
//
Процедура ДобавитьУведомление(
	Пользователь,
	ВидСобытия,
	СпособУведомления,
	Объект,
	ОбъектПодписки,
	ДополнительноеОписание) Экспорт
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыПользователя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Пользователь,
		"Недействителен, ПометкаУдаления");
	Если РеквизитыПользователя.Недействителен Или РеквизитыПользователя.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительнаяПроверкаПодписки = Перечисления.СобытияУведомлений.ДополнительнаяПроверкаПодписки(
		Пользователь,
		ВидСобытия,
		Объект);
	Если Не ДополнительнаяПроверкаПодписки Тогда
		Возврат;
	КонецЕсли;
	
	Если СпособУведомления = Перечисления.СпособыУведомления.ПоPush
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьPushУведомления") Тогда
		Возврат;
	
	КонецЕсли;
	
	Если СпособУведомления = Перечисления.СпособыУведомления.Чат
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьЧатБота") Тогда
		Возврат;
	КонецЕсли;
	
	Если СпособУведомления = Перечисления.СпособыУведомления.Telegram
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьУведомленияЧерезTelegram") Тогда
		Возврат;
	
	КонецЕсли;
	
	Если СпособУведомления = Перечисления.СпособыУведомления.ВКонтакте
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьУведомленияЧерезВКонтакте") Тогда
		Возврат;
	
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.ОчередьУведомлений.СоздатьМенеджерЗаписи();
	Запись.Пользователь = Пользователь;
	Запись.ВидСобытия = ВидСобытия;
	Запись.СпособУведомления = СпособУведомления;
	Запись.Объект = Объект;
	Запись.ОбъектПодписки = ОбъектПодписки;
	
	Если Запись.ВидСобытия = Справочники.ВидыБизнесСобытий.ОтменаВыполненияЗадачи
		Или Запись.ВидСобытия = Перечисления.СобытияУведомлений.СводкаПоЗадачам
		Или Запись.ВидСобытия = Перечисления.СобытияУведомлений.ПросроченаЗадачаАвтора Тогда
		
		Запись.ДополнительноеОписание = ДополнительноеОписание;
		
	Иначе
		
		Запись.Прочитать();
		
		Запись.Пользователь = Пользователь;
		Запись.ВидСобытия = ВидСобытия;
		Запись.СпособУведомления = СпособУведомления;
		Запись.Объект = Объект;
		Запись.ОбъектПодписки = ОбъектПодписки;
		
		Если Запись.ВидСобытия = Справочники.ВидыБизнесСобытий.ИзменениеЗадачи Тогда
			РаботаСУведомлениями.ПриДобавленииУведомленияПоИзменениюЗадачи(Запись, ДополнительноеОписание);
		ИначеЕсли ЗначениеЗаполнено(Запись.ДополнительноеОписание) Тогда
			Запись.ДополнительноеОписание = Запись.ДополнительноеОписание + Символы.ПС + Символы.ПС + ДополнительноеОписание;
		Иначе
			Запись.ДополнительноеОписание = ДополнительноеОписание;
		КонецЕсли;
		
	КонецЕсли;
	
	Запись.КоличествоПопытокОтправки = 0;
	
	Запись.ДатаПомещенияВОчередь = ТекущаяДатаСеанса();
	
	Запись.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
