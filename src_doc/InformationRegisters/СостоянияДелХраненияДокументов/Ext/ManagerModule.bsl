#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает данные о состоянии дела.
//
// Параметры:
//  Структура - Список типов - Текстовое описание параметра функции.
//   * ИмяСвойстваПараметра - Список типов - Текстовое описание свойства (колонки) параметра.
//
// Возвращаемое значение:
//  Структура - данные о состоянии дела.
//   * Состояние - ПеречислениеСсылка.СостоянияДелХраненияДокументов - состояние.
//   * ПредставлениеСостояния - Строка - строковое представление.
//   * ДатаСостояния - Дата - дата установки состояния.
//   * Регистратор - ДокументСсылка - регистратор текущего состояния.
//
Функция СостояниеДела(Дело) Экспорт
	
	ДанныеОСостоянии = Новый Структура;
	ДанныеОСостоянии.Вставить("Состояние", Неопределено);
	ДанныеОСостоянии.Вставить("ПереданоВ1САрхив", Ложь);
	ДанныеОСостоянии.Вставить("ПредставлениеСостояния", "");
	ДанныеОСостоянии.Вставить("ДатаСостояния", Неопределено);
	ДанныеОСостоянии.Вставить("Регистратор", Неопределено);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СостоянияДелХраненияДокументов.Состояние КАК Состояние,
		|	СостоянияДелХраненияДокументов.ПереданоВ1САрхив КАК ПереданоВ1САрхив,
		|	СостоянияДелХраненияДокументов.Период КАК Период,
		|	СостоянияДелХраненияДокументов.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрСведений.СостоянияДелХраненияДокументов.СрезПоследних(, ДелоХраненияДокументов = &Дело) КАК СостоянияДелХраненияДокументов";
	Запрос.УстановитьПараметр("Дело", Дело);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДанныеОСостоянии.Состояние = Выборка.Состояние;
		ДанныеОСостоянии.ДатаСостояния = Формат(Выборка.Период, "ДЛФ=D");
		ДанныеОСостоянии.Регистратор = Выборка.Регистратор;
		Если Выборка.Состояние = Перечисления.СостоянияДелХраненияДокументов.ПереданоВАрхив Тогда 
			ДанныеОСостоянии.ПредставлениеСостояния = НСтр("ru = 'Дело передано в архив'");
		ИначеЕсли Выборка.Состояние = Перечисления.СостоянияДелХраненияДокументов.Уничтожено Тогда 
			ДанныеОСостоянии.ПредставлениеСостояния = НСтр("ru = 'Дело уничтожено'");
		КонецЕсли;
		ДанныеОСостоянии.ПереданоВ1САрхив = Выборка.ПереданоВ1САрхив;
	КонецЕсли;
	
	Возврат ДанныеОСостоянии;
	
КонецФункции

// Отражает передачу дел по документу в 1С:Архив.
// 
// Параметры:
//  Документ - ДокументСсылка.ПередачаДелВАрхив.
// 
Процедура ОтразитьПередачуВ1САрхив(Документ) Экспорт
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьОтключениеБезопасногоРежима(Истина);
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПередачаДелВАрхив.Ссылка
		|ИЗ
		|	Документ.ПередачаДелВАрхив КАК ПередачаДелВАрхив
		|ГДЕ
		|	ПередачаДелВАрхив.Ссылка = &Документ");
	Запрос.УстановитьПараметр("Документ", Документ);
	РезультатЗапроса = Запрос.Выполнить();
	ЕстьДанные = Не РезультатЗапроса.Пустой();
	
	Если Не ЕстьДанные Тогда
		Возврат;
	КонецЕсли;
		
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СостоянияДелХраненияДокументов.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", Документ);
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Документ);
		
		НаборЗаписей.Прочитать();
		Для Каждого Запись Из НаборЗаписей Цикл
			Запись.ПереданоВ1САрхив = Истина;
		КонецЦикла;
		ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные(НаборЗаписей);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

