// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает таблицу предшественников элементов по схеме.
// 
// Параметры:
// 	ДанныеСхемы - СправочникСсылка.СхемыПроцессов
// 				- см. СхемыПроцессовКлиентСервер.СтруктураДанныхСхемыПроцесса
// 	ИдентификаторОбъекта - Строка - Уникальный идентификатор владельца схемы
// 																					
// Возвращаемое значение:
//  см. СрокиИсполненияПроцессов.ПустаяТаблицаПредшественников
// 	
Функция ПредшественникиДействийПоПутямСхемы(ДанныеСхемы, ИдентификаторОбъекта = "") Экспорт
	
	Предшественники = СрокиИсполненияПроцессов.ПустаяТаблицаПредшественников();
	СхемаСсылка = ДанныеСхемы;
	Если ТипЗнч(ДанныеСхемы) = Тип("Структура") Тогда
		СхемаСсылка = ДанныеСхемы.Ссылка;
	КонецЕсли;	
	
	Идентификатор = ИдентификаторОбъекта;
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Идентификатор = СхемыПроцессовСервер.ИдентификаторВладельцаСхемыСтрокой(ДанныеСхемы);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПредшественникиЭлементовПоДаннымСхемыПроцесса.Предшественник КАК Предшественник,
		|	ПредшественникиЭлементовПоДаннымСхемыПроцесса.Последователь КАК Последователь
		|ИЗ
		|	РегистрСведений.ПредшественникиДействийПоПутямСхемыПроцесса КАК ПредшественникиЭлементовПоДаннымСхемыПроцесса
		|ГДЕ
		|	ПредшественникиЭлементовПоДаннымСхемыПроцесса.Схема = &Схема";
	
	Запрос.УстановитьПараметр("Схема", СхемаСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтрокаТЗ = Предшественники.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЗ, Выборка);
		Если СтрокаТЗ.Последователь = УникальныйИдентификаторПустойСтрокой() Тогда
			СтрокаТЗ.Последователь = УникальныйИдентификаторПустой();
		Иначе
			ПоследовательМассив = СтрРазделить(СтрокаТЗ.Последователь, "_");
			СтрокаТЗ.Последователь = Идентификатор + ПоследовательМассив[1];
		КонецЕсли;	
		Если СтрокаТЗ.Предшественник = УникальныйИдентификаторПустойСтрокой() Тогда
			СтрокаТЗ.Предшественник = УникальныйИдентификаторПустой();
		Иначе	
			ПредшественникМассив = СтрРазделить(СтрокаТЗ.Предшественник, "_");
			СтрокаТЗ.Предшественник = Идентификатор + ПредшественникМассив[1];
		КонецЕсли;	
	КонецЦикла;
	
	Возврат Предшественники;
	
КонецФункции

// Записывает набор последователей, предшественников действий схемы
// 
// Параметры:
// 	СхемаСсылка - СправочникСсылка.СхемыПроцессов
// 	Предшественники - см. СрокиИсполненияПроцессов.ПустаяТаблицаПредшественников
// 	ВыполняетсяОбновлениеИБ - Булево
// 	
Процедура ЗаписатьДанныеПредшественников(СхемаСсылка, Предшественники, ВыполняетсяОбновлениеИБ = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Схема.Установить(СхемаСсылка, Истина);
	Для Каждого Предшественник Из Предшественники Цикл
		Запись = Набор.Добавить();
		//@skip-warning
		Запись.Последователь = Предшественник.Последователь;
		Запись.Предшественник = Предшественник.Предшественник;
		Запись.Схема = СхемаСсылка;
	КонецЦикла;
	
	Если ВыполняетсяОбновлениеИБ Тогда
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
	Иначе	
		Набор.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Удаляет записи
// 
// Параметры:
//  СхемаСсылка - СправочникСсылка.СхемыПроцессов
//
Процедура УдалитьЗаписи(СхемаСсылка) Экспорт
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Схема.Установить(СхемаСсылка, Истина);
	Набор.Записать();
	
КонецПроцедуры

// Скопировать данные предшественников.
// 
// Параметры:
//  СхемаИсточник - СправочникСсылка.СхемыПроцессов
//  СхемаПриемник - СправочникСсылка.СхемыПроцессов
// 
Процедура СкопироватьДанныеПредшественников(СхемаИсточник, СхемаПриемник) Экспорт
	
	// Ответственное чтение не нужно
	
	Предшественники = ПредшественникиДействийПоПутямСхемы(СхемаИсточник);
	Если Предшественники.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Схема.Установить(СхемаПриемник, Истина);
	Для Каждого Предшественник Из Предшественники Цикл
		Запись = Набор.Добавить();
		Запись.Последователь = Предшественник.Последователь;
		Запись.Предшественник = Предшественник.Предшественник;
		Запись.Схема = СхемаПриемник;
	КонецЦикла;
	Набор.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция УникальныйИдентификаторПустойСтрокой()
	
	Возврат "00000000-0000-0000-0000-000000000000";
	
КонецФункции	

#КонецОбласти

#КонецЕсли
