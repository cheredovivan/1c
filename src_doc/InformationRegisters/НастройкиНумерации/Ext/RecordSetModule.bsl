#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Запись.Нумеровать = Нумерация.СформироватьПолеНумеровать(Запись.СпособНумерации, Запись.Нумератор);
	КонецЦикла;	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Измерения = Метаданные.РегистрыСведений.НастройкиНумерации.Измерения;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		ЗаполненныеИзмерения = Новый Массив;
		НезаполненныеИзмерения = Новый Массив;
		
		Для Каждого Измерение Из Измерения Цикл
			ИмяИзмерения = Измерение.Имя;
			
			Если (ИмяИзмерения = "Организация" И Не ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям"))
			 Или (ИмяИзмерения = "ВопросДеятельности" И Не ПолучитьФункциональнуюОпцию("ИспользоватьВопросыДеятельности"))
			 Или (ИмяИзмерения = "Проект" И Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам"))
			 Или (ИмяИзмерения = "ГрифДоступа" И Не ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа")) Тогда 
				Продолжить;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Запись[ИмяИзмерения]) Тогда 
				ЗаполненныеИзмерения.Добавить(ИмяИзмерения);
			Иначе
				НезаполненныеИзмерения.Добавить(ИмяИзмерения);
			КонецЕсли;
		КонецЦикла;	
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиНумерации.ТипДокумента КАК ТипДокумента,
		|	НастройкиНумерации.ВидДокумента КАК ВидДокумента,
		|	НастройкиНумерации.Организация КАК Организация,
		|	НастройкиНумерации.Подразделение КАК Подразделение,
		|	НастройкиНумерации.ВопросДеятельности КАК ВопросДеятельности,
		|	НастройкиНумерации.Контрагент КАК Контрагент,
		|	НастройкиНумерации.Проект КАК Проект,
		|	НастройкиНумерации.СпособНумерации КАК СпособНумерации,
		|	НастройкиНумерации.Нумератор КАК Нумератор,
		|	НастройкиНумерации.Назначение КАК Назначение,
		|	НастройкиНумерации.Тематика КАК Тематика,

		|	НастройкиНумерации.ГрифДоступа
		|ИЗ
		|	РегистрСведений.НастройкиНумерации КАК НастройкиНумерации";
		
		НайденныеСтроки = Новый Массив;
		
		Результат = Запрос.Выполнить().Выгрузить();
		Для Каждого Строка Из Результат Цикл
			
			ЕстьРазличия = Ложь;
			Для Каждого ЗаполненноеИзмерение Из ЗаполненныеИзмерения Цикл
				Если ЗначениеЗаполнено(Строка[ЗаполненноеИзмерение]) И 
					Строка[ЗаполненноеИзмерение] <> Запись[ЗаполненноеИзмерение] Тогда 
					ЕстьРазличия = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;	
			
			Если ЕстьРазличия Тогда 
				Продолжить;
			КонецЕсли;	
			
			Для Каждого ЗаполненноеИзмерение Из ЗаполненныеИзмерения Цикл
				Если Не ЗначениеЗаполнено(Строка[ЗаполненноеИзмерение]) Тогда 
					НайденныеСтроки.Добавить(Строка);
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;	
		
		Для Каждого НезаполненноеИзмерение Из НезаполненныеИзмерения Цикл
			Для Каждого Строка Из НайденныеСтроки Цикл
				Если ЗначениеЗаполнено(Строка[НезаполненноеИзмерение]) Тогда 
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Настройка ""%1"" приводит к возможности неоднозначного определения параметров нумерации с настройкой ""%2"".'"),
						ПредставлениеЗаписи(Запись),
						ПредставлениеЗаписи(Строка));
						ВызватьИсключение ТекстСообщения;
				КонецЕсли;	
			КонецЦикла;
		КонецЦикла;	
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПредставлениеЗаписи(Запись)
	
	ЗаполненныеИзмерения = Новый Массив;
	
	Измерения = Метаданные.РегистрыСведений.НастройкиНумерации.Измерения;
	Для Каждого Измерение Из Измерения Цикл
		ИмяИзмерения = Измерение.Имя;
		
		Если (ИмяИзмерения = "Организация" И Не ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям"))
			Или (ИмяИзмерения = "ВопросДеятельности" И Не ПолучитьФункциональнуюОпцию("ИспользоватьВопросыДеятельности"))
			Или (ИмяИзмерения = "Проект" И Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам"))
			Или (ИмяИзмерения = "ГрифДоступа" И Не ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа")) Тогда 
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Запись[ИмяИзмерения]) Тогда 
			ЗаполненныеИзмерения.Добавить(ИмяИзмерения);
		КонецЕсли;	
	КонецЦикла;	
	
	Представление = "";
	Для Каждого ЗаполненноеИзмерение Из ЗаполненныеИзмерения Цикл
		Представление = Представление + Запись[ЗаполненноеИзмерение] + ", ";
	КонецЦикла;	
	Если Представление <> "" Тогда 
		Представление = Лев(Представление, СтрДлина(Представление)-2);
	КонецЕсли;	
	
	Возврат Представление;
	
КонецФункции

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
