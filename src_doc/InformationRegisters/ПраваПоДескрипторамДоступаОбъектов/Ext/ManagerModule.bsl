
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Возвращает права доступа правообладателей, т.е. сотрудников или пользователей, по дескрипторам объектов.
//
// Параметры:
//  ОбъектыДоступа - Массив Из ЛюбаяСсылка - Типы - См. Объект в РегистрСведений.ДескрипторыДляОбъектов
//  СотрудникиОтбор - Массив Из СправочникСсылка.Сотрудники - Сотрудники для отбора.
//  				- Неопределено - По всем. Либо должен быть передан ПользователиОтбор.
//  ПользователиОтбор - Массив Из СправочникСсылка.Пользователи - Пользователи для отбора.
//					  - Неопределено - По всем. Либо должен быть передан СотрудникиОтбор.
//  ВключаяРуководителейИДелегатов - Булево -
//
// Возвращаемое значение:
//  См. ДокументооборотПраваДоступа.ТаблицаПравСотрудниковПоОбъектам
Функция ПраваПравообладателейПоОбъектам(
			ОбъектыДоступа,
			СотрудникиОтбор = Неопределено,
			ПользователиОтбор = Неопределено,
			ВключаяРуководителейИДелегатов = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаПрав = ДокументооборотПраваДоступа.ТаблицаПравСотрудниковПоОбъектам();
	Если ОбъектыДоступа.Количество() = 0 Тогда
		Возврат ТаблицаПрав;
	КонецЕсли;
	
	// Права по дескрипторам с учетом неограниченных прав.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДескрипторыДляОбъектов.Объект КАК ОбъектДоступа,
		|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник КАК Сотрудник,
		|	ПраваПоДескрипторамДоступаОбъектов.Пользователь КАК Пользователь,
		|	ИСТИНА КАК Чтение,
		|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Добавление) КАК Добавление,
		|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Изменение) КАК Изменение,
		|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Удаление) КАК Удаление,
		|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами) КАК УправлениеПравами
		|ИЗ
		|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
		|		ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
		|ГДЕ
		|	ДескрипторыДляОбъектов.Объект В (&ОбъектыДоступа)
		|	И &ПраваПоДескрипторам_ОтборПравообладателей
		|	И ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование = НЕОПРЕДЕЛЕНО
		|
		|СГРУППИРОВАТЬ ПО
		|	ДескрипторыДляОбъектов.Объект,
		|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник,
		|	ПраваПоДескрипторамДоступаОбъектов.Пользователь");
	ДокументооборотПраваДоступа.ПодменитьОтборПравообладателейВЗапросе(
		Запрос, "&ПраваПоДескрипторам_ОтборПравообладателей", "ПраваПоДескрипторамДоступаОбъектов",
		СотрудникиОтбор, ПользователиОтбор);
	
	Если ВключаяРуководителейИДелегатов Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"И ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование = Неопределено", "");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ОбъектыДоступа", ОбъектыДоступа);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаПрав.Добавить(), Выборка);
	КонецЦикла;
	
	Возврат ТаблицаПрав;
	
КонецФункции

#Область УстаревшиеПроцедурыИФункции

// Устарела. См. ПраваПравообладателейПоОбъектам
//
// Параметры:
//  ОбъектыДоступа - Массив Из ЛюбаяСсылка -
//  СотрудникиОтбор - Массив Из СправочникСсылка.Сотрудники -
//  				- Неопределено -
//  ВключаяРуководителейИДелегатов - Булево -
//
// Возвращаемое значение:
//  См. ДокументооборотПраваДоступа.ТаблицаПравСотрудниковПоОбъектам
Функция ПолучитьПраваСотрудниковПоОбъектам(
			ОбъектыДоступа,
			СотрудникиОтбор = Неопределено,
			ВключаяРуководителейИДелегатов = Истина) Экспорт
	
	Возврат ПраваПравообладателейПоОбъектам(ОбъектыДоступа, Сотрудники, Неопределено, ВключаяРуководителейИДелегатов);

КонецФункции

#КонецОбласти

#Область ОбработчикиОбновления

// Монопольный обработчик обновления, заполняет регистр для перехода на новый формат прав доступа.
Процедура ЗаполнитьПользователяДляПереходаНаНовуюВерсию() Экспорт
	
	// Обработчик монопольный, пишем как можно быстрее, без установок блокировок.
	
	// Оббегаем пары дескриптор + сотрудник
	// Меняем только те записи, где у Сотрудника есть Пользователь, остальные оставляем с пустым пользователем:
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РегистрПрав.Сотрудник,
		|	МАКСИМУМ(СотрудникиПользователей.Пользователь) КАК Пользователь
		|ИЗ
		|	РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК РегистрПрав
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
		|		ПО СотрудникиПользователей.Сотрудник = РегистрПрав.Сотрудник
		|ГДЕ
		|	РегистрПрав.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	РегистрПрав.Сотрудник");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НадоЗаписать = Ложь;
		Набор = Неопределено;
		Попытка
			Набор = СоздатьНаборЗаписей();
			Набор.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
			Набор.Прочитать();
			
			Для Каждого Запись Из Набор Цикл
				Если ЗначениеЗаполнено(Запись.Пользователь) Тогда
					Продолжить; // Возможно, весь набор не придется перезаписывать.
				КонецЕсли;
				
				Запись.Пользователь = Выборка.Пользователь;
				НадоЗаписать = Истина;
			КонецЦикла;
			
			Если НадоЗаписать Тогда
				Набор.ДополнительныеСвойства.Вставить("ПропуститьОпределениеДескриптораДоступаИПроверкуПрав", Истина);
				Набор.ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов", Истина);
				ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные(Набор);
			КонецЕсли;
			
			НадоЗаписать = Ложь; // Записали с 1-ой попытки.
		Исключение
			Инфо = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если НадоЗаписать И Набор <> Неопределено Тогда
				// Перестраховка. Будет 2-я попытка.
			Иначе
				ВызватьИсключение Инфо;
			КонецЕсли;
		КонецПопытки;
		
		// Попытка №2. Иногда может придти набор с неуникальными записями. Это ненормальная ситуация, но возможная.
		// Пробуем записать набор еще раз, свернув:
		Если НадоЗаписать И Набор <> Неопределено Тогда
			Попытка
				ТаблицаНабора = Набор.Выгрузить();
				ТаблицаНабора.Свернуть(
					"Дескриптор, Пользователь, Сотрудник, ОбъектОснование",
					"Добавление, Изменение, Удаление, УправлениеПравами, Чтение");
				Набор.Загрузить(ТаблицаНабора);
				ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные(Набор);
			Исключение
				Инфо = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ВызватьИсключение Инфо;
			КонецПопытки;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиОбновления

#КонецОбласти // ДляВызоваИзДругихПодсистем

#КонецОбласти

#КонецЕсли
