#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Добавляет указанный объект в очередь отложенного обновления прав
//
// Параметры:
//  Приоритет - Число - Коды приоритетов См. область модуля КонстантыПриоритетыОчереди.
//  Объект - ЛюбаяСсылка - ссылка на объект, который необходимо записать в очередь.
//  ДатаВремя - Дата - Необязательный. Используется для сортровки при обработке очереди.
//  ДопСведения - Строка - Необязательный. Дополнительные данные для обработки записи.
//
Процедура Добавить(Приоритет, Объект, ДатаВремя = Неопределено, ДопСведения = "") Экспорт
	
	Если ДатаВремя = Неопределено Тогда
		
		ДатаВремя = ТекущаяДатаСеанса();
		ДатаВМиллиСекундах = ОбщегоНазначенияДокументооборот.ТекущаяДатаВМиллисекундах();
		
	Иначе
		
		// Запись должна быть обработана после других записей с такой же датой.
		ДатаВМиллисекундах = ОбщегоНазначенияДокументооборот.ДатаВМиллисекундах(ДатаВремя);
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ЕстьNull(МАКСИМУМ(ОчередьОбновленияПравДоступа.ДатаВМиллиСекундах), 0) КАК ДатаВмс
			|ИЗ
			|	РегистрСведений.ОчередьОбновленияПравДоступа КАК ОчередьОбновленияПравДоступа
			|ГДЕ
			|	ОчередьОбновленияПравДоступа.Дата = &ДатаВремя");
		
		Запрос.УстановитьПараметр("ДатаВремя", ДатаВремя);
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			ДатаВМиллисекундах = ДатаВМиллисекундах + Выборка.ДатаВмс + 1;
		КонецЕсли;
		
	КонецЕсли;
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Приоритет = Приоритет;
	МенеджерЗаписи.Объект = Объект;
	МенеджерЗаписи.ДопСведения = ДопСведения;
	МенеджерЗаписи.ИдентификаторЗаписи = "" + Новый УникальныйИдентификатор();
	МенеджерЗаписи.Дата = ДатаВремя;
	МенеджерЗаписи.ДатаВМиллиСекундах = ДатаВМиллиСекундах;
	
	// Запись с замещением для правильного выстраивания очереди
	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры

// Удаляет указанный объект из очереди отложенного обновления прав
//
// Параметры:
//  Объект - ЛюбаяСсылка, Массив Из ЛюбаяСсылка - Ссылка на объект, который необходимо удалить из очереди или массив
//  											  таких ссылок.
//  ДопСведения - Строка - Необязательный. Дополнительные данные для обработки записи.
//  ИдентификаторЗаписи - Строка - Если не указано, то по всем.
//
Процедура Удалить(Объект, ДопСведения = "", ИдентификаторЗаписи = "") Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Очередь.Объект КАК Объект,
		|	Очередь.Приоритет КАК Приоритет,
		|	Очередь.ДопСведения КАК ДопСведения,
		|	Очередь.ИдентификаторЗаписи КАК ИдентификаторЗаписи
		|ИЗ
		|	РегистрСведений.ОчередьОбновленияПравДоступа КАК Очередь
		|ГДЕ
		|	Очередь.Объект = &Объект
		|	И Очередь.ДопСведения = &ДопСведения";
	
	Если ТипЗнч(Объект) = Тип("Массив") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "= &Объект", "В (&Объект)");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Объект", Объект);
	Запрос.УстановитьПараметр("ДопСведения", ДопСведения);
	Если ИдентификаторЗаписи <> "" Тогда
		Запрос.Текст = Запрос.Текст + "
			|	И ИдентификаторЗаписи = &ИдентификаторЗаписи";
		Запрос.УстановитьПараметр("ИдентификаторЗаписи", ИдентификаторЗаписи);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Удалить();
	КонецЦикла;
	
КонецПроцедуры

// Обновляет порцию прав из очереди. В случае многопоточных заданий является также и управляющим потоком.
//
// Параметры:
//  Приоритет - Число - На вход только однопоточные приоритеты. Коды приоритетов см. область модуля
//  					КонстантыПриоритетыОчереди.
//  РазмерПорции - Число - Задается только при ручном запуске из формы. Иначе берется фиксированная, большая порция
//  					   для потенциально долгой обработки, в т.ч. и многопоточной.
//  УчитыватьЛимитПопыток - Булево - Если Ложь, то ошибочные будут обработаны не зависимо от лимита попыток.
//
// Возвращаемое значение:
//  Число - количество обработанных записей очереди.
//
Функция ОбработатьПорцию(Приоритет, РазмерПорции = 0, УчитыватьЛимитПопыток = Истина) Экспорт
	
	Запрос = Новый запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 10000 // Порционность ""над порциями"". Некоторое ограничение памяти для управляющего потока.
		|					  // Кроме того, для ограничения памяти для таблицы ОбработанныеВТекущейИтерации (см. ниже).
		|					  // Она может очень сильно разрастаться, т.к. некоторые задания добавляют в нее много элементов.
		|	Очередь.Приоритет КАК Приоритет,
		|	Очередь.Объект КАК Объект,
		|	Очередь.ДопСведения КАК ДопСведения,
		|	Очередь.ИдентификаторЗаписи КАК ИдентификаторЗаписи
		|ИЗ
		|	РегистрСведений.ОчередьОбновленияПравДоступа КАК Очередь
		|ГДЕ
		|	Очередь.Приоритет В (&Приоритеты)
		|	И Очередь.КоличествоПопытокОбработки < 3
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет, // Важно! Сначала обычные задания, только потом многопоточные.
		|	ДатаВМиллиСекундах");
	Если РазмерПорции <> 0 Тогда
		// Принудительная обработка порции
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "10000", Формат(РазмерПорции, "ЧГ="));
	КонецЕсли;
	Если УчитыватьЛимитПопыток = Ложь Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"И Очередь.КоличествоПопытокОбработки < 3", "");
	КонецЕсли;
	
	Приоритеты = Новый Массив();
	Приоритеты.Добавить(Приоритет);
	Если Приоритет = ПриоритетОперативная() Тогда
		Приоритеты.Добавить(ПриоритетОперативнаяМногопоточная());
	ИначеЕсли Приоритет = ПриоритетДолгая() Тогда
		Приоритеты.Добавить(ПриоритетДолгаяМногопоточная());
	Иначе
		// Перестраховка. Потенциально невозможная ситуация:
		ВызватьИсключение Текст_НеПредусмотренныйПриоритет(
				Приоритет, "РегистрСведений.ОчередьОбновленияПравДоступа.ОбработатьПорцию");
	КонецЕсли;
	Запрос.УстановитьПараметр("Приоритеты", Приоритеты);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ТаблицаПорции = Новый ТаблицаЗначений();
	Для Каждого Колонка Из Результат.Колонки Цикл
		ТаблицаПорции.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения);
	КонецЦикла;
	
	КолвоПотоков = ДокументооборотПраваДоступаПовтИсп.КоличествоПотоковОчередиПрав();
	РазмерПорцииДляПотока = ДокументооборотПраваДоступаПовтИсп.РазмерПорцииПриОбработкеОчереди(Ложь);
	
	ОбработанныеВТекущейИтерации = ТаблицаОбработанныхЗаписей();
	АктивныеЗадания = Новый Массив();
	Пока Выборка.Следующий() Цикл
		
		// Объект мог быть обработан при групповой обработке записей (но среди таких заданий нет многопоточных).
		Отбор = Новый Структура("Объект, ДопСведения", Выборка.Объект, Выборка.ДопСведения);
		НайденныеЗаписи = ОбработанныеВТекущейИтерации.НайтиСтроки(Отбор); 
		Если НайденныеЗаписи.Количество() > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если Выборка.Приоритет = ПриоритетОперативная() Или Выборка.Приоритет = ПриоритетДолгая()
			Или КолвоПотоков = 1 // Если 1 поток, то даже не включаем многопоточность.
			Или РазмерПорции <> 0 // При принудительном запуске тоже не включаем многопоточность.
			// В файловой базе всегда тоже принудительно 1 поток.
			Тогда
			
			// Один поток:
			//@skip-check query-in-loop - порционная обработка разнотипных объектов.
			ОбработатьЭлементОчереди(
				Выборка.Приоритет,
				Выборка.Объект,
				Выборка.ДопСведения,
				Выборка.ИдентификаторЗаписи,
				ОбработанныеВТекущейИтерации);
				
		ИначеЕсли Выборка.Приоритет = ПриоритетОперативнаяМногопоточная()
			Или Выборка.Приоритет = ПриоритетДолгаяМногопоточная() Тогда
			
			// Много потоков:
			НоваяЗапись = ТаблицаПорции.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			
			Если ТаблицаПорции.Количество() >= РазмерПорцииДляПотока Тогда
				// Накопилось на следующий поток, запускаем его:
				// много потоков, пускаем очередной поток:
				Если АктивныеЗадания.Количество() < КолвоПотоков Тогда
					ЗапуститьНовоеЗадание(АктивныеЗадания, ТаблицаПорции);
				КонецЕсли;
			КонецЕсли;
			
			// Все потоки заполнились, ждем выполнения:
			Если АктивныеЗадания.Количество() >= КолвоПотоков Тогда
				ОжидатьВыполненияЗаданий(АктивныеЗадания);
			КонецЕсли;
		Иначе
			// Перестраховка. Потенциально невозможная ситуация:
			ВызватьИсключение Текст_НеПредусмотренныйПриоритет(
				Выборка.Приоритет, "РегистрСведений.ОчередьОбновленияПравДоступа.ОбработатьПорцию");
		КонецЕсли;
		
	КонецЦикла;
	
	// Останется "хвост" от не кратного размера порции потока кол-ву выборки, кладем его в еще один последний поток:
	Если ТаблицаПорции.Количество() > 0 Тогда
		ЗапуститьНовоеЗадание(АктивныеЗадания, ТаблицаПорции);
	КонецЕсли;
	
	// Останутся потоки, "хвост" от не кратного кол-ва порций потоков общему кол-ву выборки, плюс этот последний поток:
	ОжидатьВыполненияЗаданий(АктивныеЗадания);
	
	Количество = ОбработанныеВТекущейИтерации.Количество();
	
	Возврат Количество;
	
КонецФункции

// Обрабатывает обновление прав по указанному элементу очереди и удаляет его из очереди обновления.
// 
// Параметры:
//  Приоритет - Число - Коды приоритетов См. область модуля КонстантыПриоритетыОчереди.
//  ОбъектОчереди - ЛюбаяСсылка - Объект из очереди.
//  ДопСведения - Строка - Название задания очереди.
//  ИдентификаторЗаписи - Строка -
//  ОбработанныеВТекущейИтерации - Неопределено - Без учета обработанных, для многопоточных и когда вручную из формы.
//  							 - ТаблицаЗначений - См. ТаблицаОбработанныхЗаписей.
// 
// Возвращаемое значение:
//  Булево - Удалось обработкать или нет.
Функция ОбработатьЭлементОчереди(
	Приоритет, ОбъектОчереди, ДопСведения, ИдентификаторЗаписи, ОбработанныеВТекущейИтерации = Неопределено) Экспорт
	
	ТипЭлемента = ТипЗнч(ОбъектОчереди);
	ЭтоЗаданиеОчереди = (ТипЭлемента = Тип("ПеречислениеСсылка.ЗаданияОчередиОбновленияПрав"));
	
	ОбработанныеЭлементы = Неопределено;
	ЕстьОшибки = Ложь;
	ОписаниеОшибки = "";
	
	Попытка
		
		Если ЭтоДескриптор(ТипЭлемента) Тогда
			
			#Область ЕслиЭтоДескриптор
			
			ОбработанныеЭлементы = Новый Массив;
			ДескрипторыКРасчету = Новый Массив;
			
			ЭтоДескрипторОбъекта = ТипЭлемента = Тип("СправочникСсылка.ДескрипторыДоступаОбъектов");
			ЭтоИндивидуальныйДескриптор = Ложь;
			ЭтоДескрипторДляЛокальныхАдминистраторов = Ложь;
			
			Если ЭтоДескрипторОбъекта Тогда
				РеквизитыДескриптора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
					ОбъектОчереди, "ИдентификаторОбъектаМетаданных, КонтейнерСотрудников, ДляЛокальныхАдминистраторов");
				ЭтоИндивидуальныйДескриптор = ЗначениеЗаполнено(РеквизитыДескриптора.КонтейнерСотрудников);
				ЭтоДескрипторДляЛокальныхАдминистраторов = РеквизитыДескриптора.ДляЛокальныхАдминистраторов;
				ИдОбъектаМетаданных = РеквизитыДескриптора.ИдентификаторОбъектаМетаданных;
			Иначе
				// Дескриптор регистра.
				ИдОбъектаМетаданных = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектОчереди, "ОбъектМетаданных");
			КонецЕсли;
			
			// Поиск дескрипторов к расчету.
			Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Очередь.Объект КАК Объект
				|ИЗ
				|	&Таблица КАК ДескрипторыДоступа
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОчередьОбновленияПравДоступа КАК Очередь
				|		ПО ДескрипторыДоступа.Ссылка = Очередь.Объект
				|ГДЕ
				|	Очередь.Приоритет = &Приоритет
				|	И &УсловиеОтбора
				|	
				|УПОРЯДОЧИТЬ ПО
				|	Очередь.ДатаВМиллиСекундах");
			
			РазмерПорцииДескрипторов = ДокументооборотПраваДоступаПовтИсп.РазмерПорцииПриОбработкеОчереди(
					ЭтоИндивидуальныйДескриптор);
			УсловиеПоПолямДескриптора = "";
			Если ЭтоИндивидуальныйДескриптор Тогда
				
				УсловиеПоПолямДескриптора =
					"ДескрипторыДоступа.КонтейнерСотрудников <> Неопределено";
				
			ИначеЕсли ЭтоДескрипторДляЛокальныхАдминистраторов Тогда
				
				УсловиеПоПолямДескриптора =
					"ДескрипторыДоступа.ДляЛокальныхАдминистраторов
					|И ДескрипторыДоступа.ИдентификаторОбъектаМетаданных = &ИОМ";
				
			ИначеЕсли ЭтоДескрипторОбъекта Тогда
				
				УсловиеПоПолямДескриптора =
					"ДескрипторыДоступа.КонтейнерСотрудников = Неопределено
					|И Не ДескрипторыДоступа.ДляЛокальныхАдминистраторов
					|И ДескрипторыДоступа.ИдентификаторОбъектаМетаданных = &ИОМ";
				
			Иначе
				
				УсловиеПоПолямДескриптора =
					"ДескрипторыДоступа.ОбъектМетаданных = &ИОМ";
				
			КонецЕсли;
			
			Запрос.Текст = СтрЗаменить(
				Запрос.Текст, "ВЫБРАТЬ", "ВЫБРАТЬ ПЕРВЫЕ " + Формат(РазмерПорцииДескрипторов, "ЧГ="));
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Таблица", ОбъектОчереди.Метаданные().ПолноеИмя());
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОтбора", УсловиеПоПолямДескриптора);
			Запрос.УстановитьПараметр("ИОМ", ИдОбъектаМетаданных);
			Запрос.УстановитьПараметр("Приоритет", Приоритет);
			
			Результат = Запрос.Выполнить();
			ТаблицаРезультата = Результат.Выгрузить();
			
			ДескрипторыКРасчету = ТаблицаРезультата.ВыгрузитьКолонку("Объект");
			ОбработанныеЭлементы = ТаблицаРезультата.ВыгрузитьКолонку("Объект");
			
			Если ДескрипторыКРасчету.Найти(ОбъектОчереди) = Неопределено Тогда
				ДескрипторыКРасчету.Добавить(ОбъектОчереди);
				ОбработанныеЭлементы.Добавить(ОбъектОчереди);
			КонецЕсли;
			
			// Расчет прав по дескрипторам.
			Если ТипЭлемента = Тип("СправочникСсылка.ДескрипторыДоступаОбъектов") Тогда
				
				НазначениеДескриптора = Справочники.ДескрипторыДоступаОбъектов.НазначениеДескриптора(
					ЭтоИндивидуальныйДескриптор, ЭтоДескрипторДляЛокальныхАдминистраторов);
				
				Справочники.ДескрипторыДоступаОбъектов.РассчитатьПраваЗапросом(
					ДескрипторыКРасчету, ИдОбъектаМетаданных, НазначениеДескриптора);
				
				ДокументооборотПраваДоступа.ОбновитьПраваСвязанныхДескрипторовПоДескрипторам(
					ОбработанныеЭлементы);
				
			Иначе
				
				Справочники.ДескрипторыДоступаРегистров.РассчитатьПраваЗапросом(
					ДескрипторыКРасчету, ИдОбъектаМетаданных);
				
			КонецЕсли;
			
			#КонецОбласти // ЕслиЭтоДескриптор
			
		ИначеЕсли ТипЭлемента = Тип("СправочникСсылка.ИдентификаторыОбъектовМетаданных") Тогда
			
			Если ДопСведения = "ОпределитьПрава" Тогда
				ДокументооборотПраваДоступа.ОпределитьПраваОбъектовТаблицы(ОбъектОчереди, Истина);
				
			ИначеЕсли ДопСведения = "ОпределитьПраваДляЛокальныхАдминистраторов" Тогда
				ДокументооборотПраваДоступа.ОпределитьПраваОбъектовТаблицыДляЛокальныхАдминистраторов(ОбъектОчереди, Истина);
				
			ИначеЕсли ДопСведения = "РассчитатьПрава" Тогда
				ДокументооборотПраваДоступа.РассчитатьПраваОбъектовТаблицы(ОбъектОчереди, Истина);
				
			КонецЕсли;
			
		ИначеЕсли ДопСведения = "ИзменениеСоставаКонтейнера" Тогда
			
			// Все записи по изменению состава контейнеров обрабатываются вместе.
			Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Очередь.Объект КАК Объект
				|ИЗ
				|	РегистрСведений.ОчередьОбновленияПравДоступа КАК Очередь
				|ГДЕ
				|	Очередь.Приоритет = &Приоритет
				|	И Очередь.ДопСведения = &ДопСведения");
			
			Запрос.УстановитьПараметр("ДопСведения", ДопСведения);
			Запрос.УстановитьПараметр("Приоритет", Приоритет);
			
			ТаблицаКонтейнеров = Запрос.Выполнить().Выгрузить();
			ИзмененнныеКонтейнеры = ТаблицаКонтейнеров.ВыгрузитьКолонку("Объект");
			ОбработанныеЭлементы = ТаблицаКонтейнеров.ВыгрузитьКолонку("Объект");
			
			ДокументооборотПраваДоступа.ОбновитьПраваПоСоставуКонтейнеров(
				ИзмененнныеКонтейнеры,
				Истина); // Немедленно
			
		ИначеЕсли ДопСведения = "ОбновитьПраваПоЗначениюРазрезаДоступа" Тогда
			
			ДокументооборотПраваДоступа.ОбновитьПраваПоЗначениюРазрезаДоступа(ОбъектОчереди, Истина);
			
		ИначеЕсли ДопСведения = "АктуализироватьПраваРуководителейИДелегатов" Тогда
			
			#Область ЕслиАктуализироватьПраваРуководителейИДелегатов
			
			// Все записи по актуализации прав руководителей и делегатов обрабатываются вместе.
			Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Очередь.Объект КАК Объект
				|ИЗ
				|	РегистрСведений.ОчередьОбновленияПравДоступа КАК Очередь
				|ГДЕ
				|	Очередь.Приоритет = &Приоритет
				|	И Очередь.ДопСведения = &ДопСведения");
			
			Запрос.УстановитьПараметр("ДопСведения", ДопСведения);
			Запрос.УстановитьПараметр("Приоритет", Приоритет);
			
			ТаблицаСотрудников = Запрос.Выполнить().Выгрузить();
			СотрудникиКОбработке = ТаблицаСотрудников.ВыгрузитьКолонку("Объект");
			ОбработанныеЭлементы = ТаблицаСотрудников.ВыгрузитьКолонку("Объект");
			
			ДокументооборотПраваДоступа.АктуализироватьПраваСотрудниковПоСоставуСубъектов(
				СотрудникиКОбработке,
				Истина); // Немедленно
			
			#КонецОбласти
		
		ИначеЕсли ДопСведения = "ОпределитьПраваОбъекта" Тогда
			ДокументооборотПраваДоступа.ОпределитьДескрипторыОбъекта(ОбъектОчереди);
			Если ТипЭлемента = Тип("СправочникСсылка.Контрагенты") Тогда
				ДокументооборотПраваДоступа.ОбновитьПраваПоКонтрагенту(ОбъектОчереди, Истина);
			КонецЕсли;
		
		ИначеЕсли ТипЭлемента = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
			
			Если ДопСведения = "АктуализироватьПраваРуководителей" Тогда
				
				// Все аналогичные записи обрабатываются вместе.
				Запрос = Новый Запрос(
					"ВЫБРАТЬ
					|	Очередь.Объект КАК Объект
					|ИЗ
					|	РегистрСведений.ОчередьОбновленияПравДоступа КАК Очередь
					|ГДЕ
					|	Очередь.Приоритет = &Приоритет
					|	И Очередь.ДопСведения = &ДопСведения");
				
				Запрос.УстановитьПараметр("ДопСведения", ДопСведения);
				Запрос.УстановитьПараметр("Приоритет", Приоритет);
				
				ТаблицаПодразделений = Запрос.Выполнить().Выгрузить();
				ПодразделенияКОбработке = ТаблицаПодразделений.ВыгрузитьКолонку("Объект");
				ОбработанныеЭлементы = ТаблицаПодразделений.ВыгрузитьКолонку("Объект");
				
				ДокументооборотПраваДоступа.АктуализироватьПраваСотрудниковПодразделенийПоССПД(
					ПодразделенияКОбработке,
					Истина); // Немедленно
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("СправочникСсылка.Контрагенты") Тогда
			
			ДокументооборотПраваДоступа.ОбновитьПраваПоКонтрагенту(
				ОбъектОчереди, 
				Истина); // Немедленно
			
		ИначеЕсли ЭтоЗаданиеОчереди Тогда
			
			Если ОбъектОчереди = Перечисления.ЗаданияОчередиОбновленияПрав.ОчиститьВсеПрава Тогда
				ДокументооборотПраваДоступа.УдалитьВсеДанныеОПравахДоступа(Истина);
				
			ИначеЕсли ОбъектОчереди = Перечисления.ЗаданияОчередиОбновленияПрав.РассчитатьВсеПрава Тогда
				ДокументооборотПраваДоступа.ОбновитьПраваВсехДанных(Истина);
				
			ИначеЕсли ОбъектОчереди = Перечисления.ЗаданияОчередиОбновленияПрав.ЗаполнитьСоставСубъектовПравДоступа Тогда
				РегистрыСведений.СоставСубъектовПравДоступа.ОбновитьВсеДанные(Истина);
				
			ИначеЕсли ОбъектОчереди = Перечисления.ЗаданияОчередиОбновленияПрав.РассчитатьПраваРазрезовДоступа Тогда
				ДокументооборотПраваДоступа.ОбновитьПраваВсехРазрезовДоступа(Истина);
				
			ИначеЕсли ОбъектОчереди = Перечисления.ЗаданияОчередиОбновленияПрав.ОтключитьДескрипторыЛокАдминистраторов Тогда
				ДокументооборотПраваДоступа.ОтключитьДескрипторыЛокАдминистраторов(Истина);
				
			КонецЕсли;
			
		ИначеЕсли ДокументооборотПраваДоступа.ЭтоПапка(ТипЭлемента) Тогда
			
			Если ДопСведения = "ОбновитьПраваСодержимогоПапки" Тогда
				ДокументооборотПраваДоступа.ОбновитьПраваПоПапке(
					ОбъектОчереди,
					Истина); // Немедленно
			ИначеЕсли ДопСведения = "ПереопределитьПраваСодержимогоПапки" Тогда
				ДокументооборотПраваДоступа.ПереопределитьДескрипторыЗависимыхОбъектов(
					ОбъектОчереди,
					Истина); // Немедленно
			КонецЕсли;
			
		ИначеЕсли ДопСведения = "ОбновитьРолиПользователей" Тогда
			
			// Все записи этого типа обрабатываются вместе.
			Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Очередь.Объект КАК Объект
				|ИЗ
				|	РегистрСведений.ОчередьОбновленияПравДоступа КАК Очередь
				|ГДЕ
				|	Очередь.Приоритет = &Приоритет
				|	И Очередь.ДопСведения = &ДопСведения");
			
			Запрос.УстановитьПараметр("ДопСведения", ДопСведения);
			Запрос.УстановитьПараметр("Приоритет", Приоритет);
			
			ТаблицаПользователей = Запрос.Выполнить().Выгрузить();
			ПользователиКАктуализации = ТаблицаПользователей.ВыгрузитьКолонку("Объект");
			ОбработанныеЭлементы = ТаблицаПользователей.ВыгрузитьКолонку("Объект");
			
			УправлениеДоступом.ОбновитьРолиПользователей(
				ПользователиКАктуализации,,
				Истина); // Немедленно
		
		Иначе
			
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'Неизвестная запись очереди обновления прав.
					|Ссылка: %1
					|Доп. сведения: %2'"),
				ОбъектОчереди, ДопСведения);
			
			ВызватьИсключение ТекстОшибки
			
		КонецЕсли;
		
	Исключение
		
		ЕстьОшибки = Истина;
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Права доступа'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			ОбъектОчереди.Метаданные(),
			ОбъектОчереди,
			ОписаниеОшибки);
			
	КонецПопытки;
	
	Если ОбработанныеЭлементы = Неопределено Тогда
		ОбработанныеЭлементы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбъектОчереди);
	КонецЕсли;
	
	// Удаление обработанных элементов очереди.
	Если ЕстьОшибки Тогда
		
		Если ЭтоЗаданиеОчереди Тогда
			ОписаниеОшибки = СтрШаблон(
				НСтр("ru='%1
					|Задание: %2'"), ОписаниеОшибки, ОбъектОчереди);
		Иначе
			ОписаниеОшибки = СтрШаблон(
				НСтр("ru='%1
					|Объект: %2'"), ОписаниеОшибки, ПолучитьНавигационнуюСсылку(ОбъектОчереди));
		КонецЕсли;
		
		Если ОбработанныеЭлементы.Количество() > 0 Тогда
			ОписаниеОшибки = ОписаниеОшибки + НСтр("ru = 'Все обрабатываемые элементы:'");
			Для Каждого Эл Из ОбработанныеЭлементы Цикл
				ЭлементНаглядно = "";
				Если ЭтоЗаданиеОчереди Тогда
					 // Для перечислений получение навигационной ссылки вызывает ошибку, покажем просто строкой:
					ЭлементНаглядно = "" + Эл;
				Иначе
					ЭлементНаглядно = ПолучитьНавигационнуюСсылку(Эл);
				КонецЕсли;
				ОписаниеОшибки = ОписаниеОшибки + Символы.ПС + ЭлементНаглядно;
			КонецЦикла;
		КонецЕсли;
		
		КоличествоПопыток = ЗафиксироватьНеудачнуюПопыткуОбработки(
			Приоритет, ОбъектОчереди, ДопСведения, ИдентификаторЗаписи, ОписаниеОшибки);
		
		// Для этих заданий, если задание упало третий раз, то дальше продолжать не стоит, нет смысла, будет не совсем
		// корректный расчет, например расчет с недоочищенными правами будет неправильный. Очистим очередь и остановим 
		// расчет. Это исключительный случай. В таком случае полный перерасчет прав надо запустить вручную заново.
		Если КоличествоПопыток >= 3 И (
				ОбъектОчереди = Перечисления.ЗаданияОчередиОбновленияПрав.ОчиститьВсеПрава
				Или ОбъектОчереди = Перечисления.ЗаданияОчередиОбновленияПрав.ЗаполнитьСоставСубъектовПравДоступа
			) Тогда
			Очистить(Ложь); // Очищаем очередь полностью.
			
			// Новая запись, что не смогли очистить. Чтобы легко видеть в форме расчета прав, что случилось:
			МенеджерЗаписи = СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Приоритет = Приоритет;
			МенеджерЗаписи.Объект = ОбъектОчереди;
			МенеджерЗаписи.ДопСведения = ДопСведения;
			МенеджерЗаписи.ИдентификаторЗаписи = ИдентификаторЗаписи;
			МенеджерЗаписи.КоличествоПопытокОбработки = 3;
			МенеджерЗаписи.ТекстОшибки = ТекстОшибки;
			МенеджерЗаписи.Записать();
		КонецЕсли;
		
	Иначе
		Если ОбработанныеЭлементы.Количество() = 1 Тогда
			Удалить(ОбработанныеЭлементы, ДопСведения, ИдентификаторЗаписи); // Удаление одного задания.
		Иначе
			Удалить(ОбработанныеЭлементы, ДопСведения, ""); // Удаление всех обработанных заданий.
		КонецЕсли;
		
		Если ОбработанныеВТекущейИтерации <> Неопределено Тогда
			Для Каждого Эл Из ОбработанныеЭлементы Цикл
				// Добавляем, только если элемента еще нет (иначе таблица может сильно разрастаться в большой ИБ):
				СтрокиУжеЕсть = ОбработанныеВТекущейИтерации.НайтиСтроки(Новый Структура("Объект, ДопСведения", Эл, ДопСведения));
				Если СтрокиУжеЕсть.Количество() <> 0 Тогда
					Продолжить;
				КонецЕсли;
				
				Стр = ОбработанныеВТекущейИтерации.Добавить();
				Стр.Объект = Эл;
				Стр.ДопСведения = ДопСведения;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Не ЕстьОшибки;
	
КонецФункции

// Очищает всю очередь - удаляет все записи,
// кроме ссылок на перечисление ЗаданияОчередиОбновленияПрав.
// 
Процедура Очистить(ОставитьЗаписиСПустойДатой = Истина) Экспорт
	
	Набор = СоздатьНаборЗаписей();
	
	Если ОставитьЗаписиСПустойДатой Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Очередь.Приоритет КАК Приоритет,
			|	Очередь.Объект КАК Объект,
			|	Очередь.ДопСведения КАК ДопСведения,
			|	Очередь.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
			|	Очередь.ДатаВМиллиСекундах КАК ДатаВМиллиСекундах,
			|	Очередь.Дата КАК Дата
			|ИЗ
			|	РегистрСведений.ОчередьОбновленияПравДоступа КАК Очередь
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	Перечисление.ЗаданияОчередиОбновленияПрав КАК ПеречислениеЗадания
			|		ПО Очередь.Объект = ПеречислениеЗадания.Ссылка");
		
		ТаблицаНабора = Запрос.Выполнить().Выгрузить();
		Набор.Загрузить(ТаблицаНабора);
		
	КонецЕсли;
	
	Набор.Записать();
	
КонецПроцедуры

// Возвращает размер очереди.
// 
// Параметры:
//  Приоритеты - Массив Из Число - Типы очереди, см. секцию КонстантыПриоритетыОчереди.
//  		   - Неопределено - Если не указан, то все.
// 
// Возвращаемое значение:
//  Число - Размер очереди
Функция РазмерОчереди(Приоритеты = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если Приоритеты = Неопределено Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(*) Как ЧислоЗаписей
			|ИЗ
			|	РегистрСведений.ОчередьОбновленияПравДоступа КАК ОчередьОбновленияПравДоступа";
	Иначе
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(*) Как ЧислоЗаписей
			|ИЗ
			|	РегистрСведений.ОчередьОбновленияПравДоступа КАК ОчередьОбновленияПравДоступа
			|ГДЕ
			|	ОчередьОбновленияПравДоступа.Приоритет В (&Приоритеты)";
		Запрос.УстановитьПараметр("Приоритеты", Приоритеты);
	КонецЕсли;

	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.ЧислоЗаписей;
	КонецЕсли;

	Возврат 0;
	
КонецФункции

#Область КонстантыПриоритетыОчереди

// Этот объект метаданных с зависимыми правами, он рассчитывается в последнюю очередь и многопоточно.
// 
// Параметры:
//  ОбъектМетаданных - СправочникСсылка.ИдентификаторыОбъектовМетаданных
// 
// Возвращаемое значение:
//  Число
Функция ЭтоЗависимыйОбъектМетаданных(ОбъектМетаданных) Экспорт

	ЗависимыеОМ = ДокументооборотПраваДоступаПереопределяемый.ОбъектыСЗависимымиПравами();
	Если ЗависимыеОМ.Найти(ОбъектМетаданных) <> Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПриоритетОперативная() Экспорт
	
	Возврат 1;
	
КонецФункции

Функция ПриоритетДолгая() Экспорт
	
	Возврат 2;
	
КонецФункции

Функция ПриоритетОперативнаяМногопоточная() Экспорт
	
	Возврат 10;
	
КонецФункции

Функция ПриоритетДолгаяМногопоточная() Экспорт
	
	Возврат 20;
	
КонецФункции

Функция ПриоритетыОперативной() Экспорт
	
	Приоритеты = Новый Массив();
	Приоритеты.Добавить(ПриоритетОперативная());
	Приоритеты.Добавить(ПриоритетОперативнаяМногопоточная());
	Возврат Приоритеты;
	
КонецФункции

Функция ПриоритетыДолгой() Экспорт
	
	Приоритеты = Новый Массив();
	Приоритеты.Добавить(ПриоритетДолгая());
	Приоритеты.Добавить(ПриоритетДолгаяМногопоточная());
	Возврат Приоритеты;
	
КонецФункции

Функция Текст_НеПредусмотренныйПриоритет(Приоритет, Источник) Экспорт
	
	Возврат СтрШаблон(
		НСтр("ru = 'Не предусмотренный приоритет очереди - %1. Источник %2'"),
		Приоритет, Источник);
	
КонецФункции

#КонецОбласти // КонстантыПриоритетыОчереди

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

#Область МнопоточностьСлужебные

Процедура ЗапуститьНовоеЗадание(АктивныеЗадания, ТаблицаПорции)
	
	Параметры = Новый Массив();
	Параметры.Добавить(ТаблицаПорции);
	НовоеЗадание = ФоновыеЗадания.Выполнить(
		"ДокументооборотПраваДоступа.ОбработатьПорциюОчереди_ВФоне", Параметры);
	АктивныеЗадания.Добавить(НовоеЗадание);
	ТаблицаПорции.Очистить(); // Начинаем новую порцию на следующем шаге цикла.
	
КонецПроцедуры

Процедура ОжидатьВыполненияЗаданий(АктивныеЗадания)
	
	Пока АктивныеЗадания.Количество() > 0 Цикл
		АктивныеЗадания = ФоновыеЗадания.ОжидатьЗавершенияВыполнения(АктивныеЗадания, 1);
		Счетчик = АктивныеЗадания.Количество() - 1;
		Пока Счетчик >= 0 Цикл // Бежим с конца.
			ТекущееАктивное = АктивныеЗадания[Счетчик];
			Если ТекущееАктивное.Состояние <> СостояниеФоновогоЗадания.Активно Тогда
				АктивныеЗадания.Удалить(Счетчик);
			КонецЕсли;
			Счетчик = Счетчик - 1;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // МнопоточностьСлужебные

Функция ТаблицаОбработанныхЗаписей()

	ОбработанныеВТекущейИтерации = Новый ТаблицаЗначений;
	ОбработанныеВТекущейИтерации.Колонки.Добавить("Объект");
	ОбработанныеВТекущейИтерации.Колонки.Добавить("ДопСведения");
	Возврат ОбработанныеВТекущейИтерации;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
	
Функция ЭтоДескриптор(ТипЭлемента)
	
	Если ТипЭлемента = Тип("СправочникСсылка.ДескрипторыДоступаОбъектов") 
		ИЛИ ТипЭлемента = Тип("СправочникСсылка.ДескрипторыДоступаРегистров") Тогда
		
		Возврат Истина;
		
	КонецЕсли;	
	
	Возврат Ложь;
	
КонецФункции

Функция ЗафиксироватьНеудачнуюПопыткуОбработки(
	Приоритет, ОбъектОчереди, ДопСведения, ИдентификаторЗаписи, ТекстОшибки)
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Приоритет = Приоритет;
	МенеджерЗаписи.Объект = ОбъектОчереди;
	МенеджерЗаписи.ДопСведения = ДопСведения;
	МенеджерЗаписи.ИдентификаторЗаписи = ИдентификаторЗаписи;
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.КоличествоПопытокОбработки = МенеджерЗаписи.КоличествоПопытокОбработки + 1;
		МенеджерЗаписи.ТекстОшибки = ТекстОшибки;
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
	Возврат МенеджерЗаписи.КоличествоПопытокОбработки;
	
КонецФункции

#КонецОбласти

#КонецЕсли
