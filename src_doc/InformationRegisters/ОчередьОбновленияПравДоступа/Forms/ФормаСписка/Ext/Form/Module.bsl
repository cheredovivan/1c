
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидОчереди = "Долгая";
	УстановитьУсловноеОформление();
	ОбновитьСервер();
	
	// Техническая колонка для отличия многопоточного задания от обычного:
	Элементы.Приоритет.Видимость = ОбщегоНазначения.РежимОтладки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидОчередиПриИзменении(Элемент)
	
	ОбновитьСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(, Элементы.Список.ТекущиеДанные.Объект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	ТекстВопроса = НСтр("ru = 'Внимание! 
		|Очистка приведет к тому, что стоящие в очереди права не будут обновлены. 
		|Очистить очередь?'");
		
	ОписаниеОповещения = 
		Новый ОписаниеОповещения("ОчиститьПродолжение", ЭтотОбъект);
		
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПорцию(Команда)
	
	ТекстПредупреждения = ПроверитьРегламентныеЗаданияНаСервере();
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	РазмерПорции = 100;
	ОбновитьПорциюСервер(РазмерПорции, Ложь); // Без отбора по количеству попыток.
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВыбранные(Команда)
	
	ТекстПредупреждения = ПроверитьРегламентныеЗаданияНаСервере();
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ОбновитьВыбранныеСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВсе(Команда)
	
	ТекстПредупреждения = ПроверитьРегламентныеЗаданияНаСервере();
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Выполняется обработка очереди обновления прав.
		|Пожалуйста подождите ...'"));
		
	ДатаНачала = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	ОбщееЧисло = РазмерОчереди();
	Обработано = 0;
	РазмерПорции = 100;
	ОбработаноВТекущейИтерации = ОбновитьПорциюСервер(РазмерПорции);
	Пока ОбработаноВТекущейИтерации <> 0 Цикл
		
		Обработано = Обработано + ОбработаноВТекущейИтерации;
		Если Обработано > ОбщееЧисло Тогда
			Обработано = 0;
			//@skip-check query-in-loop - порционная обработка.
			ОбщееЧисло = РазмерОчереди();
		КонецЕсли;	
		
		ТекстПояснения = СтрШаблон(НСтр("ru = 'Обработано: %1 из %2'"), Обработано, ОбщееЧисло);
			
		Если ОбщееЧисло <> 0 Тогда	
			Процент = (Обработано*100)/ОбщееЧисло;
		Иначе	
			Процент = 100;
		КонецЕсли;
		
		Состояние(
			НСтр("ru = 'Выполняется обработка очереди обновления прав...'"),
			Процент,
			ТекстПояснения,
			БиблиотекаКартинок.Информация32);
		
		ОбработаноВТекущейИтерации = ОбновитьПорциюСервер(РазмерПорции);
		
	КонецЦикла;	
	
	ДатаОкончания = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	Состояние("");
	
	ТекстПредупреждения = СтрШаблон(
		НСтр("ru = 'Обработка очереди обновления прав завершена (%1 сек).'"),
		ДатаОкончания - ДатаНачала);
		
	ПоказатьПредупреждение(, ТекстПредупреждения);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОчиститьПродолжение(Ответ, Параметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОчиститьСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьСервер()
	
	Приоритеты = ТекущиеПриоритеты();
	Для Каждого Приоритет Из Приоритеты Цикл
		Набор = РегистрыСведений.ОчередьОбновленияПравДоступа.СоздатьНаборЗаписей();
		Набор.Отбор.Приоритет.Установить(Приоритет);
		Набор.Записать();
	КонецЦикла;
	
	ОбновитьСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСервер()
	
	Список.Отбор.Элементы.Очистить();
	
	ТекущийПриоритет = ТекущийОсновнойПриоритет();
	Модуль = РегистрыСведений.ОчередьОбновленияПравДоступа;
	Приоритеты = Новый Массив();
	Приоритеты.Добавить(ТекущийПриоритет);
	Если ТекущийПриоритет = Модуль.ПриоритетОперативная() Тогда
		Приоритеты.Добавить(Модуль.ПриоритетОперативнаяМногопоточная());
	ИначеЕсли ТекущийПриоритет = Модуль.ПриоритетДолгая() Тогда 
		Приоритеты.Добавить(Модуль.ПриоритетДолгаяМногопоточная());
	Иначе
		ВызватьИсключение Модуль.Текст_НеПредусмотренныйПриоритет(
			ТекущийПриоритет, "РегистрСведений.ОчередьОбновленияПравДоступа.ФормаСписка.ОбновитьСервер");
	КонецЕсли;
	
	ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Приоритет");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ПравоеЗначение = Приоритеты;
	
	Элементы.Список.Обновить();
	ОбновитьКоличество();
	
КонецПроцедуры

&НаСервере
Функция ОбновитьПорциюСервер(Знач РазмерПорции, Знач УчитыватьЛимитПопыток = Истина)
	
	ОбработанныхВсего = 0;
	Попытка
		ТекущийПриоритет = ТекущийОсновнойПриоритет();
		ОбработанныхВсего = РегистрыСведений.ОчередьОбновленияПравДоступа.ОбработатьПорцию(
			ТекущийПриоритет, РазмерПорции, УчитыватьЛимитПопыток);
	Исключение
		ВызватьИсключение;
	КонецПопытки;
	
	ОбновитьСервер();
	
	Возврат ОбработанныхВсего;
	
КонецФункции

&НаСервере
Процедура ОбновитьВыбранныеСервер()
	
	Попытка
		
		Для Каждого Эл Из Элементы.Список.ВыделенныеСтроки Цикл
			РегистрыСведений.ОчередьОбновленияПравДоступа.ОбработатьЭлементОчереди(
				Эл.Приоритет, Эл.Объект, Эл.ДопСведения, Эл.ИдентификаторЗаписи);
		КонецЦикла;
		
	Исключение
		ВызватьИсключение;
	КонецПопытки;
	
	ОбновитьСервер();
	
КонецПроцедуры

// Текущий приоритет. Здесь учитываются только приоритеты основных очередей без учета разделения на многопоточность.
// 
// Возвращаемое значение:
//  Число
&НаСервере
Функция ТекущийОсновнойПриоритет()
	
	Модуль = РегистрыСведений.ОчередьОбновленияПравДоступа;
	Приоритет = 0;
	Если ВидОчереди = "Оперативная" Тогда
		Приоритет = Модуль.ПриоритетОперативная();
	ИначеЕсли ВидОчереди = "Долгая" Тогда 
		Приоритет = Модуль.ПриоритетДолгая();
	Иначе
		ВызватьИсключение Модуль.Текст_НеПредусмотренныйПриоритет(
			ВидОчереди, "РегистрСведений.ОчередьОбновленияПравДоступа.ФормаСписка.ТекущийОсновнойПриоритет");
	КонецЕсли;
	
	Возврат Приоритет;
	
КонецФункции

&НаСервере
Функция ТекущиеПриоритеты()
	
	Приоритеты = Новый Массив();
	ТекущийОсновной = ТекущийОсновнойПриоритет(); // без учета многопоточности.
	Приоритеты.Добавить(ТекущийОсновной);
	Модуль = РегистрыСведений.ОчередьОбновленияПравДоступа;
	Если ТекущийОсновной = Модуль.ПриоритетОперативная() Тогда
		Приоритеты.Добавить(Модуль.ПриоритетОперативнаяМногопоточная());
	ИначеЕсли ТекущийОсновной = Модуль.ПриоритетДолгая() Тогда 
		Приоритеты.Добавить(Модуль.ПриоритетДолгаяМногопоточная());
	Иначе
		ВызватьИсключение Модуль.Текст_НеПредусмотренныйПриоритет(
			ТекущийОсновной, "РегистрСведений.ОчередьОбновленияПравДоступа.ФормаСписка.ТекущиеПриоритеты");
	КонецЕсли;
	
	Возврат Приоритеты;
	
КонецФункции

&НаСервере
Процедура ОбновитьКоличество()
	
	РазмерыОчереди = РазмерыОчереди();
	Для Каждого Эл Из Элементы.ВидОчереди.СписокВыбора Цикл
		Размер = РазмерыОчереди.Получить(Эл.Значение);
		Размер = ?(Размер = Неопределено, 0, Размер);
		Эл.Представление = Эл.Значение + " (" + Размер + ")";
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция РазмерОчереди()
	
	Возврат РегистрыСведений.ОчередьОбновленияПравДоступа.РазмерОчереди(ТекущиеПриоритеты());
	
КонецФункции

// В целом, без учета основная/многопоточная
&НаСервере
Функция РазмерыОчереди()
	
	Модуль = РегистрыСведений.ОчередьОбновленияПравДоступа;
	
	РазмерыОчереди = Новый Соответствие();
	РазмерыОчереди.Вставить("Оперативная", Модуль.РазмерОчереди(Модуль.ПриоритетыОперативной()));
	РазмерыОчереди.Вставить("Долгая", Модуль.РазмерОчереди(Модуль.ПриоритетыДолгой()));
	Возврат РазмерыОчереди;
	
КонецФункции

&НаСервере
Функция ПроверитьРегламентныеЗаданияНаСервере()
	
	ТекстПредупреждения = "";
	РеглЗадание = Неопределено;
	
	РеглЗадание = Неопределено;
	Модуль = РегистрыСведений.ОчередьОбновленияПравДоступа;
	Приоритет = ТекущийОсновнойПриоритет();
	Если Приоритет = Модуль.ПриоритетОперативная() Тогда
		РеглЗадание = РегламентныеЗадания.НайтиПредопределенное(
			Метаданные.РегламентныеЗадания.ДокументооборотОбновлениеПравДоступаОперативное);
	ИначеЕсли Приоритет = Модуль.ПриоритетДолгая() Тогда
		РеглЗадание = РегламентныеЗадания.НайтиПредопределенное(
			Метаданные.РегламентныеЗадания.ДокументооборотОбновлениеПравДоступаДолгое);
	Иначе
		ВызватьИсключение Модуль.Текст_НеПредусмотренныйПриоритет(
			ВидОчереди,
			"РегистрСведений.ОчередьОбновленияПравДоступа.ФормаСписка.ПроверитьРегламентныеЗаданияНаСервере");
	КонецЕсли;
	Если РеглЗадание = Неопределено Тогда
		// Перестраховка, штатно невозможная ситуация.
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Не удалось получить регламентное задание. Источник %1'"),
			"РегистрСведений.ОчередьОбновленияПравДоступа.ФормаСписка.ПроверитьРегламентныеЗаданияНаСервере");
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура("Состояние, УникальныйИдентификатор",
		СостояниеФоновогоЗадания.Активно, РеглЗадание.УникальныйИдентификатор);
	
	НайденныеФЗ = ФоновыеЗадания.ПолучитьФоновыеЗадания(СтруктураОтбора);
	Если НайденныеФЗ.Количество() > 0 Тогда
		ТекстПредупреждения = СтрШаблон(НСтр("ru = 'Выполняется фоновое задание ""%1"".
			|Обработка очереди вручную запрещена.'"), РеглЗадание.Метаданные.Синоним);
	КонецЕсли;
	
	Возврат ТекстПредупреждения;
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Список.УсловноеОформление.Элементы.Очистить();
	
	Элемент = Список.УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ТекстОшибки");
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("КоличествоПопытокОбработки");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТекстОшибки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ЦветОшибки = Метаданные.ЭлементыСтиля.ОшибочныеДанные;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветОшибки.Значение);
	
КонецПроцедуры

#КонецОбласти
