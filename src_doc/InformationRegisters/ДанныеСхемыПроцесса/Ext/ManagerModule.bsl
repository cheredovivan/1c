
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Записывает данные схемы.
// 
// Параметры:
//  ДанныеСхемы  - см. СхемыПроцессовКлиентСервер.СтруктураДанныхСхемыПроцесса
//  ИменаРесурсов - Строка - Имена ресурсов указываются через запятую
//  ВыполняетсяОбновлениеИБ - Булево
//
Процедура ЗаписатьДанныеСхемы(ДанныеСхемы, ИменаРесурсов = "", ВыполняетсяОбновлениеИБ = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеРесурсов = ДанныеРесурсов(ДанныеСхемы, ИменаРесурсов);
	Если ДанныеРесурсов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Схема.Установить(ДанныеСхемы.Ссылка, Истина);
	Набор.Прочитать();
	
	Запись = Неопределено;
	Если Набор.Количество() > 0 Тогда
		Запись = Набор[0];
	Иначе
		Запись = Набор.Добавить();	
	КонецЕсли;	
	
	Запись.Схема = ДанныеСхемы.Ссылка;
	ЗаполнитьЗначенияСвойств(Запись, ДанныеРесурсов);
	
	Если ВыполняетсяОбновлениеИБ Тогда
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
	Иначе	
		Набор.Записать();
	КонецЕсли;
	
КонецПроцедуры	

// Удаляет записи
// 
// Параметры:
//  СхемаСсылка - СправочникСсылка.СхемыПроцессов
//
Процедура УдалитьЗаписи(СхемаСсылка) Экспорт
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Схема.Установить(СхемаСсылка, Истина);
	Набор.Записать();
	
КонецПроцедуры

// Копирует данные схемы в другую схему.
// 
// Параметры:
//  СхемаИсточник - Справочникссылка.СхемыПроцессов
//  СхемаПриемник - Справочникссылка.СхемыПроцессов
//
Процедура СкопироватьДанныеСхемы(СхемаИсточник, СхемаПриемник) Экспорт

	// Ответственное чтение не нужно
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДанныеСхемыПроцесса.ВсеПути,
		|	ДанныеСхемыПроцесса.Длительность
		|ИЗ
		|	РегистрСведений.ДанныеСхемыПроцесса КАК ДанныеСхемыПроцесса
		|ГДЕ
		|	ДанныеСхемыПроцесса.Схема = &Схема";
	
	Запрос.УстановитьПараметр("Схема", СхемаИсточник);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.Схема = СхемаПриемник;
	Запись.ВсеПути = Новый ХранилищеЗначения(Выборка.ВсеПути.Получить());
	Запись.Длительность = Выборка.Длительность;
	
	Запись.Записать();
	
КонецПроцедуры

// Получает все пути схемы.
// 
// Параметры:
//  СхемаСсылка - СправочникСсылка.СхемыПроцессов
// Возвращаемое значение:
//  см. СхемыПроцессовСервер.ПутиСхемыПроцесса
//
Функция ВсеПутиСхемы(СхемаСсылка) Экспорт
	
	ВсеПути = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеСхемыПроцесса.ВсеПути
	|ИЗ
	|	РегистрСведений.ДанныеСхемыПроцесса КАК ДанныеСхемыПроцесса
	|ГДЕ
	|	ДанныеСхемыПроцесса.Схема = &Схема";

	Запрос.УстановитьПараметр("Схема", СхемаСсылка);

	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ВсеПути;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	ДвоичныеДанныеПутей = Выборка.ВсеПути.Получить();
	Если ТипЗнч(ДвоичныеДанныеПутей) = Тип("ДвоичныеДанные") Тогда
		ПотокДанных = ДвоичныеДанныеПутей.ОткрытьПотокДляЧтения();
		Чтение = Новый ЧтениеJSON;
		Чтение.ОткрытьПоток(ПотокДанных);
		ВсеПути = ПрочитатьJSON(Чтение, Ложь); // см.СхемыПроцессовСервер.ПутиСхемыПроцесса 
		Чтение.Закрыть();
	КонецЕсли;

	Возврат ВсеПути;
	
КонецФункции

// Получает длительность процесса схемы.
// 
// Параметры:
//  СхемаСсылка - СправочникСсылка.СхемыПроцессов
// Возвращаемое значение:
//  Число
//
Функция Длительность(СхемаСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДанныеСхемыПроцесса.Длительность
		|ИЗ
		|	РегистрСведений.ДанныеСхемыПроцесса КАК ДанныеСхемыПроцесса
		|ГДЕ
		|	ДанныеСхемыПроцесса.Схема = &Схема";
	
	Запрос.УстановитьПараметр("Схема", СхемаСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	КонецЕсли;	
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Длительность;
	
КонецФункции	

// Удаляет порцию устаревших данных.
// 
// Возвращаемое значение:
// 	Булево - Истина, если были найдены устаревшие данные, в противном случае Ложь.
// 
Функция УдалитьПорциюУстаревшихДанных() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ПараметрыСхемДляОбработокОбъектов.Схема КАК Схема
		|ИЗ
		|	Справочник.НастройкиОбработкиВидовОбъектов КАК НастройкиОбработкиВидовОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПараметрыСхемДляОбработокОбъектов КАК ПараметрыСхемДляОбработокОбъектов
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеСхемыПроцесса КАК ДанныеСхемыПроцесса
		|			ПО ПараметрыСхемДляОбработокОбъектов.Схема = ДанныеСхемыПроцесса.Схема
		|		ПО НастройкиОбработкиВидовОбъектов.Ссылка = ПараметрыСхемДляОбработокОбъектов.ВладелецСхемы
		|		И НЕ ПараметрыСхемДляОбработокОбъектов.АвтоформируемаяСхема
		|		И (НастройкиОбработкиВидовОбъектов.ДействуетПо <> ДАТАВРЕМЯ(1, 1, 1)
		|		И НастройкиОбработкиВидовОбъектов.ДействуетПо < &ТекущаяДата)
		|		И НЕ НастройкиОбработкиВидовОбъектов.ПометкаУдаления
		|ГДЕ
		|	НЕ ДанныеСхемыПроцесса.Схема ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ПараметрыСхемДляКомплексныхПроцессов.Схема
		|ИЗ
		|	Справочник.ПараметрыСхемДляКомплексныхПроцессов КАК ПараметрыСхемДляКомплексныхПроцессов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШаблоныКомплексныхБизнесПроцессов КАК ШаблоныКомплексныхБизнесПроцессов
		|		ПО ПараметрыСхемДляКомплексныхПроцессов.ВладелецСхемы = ШаблоныКомплексныхБизнесПроцессов.Ссылка
		|		И ПараметрыСхемДляКомплексныхПроцессов.Схема = ШаблоныКомплексныхБизнесПроцессов.Схема
		|		И ШаблоныКомплексныхБизнесПроцессов.ПометкаУдаления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеСхемыПроцесса КАК ДанныеСхемыПроцесса
		|		ПО ПараметрыСхемДляКомплексныхПроцессов.Схема = ДанныеСхемыПроцесса.Схема
		|ГДЕ
		|	НЕ ДанныеСхемыПроцесса.Схема ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ПараметрыСхемДляКомплексныхПроцессов.Схема
		|ИЗ
		|	Справочник.ПараметрыСхемДляКомплексныхПроцессов КАК ПараметрыСхемДляКомплексныхПроцессов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК КомплексныйПроцесс
		|		ПО ПараметрыСхемДляКомплексныхПроцессов.ВладелецСхемы = КомплексныйПроцесс.Ссылка
		|		И ПараметрыСхемДляКомплексныхПроцессов.Схема = КомплексныйПроцесс.Схема
		|		И (КомплексныйПроцесс.Завершен
		|		ИЛИ КомплексныйПроцесс.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Прерван)
		|		ИЛИ КомплексныйПроцесс.ПометкаУдаления)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеСхемыПроцесса КАК ДанныеСхемыПроцесса
		|		ПО ПараметрыСхемДляКомплексныхПроцессов.Схема = ДанныеСхемыПроцесса.Схема
		|ГДЕ
		|	НЕ ДанныеСхемыПроцесса.Схема ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ПараметрыСхемДляОбработокОбъектов.Схема
		|ИЗ
		|	Справочник.ПараметрыСхемДляОбработокОбъектов КАК ПараметрыСхемДляОбработокОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбработкиОбъектов КАК ОбработкиОбъектов
		|		ПО ПараметрыСхемДляОбработокОбъектов.ВладелецСхемы = ОбработкиОбъектов.Ссылка
		|		И НЕ ПараметрыСхемДляОбработокОбъектов.АвтоформируемаяСхема
		|		И ОбработкиОбъектов.Состояние В (ЗНАЧЕНИЕ(Перечисление.СостоянияОбработкиОбъектов.Завершена),
		|			ЗНАЧЕНИЕ(Перечисление.СостоянияОбработкиОбъектов.Прервана))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеСхемыПроцесса КАК ДанныеСхемыПроцесса
		|		ПО ПараметрыСхемДляОбработокОбъектов.Схема = ДанныеСхемыПроцесса.Схема
		|ГДЕ
		|	НЕ ДанныеСхемыПроцесса.Схема ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбработаноЗаписей = Выборка.Количество();
	ОбработаноУспешно = 0;
	ОбработаноСОшибками = 0;
	
	Пока Выборка.Следующий() Цикл
		
		СсылкаНаСхемуСтрокой = ПолучитьНавигационнуюСсылку(Выборка.Схема);
		
		Попытка
			УдалитьЗаписи(Выборка.Схема);
			РегистрыСведений.ПредшественникиДействийПоПутямСхемыПроцесса.УдалитьЗаписи(Выборка.Схема);
			ОбработаноУспешно = ОбработаноУспешно + 1;
		Исключение
			ОбработаноСОшибками = ОбработаноСОшибками + 1;
			
			ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru='Ошибка при удалении неактуальных кэширующих данных по схеме процесса'"),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Справочники.СхемыПроцессов,
				СсылкаНаСхемуСтрокой,
				ТекстОшибки);
			
			Продолжить;
		КонецПопытки;
			
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru='Удаление устаревших кэширующих данных схем процессов'"), 
		УровеньЖурналаРегистрации.Информация,
		Метаданные.Справочники.СхемыПроцессов, , 
		СтрШаблон(
			НСтр("ru = 'Процедура завершена.
				|Обработано записей: %1
				|Из них:
				|	Успешно - %2
				|	С ошибками - %3'"),
			ОбработаноЗаписей, ОбработаноУспешно, ОбработаноСОшибками));
	
	
	Возврат ОбработаноЗаписей > 0;
		
КонецФункции

#Область ПерейтиНаВерсию_3_0_19_12

// Обработать данные для перехода на версию 3.0.19.12
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеДляПереходаНаВерсию_3_0_19_12(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.Справочники.СхемыПроцессов;
	
	ПолныеИменаОбъектов = Новый Массив;
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	ПолныеИменаОбъектов.Добавить(ПолноеИмяОбъекта);
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	ПараметрыВыборки.ПолныеИменаОбъектов = СтрСоединить(ПолныеИменаОбъектов, ",");
	
	ЕстьДанныеКОбработке = Истина;
	СсылкаКОбновлению = Справочники.СхемыПроцессов.ПустаяСсылка();
	Пока ЕстьДанныеКОбработке Цикл		
		
		ЗапросДлярегистрации = ЗапросДляРегистрацииСхемДляОбработки(СсылкаКОбновлению);
		
		//@skip-check query-in-loop
		ДанныеКОбработке = ЗапросДлярегистрации.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		КоличествоКОбработке = ДанныеКОбработке.Количество();
		ЕстьДанныеКОбработке = КоличествоКОбработке > 0;
		Если ЕстьДанныеКОбработке Тогда
			СсылкаКОбновлению = ДанныеКОбработке[КоличествоКОбработке - 1];
		КонецЕсли;		
		
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДанныеКОбработке);
		
	КонецЦикла;
	
КонецПроцедуры	

// Обработать данные для перехода на версию 3.0.19.12
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ОбработатьДанныеДляПереходаНаВерсию_3_0_19_12(Параметры) Экспорт
	
	ВыполняетсяОбновление = ОбновлениеИнформационнойБазы.ВыполняетсяОбновлениеИнформационнойБазы();
	МетаданныеОбъекта = Метаданные.Справочники.СхемыПроцессов;
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	
	СхемыДляОбработки = Новый Массив;
	ТаблицаОбновления = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Для Каждого СтрокаКобновлению Из ТаблицаОбновления Цикл
		СхемыДляОбработки.Добавить(СтрокаКобновлению.Ссылка);
	КонецЦикла;
	
	Запрос = ЗапросДляОбработки(СхемыДляОбработки);
	РезультатЗапроса = Запрос.Выполнить(); // РезультатЗапроса
	ВыборкаДанных = РезультатЗапроса.Выбрать();
	
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;

	Пока ВыборкаДанных.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			ДанныеСхемы = Справочники.СхемыПроцессов.ДанныеСхемыПроцесса(ВыборкаДанных.Схема);
			ДанныеСхемы.ПутиИзменены = Истина;		
			ДаныеПараметровСхемы = Новый Структура;
			Если ТипЗнч(ВыборкаДанных.ПараметрыСхемы) = Тип("СправочникСсылка.ПараметрыСхемДляКомплексныхПроцессов")
				Тогда
					ДаныеПараметровСхемы = Справочники.ПараметрыСхемДляКомплексныхПроцессов.ДанныеПараметровСхемы(
						ВыборкаДанных.ПараметрыСхемы);
					ДанныеСхемы.РассчитатьОтносительныйСрок = Не ВыборкаДанных.ПроцессСтартован;
			КонецЕсли;
			
			Если ДанныеСхемы.РассчитатьОтносительныйСрок И ЗначениеЗаполнено(ВыборкаДанных.Процесс) Тогда
				ДанныеСхемы.СрокиЭлементов = СхемыПроцессовСервер.СрокиЭлементовПоДаннымСхемы(ДаныеПараметровСхемы,
					ВыборкаДанных.Процесс);
				ДанныеСхемы.СмещенияДатыОтсчета = СрокиИсполненияПроцессовКОРП.СмещенияДатыОтсчета(
					ВыборкаДанных.Процесс);
			КонецЕсли;

			Кэш = СхемыПроцессовСервер.РезультатРасчетаКэширующихДанныхСхемы(ДанныеСхемы, ДаныеПараметровСхемы);
			ЗаполнитьЗначенияСвойств(ДанныеСхемы, Кэш);
			СхемыПроцессовСервер.ЗаписатьКэшСхемы(ДанныеСхемы, Кэш.ИменаДанныхДляЗаписи, Кэш.ТаблицаПредшественников,
				ВыполняетсяОбновление);
			
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ВыборкаДанных.Схема);
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось заполнить кэширующие данные схемы %1 по причине: %2'"),
				ВыборкаДанных.Схема, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка, МетаданныеОбъекта, , ТекстСообщения);
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
				
		КонецПопытки;

	КонецЦикла;
		
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
		ШаблонСообщения = НСтр("ru = 'Не удалось обработать некоторые схемы процессов для формирования кэширующих данных(пропущены): %1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ШаблонСообщения = НСтр("ru = 'Обработана очередная порция схем процессов для формирования кэширующих данных: %1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ОбъектовОбработано);
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			МетаданныеОбъекта,, ТекстСообщения);
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов =
		Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ОбъектовОбработано;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь,
		ПолноеИмяОбъекта);
			
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	
// Возвращает сериализованные пути схемы, помещенные в хранилище значений
// 
// Параметры:
//  ВсеПути  - см. СхемыПроцессовСервер.ПутиСхемыПроцесса
// 
// Возвращаемое значение:
//  ХранилищеЗначения
//
Функция ХранилищеВсехПутей(ВсеПути)
	
	Поток = Новый ПотокВПамяти();
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.ОткрытьПоток(Поток);
	ЗаписатьJSON(ЗаписьJSON, ВсеПути);
	ЗаписьJSON.Закрыть();
	
	Возврат Новый ХранилищеЗначения(Поток.ЗакрытьИПолучитьДвоичныеДанные(), Новый СжатиеДанных(9));
	
КонецФункции	

// Возвращает структуру с именами и значениями ресурсов для записи.
// 
// Параметры:
//  ДанныеСхемы  - см. СхемыПроцессовКлиентСервер.СтруктураДанныхСхемыПроцесса
//  ИменаРесурсов - Строка - Имена ресурсов
// 
// Возвращаемое значение:
//  Структура:
//   ВсеПути - ХранилищеЗначения
//   Длительность - Число
//  
Функция ДанныеРесурсов(ДанныеСхемы, ИменаРесурсов)	
	
	Если Не ЗначениеЗаполнено(ИменаРесурсов) Тогда
		СтруктураРесурсов = СтруктураРесурсов();
		СтруктураРесурсов.ВсеПути = ХранилищеВсехПутей(ДанныеСхемы.ВсеПути);
		СтруктураРесурсов.Длительность = ДанныеСхемы.Длительность;
		Возврат СтруктураРесурсов;
	КонецЕсли; 
	
	МассивИмен = СтрРазделить(ИменаРесурсов, ",");	
	СтруктураРесурсов = Новый Структура;
	
	Для Каждого Имя Из МассивИмен Цикл
		
		Если Имя = "ВсеПути" Тогда
			СтруктураРесурсов.Вставить(Имя, ХранилищеВсехПутей(ДанныеСхемы.ВсеПути));
		ИначеЕсли Имя = "Длительность" Тогда
			СтруктураРесурсов.Вставить(Имя, ДанныеСхемы.Длительность);
		КонецЕсли;
			
	КонецЦикла;	
	
	Возврат СтруктураРесурсов;
	
КонецФункции	

Функция СтруктураРесурсов()
	
	Структура = Новый Структура;
	Структура.Вставить("ВсеПути", Неопределено);
	Структура.Вставить("Длительность", 0);
	
	Возврат Структура;
	
КонецФункции	

#Область ПерейтиНаВерсию_3_0_19_12

// Запрос для обработки схем для расчета и записи кэширующих данных.
// 
// Параметры:
//  СхемыДляОбработки - Массив из СправочникСсылка.СхемыПроцессов - 
// 
// Возвращаемое значение:
//  Запрос
//
Функция ЗапросДляОбработки(СхемыДляОбработки)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПараметрыСхемДляОбработокОбъектов.Схема КАК Схема,
		|	НЕОПРЕДЕЛЕНО КАК Процесс,
		|	ПараметрыСхемДляОбработокОбъектов.Ссылка КАК ПараметрыСхемы,
		|	ЛОЖЬ КАК ПроцессСтартован
		|ИЗ
		|	Справочник.ПараметрыСхемДляОбработокОбъектов КАК ПараметрыСхемДляОбработокОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиОбработкиВидовОбъектов КАК НастройкиОбработкиВидовОбъектов
		|		ПО ПараметрыСхемДляОбработокОбъектов.ВладелецСхемы = НастройкиОбработкиВидовОбъектов.Ссылка
		|		И ПараметрыСхемДляОбработокОбъектов.Схема В (&СхемыДляОбработки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПараметрыСхемДляОбработокОбъектов.Схема,
		|	НЕОПРЕДЕЛЕНО,
		|	ПараметрыСхемДляОбработокОбъектов.Ссылка,
		|	ЛОЖЬ
		|ИЗ
		|	Справочник.ПараметрыСхемДляОбработокОбъектов КАК ПараметрыСхемДляОбработокОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбработкиОбъектов КАК ОбработкиОбъектов
		|		ПО ПараметрыСхемДляОбработокОбъектов.ВладелецСхемы = ОбработкиОбъектов.Ссылка
		|		И ПараметрыСхемДляОбработокОбъектов.Схема В (&СхемыДляОбработки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПараметрыСхемДляКомплексныхПроцессов.Схема,
		|	ШаблоныКомплексныхБизнесПроцессов.Ссылка,
		|	ПараметрыСхемДляКомплексныхПроцессов.Ссылка,
		|	ЛОЖЬ
		|ИЗ
		|	Справочник.ПараметрыСхемДляКомплексныхПроцессов КАК ПараметрыСхемДляКомплексныхПроцессов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШаблоныКомплексныхБизнесПроцессов КАК ШаблоныКомплексныхБизнесПроцессов
		|		ПО ПараметрыСхемДляКомплексныхПроцессов.ВладелецСхемы = ШаблоныКомплексныхБизнесПроцессов.Ссылка
		|		И ПараметрыСхемДляКомплексныхПроцессов.Схема В (&СхемыДляОбработки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПараметрыСхемДляКомплексныхПроцессов.Схема,
		|	КомплексныйПроцесс.Ссылка,
		|	ПараметрыСхемДляКомплексныхПроцессов.Ссылка,
		|	КомплексныйПроцесс.ДатаНачала <> ДАТАВРЕМЯ(1, 1, 1)
		|ИЗ
		|	Справочник.ПараметрыСхемДляКомплексныхПроцессов КАК ПараметрыСхемДляКомплексныхПроцессов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК КомплексныйПроцесс
		|		ПО ПараметрыСхемДляКомплексныхПроцессов.ВладелецСхемы = КомплексныйПроцесс.Ссылка
		|		И ПараметрыСхемДляКомплексныхПроцессов.Схема В (&СхемыДляОбработки)";
	
	Запрос.УстановитьПараметр("СхемыДляОбработки", СхемыДляОбработки);
	
	Возврат Запрос;
	
КонецФункции	

// Запрос для регистрации схем процессов для расчета и записи кэширующих даннх.
// 
// Параметры:
//  СсылкаКОбновлению - СправочникСсылка.СхемыПроцессов
// 
// Возвращаемое значение:
//  Запрос
//
Функция ЗапросДляРегистрацииСхемДляОбработки(СсылкаКОбновлению)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПараметрыСхемДляОбработокОбъектов.Схема
		|ПОМЕСТИТЬ ВТ_Схемы
		|ИЗ
		|	Справочник.ПараметрыСхемДляОбработокОбъектов КАК ПараметрыСхемДляОбработокОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиОбработкиВидовОбъектов КАК НастройкиОбработкиВидовОбъектов
		|		ПО ПараметрыСхемДляОбработокОбъектов.ВладелецСхемы = НастройкиОбработкиВидовОбъектов.Ссылка
		|		И НЕ ПараметрыСхемДляОбработокОбъектов.АвтоформируемаяСхема
		|		И НЕ ПараметрыСхемДляОбработокОбъектов.ПометкаУдаления
		|		И НЕ НастройкиОбработкиВидовОбъектов.ПометкаУдаления
		|		И (НастройкиОбработкиВидовОбъектов.ДействуетПо >= &ТекущаяДата
		|		ИЛИ НастройкиОбработкиВидовОбъектов.ДействуетПо = ДАТАВРЕМЯ(1, 1, 1))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеСхемыПроцесса КАК ДанныеСхемыПроцесса
		|		ПО ПараметрыСхемДляОбработокОбъектов.Схема = ДанныеСхемыПроцесса.Схема
		|ГДЕ
		|	ДанныеСхемыПроцесса.Схема ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПараметрыСхемДляОбработокОбъектов.Схема
		|ИЗ
		|	Справочник.ПараметрыСхемДляОбработокОбъектов КАК ПараметрыСхемДляОбработокОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбработкиОбъектов КАК ОбработкиОбъектов
		|		ПО ПараметрыСхемДляОбработокОбъектов.ВладелецСхемы = ОбработкиОбъектов.Ссылка
		|		И НЕ ПараметрыСхемДляОбработокОбъектов.АвтоформируемаяСхема
		|		И НЕ ПараметрыСхемДляОбработокОбъектов.ПометкаУдаления
		|		И НЕ ОбработкиОбъектов.ПометкаУдаления
		|		И ОбработкиОбъектов.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияОбработкиОбъектов.Завершена)
		|		И ОбработкиОбъектов.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияОбработкиОбъектов.Прервана)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеСхемыПроцесса КАК ДанныеСхемыПроцесса
		|		ПО ПараметрыСхемДляОбработокОбъектов.Схема = ДанныеСхемыПроцесса.Схема
		|ГДЕ
		|	ДанныеСхемыПроцесса.Схема ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПараметрыСхемДляКомплексныхПроцессов.Схема
		|ИЗ
		|	Справочник.ПараметрыСхемДляКомплексныхПроцессов КАК ПараметрыСхемДляКомплексныхПроцессов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШаблоныКомплексныхБизнесПроцессов КАК ШаблоныКомплексныхБизнесПроцессов
		|		ПО ПараметрыСхемДляКомплексныхПроцессов.ВладелецСхемы = ШаблоныКомплексныхБизнесПроцессов.Ссылка
		|		И НЕ ПараметрыСхемДляКомплексныхПроцессов.ПометкаУдаления
		|		И НЕ ШаблоныКомплексныхБизнесПроцессов.ПометкаУдаления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеСхемыПроцесса КАК ДанныеСхемыПроцесса
		|		ПО ПараметрыСхемДляКомплексныхПроцессов.Схема = ДанныеСхемыПроцесса.Схема
		|ГДЕ
		|	ДанныеСхемыПроцесса.Схема ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПараметрыСхемДляКомплексныхПроцессов.Схема
		|ИЗ
		|	Справочник.ПараметрыСхемДляКомплексныхПроцессов КАК ПараметрыСхемДляКомплексныхПроцессов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.КомплексныйПроцесс КАК КомплексныйПроцесс
		|		ПО ПараметрыСхемДляКомплексныхПроцессов.ВладелецСхемы = КомплексныйПроцесс.Ссылка
		|		И НЕ ПараметрыСхемДляКомплексныхПроцессов.ПометкаУдаления
		|		И НЕ КомплексныйПроцесс.ПометкаУдаления
		|		И КомплексныйПроцесс.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Прерван)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеСхемыПроцесса КАК ДанныеСхемыПроцесса
		|		ПО ПараметрыСхемДляКомплексныхПроцессов.Схема = ДанныеСхемыПроцесса.Схема
		|ГДЕ
		|	ДанныеСхемыПроцесса.Схема ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|	ВТ_Схемы.Схема КАК Ссылка
		|ИЗ
		|	ВТ_Схемы КАК ВТ_Схемы
		|ГДЕ
		|	ВТ_Схемы.Схема > &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка";
		
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Ссылка", СсылкаКОбновлению);
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти
	
#КонецОбласти

#КонецЕсли
