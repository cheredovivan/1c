#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

#Область ОбработчикиОбновления

// Регистрирует данные к обновлению в плане обмена ОбновлениеИнформационнойБазы.
//
// Параметры:
//	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке.
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию_3_0_19_15(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.РегистрыСведений.ОтметкиВремениСсылочныхОбъектов;
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяОбъекта;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("ТипКлюча");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("ТипКлюча");
	ПараметрыВыборки.ИмяИзмеренияДляОтбора = "ТипКлюча";
	ПараметрыВыборки.МаксимумВыборки = 1;
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяОбъекта;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 20000
		|	ДанныеТаблицы.ТипКлюча КАК ТипКлюча,
		|	ДанныеТаблицы.Граница КАК Граница
		|ПОМЕСТИТЬ ДанныеТаблицы
		|ИЗ
		|	#ПолноеИмяОбъекта КАК ДанныеТаблицы
		|ГДЕ
		|	ДанныеТаблицы.Граница > &Граница
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДанныеТаблицы.Граница
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(МАКСИМУМ(Таблица.Граница), НЕОПРЕДЕЛЕНО) КАК Граница
		|ПОМЕСТИТЬ МаксимальнаяГраница
		|ИЗ
		|	ДанныеТаблицы КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.ТипКлюча КАК ТипКлюча
		|ИЗ
		|	(ВЫБРАТЬ
		|		Таблица.ТипКлюча КАК ТипКлюча
		|	ИЗ
		|		ДанныеТаблицы КАК Таблица
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксимальнаяГраница КАК МаксимальнаяГраница
		|			ПО Таблица.Граница <> МаксимальнаяГраница.Граница
		|			ЛЕВОЕ СОЕДИНЕНИЕ #ИзмененияОбъекта КАК ТаблицаИзменений
		|			ПО Таблица.ТипКлюча = ТаблицаИзменений.ТипКлюча
		|	ГДЕ
		|		ТаблицаИзменений.ТипКлюча ЕСТЬ NULL
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Таблица.ТипКлюча
		|	ИЗ
		|		#ПолноеИмяОбъекта КАК Таблица
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксимальнаяГраница КАК МаксимальнаяГраница
		|			ПО Таблица.Граница = МаксимальнаяГраница.Граница
		|			ЛЕВОЕ СОЕДИНЕНИЕ #ИзмененияОбъекта КАК ТаблицаИзменений
		|			ПО Таблица.ТипКлюча = ТаблицаИзменений.ТипКлюча
		|	ГДЕ
		|		ТаблицаИзменений.ТипКлюча ЕСТЬ NULL) КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МаксимальнаяГраница.Граница КАК Граница
		|ИЗ
		|	МаксимальнаяГраница КАК МаксимальнаяГраница");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ПолноеИмяОбъекта", ПолноеИмяОбъекта);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИзмененияОбъекта", СтрШаблон("%1.Изменения", ПолноеИмяОбъекта));
	
	Граница = -1;
	Пока Истина Цикл
		Запрос.УстановитьПараметр("Граница", Граница);
		
		НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
		РезультатЗапроса = Запрос.ВыполнитьПакет(); //@skip-check query-in-loop
		КонецЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
		
		ВыборкаГраница = РезультатЗапроса[РезультатЗапроса.ВГраница()].Выбрать();
		Если Не ВыборкаГраница.Следующий() Или ВыборкаГраница.Граница = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			МетаданныеОбъекта,,
			СтрШаблон(Нстр("ru = 'Обработчик обновления: %1.
								|Выбрана порция данных: %2 - %3 (%4 мсек)'"),
				"ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию_3_0_19_15",
				Граница,
				ВыборкаГраница.Граница,
				КонецЗамера - НачалоЗамера));
		
		Граница = ВыборкаГраница.Граница;
		
		Если РезультатЗапроса[РезультатЗапроса.ВГраница() - 1].Пустой() Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеКОбработке = РезультатЗапроса[РезультатЗапроса.ВГраница() - 1].Выгрузить();
		
		НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
		
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДанныеКОбработке, ДополнительныеПараметры);
		
		КонецЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			МетаданныеОбъекта,,
			СтрШаблон(Нстр("ru = 'Обработчик обновления: %1.
								|Зарегистрирована порция данных для обработки: %2 (%3 мсек)'"),
				"ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию_3_0_19_15",
				ДанныеКОбработке.Количество(),
				КонецЗамера - НачалоЗамера));
		
	КонецЦикла;
	
КонецПроцедуры

// Обрабатывает данные, зарегистрированные в плане обмена ОбновлениеИнформационнойБазы.
//
// Параметры:
//	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию_3_0_19_15(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.РегистрыСведений.ОтметкиВремениСсылочныхОбъектов;
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();

	ДанныеДляОбновления = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если Не ЗначениеЗаполнено(ДанныеДляОбновления) Тогда
		Параметры.ОбработкаЗавершена =
			ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Данные.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	Данные.ТипКлюча КАК ТипКлюча,
		|	Данные.Граница КАК Граница
		|ПОМЕСТИТЬ ДанныеДляОбновления
		|ИЗ
		|	&ДанныеДляОбновления КАК Данные";
	Запрос.УстановитьПараметр("ДанныеДляОбновления", ДанныеДляОбновления);
	Запрос.Выполнить();
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДляОбновления.ТипКлюча КАК ТипКлюча
		|ИЗ
		|	ДанныеДляОбновления КАК ДанныеДляОбновления
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТипКлюча";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Попытка
			ЗначениеКлюча = Новый Структура;
			ЗначениеКлюча.Вставить("ТипКлюча", Выборка.ТипКлюча);
			КлючБлокировки = СоздатьКлючЗаписи(ЗначениеКлюча);
			ЗаблокироватьДанныеДляРедактирования(КлючБлокировки);
		Исключение
			// Блокировка используется для распределения по потокам и исключения обновления одних и тех же данных.
			Продолжить;
		КонецПопытки;
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	&ОбрабатываемыйТип КАК ТипКлюча
			|ПОМЕСТИТЬ ДанныеДляОбновленияОбрабатываемыйТип";
		Запрос.УстановитьПараметр("ОбрабатываемыйТип", Выборка.ТипКлюча);
		Запрос.Выполнить(); //@skip-check query-in-loop
		
		ЕстьДанныеДляОбработки = Истина;
		Пока ЕстьДанныеДляОбработки Цикл
			
			Запрос.Текст =
				"ВЫБРАТЬ ПЕРВЫЕ 1000
				|	ДанныеРегистра.ИдентификаторКлюча КАК ИдентификаторКлюча,
				|	ДанныеРегистра.ТипКлюча КАК ТипКлюча
				|ПОМЕСТИТЬ ДублирующиесяКлючи
				|ИЗ
				|	#ПолноеИмяОбъекта КАК ДанныеРегистра
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДляОбновленияОбрабатываемыйТип КАК ДанныеДляОбновленияОбрабатываемыйТип
				|		ПО ДанныеРегистра.ТипКлюча = ДанныеДляОбновленияОбрабатываемыйТип.ТипКлюча
				|
				|СГРУППИРОВАТЬ ПО
				|	ДанныеРегистра.ИдентификаторКлюча,
				|	ДанныеРегистра.ТипКлюча
				|
				|ИМЕЮЩИЕ
				|	КОЛИЧЕСТВО(ДанныеРегистра.Граница) > 1
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ДублирующиесяКлючи.ИдентификаторКлюча КАК ИдентификаторКлюча,
				|	ДублирующиесяКлючи.ТипКлюча КАК ТипКлюча
				|ИЗ
				|	ДублирующиесяКлючи КАК ДублирующиесяКлючи";
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ПолноеИмяОбъекта", ПолноеИмяОбъекта);
			
			ДанныеДляОбновления = Запрос.Выполнить().Выгрузить(); //@skip-check query-in-loop
			КоличествоДанныхДляОбновления = ДанныеДляОбновления.Количество();
			ЕстьДанныеДляОбработки = КоличествоДанныхДляОбновления > 0;
			
			Если ЕстьДанныеДляОбработки = Истина Тогда
				
				НачатьТранзакцию();
				Попытка
					Блокировка = Новый БлокировкаДанных();
					ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
					ЭлементБлокировки.ИсточникДанных = ДанныеДляОбновления;
					ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ИдентификаторКлюча", "ИдентификаторКлюча");
					ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипКлюча", "ТипКлюча");
					ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
					Блокировка.Заблокировать();
					
					Запрос.Текст =
						"ВЫБРАТЬ
						|	ДанныеРегистра.ИдентификаторКлюча КАК ИдентификаторКлюча,
						|	ДанныеРегистра.ТипКлюча КАК ТипКлюча,
						|	ДанныеРегистра.Граница КАК Граница,
						|	ДанныеРегистра.Отметка КАК Отметка
						|ПОМЕСТИТЬ ДанныеРегистра
						|ИЗ
						|	#ПолноеИмяОбъекта КАК ДанныеРегистра
						|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДублирующиесяКлючи КАК ДублирующиесяКлючи
						|		ПО ДанныеРегистра.ИдентификаторКлюча = ДублирующиесяКлючи.ИдентификаторКлюча
						|			И ДанныеРегистра.ТипКлюча = ДублирующиесяКлючи.ТипКлюча
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|ВЫБРАТЬ
						|	ДанныеРегистра.ИдентификаторКлюча КАК ИдентификаторКлюча,
						|	ДанныеРегистра.ТипКлюча КАК ТипКлюча,
						|	МАКСИМУМ(ДанныеРегистра.Отметка) КАК Отметка
						|ПОМЕСТИТЬ АктуальныеОтметкиВремени
						|ИЗ
						|	ДанныеРегистра КАК ДанныеРегистра
						|
						|СГРУППИРОВАТЬ ПО
						|	ДанныеРегистра.ИдентификаторКлюча,
						|	ДанныеРегистра.ТипКлюча
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|ВЫБРАТЬ
						|	ДанныеРегистра.ИдентификаторКлюча КАК ИдентификаторКлюча,
						|	ДанныеРегистра.ТипКлюча КАК ТипКлюча,
						|	МАКСИМУМ(ДанныеРегистра.Граница) КАК Граница
						|ПОМЕСТИТЬ АктуальныеЗаписи
						|ИЗ
						|	ДанныеРегистра КАК ДанныеРегистра
						|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АктуальныеОтметкиВремени КАК АктуальныеОтметкиВремени
						|		ПО ДанныеРегистра.ИдентификаторКлюча = АктуальныеОтметкиВремени.ИдентификаторКлюча
						|			И ДанныеРегистра.ТипКлюча = АктуальныеОтметкиВремени.ТипКлюча
						|			И ДанныеРегистра.Отметка = АктуальныеОтметкиВремени.Отметка
						|
						|СГРУППИРОВАТЬ ПО
						|	ДанныеРегистра.ИдентификаторКлюча,
						|	ДанныеРегистра.ТипКлюча
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|ВЫБРАТЬ
						|	ДанныеРегистра.ИдентификаторКлюча КАК ИдентификаторКлюча,
						|	ДанныеРегистра.ТипКлюча КАК ТипКлюча,
						|	ДанныеРегистра.Граница КАК Граница
						|ИЗ
						|	ДанныеРегистра КАК ДанныеРегистра
						|		ЛЕВОЕ СОЕДИНЕНИЕ АктуальныеЗаписи КАК АктуальныеЗаписи
						|		ПО ДанныеРегистра.ИдентификаторКлюча = АктуальныеЗаписи.ИдентификаторКлюча
						|			И ДанныеРегистра.ТипКлюча = АктуальныеЗаписи.ТипКлюча
						|			И ДанныеРегистра.Граница = АктуальныеЗаписи.Граница
						|ГДЕ
						|	АктуальныеЗаписи.ИдентификаторКлюча ЕСТЬ NULL";
					Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ПолноеИмяОбъекта", ПолноеИмяОбъекта);
					
					ДанныеДляУдаления = Запрос.Выполнить().Выгрузить(); //@skip-check query-in-loop
					
					Если ДанныеДляУдаления.Количество() Тогда
						НаборЗаписей = СоздатьНаборЗаписей();
						НаборЗаписей.Загрузить(ДанныеДляУдаления);
						ОтметкиВремени.ЗаписатьНабор(НаборЗаписей, РежимЗамещения.Удаление, Истина);
					КонецЕсли;
					
					ЗафиксироватьТранзакцию();
					
					Запрос.Текст =
						"УНИЧТОЖИТЬ ДанныеРегистра
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|УНИЧТОЖИТЬ АктуальныеОтметкиВремени
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|УНИЧТОЖИТЬ АктуальныеЗаписи";
					Запрос.Выполнить(); //@skip-check query-in-loop
					
				Исключение
					
					ОтменитьТранзакцию();
					
					Если ТранзакцияАктивна() Тогда
						ВызватьИсключение;
					КонецЕсли;
					
					ЗаписьЖурналаРегистрации(
						ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
						УровеньЖурналаРегистрации.Ошибка,
						МетаданныеОбъекта,,
						ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
					ВызватьИсключение СтрШаблон(
						НСтр("ru = 'Не удалось обработать некоторые объекты (пропущены): %1'"),
						КоличествоДанныхДляОбновления);
					
				КонецПопытки;
				
				ЗаписьЖурналаРегистрации(
					ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
					УровеньЖурналаРегистрации.Информация,
					МетаданныеОбъекта,,
					СтрШаблон(НСтр("ru = 'Обработана очередная порция: %1'"), КоличествоДанныхДляОбновления));
					
			Иначе
				
				ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
				ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
				ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяОбъекта;
				
				Запрос.Текст =
					"ВЫБРАТЬ
					|	ОбработанныеДанные.ТипКлюча КАК ТипКлюча
					|ИЗ
					|	ДанныеДляОбновленияОбрабатываемыйТип КАК ОбработанныеДанные";
				ОбработанныеДанные = Запрос.Выполнить().Выгрузить(); //@skip-check query-in-loop
				
				Если ОбработанныеДанные.Количество() > 0 Тогда
					ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(
						ОбработанныеДанные, ДополнительныеПараметры);
					
					Параметры.ПрогрессВыполнения.ОбработаноОбъектов  =
						Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ОбработанныеДанные.Количество();
				КонецЕсли;
				
			КонецЕсли;
			
			Запрос.Текст =
				"УНИЧТОЖИТЬ ДублирующиесяКлючи";
			Запрос.Выполнить(); //@skip-check query-in-loop
			
		КонецЦикла;
		
		Запрос.Текст =
			"УНИЧТОЖИТЬ ДанныеДляОбновленияОбрабатываемыйТип";
		Запрос.Выполнить(); //@skip-check query-in-loop
		
	КонецЦикла;
	
	МенеджерВременныхТаблиц.Закрыть();
	МенеджерВременныхТаблиц = Неопределено;
	
	Параметры.ОбработкаЗавершена =
		ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

// Удаляет порцию устаревших данных.
//
// Возвращаемое значение:
//	Булево - Истина, если были найдены устаревшие данные, в противном случае Ложь.
//
Функция УдалитьПорциюУстаревшихДанных() Экспорт
	
	//если отметки времени еще используем - не чистим.  
	ИспользоватьОтметкиВремени = Константы.ИспользоватьОтметкиВремени.Получить();
	Если ИспользоватьОтметкиВремени Тогда
		Возврат Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 10000
		|	ОтметкиВремениСсылочныхОбъектов.Граница КАК Граница,
		|	ОтметкиВремениСсылочныхОбъектов.ИдентификаторКлюча КАК ИдентификаторКлюча,
		|	ОтметкиВремениСсылочныхОбъектов.ТипКлюча КАК ТипКлюча
		|ИЗ
		|	РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремениСсылочныхОбъектов
		|
		|УПОРЯДОЧИТЬ ПО
		|	Граница");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл 
		
		НаборЗаписей = СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Граница.Установить(Выборка.Граница);
		НаборЗаписей.Отбор.ИдентификаторКлюча.Установить(Выборка.ИдентификаторКлюча);
		НаборЗаписей.Отбор.ТипКлюча.Установить(Выборка.ТипКлюча);
		
		НаборЗаписей.Записать(); // стираем
		
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru='Удаление устаревших данных'"), 
		УровеньЖурналаРегистрации.Информация,
		Метаданные.РегистрыСведений.ОтметкиВремениСсылочныхОбъектов,, 
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедура завершена успешно, обработано %1 записей'"), Выборка.Количество()));
	
	Возврат Выборка.Количество() > 0;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли