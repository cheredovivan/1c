#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает данные контроля в реестре конкретного пользователя.
// 
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи
//  КонтрольСсылка - СправочникСсылка.Контроль
// 
// Возвращаемое значение:
//  - Неопределено
//  - Структура
// 
Функция ДанныеКонтроляВРеестре(Пользователь, КонтрольСсылка) Экспорт
	
	ДанныеКонтроляВРеестре = Неопределено;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РеестрКонтролей.Пользователь КАК Пользователь,
		|	РеестрКонтролей.ДатаСортировки КАК ДатаСортировки,
		|	РеестрКонтролей.Основание КАК Основание,
		|	РеестрКонтролей.НаКонтроле КАК НаКонтроле,
		|	РеестрКонтролей.Просрочено КАК Просрочено,
		|	РеестрКонтролей.ДатаПостановкиНаКонтроль КАК ДатаПостановкиНаКонтроль,
		|	РеестрКонтролей.ИсполнителиСтрокой КАК ИсполнителиСтрокой,
		|	РеестрКонтролей.Контролер КАК Контролер,
		|	РеестрКонтролей.ЧтоКонтролируем КАК ЧтоКонтролируем,
		|	РеестрКонтролей.СнятСКонтроля КАК СнятСКонтроля,
		|	РеестрКонтролей.СрокИсполнения КАК СрокИсполнения
		|ИЗ
		|	РегистрСведений.РеестрКонтролей КАК РеестрКонтролей
		|ГДЕ
		|	РеестрКонтролей.Основание = &Основание
		|	И РеестрКонтролей.Пользователь = &Пользователь");
	
	Запрос.УстановитьПараметр("Основание", КонтрольСсылка);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ДанныеКонтроляВРеестре;
	КонецЕсли;
	
	ТаблицаРезультата = РезультатЗапроса.Выгрузить();
	СтрокаРезультата = ТаблицаРезультата[0];
	
	ДанныеКонтроляВРеестре = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаРезультата);
	
	Возврат ДанныеКонтроляВРеестре;
	
КонецФункции

#КонецОбласти

#КонецЕсли