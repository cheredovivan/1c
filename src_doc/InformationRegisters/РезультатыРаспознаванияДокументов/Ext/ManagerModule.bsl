#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает Истина ести файл уже обработан.
// 
// Параметры:
//  ИдентификаторРезультата - Строка
// 
// Возвращаемое значение:
//  Булево - Файл уже обработан
Функция ДанныеУжеЗагружены(ИдентификаторРезультата) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	РезультатыРаспознаванияДокументов.Документ
		|ИЗ
		|	РегистрСведений.РезультатыРаспознаванияДокументов КАК РезультатыРаспознаванияДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФайлыДокументовНаРаспознавании КАК ФайлыДокументовНаРаспознавании
		|		ПО ФайлыДокументовНаРаспознавании.Файл.Владелец.ВладелецФайла = РезультатыРаспознаванияДокументов.Документ
		|ГДЕ
		|	РезультатыРаспознаванияДокументов.ИдентификаторРезультата = &ИдентификаторРезультата
		|	И ФайлыДокументовНаРаспознавании.Файл ЕСТЬ NULL");
	
	Запрос.УстановитьПараметр("ИдентификаторРезультата",ИдентификаторРезультата);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Добавляет новую запись.
// 
// Параметры:
//  Документ - СправочникСсылка.ДокументыПредприятия
//  ИдентификаторРезультата - строка
//  ИдентификаторЗадания - строка
//  ВерсияФайлаСсылка - СправочникСсылка.ВерсииФайлов
//  Ответ - Структура
Процедура ДобавитьЗапись(Документ,ИдентификаторРезультата, ИдентификаторЗадания, ВерсияФайлаСсылка, Ответ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.Документ = Документ;
	Запись.ИдентификаторЗадания = ИдентификаторЗадания;
	Запись.ИдентификаторРезультата = ИдентификаторРезультата;
	Запись.Файл = ВерсияФайлаСсылка;
	Запись.ТипДокумента = Ответ.ТипФормализованногоДокумента;
	Запись.Ответ = Новый ХранилищеЗначения(Ответ);

	Запись.Записать(Истина);		
	
КонецПроцедуры
	
// Удаляет запись регистра.
// 
// Параметры:
//  Документ - СправочникСсылка.ДокументыПредприятия
Процедура УдалитьЗапись(Документ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(Документ);
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Возвращает результат по идентификатору.
// 
// Параметры:
//  ИдентификаторРезультата - Строка
// 
// Возвращаемое значение:
//  Структура:
// * Документ - СправочникСсылка.ДокументыПредприятия
// * ИдентификаторЗадания - Строка 
// * Файл - СправочникСсылка.ВерсииФайлов
// * Ответ - Структура
// * ТипДокумента - ПеречислениеСсылка.ТипыФормализованныхДокументов
Функция РезультатПоИдентификатору(ИдентификаторРезультата) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос("
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	РезультатыРаспознаванияДокументов.Документ КАК Документ,
		|	РезультатыРаспознаванияДокументов.ИдентификаторЗадания КАК ИдентификаторЗадания,
		|	РезультатыРаспознаванияДокументов.Файл КАК Файл,
		|	РезультатыРаспознаванияДокументов.Ответ КАК Ответ,
		|	РезультатыРаспознаванияДокументов.ТипДокумента КАК ТипДокумента
		|ИЗ
		|	РегистрСведений.РезультатыРаспознаванияДокументов КАК РезультатыРаспознаванияДокументов
		|ГДЕ
		|	РезультатыРаспознаванияДокументов.ИдентификаторРезультата = &ИдентификаторРезультата");
	
	Запрос.УстановитьПараметр("ИдентификаторРезультата",ИдентификаторРезультата);

	Выборка = Запрос.Выполнить().Выбрать();
	
	СтруктураВозврата = СтруктураРезультатПоИдентификатору();
	
	Если Выборка.Следующий() Тогда 
		
		СтруктураВозврата.Ответ = Выборка.Ответ.Получить();
		ЗаполнитьЗначенияСвойств(СтруктураВозврата, Выборка,,"Ответ");
		Возврат СтруктураВозврата;
		
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

// Возвращает двоичные данные распознанного файла.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
//  НормализованноеИзображение - Булево 
// 
// Возвращаемое значение:
// Файл - СправочникСсылка.ВерсииФайлов
Функция РаспознанныйФайлПоДокументу(ДокументСсылка, НормализованноеИзображение = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос("ВЫБРАТЬ
	|	РезультатыРаспознаванияДокументов.Файл КАК ИсходныйФайл,
	|	РезультатыРаспознаванияДокументов.Ответ КАК Ответ
	|ИЗ
	|	РегистрСведений.РезультатыРаспознаванияДокументов КАК РезультатыРаспознаванияДокументов
	|ГДЕ
	|	РезультатыРаспознаванияДокументов.Документ = &Документ");
	
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);

	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		
		Если НормализованноеИзображение = Ложь Тогда
		
			Возврат РаботаСФайлами.ДвоичныеДанныеФайла(Выборка.ИсходныйФайл);
			
		Иначе

			Пакет = Выборка.Ответ.Получить();
			
			ДесериализованноеЗначение = Пакет.Данные.ДесериализованноеЗначение.Получить("result");
			
			СвойстваШапки = ДесериализованноеЗначение["Страница"];
			
			ИсходноеИзображениеХранилище = РаспознаваниеДокументовКоннекторСлужебный.ИзвлечьДанныеКартинки(
				СвойстваШапки["chunk_img"]);
			
			Если ИсходноеИзображениеХранилище <> Неопределено Тогда
				Возврат ИсходноеИзображениеХранилище.Получить();
			КонецЕсли;

		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Структура результат по идентификатору.
// 
// Возвращаемое значение:
//  Структура - Структура результат по идентификатору:
Функция СтруктураРезультатПоИдентификатору()
	
	РезультатПоИдентификатору = Новый Структура;
	РезультатПоИдентификатору.Вставить("Документ");
	РезультатПоИдентификатору.Вставить("ИдентификаторЗадания");
	РезультатПоИдентификатору.Вставить("Файл");
	РезультатПоИдентификатору.Вставить("Ответ");
	РезультатПоИдентификатору.Вставить("ТипДокумента");
	Возврат РезультатПоИдентификатору;
	
КонецФункции

#КонецОбласти

#КонецЕсли