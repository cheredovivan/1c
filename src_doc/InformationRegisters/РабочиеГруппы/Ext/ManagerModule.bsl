#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Записывает набор записей в регистр сведений с отбором по объекту
//
Процедура ЗаписатьНаборПоОбъекту(Объект, ТаблицаИсточник, ОбновитьПраваДоступа) Экспорт
	
	Если Не ЗначениеЗаполнено(Объект) Тогда
		Возврат;
	КонецЕсли;
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Объект.Установить(Объект);
	
	Для каждого ИсточникСтрока Из ТаблицаИсточник Цикл
		
		Если Не ЗначениеЗаполнено(ИсточникСтрока.Участник) Тогда
			Продолжить;
		КонецЕсли;
		
		Запись = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, ИсточникСтрока);
		Запись.Объект = Объект;
		
	КонецЦикла;
	
	Набор.Записать(Истина);
	
	// Обновление прав доступа.
	Если ОбновитьПраваДоступа Тогда
		ТипыОбъектовВходящихВМеханизмПрав =
			ДокументооборотПраваДоступаПовтИсп.ТипыСсылокИспользующихДоступПоДескрипторам();
		Если ТипыОбъектовВходящихВМеханизмПрав.Найти(ТипЗнч(Объект)) <> Неопределено Тогда
			ДокументооборотПраваДоступа.ОпределитьДескрипторыОбъекта(Объект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает таблицу участников рабочей группы по объекту.
// 
// Параметры:
//  Объект - СправочникСсылка.ТемыОбсуждений, СправочникСсылка.ШаблоныУтверждения, СправочникСсылка.Проекты, СправочникСсылка.ШаблоныОзнакомления, СправочникСсылка.ШаблоныСоставныхБизнесПроцессов, СправочникСсылка.ЗаписиРабочегоКалендаря, СправочникСсылка.ШаблоныРассмотрения, СправочникСсылка.ШаблоныСогласования, ДокументСсылка.Задача, СправочникСсылка.ШаблоныДокументов, СправочникСсылка.ДокументыПредприятия, ДокументСсылка.ДействиеЗадачи, СправочникСсылка.ШаблоныПоручения, СправочникСсылка.ШаблоныКомплексныхБизнесПроцессов, СправочникСсылка.ГруппыКонтактовПользователей, СправочникСсылка.ШаблоныПриглашения, СправочникСсылка.ШаблоныИсполнения, СправочникСсылка.УчетныеЗаписиЭлектроннойПочты, СправочникСсылка.ШаблоныПодписания, СправочникСсылка.ШаблоныРегистрации, СправочникСсылка.Мероприятия, СправочникСсылка.ПроектныеЗадачи, БизнесПроцессСсылка.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица участников рабочей группы по объекту:
//	* Объект - СправочникСсылка.ТемыОбсуждений -
//			  - СправочникСсылка.ШаблоныУтверждения -
//			  - СправочникСсылка.Проекты -
//			  - СправочникСсылка.ШаблоныОзнакомления -
//			  - СправочникСсылка.ЗаписиРабочегоКалендаря -
//			  - СправочникСсылка.ШаблоныРассмотрения -
//			  - СправочникСсылка.ШаблоныСогласования -
//			  - ДокументСсылка.Задача -
//			  - СправочникСсылка.ШаблоныДокументов -
//			  - СправочникСсылка.ДокументыПредприятия -
//			  - ДокументСсылка.ДействиеЗадачи -
//			  - СправочникСсылка.ШаблоныКомплексныхБизнесПроцессов -
//			  - СправочникСсылка.ГруппыКонтактовПользователей -
//			  - СправочникСсылка.ШаблоныПриглашения -
//			  - СправочникСсылка.ШаблоныИсполнения -
//			  - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты -
//			  - СправочникСсылка.ШаблоныПодписания -
//			  - СправочникСсылка.ШаблоныРегистрации -
//			  - СправочникСсылка.Мероприятия -
//			  - СправочникСсылка.ПроектныеЗадачи -
//			  - БизнесПроцессСсылка -
//	* Участник - ОпределяемыйТип.КонтейнерыСотрудников -
//	* Автоподстановка - СправочникСсылка.АвтоподстановкиДляОбъектов -
//	* Изменение - Булево -
//	* Источник - СправочникСсылка.Контроль -
//			   - СправочникСсылка.Проекты -
//			   - СправочникСсылка.ДействияОзнакомления -
//			   - ДокументСсылка.ВходящееПисьмо -
//			   - СправочникСсылка.ДействияСогласования -
//			   - СправочникСсылка.ДействияРегистрации -
//			   - ДокументСсылка.ИсходящееПисьмо -
//			   - СправочникСсылка.ДействияПодписания -
//			   - СправочникСсылка.ДействияУтверждения -
//			   - СправочникСсылка.ШаблоныДокументов -
//			   - СправочникСсылка.ДокументыПредприятия -
//			   - СправочникСсылка.ДействияИсполнения -
//			   - СправочникСсылка.Мероприятия -
//	* Недействителен - Булево -
//
Функция ПолучитьУчастниковПоОбъекту(Объект) Экспорт
	
	Набор = СоздатьНаборЗаписей();
	ТаблицаУчастников = Набор.ВыгрузитьКолонки();
	ТаблицаУчастников.Колонки.Добавить(
		"УчастникПредставление", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(150)));
	ТаблицаУчастников.Колонки.Добавить("Недействителен", Новый ОписаниеТипов("Булево"));
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РабочиеГруппы.Объект КАК Объект,
		|	РабочиеГруппы.Участник КАК Участник,
		|	ПРЕДСТАВЛЕНИЕ(РабочиеГруппы.Участник) КАК УчастникПредставление,
		|	ВЫБОР
		|		КОГДА РабочиеГруппы.Участник ССЫЛКА Справочник.Пользователи
		|			ТОГДА РабочиеГруппы.Участник.Недействителен
		|		КОГДА РабочиеГруппы.Участник ССЫЛКА Справочник.РабочиеГруппы
		|			ТОГДА РабочиеГруппы.Участник.Недействительна
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Недействителен,
		|	РабочиеГруппы.Изменение КАК Изменение,
		|	РабочиеГруппы.Автоподстановка КАК Автоподстановка,
		|	РабочиеГруппы.Источник КАК Источник
		|ИЗ
		|	РегистрСведений.РабочиеГруппы КАК РабочиеГруппы
		|ГДЕ
		|	РабочиеГруппы.Объект = &Объект");
		
	Запрос.УстановитьПараметр("Объект", Объект);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаУчастников.Добавить(), Выборка);
	КонецЦикла;
	
	ТаблицаУчастников.Индексы.Добавить("Участник, Изменение"); // Для поиска в ДобавитьУчастникаВТаблицуНабора
	
	Возврат ТаблицаУчастников;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	
Процедура ОпределитьДескрипторыПриЗаписиРабочейГруппы(Набор, Отказ, Замещение) Экспорт
	
	Если МиграцияПриложенийПереопределяемый.ЭтоЗагрузка(Набор) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработанныеОбъекты = Новый Соответствие;
	ТипыОбъектовВходящихВМеханизмПрав = 
		ДокументооборотПраваДоступаПовтИсп.ТипыСсылокИспользующихДоступПоДескрипторам();
	
	Для Каждого Запись Из Набор Цикл
		
		ТекущийОбъект = Запись.Объект;
		Если ОбработанныеОбъекты.Получить(ТекущийОбъект) = Неопределено Тогда
			ТипОбъекта = ТипЗнч(ТекущийОбъект);
			Если ТипыОбъектовВходящихВМеханизмПрав.Найти(ТипОбъекта) <> Неопределено Тогда
				
				Если РегистрыСведений.ДескрипторыДляОбъектов.ЕстьДескрипторыОбъекта(ТекущийОбъект) Тогда
					ДокументооборотПраваДоступа.ОпределитьДескрипторыОбъекта(ТекущийОбъект);
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		ОбработанныеОбъекты.Вставить(ТекущийОбъект, Истина);
		
	КонецЦикла;
	
КонецПроцедуры
	
#КонецОбласти

#КонецЕсли