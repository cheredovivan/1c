#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет соответствия для каждого физического лица в списке.
//
// Параметры:
//   СписокФизЛиц - Массив из СправочникСсылка.ФизическиеЛица - список физических лиц, для которых
//     необходимо заполнить соответствия.
//
Процедура ЗаполнитьСоответствия(СписокФизЛиц) Экспорт
	
	Для Каждого ФизЛицо Из СписокФизЛиц Цикл
		ЗаполнитьСоответствияФизЛица(ФизЛицо);
	КонецЦикла;
	
КонецПроцедуры

// Заполняет соответствия физического лица сотрудникам, должностям и подразделениям в регистре
// сведений СоответствиеСотрудниковИССотрудникамДО.
//
// Параметры:
//   ФизЛицо - СправочникСсылка.ФизическиеЛица - физическое лицо, для которого необходимо заполнить соответствия.
//
Процедура ЗаполнитьСоответствияФизЛица(ФизЛицо) Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		БлокировкаДанных = Новый БлокировкаДанных;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("Справочник.Сотрудники");
		ЭлементБлокировки.УстановитьЗначение("Владелец", ФизЛицо);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.КадроваяИсторияСотрудников");
		ЭлементБлокировки.УстановитьЗначение("ФизическоеЛицо", ФизЛицо);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.СоответствиеСотрудниковИССотрудникамДО");
		ЭлементБлокировки.УстановитьЗначение("ФизическоеЛицо", ФизЛицо);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		БлокировкаДанных.Заблокировать();
		
		ЗаписиСопоставление = СоздатьНаборЗаписей();
		ЗаписиСопоставление.Отбор.ФизическоеЛицо.Установить(ФизЛицо);
		ЗаписиСопоставление.Записать();
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Сотрудники.Ссылка КАК Ссылка,
			|	Сотрудники.Должность КАК Должность,
			|	Сотрудники.Подразделение КАК Подразделение
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|ГДЕ
			|	Сотрудники.Владелец = &ФизЛицо
			|	И НЕ Сотрудники.ПометкаУдаления
			|
			|УПОРЯДОЧИТЬ ПО
			|	Сотрудники.ДатаНачалаДействия
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КадроваяИсторияСотрудников.ВидСобытия КАК ВидСобытия,
			|	КадроваяИсторияСотрудников.Период КАК Период,
			|	КадроваяИсторияСотрудников.ДействуетДо КАК ДействуетДо,
			|	КадроваяИсторияСотрудников.СотрудникИС КАК СотрудникИС,
			|	КадроваяИсторияСотрудников.ЭтоГоловнойСотрудник КАК ЭтоГоловнойСотрудник,
			|	КадроваяИсторияСотрудников.Подразделение КАК Подразделение,
			|	КадроваяИсторияСотрудников.Должность КАК Должность
			|ИЗ
			|	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
			|ГДЕ
			|	КадроваяИсторияСотрудников.ФизическоеЛицо = &ФизЛицо
			|
			|УПОРЯДОЧИТЬ ПО
			|	Период
			|ИТОГИ
			|ПО
			|	СотрудникИС");
		Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		ВсеСуществующиеСотрудникиДО = МассивРезультатов[0].Выгрузить();
		ВсяКадроваяИсторияФизЛица = МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ВсеСотрудникиИС = Новый Массив;
		СопоставленныеСотрудники = Новый ТаблицаЗначений;
		СопоставленныеСотрудники.Колонки.Добавить("СотрудникИС", Новый ОписаниеТипов("Строка"));
		СопоставленныеСотрудники.Колонки.Добавить("СотрудникДО", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
		СопоставленныеСотрудники.Колонки.Добавить("ДатаНачалаДействия", Новый ОписаниеТипов("Дата"));
		СопоставленныеСотрудники.Колонки.Добавить(
			"Основной", // Истина, если сотрудник ИС - головной, и сотрудник ДО действует на текущую дату.
			Новый ОписаниеТипов("Булево"));
		
		МаксимальнаяДата = ОбщегоНазначенияДокументооборотКлиентСервер.МаксимальнаяДата();
		ПустаяДата = ОбщегоНазначенияДокументооборотКлиентСервер.ПустаяДата();
		ТекущаяДата = НачалоДня(ТекущаяДатаСеанса());
		
		// 1. Сформируем таблицу с кадровой историей физ. лица, состоящую исключительно из приемов и увольнений.
		// Массив ВсеСотрудникиИС - неявно возвращаемое значение. Он будет заполнен при выполнении функции.
		КадроваяИсторияПриемовИУвольнений = КадроваяИсторияПриемовИУвольнений(
			ВсяКадроваяИсторияФизЛица,
			ВсеСотрудникиИС);
		
		// 2. Очистим старые связи всех сотрудников ИС в регистре сведений СвязиОбъектовИнтегрированныхСистем.
		// Они будут перезаполнены после сопоставления сотрудников.
		ТипВнешнегоОбъектаСотрудники = "Справочник.Сотрудники";
		
		БлокировкаДанных = Новый БлокировкаДанных;
		Для Каждого СотрудникИС Из ВсеСотрудникиИС Цикл
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.СвязиОбъектовИнтегрированныхСистем");
			ЭлементБлокировки.УстановитьЗначение("ТипВнешнегоОбъекта", ТипВнешнегоОбъектаСотрудники);
			ЭлементБлокировки.УстановитьЗначение("ИДВнешнегоОбъекта", СотрудникИС);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		КонецЦикла;
		БлокировкаДанных.Заблокировать();
		
		УзлыСотрудниковИС = Новый Соответствие;
		ОчиститьСвязиОбъектовИнтегрированныхСистем(ВсеСотрудникиИС, ТипВнешнегоОбъектаСотрудники, УзлыСотрудниковИС);
		
		// 3. Разделим строки таблицы кадровой истории на группы по комбинации полей Должность/Подразделение.
		КадроваяИсторияПоГруппам = КадроваяИсторияПоГруппам(КадроваяИсторияПриемовИУвольнений);
		
		// 4. Обработаем каждую группу строк кадровой истории по отдельности.
		Для Каждого Группа Из КадроваяИсторияПоГруппам Цикл
			
			МассивСтрокКадровойИстории = Группа.Значение;
			Если МассивСтрокКадровойИстории.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ТекущаяДолжность = МассивСтрокКадровойИстории[0].Должность;
			ТекущееПодразделение = МассивСтрокКадровойИстории[0].Подразделение;
			
			// 5. Сформируем таблицу, содержащую периоды, в которые физ. лицо работало в указанном
			// подразделении на указанной должности. Таблица отсортирована по дате начала действия.
			ТаблицаДляСопоставленияСотрудников = ТаблицаДляСопоставленияСотрудников(
				МассивСтрокКадровойИстории,
				ПустаяДата,
				МаксимальнаяДата);
			
			// 6. Отберем существующих сотрудников ДО по указанным должности и подразделению.
			// Сотрудники ДО уже отсортированы по дате начала действия.
			СуществующиеСотрудники = ВсеСуществующиеСотрудникиДО.НайтиСтроки(
				Новый Структура("Должность, Подразделение", ТекущаяДолжность, ТекущееПодразделение));
			
			// 7. Создадим набор записей регистра, и заполним их значениями из таблицы для сопоставления.
			ЗаписиСопоставление = СоздатьНаборЗаписей();
			ЗаписиСопоставление.Отбор.ФизическоеЛицо.Установить(ФизЛицо);
			ЗаписиСопоставление.Отбор.Должность.Установить(ТекущаяДолжность);
			ЗаписиСопоставление.Отбор.Подразделение.Установить(ТекущееПодразделение);
			
			ИндексСоответствия = 0;
			Для Каждого СтрокаДляСопоставления Из ТаблицаДляСопоставленияСотрудников Цикл
				
				Запись = ЗаписиСопоставление.Добавить();
				Запись.ФизическоеЛицо = ФизЛицо;
				Запись.Должность = ТекущаяДолжность;
				Запись.Подразделение = ТекущееПодразделение;
				
				Запись.ДатаНачалаДействия = СтрокаДляСопоставления.ДатаНачалаДействия;
				Запись.ДатаОкончанияДействия = СтрокаДляСопоставления.ДатаОкончанияДействия;
				Запись.СотрудникИС = СтрокаДляСопоставления.СотрудникИС;
				
				// 8. Сопоставим существующих сотрудников ДО, отсортированных по дате начала действия с
				// отсортированной таблицей периодов работы сотруников ИС в указанных должности и подразделении.
				// Если сотрудников ДО не хватает - создадим новых.
				Запись.СотрудникДО = ПодходящийСотрудникДО(
					ИндексСоответствия,
					СуществующиеСотрудники,
					ФизЛицо,
					ТекущаяДолжность,
					ТекущееПодразделение,
					СтрокаДляСопоставления.ДатаНачалаДействия,
					СтрокаДляСопоставления.ДатаОкончанияДействия);
				
				// 9. Дополним таблицу сопоставленных сотрудников.
				// Если один и тот же сотрудник ИС сопоставлен с несколькими сотрудниками ДО - в таблицу
				// будет добавлена только одна, наиболее актуальная запись.
				ДополнитьТаблицуСопоставленныхСотрудников(
					СопоставленныеСотрудники,
					Запись,
					СтрокаДляСопоставления,
					ТекущаяДата,
					ПустаяДата);
				
				ИндексСоответствия = ИндексСоответствия + 1;
				
			КонецЦикла;
			
			ЗаписиСопоставление.Записать();
			
		КонецЦикла;
		
		// 10. Запишем связи объектов.
		Для Каждого СтрокаСопоставления Из СопоставленныеСотрудники Цикл
			Узел = УзлыСотрудниковИС[СтрокаСопоставления.СотрудникИС];
			Если ЗначениеЗаполнено(Узел) Тогда
				ПараметрыСеанса.УзелИнтегрированнойСистемы = Узел;
			КонецЕсли;
			РегистрыСведений.СвязиОбъектовИнтегрированныхСистем.ДобавитьСвязьОбъектаДОИВнешнегоОбъекта(
				СтрокаСопоставления.СотрудникИС,
				ТипВнешнегоОбъектаСотрудники,
				СтрокаСопоставления.СотрудникДО);
		КонецЦикла;
		
		// 11. Установим основного сотрудника.
		СтрокаОсновнойСотрудник = СопоставленныеСотрудники.Найти(Истина, "Основной");
		Если СтрокаОсновнойСотрудник <> Неопределено Тогда
			РегистрыСведений.ОсновныеСотрудники.УстановитьОсновногоСотрудника(
				ФизЛицо,
				СтрокаОсновнойСотрудник.СотрудникДО);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(
			ОбменДаннымиСервер.СобытиеЖурналаРегистрацииОбменДанными(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Проверяет, загружен ли сотрудник из интегрированной системы (ИС).
//
// Параметры:
//   СотрудникДО - СправочникСсылка.Сотрудники - сотрудник, для которого необходимо проверить факт загрузки из ИС.
//
// Возвращаемое значение:
//   Булево - Истина, если сотрудник загружен из ИС, иначе Ложь.
//
Функция СотрудникЗагруженИзИС(СотрудникДО) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СоответствиеСотрудниковИССотрудникамДО.СотрудникДО КАК СотрудникДО
		|ИЗ
		|	РегистрСведений.СоответствиеСотрудниковИССотрудникамДО КАК СоответствиеСотрудниковИССотрудникамДО
		|ГДЕ
		|	СоответствиеСотрудниковИССотрудникамДО.СотрудникДО = &СотрудникДО");
	Запрос.УстановитьПараметр("СотрудникДО", СотрудникДО);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Дополняет таблицу СопоставленныеСотрудники.
//
// Параметры:
//   СопоставленныеСотрудники - ТаблицаЗначений:
//     * СотрудникИС - Строка - идентификатор сотрудника в ИС.
//     * СотрудникДО - СправочникСсылка.Сотрудники - ссылка на объект ДО.
//     * ДатаНачалаДействия - Дата - дата начала действия.
//     * Основной - Булево - признак основного сотрудника.
//   Запись - РегистрСведенийНаборЗаписей.СоответствиеСотрудниковИССотрудникамДО - запись регистра.
//   СтрокаДляСопоставления - СтрокаТаблицыЗначений - строка таблицы ТаблицаДляСопоставленияСотрудников.
//   ТекущаяДата - Дата - текущая дата.
//   ПустаяДата - Дата - пустая дата.
//
Процедура ДополнитьТаблицуСопоставленныхСотрудников(СопоставленныеСотрудники, Запись, СтрокаДляСопоставления,
		ТекущаяДата, ПустаяДата)
	
	НайденныеСтрокиСопоставленныеСотрудники = СопоставленныеСотрудники.НайтиСтроки(
		Новый Структура("СотрудникИС", Запись.СотрудникИС));
	
	Если НайденныеСтрокиСопоставленныеСотрудники.Количество() > 0 Тогда
		СтрокаСопоставленные = НайденныеСтрокиСопоставленныеСотрудники[0];
	Иначе
		СтрокаСопоставленные = СопоставленныеСотрудники.Добавить();
	КонецЕсли;
	
	Если Запись.ДатаНачалаДействия > СтрокаСопоставленные.ДатаНачалаДействия Тогда
		ЗаполнитьЗначенияСвойств(СтрокаСопоставленные, Запись);
		СтрокаСопоставленные.Основной = СтрокаДляСопоставления.ЭтоГоловнойСотрудник
			И СтрокаДляСопоставления.ДатаНачалаДействия <= ТекущаяДата
			И (СтрокаДляСопоставления.ДатаОкончанияДействия >= ТекущаяДата
				Или СтрокаДляСопоставления.ДатаОкончанияДействия = ПустаяДата);
	КонецЕсли;
	
КонецПроцедуры

// Делит строки таблицы кадровой истории на группы по комбинации полей Должность/Подразделение.
//
// Параметры:
//   КадроваяИсторияПриемовИУвольнений - см. КадроваяИсторияПриемовИУвольнений
//
// Возвращаемое значение:
//   Соответствие из КлючИЗначение:
//     * Ключ - Строка - идентификатор группы (Должность/Подразделение).
//     * Значение - Массив из СтрокаТаблицыЗначений - строки кадровой истории, входящие в группу.
//
Функция КадроваяИсторияПоГруппам(КадроваяИсторияПриемовИУвольнений)
	
	КадроваяИсторияПоГруппам = Новый Соответствие;
	Для Каждого СтрокаКадровойИстории Из КадроваяИсторияПриемовИУвольнений Цикл
		Ключ = СтрШаблон("%1 - %2", СтрокаКадровойИстории.Должность, СтрокаКадровойИстории.Подразделение);
		Если КадроваяИсторияПоГруппам[Ключ] = Неопределено Тогда
			КадроваяИсторияПоГруппам[Ключ] = Новый Массив;
		КонецЕсли;
		КадроваяИсторияПоГруппам[Ключ].Добавить(СтрокаКадровойИстории);
	КонецЦикла;
	
	Возврат КадроваяИсторияПоГруппам;
	
КонецФункции

// Формирует таблицу значений с кадровой историей приемов и увольнений физического лица.
// Кадровое перемещение при этом преобразуется в комбинацию увольнения и приема на работу.
//
// Параметры:
//   ВсяКадроваяИсторияФизЛица - ВыборкаИзРезультатаЗапроса - выборка из результата запроса,
//     содержащая всю кадровую историю физического лица.
//   ВсеСотрудникиИС - Массив из Строка - неявно возвращаемое значение, массив идентификаторов сотрудников ИС.
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * ВидСобытия - ПеречислениеСсылка.ВидыКадровыхСобытий - вид события кадровой истории.
//     * Период - Дата - дата события.
//     * Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение сотрудника.
//     * Должность - СправочникСсылка.Должности - должность сотрудника.
//     * СотрудникИС - Строка - идентификатор сотрудника в ИС.
//     * ЭтоГоловнойСотрудник - Булево - признак того, что сотрудник ИС является головным.
//
Функция КадроваяИсторияПриемовИУвольнений(ВсяКадроваяИсторияФизЛица, ВсеСотрудникиИС)
	
	КадроваяИстория = Новый ТаблицаЗначений;
	КадроваяИстория.Колонки.Добавить("ВидСобытия", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыКадровыхСобытий"));
	КадроваяИстория.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	КадроваяИстория.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	КадроваяИстория.Колонки.Добавить("Должность", Новый ОписаниеТипов("СправочникСсылка.Должности"));
	КадроваяИстория.Колонки.Добавить("СотрудникИС", Новый ОписаниеТипов("Строка"));
	КадроваяИстория.Колонки.Добавить("ЭтоГоловнойСотрудник", Новый ОписаниеТипов("Булево"));
	
	Индекс = -1;
	Пока ВсяКадроваяИсторияФизЛица.Следующий() Цикл
		
		ВсеСотрудникиИС.Добавить(ВсяКадроваяИсторияФизЛица.СотрудникИС);
		
		ВыборкаПоСотрудникуИС = ВсяКадроваяИсторияФизЛица.Выбрать();
		Пока ВыборкаПоСотрудникуИС.Следующий() Цикл
			
			Если ВыборкаПоСотрудникуИС.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Прием
					Или ВыборкаПоСотрудникуИС.ВидСобытия = Перечисления.ВидыКадровыхСобытий.НачальныеДанные Тогда
				
				НоваяСтрока = КадроваяИстория.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоСотрудникуИС);
				НоваяСтрока.Период = НачалоДня(ВыборкаПоСотрудникуИС.Период);
				Индекс = Индекс + 1;
				Если ЗначениеЗаполнено(ВыборкаПоСотрудникуИС.ДействуетДо) Тогда
					НоваяСтрока = КадроваяИстория.Добавить();
					Индекс = Индекс + 1;
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоСотрудникуИС);
					НоваяСтрока.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение;
					НоваяСтрока.Период = НачалоДня(ВыборкаПоСотрудникуИС.ДействуетДо) - 1;
				КонецЕсли;
				
			ИначеЕсли ВыборкаПоСотрудникуИС.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Перемещение
					И КадроваяИстория.Количество() > 0 Тогда
				
				ПредыдущаяСтрока = Новый Структура("Период, Подразделение, Должность, СотрудникИС, ЭтоГоловнойСотрудник");
				ЗаполнитьЗначенияСвойств(ПредыдущаяСтрока, КадроваяИстория[Индекс]);
				
				Если ПредыдущаяСтрока.Подразделение = ВыборкаПоСотрудникуИС.Подразделение
						И ПредыдущаяСтрока.Должность = ВыборкаПоСотрудникуИС.Должность Тогда
					Продолжить;
				КонецЕсли;
				
				ДатаУвольнения = НачалоДня(ВыборкаПоСотрудникуИС.Период) - 1;
				Если ДатаУвольнения <= ПредыдущаяСтрока.Период Тогда
					// Удалим запись.
					КадроваяИстория.Удалить(Индекс);
					Индекс = Индекс - 1;
					ПредПредыдущаяСтрока = КадроваяИстория[Индекс];
					Если ДатаУвольнения <= ПредПредыдущаяСтрока.Период
							И ПредПредыдущаяСтрока.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение Тогда
						// Подвинем дату увольнения предыдущей записи.
						ПредПредыдущаяСтрока.Период = ДатаУвольнения;
					КонецЕсли;
				Иначе
					НоваяСтрока = КадроваяИстория.Добавить();
					Индекс = Индекс + 1;
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ПредыдущаяСтрока);
					НоваяСтрока.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение;
					НоваяСтрока.Период = ДатаУвольнения;
				КонецЕсли;
				
				НоваяСтрока = КадроваяИстория.Добавить();
				Индекс = Индекс + 1;
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоСотрудникуИС);
				НоваяСтрока.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Прием;
				НоваяСтрока.Период = НачалоДня(ВыборкаПоСотрудникуИС.Период);
				
				Если ЗначениеЗаполнено(ВыборкаПоСотрудникуИС.ДействуетДо) Тогда
					НоваяСтрока = КадроваяИстория.Добавить();
					Индекс = Индекс + 1;
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоСотрудникуИС);
					НоваяСтрока.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение;
					НоваяСтрока.Период = НачалоДня(ВыборкаПоСотрудникуИС.ДействуетДо) - 1;
					
					НоваяСтрока = КадроваяИстория.Добавить();
					Индекс = Индекс + 1;
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ПредыдущаяСтрока);
					НоваяСтрока.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Прием;
					НоваяСтрока.Период = НачалоДня(ВыборкаПоСотрудникуИС.ДействуетДо);
				КонецЕсли;
				
			ИначеЕсли ВыборкаПоСотрудникуИС.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение
					И КадроваяИстория.Количество() > 0 Тогда
				
				НоваяСтрока = КадроваяИстория.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоСотрудникуИС);
				НоваяСтрока.Период = НачалоДня(ВыборкаПоСотрудникуИС.Период) - 1;
				Индекс = Индекс + 1;
				
			Иначе
				
				ВызватьИсключение НСтр("ru = 'Неизвестный вид кадрового события'");
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	КадроваяИстория.Сортировать("Период");
	
	Возврат КадроваяИстория;
	
КонецФункции

// Очищает связи в регистре сведений СвязиОбъектовИнтегрированныхСистем.
//
// Параметры:
//   ВсеСотрудникиИС - Массив из Строка - список идентификаторов сотрудников ИС.
//   ТипВнешнегоОбъектаСотрудники - Строка - тип внешнего объекта, указываемый в регистре.
//   УзлыСотрудниковИС - Соответствие из КлючИЗначение - неявно возвращаемое значение, соответствие
//         идентификаторов сотрудников ИС с узлами интегрированных систем:
//     * Ключ - Строка - идентификатор сотрудника ИС.
//     * Значение - ПланОбменаСсылка.ИнтегрированныеСистемы - узел интегрированной системы.
//
Процедура ОчиститьСвязиОбъектовИнтегрированныхСистем(ВсеСотрудникиИС, ТипВнешнегоОбъектаСотрудники, УзлыСотрудниковИС)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СвязиОбъектовИнтегрированныхСистем.УзелИнтегрированнойСистемы КАК УзелИнтегрированнойСистемы,
		|	СвязиОбъектовИнтегрированныхСистем.ТипВнешнегоОбъекта КАК ТипВнешнегоОбъекта,
		|	СвязиОбъектовИнтегрированныхСистем.ИДВнешнегоОбъекта КАК ИДВнешнегоОбъекта
		|ИЗ
		|	РегистрСведений.СвязиОбъектовИнтегрированныхСистем КАК СвязиОбъектовИнтегрированныхСистем
		|ГДЕ
		|	СвязиОбъектовИнтегрированныхСистем.ИДВнешнегоОбъекта В (&ВсеСотрудникиИС)
		|	И СвязиОбъектовИнтегрированныхСистем.ТипВнешнегоОбъекта = &ТипВнешнегоОбъектаСотрудники");
	Запрос.УстановитьПараметр("ВсеСотрудникиИС", ВсеСотрудникиИС);
	Запрос.УстановитьПараметр("ТипВнешнегоОбъектаСотрудники", ТипВнешнегоОбъектаСотрудники);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		УзлыСотрудниковИС[Выборка.ИДВнешнегоОбъекта] = Выборка.УзелИнтегрированнойСистемы;
		ЗаписиСвязиОбъектов = РегистрыСведений.СвязиОбъектовИнтегрированныхСистем.СоздатьНаборЗаписей();
		ЗаписиСвязиОбъектов.Отбор.УзелИнтегрированнойСистемы.Установить(Выборка.УзелИнтегрированнойСистемы);
		ЗаписиСвязиОбъектов.Отбор.ТипВнешнегоОбъекта.Установить(Выборка.ТипВнешнегоОбъекта);
		ЗаписиСвязиОбъектов.Отбор.ИДВнешнегоОбъекта.Установить(Выборка.ИДВнешнегоОбъекта);
		ЗаписиСвязиОбъектов.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Возвращает подходящего сотрудника ДО.
//
// Параметры:
//   ИндексСоответствия - Число - индекс для сопоставления сотрудника ИС с сотрудником ДО.
//   СуществующиеСотрудники - Массив из СтрокаТаблицыЗначений - существующие в базе сотрудники ДО:
//     * Ссылка - СправочникСсылка.Сотрудники - ссылка на сотрудника.
//     * Должность - СправочникСсылка.Должности - должность сотрудника.
//     * Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение сотрудника.
//   ФизЛицо - СправочникСсылка.ФизическиеЛица - владелец сотрудника.
//   ТекущаяДолжность - СправочникСсылка.Должности - текущая должность сотрудника.
//   ТекущееПодразделение - СправочникСсылка.СтруктураПредприятия - текущее подразделение сотрудника.
//   ДатаНачалаДействия - Дата - дата начала действия сотрудника.
//   ДатаОкончанияДействия - Дата - дата окончания действия сотрудника.
//
// Возвращаемое значение:
//   СправочникСсылка.Сотрудники - подходящий сотрудник ДО.
//
Функция ПодходящийСотрудникДО(ИндексСоответствия, СуществующиеСотрудники, ФизЛицо, ТекущаяДолжность,
		ТекущееПодразделение, ДатаНачалаДействия, ДатаОкончанияДействия)
	
	Если СуществующиеСотрудники.Количество() > ИндексСоответствия Тогда
		СотрудникДООбъект = СуществующиеСотрудники[ИндексСоответствия].Ссылка.ПолучитьОбъект();
	Иначе
		СотрудникДООбъект = Справочники.Сотрудники.СоздатьЭлемент();
		СотрудникДООбъект.Владелец = ФизЛицо;
		СотрудникДООбъект.Должность = ТекущаяДолжность;
		СотрудникДООбъект.Подразделение = ТекущееПодразделение;
	КонецЕсли;
	Если СотрудникДООбъект.ДатаНачалаДействия <> ДатаНачалаДействия Тогда
		СотрудникДООбъект.ДатаНачалаДействия = ДатаНачалаДействия;
	КонецЕсли;
	Если СотрудникДООбъект.ДатаОкончанияДействия <> ДатаОкончанияДействия Тогда
		СотрудникДООбъект.ДатаОкончанияДействия = ДатаОкончанияДействия;
	КонецЕсли;
	Если СотрудникДООбъект.Модифицированность() Или СотрудникДООбъект.ЭтоНовый() Тогда
		СотрудникДООбъект.Заблокировать();
		СотрудникДООбъект.ДополнительныеСвойства.Вставить("СинхронизацияСотрудников", Истина);
		СотрудникДООбъект.Записать();
	КонецЕсли;
	
	Возврат СотрудникДООбъект.Ссылка;
	
КонецФункции

// Формирует таблицу для сопоставления сотрудников ИС с существующими сотрудниками ДО
// на основе массива строк кадровой истории.
//
// Параметры:
//   МассивСтрокКадровойИстории - Массив из СтрокаТаблицыЗначений - массив строк кадровой истории физ. лица,
//         соответствующие одной комбинации полей Должность/Подразделение:
//     * ВидСобытия - ПеречислениеСсылка.ВидыКадровыхСобытий - вид события кадровой истории.
//     * Период - Дата - дата события.
//     * Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение сотрудника.
//     * Должность - СправочникСсылка.Должности - должность сотрудника.
//     * СотрудникИС - Строка - идентификатор сотрудника в ИС. Несмотря на то, что комбинация
//         полей Должность/Подразделение во всех строках одна и таже, сотрудники ИС могут быть разными.
//   ПустаяДата - Дата - дата, используемая для заполнения пустых значений даты.
//   МаксимальнаяДата - Дата - максимальная дата, используемая для корректной сортировки таблицы сопоставления.
//
// Возвращаемое значение:
//  ТаблицаЗначений - содержит периоды, в которые физ. лицо работало в указанном подразделении на указанной должности:
//     * ДатаНачалаДействия - Дата - дата начала действия сотрудника ДО.
//     * ДатаОкончанияДействия - Дата - дата окончания действия сотрудника ДО.
//     * СотрудникИС - Строка - идентификатор сотрудника в ИС, соответствующего указанным периодам.
//     * ЭтоГоловнойСотрудник - Булево - признак того, что сотрудник ИС является головным.
//
Функция ТаблицаДляСопоставленияСотрудников(МассивСтрокКадровойИстории, ПустаяДата, МаксимальнаяДата)
	
	ТаблицаДляСопоставленияСотрудников = Новый ТаблицаЗначений;
	ТаблицаДляСопоставленияСотрудников.Колонки.Добавить("ДатаНачалаДействия", Новый ОписаниеТипов("Дата"));
	ТаблицаДляСопоставленияСотрудников.Колонки.Добавить("ДатаОкончанияДействия", Новый ОписаниеТипов("Дата"));
	ТаблицаДляСопоставленияСотрудников.Колонки.Добавить("СотрудникИС", Новый ОписаниеТипов("Строка"));
	ТаблицаДляСопоставленияСотрудников.Колонки.Добавить("ЭтоГоловнойСотрудник", Новый ОписаниеТипов("Булево"));
	
	Для Каждого СтрокаКадровойИстории Из МассивСтрокКадровойИстории Цикл
		Если СтрокаКадровойИстории.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Прием
				Или СтрокаКадровойИстории.ВидСобытия = Перечисления.ВидыКадровыхСобытий.НачальныеДанные Тогда
			НоваяСтрока = ТаблицаДляСопоставленияСотрудников.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКадровойИстории);
			НоваяСтрока.ДатаНачалаДействия = СтрокаКадровойИстории.Период;
		ИначеЕсли СтрокаКадровойИстории.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение Тогда
			// Найдем строку, к которой относится увольнение.
			ДатаОкончанияУстановлена = Ложь;
			ТекущееКоличествоСтрок = ТаблицаДляСопоставленияСотрудников.Количество();
			Для Индекс = 1 По ТекущееКоличествоСтрок Цикл
				ПодходящаяСтрока = ТаблицаДляСопоставленияСотрудников[ТекущееКоличествоСтрок - Индекс];
				Если ПодходящаяСтрока.СотрудникИС = СтрокаКадровойИстории.СотрудникИС Тогда
					ПодходящаяСтрока.ДатаОкончанияДействия = СтрокаКадровойИстории.Период;
					ДатаОкончанияУстановлена = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если Не ДатаОкончанияУстановлена Тогда
				ВызватьИсключение НСтр("ru = 'Не удалось установить период кадрового события'");
			КонецЕсли;
		Иначе
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Не удалось установить период события ""%1""'"),
				Строка(СтрокаКадровойИстории.ВидСобытия));
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Запись Из ТаблицаДляСопоставленияСотрудников Цикл
		Если Запись.ДатаОкончанияДействия = ПустаяДата Тогда
			Запись.ДатаОкончанияДействия = МаксимальнаяДата; // Требуется для корректной сортировки.
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаДляСопоставленияСотрудников.Сортировать("ДатаНачалаДействия, ДатаОкончанияДействия");
	
	// Удалим все записи, пересекающиеся по периоду действия.
	ИндексЗаписи = 0;
	Пока ИндексЗаписи < ТаблицаДляСопоставленияСотрудников.Количество() - 1 Цикл
		Запись = ТаблицаДляСопоставленияСотрудников[ИндексЗаписи];
		БылоУдаление = Ложь;
		Для ВложенныйИндекс = ИндексЗаписи + 1 По ТаблицаДляСопоставленияСотрудников.Количество() - 1 Цикл
			ВложеннаяЗапись = ТаблицаДляСопоставленияСотрудников[ВложенныйИндекс];
			Если Запись.ДатаОкончанияДействия >= (ВложеннаяЗапись.ДатаНачалаДействия - 1) Тогда
				
				Запись.ДатаОкончанияДействия = ВложеннаяЗапись.ДатаОкончанияДействия;
				Запись.СотрудникИС = ВложеннаяЗапись.СотрудникИС;
				ТаблицаДляСопоставленияСотрудников.Удалить(ВложенныйИндекс);
				БылоУдаление = Истина;
				Прервать;
				
			КонецЕсли;
		КонецЦикла;
		Если Не БылоУдаление Тогда
			ИндексЗаписи = ИндексЗаписи + 1;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Запись Из ТаблицаДляСопоставленияСотрудников Цикл
		Если Запись.ДатаОкончанияДействия = МаксимальнаяДата Тогда
			Запись.ДатаОкончанияДействия = ПустаяДата; // Вернем изначальное значение.
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаДляСопоставленияСотрудников;
	
КонецФункции

#КонецОбласти

#КонецЕсли
