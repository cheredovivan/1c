#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Добавляет в регистр
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - учетная запись
//  ИдетОбработка - Булево - Истина если взято в работу (получение)
//  ЧислоПисем - Число.
//
// (аргумент ИдетОбработка – фоновые-«проверяльщики» будут отдавать Ложь, приемка по F5 - Истина)
Процедура Добавить(УчетнаяЗапись, ЧислоПисем, ИдетОбработка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
	
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПриемкаВнешнейПочты");
		ЭлементБлокировки.УстановитьЗначение("УчетнаяЗапись", УчетнаяЗапись);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		
		МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
		МенеджерЗаписи.ДатаПоявленияПисем = ТекущаяДатаСеанса();
		МенеджерЗаписи.ДатаОбработки = ТекущаяДатаСеанса();
		МенеджерЗаписи.ИдетОбработка = ИдетОбработка;
		МенеджерЗаписи.ЧислоПисем = ЧислоПисем;
		
		МенеджерЗаписи.Записать();

		ЗафиксироватьТранзакцию();	
		
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
КонецПроцедуры

// Взводит ИдетОбработка в Истина
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - учетная запись
//
// (записывает с ИдетОбработка = Истина, если записи не было, создает ее. Если запись уже была с ИдетОбработка = Истина – возвращает ошибку (значит кто-то до нас успел взять в работу).)
Функция ВзятьВРаботу(УчетнаяЗапись) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
	
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПриемкаВнешнейПочты");
		ЭлементБлокировки.УстановитьЗначение("УчетнаяЗапись", УчетнаяЗапись);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		
		МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
		МенеджерЗаписи.Прочитать();
		
		Если Не МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
			МенеджерЗаписи.ДатаПоявленияПисем = ТекущаяДатаСеанса();
			МенеджерЗаписи.ДатаОбработки = ТекущаяДатаСеанса();
			МенеджерЗаписи.ЧислоПисем = 1;
		Иначе	
			
			Если МенеджерЗаписи.ИдетОбработка = Истина Тогда // кто-то раньше нас успел занять
				ОтменитьТранзакцию();
				Возврат Ложь;
			КонецЕсли;		
			
		КонецЕсли;	
			
		МенеджерЗаписи.ИдетОбработка = Истина;
		МенеджерЗаписи.НомерСеанса = НомерСеансаИнформационнойБазы();
		МенеджерЗаписи.Записать();

		ЗафиксироватьТранзакцию();	
		
	Исключение
		ОтменитьТранзакцию();
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

// Записывает с ИдетОбработка = Ложь
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - учетная запись
//
Процедура Освободить(УчетнаяЗапись) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
	
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПриемкаВнешнейПочты");
		ЭлементБлокировки.УстановитьЗначение("УчетнаяЗапись", УчетнаяЗапись);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		
		МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
		МенеджерЗаписи.Прочитать();
		
		Если Не МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
			МенеджерЗаписи.ДатаПоявленияПисем = ТекущаяДатаСеанса();
			МенеджерЗаписи.ДатаОбработки = ТекущаяДатаСеанса();
			МенеджерЗаписи.ЧислоПисем = 1;
		КонецЕсли;	
		
		МенеджерЗаписи.НомерСеанса = 0;
		МенеджерЗаписи.ИдетОбработка = Ложь;
		МенеджерЗаписи.Записать();

		ЗафиксироватьТранзакцию();	
		
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
КонецПроцедуры

// Обновляет дату обработки
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - учетная запись
//  ДатаОбработки - Дата и время.
//
Процедура ОбновитьДатуОбработки(УчетнаяЗапись, ДатаОбработки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
	
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПриемкаВнешнейПочты");
		ЭлементБлокировки.УстановитьЗначение("УчетнаяЗапись", УчетнаяЗапись);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		
		МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
		МенеджерЗаписи.Прочитать();
		
		Если Не МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
			МенеджерЗаписи.ДатаПоявленияПисем = ТекущаяДатаСеанса();
			МенеджерЗаписи.ИдетОбработка = Истина;
			МенеджерЗаписи.ЧислоПисем = 1;
		КонецЕсли;	
		
		МенеджерЗаписи.НомерСеанса = НомерСеансаИнформационнойБазы();
		МенеджерЗаписи.ДатаОбработки = ДатаОбработки;
		МенеджерЗаписи.Записать();

		ЗафиксироватьТранзакцию();	
		
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
КонецПроцедуры

// Удаляет из регистра
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - учетная запись
//
Процедура Удалить(УчетнаяЗапись) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
	
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПриемкаВнешнейПочты");
		ЭлементБлокировки.УстановитьЗначение("УчетнаяЗапись", УчетнаяЗапись);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();	
		НаборЗаписей.Отбор.УчетнаяЗапись.Установить(УчетнаяЗапись);
		НаборЗаписей.Записать();

		ЗафиксироватьТранзакцию();	
		
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
КонецПроцедуры

// Определяет статистику очереди приемки внешней маршрутизации.
//
// Возвращаемое значение:
//  Структура - Статистика очереди приемки внешней маршрутизации:
//   * ОчередьИспользуется - Булево - Очередь используется.
//   * РазмерОчереди       - Число  - Размер очереди.
//   * ВозрастОчереди      - Число  - Возраст очереди.
//
Функция СтатистикаОчереди() Экспорт
	
	СтатистикаОчереди = Новый Структура("ОчередьИспользуется, РазмерОчереди, ВозрастОчереди");
	СтатистикаОчереди.ОчередьИспользуется = Истина;
	СтатистикаОчереди.РазмерОчереди = 0;
	СтатистикаОчереди.ВозрастОчереди = 0;
	Если Не СтатистикаОчереди.ОчередьИспользуется Тогда
		Возврат СтатистикаОчереди;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ПриемкаВнешнейПочты.ЧислоПисем), 0) КАК РазмерОчереди,
		|	ЕСТЬNULL(МИНИМУМ(ПриемкаВнешнейПочты.ДатаПоявленияПисем), ДАТАВРЕМЯ(1, 1, 1)) КАК МинимумДатаПоявленияПисем
		|ИЗ
		|	РегистрСведений.ПриемкаВнешнейПочты КАК ПриемкаВнешнейПочты");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтатистикаОчереди.РазмерОчереди = Выборка.РазмерОчереди;
		СтатистикаОчереди.ВозрастОчереди = ?(СтатистикаОчереди.РазмерОчереди > 0,
			ТекущаяДатаСеанса() - Выборка.МинимумДатаПоявленияПисем,
			0);
	КонецЕсли;
	
	Возврат СтатистикаОчереди;
	
КонецФункции

#КонецОбласти

#КонецЕсли