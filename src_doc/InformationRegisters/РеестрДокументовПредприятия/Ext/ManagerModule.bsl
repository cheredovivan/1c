#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет, выполнено ли первоначальное заполнение регистра.
// 
// Возвращаемое значение:
//  Булево
// 
Функция ПервоначальноеЗаполнениеЗавершено() Экспорт
	
	ИмяОбработчика = "Справочники.ДокументыПредприятия.ОбработатьДанныеДляПереходаНаНовуюВерсию_3_0_17_2";
	ПервоначальноеЗаполнениеЗавершено = ОбщегоНазначенияДокументооборот.ОтложенноеОбновлениеЗавершено(ИмяОбработчика);
	
	Возврат ПервоначальноеЗаполнениеЗавершено;
	
КонецФункции

// Возвращает ключ записи реестра я в реестре конкретного пользователя.
// 
// Параметры:
//  Реестр - СправочникСсылка.Реестры
//  Документ - СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение:
//  РегистрСведенийКлючЗаписи.РеестрДокументовПредприятия
//  Неопределено
// 
Функция КлючЗаписиРеестра(Реестр, Документ) Экспорт
	
	КлючЗаписиРеестра = Неопределено;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РеестрДокументовПредприятия.Реестр КАК Реестр,
		|	РеестрДокументовПредприятия.ДатаСортировки КАК ДатаСортировки,
		|	РеестрДокументовПредприятия.Документ КАК Документ
		|ИЗ
		|	РегистрСведений.РеестрДокументовПредприятия КАК РеестрДокументовПредприятия
		|ГДЕ
		|	РеестрДокументовПредприятия.Реестр = &Реестр
		|	И РеестрДокументовПредприятия.Документ = &Документ");
	
	Запрос.УстановитьПараметр("Реестр", Реестр);
	Запрос.УстановитьПараметр("Документ", Документ);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат КлючЗаписиРеестра;
	КонецЕсли;
	
	ТаблицаРезультата = РезультатЗапроса.Выгрузить();
	СтрокаРезультата = ТаблицаРезультата[0];
	
	СтруктураКлюча = Новый Структура;
	СтруктураКлюча.Вставить("Реестр", СтрокаРезультата.Реестр);
	СтруктураКлюча.Вставить("ДатаСортировки", СтрокаРезультата.ДатаСортировки);
	СтруктураКлюча.Вставить("Документ", СтрокаРезультата.Документ);
	КлючЗаписиРеестра = СоздатьКлючЗаписи(СтруктураКлюча);
	
	Возврат КлючЗаписиРеестра;
	
КонецФункции

// Отражает основание.
// 
// Параметры:
//  Документ - СправочникСсылка.ДокументыПредприятия
// 
Процедура Отразить(Документ) Экспорт
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьОтключениеБезопасногоРежима(Истина);
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	*
		|ИЗ
		|	РегистрСведений.ДанныеДокументовПредприятия КАК ДанныеДокументовПредприятия
		|ГДЕ
		|	ДанныеДокументовПредприятия.Документ = &Документ");
	Запрос.УстановитьПараметр("Документ", Документ);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ЕстьДанные = ВыборкаДетальныеЗаписи.Следующий();
	
	ОтображатьВРеестре = ЕстьДанные И Не ВыборкаДетальныеЗаписи.ПометкаУдаления;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РеестрДокументовПредприятия");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Документ", Документ);
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Документ.Установить(Документ);
		
		Если ОтображатьВРеестре Тогда
			
			ПодходящиеРеестры = РаботаСРеестрами.ПодходящиеРеестры(Документ);
			Для Каждого Реестр Из ПодходящиеРеестры Цикл
				Запись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, ВыборкаДетальныеЗаписи);
				Запись.Реестр = Реестр;
			КонецЦикла;
			
		КонецЕсли;
		
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

#Область ОбновлениеКэширующихДанных

// Обрабатывает обновление кэширующих данных.
// 
// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса:
//   * ОтметкаВремени - ОпределяемыйТип.ОтметкаВремени
//   * ЗависимыйОбъектМетаданных - СправочникСсылка.ИдентификаторыОбъектовМетаданных
//   * ВлияющийОбъектМетаданных - СправочникСсылка.ИдентификаторыОбъектовМетаданных
//   * КлючВлияющихДанных - ЛюбаяСсылка
//   * Автор - СправочникСсылка.Пользователи
//   * ЗагрузкаОбработанныхДанныхИзДругойСистемы - Булево
//   * ИзмененияВлияющихДанных - ХранилищеЗначения
//   * Попыток - Число
//   * ДатаКОбработке - Дата
// 
Процедура ОбновитьКэширующиеДанные(Выборка) Экспорт
	
	Если ТипЗнч(Выборка.КлючВлияющихДанных) = Тип("СправочникСсылка.ВидыДокументов") Тогда
		
		РеквизитыВидаДокументов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Выборка.КлючВлияющихДанных, "ЭтоГруппа");
		Если РеквизитыВидаДокументов.ЭтоГруппа Тогда
			ОбновитьКэширующиеДанныеПоГруппеВидовДокументов(Выборка);
		Иначе
			ОбновитьКэширующиеДанныеПоВидуДокументов(Выборка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Выборка.КлючВлияющихДанных) = Тип("СправочникСсылка.Реестры") Тогда
		
		ОбновитьКэширующиеДанныеПоРеестру(Выборка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обновить кэширующие данные по группе видов документов.
// 
// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса
// 
Процедура ОбновитьКэширующиеДанныеПоГруппеВидовДокументов(Выборка)
	
	ВидДокументов = Выборка.КлючВлияющихДанных; // СправочникСсылка.ВидыДокументов
	
	ЗатронутыеВидыДокументов = Справочники.ВидыДокументов.НайтиПоГруппе(ВидДокументов);
	
	НачатьТранзакцию();
	Попытка
		
		Для Каждого ВидДокументов Из ЗатронутыеВидыДокументов Цикл
			РегистрыСведений.ОчередьОбновленияКэширующихДанных.Добавить(
				"РегистрСведений.РеестрДокументовПредприятия",
				"Справочник.ВидыДокументов",
				ВидДокументов,
				Неопределено,
				Истина);
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Обновить кэширующие данные по виду документов.
// 
// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса
// 
Процедура ОбновитьКэширующиеДанныеПоВидуДокументов(Выборка)
	
	Если Не Выборка.Долгое Тогда
		ИзмененияВлияющихДанных = Выборка.ИзмененияВлияющихДанных.Получить();
		РегистрыСведений.ОчередьОбновленияКэширующихДанных.Добавить(
			Выборка.ЗависимыйОбъектМетаданных,
			Выборка.ВлияющийОбъектМетаданных,
			Выборка.КлючВлияющихДанных,
			ИзмененияВлияющихДанных,
			Истина);
		Возврат;
	КонецЕсли;
	
	ВидДокументов = Выборка.КлючВлияющихДанных; // СправочникСсылка.ВидыДокументов
	
	ДатаКОбработке = Выборка.ДатаКОбработке;
	Если Не ЗначениеЗаполнено(ДатаКОбработке) Тогда
		ДатаКОбработке = ТекущаяДатаСеанса();
	КонецЕсли;
	
	НомерЦикла = 0;
	ДатаЦикла = ДатаКОбработке;
	
	ЕстьДанныеКОбработке = Истина;
	Пока ЕстьДанныеКОбработке Цикл
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1000
			|	ДанныеДокументовПредприятия.Документ КАК Документ,
			|	ДанныеДокументовПредприятия.ДатаСортировки КАК ДатаСортировки
			|ИЗ
			|	РегистрСведений.ДанныеДокументовПредприятия КАК ДанныеДокументовПредприятия
			|ГДЕ
			|	ДанныеДокументовПредприятия.ВидДокумента = &ВидДокументов
			|	И ДанныеДокументовПредприятия.ДатаСортировки <= &ДатаКОбработке
			|
			|УПОРЯДОЧИТЬ ПО
			|	ДанныеДокументовПредприятия.ДатаСортировки УБЫВ");
		
		Запрос.УстановитьПараметр("ВидДокументов", ВидДокументов);
		Запрос.УстановитьПараметр("ДатаКОбработке", ДатаКОбработке);
		
		//@skip-check query-in-loop
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ЕстьДанныеКОбработке = Истина;
			
			НачатьТранзакцию();
			Попытка
				
				//@skip-check query-in-loop
				Отразить(ВыборкаДетальныеЗаписи.Документ);
				
				Если ВыборкаДетальныеЗаписи.ДатаСортировки <> ДатаКОбработке Тогда
					ДатаКОбработке = ВыборкаДетальныеЗаписи.ДатаСортировки;
					РегистрыСведений.ОчередьОбновленияКэширующихДанных.ОбновитьДатуКОбработке(
						Выборка.ОтметкаВремени,
						Выборка.ЗависимыйОбъектМетаданных,
						Выборка.ВлияющийОбъектМетаданных,
						Выборка.КлючВлияющихДанных,
						ДатаКОбработке);
				КонецЕсли;
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				ОтменитьТранзакцию();
				ВызватьИсключение;
			КонецПопытки;
			
		КонецЦикла;
		
		ЕстьДанныеКОбработке = ВыборкаДетальныеЗаписи.Количество() = 1000;
		Если ЕстьДанныеКОбработке И ДатаЦикла = ДатаКОбработке Тогда
			ВызватьИсключение НСтр("ru = 'Обнаружено зацикливание.'");
		КонецЕсли;
		
		ДатаЦикла = ДатаКОбработке;
		НомерЦикла = НомерЦикла + 1;
		Если НомерЦикла > 10000 Тогда
			ВызватьИсключение НСтр("ru = 'Обнаружено зацикливание.'");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Обновить кэширующие данные по реестру.
// 
// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса
// 
Процедура ОбновитьКэширующиеДанныеПоРеестру(Выборка)
	
	ИзмененияВлияющихДанных = Выборка.ИзмененияВлияющихДанных.Получить(); // Структура
	
	ПредыдущиеЗначенияРеквизитов = ИзмененияВлияющихДанных.ПредыдущиеЗначенияРеквизитов; // Структура
	Если Не ПредыдущиеЗначенияРеквизитов.ПометкаУдаления Тогда
		ПредыдущиеВидыЭлементовТаблица = ПредыдущиеЗначенияРеквизитов.ВидыЭлементовРеестра; // ТаблицаЗначений
		ПредыдущиеВидыЭлементовМассив = ПредыдущиеВидыЭлементовТаблица.ВыгрузитьКолонку("ВидЭлементовРеестра");
	Иначе
		ПредыдущиеВидыЭлементовМассив = Новый Массив;
	КонецЕсли;
	
	НовыеЗначенияРеквизитов = ИзмененияВлияющихДанных.НовыеЗначенияРеквизитов; // Структура
	Если Не НовыеЗначенияРеквизитов.ПометкаУдаления Тогда
		НовыеВидыЭлементовТаблица = НовыеЗначенияРеквизитов.ВидыЭлементовРеестра; // ТаблицаЗначений
		НовыеВидыЭлементовМассив = НовыеВидыЭлементовТаблица.ВыгрузитьКолонку("ВидЭлементовРеестра");
	Иначе
		НовыеВидыЭлементовМассив = Новый Массив;
	КонецЕсли;
	
	УдаленныеВидыЭлементов = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
		ПредыдущиеВидыЭлементовМассив,
		НовыеВидыЭлементовМассив);
	ДобавленныеВидыЭлементов = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
		НовыеВидыЭлементовМассив,
		ПредыдущиеВидыЭлементовМассив);
	
	ЗатронутыеВидыДокументов = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ЗатронутыеВидыДокументов, ДобавленныеВидыЭлементов, Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ЗатронутыеВидыДокументов, УдаленныеВидыЭлементов, Истина);
	
	НачатьТранзакцию();
	Попытка
		
		Для Каждого ВидДокументов Из ЗатронутыеВидыДокументов Цикл
			РегистрыСведений.ОчередьОбновленияКэширующихДанных.Добавить(
				"РегистрСведений.РеестрДокументовПредприятия",
				"Справочник.ВидыДокументов",
				ВидДокументов,
				Неопределено,
				Истина);
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли