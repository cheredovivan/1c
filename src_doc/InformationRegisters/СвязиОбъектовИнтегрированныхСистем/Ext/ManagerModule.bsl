#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет в регистр запись о связи. Если убеждается, что такая запись есть, то ничего не делает.
//
// Параметры:
//   ИДВнешнегоОбъекта - Строка - идентификатор внешнего объекта.
//   ТипВнешнегоОбъекта - Строка - имя типа XDTO.
//   СсылкаНаОбъектДО - ЛюбаяСсылка - ссылка на объект Документооборота.
//   ЕстьОбратнаяСвязьВУзле - Булево - признак парной связи, в базе-корреспонденте есть связь на объект ДО.
//   ПредставлениеВнешнегоОбъекта - Строка - представление внешнего объекта.
//   НавигационнаяСсылкаВнешнегоОбъекта - Строка - навигационная ссылка на внешний объект.
//
Процедура ДобавитьСвязьОбъектаДОИВнешнегоОбъекта(ИДВнешнегоОбъекта, ТипВнешнегоОбъекта, СсылкаНаОбъектДО,
		ЕстьОбратнаяСвязьВУзле = Ложь, ПредставлениеВнешнегоОбъекта = "",
		НавигационнаяСсылкаВнешнегоОбъекта = "") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Узел = ПараметрыСеанса.УзелИнтегрированнойСистемы;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.УзелИнтегрированнойСистемы.Установить(Узел);
	НаборЗаписей.Отбор.ИДВнешнегоОбъекта.Установить(ИДВнешнегоОбъекта);
	НаборЗаписей.Отбор.ТипВнешнегоОбъекта.Установить(ТипВнешнегоОбъекта);
	НаборЗаписей.Отбор.СсылкаНаОбъектДО.Установить(СсылкаНаОбъектДО);
	НаборЗаписей.Прочитать();
	Для Каждого Запись Из НаборЗаписей Цикл
		Если Запись.УзелИнтегрированнойСистемы = Узел
				И Запись.ИДВнешнегоОбъекта = ИДВнешнегоОбъекта
				И Запись.ТипВнешнегоОбъекта = ТипВнешнегоОбъекта
				И Запись.СсылкаНаОбъектДО = СсылкаНаОбъектДО
				И Запись.ЕстьОбратнаяСвязьВУзле = ЕстьОбратнаяСвязьВУзле
				И Запись.ПредставлениеВнешнегоОбъекта = ПредставлениеВнешнегоОбъекта
				И Запись.НавигационнаяСсылкаВнешнегоОбъекта = НавигационнаяСсылкаВнешнегоОбъекта Тогда
			Возврат; // Такая же запись уже есть.
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.Очистить();
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.УзелИнтегрированнойСистемы = Узел;
	НоваяЗапись.ИДВнешнегоОбъекта = ИДВнешнегоОбъекта;
	НоваяЗапись.ТипВнешнегоОбъекта = ТипВнешнегоОбъекта;
	НоваяЗапись.СсылкаНаОбъектДО = СсылкаНаОбъектДО;
	НоваяЗапись.ЕстьОбратнаяСвязьВУзле = ЕстьОбратнаяСвязьВУзле;
	НоваяЗапись.ПредставлениеВнешнегоОбъекта = ПредставлениеВнешнегоОбъекта;
	НоваяЗапись.НавигационнаяСсылкаВнешнегоОбъекта = НавигационнаяСсылкаВнешнегоОбъекта;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Исправляет запись регистра СвязиОбъектовИнтегрированныхСистем обновленными сведениями.
//
// Параметры:
//   Объект - ЛюбаяСсылка - ссылка на связанный объект.
//   ИсходныйУзел - ПланОбменаСсылка.ИнтегрированныеСистемы - старое значение узла.
//   ИсходныйТипОбъекта - Строка - старое имя типа XDTO связанного объекта.
//   Узел - ПланОбменаСсылка.ИнтегрированныеСистемы - новое значение узла.
//   ТипОбъекта - Строка - новое имя типа XDTO связанного объекта.
//   ИдентификаторОбъекта - Строка - идентификатор связанного объекта.
//   НовоеПредставлениеВнешнегоОбъекта - Строка - новое представление внешнего объекта.
//   НоваяНавигационнаяСсылкаВнешнегоОбъекта - Строка - новая навигационная ссылка на внешний объект.
//
Процедура ИсправитьЗаписьСвязиОбъектов(Объект, ИсходныйУзел, ИсходныйТипОбъекта, Узел, ТипОбъекта,
		ИдентификаторОбъекта, НовоеПредставлениеВнешнегоОбъекта = "",
		НоваяНавигационнаяСсылкаВнешнегоОбъекта = "") Экспорт
	
	Если Не ЗначениеЗаполнено(Узел) Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.СсылкаНаОбъектДО = Объект;
	МенеджерЗаписи.УзелИнтегрированнойСистемы = ИсходныйУзел;
	МенеджерЗаписи.ТипВнешнегоОбъекта = ИсходныйТипОбъекта;
	МенеджерЗаписи.ИДВнешнегоОбъекта = ИдентификаторОбъекта;
	МенеджерЗаписи.Прочитать();
	ЕстьОбратнаяСвязьВУзле = МенеджерЗаписи.ЕстьОбратнаяСвязьВУзле;
	Если ЗначениеЗаполнено(НовоеПредставлениеВнешнегоОбъекта) Тогда
		ПредставлениеВнешнегоОбъекта = НовоеПредставлениеВнешнегоОбъекта;
	Иначе
		ПредставлениеВнешнегоОбъекта = МенеджерЗаписи.ПредставлениеВнешнегоОбъекта;
	КонецЕсли;
	Если ЗначениеЗаполнено(НоваяНавигационнаяСсылкаВнешнегоОбъекта) Тогда
		НавигационнаяСсылкаВнешнегоОбъекта = НоваяНавигационнаяСсылкаВнешнегоОбъекта;
	Иначе
		НавигационнаяСсылкаВнешнегоОбъекта = МенеджерЗаписи.НавигационнаяСсылкаВнешнегоОбъекта;
	КонецЕсли;
	Если ЗначениеЗаполнено(ИсходныйУзел) Тогда
		МенеджерЗаписи.Удалить();
	КонецЕсли;
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.СсылкаНаОбъектДО = Объект;
	МенеджерЗаписи.УзелИнтегрированнойСистемы = Узел;
	МенеджерЗаписи.ТипВнешнегоОбъекта = ТипОбъекта;
	МенеджерЗаписи.ИДВнешнегоОбъекта = ИдентификаторОбъекта;
	МенеджерЗаписи.ЕстьОбратнаяСвязьВУзле = ЕстьОбратнаяСвязьВУзле;
	МенеджерЗаписи.ПредставлениеВнешнегоОбъекта = ПредставлениеВнешнегоОбъекта;
	МенеджерЗаписи.НавигационнаяСсылкаВнешнегоОбъекта = НавигационнаяСсылкаВнешнегоОбъекта;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Очищает во всех записях регистра реквизит ЕстьОбратнаяСвязьВУзле.
//
Процедура ОчиститьПризнакЕстьОбратнаяСвязьВУзле() Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		УзелИнтегрированнойСистемы = ПараметрыСеанса.УзелИнтегрированнойСистемы;
		
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СвязиОбъектовИнтегрированныхСистем");
		ЭлементБлокировкиДанных.УстановитьЗначение("УзелИнтегрированнойСистемы", УзелИнтегрированнойСистемы);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	СвязиОбъектовИнтегрированныхСистем.УзелИнтегрированнойСистемы КАК УзелИнтегрированнойСистемы,
			|	СвязиОбъектовИнтегрированныхСистем.СсылкаНаОбъектДО КАК СсылкаНаОбъектДО,
			|	СвязиОбъектовИнтегрированныхСистем.ТипВнешнегоОбъекта КАК ТипВнешнегоОбъекта,
			|	СвязиОбъектовИнтегрированныхСистем.ИДВнешнегоОбъекта КАК ИДВнешнегоОбъекта,
			|	СвязиОбъектовИнтегрированныхСистем.ПредставлениеВнешнегоОбъекта КАК ПредставлениеВнешнегоОбъекта,
			|	СвязиОбъектовИнтегрированныхСистем.НавигационнаяСсылкаВнешнегоОбъекта КАК НавигационнаяСсылкаВнешнегоОбъекта,
			|	ЛОЖЬ КАК ЕстьОбратнаяСвязьВУзле
			|ИЗ
			|	РегистрСведений.СвязиОбъектовИнтегрированныхСистем КАК СвязиОбъектовИнтегрированныхСистем
			|ГДЕ
			|	СвязиОбъектовИнтегрированныхСистем.УзелИнтегрированнойСистемы = &УзелИнтегрированнойСистемы");
		Запрос.УстановитьПараметр("УзелИнтегрированнойСистемы", УзелИнтегрированнойСистемы);
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.УзелИнтегрированнойСистемы.Установить(УзелИнтегрированнойСистемы);
		НаборЗаписей.Загрузить(РезультатЗапроса.Выгрузить());
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Функция - Возвращает идентификатор внешнего объекта по объекту в текущей базе.
//
// Параметры:
//   СсылкаНаОбъектДО - ЛюбаяСсылка - Ссылка на объект в текущей базе.
//   ТипВнешнегоОбъекта - Строка - Название типа внешнего объекта.
//
// Возвращаемое значение:
//   Строка - Идентификатор, поле ИДВнешнегоОбъекта, ранее полученный из внешней системы,
//     или пустая строка в отсутствие полученного.
//
Функция ПолучитьИдентификаторПоОбъектуДО(СсылкаНаОбъектДО, ТипВнешнегоОбъекта) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИДВнешнегоОбъекта
		|ИЗ
		|	РегистрСведений.СвязиОбъектовИнтегрированныхСистем
		|ГДЕ
		|	ТипВнешнегоОбъекта = &ТипВнешнегоОбъекта
		|	И СсылкаНаОбъектДО = &СсылкаНаОбъектДО");
	Запрос.УстановитьПараметр("СсылкаНаОбъектДО", СсылкаНаОбъектДО);
	Запрос.УстановитьПараметр("ТипВнешнегоОбъекта", ТипВнешнегоОбъекта);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ИДВнешнегоОбъекта;
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

// Возвращает объекты конфигурации по ID и Типу внешнего объекта, запуская исправление связей,
// если в выборке обнаруживается запись с пустой ссылкой на узел интегрированной системы.
//
// Параметры:
//   ID - Строка - идентификатор внешнего объекта.
//   Тип - Строка - имя типа XDTO.
//   ПоказыватьПомеченныеНаУдаление - Булево - Истина, если нужны помеченные на удаление объекты.
//
// Возвращаемое значение:
//   Массив Из ЛюбаяСсылка - массив ссылок на связанные объекты.
//
Функция ПолучитьОбъектыДОПоВнешнемуОбъекту(ID, Тип, ПоказыватьПомеченныеНаУдаление = Ложь) Экспорт
	
	МассивСсылокНаОбъектыДО = Новый Массив;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СсылкаНаОбъектДО КАК СсылкаНаОбъектДО,
		|	УзелИнтегрированнойСистемы КАК УзелИнтегрированнойСистемы,
		|	ТипВнешнегоОбъекта КАК ТипВнешнегоОбъекта,
		|	ИСТИНА КАК СвязиОбъектовИнтегрированныхСистем
		|ИЗ
		|	РегистрСведений.СвязиОбъектовИнтегрированныхСистем
		|ГДЕ
		|	УзелИнтегрированнойСистемы В (&Узлы)
		|	И ИДВнешнегоОбъекта = &Идентификатор
		|	И ТипВнешнегоОбъекта В (&ТипыВСвязях)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	УникальныйИдентификаторИсточника,
		|	НЕОПРЕДЕЛЕНО,
		|	ТипПриемника,
		|	ЛОЖЬ
		|ИЗ
		|	РегистрСведений.СоответствияОбъектовИнформационныхБаз
		|ГДЕ
		|	УникальныйИдентификаторПриемника = &Идентификатор
		|	И ТипПриемника = &ТипВСоответствиях");
	
	Узлы = Новый Массив;
	Узлы.Добавить(ПараметрыСеанса.УзелИнтегрированнойСистемы);
	Узлы.Добавить(ПланыОбмена.ИнтегрированныеСистемы.ПустаяСсылка());
	
	ТипыВСвязях = Новый Массив;
	ТипыВСвязях.Добавить("");
	ТипыВСвязях.Добавить(Тип);
	
	ТипВСоответствиях = СтрЗаменить(Тип, "Документ.", "ДокументСсылка.");
	ТипВСоответствиях = СтрЗаменить(ТипВСоответствиях, "Справочник.", "СправочникСсылка.");
	
	Запрос.УстановитьПараметр("Узлы", Узлы);
	Запрос.УстановитьПараметр("ТипыВСвязях", ТипыВСвязях);
	Запрос.УстановитьПараметр("ТипВСоответствиях", ТипВСоответствиях);
	Запрос.УстановитьПараметр("Идентификатор", ID);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(Выборка.УзелИнтегрированнойСистемы)
				Или Не ЗначениеЗаполнено(Выборка.ТипВнешнегоОбъекта)
				И Выборка.СвязиОбъектовИнтегрированныхСистем Тогда
			ИсправитьЗаписьСвязиОбъектов(
				Выборка.СсылкаНаОбъектДО,
				Выборка.УзелИнтегрированнойСистемы,
				Выборка.ТипВнешнегоОбъекта,
				ПараметрыСеанса.УзелИнтегрированнойСистемы,
				Тип,
				ID);
		КонецЕсли;
		
		// Проверим доступность по правам и RLS.
		Если Не ПравоДоступа("Чтение", Выборка.СсылкаНаОбъектДО.Метаданные()) Тогда 
			Продолжить;
		КонецЕсли;
		
		ЗапросДоступности = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Объекты.Ссылка КАК Ссылка
			|ИЗ
			|	" + Выборка.СсылкаНаОбъектДО.Метаданные().ПолноеИмя() + " КАК Объекты
			|ГДЕ
			|	Объекты.Ссылка = &СсылкаНаВладельца");
		ЗапросДоступности.УстановитьПараметр("СсылкаНаВладельца", Выборка.СсылкаНаОбъектДО);
		Если ЗапросДоступности.Выполнить().Выбрать().Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ДобавитьОбъект = Истина;
		Если Не ПоказыватьПомеченныеНаУдаление
				И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.СсылкаНаОбъектДО, "ПометкаУдаления") Тогда
			ДобавитьОбъект = Ложь;
		КонецЕсли;
		
		Если ДобавитьОбъект Тогда
			МассивСсылокНаОбъектыДО.Добавить(Выборка.СсылкаНаОбъектДО);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивСсылокНаОбъектыДО;
	
КонецФункции

// Получает список внешних объектов для объекта ДО для обращающейся интегрированной системы.
//
// Параметры:
//   Объект - ЛюбаяСсылка - ссылка на объект Документооборота.
//   УзелИнтегрированнойСистемы - ПланОбменаСсылка.ИнтегрированныеСистемы - узел, для которого необходимо получить
//     список внешних объектов.
//
// Возвращаемое значение:
//   Массив из Структура - массив структур с данными внешнего объекта:
//     * Идентификатор - Строка - идентификатор внешнего объекта.
//     * Тип - Строка - тип внешнего объекта.
//     * НавигационнаяСсылка - Строка - навигационная ссылка на внешний объект.
//     * Представление - Строка - представление внешнего объекта.
//
Функция ПолучитьСписокВнешнихОбъектов(Объект, УзелИнтегрированнойСистемы = Неопределено) Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Связи.ИДВнешнегоОбъекта КАК Идентификатор,
		|	Связи.ТипВнешнегоОбъекта КАК Тип,
		|	Связи.НавигационнаяСсылкаВнешнегоОбъекта КАК НавигационнаяСсылка,
		|	Связи.ПредставлениеВнешнегоОбъекта КАК Представление,
		|	ИСТИНА КАК ТипНеТребуетПреобразования
		|ПОМЕСТИТЬ СвязиОбъектов
		|ИЗ
		|	РегистрСведений.СвязиОбъектовИнтегрированныхСистем КАК Связи
		|ГДЕ
		|	Связи.СсылкаНаОбъектДО = &СсылкаНаОбъектДО
		|	И Связи.УзелИнтегрированнойСистемы = &УзелИнтегрированнойСистемы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СвязиОбъектов.Идентификатор КАК Идентификатор,
		|	СвязиОбъектов.Тип КАК Тип,
		|	СвязиОбъектов.НавигационнаяСсылка КАК НавигационнаяСсылка,
		|	СвязиОбъектов.Представление КАК Представление,
		|	СвязиОбъектов.ТипНеТребуетПреобразования КАК ТипНеТребуетПреобразования
		|ИЗ
		|	СвязиОбъектов");
		
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСинхронизациюДанных") Тогда
		
		Запрос.Текст = Запрос.Текст + "
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Соответствия.УникальныйИдентификаторПриемника КАК Идентификатор,
			|	Соответствия.ТипПриемника КАК Тип,
			|	"""" КАК НавигационнаяСсылка,
			|	"""" КАК Представление,
			|	ЛОЖЬ КАК ТипНеТребуетПреобразования
			|ИЗ
			|	РегистрСведений.СоответствияОбъектовИнформационныхБаз КАК Соответствия
			|ГДЕ
			|	Соответствия.УникальныйИдентификаторИсточника = &СсылкаНаОбъектДО
			|	И Соответствия.УзелИнформационнойБазы В
			|		(ВЫБРАТЬ
			|			УзелДляСинхронизацииДанных
			|		ИЗ
			|			ПланОбмена.ИнтегрированныеСистемы
			|		ГДЕ
			|			Ссылка = &УзелИнтегрированнойСистемы)
			|	И Соответствия.УникальныйИдентификаторПриемника НЕ В
			|		(ВЫБРАТЬ
			|			Идентификатор
			|		ИЗ
			|			СвязиОбъектов)";
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("СсылкаНаОбъектДО", Объект);
	Если УзелИнтегрированнойСистемы = Неопределено Тогда
		Запрос.УстановитьПараметр("УзелИнтегрированнойСистемы", ПараметрыСеанса.УзелИнтегрированнойСистемы);
	Иначе
		Запрос.УстановитьПараметр("УзелИнтегрированнойСистемы", УзелИнтегрированнойСистемы);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ТипНеТребуетПреобразования Тогда // тип сразу в виде "Справочник.Контрагенты"
			Тип = Выборка.Тип;
		Иначе // тип в виде "СправочникСсылка.Контрагенты" требует преобразования
			Тип = СтрЗаменить(Выборка.Тип, "Ссылка.", ".");
		КонецЕсли;
		
		ДанныеВнешнегоОбъекта = Новый Структура;
		ДанныеВнешнегоОбъекта.Вставить("Идентификатор", Выборка.Идентификатор);
		ДанныеВнешнегоОбъекта.Вставить("Тип", Тип);
		ДанныеВнешнегоОбъекта.Вставить("НавигационнаяСсылка", Выборка.НавигационнаяСсылка);
		ДанныеВнешнегоОбъекта.Вставить("Представление", Выборка.Представление);
		
		Результат.Добавить(ДанныеВнешнегоОбъекта);
		
	КонецЦикла;
		
	Возврат Результат;
		
КонецФункции

// Получает список внешних объектов для объекта ДО для обращающейся интегрированной системы.
//
// Параметры:
//   Объект - ЛюбаяСсылка - ссылка на объект Документооборота.
//
// Возвращаемое значение:
//   Массив из Структура - массив структур с данными внешнего объекта:
//     * ПредставлениеУзла - Строка - представление узла интегрированной системы.
//     * АдресБазыИС - Строка - адрес базы интегрированной системы.
//     * НавигационнаяСсылка - Строка - навигационная ссылка на внешний объект.
//     * Представление - Строка - представление внешнего объекта.
//
Функция СписокСсылокНаВнешниеОбъекты(Объект) Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Связи.УзелИнтегрированнойСистемы КАК УзелИнтегрированнойСистемы,
		|	ИнтегрированныеСистемы.Наименование КАК ПредставлениеУзла,
		|	Связи.НавигационнаяСсылкаВнешнегоОбъекта КАК НавигационнаяСсылка,
		|	Связи.ПредставлениеВнешнегоОбъекта КАК Представление
		|ИЗ
		|	РегистрСведений.СвязиОбъектовИнтегрированныхСистем КАК Связи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.ИнтегрированныеСистемы КАК ИнтегрированныеСистемы
		|		ПО Связи.УзелИнтегрированнойСистемы = ИнтегрированныеСистемы.Ссылка
		|ГДЕ
		|	Связи.СсылкаНаОбъектДО = &СсылкаНаОбъектДО
		|	И Связи.НавигационнаяСсылкаВнешнегоОбъекта <> """"
		|	И Связи.ПредставлениеВнешнегоОбъекта <> """"
		|
		|УПОРЯДОЧИТЬ ПО
		|	УзелИнтегрированнойСистемы,
		|	Представление");
	Запрос.УстановитьПараметр("СсылкаНаОбъектДО", Объект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СсылкаНаВнешнийОбъект  = Новый Структура;
		СсылкаНаВнешнийОбъект.Вставить("ПредставлениеУзла", Выборка.ПредставлениеУзла);
		СсылкаНаВнешнийОбъект.Вставить("АдресБазыИС",
			РаботаСИнтегрированнымиСистемамиПовтИсп.НавигационнаяСсылкаБазыИС(Выборка.УзелИнтегрированнойСистемы));
		СсылкаНаВнешнийОбъект.Вставить("НавигационнаяСсылка", Выборка.НавигационнаяСсылка);
		СсылкаНаВнешнийОбъект.Вставить("Представление", Выборка.Представление);
		Результат.Добавить(СсылкаНаВнешнийОбъект);
	КонецЦикла;
		
	Возврат Результат;
		
КонецФункции

// Получает ссылку по идентификатору и типу внешнего объекта, если объект существует в БД.
//
// Параметры:
//   ИДВнешнегоОбъекта - Строка - идентификатор внешнего объекта.
//   ТипВнешнегоОбъекта - Строка - тип внешнего объекта.
//   ВидСправочника - Строка - вид справочника.
//   ЛюбойУзел - Булево - Истина, если нужно получить ссылку по любому узлу.
//   ПредставлениеВнешнегоОбъекта - Строка - представление внешнего объекта.
//   НавигационнаяСсылкаВнешнегоОбъекта - Строка - навигационная ссылка внешнего объекта.
//
// Возвращаемое значение:
//   СправочникСсылка - ссылка, если объект найден по идентификатору и типу.
//   Неопределено - если объект не найден.
//
Функция СправочникСсылкаПоИдентификаторуВнешнегоОбъекта(ИДВнешнегоОбъекта, ТипВнешнегоОбъекта, Знач ВидСправочника,
		ЛюбойУзел = Ложь, ПредставлениеВнешнегоОбъекта = "", НавигационнаяСсылкаВнешнегоОбъекта = "") Экспорт
	
	Если Не ЗначениеЗаполнено(ВидСправочника) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЧастиИмениТипаОбъектаДО = СтрРазделить(ВидСправочника, ".");
	Если ЧастиИмениТипаОбъектаДО.Количество() > 1 Тогда
		Если Не СтрНачинаетсяС(ЧастиИмениТипаОбъектаДО[0], "Справочник") Тогда
			Возврат Неопределено;
		КонецЕсли;
		ВидСправочника = ЧастиИмениТипаОбъектаДО[1];
	КонецЕсли;
	
	Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ИДВнешнегоОбъекта) Тогда
		СсылкаПоИдентификатору = Справочники[ВидСправочника].ПолучитьСсылку(
			Новый УникальныйИдентификатор(ИДВнешнегоОбъекта));
	Иначе
		СсылкаПоИдентификатору = Неопределено;
	КонецЕсли;
	
	// У связей, сохраненных в регистре - приоритет.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТребуемыйСправочник.Ссылка КАК Ссылка,
		|	ТребуемыйСправочник.ПометкаУдаления КАК ПометкаУдаления,
		|	СвязиОбъектовИнтегрированныхСистем.УзелИнтегрированнойСистемы КАК УзелИнтегрированнойСистемы,
		|	СвязиОбъектовИнтегрированныхСистем.ТипВнешнегоОбъекта КАК ТипВнешнегоОбъекта,
		|	СвязиОбъектовИнтегрированныхСистем.ПредставлениеВнешнегоОбъекта КАК ПредставлениеВнешнегоОбъекта,
		|	СвязиОбъектовИнтегрированныхСистем.НавигационнаяСсылкаВнешнегоОбъекта КАК НавигационнаяСсылкаВнешнегоОбъекта,
		|	ИСТИНА КАК ЗаписьРегистра
		|ИЗ
		|	РегистрСведений.СвязиОбъектовИнтегрированныхСистем КАК СвязиОбъектовИнтегрированныхСистем
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДокументыПредприятия КАК ТребуемыйСправочник
		|		ПО СвязиОбъектовИнтегрированныхСистем.СсылкаНаОбъектДО = ТребуемыйСправочник.Ссылка
		|ГДЕ
		|	СвязиОбъектовИнтегрированныхСистем.УзелИнтегрированнойСистемы = &Узел
		|	И СвязиОбъектовИнтегрированныхСистем.ИДВнешнегоОбъекта = &ИДВнешнегоОбъекта
		|	И (СвязиОбъектовИнтегрированныхСистем.ТипВнешнегоОбъекта = &ТипВнешнегоОбъекта
		|	ИЛИ СвязиОбъектовИнтегрированныхСистем.ТипВнешнегоОбъекта = """")
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТребуемыйСправочник.Ссылка,
		|	ТребуемыйСправочник.ПометкаУдаления,
		|	СвязиОбъектовИнтегрированныхСистем.УзелИнтегрированнойСистемы,
		|	СвязиОбъектовИнтегрированныхСистем.ТипВнешнегоОбъекта,
		|	СвязиОбъектовИнтегрированныхСистем.ПредставлениеВнешнегоОбъекта,
		|	СвязиОбъектовИнтегрированныхСистем.НавигационнаяСсылкаВнешнегоОбъекта,
		|	ИСТИНА
		|ИЗ
		|	РегистрСведений.СвязиОбъектовИнтегрированныхСистем КАК СвязиОбъектовИнтегрированныхСистем
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДокументыПредприятия КАК ТребуемыйСправочник
		|		ПО СвязиОбъектовИнтегрированныхСистем.СсылкаНаОбъектДО = ТребуемыйСправочник.Ссылка
		|ГДЕ
		|	СвязиОбъектовИнтегрированныхСистем.УзелИнтегрированнойСистемы = ЗНАЧЕНИЕ(ПланОбмена.ИнтегрированныеСистемы.ПустаяСсылка)
		|	И СвязиОбъектовИнтегрированныхСистем.ИДВнешнегоОбъекта = &ИДВнешнегоОбъекта
		|	И (СвязиОбъектовИнтегрированныхСистем.ТипВнешнегоОбъекта = &ТипВнешнегоОбъекта
		|	ИЛИ СвязиОбъектовИнтегрированныхСистем.ТипВнешнегоОбъекта = """")
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТребуемыйСправочник.Ссылка,
		|	ТребуемыйСправочник.ПометкаУдаления,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ЛОЖЬ
		|ИЗ
		|	Справочник.ДокументыПредприятия КАК ТребуемыйСправочник
		|ГДЕ
		|	Ссылка = &СсылкаПоИдентификатору");
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"Справочник.ДокументыПредприятия КАК ТребуемыйСправочник",
		СтрШаблон("Справочник.%1 КАК ТребуемыйСправочник", ВидСправочника));
	Если ЛюбойУзел Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"СвязиОбъектовИнтегрированныхСистем.УзелИнтегрированнойСистемы = &Узел",
			"ИСТИНА");
	Иначе
		Запрос.УстановитьПараметр("Узел", ПараметрыСеанса.УзелИнтегрированнойСистемы);
	КонецЕсли;
	Запрос.УстановитьПараметр("СсылкаПоИдентификатору", СсылкаПоИдентификатору);
	Запрос.УстановитьПараметр("ИДВнешнегоОбъекта", ИДВнешнегоОбъекта);
	Запрос.УстановитьПараметр("ТипВнешнегоОбъекта", ТипВнешнегоОбъекта);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Действует = Не Выборка.ПометкаУдаления;
		Если Действует И ОбработкаЗапросовXDTOПовтИсп.ЕстьРеквизитДействует(
				ОбщегоНазначения.ИмяТаблицыПоСсылке(Выборка.Ссылка)) Тогда
			Действует = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Ссылка, "Действует");
		КонецЕсли;
		Если Не Действует Тогда
			Если Выборка.ЗаписьРегистра Тогда
				УдалитьСвязьОбъектаДОИВнешнегоОбъекта(ИДВнешнегоОбъекта, ТипВнешнегоОбъекта, Выборка.Ссылка);
			КонецЕсли;
			
			Возврат Неопределено;
		КонецЕсли;
		
		Если Выборка.ЗаписьРегистра
				И (Не ЗначениеЗаполнено(Выборка.УзелИнтегрированнойСистемы)
					Или Не ЗначениеЗаполнено(Выборка.ТипВнешнегоОбъекта)
					Или (Не ЗначениеЗаполнено(Выборка.ПредставлениеВнешнегоОбъекта)
						И ЗначениеЗаполнено(ПредставлениеВнешнегоОбъекта))
					Или (Не ЗначениеЗаполнено(Выборка.НавигационнаяСсылкаВнешнегоОбъекта)
						И ЗначениеЗаполнено(НавигационнаяСсылкаВнешнегоОбъекта))) Тогда
			ИсправитьЗаписьСвязиОбъектов(
				Выборка.Ссылка,
				Выборка.УзелИнтегрированнойСистемы,
				Выборка.ТипВнешнегоОбъекта,
				ПараметрыСеанса.УзелИнтегрированнойСистемы,
				ТипВнешнегоОбъекта,
				ИДВнешнегоОбъекта,
				ПредставлениеВнешнегоОбъекта,
				НавигационнаяСсылкаВнешнегоОбъекта);
		КонецЕсли;
		
		Возврат Выборка.Ссылка;
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Удаляет из регистра запись о связи.
//
// Параметры:
//   ИДВнешнегоОбъекта - Строка - идентификатор внешнего объекта.
//   ТипВнешнегоОбъекта - Строка - имя типа XDTO.
//   СсылкаНаОбъектДО - ЛюбаяСсылка - ссылка на объект Документооборота.
//
Процедура УдалитьСвязьОбъектаДОИВнешнегоОбъекта(ИДВнешнегоОбъекта, ТипВнешнегоОбъекта, СсылкаНаОбъектДО) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Узел = ПараметрыСеанса.УзелИнтегрированнойСистемы;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.УзелИнтегрированнойСистемы.Установить(Узел);
	НаборЗаписей.Отбор.ИДВнешнегоОбъекта.Установить(ИДВнешнегоОбъекта);
	НаборЗаписей.Отбор.ТипВнешнегоОбъекта.Установить(ТипВнешнегоОбъекта);
	НаборЗаписей.Отбор.СсылкаНаОбъектДО.Установить(СсылкаНаОбъектДО);
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли