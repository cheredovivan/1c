#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
// 
// Параметры:
//  Ограничение - См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
	
	Ограничение.ТекстДляВнешнихПользователей =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Параметры:
//  СсылкаНаОбъект - См. РегистрСведений.ЧерновикиЭлектронныхДокументовЛегкийКлиентЭДО.СсылкаНаОбъект
//  СостояниеЭДО - См. ПеречислениеСсылка.СостоянияДокументовЭДО
//  ОписаниеЧерновика - Неопределено
//                    - См. НовоеОписаниеЧерновика
Процедура Актуализировать(СсылкаНаОбъект, СостояниеЭДО, ОписаниеЧерновика = Неопределено) Экспорт
	
	Если ДокументПодлежитХранениюВЧерновикахПоСостояниюЭДО(СостояниеЭДО) Тогда
		Записать(СсылкаНаОбъект, СостояниеЭДО, ОписаниеЧерновика);
	Иначе
		Удалить(СсылкаНаОбъект);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  СсылкаНаОбъект - См. РегистрСведений.ЧерновикиЭлектронныхДокументовЛегкийКлиентЭДО.СсылкаНаОбъект
//  СостояниеЭДО - Неопределено
//               - См. РегистрСведений.ЧерновикиЭлектронныхДокументовЛегкийКлиентЭДО.СостояниеЭДО
//  ОписаниеЧерновика - Неопределено
//                    - См. НовоеОписаниеЧерновика
Процедура Записать(СсылкаНаОбъект, СостояниеЭДО = Неопределено, ОписаниеЧерновика = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(СостояниеЭДО) Тогда
		СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.НеСформирован;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.СсылкаНаОбъект.Установить(СсылкаНаОбъект);
	ОбщегоНазначенияБЭД.УстановитьУправляемуюБлокировкуПоНаборуЗаписей(НаборЗаписей);
	
	НаборЗаписей.Прочитать();
	
	Если Не ЗначениеЗаполнено(НаборЗаписей) Тогда
		ЗаписьНабора = НаборЗаписей.Добавить();
		ЗаписьНабора.СсылкаНаОбъект = СсылкаНаОбъект;
	КонецЕсли;
	
	Для Каждого Запись Из НаборЗаписей Цикл
		Запись.СостояниеЭДО = СостояниеЭДО;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ОписаниеЧерновика) Тогда
		Для Каждого Запись Из НаборЗаписей Цикл
			ЗаполнитьЗначенияСвойств(Запись, ОписаниеЧерновика);
		КонецЦикла;
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Параметры:
//  СсылкаНаОбъект - См. РегистрСведений.ЧерновикиЭлектронныхДокументовЛегкийКлиентЭДО.СсылкаНаОбъект
Процедура Удалить(СсылкаНаОбъект) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.СсылкаНаОбъект.Установить(СсылкаНаОбъект);
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура:
//  * Организация - Неопределено
//                - См. РегистрСведений.ЧерновикиЭлектронныхДокументовЛегкийКлиентЭДО.Организация
//  * Контрагент - Неопределено
//               - См. РегистрСведений.ЧерновикиЭлектронныхДокументовЛегкийКлиентЭДО.Контрагент
//  * СуммаДокумента - См. РегистрСведений.ЧерновикиЭлектронныхДокументовЛегкийКлиентЭДО.СуммаДокумента
//  * Дата - См. РегистрСведений.ЧерновикиЭлектронныхДокументовЛегкийКлиентЭДО.Дата
//  * Номер - См. РегистрСведений.ЧерновикиЭлектронныхДокументовЛегкийКлиентЭДО.Номер
//  * Вид - См. РегистрСведений.ЧерновикиЭлектронныхДокументовЛегкийКлиентЭДО.Вид
Функция НовоеОписаниеЧерновика() Экспорт
	
	ОписаниеЧерновика = Новый Структура;
	ОписаниеЧерновика.Вставить("Организация", Неопределено);
	ОписаниеЧерновика.Вставить("Контрагент", Неопределено);
	ОписаниеЧерновика.Вставить("СуммаДокумента", 0);
	ОписаниеЧерновика.Вставить("Дата", Дата(1, 1, 1));
	ОписаниеЧерновика.Вставить("Номер", "");
	ОписаниеЧерновика.Вставить("Вид", "");
	Возврат ОписаниеЧерновика;
	
КонецФункции

// Возвращаемое значение:
//  Массив Из См. ПеречислениеСсылка.СостоянияДокументовЭДО
Функция СостоянияЭДОДляЧерновиков() Экспорт
	
	СостоянияЭДО = Новый Массив; // Массив Из См. ПеречислениеСсылка.СостоянияДокументовЭДО
	СостоянияЭДО.Добавить(Перечисления.СостоянияДокументовЭДО.НеСформирован);
	СостоянияЭДО.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписание);
	СостоянияЭДО.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяУтверждение);
	СостоянияЭДО.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяОтправка);
	СостоянияЭДО.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодготовкаКОтправке);
	Возврат СостоянияЭДО;
	
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  СостояниеЭДО - См. ПеречислениеСсылка.СостоянияДокументовЭДО
//
// Возвращаемое значение:
//  Булево
Функция ДокументПодлежитХранениюВЧерновикахПоСостояниюЭДО(СостояниеЭДО)
	
	СостоянияЭДОДляЧерновиков = СостоянияЭДОДляЧерновиков();
	Если СостоянияЭДОДляЧерновиков.Найти(СостояниеЭДО) = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
		
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли
