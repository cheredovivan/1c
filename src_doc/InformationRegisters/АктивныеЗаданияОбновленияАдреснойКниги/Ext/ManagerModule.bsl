#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает информацию об активных заданиях обновления.
// 
// Возвращаемое значение:
//  Массив из УникальныйИдентификатор - Идентификаторы активных заданий.
// 
Функция АктивныеЗаданияОбновления() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	АктивныеЗаданияОбновленияАдреснойКниги.ИдентификаторЗадания КАК ИдентификаторЗадания
		|ИЗ
		|	РегистрСведений.АктивныеЗаданияОбновленияАдреснойКниги КАК АктивныеЗаданияОбновленияАдреснойКниги");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаРезультата = РезультатЗапроса.Выгрузить();
	
	АктивныеЗаданияОбновления = ТаблицаРезультата.ВыгрузитьКолонку("ИдентификаторЗадания");
	
	Возврат АктивныеЗаданияОбновления;
	
КонецФункции

// Добавляет информацию об активном задании обновления адресной книги.
// 
// Параметры:
//  ИдентификаторЗадания - УникальныйИдентификатор - Идентификатор задания.
// 
Процедура Добавить(ИдентификаторЗадания) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИдентификаторЗадания.Установить(ИдентификаторЗадания);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ИдентификаторЗадания = ИдентификаторЗадания;
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Удаляет информацию об активном задании обновления адресной книги.
//
// Параметры:
//	ИдентификаторЗадания - УникальныйИдентификатор - Идентификатор задания.
// 
Процедура Удалить(ИдентификаторЗадания) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИдентификаторЗадания.Установить(ИдентификаторЗадания);
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
