
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВернутьРезультат = Параметры.ВернутьРезультат;
	ФизическоеЛицо = Объект.Ссылка;
	
	ПолФизЛица = Объект.Пол;
	ПолЧислом = ФизическиеЛица.ПолЦифрой(ПолФизЛица);
	
	Если ПолЧислом = 0 Тогда
		ПолЧислом = 1;
	КонецЕсли;	
	
	ДатаИзменения = НачалоДня(ТекущаяДатаСеанса());
	// ++ Локализация
	СуффиксРусскогоЯзыка = СуффиксРусскогоЯзыка();
	ПросклонятьФИОНаСервере(); // заполним падежи, на случай сохранения настроек.
	// -- Локализация
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПослеВводаСтрокНаРазныхЯзыках" Тогда
		
		// ++ Локализация
		ПросклонятьФИО();
		// -- Локализация
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФИОПриИзменении(Элемент)
	
	// ++ Локализация
	ПросклонятьФИО(); 
	// -- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда) 
	
	ДанныеСменыФИО = ВыполнитьДействие();
	Если ВернутьРезультат Тогда
		
		Закрыть(ДанныеСменыФИО);
		
	Иначе
		
		Оповестить(ФизическиеЛицаКлиент.ИмяОповещенияОбИзмененииФИО(), ФизическоеЛицо);
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)
	
	МультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

// Если ВернутьРезультат, то возвращает данные для изменения, иначе - записывает все изменения
// 
// Возвращаемое значение:
//  См. ДанныеСменыФИО
//
&НаСервере
Функция ВыполнитьДействие()
	
	// При чтении на сервере мы меняем местами реквизиты Наименования, чтобы пользователю отражалось ФИО на его языке
	// МультиязычностьСервер.ПередЗаписьюНаСервере меняет реквизиты Наименования местами обратно
	//
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	МультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);
		Объект.Наименование = ТекущийОбъект.Наименование;
	Объект.Наименование = ТекущийОбъект.Наименование;
	Объект.НаименованиеЯзык1 = ТекущийОбъект.НаименованиеЯзык1;
	Объект.НаименованиеЯзык2 = ТекущийОбъект.НаименованиеЯзык2; 
	ДанныеСменыФИО = ДанныеСменыФИО();
	
	Если ВернутьРезультат Тогда
		
		Возврат ДанныеСменыФИО;
		
	КонецЕсли;
	
	ИспользуетсяЯзык1 = МультиязычностьСервер.ИспользуетсяДополнительныйЯзык(1);
	ИспользуетсяЯзык2 = МультиязычностьСервер.ИспользуетсяДополнительныйЯзык(2);
	
	МенеджерЗаписи = РегистрыСведений.ФИОФизическихЛиц.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ДанныеСменыФИО);
	
	НовоеФИО = Объект.Наименование;
	
	Если ИспользуетсяЯзык1 Тогда
		НовоеФИО1 = Объект.НаименованиеЯзык1;
		МенеджерЗаписи.ФИОЯзык1 = ?(ЗначениеЗаполнено(НовоеФИО1), НовоеФИО1, НовоеФИО);
	КонецЕсли;
	
	Если ИспользуетсяЯзык2 Тогда
		НовоеФИО2 = Объект.НаименованиеЯзык2;
		МенеджерЗаписи.ФИОЯзык2 = ?(ЗначениеЗаполнено(НовоеФИО2), НовоеФИО2, НовоеФИО); 
	КонецЕсли;
	
	МенеджерЗаписи.Записать(Истина);
	
	ФизическиеЛица.ОбновитьНаименованиеИСклоненияФизЛица(ФизическоеЛицо);
	
	Возврат ДанныеСменыФИО;
	
КонецФункции

// ++ Локализация
&НаКлиенте
Процедура ЗавершитьСклонение(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Склонения <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Склонения);
	КонецЕсли;
	
	Модифицированность = Ложь;
	
КонецПроцедуры
// -- Локализация

// Данные для записи в РС ФиоФизическихЛиц
// 
// Возвращаемое значение:
//  Структура:
// * ФИО - Строка
// * Период - Дата
// * ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
// // ++ Локализация
// * ИменительныйПадеж - Строка
// * РодительныйПадеж - Строка
// * ВинительныйПадеж - Строка
// * ДательныйПадеж - Строка
// * ТворительныйПадеж - Строка
// * ПредложныйПадеж - Строка
// // -- Локализация
&НаСервере
Функция ДанныеСменыФИО()
	
	ДанныеСменыФИО = Новый Структура();
	ДанныеСменыФИО.Вставить("ФИО", Объект.Наименование);
	ДанныеСменыФИО.Вставить("ФИОЯзык1", Объект.НаименованиеЯзык1);
	ДанныеСменыФИО.Вставить("ФИОЯзык2", Объект.НаименованиеЯзык2);
	ДанныеСменыФИО.Вставить("Период", ДатаИзменения);
	ДанныеСменыФИО.Вставить("ФизическоеЛицо", ФизическоеЛицо);
	// ++ Локализация
	ДанныеСменыФИО.Вставить("ИменительныйПадеж", Именительный);
	ДанныеСменыФИО.Вставить("РодительныйПадеж", Родительный);
	ДанныеСменыФИО.Вставить("ВинительныйПадеж", Винительный);
	ДанныеСменыФИО.Вставить("ДательныйПадеж", Дательный);
	ДанныеСменыФИО.Вставить("ТворительныйПадеж", Творительный);
	ДанныеСменыФИО.Вставить("ПредложныйПадеж", Предложный);
	// -- Локализация
	Возврат ДанныеСменыФИО;
	
КонецФункции

// ++ Локализация
// Возвращает суффикс русского языка
// 
// Возвращаемое значение:
//  Строка
//
&НаСервереБезКонтекста
Функция СуффиксРусскогоЯзыка()
	
	КодРусскогоЯзыка = "ru";
	
	Если ТекущийЯзык().КодЯзыка = КодРусскогоЯзыка Тогда
		
		Возврат "";
		
	КонецЕсли;
	
	Возврат МультиязычностьДокументооборот.СуффиксРусскогоЯзыка();
	
КонецФункции
// -- Локализация

// ++ Локализация
// Склоняет ФИО на РУССКОМ языке (даже если основной английский, то процедура находит русский и его склоняет)
//
&НаКлиенте
Процедура ПросклонятьФИО()
	
	ПодсистемаСклоненийСуществует = Ложь;
	Если ЗначениеЗаполнено(Объект.Наименование) Тогда
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершитьСклонение", ЭтотОбъект);
		
		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.СклонениеПредставленийОбъектов") Тогда
			ПодсистемаСклоненийСуществует = Истина;
			МодульСклонениеПредставленийОбъектовКлиентСервер = ОбщегоНазначенияКлиент.ОбщийМодуль(
				"СклонениеПредставленийОбъектовКлиентСервер");
			МодульСклонениеПредставленийОбъектовКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
				"СклонениеПредставленийОбъектовКлиент");
		КонецЕсли;
		
		ПараметрыСклонения = МодульСклонениеПредставленийОбъектовКлиентСервер.ПараметрыСклонения();
		ПараметрыСклонения.ЭтоФИО = Истина; 
		Если ЗначениеЗаполнено(ПолЧислом) Тогда
			ПараметрыСклонения.Пол = ПолЧислом;
		КонецЕсли;
		Представление = Объект["Наименование" + СуффиксРусскогоЯзыка];
		
		Если ПодсистемаСклоненийСуществует Тогда
			МодульСклонениеПредставленийОбъектовКлиент.НачатьСклонение(ЭтотОбъект, Представление,
				ПараметрыСклонения, Истина, ОповещениеОЗавершении);
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры
// -- Локализация

// ++ Локализация
// Склоняет ФИО на РУССКОМ языке (даже если основной английский, то процедура находит русский и его склоняет)
&НаСервере
Процедура ПросклонятьФИОНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Наименование) Тогда
		Возврат;
	КонецЕсли;
	
	ПодсистемаСклоненийСуществует = Ложь;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.СклонениеПредставленийОбъектов") Тогда
		ПодсистемаСклоненийСуществует = Истина;
		МодульСклонениеПредставленийОбъектов = ОбщегоНазначения.ОбщийМодуль(
			"СклонениеПредставленийОбъектов");
	КонецЕсли;
	
	Представление = Объект["Наименование" + СуффиксРусскогоЯзыка];
	
	Если ПодсистемаСклоненийСуществует Тогда
		Именительный = МодульСклонениеПредставленийОбъектов.ПросклонятьФИО(
			Представление, 1, Объект.Ссылка, ПолЧислом);
		Родительный = МодульСклонениеПредставленийОбъектов.ПросклонятьФИО(
			Представление, 2, Объект.Ссылка, ПолЧислом);
		Дательный = МодульСклонениеПредставленийОбъектов.ПросклонятьФИО(
			Представление, 3, Объект.Ссылка, ПолЧислом);
		Винительный = МодульСклонениеПредставленийОбъектов.ПросклонятьФИО(
			Представление, 4, Объект.Ссылка, ПолЧислом);
		Творительный = МодульСклонениеПредставленийОбъектов.ПросклонятьФИО(
			Представление, 5, Объект.Ссылка, ПолЧислом);
		Предложный = МодульСклонениеПредставленийОбъектов.ПросклонятьФИО(
			Представление, 6, Объект.Ссылка, ПолЧислом);
	КонецЕсли;
	
КонецПроцедуры
// -- Локализация
#КонецОбласти
