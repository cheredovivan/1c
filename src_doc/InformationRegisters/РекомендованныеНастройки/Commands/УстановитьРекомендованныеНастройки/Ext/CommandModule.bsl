#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ФормаДляСохраненияНастроек = ПараметрыВыполненияКоманды.Источник;
	Если ТипЗнч(ФормаДляСохраненияНастроек) <> Тип("ФормаКлиентскогоПриложения") Тогда
		
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Невозможно установить рекомендованные настройки для объекта типа ""%1""'"),
			ТипЗнч(ФормаДляСохраненияНастроек));
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ФормаДляСохраненияНастроек.Закрыть();
	Если ФормаДляСохраненияНастроек.Открыта() Тогда
		
		ПоказатьПредупреждение(,
			НСтр("ru = 'Не удалось установить настройки, так как форма осталась открытой'"),,
			НСтр("ru = 'Настройки не установлены'"));
		
		Возврат;
		
	КонецЕсли;
	
	УстановитьРекомендованныеНастройки(
		ФормаДляСохраненияНастроек.ИмяФормы,
		ФормаДляСохраненияНастроек.КлючНазначенияИспользования);
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Настройки установлены'"),,
		НСтр("ru = 'Рекомендованные настройки формы установлены'"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьРекомендованныеНастройки(Знач ИмяФормы, Знач КлючНазначенияИспользования)
	
	РаботаСРекомендованнымиНастройками.УстановитьРекомендованныеНастройки(
		ИмяФормы,
		КлючНазначенияИспользования,
		Истина);
	
КонецПроцедуры

#КонецОбласти