#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Записывает статьи ДДС указанного документа в привилегированном режиме.
//
// Параметры:
//   ДокументОбъект - СправочникОбъект.ДокументыПредприятия
//   СтатьиДвиженияДенежныхСредств - ДанныеФормыКоллекция
//                                 - ТаблицаЗначений
//
Процедура ЗаписатьСтатьиДокумента(ДокументОбъект, СтатьиДвиженияДенежныхСредств) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Документ = ДокументОбъект.Ссылка;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(Документ);
	
	СуммаСтатей = 0;
	
	Для Каждого СтрокаСтатьи Из СтатьиДвиженияДенежныхСредств Цикл
		Запись = НаборЗаписей.Добавить();
		Запись.Документ = Документ;
		Запись.СтатьяДвиженияДенежныхСредств = СтрокаСтатьи.СтатьяДвиженияДенежныхСредств;
		Запись.Сумма = СтрокаСтатьи.Сумма;
		Запись.СуммаНДС = СтрокаСтатьи.СуммаНДС;
		
		СуммаСтатей = СуммаСтатей + СтрокаСтатьи.Сумма;
	КонецЦикла;
	
	Если СуммаСтатей <> ДокументОбъект.Сумма Тогда
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'В документе %1 сумма не соответствует ее расшифровке по статьям движения денежных средств'"),
			Документ);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Читает статьи ДДС указанного документа в привилегированном режиме.
//
// Параметры:
//   Документ - СправочникСсылка.ДокументыПредприятия
//   СтатьиДвиженияДенежныхСредств - ДанныеФормыКоллекция
//                                 - ТаблицаЗначений - неявно возвращаемый параметр.
//
Процедура ПрочитатьСтатьиДокумента(Документ, СтатьиДвиженияДенежныхСредств) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СтатьиДвиженияДенежныхСредствДокументов.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	СтатьиДвиженияДенежныхСредствДокументов.Сумма КАК Сумма,
		|	СтатьиДвиженияДенежныхСредствДокументов.СуммаНДС КАК СуммаНДС
		|ИЗ
		|	РегистрСведений.СтатьиДвиженияДенежныхСредствДокументов КАК СтатьиДвиженияДенежныхСредствДокументов
		|ГДЕ
		|	СтатьиДвиженияДенежныхСредствДокументов.Документ = &Документ");
	Запрос.УстановитьПараметр("Документ", Документ);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	СтатьиДвиженияДенежныхСредств.Очистить();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СтрокаСтатьи = СтатьиДвиженияДенежныхСредств.Добавить();
		СтрокаСтатьи.СтатьяДвиженияДенежныхСредств = ВыборкаДетальныеЗаписи.СтатьяДвиженияДенежныхСредств;
		СтрокаСтатьи.Сумма = ВыборкаДетальныеЗаписи.Сумма;
		СтрокаСтатьи.СуммаНДС = ВыборкаДетальныеЗаписи.СуммаНДС;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
