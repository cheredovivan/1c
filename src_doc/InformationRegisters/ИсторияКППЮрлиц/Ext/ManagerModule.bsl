#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Прочитать историю юрлица - функция-обертка для лаконичности вызовов.
// 
// Параметры:
//  Юрлицо - ОпределяемыйТип.Юрлицо - Юрлицо
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица значений
Функция ПрочитатьИсториюЮрлица(Юрлицо) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	История.Период КАК Период,
		|	История.Юрлицо,
		|	История.КПП
		|ИЗ
		|	РегистрСведений.ИсторияКППЮрлиц КАК История
		|ГДЕ
		|	История.Юрлицо = &Юрлицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период"
	);
	Запрос.УстановитьПараметр("Юрлицо", Юрлицо);
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Создать набор записей по юрлицу с отбором без чтения - функция-обертка для лаконичности вызовов.
// 
// Параметры:
//  Юрлицо - ОпределяемыйТип.Юрлицо - Юрлицо
// 
// Возвращаемое значение:
//  РегистрСведенийНаборЗаписей - Набор записей
Функция СоздатьНаборИсторииЮрлица(Юрлицо) Экспорт
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Юрлицо.Установить(Юрлицо);
	Возврат Набор;
	
КонецФункции

#КонецОбласти

#КонецЕсли