#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавить запись.
// 
// Параметры:
//  ПараметрыЗаписи - Структура
Процедура ДобавитьЗапись(ПараметрыЗаписи) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, ПараметрыЗаписи);
	Запись.Записать(Истина);		
	
КонецПроцедуры
	
// Удалить запись.
// 
// Параметры:
//  Файл - СправочникСсылка.ВерсииФайлов
Процедура УдалитьЗапись(Файл) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Файл.Установить(Файл);
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Записать данные.
// 
// Параметры:
//  Файл - СправочникСсылка.ВерсииФайлов
//  РаспознанныеДанные  - ТаблицаЗначений
//  ИмяТаблицы - Строка
//  ЗаписыватьНеРаспознанные - Булево
Процедура ЗаписатьДанные(Файл, РаспознанныеДанные, ИмяТаблицы = "Товары", ЗаписыватьНеРаспознанные = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Файл.Установить(Файл);
	
	Для Каждого Данные Из РаспознанныеДанные Цикл
		
		Если Не ЗаписыватьНеРаспознанные Тогда
			Если ПустаяСтрока(Данные.РаспознанныйТекст) Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Данные);
		НоваяЗапись.Файл = Файл;
		НоваяЗапись.ИмяТаблицы = ИмяТаблицы;
		НоваяЗапись.ИмяРаспознанногоРеквизита = Данные.ИмяРеквизита;
		
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти
	
#Область СлужебныйПрограммныйИнтерфейс

Функция ДанныеТаблицыДокумента(ДокументСсылка, ИмяТаблицы) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	РаспознанныеДанныеТаблиц.ИмяТаблицы КАК ИмяТаблицы,
	|	РаспознанныеДанныеТаблиц.ИмяРаспознанногоРеквизита КАК ИмяРеквизита,
	|	РаспознанныеДанныеТаблиц.НомерСтрокиТаблицы КАК НомерСтрокиТаблицы,
	|	РаспознанныеДанныеТаблиц.КоординатаX0 КАК КоординатаX0,
	|	РаспознанныеДанныеТаблиц.КоординатаX1 КАК КоординатаX1,
	|	РаспознанныеДанныеТаблиц.КоординатаY0 КАК КоординатаY0,
	|	РаспознанныеДанныеТаблиц.КоординатаY1 КАК КоординатаY1,
	|	РаспознанныеДанныеТаблиц.ОбластьИзображения КАК ОбластьИзображения,
	|	РаспознанныеДанныеТаблиц.РаспознанноеЗначение КАК РаспознанноеЗначение,
	|	РаспознанныеДанныеТаблиц.РаспознанныйТекст КАК РаспознанныйТекст,
	|	РаспознанныеДанныеТаблиц.СтрокВИзображении КАК СтрокВИзображении,
	|	РаспознанныеДанныеТаблиц.УверенностьРаспознанногоЗначения КАК УверенностьРаспознанногоЗначения,
	|	РаспознанныеДанныеТаблиц.ЭлементСоздан КАК ЭлементСоздан
	|ИЗ
	|	РегистрСведений.РезультатыРаспознаванияДокументов КАК РезультатыРаспознаванияДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаспознанныеДанныеТаблиц КАК РаспознанныеДанныеТаблиц
	|		ПО РезультатыРаспознаванияДокументов.Файл = РаспознанныеДанныеТаблиц.Файл
	|ГДЕ
	|	РезультатыРаспознаванияДокументов.Документ = &Документ
	|	И РаспознанныеДанныеТаблиц.ИмяТаблицы В (&ИмяТаблицы)");
	
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	Запрос.УстановитьПараметр("ИмяТаблицы", ИмяТаблицы);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#КонецЕсли