#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСинхронизациюДанных") Тогда
		ЗарегистрироватьСогласуемыеДокументы("ОбменБухгалтерияПредприятияДокументооборот20");
		ЗарегистрироватьСогласуемыеДокументы("ОбменУправлениеХолдингомДокументооборот20");
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить(
		"ПараметрыОбновленияДанныхКэширующихОбъектов",
		РегистрыСведений.ТекущиеСостоянияДокументов.ЗначенияПараметровОбновленияДанныхКэширующихОбъектов(ЭтотОбъект));
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.ТекущиеСостоянияДокументов.ОбновитьДанныеКэширующихОбъектов(
		ЭтотОбъект, ДополнительныеСвойства.ПараметрыОбновленияДанныхКэширующихОбъектов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Регистрирует документы при изменении состояния согласования
//
// Параметры:
//  ИмяПланаОбмена - Строка - имя плана обмена, для узлов которого следует выполнить регистрацию
//
Процедура ЗарегистрироватьСогласуемыеДокументы(ИмяПланаОбмена)
	
	УзлыПланаОбмена = ОбменДаннымиДокументооборотПовтИсп.УзлыПланаОбмена(ИмяПланаОбмена);
	Если УзлыПланаОбмена.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СоставПланаОбмена = Метаданные.ПланыОбмена[ИмяПланаОбмена].Состав;
	
	Для каждого Запись Из ЭтотОбъект Цикл
		Если (Запись.Состояние = Перечисления.СостоянияДокументов.НаСогласовании
			или Запись.Состояние = Перечисления.СостоянияДокументов.Согласован
			или Запись.Состояние = Перечисления.СостоянияДокументов.НеСогласован) 
			и СоставПланаОбмена.Содержит(Запись.Документ.Метаданные()) Тогда
			ПланыОбмена.ЗарегистрироватьИзменения(УзлыПланаОбмена, Запись.Документ);
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли