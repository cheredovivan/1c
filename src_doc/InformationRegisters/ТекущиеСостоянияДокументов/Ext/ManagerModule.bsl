#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет есть ли текущее состояние у документа.
// 
// Параметры:
//  ДокументПредприятия - СправочникСсылка.ДокументыПредприятия
//  Состояние - ПеречислениеСсылка.СостоянияДокументов
// 
// Возвращаемое значение:
//  Булево
// 
Функция ЕстьСостояние(ДокументПредприятия, Состояние) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТекущиеСостоянияДокументов.Документ КАК Документ
		|ИЗ
		|	РегистрСведений.ТекущиеСостоянияДокументов КАК ТекущиеСостоянияДокументов
		|ГДЕ
		|	ТекущиеСостоянияДокументов.Документ = &ДокументПредприятия
		|	И ТекущиеСостоянияДокументов.Состояние = &Состояние");
	Запрос.УстановитьПараметр("ДокументПредприятия", ДокументПредприятия);
	Запрос.УстановитьПараметр("Состояние", Состояние);
	РезультатЗапроса = Запрос.Выполнить();
	ЕстьСостояние = Не РезультатЗапроса.Пустой();
	
	Возврат ЕстьСостояние;
	
КонецФункции

// Конструктор параметров обновления данных кэширующих объектов.
//
// Возвращаемое значение:
//  Структура:
//   * ДокументыДляОбновления - Массив из СправочникСсылка.ДокументыПредприятия
//
Функция ПараметрыОбновленияДанныхКэширующихОбъектов() Экспорт
	
	ПараметрыОбновления = Новый Структура;
	ПараметрыОбновления.Вставить("ДокументыДляОбновления", Новый Массив);
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Устанавливает значения параметров обновления данных кэширующих объектов по данным объекта.
//
// Параметры:
//  НаборЗаписей - РегистрСведенийНаборЗаписей.ТекущиеСостоянияДокументов
//
// Возвращаемое значение:
//  См. РегистрыСведений.ТекущиеСостоянияДокументов.ПараметрыОбновленияДанныхКэширующихОбъектов
//
Функция ЗначенияПараметровОбновленияДанныхКэширующихОбъектов(НаборЗаписей) Экспорт
	
	ПараметрыОбновления = ПараметрыОбновленияДанныхКэширующихОбъектов();
	
	ПредыдущиеЗаписи = ОбщегоНазначенияДокументооборот.ПредыдущиеЗаписи(НаборЗаписей);
	ТекущиеЗаписи = НаборЗаписей.Выгрузить();
	
	// Влияет на Документы.МоиДокументы.ДанныеМоегоДокумента().
	УдаленныеИДобавленныеЗаписи =
		ОбщегоНазначенияДокументооборот.УдаленныеИДобавленныеЗаписи(НаборЗаписей, ТекущиеЗаписи, ПредыдущиеЗаписи);
	Для Каждого Запись Из УдаленныеИДобавленныеЗаписи Цикл
		
		Если Запись.Состояние <> Перечисления.СостоянияДокументов.Исполнен Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПараметрыОбновления.ДокументыДляОбновления.Найти(Запись.Документ) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыОбновления.ДокументыДляОбновления.Добавить(Запись.Документ);
		
	КонецЦикла;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Обновляет данные зависимых объектов согласно установленным параметрам.
//
// Параметры:
//  НаборЗаписей - РегистрСведенийНаборЗаписей.ТекущиеСостоянияДокументов
//  ПараметрыОбновления - См. РегистрыСведений.ТекущиеСостоянияДокументов.ПараметрыОбновленияДанныхКэширующихОбъектов
//
Процедура ОбновитьДанныеКэширующихОбъектов(НаборЗаписей, ПараметрыОбновления) Экспорт
	
	Для Каждого ДокументДляОбновления Из ПараметрыОбновления.ДокументыДляОбновления Цикл
		ВлияющийОбъектМетаданных = Метаданные.РегистрыСведений.ТекущиеСостоянияДокументов.ПолноеИмя();
		ЗависимыйОбъектМетаданных = Метаданные.Документы.ДанныеМоегоДокумента.ПолноеИмя();
		РегистрыСведений.ОчередьОбновленияКэширующихДанных.Добавить(
			ЗависимыйОбъектМетаданных,
			ВлияющийОбъектМетаданных,
			ДокументДляОбновления);
	КонецЦикла;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

#Область ОбработчикиОбновления

#Область ПерейтиНаВерсию_3_0_18_13

// Регистрирует данные к обновлению в плане обмена ОбновлениеИнформационнойБазы.
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию_3_0_18_13(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.РегистрыСведений.ТекущиеСостоянияДокументов;
	ПолноеИмяРегистра = МетаданныеОбъекта.ПолноеИмя();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяРегистра;
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ТекущиеСостоянияДокументов.Документ КАК Документ,
		|	ТекущиеСостоянияДокументов.Состояние КАК Состояние,
		|	ТекущиеСостоянияДокументов.Действие КАК Действие
		|ИЗ
		|	РегистрСведений.ТекущиеСостоянияДокументов КАК ТекущиеСостоянияДокументов
		|ГДЕ
		|	ТекущиеСостоянияДокументов.Документ >= &Документ
		|	И ТекущиеСостоянияДокументов.Действие <> НЕОПРЕДЕЛЕНО
		|
		|УПОРЯДОЧИТЬ ПО
		|	Документ");
	
	Документ = МетаданныеОбъекта.Измерения.Документ.Тип.ПривестиЗначение();
	
	Пока Истина Цикл
		
		Запрос.УстановитьПараметр("Документ", Документ);
		
		//@skip-check query-in-loop
		Выгрузка = Запрос.Выполнить().Выгрузить();
		КоличествоСтрок = Выгрузка.Количество();
		
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Выгрузка, ДополнительныеПараметры);
		
		Если КоличествоСтрок < 1000 Тогда
			Прервать;
		Иначе
			Документ = Выгрузка[КоличествоСтрок - 1].Документ;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Обрабатывает данные, зарегистрированные в плане обмена ОбновлениеИнформационнойБазы.
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию_3_0_18_13(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.РегистрыСведений.ТекущиеСостоянияДокументов;
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	ТаблицаКОбновления = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Для Каждого СтрокаКОбновлению Из ТаблицаКОбновления Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных();
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ТекущиеСостоянияДокументов");
			ЭлементБлокировки.УстановитьЗначение("Документ", СтрокаКОбновлению.Документ);
			ЭлементБлокировки.УстановитьЗначение("Состояние", СтрокаКОбновлению.Состояние);
			ЭлементБлокировки.УстановитьЗначение("Действие", СтрокаКОбновлению.Действие);
			Блокировка.Заблокировать();
			
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Документ.Установить(СтрокаКОбновлению.Документ);
			НаборЗаписей.Отбор.Состояние.Установить(СтрокаКОбновлению.Состояние);
			НаборЗаписей.Отбор.Действие.Установить(СтрокаКОбновлению.Действие);
			НаборЗаписей.Прочитать();
			
			ОбработкаДанных_3_0_18_13(НаборЗаписей);
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				МетаданныеОбъекта,
				СтрокаКОбновлению.Документ,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Не удалось обработать некоторые объекты (пропущены): %1'"), 
			ПроблемныхОбъектов);
	Иначе
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			МетаданныеОбъекта,,
			СтрШаблон(
				НСтр("ru = 'Обработана очередная порция: %1'"),
				ОбъектовОбработано));
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = ОбъектовОбработано;
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
		Параметры.Очередь,
		ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет реквизит "ВидДействия".
//
Процедура ОбработкаДанных_3_0_18_13(НаборЗаписей)
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запись = НаборЗаписей[0];
	
	Если Не ЗначениеЗаполнено(Запись.Действие) Или ЗначениеЗаполнено(Запись.ВидДействия) Тогда
		Возврат;
	КонецЕсли;
	
	//@skip-check bsl-legacy-check-string-literal
	Запись.ВидДействия = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.Действие, "ВидДействия");
	
	ОбновлениеИнформационнойБазыДокументооборот.ЗаписатьДанные(НаборЗаписей);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли