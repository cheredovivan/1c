#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает структуру-описание абстрактного события для записи в историю вызовом .Добавить().
//
// Возвращаемое значение:
//  Структура - см. код.
//
Функция Событие(ИмяФайла = "", ЭтоЗагрузкаДанных = Истина) Экспорт
	
	Событие = Новый Структура;
	Событие.Вставить("Дата", Неопределено);
	Событие.Вставить("ОсновнойОбъект", Неопределено);
	Событие.Вставить("Объект", Неопределено);
	Событие.Вставить("ТипСобытия", Неопределено);
	Событие.Вставить("ИмяФайла", ИмяФайла);
	Событие.Вставить("КраткоеОписание", "");
	Событие.Вставить("Направление", Неопределено);
	Событие.Вставить("ДополнительныеСведения", "");
	
	Событие.Направление = ?(ЭтоЗагрузкаДанных,
		Перечисления.НаправленияОбмена.Получение, Перечисления.НаправленияОбмена.Отправка);
	
	Возврат Событие;
	
КонецФункции

// Добавляет запись в историю.
//
// Параметры:
//  Событие - Структура - см Событие.
//
Процедура Добавить(Событие) Экспорт
	
	Если ЗначениеЗаполнено(Событие.Объект) И Не ЗначениеЗаполнено(Событие.ОсновнойОбъект) Тогда
		Событие.ОсновнойОбъект = Событие.Объект;
	КонецЕсли;
	Если ЗначениеЗаполнено(Событие.ОсновнойОбъект) И Не ЗначениеЗаполнено(Событие.Объект) Тогда
		Событие.Объект = Событие.ОсновнойОбъект;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Событие.Дата) Тогда
		// Потребитель должен передать универсальную дату.
		Событие.Дата = МестноеВремя(Событие.Дата, ЧасовойПоясСеанса());
		Событие.Вставить("ОтметкаВремени", ОбменСАрхивом.ОтметкаВремениПоДате(Событие.Дата));
	Иначе
		Событие.Дата = ТекущаяДатаСеанса();
		Событие.Вставить("ОтметкаВремени", ОбменСАрхивом.ТекущаяОтметкаВремени());
	КонецЕсли;
	
	ДлинаКраткогоОписания = Метаданные.РегистрыСведений.ИсторияОбменаС1САрхивом.
		Ресурсы.КраткоеОписание.Тип.КвалификаторыСтроки.Длина;
	Если ДлинаКраткогоОписания < СтрДлина(Событие.КраткоеОписание)
			И Не ЗначениеЗаполнено(Событие.ДополнительныеСведения) Тогда
		Событие.ДополнительныеСведения = Событие.КраткоеОписание;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, Событие);
	Запись.Записать(Истина);
	
КонецПроцедуры

// Возвращает признак наличия ошибок при последней попытке выгрузки описи.
//
// Параметры:
//  ОписьСсылка - ДокументСсылка.ПередачаДелВАрхив - опись.
//
// Возвращаемое значение:
//  Булево - Истина, если есть ошибки.
// 
Функция ЕстьОшибкиВыгрузкиОписи(ОписьСсылка) Экспорт
	
	Условие = "История.ОсновнойОбъект = &ОписьСсылка";
	ПараметрыЗапроса = Новый Структура("ОписьСсылка", ОписьСсылка);
	
	Возврат ЕстьОшибкиПоУсловию(Условие, ПараметрыЗапроса);
	
КонецФункции

// Возвращает признак наличия ошибок при последней попытке загрузки файла.
//
// Параметры:
//  ИмяФайла - Строка - имя файла (не полное).
//
// Возвращаемое значение:
//  Булево - Истина, если есть ошибки.
// 
Функция ЕстьОшибкиЗагрузкиФайла(ИмяФайла) Экспорт
	
	Условие = "История.ИмяФайла = &ИмяФайла";
	ПараметрыЗапроса = Новый Структура("ИмяФайла", ИмяФайла);
	
	Возврат ЕстьОшибкиПоУсловию(Условие, ПараметрыЗапроса);
	
КонецФункции

// Проверяет, является ли объект основным (т.е. таким, который передается в отдельном файле при обмене).
//
// Параметры:
//  ТипСсылкиНаОбъект - Тип - тип ссылка на объект истории.
//
// Возвращаемое значение:
//  Булево - Истина, если объект является основным объектом обмена.
//
Функция ЭтоОсновнойОбъектОбмена(ТипСсылкиНаОбъект) Экспорт
	
	Возврат ТипСсылкиНаОбъект = Тип("ДокументСсылка.ПередачаДелВАрхив")
				Или ТипСсылкиНаОбъект = Тип("ДокументСсылка.УничтожениеДел");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЕстьОшибкиПоУсловию(Условие, ПараметрыЗапроса)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОтметкаНачалаПоследнейВыгрузки = "";
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЕСТЬNULL(МАКСИМУМ(История.ОтметкаВремени), """") КАК ОтметкаВремени
		|ИЗ
		|	РегистрСведений.ИсторияОбменаС1САрхивом КАК История
		|ГДЕ
		|	&Условие
		|	И История.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.СобытияОбменаС1САрхивом.НачалоОбмена)");
	УсловиеДляВычисленияДатыНачала = Условие;
	Если Условие = "История.ИмяФайла = &ИмяФайла" Тогда
		УсловиеДляВычисленияДатыНачала = УсловиеДляВычисленияДатыНачала
			+ " И История.ОсновнойОбъект = НЕОПРЕДЕЛЕНО";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", УсловиеДляВычисленияДатыНачала);
	Для Каждого КлючИЗначение Из ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ОтметкаНачалаПоследнейВыгрузки = Выборка.ОтметкаВремени;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ОтметкаНачалаПоследнейВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьЗаписи
		|ИЗ
		|	РегистрСведений.ИсторияОбменаС1САрхивом КАК История
		|ГДЕ
		|	&Условие
		|	И История.ОтметкаВремени >= &ОтметкаНачалаПоследнейВыгрузки
		|	И История.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.СобытияОбменаС1САрхивом.Ошибка)";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", Условие);
	Запрос.УстановитьПараметр("ОтметкаНачалаПоследнейВыгрузки", ОтметкаНачалаПоследнейВыгрузки);
	ЕстьОшибки = Не Запрос.Выполнить().Пустой();
	
	Возврат ЕстьОшибки;
	
КонецФункции

#КонецОбласти

#КонецЕсли
