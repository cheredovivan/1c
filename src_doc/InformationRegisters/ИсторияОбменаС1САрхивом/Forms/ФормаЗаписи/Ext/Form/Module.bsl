
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТолькоПросмотр = Истина;
	Элементы.КартинкаСобытия.Подсказка = Строка(Запись.ТипСобытия);
	Если Запись.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Ошибка Тогда
		Элементы.КартинкаСобытия.Картинка = БиблиотекаКартинок.Ошибка;
	ИначеЕсли Запись.ТипСобытия = Перечисления.СобытияОбменаС1САрхивом.Предупреждение Тогда
		Элементы.КартинкаСобытия.Картинка = БиблиотекаКартинок.Предупреждение;
	Иначе
		Элементы.КартинкаСобытия.Картинка = БиблиотекаКартинок.СобытиеИнформация;
		Элементы.КартинкаСобытия.Подсказка = Строка(Перечисления.СобытияОбменаС1САрхивом.Информация);
	КонецЕсли;
	
	// Поиск файла на сервере.
	Если Не ЗначениеЗаполнено(Запись.ИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаФайлов = ОбменСАрхивомТранспорт.НоваяТаблицаФайловДляЗагрузки();
	ОтображаемыйПутьФайла = Запись.ИмяФайла;
	ПараметрыТранспорта = ОбменСАрхивомТранспорт.ПараметрыТранспортаДляСеансаОбмена();
	ТаблицаФайлов = ОбменСАрхивомТранспорт.НайтиФайлИзИстории(
		ПараметрыТранспорта, Запись.ИмяФайла, Запись.ТипСобытия, Запись.Направление);
	
	ЭлементПутьФайла = Элементы.ОтображаемыйПутьФайла;
	ЭлементПутьФайла.Подсказка = "";
	ЭлементПутьФайла.КнопкаОткрытия = ТаблицаФайлов.Количество() = 1;
	Если ТаблицаФайлов.Количество() = 0 Тогда
		ЭлементПутьФайла.Подсказка =
			НСтр("ru = 'Файл не найден в каталоге обмена. Он был перемещен, либо настройки обмена изменены с момента события.'");
	Иначе
		СтрокаФайла = ТаблицаФайлов[0];
		ОтображаемыйПутьФайла = СтрокаФайла.ПолноеИмяНаСервере;
		Если ЗначениеЗаполнено(СтрокаФайла.ПолноеИмя) Тогда
			Файл = Новый Файл(СтрокаФайла.ПолноеИмя);
			Если Файл.Существует() Тогда
				ПолноеИмяФайла = СтрокаФайла.ПолноеИмя;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолноеИмяФайлаНаСервереОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(ОтображаемыйПутьФайла) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПолноеИмяФайла) Тогда
		ФайловаяСистемаКлиент.ОткрытьФайл(ПолноеИмяФайла);
	Иначе
		Если Не ЗначениеЗаполнено(АдресХранилищаФайла) Тогда
			ПоместитьФайлВоВременноеХранилищеНаСервере();
		КонецЕсли;
		ДанныеФайла = ПолучитьИзВременногоХранилища(АдресХранилищаФайла);
		Если ДанныеФайла = Неопределено Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Не удалось получить файл на сервере'"));
			Возврат;
		КонецЕсли;
		СтруктураИмениФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ОтображаемыйПутьФайла);
		ФайловаяСистемаКлиент.ОткрытьФайл(АдресХранилищаФайла,, СтруктураИмениФайла.Имя);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПоместитьФайлВоВременноеХранилищеНаСервере()
	
	ПараметрыТранспорта = ОбменСАрхивомТранспорт.ПараметрыТранспортаДляСеансаОбмена();
	АдресХранилищаФайла = ОбменСАрхивомТранспорт.ПоместитьФайлВоВременноеХранилище(
		ПараметрыТранспорта, ОтображаемыйПутьФайла, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти
