
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ТипСобытий) Тогда
		ТипСобытияОтбор = Параметры.ТипСобытий;
		УстановитьБыстрыеОтборы();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ОбъектОбмена) Тогда
		ОбъектОбмена = Параметры.ОбъектОбмена;
		АвтоЗаголовок = Ложь;
		Заголовок = СтрШаблон(НСтр("ru = 'История обмена: %1'"), ОбъектОбмена);
		ПолеОтбора = "Объект";
		Если РегистрыСведений.ИсторияОбменаС1САрхивом.ЭтоОсновнойОбъектОбмена(ТипЗнч(ОбъектОбмена)) Тогда
			ПолеОтбора = "ОсновнойОбъект";
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, ПолеОтбора, ОбъектОбмена);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьБыстрыеОтборы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипСобытияОтборПриИзменении(Элемент)
	
	УстановитьБыстрыеОтборы();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьБыстрыеОтборы()
	
	Если Не ЗначениеЗаполнено(ТипСобытияОтбор) Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор, "ТипСобытия");
	ИначеЕсли ТипСобытияОтбор = Перечисления.СобытияОбменаС1САрхивом.Информация Тогда
		НедоступныеТипы = Новый СписокЗначений;
		НедоступныеТипы.Добавить(Перечисления.СобытияОбменаС1САрхивом.Ошибка);
		НедоступныеТипы.Добавить(Перечисления.СобытияОбменаС1САрхивом.Предупреждение);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ТипСобытия", НедоступныеТипы,
			ВидСравненияКомпоновкиДанных.НеВСписке);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ТипСобытия", ТипСобытияОтбор,
			ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(
		Элементы.ТипСобытияОтбор, ТипСобытияОтбор);
	
КонецПроцедуры

#КонецОбласти
