#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныйПрограммныйИнтерфейс

// Добавляет запись в регистр
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
//  ИмяРеквизита - Строка
//  ТабличнаяЧасть - Строка
//  НомерСтрокиТЧ - Число
//  ПараметрыПроверки - Структура
Процедура ДобавитьПроверяемыйРеквизит(Знач ПараметрыПроверки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, ПараметрыПроверки);
	Запись.Записать(Истина);
	
КонецПроцедуры

// Требует проверки.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение:
//  Булево - Требует проверки
Функция НеобходимаПроверкиРеквизитовТабличныхЧастей(ДокументСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ВерификацияРеквизитовТЧРаспознанныхДокументов.Документ
		|ИЗ
		|	РегистрСведений.ВерификацияРеквизитовТЧРаспознанныхДокументов КАК ВерификацияРеквизитовТЧРаспознанныхДокументов
		|ГДЕ
		|	ВерификацияРеквизитовТЧРаспознанныхДокументов.Документ = &Документ
		|	И ВерификацияРеквизитовТЧРаспознанныхДокументов.ТребуетПроверки = ИСТИНА
		|		И ВерификацияРеквизитовТЧРаспознанныхДокументов.Проверен = ЛОЖЬ");
		
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Удалить данные документа.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
Процедура УдалитьДанныеДокумента(ДокументСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(ДокументСсылка);
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Записать данные верификации документа.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
//  ДанныеВерификации - ТаблицаЗначений
//  ДанныеВерифицированы - Булево
//  ДанныеПодобраны - Булево
Процедура ЗаписатьДанныеДокумента(ДокументСсылка, ДанныеВерификации, ДанныеВерифицированы) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ДанныеВерифицированы = Истина Тогда
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Документ.Установить(ДокументСсылка);
		НаборЗаписей.Загрузить(ДанныеВерификации.Выгрузить());
		
		Для Каждого Запись Из НаборЗаписей Цикл
			Запись.Документ = ДокументСсылка;
			Запись.Ответственный = Пользователи.ТекущийПользователь();
			Запись.ДатаПроверки = ТекущаяДатаСеанса();
		КонецЦикла;
		
	Иначе
		Возврат;
	КонецЕсли;
		
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли