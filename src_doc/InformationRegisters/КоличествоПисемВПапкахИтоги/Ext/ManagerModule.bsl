#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура Добавить(Письмо, Папка, Пользователь = Неопределено, Всего = 1, Прочтенных = 0, ПометкаУдаления = Неопределено) Экспорт
	
	Если ПометкаУдаления = Истина Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.КоличествоПисемВПапкахДвижения.СоздатьМенеджерЗаписи();
	Запись.Папка = Папка;
	Запись.Пользователь = Пользователь;
	Запись.Идентификатор = Новый УникальныйИдентификатор();
	Запись.Сегмент = ПолучитьСегмент();
	Запись.Всего = Всего;
	Запись.Прочтенных = Прочтенных;
	Запись.Записать(Ложь);
	
КонецПроцедуры

Процедура ДобавитьИзменение(Письмо, ЭтоНовый = Ложь, НоваяПапка, СтараяПапка = Неопределено, НоваяПометкаУдаления = Неопределено, СтараяПометкаУдаления = Неопределено, Буфер = Неопределено) Экспорт
	
	Если НоваяПометкаУдаления = Истина И СтараяПометкаУдаления = Истина Тогда
		//Уже был удален. Итоги обработаны. Счетчик для помеченных на удаление не меняется.
		Возврат;
	КонецЕсли;
		
	УстановитьПривилегированныйРежим(Истина);
	
	Если Буфер = Неопределено Тогда
		Набор = РегистрыСведений.КоличествоПисемВПапкахДвижения.СоздатьНаборЗаписей();
		Сегмент = ПолучитьСегмент();
		ИД = Новый УникальныйИдентификатор();
	Иначе
		Набор = Буфер;
		Сегмент = 0;
		ИД = Неопределено;
	КонецЕсли;
		
	Если Не ЭтоНовый Тогда
		// Блокировка СведенияОПрочтении должна быть установлена ранее.
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СведенияОПрочтении.Пользователь КАК Пользователь
		|ИЗ
		|	РегистрСведений.СведенияОПрочтении КАК СведенияОПрочтении
		|ГДЕ
		|	СведенияОПрочтении.Объект = &Письмо
		|	И СведенияОПрочтении.Прочтен = ИСТИНА");
		Запрос.УстановитьПараметр("Письмо", Письмо);
		Выборка = Запрос.Выполнить().Выбрать();
	КонецЕсли;
	
	УменьшающаясяПапка = ?(СтараяПапка = Неопределено, НоваяПапка, СтараяПапка);
	
	Если Не ЭтоНовый И Выборка.Количество() Тогда
		//Прочтенных для каждого пользователя. В первой записи сразу инкремент для Всего. 
		
		ЭтоПервый = Истина;
		Пока Выборка.Следующий() Цикл
			
			Если НоваяПапка <> СтараяПапка И СтараяПометкаУдаления <> Истина И ЗначениеЗаполнено(СтараяПапка)
				Или НоваяПометкаУдаления = Истина И СтараяПометкаУдаления = Ложь И ЗначениеЗаполнено(УменьшающаясяПапка) Тогда
				
				Запись = Набор.Добавить();
				Запись.Папка = УменьшающаясяПапка;
				Запись.Пользователь = Выборка.Пользователь;
				Запись.Идентификатор = ИД;
				Запись.Сегмент = Сегмент;
				Запись.Прочтенных = -1;
				Если ЭтоПервый Тогда
					Запись.Всего = -1;
				КонецЕсли;
				
			КонецЕсли;
						
			Если НоваяПометкаУдаления <> Истина Тогда
				
				Запись = Набор.Добавить();
				Запись.Папка = НоваяПапка;
				Запись.Пользователь = Выборка.Пользователь;
				Запись.Идентификатор = ИД;
				Запись.Сегмент = Сегмент;
				Запись.Прочтенных = 1;
				Если ЭтоПервый Тогда
					Запись.Всего = 1;
				КонецЕсли;
				
			КонецЕсли;
			
			ЭтоПервый = Ложь;
		КонецЦикла;
		
	Иначе
		
		Если НоваяПапка <> СтараяПапка И СтараяПометкаУдаления <> Истина И ЗначениеЗаполнено(СтараяПапка)
			Или НоваяПометкаУдаления = Истина И СтараяПометкаУдаления = Ложь И ЗначениеЗаполнено(УменьшающаясяПапка) Тогда
			
			Запись = Набор.Добавить();
			Запись.Папка = УменьшающаясяПапка;
			Запись.Идентификатор = ИД;
			Запись.Сегмент = Сегмент;
			Запись.Всего = -1;
						
		КонецЕсли;
				
		Если НоваяПометкаУдаления <> Истина Тогда 
			
			Запись = Набор.Добавить();
			Запись.Папка = НоваяПапка;
			Запись.Идентификатор = ИД;
			Запись.Сегмент = Сегмент;
			Запись.Всего = 1;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Буфер = Неопределено Тогда
		Набор.Записать(Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура Загрузить(Таблица) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИД = Новый УникальныйИдентификатор();
	Сегмент = ПолучитьСегмент();
	Набор = РегистрыСведений.КоличествоПисемВПапкахДвижения.СоздатьНаборЗаписей();
	
	Для Каждого Строка Из Таблица Цикл
		Запись = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, Строка);
		Запись.Идентификатор = ИД;
		Запись.Сегмент = Сегмент;
	КонецЦикла;
	Набор.Записать(Ложь);
	
КонецПроцедуры

Процедура Заполнить(Папка = Неопределено) Экспорт
	
	ДатаНач = ТекущаяДатаСеанса();
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборИтоги = РегистрыСведений.КоличествоПисемВПапкахИтоги.СоздатьНаборЗаписей();
	НаборДвижения = РегистрыСведений.КоличествоПисемВПапкахДвижения.СоздатьНаборЗаписей();
	Блокировка = Новый БлокировкаДанных;
		
	Если Папка = Неопределено Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	КраткиеОписанияПисем.Папка КАК Папка,
		|	ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка) КАК Пользователь,
		|	КОЛИЧЕСТВО(1) КАК Всего,
		|	0 КАК Прочтенных
		|ИЗ
		|	ЖурналДокументов.ЭлектроннаяПочта КАК КраткиеОписанияПисем
		|ГДЕ
		|	КраткиеОписанияПисем.ПометкаУдаления = ЛОЖЬ
		|
		|СГРУППИРОВАТЬ ПО
		|	КраткиеОписанияПисем.Папка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КраткиеОписанияПисем.Папка,
		|	Пользователи.Ссылка,
		|	0,
		|	КОЛИЧЕСТВО(1)
		|ИЗ
		|	РегистрСведений.СведенияОПрочтении КАК СведенияОПрочтении
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЖурналДокументов.ЭлектроннаяПочта КАК КраткиеОписанияПисем
		|		ПО СведенияОПрочтении.Объект = КраткиеОписанияПисем.Ссылка
		|			И (КраткиеОписанияПисем.ПометкаУдаления = ЛОЖЬ)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО СведенияОПрочтении.Пользователь = Пользователи.Ссылка
		|			И (Пользователи.Недействителен = ЛОЖЬ)
		|			И (Пользователи.ПометкаУдаления = ЛОЖЬ)
		|ГДЕ
		|	СведенияОПрочтении.Прочтен = ИСТИНА
		|
		|СГРУППИРОВАТЬ ПО
		|	КраткиеОписанияПисем.Папка,
		|	Пользователи.Ссылка");
		
		Блокировка.Добавить("РегистрСведений.КоличествоПисемВПапкахДвижения");
		
	Иначе
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	КраткиеОписанияПисем.Папка КАК Папка,
		|	ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка) КАК Пользователь,
		|	КОЛИЧЕСТВО(1) КАК Всего,
		|	0 КАК Прочтенных
		|ИЗ
		|	ЖурналДокументов.ЭлектроннаяПочта КАК КраткиеОписанияПисем
		|ГДЕ
		|	КраткиеОписанияПисем.Папка = &Папка
		|	И КраткиеОписанияПисем.ПометкаУдаления = ЛОЖЬ
		|
		|СГРУППИРОВАТЬ ПО
		|	КраткиеОписанияПисем.Папка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КраткиеОписанияПисем.Папка,
		|	Пользователи.Ссылка,
		|	0,
		|	КОЛИЧЕСТВО(1)
		|ИЗ
		|	ЖурналДокументов.ЭлектроннаяПочта КАК КраткиеОписанияПисем
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПрочтении КАК СведенияОПрочтении
		|		ПО КраткиеОписанияПисем.Ссылка = СведенияОПрочтении.Объект
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО (СведенияОПрочтении.Пользователь = Пользователи.Ссылка)
		|			И (Пользователи.Недействителен = ЛОЖЬ)
		|			И (Пользователи.ПометкаУдаления = ЛОЖЬ)
		|ГДЕ
		|	КраткиеОписанияПисем.Папка = &Папка
		|	И КраткиеОписанияПисем.ПометкаУдаления = ЛОЖЬ
		|	И СведенияОПрочтении.Прочтен = ИСТИНА
		|
		|СГРУППИРОВАТЬ ПО
		|	КраткиеОписанияПисем.Папка,
		|	Пользователи.Ссылка");
		Запрос.УстановитьПараметр("Папка", Папка);
		
		Блок = Блокировка.Добавить("РегистрСведений.КоличествоПисемВПапкахДвижения");
		Блок.УстановитьЗначение("Папка", Папка);
		
		НаборИтоги.Отбор.Папка.Установить(Папка);
		НаборДвижения.Отбор.Папка.Установить(Папка);
		
	КонецЕсли;
	
	НачатьТранзакцию();
		
	Попытка
		
		Блокировка.Заблокировать();
		
		НаборИтоги.Загрузить(Запрос.Выполнить().Выгрузить());
		НаборИтоги.Записать();
		
		НаборДвижения.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
	Если Папка = Неопределено Тогда
		Сообщить(СтрШаблон(НСтр("ru = 'Заполнено за %1 сек.'"), ТекущаяДатаСеанса() - ДатаНач));
	КонецЕсли;
	
КонецПроцедуры

Процедура Свернуть(Интерактивно = Ложь) Экспорт
	
	Если Интерактивно Тогда
		//Локальный лог выполнения.
		Лог = Новый Структура("Начало,Сегменты",
			ТекущаяУниверсальнаяДатаВМиллисекундах(),
			Новый Массив);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущийСегмент = ПолучитьСегмент(3);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Сегмент КАК Сегмент
	|ИЗ
	|	РегистрСведений.КоличествоПисемВПапкахДвижения КАК Т
	|ГДЕ
	|	Т.Сегмент < &Сегмент
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сегмент");
	Запрос.УстановитьПараметр("Сегмент", ТекущийСегмент);
		
	Сегменты = Запрос.Выполнить();
	Если Сегменты.Пустой() Тогда
		
		Если Лог <> Неопределено Тогда
			Сообщить(СтрШаблон(НСтр("ru = 'Выполнено за %1 сек.'"), (ТекущаяУниверсальнаяДатаВМиллисекундах() - Лог.Начало)/1000));
			Сообщить(НСтр("ru = 'Нет записей'"));
		КонецЕсли;
		
		Возврат;
	Иначе
		Сегменты = Сегменты.Выбрать();
	КонецЕсли;
	
	//Движения
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Т.Папка КАК Папка,
	|	Т.Пользователь КАК Пользователь,
	|	&Идентификатор КАК Идентификатор,
	|	СУММА(Т.Всего) КАК Всего,
	|	СУММА(Т.Прочтенных) КАК Прочтенных
	|ИЗ
	|	РегистрСведений.КоличествоПисемВПапкахДвижения КАК Т
	|ГДЕ
	|	Т.Сегмент = &Сегмент
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Папка,
	|	Т.Пользователь");
	
	НаборИтоги = РегистрыСведений.КоличествоПисемВПапкахИтоги.СоздатьНаборЗаписей();
	НаборДвижения = РегистрыСведений.КоличествоПисемВПапкахДвижения.СоздатьНаборЗаписей();
	Есть = Ложь;
	
	Пока Сегменты.Следующий() Цикл
		
		Сегмент = Сегменты.Сегмент;
		
		Если Лог <> Неопределено Тогда
			Лог.Сегменты.Добавить(Новый Структура("Сегмент,Начало,Блокировка,Запрос,ЗаписьИтоги,ЗаписьДвижения,Всего,Количество,Ошибка", Сегмент, ТекущаяУниверсальнаяДатаВМиллисекундах(),0,0,0,0,0,0));			
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Идентификатор", Новый УникальныйИдентификатор());
		Запрос.УстановитьПараметр("Сегмент", Сегмент);
		
		НаборДвижения.Отбор.Сегмент.Установить(Сегмент);
		
		Блокировка = Новый БлокировкаДанных;
		Блок = Блокировка.Добавить("РегистрСведений.КоличествоПисемВПапкахДвижения");
		Блок.УстановитьЗначение("Сегмент", Сегмент);
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка.Заблокировать();
			
			Если Лог <> Неопределено Тогда
				Лог.Сегменты[Лог.Сегменты.ВГраница()].Блокировка = ТекущаяУниверсальнаяДатаВМиллисекундах();
			КонецЕсли;
			
			НаборИтоги.Загрузить(Запрос.Выполнить().Выгрузить());
			Если НаборИтоги.Количество() Тогда
				
				Если Лог <> Неопределено Тогда
					Лог.Сегменты[Лог.Сегменты.ВГраница()].Запрос = ТекущаяУниверсальнаяДатаВМиллисекундах();
					Лог.Сегменты[Лог.Сегменты.ВГраница()].Количество = НаборИтоги.Количество();
				КонецЕсли;
				
				НаборИтоги.Записать(Ложь);
				
				Если Лог <> Неопределено Тогда
					Лог.Сегменты[Лог.Сегменты.ВГраница()].ЗаписьИтоги = ТекущаяУниверсальнаяДатаВМиллисекундах();
				КонецЕсли;
				
				НаборДвижения.Записать(Истина);
				
				Если Лог <> Неопределено Тогда
					Лог.Сегменты[Лог.Сегменты.ВГраница()].ЗаписьДвижения = ТекущаяУниверсальнаяДатаВМиллисекундах();
				КонецЕсли;
				
				ЗафиксироватьТранзакцию();
				Есть = Истина;
				
			Иначе
				ОтменитьТранзакцию();
				
				Если Лог <> Неопределено Тогда
					Лог.Сегменты[Лог.Сегменты.ВГраница()].Запрос = ТекущаяУниверсальнаяДатаВМиллисекундах();
				КонецЕсли;
				
			КонецЕсли;
			
		Исключение
			
			ОтменитьТранзакцию();
			
			Если Лог <> Неопределено Тогда
				Лог.Сегменты[Лог.Сегменты.ВГраница()].Ошибка = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Иначе
				ЗаписьЖурналаРегистрации(
					НСтр("ru = 'Почта'"),
					УровеньЖурналаРегистрации.Ошибка,
					Метаданные.РегистрыСведений.КоличествоПисемВПапкахИтоги,
					СтрШаблон("%1[%2]", НСтр("ru = 'Свертка'"), Сегмент),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецЕсли;
			
		КонецПопытки;
		
		НаборИтоги.Очистить();
		
	КонецЦикла;
		
	//Итоги
	Если Есть Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Итоги.Папка КАК Папка,
		|	ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка) КАК Пользователь,
		|	СУММА(Итоги.Всего) КАК Всего,
		|	0 КАК Прочтенных
		|ИЗ
		|	РегистрСведений.КоличествоПисемВПапкахИтоги КАК Итоги
		|ГДЕ
		|	Итоги.Папка В
		|			(ВЫБРАТЬ
		|				Т.Папка КАК Папка
		|			ИЗ
		|				РегистрСведений.КоличествоПисемВПапкахИтоги КАК Т
		|			ГДЕ
		|				Т.Идентификатор > &ПустойИД)
		|
		|СГРУППИРОВАТЬ ПО
		|	Итоги.Папка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Итоги.Папка,
		|	Итоги.Пользователь,
		|	0,
		|	СУММА(Итоги.Прочтенных)
		|ИЗ
		|	РегистрСведений.КоличествоПисемВПапкахИтоги КАК Итоги
		|ГДЕ
		|	(Итоги.Папка, Итоги.Пользователь) В
		|			(ВЫБРАТЬ
		|				Т.Папка КАК Папка,
		|				Т.Пользователь КАК Пользователь
		|			ИЗ
		|				РегистрСведений.КоличествоПисемВПапкахИтоги КАК Т
		|			ГДЕ
		|				Т.Идентификатор > &ПустойИД
		|				И Т.Пользователь <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка))
		|
		|СГРУППИРОВАТЬ ПО
		|	Итоги.Папка,
		|	Итоги.Пользователь
		|
		|УПОРЯДОЧИТЬ ПО
		|	Папка,
		|	Пользователь");
		Запрос.УстановитьПараметр("ПустойИД", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
		
		НаборДобавление = РегистрыСведений.КоличествоПисемВПапкахИтоги.СоздатьНаборЗаписей();
		НаборДобавление.Отбор.Папка.Использование = Истина;
		НаборДобавление.Отбор.Пользователь.Использование = Истина;
		Запись = НаборДобавление.Добавить();
		НаборУдаление = Неопределено;
		
		Блокировка = Новый БлокировкаДанных;
		Блокировка.Добавить("РегистрСведений.КоличествоПисемВПапкахИтоги");
		
		Если Лог <> Неопределено Тогда
			Лог.Сегменты.Добавить(Новый Структура("Сегмент,Начало,Блокировка,Запрос,ЗаписьИтоги,ЗаписьДвижения,Всего,Количество,Ошибка", -1, ТекущаяУниверсальнаяДатаВМиллисекундах(),0,0,0,0,0,0));			
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			Блокировка.Заблокировать();
			
			Если Лог <> Неопределено Тогда
				Лог.Сегменты[Лог.Сегменты.ВГраница()].Блокировка = ТекущаяУниверсальнаяДатаВМиллисекундах();
			КонецЕсли;
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Лог <> Неопределено Тогда
				Лог.Сегменты[Лог.Сегменты.ВГраница()].Запрос = ТекущаяУниверсальнаяДатаВМиллисекундах();
				Лог.Сегменты[Лог.Сегменты.ВГраница()].Количество = Выборка.Количество();
			КонецЕсли;
			
			Пока Выборка.Следующий() Цикл
				Если Выборка.Всего = 0 И Выборка.Прочтенных = 0 Тогда
					Если НаборУдаление = Неопределено Тогда
						НаборУдаление = РегистрыСведений.КоличествоПисемВПапкахИтоги.СоздатьНаборЗаписей();
						НаборУдаление.Отбор.Папка.Использование = Истина;
						НаборУдаление.Отбор.Пользователь.Использование = Истина;
					КонецЕсли;
					НаборУдаление.Отбор.Папка.Значение = Выборка.Папка;
					НаборУдаление.Отбор.Пользователь.Значение = Выборка.Пользователь;
					НаборУдаление.Записать();
				Иначе
					ЗаполнитьЗначенияСвойств(Запись, Выборка);
					НаборДобавление.Отбор.Папка.Значение = Выборка.Папка;
					НаборДобавление.Отбор.Пользователь.Значение = Выборка.Пользователь;
					НаборДобавление.Записать();
				КонецЕсли;
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ВызватьИсключение;
			
		КонецПопытки;
	КонецЕсли;
		
	Если Лог <> Неопределено Тогда
		Лог.Сегменты[Лог.Сегменты.ВГраница()].ЗаписьИтоги = ТекущаяУниверсальнаяДатаВМиллисекундах();
		
		Сообщить(СтрШаблон(НСтр("ru = 'Выполнено за %1 сек.'"), (ТекущаяУниверсальнаяДатаВМиллисекундах() - Лог.Начало)/1000));
		Сообщить(НСтр("ru = 'Затраты времени:'"));
		Для Каждого Сегмент Из Лог.Сегменты Цикл
			Если ЗначениеЗаполнено(Сегмент.Ошибка) Тогда
				Сообщить(СтрШаблон(НСтр("ru = 'Сегмент %1: %2'"), Сегмент.Сегмент, СтрЗаменить(Сегмент.Ошибка, Символы.ПС, "¶")));
			Иначе
				Сообщить(СтрШаблон(НСтр("ru = 'Сегмент %1: Блокировка: %2 сек.; Запрос: %3 сек. (%7 шт.); Запись итогов: %4 сек.; Очистка движений: %5 сек.; Всего: %6 сек.'"),
					?(Сегмент.Сегмент = -1, НСтр("ru = 'Итоги'"), Сегмент.Сегмент),
					Макс(Сегмент.Блокировка - Сегмент.Начало, 0)/1000,
					Макс(Сегмент.Запрос - Сегмент.Блокировка, 0)/1000,
					Макс(Сегмент.ЗаписьИтоги - Сегмент.Запрос, 0)/1000,
					Макс(Сегмент.ЗаписьДвижения - Сегмент.ЗаписьИтоги, 0)/1000,
					Макс(Сегмент.ЗаписьДвижения - Сегмент.Начало, 0)/1000,
					Сегмент.Количество));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
		
	Если ТекущийСегмент < ПолучитьСегмент(3) Тогда
		Свернуть(Интерактивно);
	КонецЕсли;
	
КонецПроцедуры

Процедура СвернутьПоПапкам() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Папка КАК Папка
	|ИЗ
	|	РегистрСведений.КоличествоПисемВПапкахДвижения КАК Т");
			
	Сегменты = Запрос.Выполнить();
	Если Сегменты.Пустой() Тогда
		Возврат;
	Иначе
		Сегменты = Сегменты.Выбрать();
	КонецЕсли;
	
	//Движения
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Т.Папка КАК Папка,
	|	ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка) КАК Пользователь,
	|	&Идентификатор КАК Идентификатор,
	|	СУММА(Т.Всего) КАК Всего,
	|	0 КАК Прочтенных
	|ИЗ
	|	РегистрСведений.КоличествоПисемВПапкахДвижения КАК Т
	|ГДЕ
	|	Т.Папка = &Папка
	|	И Т.Всего <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Папка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Папка,
	|	Т.Пользователь,
	|	&Идентификатор,
	|	0,
	|	СУММА(Т.Прочтенных)
	|ИЗ
	|	РегистрСведений.КоличествоПисемВПапкахДвижения КАК Т
	|ГДЕ
	|	Т.Папка = &Папка
	|	И Т.Прочтенных <> 0
	|	И Т.Пользователь <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Папка,
	|	Т.Пользователь");
	
	НаборИтоги = РегистрыСведений.КоличествоПисемВПапкахИтоги.СоздатьНаборЗаписей();
	НаборДвижения = РегистрыСведений.КоличествоПисемВПапкахДвижения.СоздатьНаборЗаписей();
	Есть = Ложь;
	
	Пока Сегменты.Следующий() Цикл
		
		Запрос.УстановитьПараметр("Идентификатор", Новый УникальныйИдентификатор());
		Запрос.УстановитьПараметр("Папка", Сегменты.Папка);
		
		НаборДвижения.Отбор.Папка.Установить(Сегменты.Папка);
		
		Блокировка = Новый БлокировкаДанных;
		Блок = Блокировка.Добавить("РегистрСведений.КоличествоПисемВПапкахДвижения");
		Блок.УстановитьЗначение("Папка", Сегменты.Папка);
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка.Заблокировать();
			
			НаборИтоги.Загрузить(Запрос.Выполнить().Выгрузить());
			Если НаборИтоги.Количество() Тогда
				НаборИтоги.Записать(Ложь);
				НаборДвижения.Записать(Истина);
				ЗафиксироватьТранзакцию();
				Есть = Истина;
			Иначе
				ОтменитьТранзакцию();
			КонецЕсли;
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Почта'"),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.КоличествоПисемВПапкахИтоги,
				СтрШаблон("%1[%2]", НСтр("ru = 'Свертка'"), XMLСтрока(Сегменты.Папка)),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		КонецПопытки;
		
		НаборИтоги.Очистить();
		
	КонецЦикла;
	
	//Итоги
	Если Есть Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Итоги.Папка КАК Папка,
		|	ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка) КАК Пользователь,
		|	СУММА(Итоги.Всего) КАК Всего,
		|	0 КАК Прочтенных
		|ИЗ
		|	РегистрСведений.КоличествоПисемВПапкахИтоги КАК Итоги
		|ГДЕ
		|	Итоги.Папка В
		|			(ВЫБРАТЬ
		|				Т.Папка КАК Папка
		|			ИЗ
		|				РегистрСведений.КоличествоПисемВПапкахИтоги КАК Т
		|			ГДЕ
		|				Т.Идентификатор > &ПустойИД)
		|
		|СГРУППИРОВАТЬ ПО
		|	Итоги.Папка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Итоги.Папка,
		|	Итоги.Пользователь,
		|	0,
		|	СУММА(Итоги.Прочтенных)
		|ИЗ
		|	РегистрСведений.КоличествоПисемВПапкахИтоги КАК Итоги
		|ГДЕ
		|	(Итоги.Папка, Итоги.Пользователь) В
		|			(ВЫБРАТЬ
		|				Т.Папка КАК Папка,
		|				Т.Пользователь КАК Пользователь
		|			ИЗ
		|				РегистрСведений.КоличествоПисемВПапкахИтоги КАК Т
		|			ГДЕ
		|				Т.Идентификатор > &ПустойИД
		|				И Т.Пользователь <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка))
		|
		|СГРУППИРОВАТЬ ПО
		|	Итоги.Папка,
		|	Итоги.Пользователь
		|
		|УПОРЯДОЧИТЬ ПО
		|	Папка,
		|	Пользователь");
		Запрос.УстановитьПараметр("ПустойИД", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
		
		НаборДобавление = РегистрыСведений.КоличествоПисемВПапкахИтоги.СоздатьНаборЗаписей();
		НаборДобавление.Отбор.Папка.Использование = Истина;
		НаборДобавление.Отбор.Пользователь.Использование = Истина;
		Запись = НаборДобавление.Добавить();
		НаборУдаление = Неопределено;
		
		Блокировка = Новый БлокировкаДанных;
		Блокировка.Добавить("РегистрСведений.КоличествоПисемВПапкахИтоги");
		
		НачатьТранзакцию();
		
		Попытка
			Блокировка.Заблокировать();
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Если Выборка.Всего = 0 И Выборка.Прочтенных = 0 Тогда
					Если НаборУдаление = Неопределено Тогда
						НаборУдаление = РегистрыСведений.КоличествоПисемВПапкахИтоги.СоздатьНаборЗаписей();
						НаборУдаление.Отбор.Папка.Использование = Истина;
						НаборУдаление.Отбор.Пользователь.Использование = Истина;
					КонецЕсли;
					НаборУдаление.Отбор.Папка.Значение = Выборка.Папка;
					НаборУдаление.Отбор.Пользователь.Значение = Выборка.Пользователь;
					НаборУдаление.Записать();
				Иначе
					ЗаполнитьЗначенияСвойств(Запись, Выборка);
					НаборДобавление.Отбор.Папка.Значение = Выборка.Папка;
					НаборДобавление.Отбор.Пользователь.Значение = Выборка.Пользователь;
					НаборДобавление.Записать();
				КонецЕсли;
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ВызватьИсключение;
			
		КонецПопытки;
	КонецЕсли;
		
КонецПроцедуры

Процедура Исправить() Экспорт
	
	Выборка = РегистрыСведений.КоличествоПисемВПапкахИтоги.Расхождения().Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("Папка") Цикл
		Заполнить(Выборка.Папка);		
	Конеццикла;
	
КонецПроцедуры

Процедура ИсправитьПорциями() Экспорт
	
	ДатаНач = ТекущаяДатаСеанса();
	ИтогИсправлено = 0;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПапкиПисем.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ПапкиПисем КАК ПапкиПисем");
	
	К = 100;
	Папки = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
	Если Папки.Количество() Тогда
		Порция = Новый Массив;
		Последняя = Папки[Папки.ВГраница()];
		
		Для каждого Папка Из Папки Цикл
			Порция.Добавить(Папка);
			
			Если Порция.Количество() >= К Или Папка = Последняя Тогда
				Выборка = РегистрыСведений.КоличествоПисемВПапкахИтоги.Расхождения(Порция).Выбрать();
		
				Пока Выборка.СледующийПоЗначениюПоля("Папка") Цикл
					Заполнить(Выборка.Папка);
					ИтогИсправлено = ИтогИсправлено + 1;
				Конеццикла;
				
				Порция.Очистить();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Сообщить(СтрШаблон(НСтр("ru = 'Исправлено %1 папок за %2 сек.'"), ИтогИсправлено, ТекущаяДатаСеанса() - ДатаНач));
	
КонецПроцедуры

Функция Расхождения(Папка = Неопределено) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Т.Папка КАК Папка,
	|	ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка) КАК Пользователь,
	|	КОЛИЧЕСТВО(1) КАК ПервоисточникВсего,
	|	0 КАК ПервоисточникПрочтенных,
	|	0 КАК ИтогиВсего,
	|	0 КАК ИтогиПрочтенных
	|ПОМЕСТИТЬ Исходники
	|ИЗ
	|	РегистрСведений.КраткиеОписанияПисем КАК Т
	|ГДЕ
	|	Т.ПометкаУдаления = ЛОЖЬ
	|	И Т.Папка В(&Папка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Папка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Папка,
	|	Пользователи.Ссылка,
	|	0,
	|	КОЛИЧЕСТВО(1),
	|	0,
	|	0
	|ИЗ
	|	РегистрСведений.КраткиеОписанияПисем КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПрочтении КАК СведенияОПрочтении
	|		ПО Т.Письмо = СведенияОПрочтении.Объект
	|			И (СведенияОПрочтении.Прочтен = ИСТИНА)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО (СведенияОПрочтении.Пользователь = Пользователи.Ссылка)
	|			И (Пользователи.Недействителен = ЛОЖЬ)
	|			И (Пользователи.ПометкаУдаления = ЛОЖЬ)
	|ГДЕ
	|	Т.ПометкаУдаления = ЛОЖЬ
	|	И Т.Папка В(&Папка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Папка,
	|	Пользователи.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Папка,
	|	ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка),
	|	0,
	|	0,
	|	СУММА(Т.Всего),
	|	0
	|ИЗ
	|	РегистрСведений.КоличествоПисемВПапкахИтоги КАК Т
	|ГДЕ
	|	ИСТИНА
	|	И Т.Папка В(&Папка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Папка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Папка,
	|	Т.Пользователь,
	|	0,
	|	0,
	|	0,
	|	СУММА(Т.Прочтенных)
	|ИЗ
	|	РегистрСведений.КоличествоПисемВПапкахИтоги КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО Т.Пользователь = Пользователи.Ссылка
	|			И (Пользователи.Недействителен = ЛОЖЬ)
	|			И (Пользователи.ПометкаУдаления = ЛОЖЬ)
	|ГДЕ
	|	ИСТИНА
	|	И Т.Папка В(&Папка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Папка,
	|	Т.Пользователь
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Папка,
	|	ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка),
	|	0,
	|	0,
	|	СУММА(Т.Всего),
	|	0
	|ИЗ
	|	РегистрСведений.КоличествоПисемВПапкахДвижения КАК Т
	|ГДЕ
	|	ИСТИНА
	|	И Т.Папка В(&Папка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Папка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Папка,
	|	Т.Пользователь,
	|	0,
	|	0,
	|	0,
	|	СУММА(Т.Прочтенных)
	|ИЗ
	|	РегистрСведений.КоличествоПисемВПапкахДвижения КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО Т.Пользователь = Пользователи.Ссылка
	|			И (Пользователи.Недействителен = ЛОЖЬ)
	|			И (Пользователи.ПометкаУдаления = ЛОЖЬ)
	|ГДЕ
	|	ИСТИНА
	|	И Т.Папка В(&Папка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Папка,
	|	Т.Пользователь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Исходники.Папка КАК Папка,
	|	Исходники.Пользователь КАК Пользователь,
	|	СУММА(Исходники.ПервоисточникВсего) КАК ПервоисточникВсего,
	|	СУММА(Исходники.ПервоисточникПрочтенных) КАК ПервоисточникПрочтенных,
	|	СУММА(Исходники.ИтогиВсего) КАК ИтогиВсего,
	|	СУММА(Исходники.ИтогиПрочтенных) КАК ИтогиПрочтенных
	|ПОМЕСТИТЬ Расхождения
	|ИЗ
	|	Исходники КАК Исходники
	|
	|СГРУППИРОВАТЬ ПО
	|	Исходники.Папка,
	|	Исходники.Пользователь
	|
	|ИМЕЮЩИЕ
	|	(ЕСТЬNULL(СУММА(Исходники.ПервоисточникВсего), 0) <> ЕСТЬNULL(СУММА(Исходники.ИтогиВсего), 0)
	|		ИЛИ СУММА(Исходники.ПервоисточникПрочтенных) <> СУММА(Исходники.ИтогиПрочтенных))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Исходники.Папка КАК Папка,
	|	ПапкиУчетныхЗаписей.УчетнаяЗапись КАК УчетнаяЗапись,
	|	Исходники.Пользователь КАК Пользователь,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Исходники.Папка) КАК ПапкаПредставление,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ПапкиУчетныхЗаписей.УчетнаяЗапись) КАК УчетнаяЗаписьПредставление,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Исходники.Пользователь) КАК ПользовательПредставление,
	|	Исходники.ИтогиВсего КАК ИтогиВсего,
	|	Исходники.ИтогиПрочтенных КАК ИтогиПрочтенных,
	|	Исходники.ПервоисточникВсего КАК ПервоисточникВсего,
	|	Исходники.ПервоисточникПрочтенных КАК ПервоисточникПрочтенных,
	|	Исходники.ИтогиВсего - Исходники.ПервоисточникВсего КАК ОтклонениеВсего,
	|	Исходники.ИтогиПрочтенных - Исходники.ПервоисточникПрочтенных КАК ОтклонениеПрочтенных
	|ИЗ
	|	Расхождения КАК Исходники
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПапкиУчетныхЗаписей КАК ПапкиУчетныхЗаписей
	|		ПО Исходники.Папка = ПапкиУчетныхЗаписей.Папка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Папка,
	|	Пользователь");
	
	Если ЗначениеЗаполнено(Папка) Тогда
		Запрос.УстановитьПараметр("Папка", Папка);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Т.Папка В(&Папка)", "");	
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить();
		
КонецФункции

Функция ПолучитьСегмент(Задержка = 0) Экспорт
	
	Секунда = ТекущаяУниверсальнаяДата() - '20190101' - Задержка;
	Возврат Секунда - (Секунда % 30);
	
КонецФункции

#КонецОбласти

#КонецЕсли
