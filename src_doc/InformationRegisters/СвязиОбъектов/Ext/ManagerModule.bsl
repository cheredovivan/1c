#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает структуру полей связи
//
// Возвращаемое значение:
//   Структура
//     Документ
//     СвязанныйДокумент
//     ТипСвязи
//     Комментарий
//
Функция ПолучитьСтруктуруСвязи() Экспорт
	
	СтруктураСвязи = Новый Структура;
	СтруктураСвязи.Вставить("Объект");
	СтруктураСвязи.Вставить("СвязанныйОбъект");
	СтруктураСвязи.Вставить("ТипСвязи");
	СтруктураСвязи.Вставить("Комментарий");
	
	Возврат СтруктураСвязи;
	
КонецФункции

// Создает и записывает в БД связь
//
// Параметры:
//   СтруктураСвязи - Структура - структура полей связи.
//
Процедура СоздатьСвязь(СтруктураСвязи) Экспорт
	
	СвязиОбъектов.СоздатьСвязь(
		СтруктураСвязи.Объект,
		СтруктураСвязи.СвязанныйОбъект,
		СтруктураСвязи.ТипСвязи);
	
КонецПроцедуры

// Конструктор параметров обновления данных кэширующих объектов.
//
// Возвращаемое значение:
//	Структура:
//	 * ДокументыДляОбновления - Массив из СправочникСсылка.ДокументыПредприятия
//
Функция ПараметрыОбновленияДанныхКэширующихОбъектов() Экспорт
	
	ПараметрыОбновления = Новый Структура;
	ПараметрыОбновления.Вставить("ДокументыДляОбновления", Новый Массив);
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Устанавливает значения параметров обновления данных кэширующих объектов по данным объекта.
//
// Параметры:
//  НаборЗаписей - РегистрСведенийНаборЗаписей.СвязиОбъектов
//
// Возвращаемое значение:
//	См. РегистрыСведений.СвязиОбъектов.ПараметрыОбновленияДанныхКэширующихОбъектов
//
Функция ЗначенияПараметровОбновленияДанныхКэширующихОбъектов(НаборЗаписей) Экспорт
	
	ПараметрыОбновления = ПараметрыОбновленияДанныхКэширующихОбъектов();
	
	ПредыдущиеЗаписи = ОбщегоНазначенияДокументооборот.ПредыдущиеЗаписи(НаборЗаписей);
	ТекущиеЗаписи = НаборЗаписей.Выгрузить();
	
	// Влияет на Документы.МоиДокументы.ДанныеМоегоДокумента().
	УдаленныеИДобавленныеЗаписи =
		ОбщегоНазначенияДокументооборот.УдаленныеИДобавленныеЗаписи(НаборЗаписей, ТекущиеЗаписи, ПредыдущиеЗаписи);
	Для Каждого Запись Из УдаленныеИДобавленныеЗаписи Цикл
		
		Если ТипЗнч(Запись.Объект) <> Тип("СправочникСсылка.ДокументыПредприятия")
			Или Запись.ТипСвязи <> Справочники.ТипыСвязей.ПолученОтвет Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПараметрыОбновления.ДокументыДляОбновления.Найти(Запись.Объект) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыОбновления.ДокументыДляОбновления.Добавить(Запись.Объект);
		
	КонецЦикла;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Обновляет данные зависимых объектов согласно установленным параметрам.
//
// Параметры:
//  НаборЗаписей - РегистрСведенийНаборЗаписей.СвязиОбъектов
//  ПараметрыОбновления - См. РегистрыСведений.СвязиОбъектов.ПараметрыОбновленияДанныхКэширующихОбъектов
//
Процедура ОбновитьДанныеКэширующихОбъектов(НаборЗаписей, ПараметрыОбновления) Экспорт
	
	Для Каждого ДокументДляОбновления Из ПараметрыОбновления.ДокументыДляОбновления Цикл
		ВлияющийОбъектМетаданных = Метаданные.РегистрыСведений.СвязиОбъектов.ПолноеИмя();
		ЗависимыйОбъектМетаданных = Метаданные.Документы.ДанныеМоегоДокумента.ПолноеИмя();
		РегистрыСведений.ОчередьОбновленияКэширующихДанных.Добавить(
			ЗависимыйОбъектМетаданных,
			ВлияющийОбъектМетаданных,
			ДокументДляОбновления);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли