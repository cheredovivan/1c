
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Проверяет наличие в регистре записей по дескриптору.
//
// Параметры:
//  ДескрипторСсылка - СправочникСсылка - ссылка на дескриптор.
//
// Возвращаемое значение:
//  Булево - Истина, если найдена хотя бы одна запись.
//
Функция ЕстьЗаписиПоДескриптору(ДескрипторСсылка) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьЗаписи
		|ИЗ
		|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|ГДЕ
		|	ДескрипторыДляОбъектов.Дескриптор = &Дескриптор");
	
	Запрос.УстановитьПараметр("Дескриптор", ДескрипторСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

#Область КонстантыТиповДескрипторов

// Блок функций, возвращающих числовые коды соответствующих типов дескрипторов объектов.

// Тип дескриптора - основной.
// 
// Возвращаемое значение:
//  Число
Функция ТипОсновной() Экспорт
	
	Возврат 0;
	
КонецФункции

// Тип дескриптора - по рабочей группе.
// 
// Возвращаемое значение:
//  Число
Функция ТипПоРабочейГруппе() Экспорт
	
	Возврат 1;
	
КонецФункции

// Тип дескриптора - индивидуальный.
// 
// Возвращаемое значение:
//  Число
Функция ТипИндивидуальный() Экспорт
	
	Возврат 2;
	
КонецФункции

// Тип дескриптора - от владельца.
// 
// Возвращаемое значение:
//  Число
Функция ТипОтВладельца() Экспорт
	
	Возврат 3;
	
КонецФункции

// Тип дескриптора - для локальных администраторов.
// 
// Возвращаемое значение:
//  Число
Функция ТипДляЛокальныхАдминов() Экспорт
	
	Возврат 4;
	
КонецФункции

#КонецОбласти // КонстантыТиповДескрипторов

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныйПрограммныйИнтерфейс

// Сохраняет соответствие объекта доступа и его дескрипторов
// 
// Параметры:
//  ТаблицаДескрипторов - ТаблицаЗначений - таблица, содержащая данные для записи в регистр.
//  ОбъектДоступа - ЛюбаяСсылка - ссылка на объект, чьи дескрипторы нужно записать.
//  ОпределятьЗависимыеПрава - Булево - признак необходимости обновления зависимых прав.
//  ЭтоНовый - Булево - Признак, что объект новый. Для оптимизации, чтобы не проверять на существование дескрипторов.
// 
Процедура Сохранить(ТаблицаДескрипторов, ОбъектДоступа, ОпределятьЗависимыеПрава = Истина, ЭтоНовый = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаДескрипторов.Свернуть("Объект, Дескриптор, ОбъектМетаданных, ТипДескриптора, ПолученОт");
	
	// Новые дескрипторы записываются в базу только если есть отличия от старых.
	ЕстьОтличия = Истина;
	Если Не ЭтоНовый Тогда
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	НовыеДескрипторы.Дескриптор,
			|	НовыеДескрипторы.ТипДескриптора,
			|	НовыеДескрипторы.ПолученОт
			|ПОМЕСТИТЬ НовыеДескрипторы
			|ИЗ
			|	&НовыеДескрипторы КАК НовыеДескрипторы
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДескрипторыДляОбъектов.Дескриптор,
			|	ДескрипторыДляОбъектов.ТипДескриптора,
			|	ДескрипторыДляОбъектов.ПолученОт
			|ПОМЕСТИТЬ СтарыеДескрипторы
			|ИЗ
			|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|ГДЕ
			|	ДескрипторыДляОбъектов.Объект = &Объект
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИСТИНА КАК ЕстьОтличия
			|ИЗ
			|	СтарыеДескрипторы КАК СтарыеДескрипторы
			|		ЛЕВОЕ СОЕДИНЕНИЕ НовыеДескрипторы КАК НовыеДескрипторы
			|		ПО СтарыеДескрипторы.Дескриптор = НовыеДескрипторы.Дескриптор
			|			И СтарыеДескрипторы.ТипДескриптора = НовыеДескрипторы.ТипДескриптора
			|			И СтарыеДескрипторы.ПолученОт = НовыеДескрипторы.ПолученОт
			|ГДЕ
			|	НовыеДескрипторы.Дескриптор ЕСТЬ NULL 
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИСТИНА
			|ИЗ
			|	НовыеДескрипторы КАК НовыеДескрипторы
			|		ЛЕВОЕ СОЕДИНЕНИЕ СтарыеДескрипторы КАК СтарыеДескрипторы
			|		ПО СтарыеДескрипторы.Дескриптор = НовыеДескрипторы.Дескриптор
			|			И СтарыеДескрипторы.ТипДескриптора = НовыеДескрипторы.ТипДескриптора
			|			И СтарыеДескрипторы.ПолученОт = НовыеДескрипторы.ПолученОт
			|ГДЕ
			|	СтарыеДескрипторы.Дескриптор ЕСТЬ NULL ");
		
		Запрос.УстановитьПараметр("Объект", ОбъектДоступа);
		Запрос.УстановитьПараметр("НовыеДескрипторы", ТаблицаДескрипторов);
		
		ЕстьОтличия = Не Запрос.Выполнить().Пустой();
	КонецЕсли;
	
	Если ЕстьОтличия Тогда
		
		ТаблицаДескрипторов.ЗаполнитьЗначения(ОбъектДоступа, "Объект");
		ТаблицаДескрипторов.ЗаполнитьЗначения(
			ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОбъектДоступа.Метаданные()), "ОбъектМетаданных");
		
		Набор = СоздатьНаборЗаписей();
		Набор.Отбор.Объект.Установить(ОбъектДоступа);
		Набор.Загрузить(ТаблицаДескрипторов);
		
		Если Не ОпределятьЗависимыеПрава Тогда
			Набор.ДополнительныеСвойства.Вставить("ПропуститьОпределениеЗависимыхПрав", Истина);
		КонецЕсли;
		
		Набор.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

// Сохраняет соответствие объекта доступа и его основного дескриптора
// 
// Параметры:
//  Дескриптор - СправочникСсылка.ДескрипторыДоступаОбъектов
//  ОбъектДоступа - ОпределяемыйТип.ОбъектДоступа - ссылка на объект, чьи дескрипторы нужно записать.
//  ОпределятьЗависимыеПрава - Булево - признак необходимости обновления зависимых прав.
// 
Процедура СохранитьОсновнойДескриптор(Дескриптор, ОбъектДоступа, ОпределятьЗависимыеПрава = Истина) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДескрипторыДляОбъектов.Дескриптор
		|ИЗ
		|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|ГДЕ
		|	ДескрипторыДляОбъектов.Объект = &ОбъектДоступа
		|	И ДескрипторыДляОбъектов.ТипДескриптора = &ТипОсновной");
	Запрос.УстановитьПараметр("ТипОсновной", ТипОсновной());
	Запрос.УстановитьПараметр("ОбъектДоступа", ОбъектДоступа);
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтарыйДескриптор = Неопределено;
	
	Если Выборка.Следующий() Тогда
		СтарыйДескриптор = Выборка.Дескриптор;
	КонецЕсли;
	
	Если СтарыйДескриптор <> Дескриптор Тогда
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Объект.Установить(ОбъектДоступа);
		НаборЗаписей.Отбор.ТипДескриптора.Установить(ТипОсновной());
		
		Строка = НаборЗаписей.Добавить();
		Строка.Объект = ОбъектДоступа;
		Строка.Дескриптор = Дескриптор;
		Строка.ОбъектМетаданных = 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Дескриптор, "ИдентификаторОбъектаМетаданных");
		Строка.ТипДескриптора = ТипОсновной();
		
		Если Не ОпределятьЗависимыеПрава Тогда
			НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьОпределениеЗависимыхПрав", Истина);
		КонецЕсли;
		
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает массив дескрипторов, которые назначены объекту.
// 
// Параметры:
//  ОбъектДоступа - ОпределяемыйТип.ОбъектДоступа - Ссылка на объект, чьи дескрипторы нужно получить.
// 
// Возвращаемое значение:
//  Массив Из СправочникСсылка.ДескрипторыДоступаОбъектов - массив дескрипторов объекта.
// 
Функция ПолучитьДескрипторыОбъекта(ОбъектДоступа) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДескрипторыДляОбъектов.Дескриптор
		|ИЗ
		|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|ГДЕ
		|	ДескрипторыДляОбъектов.Объект = &ОбъектДоступа";
	
	Запрос.УстановитьПараметр("ОбъектДоступа", ОбъектДоступа);
	Дескрипторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Дескриптор");
	
	Возврат Дескрипторы;
	
КонецФункции

// Проверяет наличие дескрипторов для объекта.
// 
// Параметры:
//  СсылкаНаОбъект - ЛюбаяСсылка - ссылка на объект.
//  ОтборПоТипу - Число, Неопределено - тип дескрипторов, которые нужно получить. Если Неопределено, то все.
// 
// Возвращаемое значение:
//  Булево - Истина, если дескрипторы найдены.
// 
Функция ЕстьДескрипторыОбъекта(СсылкаНаОбъект, ОтборПоТипу = Неопределено) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьЗаписи
		|ИЗ
		|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|ГДЕ
		|	ДескрипторыДляОбъектов.Объект = &СсылкаНаОбъект
		|	И &ДопУсловия");
	
	ДопУсловия = "ИСТИНА";
	Если ОтборПоТипу <> Неопределено Тогда
		ДопУсловия = "ДескрипторыДляОбъектов.ТипДескриптора = &ОтборПоТипу";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДопУсловия", ДопУсловия);
	Запрос.УстановитьПараметр("СсылкаНаОбъект", СсылкаНаОбъект);
	Запрос.УстановитьПараметр("ОтборПоТипу", ОтборПоТипу);
	
	ЕстьЗаписи = Не Запрос.Выполнить().Пустой();
	
	Возврат ЕстьЗаписи;
	
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#КонецЕсли
