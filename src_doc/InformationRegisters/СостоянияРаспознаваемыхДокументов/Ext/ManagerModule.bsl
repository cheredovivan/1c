#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Добавить запись.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
//  СтатусРаспознавания - ПеречислениеСсылка.СтатусыРаспознаванияДокументов
Процедура ДобавитьЗапись(ДокументСсылка, СтатусРаспознавания) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = СоздатьМенеджерЗаписи();  
	Запись.Период = ТекущаяДатаСеанса();
	Запись.Документ = ДокументСсылка;
	Запись.СтатусРаспознавания = СтатусРаспознавания;
	Запись.Ответственный = ПараметрыСеанса.ТекущийПользователь;
	Запись.Записать(Истина);
	
КонецПроцедуры

// Удаляет запись из регистра
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
Процедура УдалитьЗаписьПоДокументу(ДокументСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(ДокументСсылка);
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Получить историю по документу.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение:
//  ТаблицаЗначений
Функция ИсторияРаспознавания(ДокументСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	СостоянияРаспознаваемыхДокументов.Период КАК Период,
		|	СостоянияРаспознаваемыхДокументов.Ответственный,
		|	СостоянияРаспознаваемыхДокументов.СтатусРаспознавания
		|ИЗ
		|	РегистрСведений.СостоянияРаспознаваемыхДокументов КАК СостоянияРаспознаваемыхДокументов
		|ГДЕ
		|	СостоянияРаспознаваемыхДокументов.Документ = &Документ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период Убыв";
	
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Получить текущий статус по документу.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
//  ВозвращатьСтатусПрервано - Булево
//  ТолькоСтатус - Булево
// 
// Возвращаемое значение:
//  Неопределено, Структура - Получить текущий статус по документу
Функция ТекущийСтатусПоДокументу(ДокументСсылка, ВозвращатьСтатусПрервано = Ложь, ТолькоСтатус = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	СостоянияРаспознаваемыхДокументовСрезПоследних.Период,
		|	СостоянияРаспознаваемыхДокументовСрезПоследних.Ответственный,
		|	СостоянияРаспознаваемыхДокументовСрезПоследних.СтатусРаспознавания
		|ИЗ
		|	РегистрСведений.СостоянияРаспознаваемыхДокументов.СрезПоследних(, Документ = &ДокументСсылка) КАК
		|		СостоянияРаспознаваемыхДокументовСрезПоследних";
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	Результат = Неопределено;
	
	Если ТаблицаРезультат.Количество() > 0 Тогда
		
		Если ТолькоСтатус = Истина Тогда
			ТекущийСтатус = ТаблицаРезультат[0].СтатусРаспознавания;
			Если ТекущийСтатус = Перечисления.СтатусыРаспознаванияДокументов.Прервано И ВозвращатьСтатусПрервано = Ложь Тогда
				Возврат Неопределено;
			Иначе
				Возврат ТекущийСтатус;
			КонецЕсли;
		Иначе
		
			Результат = Новый Структура;
			Для Каждого Колонка Из ТаблицаРезультат.Колонки Цикл
				Результат.Вставить(Колонка.Имя,ТаблицаРезультат[0][Колонка.Имя]);
			КонецЦикла;
			
			Если Результат.СтатусРаспознавания = Перечисления.СтатусыРаспознаванияДокументов.Прервано И ВозвращатьСтатусПрервано = Ложь Тогда
				Результат = Неопределено;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Документ находится на распознавании.
// 
// Параметры:
//  ДокументСсылка - СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение:
//  Булево
Функция ДокументНаходитсяНаРаспознавании(ДокументСсылка) Экспорт
	
	ТекущийСтатус = ТекущийСтатусПоДокументу(ДокументСсылка);
	
	Если ТекущийСтатус = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НаРаспознавании = ТекущийСтатус.СтатусРаспознавания = Перечисления.СтатусыРаспознаванияДокументов.НаРаспознавании;
	
	Возврат НаРаспознавании;
	
КонецФункции

#КонецОбласти

#КонецЕсли