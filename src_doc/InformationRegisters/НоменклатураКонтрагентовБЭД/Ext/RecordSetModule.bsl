////////////////////////////////////////////////////////////////////////////////
// Модуль набора записей РегистрСведений.НоменклатураКонтрагентовБЭД
//
////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НеПроверяемыеРеквизиты = Новый Массив;
	НеПроверяемыеРеквизиты.Добавить("Владелец");
	
	МетаданныеСопоставления = СопоставлениеНоменклатурыКонтрагентовСлужебный.МетаданныеСопоставленияНоменклатуры();
	ПредставлениеВладельца = МетаданныеСопоставления.ВладелецНоменклатурыПредставлениеОбъекта;
	
	ТекстВладелецНеЗаполнен = СтрШаблон(НСтр("ru = 'Поле ""%1"" не заполнено'"), ПредставлениеВладельца);
	
	Для каждого Запись Из ЭтотОбъект Цикл
		Если Не ЗначениеЗаполнено(Запись.Владелец) Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстВладелецНеЗаполнен, , "Владелец", "Запись", Отказ);
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НеПроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоУдалениеВРежимеЗамещения", ЭтоУдалениеВРежимеЗамещения(Замещение));
	
	Если ОбщегоНазначенияБЭД.ЭтоУдалениеНабораЗаписей(ЭтотОбъект, Замещение) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Ключи = СопоставлениеНоменклатурыКонтрагентовСлужебный.ХешиНатуральныхКлючейНоменклатуры(
			Запись.Наименование,
			Запись.Артикул,
			Запись.КодНоменклатуры,
			Запись.ШтрихкодКомбинации);
		
		ЗаполнитьЗначенияСвойств(Запись, Ключи);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Определяет, выполняется ли удаление записей набора при замещении в режиме совместимости до 8.3.26 включительно.
// При записи менеджера записи с замещением в режиме совместимости до 8.3.26 включительно запись набора выполняется
//  дважды: сначала удаляются существующие записи, затем записываются новые.
// Первую запись (удаление) нужно пропустить в обработчиках подписок на событие.
//
// Возвращаемое значение:
//  Булево - Истина, если выполняется удаление записей набора записей при замещении в режиме совместимости до 8.3.26.
//
Функция ЭтоУдалениеВРежимеЗамещения(Режим)
	
	Возврат Режим = Истина
		И Количество() = 0
		И Не Модифицированность();
	
КонецФункции


#КонецОбласти

#Иначе
	
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
	
#КонецЕсли