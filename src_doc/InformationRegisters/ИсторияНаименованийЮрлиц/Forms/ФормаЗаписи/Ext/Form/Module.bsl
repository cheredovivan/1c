// @strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект);
		
	Если Не Юрлица.ЭтоИсторическаяЗапись(Запись.Юрлицо, Запись.Период) Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
		
	Если Не МультиязычностьДокументооборот.ИспользуетсяТолькоОдинЯзык() Тогда 
		Элементы.Наименование.КнопкаОткрытия = Истина;
		Элементы.НаименованиеПолное.КнопкаОткрытия = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	МультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(Результат) = Тип("Структура") Тогда 
		Возврат;
	КонецЕсли;	
	
	Модифицированность = Результат.Модифицированность;
	
	Реквизит = ДополнительныеПараметры.ИмяРеквизита;
	МассивМультиязычныхНаименований = Результат.ЗначенияНаРазныхЯзыках;
	
	ТекущийЭлемент.ВыделенныйТекст = Результат.СтрокаНаТекущемЯзыке;
	
	Если МассивМультиязычныхНаименований.Количество() > 0 Тогда
		Для Каждого Наименование Из МассивМультиязычныхНаименований Цикл
			Запись[Реквизит + Наименование.Суффикс] = Наименование.ЗначениеРеквизита;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПолноеОткрытие(Элемент, СтандартнаяОбработка)
	
	НаименованиеОткрытие(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяРеквизита = Элемент.Имя;
	Ключ = Новый Структура("Ключ", Запись.Юрлицо);
	Форма = ПолучитьФорму("Справочник.Контрагенты.ФормаОбъекта", Ключ);
	
	ПараметрыФормы = ПараметрыФормыСУчетомМультиязычности(ИмяРеквизита, Запись);
	ПараметрыФормы.Вставить("Объект", Форма.Объект);
	ПараметрыФормы.Вставить("ТекущиеЗначение", Элемент.ТекстРедактирования);
	
	Оповещение = Новый ОписаниеОповещения("ОбработкаОповещения", ЭтотОбъект, ПараметрыФормы);

	ОткрытьФорму("ОбщаяФорма.ВводНаРазныхЯзыках", ПараметрыФормы, , , , , Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПараметрыФормыСУчетомМультиязычности(ИмяРеквизита, Реквизиты)
	
	ИмяРеквизита = СтрЗаменить(ИмяРеквизита, "ПолеВвода", "");

	ЗначенияРеквизитов = Новый Структура;
	ЗначенияРеквизитов.Вставить(ИмяРеквизита, Реквизиты[ИмяРеквизита]);
	ЗначенияРеквизитов.Вставить(ИмяРеквизита + "Язык1", Реквизиты[ИмяРеквизита + "Язык1"]);
	ЗначенияРеквизитов.Вставить(ИмяРеквизита + "Язык2", Реквизиты[ИмяРеквизита + "Язык2"]);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Объект", Реквизиты);
	ПараметрыФормы.Вставить("ИмяРеквизита", ИмяРеквизита);
	ПараметрыФормы.Вставить("ЗначенияРеквизитов", ЗначенияРеквизитов);

	Возврат ПараметрыФормы;	
	
КонецФункции

#КонецОбласти