#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Прочитать историю юрлица - функция-обертка для лаконичности вызовов.
// 
// Параметры:
//  Юрлицо - ОпределяемыйТип.Юрлицо - Юрлицо
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица значений
Функция ПрочитатьИсториюЮрлица(Юрлицо) Экспорт
	
	ТекстЗапроса = "
		|	ВЫБРАТЬ
		|	История.Период КАК Период,
		|	История.Юрлицо КАК Юрлицо,
		|	История.Наименование КАК Наименование,
		|	История.НаименованиеПолное КАК НаименованиеПолное
		|	,&Язык1 
		|	,&Язык2
		|ИЗ
		|	РегистрСведений.ИсторияНаименованийЮрлиц КАК История
		|ГДЕ
		|	История.Юрлицо = &Юрлицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
	
	СведенияОЯзыках = МультиязычностьСервер.СведенияОЯзыках();
	ИспользуемыеЯзыки = СведенияОЯзыках.Используется;
	Если ИспользуемыеЯзыки.Язык1 Тогда
		Если СведенияОЯзыках.ОсновнойЯзык = ТекущийЯзык().КодЯзыка Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&Язык1", ",История.НаименованиеЯзык1 КАК НаименованиеЯзык1
														|	,История.НаименованиеПолноеЯзык1 КАК НаименованиеПолноеЯзык1");
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "КАК Наименование,", "КАК НаименованиеЯзык1,");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "КАК НаименованиеПолное", "КАК НаименованиеПолноеЯзык1");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&Язык1", ",История.НаименованиеЯзык1 КАК Наименование 
															|	,История.НаименованиеПолноеЯзык1 КАК НаименованиеПолное");
		КонецЕсли;
	Иначе	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&Язык1", "");
	КонецЕсли;	
		
	Если ИспользуемыеЯзыки.Язык2 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&Язык2", ",История.НаименованиеЯзык2 КАК НаименованиеЯзык2
														|	,История.НаименованиеПолноеЯзык2 КАК НаименованиеПолноеЯзык2");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&Язык2", "");
	КонецЕсли;
		
	Запрос = Новый Запрос(ТекстЗапроса);	
	Запрос.УстановитьПараметр("Юрлицо", Юрлицо);
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Создать набор записей по юрлицу с отбором без чтения - функция-обертка для лаконичности вызовов.
// 
// Параметры:
//  Юрлицо - ОпределяемыйТип.Юрлицо - Юрлицо
// 
// Возвращаемое значение:
//  РегистрСведенийНаборЗаписей - Набор записей
Функция СоздатьНаборИсторииЮрлица(Юрлицо) Экспорт
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Юрлицо.Установить(Юрлицо);
	Возврат Набор;
	
КонецФункции

// Предварительно заполняет наименования юрлиц на дополнительных языках 
// наименованиями юрлиц на основном языке конфигурации, чтобы не было пустых полей у пользователей с интерфейсом
// на дополнительном языке.
//
Процедура ЗаполнитьНаименованияНаДополнительныхЯзыках() Экспорт 

	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	ИспользуетсяПервыйДополнительныйЯзык = МультиязычностьСервер.ИспользуетсяДополнительныйЯзык(1);
	ИспользуетсяВторойДополнительныйЯзык = МультиязычностьСервер.ИспользуетсяДополнительныйЯзык(2);
	
	Для Каждого Запись Из НаборЗаписей Цикл
		
		Если ИспользуетсяПервыйДополнительныйЯзык Тогда	
			Запись.НаименованиеЯзык1 = ?(ПустаяСтрока(Запись.НаименованиеЯзык1), Запись.Наименование, 
				Запись.НаименованиеЯзык1);
			Запись.НаименованиеПолноеЯзык1 = ?(ПустаяСтрока(Запись.НаименованиеПолноеЯзык1), 
				Запись.НаименованиеПолное, Запись.НаименованиеПолноеЯзык1);	
		КонецЕсли;	
		
		Если ИспользуетсяВторойДополнительныйЯзык Тогда	
			Запись.НаименованиеЯзык2 = ?(ПустаяСтрока(Запись.НаименованиеЯзык2), Запись.Наименование, 
				Запись.НаименованиеЯзык2);
			Запись.НаименованиеПолноеЯзык2 = ?(ПустаяСтрока(Запись.НаименованиеПолноеЯзык2), 
				Запись.НаименованиеПолное, Запись.НаименованиеПолноеЯзык2);
		КонецЕсли;
		
	КонецЦикла;

	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли