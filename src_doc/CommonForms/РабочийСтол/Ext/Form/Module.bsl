#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если РаботаСРабочимСтолом.ФормаИнициализирована(ЭтотОбъект) Тогда
		// При вызове УстановитьПараметрыФункциональныхОпцийИнтерфейса, повторно вызывается
		// ПриСозданииНаСервере - поэтому на всякий случай сначала очистим рабочий стол.
		РаботаСРабочимСтолом.УдалитьРабочийСтол(ЭтотОбъект);
	КонецЕсли;
	
	// МобильныйКлиент
	МК_НастроитьЭлементыФормы(ЭтотОбъект);
	// Конец МобильныйКлиент

	СоздатьРабочийСтол();

	Масштаб = РаботаСВиджетами.МасштабРабочегоСтола();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	РаботаСВиджетамиКлиент.ПриОткрытии(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	РаботаСВиджетамиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);

	Если ИмяСобытия = "Запись_НастройкиРабочихСтолов"
		Или ИмяСобытия = "Запись_НастройкиБлоковНавигации"
		Или ИмяСобытия = "Запись_Условие"
		Или ИмяСобытия = "Запись_БлокиНавигации"
		Или ИмяСобытия = "Запись_Виджеты"
		Или ИмяСобытия = "Запись_ГрупповыеПоказатели"
		Или ИмяСобытия = "Запись_КомандыВиджетов"
		Или ИмяСобытия = "Запись_ПоказателиВиджетов"
		Или ИмяСобытия = "Запись_РабочиеСтолы"
		Или ИмяСобытия = "Запись_Реестры" Тогда
		ПерестроитьРабочийСтол();
		РаботаСВиджетамиКлиент.ОжидатьЗавершениеДлительнойОперацииФоновогоОбновленияВиджетов(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()

	ПерестроитьРабочийСтол();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура Подключаемый_ПриИзменении(Элемент)

	РаботаСВиджетамиКлиент.ОбработчикСобытияПриИзменении(ЭтотОбъект, Элемент);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Нажатие(Элемент, СтандартнаяОбработка)

	РаботаСВиджетамиКлиент.ОбработчикСобытияНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)

	РаботаСВиджетамиКлиент.ОбработкаРасшифровки(ЭтотОбъект, Элемент, Расшифровка,
		СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_Команда(Команда)

	РаботаСВиджетамиКлиент.ОбработчикКоманды(ЭтотОбъект, Команда);

КонецПроцедуры

&НаКлиенте
Процедура МК_ИзменитьПорогДляНовых(Команда)
	
	МК_ИзменитьПорогДляНовыхПродолжение = Новый ОписаниеОповещения("МК_ИзменитьПорогДляНовыхПродолжение",
		ЭтотОбъект);
	
	ПоказатьВводЧисла(МК_ИзменитьПорогДляНовыхПродолжение,
		МК_ПорогДляНовых,
		НСтр("ru='Изменить порог новых'"),
		4,
		0);

КонецПроцедуры

&НаКлиенте
Процедура МК_ИспользоватьРазноцветныеВиджеты(Команда)

	МК_ИзменитьЗначениеЦветаВиджетов();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПерестроитьРабочийСтол()

	ВосстановитьТекущееСостояние = Истина;

	// Для мобильного клиент не нужно запоминать текущее состояние
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		ВосстановитьТекущееСостояние = Ложь;
	КонецЕсли;

	Если ВосстановитьТекущееСостояние Тогда
		РаботаСРабочимСтолом.ЗапомнитьСостояниеРабочегоСтола(ЭтотОбъект);
	КонецЕсли;

	РаботаСРабочимСтолом.УдалитьРабочийСтол(ЭтотОбъект);
	СоздатьРабочийСтол();

	Если ВосстановитьТекущееСостояние Тогда
		РаботаСРабочимСтолом.ВосстановитьСостояниеРабочегоСтола(ЭтотОбъект);
		Масштаб = РаботаСВиджетами.МасштабРабочегоСтола();
	КонецЕсли;
	
	РаботаСВиджетами.ОбновитьРеквизитыВиджетов(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьРабочийСтол()

	РабочийСтол = РаботаСРабочимСтолом.РабочийСтол();
	РаботаСРабочимСтолом.СоздатьРабочийСтол(ЭтотОбъект, РабочийСтол);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Автообновление()

	РаботаСВиджетамиКлиент.Автообновление(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОжиданиеАктивизации()

	РаботаСВиджетамиКлиент.ОжиданиеАктивизации(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗавершениеФоновогоОбновления(Результат, ДополнительныеПараметры) Экспорт

	РаботаСВиджетамиКлиент.ЗавершениеФоновогоОбновления(ЭтотОбъект, Результат,
		ДополнительныеПараметры);

КонецПроцедуры

&НаСервере
Функция Подключаемый_ОбновитьРеквизитыВиджетов() Экспорт

	РаботаСВиджетами.ОбновитьРеквизитыВиджетов(ЭтотОбъект);

КонецФункции

&НаКлиенте
Процедура Подключаемый_СоздатьЗадачу(Элемент)
	ОткрытьФорму("Документ.Задача.ФормаОбъекта", , ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СоздатьКонтроль(Элемент)
	ОткрытьФорму("Справочник.Контроль.ФормаОбъекта", , ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НовоеПисьмо(Элемент)
	ОткрытьФорму("Документ.ИсходящееПисьмо.ФормаОбъекта", , ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НовоеОтсутствие(Элемент)
	ОтсутствияКлиент.СоздатьОтсутствие();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОтчетЗаДень(Элемент)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Дата", НачалоДня(ТекущаяДата()));
	ОткрытьФорму("Документ.ЕжедневныйОтчет.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НовоеСобытиеВКалендаре(Элемент)
	ОткрытьФорму("Справочник.ЗаписиРабочегоКалендаря.Форма.ФормаЭлемента", , ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НоваяТемаНаФоруме(Элемент)
	РаботаСОбсуждениямиКлиент.СоздатьНовуюТему();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СканироватьДокументы()

#Если МобильныйКлиент Тогда
	МК_Клиент.СканироватьДокументы();
#КонецЕсли

КонецПроцедуры

&НаСервереБезКонтекста
Процедура МК_НастроитьЭлементыФормы(Форма)

	Форма.ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
	Элементы = Форма.Элементы;
	Если Форма.ЭтоМобильныйКлиент Тогда
		Форма.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Верх;
		МК.ДобавитьКомандыДляСоздания(Форма, Элементы.МК_Создать);
		Форма.МК_ПорогДляНовых = МК.ПорогДляНовых();
		Элементы.МК_ИзменитьПорогДляНовых.Заголовок = СтрШаблон(НСтр(
			"ru = 'Порог для новых: %1'"),
			Форма.МК_ПорогДляНовых);

		Форма.МК_ИспользоватьРазноцветныеВиджеты = МК.ИндексЦветаВиджетов() = 1;
		Элементы.МК_ИспользоватьРазноцветныеВиджеты.Пометка = Форма.МК_ИспользоватьРазноцветныеВиджеты;
	Иначе
		Форма.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Нет;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура МК_ИзменитьПорогДляНовыхПродолжение(ВведенноеЧисло, ДопПараметры) Экспорт

	Если ВведенноеЧисло <> Неопределено Тогда
		Если ВведенноеЧисло < 1 Тогда
			ПоказатьОповещениеПользователя(НСтр("ru = 'Значение порога не может быть меньше 1'"));
			ВведенноеЧисло = 1;
		КонецЕсли;

		МК_ПорогДляНовых = ВведенноеЧисло;
		Элементы.МК_ИзменитьПорогДляНовых.Заголовок = СтрШаблон(НСтр("ru = 'Порог для новых: %1'"),
			МК_ПорогДляНовых);
		МК_ИзменитьПорогДляНовыхНаСервере();
		ПерестроитьРабочийСтол();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура МК_ИзменитьПорогДляНовыхНаСервере()
	
	МК.ИзменитьПорогДляНовых(МК_ПорогДляНовых);

КонецПроцедуры

&НаСервере
Процедура МК_ИзменитьЗначениеЦветаВиджетов()

	МК_ИспользоватьРазноцветныеВиджеты = Не МК_ИспользоватьРазноцветныеВиджеты;
	Элементы.МК_ИспользоватьРазноцветныеВиджеты.Пометка = МК_ИспользоватьРазноцветныеВиджеты;
	МК.СохранитьИндексЦветаВиджетов(?(МК_ИспользоватьРазноцветныеВиджеты, 1, 0));
	ПерестроитьРабочийСтол();

КонецПроцедуры

#КонецОбласти