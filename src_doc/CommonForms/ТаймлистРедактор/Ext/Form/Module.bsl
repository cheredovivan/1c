
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВерсияФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Файл, "ТекущаяВерсия");
	
	РегистрыСведений.ТаймлистДанныеРаботыСервиса.ЗаписатьПолеРедактирует(ВерсияФайла,
		Пользователи.ТекущийПользователь());
	
	ДанныеРаботыСервиса = Таймлист.ДанныеРаботыСервиса(ВерсияФайла, "Расшифровка, Автопротокол, Спикеры");
	
	РасшифровкаИсходная = Таймлист.HTMLПредставлениеБезСлужебныхТегов(Параметры.Расшифровка);
	РасшифровкаРедактор = РасшифровкаИсходная;
	
	РасшифровкаСервис = ДанныеРаботыСервиса.Расшифровка.Получить();
	
	Если Параметры.Автопротокол = "" Тогда
		Заголовок = СтрШаблон(НСтр("ru = 'Редактирование расшифровки файла ""%1""'"), Параметры.Файл);
		Элементы.СтраницаАвтопротокол.Видимость = Ложь;
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		СтраницаРасшифровкиИнициализирована = Истина;
	Иначе
		Заголовок = СтрШаблон(НСтр("ru = 'Редактирование расшифровки/автопротокола файла ""%1""'"), Параметры.Файл);
		АвтопротоколИсходный = Таймлист.HTMLПредставлениеБезСлужебныхТегов(Параметры.Автопротокол);
		АвтопротоколРедактор = АвтопротоколИсходный;
		
		АвтопротоколСервис = ДанныеРаботыСервиса.Автопротокол.Получить();
		
		Если Параметры.КлючИсточника = ТаймлистКлиентСервер.КлючОтображенияРасшифровкиHTML() Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРасшифровка;
			СтраницаРасшифровкиИнициализирована = Истина;
		Иначе
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаАвтопротокол;
			СтраницаАвтопротоколаИнициализирована = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.ОтборУчастников.Количество() > 0 Тогда
		ОтборУчастников = Параметры.ОтборУчастников;
	КонецЕсли;
	
	ДанныеПоСпикерам = ДанныеРаботыСервиса.Спикеры.Получить();
	
	Для Каждого Элемент Из ДанныеПоСпикерам Цикл
		Строка = СпикерыИзСервиса.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, Элемент);
	КонецЦикла;
	
	НастройкаШрифтОтображения = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПерсональнуюНастройку("ШрифтОтображения");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВключитьРежимРедактированияHTML(РасшифровкаРедактор);
	ВключитьРежимРедактированияHTML(АвтопротоколРедактор);
	
	ВключитьРежимРедактированияHTML(РасшифровкаИсходная);
	ВключитьРежимРедактированияHTML(АвтопротоколИсходный);
	
	ВключитьРежимРедактированияHTML(РасшифровкаСервис);
	ВключитьРежимРедактированияHTML(АвтопротоколСервис);
	
	ЗаполнитьТаблицуСпикеровДляРедактирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(КлючЗакрытияФормы) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Модифицированность Тогда
		
		Если СтраницаРасшифровкиИнициализирована Тогда
			
			РасшифровкаПриОткрытии = РаботаС_HTML.ПолучитьТегВТекстеHTML(РасшифровкаИсходная, "body").Содержание;
			РасшифровкаПередЗакрытием = Элементы.Расшифровка.Документ.body.innerHTML;
			
			Если РасшифровкаПередЗакрытием <> РасшифровкаПриОткрытии Тогда
				Модифицированность = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		Если СтраницаАвтопротоколаИнициализирована Тогда
			
			АвтопротоколПриОткрытии = РаботаС_HTML.ПолучитьТегВТекстеHTML(АвтопротоколИсходный, "body").Содержание;
			АвтопротоколПередЗакрытием = Элементы.Автопротокол.Документ.body.innerHTML;
			
			Если АвтопротоколПередЗакрытием <> АвтопротоколПриОткрытии Тогда
				Модифицированность = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПередЗакрытием(
		Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Модифицированность) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	ЗакрытьФормуБезСохранения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.СтраницаРасшифровка И Не СтраницаРасшифровкиИнициализирована Тогда
		СтраницаРасшифровкиИнициализирована = Истина;
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаАвтопротокол И Не СтраницаАвтопротоколаИнициализирована Тогда
		СтраницаАвтопротоколаИнициализирована = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура АвтопротоколПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСпикеры

&НаКлиенте
Процедура СпикерыПередНачаломИзменения(Элемент, Отказ)
	
	ПредыдущееПредставлениеУчастника = Элементы.Спикеры.ТекущиеДанные.ПредставлениеУчастника;
	
КонецПроцедуры

&НаКлиенте
Процедура СпикерыПредставлениеУчастникаПриИзменении(Элемент)
	
	ЗаменитьСпикераВРасшифровке();
	
КонецПроцедуры

&НаКлиенте
Процедура СпикерыПредставлениеУчастникаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ОписаниеТаблицыУчастников = ТаймлистКлиент.ОписаниеТаблицыУчастников();
	ОписаниеТаблицыУчастников.ИмяТаблицыУчастников = "Спикеры";
	ОписаниеТаблицыУчастников.ИмяРеквизитаПредставленияУчастника = "ПредставлениеУчастника";
	ОписаниеТаблицыУчастников.ИмяРеквизитаУчастника = "Участник";
	
	ТаймлистКлиент.УчастникНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка,
		ОписаниеТаблицыУчастников, Элементы.Спикеры.ТекущиеДанные.Участник, ОтборУчастников);
	
КонецПроцедуры

&НаКлиенте
Процедура СпикерыПредставлениеУчастникаОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные,
	ВыборДобавлением, СтандартнаяОбработка)
	
	ОписаниеТаблицыУчастников = ТаймлистКлиент.ОписаниеТаблицыУчастников();
	ОписаниеТаблицыУчастников.ИмяТаблицыУчастников = "Спикеры";
	ОписаниеТаблицыУчастников.ИмяРеквизитаПредставленияУчастника = "ПредставлениеУчастника";
	ОписаниеТаблицыУчастников.ИмяРеквизитаУчастника = "Участник";
	
	ТаймлистКлиент.УчастникОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, СтандартнаяОбработка,
		ОписаниеТаблицыУчастников);
	
	ЗаменитьСпикераВРасшифровке();
	
КонецПроцедуры

&НаКлиенте
Процедура СпикерыПредставлениеУчастникаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
	СтандартнаяОбработка)
	
	ТаймлистКлиент.УчастникАвтоподбор(Текст, ДанныеВыбора, СтандартнаяОбработка, ОтборУчастников);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область РедактированиеHTML

&НаКлиенте
Процедура ИзменитьШрифт(Команда)
	
	ИзменитьШрифтОбработчик();
	
КонецПроцедуры

&НаКлиенте
Процедура УвеличитьШрифт(Команда)
	
	УвеличитьШрифтОбработчик();
	
КонецПроцедуры

&НаКлиенте
Процедура УменьшитьШрифт(Команда)
	
	УменьшитьШрифтОбработчик();
	
КонецПроцедуры

&НаКлиенте
Процедура Полужирный(Команда)
	
	ПолужирныйОбработчик();
	
КонецПроцедуры

&НаКлиенте
Процедура Наклонный(Команда)
	
	НаклонныйОбработчик();
	
КонецПроцедуры

&НаКлиенте
Процедура Подчеркнутый(Команда)
	
	ПодчеркнутыйОбработчик();
	
КонецПроцедуры

&НаКлиенте
Процедура ЦветТекста(Команда)
	
	ЦветТекстаОбработчик();
	
КонецПроцедуры

&НаКлиенте
Процедура ЦветФона(Команда)
	
	ЦветФонаОбработчик();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФормат(Команда)
	
	ОчиститьФорматОбработчик();
	
КонецПроцедуры

&НаКлиенте
Процедура МаркированныйСписок(Команда)
	
	МаркированныйСписокОбработчик();
	
КонецПроцедуры

&НаКлиенте
Процедура НумерованныйСписок(Команда)
	
	НумерованныйСписокОбработчик();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтступВлево(Команда)
	
	ОтступВлевоОбработчик();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтступВправо(Команда)
	
	ОтступВправоОбработчик();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыравниваниеВлево(Команда)
	
	ВыравниваниеВлевоОбработчик();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыравниваниеПоЦентру(Команда)
	
	ВыравниваниеПоЦентруОбработчик();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыравниваниеВправо(Команда)
	
	ВыравниваниеВправоОбработчик();
	
КонецПроцедуры

#КонецОбласти

#Область Расшифровка

&НаКлиенте
Процедура ОбъединитьСтрокиРасшифровки(Команда)
	
	ВыделенныйУзел = Элементы.Расшифровка.Документ.getSelection().anchorNode.parentNode;
	
	Если ВыделенныйУзел.parentNode = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Пока ВыделенныйУзел.parentNode.tagName <> "BODY" Цикл
		ВыделенныйУзел = ВыделенныйУзел.parentNode;
		Если ВыделенныйУзел.parentNode = Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	СледующийУзел = ВыделенныйУзел.nextSibling;
	Если СледующийУзел = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДочерниеУзлыСледующегоУзла = СледующийУзел.childNodes;
	
	НачалоСледующегоСпикераУдалено = Ложь;
	
	Для Счетчик = 0 По ДочерниеУзлыСледующегоУзла.length - 1 Цикл
		
		Если Не НачалоСледующегоСпикераУдалено Тогда
			
			НайденнаяПозиция = СтрНайти(ДочерниеУзлыСледующегоУзла[Счетчик].textContent, ": ");
			
			Если НайденнаяПозиция <> 0 Тогда
				
				ДочерниеУзлыСледующегоУзла[Счетчик].textContent =
					Сред(ДочерниеУзлыСледующегоУзла[Счетчик].textContent, НайденнаяПозиция + 1);
				
				Если ЗначениеЗаполнено(ДочерниеУзлыСледующегоУзла[Счетчик].textContent) Тогда
					КопияТекущегоУзла = ДочерниеУзлыСледующегоУзла[Счетчик].cloneNode(true);
					ВыделенныйУзел.appendChild(КопияТекущегоУзла);
				КонецЕсли;
				
				НачалоСледующегоСпикераУдалено = Истина;
				
			КонецЕсли;
			
		Иначе
			
			КопияТекущегоУзла = ДочерниеУзлыСледующегоУзла[Счетчик].cloneNode(true);
			ВыделенныйУзел.appendChild(КопияТекущегоУзла);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ВыделенныйУзел.nextSibling.remove();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РазделитьСтрокуРасшифровки(Команда)
	
	ВыделенныйУзел = Элементы.Расшифровка.Документ.getSelection().anchorNode.parentNode;
	
	Если ВыделенныйУзел.parentNode = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Пока ВыделенныйУзел.parentNode.tagName <> "BODY" Цикл
		ВыделенныйУзел = ВыделенныйУзел.parentNode;
		Если ВыделенныйУзел.parentNode = Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	ПозицияНачалаВремени = СтрНайти(ВыделенныйУзел.textContent, " (");
	ПозицияКонцаВремени = СтрНайти(ВыделенныйУзел.textContent, "): ");
	ТекущийСпикер = Лев(ВыделенныйУзел.textContent, ПозицияНачалаВремени - 1);
	ВремяСпикера = Сред(ВыделенныйУзел.textContent, ПозицияНачалаВремени + 1, ПозицияКонцаВремени - ПозицияНачалаВремени);
	
	ТекстСпикера = "";
	ДочерниеУзлы = ВыделенныйУзел.childNodes;
	ЭтоРечьСпикера = Ложь;
	
	Для Счетчик = 0 По ДочерниеУзлы.length - 1 Цикл
		
		Если Не ЭтоРечьСпикера Тогда
			
			НайденнаяПозиция = СтрНайти(ДочерниеУзлы[Счетчик].textContent, ": ");
			
			Если НайденнаяПозиция <> 0 Тогда
				
				ОставшийсяТекст = Сред(ДочерниеУзлы[Счетчик].textContent, НайденнаяПозиция + 1);
				
				Если ЗначениеЗаполнено(ОставшийсяТекст) Тогда
					
					Если ДочерниеУзлы[Счетчик].nodeName = "#text" Тогда
						ТекстСпикера = ТекстСпикера + ДочерниеУзлы[Счетчик].textContent;
					Иначе
						ТекстСпикера = ТекстСпикера + ДочерниеУзлы[Счетчик].outerHTML;
					КонецЕсли;
					
				КонецЕсли;
				
				ЭтоРечьСпикера = Истина;
				
			КонецЕсли;
			
		Иначе
			
			Если ДочерниеУзлы[Счетчик].nodeName = "#text" Тогда
				ТекстСпикера = ТекстСпикера + ДочерниеУзлы[Счетчик].textContent;
			Иначе
				ТекстСпикера = ТекстСпикера + ДочерниеУзлы[Счетчик].outerHTML;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ТекстСпикера) Тогда
		Возврат;
	КонецЕсли;
	
	ОтборСпикер = Новый Структура("ПредставлениеУчастника", ТекущийСпикер);
	
	Если Не Спикеры.НайтиСтроки(ОтборСпикер).Количество() Тогда
		Возврат;
	КонецЕсли;
	
	СписокСпикеров = Новый СписокЗначений;
	Для Каждого Спикер Из Спикеры Цикл
		Если Спикер.ПредставлениеУчастника = ТекущийСпикер Тогда
			Продолжить;
		КонецЕсли;
		СписокСпикеров.Добавить(Спикер.ПредставлениеУчастника);
	КонецЦикла;
	
	ДополнительныеПараметры = Новый Структура("ВыделенныйУзел, ТекстСпикера, ВремяСпикера", ВыделенныйУзел, ТекстСпикера,
		ВремяСпикера);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьПослеРазделенияСтрокиРасшифровки",
		ЭтотОбъект, ДополнительныеПараметры);
	ПараметрыФормы = Новый Структура("Текст, Спикер, Спикеры", ТекстСпикера, ТекущийСпикер, СписокСпикеров);
	ОткрытьФорму("ОбщаяФорма.ТаймлистРазделениеСтрокиРасшифровки", ПараметрыФормы,,,,, ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьПервоначальнуюРасшифровку(Команда)
	
	Элементы.Расшифровка.Документ.documentElement.innerHTML = РасшифровкаИсходная;
	ЗаполнитьТаблицуСпикеровДляРедактирования(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьРасшифровкойИзСервиса(Команда)
	
	Элементы.Расшифровка.Документ.documentElement.innerHTML = РасшифровкаСервис;
	ЗаполнитьТаблицуСпикеровДляРедактирования(Истина, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область Автопротокол

&НаКлиенте
Процедура ВосстановитьПервоначальныйАвтопротокол(Команда)
	
	Элементы.Автопротокол.Документ.documentElement.innerHTML = АвтопротоколИсходный;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьАвтопротоколомИзСервиса(Команда)
	
	Элементы.Автопротокол.Документ.documentElement.innerHTML = АвтопротоколСервис;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьАвтопротоколВWord(Команда)
	
	ТаймлистКлиент.ОткрытьАвтопротоколДокумент(АвтопротоколРедактор);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Сохранить(Команда)
	
	Для Каждого Спикер Из Спикеры Цикл
		СтрокаТаблицы = СпикерыИзСервиса.НайтиСтроки(Новый Структура("initialName", Спикер.ИзРасшифровки));
		СтрокаТаблицы[0].changedName = ?(Спикер.ПредставлениеУчастника <> Спикер.ИзРасшифровки,
			Спикер.ПредставлениеУчастника, "");
	КонецЦикла;
	
	СохраняемаяРасшифровка = "";
	СохраняемыйАвтопротокол = "";
	
	Если СтраницаРасшифровкиИнициализирована Тогда
		СохраняемаяРасшифровка = Элементы.Расшифровка.Документ.documentElement.outerHTML;
		ОтключитьРежимРедактированияHTML(СохраняемаяРасшифровка);
	КонецЕсли;
	
	Если СтраницаАвтопротоколаИнициализирована Тогда
		СохраняемыйАвтопротокол = Элементы.Автопротокол.Документ.documentElement.outerHTML;
		ОтключитьРежимРедактированияHTML(СохраняемыйАвтопротокол);
	КонецЕсли;
	
	ЗаписатьРезультатРедактирования(ВерсияФайла, СохраняемаяРасшифровка, СохраняемыйАвтопротокол, СпикерыИзСервиса);
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаАвтопротокол Тогда
		КлючЗакрытияФормы = ТаймлистКлиентСервер.КлючОтображенияАвтопротоколаHTML();
	Иначе
		КлючЗакрытияФормы = ТаймлистКлиентСервер.КлючОтображенияРасшифровкиHTML();
	КонецЕсли;
	
	Закрыть(КлючЗакрытияФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ЗакрытьФормуБезСохранения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиКомандРедактированияHTML

&НаКлиенте
Процедура ИзменитьШрифтОбработчик()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьШрифтПродолжение", ЭтотОбъект);
	ОткрытьФорму("Документ.ИсходящееПисьмо.Форма.ФормаВыбораШрифта",, ЭтотОбъект,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьШрифтПродолжение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
		
	ИначеЕсли Результат = "Шрифт по умолчанию" Тогда
		РедактируемыйЭлемент = РедактируемыйЭлемент();
		HTMLДокумент = РедактируемыйЭлемент.Документ;
		HTMLДокумент.execCommand("fontName", Ложь, НастройкаШрифтОтображения.Имя);
		
	ИначеЕсли Результат = "Другой.." Тогда
		ДиалогВыбораШрифта = Новый ДиалогВыбораШрифта;
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеИзменитьШрифт", ЭтотОбъект);
		ДиалогВыбораШрифта.Показать(ОписаниеОповещения);
		
	Иначе
		РедактируемыйЭлемент = РедактируемыйЭлемент();
		HTMLДокумент = РедактируемыйЭлемент.Документ;
		HTMLДокумент.execCommand("fontName", Ложь, Результат);
		
	КонецЕсли;
	
	ТекущийЭлемент = РедактируемыйЭлемент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеИзменитьШрифт(Шрифт, Параметры) Экспорт

	Если Шрифт = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РедактируемыйЭлемент = РедактируемыйЭлемент();
	
	Если Не ПустаяСтрока(Шрифт.Имя) Тогда
		
		HTMLДокумент = РедактируемыйЭлемент.Документ;
		HTMLДокумент.execCommand("fontName", Ложь, Шрифт.Имя);
		
	КонецЕсли;
	
	РазмерШрифта = 2;
	
	Если Шрифт.Размер <> -1 Тогда
		Если Шрифт.Размер < 8 Тогда
			РазмерШрифта = 1;
		ИначеЕсли Шрифт.Размер <= 10 Тогда
			РазмерШрифта = 2;
		ИначеЕсли Шрифт.Размер <= 12 Тогда
			РазмерШрифта = 3;
		ИначеЕсли Шрифт.Размер <= 14 Тогда
			РазмерШрифта = 4;
		ИначеЕсли Шрифт.Размер <= 16 Тогда
			РазмерШрифта = 5;
		ИначеЕсли Шрифт.Размер <= 18 Тогда
			РазмерШрифта = 6;
		Иначе
			РазмерШрифта = 7;
		КонецЕсли;
	КонецЕсли;
	
	HTMLДокумент = РедактируемыйЭлемент.Документ; 
	HTMLДокумент.execCommand("fontSize", Ложь, РазмерШрифта);
	
	Если Шрифт.Зачеркивание = Истина Тогда
		
		HTMLДокумент = РедактируемыйЭлемент.Документ;
		ВыполнитьHTMLКоманду("strikeThrough");
		
	КонецЕсли;
	
	Если Шрифт.Жирный = Истина Тогда
		
		HTMLДокумент = РедактируемыйЭлемент.Документ;
		ВыполнитьHTMLКоманду("Bold");
		
	КонецЕсли;
	
	Если Шрифт.Наклонный = Истина Тогда
		
		HTMLДокумент = РедактируемыйЭлемент.Документ;
		ВыполнитьHTMLКоманду("italic");
		
	КонецЕсли;	
	
	Если Шрифт.Подчеркивание = Истина Тогда
		
		HTMLДокумент = РедактируемыйЭлемент.Документ;
		ВыполнитьHTMLКоманду("underline");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УвеличитьШрифтОбработчик()
	
	РедактируемыйЭлемент = РедактируемыйЭлемент().Документ;
	Размер = РедактируемыйЭлемент.queryCommandValue("fontSize");
	
	Если Не ЗначениеЗаполнено(Размер) Тогда
		Размер = 2;
	КонецЕсли;
	РедактируемыйЭлемент.execCommand("fontSize", Ложь, Размер + 1);
	
	ТекущийЭлемент = РедактируемыйЭлемент();
	
КонецПроцедуры

&НаКлиенте
Процедура УменьшитьШрифтОбработчик()
	
	РедактируемыйЭлемент = РедактируемыйЭлемент().Документ;
	Размер = РедактируемыйЭлемент.queryCommandValue("fontSize");
	Если Не ЗначениеЗаполнено(Размер) Тогда 
		Размер = 3;
	КонецЕсли;
	РедактируемыйЭлемент.execCommand("fontSize", Ложь, Размер - 1);
	
	ТекущийЭлемент = РедактируемыйЭлемент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолужирныйОбработчик()
	
	ВыполнитьHTMLКоманду("Bold");
	
КонецПроцедуры

&НаКлиенте
Процедура НаклонныйОбработчик()
	
	ВыполнитьHTMLКоманду("italic");
	
КонецПроцедуры

&НаКлиенте
Процедура ПодчеркнутыйОбработчик()
	
	ВыполнитьHTMLКоманду("underline");
	
КонецПроцедуры

&НаКлиенте
Процедура ЦветТекстаОбработчик()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеЦветТекста", ЭтотОбъект);
	ОткрытьФорму("Документ.ИсходящееПисьмо.Форма.ФормаВыбораЦвета",, ЭтотОбъект,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеЦветТекста(Цвет, Параметры) Экспорт
	
	РедактируемыйЭлемент = РедактируемыйЭлемент();
	
	Если Цвет <> Неопределено Тогда
		HTMLДокумент = РедактируемыйЭлемент.Документ;
		HTMLДокумент.execCommand("foreColor", Ложь, Цвет );
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЦветФонаОбработчик()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеЦветФона", ЭтотОбъект);
	ОткрытьФорму("Документ.ИсходящееПисьмо.Форма.ФормаВыбораЦветаФона",, ЭтотОбъект,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеЦветФона(Цвет, Параметры) Экспорт
	
	РедактируемыйЭлемент = РедактируемыйЭлемент();
	
	Если Цвет <> Неопределено Тогда
		HTMLДокумент = РедактируемыйЭлемент.Документ;
		HTMLДокумент.execCommand("backColor", Ложь, Цвет );
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФорматОбработчик()
	
	РедактируемыйЭлемент = РедактируемыйЭлемент();
	
	HTMLДокумент = РедактируемыйЭлемент.Документ;
	HTMLДокумент.execCommand("removeFormat", Ложь, "");
	
	ТекущийЭлемент = РедактируемыйЭлемент;
	
КонецПроцедуры

&НаКлиенте
Процедура МаркированныйСписокОбработчик()
	
	ВыполнитьHTMLКоманду("insertUnorderedList");
	
КонецПроцедуры

&НаКлиенте
Процедура НумерованныйСписокОбработчик()
	
	ВыполнитьHTMLКоманду("insertOrderedList");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтступВлевоОбработчик()
	
	ВыполнитьHTMLКоманду("outdent");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтступВправоОбработчик()
	
	ВыполнитьHTMLКоманду("indent");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыравниваниеВлевоОбработчик()
	
	ВыполнитьHTMLКоманду("justifyLeft");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыравниваниеПоЦентруОбработчик()
	
	ВыполнитьHTMLКоманду("justifyCenter");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыравниваниеВправоОбработчик()
	
	ВыполнитьHTMLКоманду("justifyRight");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьHTMLКоманду(Команда)
	
	РедактируемыйЭлемент = РедактируемыйЭлемент();
	
	HTMLДокумент = РедактируемыйЭлемент.Документ;
	HTMLДокумент.execCommand(Команда);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ВключитьРежимРедактированияHTML(ТекстHTML)
	
	Если СтрНайти(ТекстHTML, "<body") <> 0 Или СтрНайти(ТекстHTML, "<BODY") <> 0 Тогда
		
		ТекстHTML = СтрЗаменить(ТекстHTML, "<body", "<body contentEditable=true");
		ТекстHTML = СтрЗаменить(ТекстHTML, "<BODY", "<BODY contentEditable=true");
		
	Иначе
		
		ТекстHTML = СтрЗаменить(ТекстHTML, "<html>", "<html><body contentEditable=true>");
		ТекстHTML = СтрЗаменить(ТекстHTML, "</html>", "</body></html>");
		ТекстHTML = СтрЗаменить(ТекстHTML, "<HTML>", "<HTML><BODY contentEditable=true>");
		ТекстHTML = СтрЗаменить(ТекстHTML, "</HTML>", "</BODY></HTML>");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьРежимРедактированияHTML(ТекстHTML)
	
	ТекстHTML = СтрЗаменить(ТекстHTML, " contentEditable=true", "");
	ТекстHTML = СтрЗаменить(ТекстHTML, " contenteditable=""true""", "");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПослеРазделенияСтрокиРасшифровки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыделенныйУзел = ДополнительныеПараметры.ВыделенныйУзел;
	
	НовыйЭлемент = Элементы.Расшифровка.Документ.createElement("p");
	НовыйЭлемент.innerHTML = СтрШаблон("<b><u>%1</u> %2: </b>%3", Результат.НовыйСпикер, ДополнительныеПараметры.ВремяСпикера,
		Результат.НовыйТекст);
	СледующийУзел = ВыделенныйУзел.nextSibling;
	Если СледующийУзел <> Неопределено Тогда
		СледующийУзел.parentElement.insertBefore(НовыйЭлемент, СледующийУзел);
	Иначе
		ВыделенныйУзел.parentElement.appendChild(НовыйЭлемент);
	КонецЕсли;
	
	ВыделенныйУзел.innerHTML = СтрЗаменить(ВыделенныйУзел.innerHTML, ДополнительныеПараметры.ТекстСпикера,
		Результат.ИзмененныйТекст);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&Наклиенте
Процедура ЗаменитьСпикераВРасшифровке()
	
	НовоеПредставлениеУчастника = Элементы.Спикеры.ТекущиеДанные.ПредставлениеУчастника;
	РасшифровкаРедактор = СтрЗаменить(Элементы.Расшифровка.Документ.documentElement.outerHTML,
		ПредыдущееПредставлениеУчастника, НовоеПредставлениеУчастника);
	Элементы.Расшифровка.Документ.documentElement.outerHTML = РасшифровкаРедактор;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТаблицуСпикеровДляРедактирования(Перезаполнить = Ложь, ИзСервиса = Ложь)
	
	Если Перезаполнить Тогда
		Спикеры.Очистить();
	КонецЕсли;
	
	Для Каждого Спикер Из СпикерыИзСервиса Цикл
		
		Строка = Спикеры.Добавить();
		Строка.ИзРасшифровки = Спикер.initialName;
		
		Если ИзСервиса Тогда
			Если ЗначениеЗаполнено(Спикер.name) Тогда
				Строка.ПредставлениеУчастника = Спикер.name;
			Иначе
				Строка.ПредставлениеУчастника = Спикер.initialName;
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(Спикер.changedName) Тогда
				Строка.ПредставлениеУчастника = Спикер.changedName;
			ИначеЕсли ЗначениеЗаполнено(Спикер.name) Тогда
				Строка.ПредставлениеУчастника = Спикер.name;
			Иначе
				Строка.ПредставлениеУчастника = Спикер.initialName;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьРезультатРедактирования(ВерсияФайла, Расшифровка = "", Автопротокол = "", Знач Спикеры)
	
	Если Не ЗначениеЗаполнено(Расшифровка) И Не ЗначениеЗаполнено(Автопротокол) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаписиДанных = Таймлист.ПараметрыЗаписиДанныхРедактирования();
	
	Если ЗначениеЗаполнено(Расшифровка) Тогда
		ПараметрыЗаписиДанных.Расшифровка = Новый ХранилищеЗначения(Расшифровка);
		ПараметрыЗаписиДанных.Спикеры = Новый ХранилищеЗначения(Спикеры.Выгрузить());
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Автопротокол) Тогда
		ПараметрыЗаписиДанных.СледующиеШаги = СледующиеШаги(Автопротокол);
		ПараметрыЗаписиДанных.Автопротокол = Новый ХранилищеЗначения(Автопротокол);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Расшифровка) И ЗначениеЗаполнено(Автопротокол) Тогда
		РегистрыСведений.ТаймлистДанныеРаботыСервиса.ЗаписатьРезультатРедактирования(ВерсияФайла, ПараметрыЗаписиДанных);
	ИначеЕсли ЗначениеЗаполнено(Расшифровка) И Не ЗначениеЗаполнено(Автопротокол) Тогда
		РегистрыСведений.ТаймлистДанныеРаботыСервиса.ЗаписатьРезультатРедактирования(ВерсияФайла, ПараметрыЗаписиДанных);
	ИначеЕсли Не ЗначениеЗаполнено(Расшифровка) И ЗначениеЗаполнено(Автопротокол) Тогда
		РегистрыСведений.ТаймлистДанныеРаботыСервиса.ЗаписатьРезультатРедактирования(ВерсияФайла, ПараметрыЗаписиДанных);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьРедактирование(ВерсияФайла)
	
	РегистрыСведений.ТаймлистДанныеРаботыСервиса.ОтменитьРедактированиеДанных(ВерсияФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуБезСохранения()
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопросОЗакрытииФормы", ЭтотОбъект);
	
	Если Не Модифицированность Тогда
		Результат = Новый Структура("Значение", КодВозвратаДиалога.Да);
		ВыполнитьОбработкуОповещения(Оповещение, Результат);
	Иначе
		
		ТекстВопроса = НСтр("ru = 'Данные не будут сохранены. Продолжить?'");
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Да'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Нет'"));
		
		ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
		ПараметрыВопроса.Заголовок = "";
		ПараметрыВопроса.КнопкаПоУмолчанию = КодВозвратаДиалога.Отмена;
		ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
		ПараметрыВопроса.Картинка = БиблиотекаКартинок.ДиалогВопрос;
		
		СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(Оповещение, ТекстВопроса, Кнопки, ПараметрыВопроса);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросОЗакрытииФормы(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Или Результат.Значение <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	КлючЗакрытияФормы = "Отмена";
	ОтменитьРедактирование(ВерсияФайла);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Функция РедактируемыйЭлемент()
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРасшифровка Тогда
		Возврат Элементы.Расшифровка;
	Иначе
		Возврат Элементы.Автопротокол;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция СледующиеШаги(Автопротокол)
	
	АвтопротоколТекст = РаботаС_HTML.ПолучитьТекстИзHTML(Автопротокол);
	ЧастиАвтопротокола = СтрРазделить(АвтопротоколТекст, Символы.ПС);
	
	ПунктыАвтопротокола = Новый Массив;
	ЭтоНачалоПунктовПротокола = Ложь;
	
	Для Каждого Часть Из ЧастиАвтопротокола Цикл
		
		Если ЭтоНачалоПунктовПротокола Тогда
			
			РезультатПоиска = СтрНайтиПоРегулярномуВыражению(Часть, "((\d+\.)|(-)){1}(\s)*");
			Если РезультатПоиска.НачальнаяПозиция = 1 Тогда
				СодержаниеПункта = Сред(Часть, РезультатПоиска.Длина + 1);
				ПунктыАвтопротокола.Добавить(СодержаниеПункта);
			КонецЕсли;
			
		КонецЕсли;
		
		Если СтрНайти(Часть, "***Следующие шаги ***") Тогда
			ЭтоНачалоПунктовПротокола = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Результат = СтрСоединить(ПунктыАвтопротокола, ";");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
