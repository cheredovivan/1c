#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЕстьДоступныеШаблоныДокументов = Делопроизводство.ЕстьДоступныеШаблоныДокументов();
	
	ПрочитатьПараметрыРаспознаванияВФорму();
	ОбновитьПараметрыАвторизации();
	ОбновитьОтображениеИнформацииОбАвторизации();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НомерСтроки = 0;
	
	Для Каждого Стр Из СоответствиеВидовДокументов Цикл
		
		НомерСтроки = НомерСтроки+1;
		
		Если ЗначениеЗаполнено(Стр.ВидДокумента) И ЗначениеЗаполнено(Стр.ТипФормализованногоДокумента) Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Имеются не заполненные значения в строке %1'", ОбщегоНазначения.КодОсновногоЯзыка()), НомерСтроки);

		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , , , Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСоответствиеВидовДокументов

&НаКлиенте
Процедура ВидыРаспознаваемыхДокументовВидИТематикаСтрокойНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, 
		СтандартнаяОбработка)
	
	Если ЕстьДоступныеШаблоныДокументов Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗаголовокФормы", НСтр("ru = 'Выбор вида и тематики документа'"));

		ОписаниеОповещения = Новый ОписаниеОповещения("ВыборВидаДокументаПродолжение",
			ЭтотОбъект);
		РаботаСШаблонамиДокументовКлиент.ПоказатьФормуСозданияДокументаПоШаблону(
			ОписаниеОповещения, "ШаблоныДокументов", ПараметрыФормы);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			ДелопроизводствоКлиентСервер.Текст_НетШаблоновИлиДоступаКНим());
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВидыРаспознаваемыхДокументовВидИТематикаСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, 
		ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
		
	Если СтрДлина(Текст) < 2 Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;

	ДанныеВыбора = Делопроизводство.ДанныеВыбораВидаДокумента(Текст);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодключитьИнтернетПоддержку(Команда)
	
	ОбработчикПодключенияПоддержки = Новый ОписаниеОповещения(
		"ПослеПодключенияИнтернетПоддержки",
		ЭтотОбъект);
	
	ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(
		ОбработчикПодключенияПоддержки, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьСервисРаспознаванияДокументов(Команда)
	
	Если СостоянияПоБалансу.ПодключениеТестовогоПериодаДоступно Тогда
		ИдентификаторСервиса = РаспознаваниеКлиентСервер.ИдентификаторСервиса();
		Обработчик = Новый ОписаниеОповещения("ПослеПодключенияТестовогоПериода", ЭтотОбъект);
		ПодключениеСервисовСопровожденияКлиент.ПодключитьТестовыйПериод(ИдентификаторСервиса, ЭтотОбъект, Обработчик);
	Иначе
		ВыполнитьАвторизацию();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьСервисРаспознаванияДокументов(Команда)
	
	УдалитьДанныеАвторизации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПополнитьБаланс(Команда)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(РаспознаваниеКлиентСервер.АдресЛичногоКабинета());
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;
	
	ЗаписатьПараметрыРаспознавания();
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьВидИТематику(Результат)
	
	ТекущиеДанные = Элементы.СоответствиеВидовДокументов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Свойство("ШаблонДокумента") = Ложь Тогда
		Возврат;
	Иначе
		
		ТекущиеДанные.ВидДокумента = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			Результат.ШаблонДокумента, "ВидДокумента");
			
		ТекущиеДанные.Шаблон = Результат.ШаблонДокумента;
		
	КонецЕсли;
	
	Если Результат.Свойство("ТематикаДокумента") Тогда
		ТекущиеДанные.Тематика = Результат.ТематикаДокумента;
	КонецЕсли;
	
	ТекущиеДанные.ВидИТематикаСтрокой = СокрЛП(ТекущиеДанные.ВидДокумента);
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Тематика) Тогда 
		ТекущиеДанные.ВидИТематикаСтрокой = ВидИТематикаСтрокой(ТекущиеДанные.ВидДокумента, ТекущиеДанные.Тематика);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВидИТематикаСтрокой(ВидДокумента, Тематика = "")
	
	ВидИТематикаСтрокой = СокрЛП(ВидДокумента);
	Если ЗначениеЗаполнено(Тематика) Тогда 
		ВидИТематикаСтрокой = СтрШаблон("%1 - %2",
			СокрЛП(ВидДокумента), СокрЛП(Тематика));
	КонецЕсли;
	
	Возврат ВидИТематикаСтрокой;
	
КонецФункции

&НаСервере
Процедура УстановитьОтображениеВидаИТематикиДокумента()
	
	Для Каждого СтрокаСоответствия Из СоответствиеВидовДокументов Цикл
		
		СтрокаСоответствия.ВидИТематикаСтрокой = ВидИТематикаСтрокой(СтрокаСоответствия.ВидДокумента, 
			СтрокаСоответствия.Тематика);
			
	КонецЦикла;
	
КонецПроцедуры

// Читает параметры распознавания в форму.
//
&НаСервере
Процедура ПрочитатьПараметрыРаспознаванияВФорму()
	
	ПараметрыРаспознавания = РаспознаваниеДокументов.ПараметрыЗаполненияКарточекДокументов();
	АдресПараметровРаспознавания = ПоместитьВоВременноеХранилище(ПараметрыРаспознавания, УникальныйИдентификатор);
	
	СоответствиеВидовДокументов.Загрузить(ПараметрыРаспознавания.СоответствиеВидовДокументов);
	
	УстановитьОтображениеВидаИТематикиДокумента();
	
КонецПроцедуры

// Пишет параметры распознавания, установленные в форме.
//
&НаСервере
Процедура ЗаписатьПараметрыРаспознавания()
	
	ПараметрыРаспознавания = ПолучитьИзВременногоХранилища(АдресПараметровРаспознавания);
	
	ПараметрыРаспознавания.СоответствиеВидовДокументов = СоответствиеВидовДокументов.Выгрузить();
	
	РаспознаваниеДокументов.УстановитьПараметрыЗаполненияКарточекДокументов(ПараметрыРаспознавания);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПараметрыАвторизации()
	
	БазаПодключенаКИТС = 
		ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	
	БазаАвторизованаВСервисе = РаспознаваниеДокументов.ПодключеноКСервисуРаспознавания(Ложь);
	
	СостоянияПоБалансу = РаспознаваниеДокументов.СостоянияПоБалансу();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеИнформацииОбАвторизации()
	
	Если БазаАвторизованаВСервисе Тогда
		Элементы.ДекорацияСервисРаспознаванияПодключен.Заголовок = ОписаниеСостоянияСервисПодключен(СостоянияПоБалансу);
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСервисРаспознаванияДокументовПодключен;
	ИначеЕсли Не БазаПодключенаКИТС Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНеПодключенаИнтернетПоддержка;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНеПодключенСервисРаспознавания;
		Если СостоянияПоБалансу.ПодключениеТестовогоПериодаДоступно Тогда
			Элементы.ПодключитьСервисРаспознаванияДокументов.Заголовок = НСтр("ru = 'Подключить тестовый период'");
		Иначе
			Элементы.ПодключитьСервисРаспознаванияДокументов.Заголовок = НСтр("ru = 'Подключить'");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьАвторизацию()
	
	ОписаниеОшибки = "";
	Попытка
		РаспознаваниеДокументов.ВыполнитьАвторизациюПоТикетуИТС();
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	ОбновитьПараметрыАвторизации();
	ОбновитьОтображениеИнформацииОбАвторизации();
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДанныеАвторизации()
	
	РаспознаваниеДокументов.УдалитьДанныеАвторизации();
	ОбновитьПараметрыАвторизации();
	ОбновитьОтображениеИнформацииОбАвторизации();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеСостоянияСервисПодключен(СостоянияПоБалансу)
		
	ОписаниеПодключенногоСервиса = СтрШаблон(
		НСтр("ru = 'Сервис распознавания документов подключен. Осталось страниц: %1 %2'"),
		СостоянияПоБалансу.Баланс,
		?(ЗначениеЗаполнено(СостоянияПоБалансу.ДатаОкончания),
			СтрШаблон(НСтр("ru = 'до %1'"), Формат(СостоянияПоБалансу.ДатаОкончания, "ДФ=dd.MM.yyyy")),
			""));
	
	Возврат ОписаниеПодключенногоСервиса;
	
КонецФункции

&НаКлиенте
Процедура ПослеПодключенияТестовогоПериода(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = ПредопределенноеЗначение("Перечисление.СостоянияПодключенияСервисов.НеПодключен") Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьАвторизацию();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияИнтернетПоддержки(РезультатПодключения, ДополнительныеПараметры) Экспорт
	
	Если РезультатПодключения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьАвторизацию();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборВидаДокументаПродолжение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ЗаполнитьВидИТематику(Результат);
	
КонецПроцедуры

#КонецОбласти
