#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИзвлечьОбщиеМодули = Параметры.ИзвлечьОбщиеМодули;
	ИзвлечьМодулиОбъектов = Параметры.ИзвлечьМодулиОбъектов;
	ИзвлечьМодулиМенеджеров = Параметры.ИзвлечьМодулиМенеджеров;
	
	Если Не ПустаяСтрока(Параметры.АдресМакета) Тогда
		АдресМакета = Параметры.АдресМакета;
	Иначе
		Если ИзвлечьОбщиеМодули Тогда
			АдресМакета = РедакторВстроенногоЯзыка.АдресМакетаОбщиеМодули(УникальныйИдентификатор);
			
		ИначеЕсли ИзвлечьМодулиОбъектов Тогда
			АдресМакета = РедакторВстроенногоЯзыка.АдресМакетаМодулиОбъектов(УникальныйИдентификатор);
			
		ИначеЕсли ИзвлечьМодулиМенеджеров Тогда
			АдресМакета = РедакторВстроенногоЯзыка.АдресМакетаМодулиМенеджеров(УникальныйИдентификатор);
			
		Иначе
			Отказ = Истина;
			
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПустаяСтрока(Параметры.ПоясняющийТекстДлительнойОперации) Тогда
		ПоясняющийТекст = Параметры.ПоясняющийТекстДлительнойОперации;
	Иначе
		Если ИзвлечьОбщиеМодули Тогда
			ПоясняющийТекст = НСтр("ru = 'Выполняется начальная распаковка общих модулей.'");
			
		ИначеЕсли ИзвлечьМодулиОбъектов Тогда
			ПоясняющийТекст = НСтр("ru = 'Выполняется начальная распаковка модулей объектов.'");
			
		ИначеЕсли ИзвлечьМодулиМенеджеров Тогда
			ПоясняющийТекст = НСтр("ru = 'Выполняется начальная распаковка модулей менеджеров.'");
			
		Иначе
			Отказ = Истина;
			
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ДекорацияПоясняющийТекстДлительнойОперации.Заголовок = СтрШаблон(
		НСтр("ru = '%1
			|Операция может занять продолжительное время.
			|Пожалуйста, подождите...'"),
		ПоясняющийТекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ИзвлечьОбщиеМодули Тогда
		ПодключитьОбработчикОжидания("ИзвлечьОбщиеМодули", 0.1, Истина);
		
	ИначеЕсли ИзвлечьМодулиОбъектов Тогда
		ПодключитьОбработчикОжидания("ИзвлечьМодулиОбъектов", 0.1, Истина);
		
	ИначеЕсли ИзвлечьМодулиМенеджеров Тогда
		ПодключитьОбработчикОжидания("ИзвлечьМодулиМенеджеров", 0.1, Истина);
		
	Иначе
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзвлечьОбщиеМодули() Экспорт
	
	Подкаталог = РедакторВстроенногоЯзыкаКлиент.ВидМетаданных().ОбщиеМодули;
	Закрыть(РедакторВстроенногоЯзыкаКлиент.ИзвлечьАрхив(АдресМакета, Подкаталог));
	
КонецПроцедуры

&НаКлиенте
Процедура ИзвлечьМодулиОбъектов() Экспорт
	
	Подкаталог = РедакторВстроенногоЯзыкаКлиент.ВидМетаданных().МодулиОбъектов;
	Закрыть(РедакторВстроенногоЯзыкаКлиент.ИзвлечьАрхив(АдресМакета, Подкаталог));
	
КонецПроцедуры

&НаКлиенте
Процедура ИзвлечьМодулиМенеджеров() Экспорт
	
	Подкаталог = РедакторВстроенногоЯзыкаКлиент.ВидМетаданных().МодулиМенеджеров;
	Закрыть(РедакторВстроенногоЯзыкаКлиент.ИзвлечьАрхив(АдресМакета, Подкаталог));
	
КонецПроцедуры

#КонецОбласти