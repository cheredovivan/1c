
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)  
	
	Если Параметры.Свойство("РежимНастроек") Тогда
		Элементы.Напечатать.Заголовок = НСтр("ru = 'Записать'");
	КонецЕсли;	

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьШтрихкоды") Тогда
		Заголовок = НСтр("ru = 'Расположение регистрационного номера'");
		КлючСохраненияПоложенияОкна = "РасположениеРегистрационногоНомера";
	Иначе
		Заголовок = НСтр("ru = 'Расположение регистрационного номера и штрихкода'");				
		КлючСохраненияПоложенияОкна = "РасположениеРегистрационногоНомераИШтрихкода";
	КонецЕсли;	
	
	ИспользоватьШтрихкоды = ПолучитьФункциональнуюОпцию("ИспользоватьШтрихкоды"); 
	Если Не ИспользоватьШтрихкоды Тогда
		Элементы.ГруппаПараметрыИПримерШтрихкод.Видимость = Ложь;
	КонецЕсли;
	
	ОриентацияСтраницы = "Альбомная";
	
	#Область ЧтениеНастроекРегНомера
	НастройкиРегНомера = ШтрихкодированиеСервер.ПерсональныеНастройкиПоложенияРегНомера();
	
	ПоказыватьПриВставке = НастройкиРегНомера.ПоказыватьПриВставке;
	
	ПоложениеРегНомера = НастройкиРегНомера.ПоложениеНаСтранице;
	ПоГоризонталиРегНомер = НастройкиРегНомера.СмещениеПоГоризонтали;
	ПоВертикалиРегНомер = НастройкиРегНомера.СмещениеПоВертикали;
	
	СтраницаВставкиПеречисление = НастройкиРегНомера.СтраницаВставки;
	Элементы.СтраницаВставкиРегНомера.РежимВыбораИзСписка = Истина;
	Элементы.СтраницаВставкиРегНомера.СписокВыбора.Добавить(ПерваяСтраница(), НСтр("ru = 'Первая'"));
	Элементы.СтраницаВставкиРегНомера.СписокВыбора.Добавить(ПоследняяСтраница(), НСтр("ru = 'Последняя'"));
	Элементы.СтраницаВставкиРегНомера.СписокВыбора.Добавить(ПроизвольнаяСтраница(), НСтр("ru = 'Произвольная'"));
	Если СтраницаВставкиПеречисление = Перечисления.СтраницаВставкиКартинки.Последняя Тогда
		СтраницаВставкиРегНомера = ПоследняяСтраница();
	Иначе
		СтраницаВставкиРегНомера = ПерваяСтраница();
	КонецЕсли;
	НомерСтраницыРегНомера = 1; // Произвольная страница не сохраняется, т.к. документы все разные
	
	Если ПоложениеРегНомера <> Перечисления.МестаВставкиКартинки.ПроизвольноеПоложение Тогда
		Элементы.ПоГоризонтали.Доступность = Ложь;
		Элементы.ПоВертикали.Доступность = Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПоложениеРегНомера) Тогда
		ПоложениеРегНомера = Перечисления.МестаВставкиКартинки.ЛевыйВерхний;
	КонецЕсли;
	
	ПереключитьСтандартныеЗначенияНастроекРегНомера();
	#КонецОбласти
	
	
	#Область ЧтениеНастроекШтрихкода
	НастройкиШтрихкода = ШтрихкодированиеСервер.ПолучитьПерсональныеНастройкиПоложенияШтрихкодаНаСтранице();
	
	Если НастройкиШтрихкода <> Неопределено И НастройкиШтрихкода.ПоказыватьФормуНастройки <> Неопределено Тогда
		ПоложениеШтрихкода = НастройкиШтрихкода.ПоложениеНаСтранице;
		ПоГоризонтали = НастройкиШтрихкода.СмещениеПоГоризонтали;
		ПоВертикали = НастройкиШтрихкода.СмещениеПоВертикали;
	Иначе 
		ПоГоризонтали = "MIN";
		ПоВертикали = "MIN";
		ПоложениеШтрихкода = Перечисления.МестаВставкиКартинки.ПравыйВерхний;
	КонецЕсли;
	
	Если ПоложениеШтрихкода <> Перечисления.МестаВставкиКартинки.ПроизвольноеПоложение Тогда
		Элементы.ПоГоризонтали.Доступность = Ложь;
		Элементы.ПоВертикали.Доступность = Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПоложениеШтрихкода) Тогда
		ПоложениеШтрихкода = Перечисления.МестаВставкиКартинки.ПравыйВерхний;
	КонецЕсли;
	
	ВставлятьЦифрыВШК = ХранилищеОбщихНастроек.Загрузить("НастройкиШтрихкода", "ВставлятьЦифрыВШК");
	ВысотаШтрихкодаПриВставкеВФайл = ХранилищеОбщихНастроек.Загрузить("НастройкиШтрихкода", "ВысотаШтрихкодаПриВставкеВФайл");
	Если НЕ ЗначениеЗаполнено(ВысотаШтрихкодаПриВставкеВФайл)
		ИЛИ ВысотаШтрихкодаПриВставкеВФайл < 10 И НЕ ВставлятьЦифрыВШК
		ИЛИ ВысотаШтрихкодаПриВставкеВФайл < 13 И ВставлятьЦифрыВШК Тогда
		Если ВставлятьЦифрыВШК Тогда
			ВысотаШтрихкодаПриВставкеВФайл = 13;
		Иначе
			ВысотаШтрихкодаПриВставкеВФайл = 10;
		КонецЕсли;
	КонецЕсли;
	
	ПереключитьСтандартныеЗначенияНастроекШтрихкода();
	#КонецОбласти
	
	// Локализация
	ИспользоватьВнешнийСервисВставкиЭП = ВизуализацияПовтИсп.ИспользуетсяВнешнийСервисВставкиШтампаЭП();
	Если ИспользоватьВнешнийСервисВставкиЭП И СтраницаВставкиРегНомера = ПроизвольнаяСтраница() Тогда
		Элементы.СтраницаВставкиРегНомера.Подсказка 
			= НСтр("ru = 'При использовании 1С:Штамп вставка будет сделана на первую страницу'");
	Иначе	
		Элементы.СтраницаВставкиРегНомера.Подсказка = "";
	КонецЕсли;
	// Локализация Конец
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область ОбработчикиШтрихкод

&НаКлиенте
Процедура ДекорацияЛевоВерхНажатие(Элемент)
	
	ПоложениеШтрихкода = ПредопределенноеЗначение("Перечисление.МестаВставкиКартинки.ЛевыйВерхний");
	ПереключитьСтандартныеЗначенияНастроекШтрихкода();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПравоВерхНажатие(Элемент)
	
	ПоложениеШтрихкода = ПредопределенноеЗначение("Перечисление.МестаВставкиКартинки.ПравыйВерхний");
	ПереключитьСтандартныеЗначенияНастроекШтрихкода();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЛевоНизНажатие(Элемент)

	ПоложениеШтрихкода = ПредопределенноеЗначение("Перечисление.МестаВставкиКартинки.ЛевыйНижний");
	ПереключитьСтандартныеЗначенияНастроекШтрихкода();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПравоНизНажатие(Элемент)
	
	ПоложениеШтрихкода = ПредопределенноеЗначение("Перечисление.МестаВставкиКартинки.ПравыйНижний");
	ПереключитьСтандартныеЗначенияНастроекШтрихкода();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизвольноеПоложениеПриИзменении(Элемент)
	
	ПоложениеШтрихкода = ПредопределенноеЗначение("Перечисление.МестаВставкиКартинки.ПроизвольноеПоложение");
	ПереключитьСтандартныеЗначенияНастроекШтрихкода();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРегНомер

&НаКлиенте
Процедура ДекорацияЛевоВерхРегНомерНажатие(Элемент)
	
	ПоложениеРегНомера = ПредопределенноеЗначение("Перечисление.МестаВставкиКартинки.ЛевыйВерхний");
	ПереключитьСтандартныеЗначенияНастроекРегНомера();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПравоВерхРегНомерНажатие(Элемент)
	
	ПоложениеРегНомера = ПредопределенноеЗначение("Перечисление.МестаВставкиКартинки.ПравыйВерхний");
	ПереключитьСтандартныеЗначенияНастроекРегНомера();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЛевоНизРегНомерНажатие(Элемент)

	ПоложениеРегНомера = ПредопределенноеЗначение("Перечисление.МестаВставкиКартинки.ЛевыйНижний");
	ПереключитьСтандартныеЗначенияНастроекРегНомера();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПравоНизРегНомерНажатие(Элемент)
	
	ПоложениеРегНомера = ПредопределенноеЗначение("Перечисление.МестаВставкиКартинки.ПравыйНижний");
	ПереключитьСтандартныеЗначенияНастроекРегНомера();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизвольноеПоложениеРегНомерПриИзменении(Элемент)
	
	ПоложениеРегНомера = ПредопределенноеЗначение("Перечисление.МестаВставкиКартинки.ПроизвольноеПоложение");
	ПереключитьСтандартныеЗначенияНастроекРегНомера();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницаВставкиРегНомераПриИзменении(Элемент)

	// Локализация
	Если ИспользоватьВнешнийСервисВставкиЭП И СтраницаВставкиРегНомера = ПроизвольнаяСтраница() Тогда
		Элементы.СтраницаВставкиРегНомера.Подсказка 
			= НСтр("ru = 'При использовании 1С:Штамп вставка будет сделана на первую страницу'");
	Иначе
		Элементы.СтраницаВставкиРегНомера.Подсказка = "";
	КонецЕсли;
	// Локализация Конец
	
	Если Не ЗначениеЗаполнено(СтраницаВставкиРегНомера) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтраницаВставкиРегНомера = ПроизвольнаяСтраница() И НомерСтраницыРегНомера = 0 Тогда
		НомерСтраницыРегНомера = 1;
	КонецЕсли;
	ПереключитьСтандартныеЗначенияНастроекРегНомера();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерСтраницыРегНомераПриИзменении(Элемент)
	
	Если НомерСтраницыРегНомера = 0 Тогда
		НомерСтраницыРегНомера = 1;
	КонецЕсли;
	ПереключитьСтандартныеЗначенияНастроекРегНомера();
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если ИспользоватьШтрихкоды 
		И ПоложениеШтрихкода = ПоложениеРегНомера
		И ПоложениеРегНомера <> ПредопределенноеЗначение("Перечисление.МестаВставкиКартинки.ПроизвольноеПоложение")
		Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Укажите разное положение для регистрационного номера и штрихкода.'"));	
		Возврат;	
	КонецЕсли;	
	
	
	НастройкиШтрихкода = ШтрихкодированиеКлиентСервер.НовыеНастройкиШтрихкода();
	НастройкиШтрихкода.СмещениеПоГоризонтали = ПоГоризонтали;
	НастройкиШтрихкода.СмещениеПоВертикали = ПоВертикали;
	НастройкиШтрихкода.ПоложениеНаСтранице = ПоложениеШтрихкода;
	НастройкиШтрихкода.ПоказыватьФормуНастройки = Не НеПоказыватьФормуНастройки; 
	НастройкиШтрихкода.ВысотаШК = ВысотаШтрихкодаПриВставкеВФайл;
	НастройкиШтрихкода.ПоказыватьЦифры = ВставлятьЦифрыВШК;
	ШтрихкодированиеСервер.ЗаписатьПерсональныеНастройкиОкнаСвойствШтрихкода(НастройкиШтрихкода);
	
	
	НастройкиРегНомера = ШтрихкодированиеКлиентСервер.НовыеНастройкиРегНомера();
	НастройкиРегНомера.ПоложениеНаСтранице = ПоложениеРегНомера;
	НастройкиРегНомера.СмещениеПоГоризонтали = ПоГоризонталиРегНомер;
	НастройкиРегНомера.СмещениеПоВертикали = ПоВертикалиРегНомер;
	Если ЗначениеЗаполнено(СтраницаВставкиРегНомера) Тогда
		Если СтраницаВставкиРегНомера = ПоследняяСтраница() Тогда
			НастройкиРегНомера.СтраницаВставки = ПредопределенноеЗначение(
				"Перечисление.СтраницаВставкиКартинки.Последняя");
		ИначеЕсли СтраницаВставкиРегНомера = ПерваяСтраница() Тогда
			НастройкиРегНомера.СтраницаВставки = ПредопределенноеЗначение(
				"Перечисление.СтраницаВставкиКартинки.Первая");
		КонецЕсли;
	Иначе
		НастройкиРегНомера.СтраницаВставки = ПредопределенноеЗначение("Перечисление.СтраницаВставкиКартинки.Первая");
	КонецЕсли; 
	
	НастройкиРегНомера.ПоказыватьПриВставке = ПоказыватьПриВставке;
	ШтрихкодированиеСервер.ЗаписатьПерсональныеНастройкиПоложенияРегНомера(НастройкиРегНомера);
	
	Если СтраницаВставкиРегНомера = ПроизвольнаяСтраница() Тогда
		// НомерСтраницыРегНомера - в настройках пользователя не сохраняется, в файлах разное количество страниц.
		// Но в вызвавшую форму - нужно вернуть произвольную страницу:
		НастройкиРегНомера.СтраницаВставки = НомерСтраницыРегНомера;
	КонецЕсли;
	
	
	ДанныеВозврата = Новый Структура("НастройкиРегНомера, НастройкиШтрихкода", НастройкиРегНомера, НастройкиШтрихкода);
	
	Закрыть(ДанныеВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПереключитьСтандартныеЗначенияНастроекШтрихкода()
	
	Элементы.ПоГоризонтали.Доступность = Ложь;
	Элементы.ПоВертикали.Доступность = Ложь;
	Элементы.ПоГоризонтали.Маска = "";
	Элементы.ПоВертикали.Маска = "";
	Элементы.ГруппаСмещениеПоВертикалиШтрихкода.Доступность = Ложь;
	Элементы.ГруппаСмещениеПоГоризонталиШтрихкода.Доступность = Ложь;
	
	Если ПоложениеШтрихкода = Перечисления.МестаВставкиКартинки.ПравыйНижний Тогда
		СмещениеПоГоризонтали = "MAX";
		СмещениеПоВертикали = "MAX";
	ИначеЕсли ПоложениеШтрихкода = Перечисления.МестаВставкиКартинки.ПравыйВерхний Тогда
		СмещениеПоГоризонтали = "MAX";
		СмещениеПоВертикали = "MIN";
	ИначеЕсли ПоложениеШтрихкода = Перечисления.МестаВставкиКартинки.ЛевыйВерхний Тогда
		СмещениеПоГоризонтали = "MIN";
		СмещениеПоВертикали = "MIN";
	ИначеЕсли ПоложениеШтрихкода = Перечисления.МестаВставкиКартинки.ЛевыйНижний Тогда
		СмещениеПоГоризонтали = "MIN";
		СмещениеПоВертикали = "MAX";
	Иначе
		Элементы.ПоГоризонтали.Доступность = Истина;
		Элементы.ПоВертикали.Доступность = Истина;
		Элементы.ПоГоризонтали.Маска = "999";
		Элементы.ПоВертикали.Маска = "999";
		Элементы.ГруппаСмещениеПоВертикалиШтрихкода.Доступность = Истина;
		Элементы.ГруппаСмещениеПоГоризонталиШтрихкода.Доступность = Истина;
		СмещениеПоГоризонтали = "0";
		СмещениеПоВертикали = "0";
	КонецЕсли; 
	
	ПроизвольноеПоложение = (ПоложениеШтрихкода = Перечисления.МестаВставкиКартинки.ПроизвольноеПоложение);
	
	Элементы.ДекорацияЛевоВерх.Картинка = БиблиотекаКартинок.ШтрихкодЛевоВерх;

	Элементы.ДекорацияПравоВерх.Картинка = БиблиотекаКартинок.ШтрихкодПравоВерх;
	
	Элементы.ДекорацияЛевоНиз.Картинка = БиблиотекаКартинок.ШтрихкодЛевоНиз;
	
	Элементы.ДекорацияПравоНиз.Картинка = БиблиотекаКартинок.ШтрихкодПравоНиз;
	
	Если ПоложениеШтрихкода = Перечисления.МестаВставкиКартинки.ПравыйНижний Тогда
		
		Элементы.ДекорацияПравоНиз.Картинка = БиблиотекаКартинок.ШтрихкодПравоНизВыделен;
		
	ИначеЕсли ПоложениеШтрихкода = Перечисления.МестаВставкиКартинки.ПравыйВерхний Тогда
		
		Элементы.ДекорацияПравоВерх.Картинка = БиблиотекаКартинок.ШтрихкодПравоВерхВыделен;
		
	ИначеЕсли ПоложениеШтрихкода = Перечисления.МестаВставкиКартинки.ЛевыйВерхний Тогда
		
		Элементы.ДекорацияЛевоВерх.Картинка = БиблиотекаКартинок.ШтрихкодЛевоВерхВыделен;
		
	ИначеЕсли ПоложениеШтрихкода = Перечисления.МестаВставкиКартинки.ЛевыйНижний Тогда
		
		Элементы.ДекорацияЛевоНиз.Картинка = БиблиотекаКартинок.ШтрихкодЛевоНизВыделен;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПереключитьСтандартныеЗначенияНастроекРегНомера()
	
	Элементы.ПоГоризонталиРегНомер.Маска = "";
	Элементы.ПоВертикалиРегНомер.Маска = "";
	Элементы.ГруппаПоложениеРегНомера3.Доступность = Ложь;
	
	Элементы.НомерСтраницыРегНомера.Доступность = (СтраницаВставкиРегНомера = ПроизвольнаяСтраница());
	
	Если ПоложениеРегНомера = Перечисления.МестаВставкиКартинки.ПравыйНижний Тогда
		СмещениеПоГоризонталиРегНомер = "MAX";
		СмещениеПоВертикалиРегНомер = "MAX";
	ИначеЕсли ПоложениеРегНомера = Перечисления.МестаВставкиКартинки.ПравыйВерхний Тогда
		СмещениеПоГоризонталиРегНомер = "MAX";
		СмещениеПоВертикалиРегНомер = "MIN";
	ИначеЕсли ПоложениеРегНомера = Перечисления.МестаВставкиКартинки.ЛевыйВерхний Тогда
		СмещениеПоГоризонталиРегНомер = "MIN";
		СмещениеПоВертикалиРегНомер = "MIN";
	ИначеЕсли ПоложениеРегНомера = Перечисления.МестаВставкиКартинки.ЛевыйНижний Тогда
		СмещениеПоГоризонталиРегНомер = "MIN";
		СмещениеПоВертикалиРегНомер = "MAX";
	Иначе
		Элементы.ПоГоризонталиРегНомер.Маска = "999";
		Элементы.ПоВертикалиРегНомер.Маска = "999";
		Элементы.ГруппаПоложениеРегНомера3.Доступность = Истина;
		СмещениеПоГоризонталиРегНомер = "0";
		СмещениеПоВертикалиРегНомер = "0";
	КонецЕсли;
	
	ПроизвольноеПоложениеРегНомер = (ПоложениеРегНомера = Перечисления.МестаВставкиКартинки.ПроизвольноеПоложение);
	
	Элементы.ДекорацияЛевоВерхРегНомер.Картинка = БиблиотекаКартинок.РегномерЛевоВерх;
	Элементы.ДекорацияПравоВерхРегНомер.Картинка = БиблиотекаКартинок.РегномерПравоВерх;
	Элементы.ДекорацияЛевоНизРегНомер.Картинка = БиблиотекаКартинок.РегномерЛевоНиз;
	Элементы.ДекорацияПравоНизРегНомер.Картинка = БиблиотекаКартинок.РегномерПравоНиз;
	
	Если ПоложениеРегНомера = Перечисления.МестаВставкиКартинки.ПравыйНижний Тогда
		
		Элементы.ДекорацияПравоНизРегНомер.Картинка = БиблиотекаКартинок.РегномерПравоНизВыделен;
		
	ИначеЕсли ПоложениеРегНомера = Перечисления.МестаВставкиКартинки.ПравыйВерхний Тогда
		
		Элементы.ДекорацияПравоВерхРегНомер.Картинка = БиблиотекаКартинок.РегномерПравоВерхВыделен;
		
	ИначеЕсли ПоложениеРегНомера = Перечисления.МестаВставкиКартинки.ЛевыйВерхний Тогда
		
		Элементы.ДекорацияЛевоВерхРегНомер.Картинка = БиблиотекаКартинок.РегномерЛевоВерхВыделен;
		
	ИначеЕсли ПоложениеРегНомера = Перечисления.МестаВставкиКартинки.ЛевыйНижний Тогда
		
		Элементы.ДекорацияЛевоНизРегНомер.Картинка = БиблиотекаКартинок.РегномерЛевоНизВыделен;
		
	КонецЕсли; 
	

	// ДекорацияПоложениеРегНомера:
	СтраницаВставкиТекст = "" + Перечисления.СтраницаВставкиКартинки.Первая;
	Если СтраницаВставкиРегНомера = ПоследняяСтраница() Тогда
		СтраницаВставкиТекст = "" + Перечисления.СтраницаВставкиКартинки.Последняя;
	ИначеЕсли СтраницаВставкиРегНомера = ПроизвольнаяСтраница() Тогда
		Если НомерСтраницыРегНомера = 0 Тогда
			НомерСтраницыРегНомера = 1;
		КонецЕсли;
		СтраницаВставкиТекст = СтрШаблон(НСтр("ru = 'Страница %1'"), НомерСтраницыРегНомера);
	КонецЕсли;
	Элементы.ДекорацияПоложениеРегНомера.Заголовок = СтрШаблон(
		"%1,
		|%2", ПоложениеРегНомера, СтраницаВставкиТекст);
	
КонецПроцедуры

#Область КакБыКонстанты

&НаКлиентеНаСервереБезКонтекста
Функция ПерваяСтраница()
	
	Возврат "ПерваяСтраница";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПоследняяСтраница()
	
	Возврат "ПоследняяСтраница";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПроизвольнаяСтраница()
	
	Возврат "ПроизвольнаяСтраница";
	
КонецФункции

#КонецОбласти

#КонецОбласти
