#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Тематики.Видимость = РаботаСТематикамиДокументов.ТематикиИспользуются();
	Элементы.СписокВидыДокументовИндекс.Видимость = Нумерация.ИспользуетсяПолеНумерации("ИндексВидаДокумента");
	
	Если Параметры.Свойство("РежимВыбора") И Параметры.РежимВыбора Тогда 
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		Элементы.ГруппаРежимВыбора.Видимость = Истина;
		
		Элементы.СписокВидыДокументов.РежимВыбора = Истина;
		
		Если Параметры.Свойство("ТекущаяСтрока") И ЗначениеЗаполнено(Параметры.ТекущаяСтрока) Тогда 
			Если ТипЗнч(Параметры.ТекущаяСтрока) = Тип("СправочникСсылка.ВидыДокументов") Тогда 	
				Элементы.Списки.ТекущаяСтраница = Элементы.ГруппаСписокВидыДокументов;
				Элементы.СписокВидыДокументов.ТекущаяСтрока = Параметры.ТекущаяСтрока;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	НастройкиФормы = ОбщегоНазначения.ХранилищеСистемныхНастроекЗагрузить(ИмяФормы + "/ТекущиеДанные", "");
	Если НастройкиФормы = Неопределено Или НастройкиФормы.Получить("ПоказыватьУдаленные") = Неопределено Тогда
		ПоказатьУдаленные();
	КонецЕсли;
	
	Делопроизводство.СписокДокументовУсловноеОформлениеПомеченныхНаУдаление(СписокВидыДокументов);
	
	МассивКолонок = Новый Массив;
	МассивКолонок.Добавить("Тематики");
	МассивКолонок.Добавить("Шаблоны");
	МассивКолонок.Добавить("КартинкаСветофор");
	МассивКолонок.Добавить("ОбработкаЗадана");
	СписокВидыДокументов.УстановитьОграниченияИспользованияВПорядке(
		МассивКолонок);
		
	НастроитьФормуДляМобильногоПриНеобходимости();
	
	МультиязычностьСервер.ИзменениеТекстаЗапросаСпискаДляТекущегоЯзыка(
		ЭтотОбъект, "СписокВидыДокументов");
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Настройки["ПоказыватьУдаленные"] <> Неопределено Тогда
		ПоказатьУдаленные();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ТематикиПомеченыНаУдаление" Или ИмяСобытия = "ШаблонПомеченНаУдаление" 
		Или ИмяСобытия = "ТематикаДокумента_Создание" Или ИмяСобытия = "ИзмененШаблонДокумента" Тогда
		Элементы.СписокВидыДокументов.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписки

&НаКлиенте
Процедура СписокВидыДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = РаботаСоСпискамиДокументовКлиент.ПолучитьДанныеТекущейСтрокиСписка(
		Элементы.СписокВидыДокументов, Элементы.СписокВидыДокументов.ТекущаяСтрока);
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ЭтоГруппа Тогда 
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Ключ", ТекущиеДанные.Ссылка);
	
	Если Поле = Элементы.Тематики Тогда
		ПараметрыОткрытия.Вставить("ОткрытьЗакладкуТематики", Истина);
	КонецЕсли;
	
	Если Поле = Элементы.Шаблоны Тогда
		ПараметрыОткрытия.Вставить("ОткрытьЗакладкуШаблоны", Истина);
	КонецЕсли;
	
	Если Поле = Элементы.СписокВидыДокументовИндекс Тогда
		ПараметрыОткрытия.Вставить("АктивироватьИндекс", Истина);
	КонецЕсли;
	
	Если Поле = Элементы.ОбработкаЗадана Тогда
		ПараметрыОткрытия.Вставить("АктивироватьОбработку", Истина);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ВидыДокументов.ФормаОбъекта",
		ПараметрыОткрытия, Элементы.СписокВидыДокументов);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокВидыДокументовПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ИспользоватьТематикиДокументов = РаботаСТематикамиДокументов.ТематикиИспользуются();
	
	МассивВидов = Новый Массив;
	Для Каждого КлючИЗначение Из Строки Цикл 
		ДанныеСтроки = КлючИЗначение.Значение.Данные;
		МассивВидов.Добавить(ДанныеСтроки.Ссылка);
		Если ЗначениеЗаполнено(ДанныеСтроки.Родитель) Тогда 
			МассивВидов.Добавить(ДанныеСтроки.Родитель);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивВидов.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТематикиДокументов.ВидДокумента КАК ВидДокумента,
		|	КОЛИЧЕСТВО(ТематикиДокументов.Ссылка) КАК КоличествоТематик,
		|	0 КАК КоличествоШаблонов,
		|	0 КАК КоличествоДействий
		|ПОМЕСТИТЬ ОбщаяТаблица
		|ИЗ
		|	Справочник.ТематикиДокументов КАК ТематикиДокументов
		|ГДЕ
		|	НЕ ТематикиДокументов.ПометкаУдаления
		|	И НЕ ТематикиДокументов.НеДействует
		|	И ТематикиДокументов.ВидДокумента В(&ВидыДокументов)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТематикиДокументов.ВидДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ШаблоныДокументов.ВидДокумента,
		|	0,
		|	КОЛИЧЕСТВО(ШаблоныДокументов.Ссылка),
		|	0
		|ИЗ
		|	Справочник.ШаблоныДокументов КАК ШаблоныДокументов
		|ГДЕ
		|	НЕ ШаблоныДокументов.ПометкаУдаления
		|	И ШаблоныДокументов.ВидДокумента В(&ВидыДокументов)
		|
		|СГРУППИРОВАТЬ ПО
		|	ШаблоныДокументов.ВидДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НастройкиОбработкиВидовОбъектов.ВидОбъекта,
		|	0,
		|	0,
		|	КОЛИЧЕСТВО(ВидыДействийНастройки.ВидДействия)
		|ИЗ
		|	Справочник.НастройкиОбработкиВидовОбъектов КАК НастройкиОбработкиВидовОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиОбработкиВидовОбъектов.ВидыДействий КАК ВидыДействийНастройки
		|		ПО НастройкиОбработкиВидовОбъектов.Ссылка = ВидыДействийНастройки.Ссылка
		|ГДЕ
		|	НастройкиОбработкиВидовОбъектов.ВидОбъекта В(&ВидыДокументов)
		|	И НастройкиОбработкиВидовОбъектов.ПометкаУдаления = ЛОЖЬ
		|	И НастройкиОбработкиВидовОбъектов.ДействуетС <= &ДатаОбработки
		|	И (НастройкиОбработкиВидовОбъектов.ДействуетПо = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ КОНЕЦПЕРИОДА(НастройкиОбработкиВидовОбъектов.ДействуетПо, ДЕНЬ) >= &ДатаОбработкиПо)
		|
		|СГРУППИРОВАТЬ ПО
		|	НастройкиОбработкиВидовОбъектов.ВидОбъекта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбщаяТаблица.ВидДокумента КАК ВидДокумента,
		|	СУММА(ОбщаяТаблица.КоличествоТематик) КАК КоличествоТематик,
		|	СУММА(ОбщаяТаблица.КоличествоШаблонов) КАК КоличествоШаблонов,
		|	СУММА(ОбщаяТаблица.КоличествоДействий) КАК КоличествоДействий
		|ИЗ
		|	ОбщаяТаблица КАК ОбщаяТаблица
		|
		|СГРУППИРОВАТЬ ПО
		|	ОбщаяТаблица.ВидДокумента";
	
	Запрос.Параметры.Вставить("ВидыДокументов", МассивВидов);
	Запрос.Параметры.Вставить("ДатаОбработки", ТекущаяДатаСеанса());
	Запрос.Параметры.Вставить("ДатаОбработкиПо", КонецДня(ТекущаяДатаСеанса()));
	ТематикиИШаблоныВидов = Запрос.Выполнить().Выгрузить();
	
	Для Каждого КлючИЗначение Из Строки Цикл 
		ДанныеСтроки = КлючИЗначение.Значение.Данные;
		
		СветофорТематик = 3; СветофорШаблонов = 3; СветофорОбработки = 3;
		
		СтрПоиска = ТематикиИШаблоныВидов.НайтиСтроки(Новый Структура("ВидДокумента", ДанныеСтроки.Ссылка));
		Если ИспользоватьТематикиДокументов И ДанныеСтроки.ВестиУчетПоТематикам Тогда 
			Если СтрПоиска.Количество() > 0 И СтрПоиска[0].КоличествоТематик > 0 Тогда 
				
				ДанныеСтроки.Тематики = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
					НСтр("ru = ';%1 тематика;;%1 тематики;%1 тематик;%1 тематика'"),
					СтрПоиска[0].КоличествоТематик);
					
				ДанныеСтроки.КоличествоТематик = СтрПоиска[0].КоличествоТематик;
			Иначе 
				ДанныеСтроки.Тематики = НСтр("ru = 'Не заданы'");
				ДанныеСтроки.КоличествоТематик = 0;
				СветофорТематик = 1;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрПоиска.Количество() > 0 И СтрПоиска[0].КоличествоШаблонов > 0 Тогда
			
			ДанныеСтроки.Шаблоны = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = ';%1 шаблон;;%1 шаблона;%1 шаблонов;%1 шаблона'"),
				СтрПоиска[0].КоличествоШаблонов);
			 
			ДанныеСтроки.КоличествоШаблонов = СтрПоиска[0].КоличествоШаблонов;
		ИначеЕсли ДанныеСтроки.ЗапретитьСозданиеДокументовНеПоШаблону Тогда 
			ДанныеСтроки.Шаблоны = НСтр("ru = 'Не заданы'");
			ДанныеСтроки.КоличествоШаблонов = 0;
			СветофорШаблонов = 2;
		КонецЕсли;
		
		ОбработкаЗадана = Ложь;
		Если СтрПоиска.Количество() > 0 И СтрПоиска[0].КоличествоДействий > 0 Тогда 
			ОбработкаЗадана = Истина;
		Иначе
			СтрПоискаРодителя = ТематикиИШаблоныВидов.НайтиСтроки(
				Новый Структура("ВидДокумента", ДанныеСтроки.Родитель));
			
			Если СтрПоискаРодителя.Количество() > 0 И СтрПоискаРодителя[0].КоличествоДействий > 0 Тогда 
				ОбработкаЗадана = Истина;
			Иначе
				СветофорОбработки = 2;
			КонецЕсли;
		КонецЕсли;
		
		Если ДанныеСтроки.Свойство("ОбработкаЗадана") Тогда
			ДанныеСтроки.ОбработкаЗадана = ОбработкаЗадана;
		КонецЕсли;
				
		Если ДанныеСтроки.ЭтоГруппа Тогда 
			ДанныеСтроки.КартинкаСветофор = 0;
		Иначе 
			ДанныеСтроки.КартинкаСветофор = Мин(СветофорТематик, СветофорШаблонов, СветофорОбработки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВидыДокументовОбработкаЗапросаОбновления(Элемент)
	
	Элементы.СписокВидыДокументов.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	 
	ОповеститьОВыборе(Элементы.СписокВидыДокументов.ТекущаяСтрока);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	Элементы.СписокВидыДокументовПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	
	Если Не ПоказыватьУдаленные Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокВидыДокументов, "ПометкаУдаления", Ложь,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
	Иначе		
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокВидыДокументов, "ПометкаУдаления");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПоказатьУдаленные()
	
	Элементы.СписокВидыДокументовПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	
	Если Не ПоказыватьУдаленные Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокВидыДокументов, "ПометкаУдаления", Ложь,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
	Иначе		
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокВидыДокументов, "ПометкаУдаления");
	КонецЕсли;
		
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции_МобильныйКлиент

&НаСервере
Процедура НастроитьФормуДляМобильногоПриНеобходимости()
	
	Если Не ПараметрыСеанса.ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	// Список.
	Элементы.СписокВидыДокументов.Шапка = Ложь;
	
	Элементы.Тематики.Видимость = Ложь;
	Элементы.ОбработкаЗадана.Видимость = Ложь;
	Элементы.СписокВидыДокументовЗапретитьСозданиеДокументовНеПоШаблону.Видимость = Ложь;
	Элементы.СписокВидыДокументовИндекс.Видимость = Ложь;
	Элементы.СписокВидыДокументовФормаДокумента.Видимость = Ложь;
	Элементы.ЯвляетсяДоговоромВн.Видимость = Ложь;
	Элементы.СписокВидыДокументовУчитыватьСуммуДокумента.Видимость = Ложь;
	Элементы.СписокВидыДокументовУчитыватьКорреспонденцию.Видимость = Ложь;
	Элементы.КомментарийВн.Видимость = Ложь;
	Элементы.СрокИсполненияВн.Видимость = Ложь;
	Элементы.ИспользоватьСрокИсполненияВн.Видимость = Ложь;
	Элементы.ВестиУчетПоНоменклатуреДелВн.Видимость = Ложь;
	Элементы.ВестиУчетПоКонтрагентамВн.Видимость = Ложь;
	Элементы.УчитыватьСрокДействияВн.Видимость = Ложь;
	Элементы.УчитыватьСуммуДокументаВн.Видимость = Ложь;
	Элементы.УчитыватьНедействующиеДокументыВн.Видимость = Ложь;
	Элементы.ЯвляетсяКомплектомДокументовВн.Видимость = Ложь;
	Элементы.СсылкаВн.Видимость = Ложь;

КонецПроцедуры

#КонецОбласти

#КонецОбласти
