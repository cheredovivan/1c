
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриСозданииНаСервереРедакцииКонфигурации();
	
	НастроитьВидПросмотра();
	
	СписокКонтактныеЛица.Параметры.УстановитьЗначениеПараметра("Контрагент", Элементы.Контрагенты.ТекущаяСтрока);
	
	ПоказыватьУдаленные = ХранилищеСистемныхНастроек.Загрузить(ИмяФормы, "ПоказыватьУдаленные");
	Если ПоказыватьУдаленные = Неопределено Тогда
		ПоказыватьУдаленные = Ложь;
	КонецЕсли;
	
	Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	
	Если Не ПоказыватьУдаленные Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СтруктураПредприятия, "ПометкаУдаления", Ложь,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Контрагенты, "ПометкаУдаления", Ложь,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокКонтактныеЛица, "ПометкаУдаления", Ложь,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(СтруктураПредприятия,
			"ПометкаУдаления");
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Контрагенты,
			"ПометкаУдаления");
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокКонтактныеЛица,
			"ПометкаУдаления");
	КонецЕсли;

	ОтобразитьИерархию();
	
	ТекущаяСтрока = Параметры.ТекущаяСтрока;
	Если ЗначениеЗаполнено(ТекущаяСтрока) Тогда
		УстановитьПозициюНаИсполнителя(ТекущаяСтрока);
	КонецЕсли;
	
	НастроитьФормуДляМобильногоПриНеобходимости();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидПросмотраПриИзменении(Элемент)
	
	#Если МобильныйКлиент Тогда
		Элементы.ГруппировкиМК.Видимость = ВидПросмотра <> НСтр("ru = 'Списком'");
		Элементы.ГруппировкиМК.Заголовок = Элементы.ГруппировкиМК.Подсказка; 
	#КонецЕсли
	
	Если ВидПросмотра = НСтр("ru = 'Списком'") Тогда
		ТекущаяРабочаяГруппа = Неопределено;
		ТекущееПодразделение = Неопределено;
	
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
		ТекущаяРабочаяГруппа = Неопределено;
		
		Если ЗначениеЗаполнено(Элементы.СписокПользователи.ТекущаяСтрока) Тогда
			ВрПодразделение = СписокПользователи.НайтиПоИдентификатору(Элементы.СписокПользователи.ТекущаяСтрока).Подразделение;
		Иначе
			ВрПодразделение = Неопределено;
		КонецЕсли;
		Элементы.СтруктураПредприятия.ТекущаяСтрока = ВрПодразделение;
		ТекущееПодразделение = Элементы.СтруктураПредприятия.ТекущаяСтрока;

	ИначеЕсли ВидПросмотра = НСтр("ru = 'По рабочим группам'") Тогда
		ТекущееПодразделение = Неопределено;
		
		Элементы.РабочиеГруппы.ТекущаяСтрока = ПредопределенноеЗначение("Справочник.РабочиеГруппы.ВсеПользователи");
		ТекущаяРабочаяГруппа = Элементы.РабочиеГруппы.ТекущаяСтрока;
	Иначе
		ВызватьИсключение "Некорректный вид просмотра";
	КонецЕсли;
	
	ОтобразитьИерархию();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСтруктураПредприятия

&НаКлиенте
Процедура СтруктураПредприятияПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработкаОжиданияПользователи", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктураПредприятияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	#Если МобильныйКлиент Тогда
		СтандартнаяОбработка = Ложь;
		ТекущиеДанные = Элемент.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		Элементы.ГруппировкиМК.Заголовок = СтрШаблон("Группировка: %1", Строка(ТекущиеДанные.Наименование));
		Элементы.ГруппировкиМК.Скрыть();
	#КонецЕсли

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокПользователи

&НаКлиенте
Процедура СписокПользователиВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(Элемент.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователиПередНачаломИзменения(Элемент, Отказ)
	
	ПоказатьЗначение(, Элементы.СписокПользователи.ТекущиеДанные.Ссылка);
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокКонтактныеЛица

&НаКлиенте
Процедура СписокКонтактныеЛицаВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокКонтактныеЛицаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Копирование Тогда 
		Возврат;
	КонецЕсли;	
	
	ТекущиеДанные = Элементы.Контрагенты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;	
	
	Отказ = Истина;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Владелец", ТекущиеДанные.Ссылка);

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
	ОткрытьФорму("Справочник.КонтактныеЛица.ФормаОбъекта", ПараметрыФормы, Элемент);	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКонтрагенты

&НаКлиенте
Процедура КонтрагентыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.Контрагенты.ТекущаяСтрока <> Неопределено
		И Элементы.Контрагенты.ТекущиеДанные.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо") Тогда 
		СтандартнаяОбработка = Ложь;
		ОповеститьОВыборе(Элементы.Контрагенты.ТекущаяСтрока);
	Иначе
		#Если МобильныйКлиент Тогда
			СтандартнаяОбработка = Ложь;			
		#КонецЕсли
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработкаОжиданияКонтактныеЛица", 0.2, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРабочиеГруппы

&НаКлиенте
Процедура РабочиеГруппыПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработкаОжиданияПользователи", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура РабочиеГруппыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	#Если МобильныйКлиент Тогда
		СтандартнаяОбработка = Ложь;
		ТекущиеДанные = Элемент.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		Элементы.ГруппировкиМК.Заголовок = СтрШаблон("Группировка: %1", Строка(ТекущиеДанные.Наименование));
		Элементы.ГруппировкиМК.Скрыть();
	#КонецЕсли

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаПользователи Тогда 
		Если Элементы.СписокПользователи.ТекущаяСтрока = Неопределено Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Не выбран сотрудник'"));
			Возврат;
		КонецЕсли;	
			
		ОповеститьОВыборе(Элементы.СписокПользователи.ТекущиеДанные.Ссылка);
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаКонтрагенты Тогда 
		Если Элементы.Контрагенты.ТекущаяСтрока <> Неопределено Тогда 
			Если Элементы.Контрагенты.ТекущиеДанные.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо") Тогда 
				ОповеститьОВыборе(Элементы.Контрагенты.ТекущаяСтрока);
			ИначеЕсли Элементы.СписокКонтактныеЛица.ТекущаяСтрока = Неопределено Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Не выбрано контактное лицо'"));
				Возврат;
			КонецЕсли;
		ИначеЕсли Элементы.СписокКонтактныеЛица.ТекущаяСтрока = Неопределено Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Не выбрано контактное лицо'"));
			Возврат;
		КонецЕсли;	
		
		ОповеститьОВыборе(Элементы.СписокКонтактныеЛица.ТекущаяСтрока);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	
	Если Не ПоказыватьУдаленные Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СтруктураПредприятия, "ПометкаУдаления", Ложь,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Контрагенты, "ПометкаУдаления", Ложь,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокКонтактныеЛица, "ПометкаУдаления", Ложь,
			ВидСравненияКомпоновкиДанных.Равно, , Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(СтруктураПредприятия,
			"ПометкаУдаления");
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Контрагенты,
			"ПометкаУдаления");
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокКонтактныеЛица,
			"ПометкаУдаления");
	КонецЕсли;
	
	ОтобразитьИерархию();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаОжиданияПользователи()
	
	Если ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
		
		Если Элементы.СтруктураПредприятия.ТекущаяСтрока <> Неопределено Тогда 
			Если ТекущееПодразделение <> Элементы.СтруктураПредприятия.ТекущаяСтрока Тогда
				ТекущееПодразделение = Элементы.СтруктураПредприятия.ТекущаяСтрока;
				ЗаполнитьПользователей();
			КонецЕсли;
		КонецЕсли;	
		
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По рабочим группам'") Тогда
		
		Если Элементы.РабочиеГруппы.ТекущаяСтрока <> Неопределено Тогда 
			Если ТекущаяРабочаяГруппа <> Элементы.РабочиеГруппы.ТекущаяСтрока Тогда
				ТекущаяРабочаяГруппа = Элементы.РабочиеГруппы.ТекущаяСтрока;
				ЗаполнитьПользователей();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОжиданияКонтактныеЛица()
	
	Если Элементы.Контрагенты.ТекущаяСтрока <> Неопределено Тогда 
		СписокКонтактныеЛица.Параметры.УстановитьЗначениеПараметра("Контрагент",
			Элементы.Контрагенты.ТекущаяСтрока);
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура НастроитьВидПросмотра()
	
	Элементы.ВидПросмотра.СписокВыбора.Очистить();
	Элементы.ВидПросмотра.СписокВыбора.Добавить(НСтр("ru = 'Списком'"));
	Элементы.ВидПросмотра.СписокВыбора.Добавить(НСтр("ru = 'По подразделениям'"));
	Элементы.ВидПросмотра.СписокВыбора.Добавить(НСтр("ru = 'По рабочим группам'"));
	
	ВидПросмотра = ХранилищеСистемныхНастроек.Загрузить(ИмяФормы, "ВидПросмотра");
	Если Элементы.ВидПросмотра.СписокВыбора.НайтиПоЗначению(ВидПросмотра) = Неопределено Тогда
		ВидПросмотра = НСтр("ru = 'По подразделениям'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьИерархию()
	
	
	Если ПустаяСтрока(ВидПросмотра) Тогда
		ВидПросмотра = НСтр("ru = 'По подразделениям'");
	КонецЕсли;
	
	Если Не ПараметрыСеанса.ЭтоМобильныйКлиент Тогда
		Если ВидПросмотра = НСтр("ru = 'Списком'") Тогда
			Элементы.СтраницыГруппировкаПользователей.Видимость = Ложь;
		Иначе
			Элементы.СтраницыГруппировкаПользователей.Видимость = Истина;
			Если ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
				Элементы.СтраницыГруппировкаПользователей.ТекущаяСтраница = Элементы.СтраницаСтруктураПредприятия;
			ИначеЕсли ВидПросмотра = НСтр("ru = 'По рабочим группам'") Тогда
				Элементы.СтраницыГруппировкаПользователей.ТекущаяСтраница = Элементы.СтраницаРабочиеГруппы;
			Иначе
				ВызватьИсключение НСтр("ru = 'Некорректный вид просмотра'");
			КонецЕсли;
		КонецЕсли;
	Иначе
		Элементы.РабочиеГруппы.Видимость = (ВидПросмотра = НСтр("ru = 'По рабочим группам'"));
		Элементы.СтруктураПредприятия.Видимость = (ВидПросмотра = НСтр("ru = 'По подразделениям'"));
	КонецЕсли;
		
	ЗаполнитьПользователей();
	ХранилищеСистемныхНастроек.Сохранить(ИмяФормы, "ВидПросмотра", ВидПросмотра);
	ХранилищеСистемныхНастроек.Сохранить(ИмяФормы, "ПоказыватьУдаленные", ПоказыватьУдаленные);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПользователей()
	
	Если ВидПросмотра = НСтр("ru = 'Списком'") Тогда
		ЗаполнитьПользователейСписком();
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По рабочим группам'") Тогда	
		ЗаполнитьПользователейВГруппе();
	Иначе
		ЗаполнитьПользователейВОтделе();
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьПользователейСписком()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Сотрудники.Ссылка КАК Ссылка,
		|	Сотрудники.ПометкаУдаления КАК ПометкаУдаления,
		|	Сотрудники.Представление КАК Наименование,
		|	Сотрудники.Должность КАК Должность,
		|	Сотрудники.Подразделение КАК Подразделение,
		|	ИСТИНА В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			Справочник.СтруктураПредприятия КАК Руководители
		|		ГДЕ
		|			Руководители.Руководитель = Сотрудники.Ссылка
		|			И Руководители.ПометкаУдаления = ЛОЖЬ) КАК ЭтоРуководитель,
		|	ЛОЖЬ КАК ЭтоРоль,
		|	Сотрудники.Предопределенный КАК Предопределенный
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Действует
		|	И &УсловиеПометкиУдаления
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&УсловиеПометкиУдаления",
		?(Не ПоказыватьУдаленные, "Не Сотрудники.ПометкаУдаления", "Истина"));
		
	Результат = Запрос.Выполнить();
	СписокВыгрузки = Результат.Выгрузить();
	
	ТекПользователь = Неопределено;
	ТекущаяСтрока = Элементы.СписокПользователи.ТекущаяСтрока;
	Если ТекущаяСтрока <> Неопределено Тогда
		Если ТипЗнч(ТекущаяСтрока) = Тип("СправочникСсылка.Пользователи")
			Или ТипЗнч(ТекущаяСтрока) = Тип("СправочникСсылка.Сотрудники") Тогда 
			ТекПользователь = ТекущаяСтрока;
		Иначе 
			ТекПользователь = СписокПользователи.НайтиПоИдентификатору(ТекущаяСтрока).Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	СписокПользователи.Очистить();
	СписокПользователи.Загрузить(СписокВыгрузки);
	Если ЗначениеЗаполнено(ТекПользователь) Тогда
		МассивСтрок = СписокПользователи.НайтиСтроки(Новый Структура("Ссылка", ТекПользователь));
		Если МассивСтрок.Количество() > 0 Тогда
			Элементы.СписокПользователи.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
				   
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьПользователейВГруппе()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Сотрудники.Ссылка КАК Ссылка,
		|	Сотрудники.ПометкаУдаления КАК ПометкаУдаления,
		|	Сотрудники.Предопределенный КАК Предопределенный,
		|	Сотрудники.Представление КАК Наименование,
		|	Сотрудники.Должность КАК Должность,
		|	Сотрудники.Подразделение КАК Подразделение,
		|	ИСТИНА В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			Справочник.СтруктураПредприятия КАК Руководители
		|		ГДЕ
		|			Руководители.Руководитель = Сотрудники.Ссылка
		|			И Руководители.ПометкаУдаления = ЛОЖЬ) КАК ЭтоРуководитель,
		|	ЛОЖЬ КАК ЭтоРоль
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РабочиеГруппы.Состав КАК РабочиеГруппыСостав
		|		ПО РабочиеГруппыСостав.Участник = Сотрудники.Ссылка
		|ГДЕ
		|	(НЕ &ИспользоватьИерархию
		|	ИЛИ Сотрудники.Подразделение = &Подразделение)
		|	И (НЕ &ИспользоватьГруппыПользователей
		|	ИЛИ &РабочаяГруппа = ЗНАЧЕНИЕ(Справочник.РабочиеГруппы.ВсеПользователи)
		|	ИЛИ РабочиеГруппыСостав.Ссылка = &РабочаяГруппа)
		|	И Сотрудники.Действует
		|	И &УсловиеПометкиУдаления
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&УсловиеПометкиУдаления",
		?(Не ПоказыватьУдаленные, "Не Сотрудники.ПометкаУдаления", "Истина"));
		
	Запрос.УстановитьПараметр("Подразделение", ТекущееПодразделение);
	Запрос.УстановитьПараметр("РабочаяГруппа", ТекущаяРабочаяГруппа);
	Запрос.УстановитьПараметр("ИспользоватьИерархию", Ложь);
	Запрос.УстановитьПараметр("ИспользоватьГруппыПользователей", Истина);
		
	Результат = Запрос.Выполнить();
	СписокВыгрузки = Результат.Выгрузить();
	
	ТекущаяСтрока = Элементы.СписокПользователи.ТекущаяСтрока;
	Если ТекущаяСтрока <> Неопределено Тогда
		Если ТипЗнч(ТекущаяСтрока) = Тип("СправочникСсылка.Пользователи")
			Или ТипЗнч(ТекущаяСтрока) = Тип("СправочникСсылка.Сотрудники") Тогда
			ТекПользователь = ТекущаяСтрока;
		Иначе 
			ТекПользователь = СписокПользователи.НайтиПоИдентификатору(ТекущаяСтрока).Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	СписокПользователи.Очистить();
	СписокПользователи.Загрузить(СписокВыгрузки);
	Если ЗначениеЗаполнено(ТекПользователь) Тогда
		МассивСтрок = СписокПользователи.НайтиСтроки(Новый Структура("Ссылка", ТекПользователь));
		Если МассивСтрок.Количество() > 0 Тогда
			Элементы.СписокПользователи.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
				   
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьПользователейВОтделе()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Сотрудники.Ссылка КАК Ссылка,
		|	Сотрудники.ПометкаУдаления КАК ПометкаУдаления,
		|	Сотрудники.Предопределенный КАК Предопределенный,
		|	Сотрудники.Представление КАК Наименование,
		|	Сотрудники.Должность КАК Должность,
		|	Сотрудники.Подразделение КАК Подразделение,
		|	ВЫБОР
		|		КОГДА Сотрудники.Ссылка = Сотрудники.Подразделение.Руководитель
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоРуководитель,
		|	ЛОЖЬ КАК ЭтоРоль
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РабочиеГруппы.Состав КАК РабочиеГруппыСостав
		|		ПО (РабочиеГруппыСостав.Участник = Сотрудники.Ссылка)
		|ГДЕ
		|	(НЕ &ИспользоватьИерархию
		|	ИЛИ Сотрудники.Подразделение = &Подразделение)
		|	И (НЕ &ИспользоватьГруппыПользователей
		|	ИЛИ &РабочаяГруппа = ЗНАЧЕНИЕ(Справочник.РабочиеГруппы.ВсеПользователи)
		|	ИЛИ РабочиеГруппыСостав.Ссылка = &РабочаяГруппа)
		|	И Сотрудники.Действует
		|	И &УсловиеПометкиУдаления
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&УсловиеПометкиУдаления",
		?(Не ПоказыватьУдаленные, "Не Сотрудники.ПометкаУдаления", "Истина"));
		
	Запрос.УстановитьПараметр("Подразделение", ТекущееПодразделение);
	Запрос.УстановитьПараметр("РабочаяГруппа", ТекущаяРабочаяГруппа);
	Запрос.УстановитьПараметр("ПоказыватьРоли", Ложь);
	
	Если ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
		Запрос.УстановитьПараметр("ИспользоватьИерархию", Истина);
		Запрос.УстановитьПараметр("ИспользоватьГруппыПользователей", Ложь);
	Иначе
		ВызватьИсключение "Некорректный вид просмотра";
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	СписокВыгрузки = Результат.Выгрузить();
	
	ТекущаяСтрока = Элементы.СписокПользователи.ТекущаяСтрока;
	Если ТекущаяСтрока <> Неопределено Тогда
		Если ТипЗнч(ТекущаяСтрока) = Тип("СправочникСсылка.Пользователи") Тогда 
			ТекПользователь = ТекущаяСтрока;
		Иначе 
			ТекПользователь = СписокПользователи.НайтиПоИдентификатору(ТекущаяСтрока).Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	СписокПользователи.Очистить();
	СписокПользователи.Загрузить(СписокВыгрузки);
	Если ЗначениеЗаполнено(ТекПользователь) Тогда
		МассивСтрок = СписокПользователи.НайтиСтроки(Новый Структура("Ссылка", ТекПользователь));
		Если МассивСтрок.Количество() > 0 Тогда
			Элементы.СписокПользователи.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
				   
КонецПроцедуры	

&НаСервере
Процедура УстановитьПозициюНаИсполнителя(Знач Исполнитель)
	
	Если ТипЗнч(Исполнитель) = Тип("СправочникСсылка.Пользователи") Тогда
		Исполнитель = Сотрудники.ОсновнойСотрудникПользователя(Исполнитель);
	КонецЕсли;
	
	Если ТипЗнч(Исполнитель) = Тип("СправочникСсылка.Сотрудники") Тогда
		
		Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаПользователи;
		
		Если ВидПросмотра = НСтр("ru = 'Списком'") Тогда 
			МассивСтрок = СписокПользователи.НайтиСтроки(Новый Структура("Ссылка", Исполнитель));
			Если МассивСтрок.Количество() = 1 Тогда
				Элементы.СписокПользователи.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
			КонецЕсли;
		Иначе
			Подразделение = РаботаСПользователями.ПолучитьПодразделение(Исполнитель);
			
			Если Не Подразделение.Пустая() Тогда
				ВидПросмотра = НСтр("ru = 'По подразделениям'");
				Элементы.СтруктураПредприятия.ТекущаяСтрока = Подразделение;
				ТекущееПодразделение = Элементы.СтруктураПредприятия.ТекущаяСтрока;
				ОтобразитьИерархию();
				
				МассивСтрок = СписокПользователи.НайтиСтроки(Новый Структура("Ссылка", Исполнитель));
				Если МассивСтрок.Количество() = 1 Тогда
					Элементы.СписокПользователи.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Исполнитель) = Тип("СправочникСсылка.КонтактныеЛица") Тогда 
		
		Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаКонтрагенты;
		Владелец = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Исполнитель, "Владелец");
		Элементы.Контрагенты.ТекущаяСтрока = Владелец;
		СписокКонтактныеЛица.Параметры.УстановитьЗначениеПараметра("Контрагент", Владелец);
		Элементы.СписокКонтактныеЛица.ТекущаяСтрока = Исполнитель;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервереРедакцииКонфигурации()
	
	ЭтаФорма.Заголовок = СтрШаблон(
		НСтр("ru = 'Сотрудники %1 и контрагентов'"), 
		РедакцииКонфигурацииКлиентСервер.ПредприятияРодительный());
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункцииМобильныйКлиент

&НаСервере
Процедура НастроитьФормуДляМобильногоПриНеобходимости()
	
	Если Не ПараметрыСеанса.ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;	
	
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	
	Элементы.ВидПросмотра.РастягиватьПоГоризонтали = Истина;
	Элементы.СтраницыГруппировкаПользователей.Видимость = Ложь;
	Элементы.ГруппировкиМК.Видимость = Истина;			
 	
 	Элементы.РабочиеГруппы.ВариантУправленияВысотой = ВариантУправленияВысотойТаблицы.ПоСодержимому;
 
 	Элементы.Переместить(Элементы.РабочиеГруппы, Элементы.ГруппировкиМК);
 	
 	Элементы.Переместить(Элементы.СтруктураПредприятия, Элементы.ГруппировкиМК);
 	
 	Элементы.Выбрать.Видимость = Ложь;
 	
 	Элементы.СписокПользователи.ПоложениеСтрокиПоиска = ПоложениеСтрокиПоиска.Верх;
 	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
