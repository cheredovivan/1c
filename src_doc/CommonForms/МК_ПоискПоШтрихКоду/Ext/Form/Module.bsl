#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ОбсужденияДоступны = Обсуждения.ОбсужденияДоступны();
	
	Элементы.ГруппаОткрыть.Видимость = Ложь;
	
	ОткрыватьФормуВНастольном = ОбсужденияДоступны;
	
	ЗакрыватьПослеСканирования = Ложь;
	
	ЗапускатьПриОткрытии = Истина;

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ЗначениеНастройкиОткрыватьФормуВНастольном = Настройки["ОткрыватьФормуВНастольном"];
	
	Если ЗначениеНастройкиОткрыватьФормуВНастольном <> Неопределено Тогда
	
		Если ОбсужденияДоступны Тогда
			ОткрыватьФормуВНастольном = ЗначениеНастройкиОткрыватьФормуВНастольном;
		Иначе
			ОткрыватьФормуВНастольном = Ложь;
		КонецЕсли;
	
	КонецЕсли;

	ЗначениеНастройкиЗакрыватьПослеСканирования = Настройки["ЗакрыватьПослеСканирования"];

	Если ЗначениеНастройкиЗакрыватьПослеСканирования <> Неопределено Тогда
		ЗакрыватьПослеСканирования = ЗначениеНастройкиЗакрыватьПослеСканирования;
	КонецЕсли;

	ЗначениеНастройкиЗапускатьПриОткрытии = Настройки["ЗапускатьПриОткрытии"];

	Если ЗначениеНастройкиЗапускатьПриОткрытии <> Неопределено Тогда
		ЗапускатьПриОткрытии = ЗначениеНастройкиЗапускатьПриОткрытии;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗапускатьПриОткрытии Тогда
		ЗапуститьСканирование();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОткрыватьФормуВНастольномПриИзменении(Элемент)
	
	Если ОткрыватьФормуВНастольном И Не ОбсужденияДоступны Тогда
		СообщитьОбсужденияНедоступны();
		ОткрыватьФормуВНастольном = Ложь;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаРезультатов

&НаКлиенте
Процедура ТаблицаРезультатовПриАктивизацииСтроки(Элемент)

	Если ТаблицаРезультатов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.ГруппаОткрыть.Видимость = Ложь Тогда
		Элементы.ГруппаОткрыть.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СканироватьШтрихкод(Команда)

	ЗапуститьСканирование();

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВНастольном(Команда)
	
	Если Не ОбсужденияДоступны Тогда
		СообщитьОбсужденияНедоступны();
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ТаблицаРезультатов.ТекущиеДанные;

	Если ТекущиеДанные = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Не выбран документ или файл для открытия'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ТекстОповещения = СтрШаблон(НСтр("ru = '%1 открывается  в настольном приложении'"),
		ТекущиеДанные.ПредставлениеОбъекта);

	ПоказатьОповещениеПользователя(ТекстОповещения);
	
	ОтправитьСлужебноеОповещение(ПолучитьНавигационнуюСсылку(ТекущиеДанные.Ключ),
		МК_КлиентСервер.ТипОперацииПереходПоНавигационнойСсылке());

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОбъект(Команда)
	
	ТекущиеДанные = Элементы.ТаблицаРезультатов.ТекущиеДанные;

	Если ТекущиеДанные = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Не выбран документ или файл для открытия'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение( ,ТекущиеДанные.Ключ);

КонецПроцедуры

&НаКлиенте
Процедура Настройки(Команда)
	
	Элементы.ГруппаНастройки.Видимость = Не Элементы.ГруппаНастройки.Видимость;
	Элементы.ФормаНастройки.Пометка = Элементы.ГруппаНастройки.Видимость;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаСканирования(Штрихкод, Результат, Сообщение, ДополнительныеПараметры) Экспорт

	Если Результат <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьСканированиеШтрихкода(Штрихкод);

#Если МобильныйКлиент Тогда
	Если ЗакрыватьПослеСканирования Тогда
		СредстваМультимедиа.ЗакрытьСканированиеШтрихКодов();
	КонецЕсли;

#Иначе
	ВызватьИсключение МК_КлиентСервер.ТекстФункциональностьНедоступна();
#КонецЕсли

КонецПроцедуры

&НаСервере
Процедура ОбработатьСканированиеШтрихкода(Знач Штрихкод)
	
	СтрокаШКИсх = Строка(Штрихкод);
	
	СтрокаШК = СтрокаШКИсх;   
	МассивШК = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаШК);
	
	// Добавим первый символ, чтобы длина строки равнялась 13
	Если СтрДлина(СтрокаШК) = 12 И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрокаШК) Тогда
		СтрокаШК = СтрШаблон("0%1", СтрокаШК);
		МассивШК.Добавить(СтрокаШК);
	КонецЕсли;
	
	НайденныеОбъекты = ШтрихкодированиеСервер.НайтиОбъектыПоШтрихкоду(МассивШК);

	Если НайденныеОбъекты.Количество() = 0 Тогда
		
		Возврат;
	
	КонецЕсли;
	
	Для Каждого НайденныйОбъект Из НайденныеОбъекты Цикл
		НоваяСтрока = ТаблицаРезультатов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденныйОбъект);
		НоваяСтрока.Штрихкод = Штрихкод;
		НоваяСтрока.ДатаСканированияМС = ТекущаяУниверсальнаяДатаВМиллисекундах();
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаРезультатов.Ключ,
		|	ТаблицаРезультатов.Штрихкод,
		|	ТаблицаРезультатов.ДатаСканированияМС КАК ДатаСканированияМС,
		|	ТаблицаРезультатов.ПредставлениеОбъекта КАК ПредставлениеОбъекта
		|ПОМЕСТИТЬ ТаблицаРезультатов
		|ИЗ
		|	&ТаблицаРезультатов КАК ТаблицаРезультатов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаРезультатов.Ключ,
		|	ТаблицаРезультатов.Штрихкод,
		|	ТаблицаРезультатов.ПредставлениеОбъекта,
		|	МАКСИМУМ(ВЫРАЗИТЬ(ТаблицаРезультатов.ДатаСканированияМС КАК ЧИСЛО)) КАК ДатаСканированияМС
		|ИЗ
		|	ТаблицаРезультатов КАК ТаблицаРезультатов
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаРезультатов.Ключ,
		|	ТаблицаРезультатов.Штрихкод,
		|	ТаблицаРезультатов.ПредставлениеОбъекта
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаСканированияМС Убыв";
	
	ТаблицаРезультатовОбъект = РеквизитФормыВЗначение("ТаблицаРезультатов");
	Запрос.УстановитьПараметр("ТаблицаРезультатов", ТаблицаРезультатовОбъект);
	
	ТаблицаРезультатов.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Если ОткрыватьФормуВНастольном Тогда
		ОтправитьСлужебноеОповещение(СтрокаШК, МК_КлиентСервер.ТипОперацииЧтениеШтрихкода());
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтправитьСлужебноеОповещение(Значение, ТипОперации)

	МК.ОтправитьСлужебноеОповещение(Значение, ТипОперации);

КонецПроцедуры

&НаКлиенте
Процедура СообщитьОбсужденияНедоступны()
	
	ТекстОбсужденияНедоступныНачало = НСтр("ru='Для открытия формы в настольном клиенте необходимо подключить механизм Обсуждений.'");
	ТекстОбсужденияНедоступныКонец = НСтр("ru='Обратитесь к администратору.'");

	ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон("%1%2%3",
		ТекстОбсужденияНедоступныНачало,
		Символы.ПС,
		ТекстОбсужденияНедоступныКонец));

КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьСканирование()

#Если МобильныйКлиент Тогда
	ОбработчикСканирования = Новый ОписаниеОповещения("ОбработкаСканирования", ЭтотОбъект);
	СредстваМультимедиа.ПоказатьСканированиеШтрихКодов(НСтр("ru='Отсканируйте штрихкод'"), ОбработчикСканирования);
#КонецЕсли

КонецПроцедуры

#КонецОбласти

