
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Проверяет является ли переданная задача процесса служебной.
//
// Параметры:
//  Задача - ЗадачаОбъект.ЗадачаИсполнителя,
//           ЗадачаСсылка.ЗадачаИсполнителя,
//           Структура со свойствами Исполнитель, РольИсполнителя, ТочкаМаршрута, Наименование, БизнесПроцесс
//
// Возвращаемое значение:
//  Булево
//
Функция ЭтаСлужебнаяЗадачаПроцесса(Задача) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТипЗадачи = ТипЗнч(Задача);
	
	РеквизитыЗадачи = Неопределено;
	Если ТипЗадачи = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		РеквизитыЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Задача, "Исполнитель, РольИсполнителя, ТочкаМаршрута, Наименование, БизнесПроцесс");
	ИначеЕсли ТипЗадачи = Тип("ЗадачаОбъект.ЗадачаИсполнителя")
		Или ТипЗадачи = Тип("Структура") Тогда
		
		РеквизитыЗадачи = Задача;
	КонецЕсли;
	
	МенеджерПроцесса = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Задача.БизнесПроцесс);
	
	Возврат МенеджерПроцесса.ЭтаСлужебнаяЗадачаПроцесса(РеквизитыЗадачи)
		Или (Не ЗначениеЗаполнено(РеквизитыЗадачи.Исполнитель)
			И Не ЗначениеЗаполнено(РеквизитыЗадачи.РольИсполнителя));
	
КонецФункции

#Область ОбновлениеКэширующихДанных

// Обрабатывает обновление кэширующих данных.
// 
// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса - Выборка из очереди обновления кэширующих данных:
//   * ОтметкаВремени - ОпределяемыйТип.ОтметкаВремени.
//   * ЗависимыйОбъектМетаданных - СправочникСсылка.ИдентификаторыОбъектовМетаданных.
//   * ВлияющийОбъектМетаданных - СправочникСсылка.ИдентификаторыОбъектовМетаданных.
//   * КлючВлияющихДанных - ЛюбаяСсылка.
//   * Автор - СправочникСсылка.Пользователи.
//   * ЗагрузкаОбработанныхДанныхИзДругойСистемы - Булево.
//   * ИзмененияВлияющихДанных - ХранилищеЗначения.
//   * Попыток - Число.
//   * ДатаКОбработке - Дата.
// 
Процедура ОбновитьКэширующиеДанные(Выборка) Экспорт      
	
	Попытка
		
		ПараметрыСеанса.МиграцияДанныхРазрешеноПрерываниеПроцессов = Истина;
	
		БизнесПроцесс = Выборка.КлючВлияющихДанных;  
		
		БизнесПроцессыИЗадачиВызовСервера.ПрерватьБизнесПроцесс(
			БизнесПроцесс, НСтр("ru = 'Прерывание процессов загруженных из ДО 2.1 при выключении загрузки.'"));
			
		ПротоколированиеРаботыСотрудников.ЗаписатьПрерываниеБизнесПроцесса(БизнесПроцесс);	
			
		ПараметрыСеанса.МиграцияДанныхРазрешеноПрерываниеПроцессов = Ложь;	
		
	Исключение
		
		ПараметрыСеанса.МиграцияДанныхРазрешеноПрерываниеПроцессов = Ложь;
		ВызватьИсключение;
			
	КонецПопытки;		
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

#Область ПраваДоступа

// Проверяет наличие метода ПраваСотрудниковПоОбъектам
// 
// Возвращаемое значение:
//  Булево - Есть ли метод ПраваСотрудниковПоОбъектам
Функция ЕстьМетодПраваПравообладателейПоОбъектам() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает права доступа правообладателей, т.е. сотрудников или пользователей, к объектам.
// 
// Параметры:
//  ОбъектыДоступа - Массив Из ЛюбаяСсылка - Ссылки на объекты, чьи права нужно получить.
//	СотрудникиОтбор - Массив Из СправочникСсылка.Сотрудники - Сотрудники для отбора.
//					- Неопределено -
//	ПользователиОтбор - Массив Из СправочникСсылка.Пользователи - Пользователи для отбора.
//					  - Неопределено -
//
// Возвращаемое значение:
//  См. ДокументооборотПраваДоступа.ТаблицаПравСотрудниковПоОбъектам
Функция ПраваПравообладателейПоОбъектам(
	ОбъектыДоступа, СотрудникиОтбор = Неопределено, ПользователиОтбор = Неопределено) Экспорт
	
	ТаблицаПрав = ДокументооборотПраваДоступа.ТаблицаПравСотрудниковПоОбъектам();
	
	Если ОбъектыДоступа.Количество() = 0 Тогда
		Возврат ТаблицаПрав;
	КонецЕсли;
	
	ПервыйОбъектДоступа = ОбъектыДоступа[0];
	ИдентификаторОМ = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПервыйОбъектДоступа.Метаданные());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
			"// ВТПраваНаЧтениеДобавление //////////////////////////////////////
			|ВЫБРАТЬ
			|	ЗадачаИсполнителя.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Сотрудник КАК Сотрудник,
			|	СоставСубъектов.Пользователь КАК Пользователь,
			|	ИСТИНА КАК Чтение,
			|	МАКСИМУМ(ВЫБОР
			|		КОГДА ЗадачаИсполнителя.Автор = СоставСубъектов.Сотрудник
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ) КАК Добавление
			|ПОМЕСТИТЬ ВТПраваНаЧтениеДобавление
			|ИЗ
			|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчастникиПроцессов КАК УчастникиПроцессов
			|		ПО (ЗадачаИсполнителя.БизнесПроцесс = УчастникиПроцессов.Процесс)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО (УчастникиПроцессов.Участник = СоставСубъектов.Субъект)
			|		И (СоставСубъектов.ИмяОбластиДелегирования В (&ОбластиДелегирования))
			|		И &СоставСубъектов_ОтборПравообладателей
			|ГДЕ
			|	ЗадачаИсполнителя.Ссылка В (&ОбъектыДоступа)
			|СГРУППИРОВАТЬ ПО
			|	ЗадачаИсполнителя.Ссылка,
			|	СоставСубъектов.Сотрудник,
			|	СоставСубъектов.Пользователь
			|;
			|
			|// ВТПраваНаИзменение ////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ЗадачаИсполнителя.Ссылка КАК ОбъектДоступа,
			|	СоставСубъектов.Сотрудник КАК Сотрудник,
			|	СоставСубъектов.Пользователь КАК Пользователь,
			|	ИСТИНА КАК Изменение
			|ПОМЕСТИТЬ ВТПраваНаИзменение
			|ИЗ
			|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|		ПО ЗадачаИсполнителя.ТекущийИсполнитель = СоставСубъектов.Субъект
			|		И (СоставСубъектов.ИмяОбластиДелегирования В (&ОбластиДелегирования))
			|		И &СоставСубъектов_ОтборПравообладателей
			|ГДЕ
			|	ЗадачаИсполнителя.Ссылка В (&ОбъектыДоступа)
			|СГРУППИРОВАТЬ ПО
			|	ЗадачаИсполнителя.Ссылка,
			|	СоставСубъектов.Сотрудник,
			|	СоставСубъектов.Пользователь
			|;
			|
			|// ВТСубъектыСотрудника ///////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	СоставСубъектов.Субъект КАК Субъект,
			|	СоставСубъектов.Сотрудник КАК Сотрудник,
			|	СоставСубъектов.Пользователь КАК Пользователь
			|ПОМЕСТИТЬ ВТСубъектыСотрудника
			|ИЗ
			|	РегистрСведений.СоставСубъектовПравДоступа КАК СоставСубъектов
			|ГДЕ
			|	&СоставСубъектов_ОтборПравообладателей
			|	И СоставСубъектов.ИмяОбластиДелегирования В ("""", ""Задачи"", ""ЗадачиПросмотр"")
			|;
			|
			|// ВТПраваПоДействиямЗадач /////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЗадачаИсполнителя.Ссылка КАК Задача,
			|	ИСТИНА КАК Чтение,
			|	ИСТИНА КАК Изменение,
			|	ИСТИНА КАК Добавление,
			|	ЛОЖЬ КАК Удаление,
			|	СубъектыСотрудника.Сотрудник КАК Исполнитель,
			|	СубъектыСотрудника.Пользователь КАК Пользователь,
			|	ЛОЖЬ КАК УправлениеПравами
			|ПОМЕСТИТЬ ВТПраваПоДействиямЗадач
			|ИЗ
			|	РегистрСведений.ВсеИсполнителиДействийЗадач КАК ВсеИсполнителиДействийЗадач
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДействиеЗадачи КАК ДействияЗадач
			|		ПО ВсеИсполнителиДействийЗадач.ДействиеЗадачи = ДействияЗадач.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
			|		ПО (ДействияЗадач.Источник = ЗадачаИсполнителя.Ссылка)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСубъектыСотрудника КАК СубъектыСотрудника
			|		ПО ВсеИсполнителиДействийЗадач.Исполнитель = СубъектыСотрудника.Субъект
			|ГДЕ
			|	ЗадачаИсполнителя.Ссылка В (&ОбъектыДоступа)
			|;
			|
			|// ВТПраваПоЗадаче /////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПраваНаЧтениеДобавление.ОбъектДоступа КАК ОбъектДоступа,
			|	ПраваНаЧтениеДобавление.Сотрудник КАК Сотрудник,
			|	ПраваНаЧтениеДобавление.Пользователь КАК Пользователь,
			|	ПраваНаЧтениеДобавление.Чтение КАК Чтение,
			|	ЕСТЬNULL(ПраваНаИзменение.Изменение, ЛОЖЬ) КАК Изменение,
			|	ПраваНаЧтениеДобавление.Добавление КАК Добавление,
			|	ЛОЖЬ КАК Удаление,
			|	ЛОЖЬ КАК УправлениеПравами
			|ПОМЕСТИТЬ ВТПраваПоЗадаче
			|ИЗ
			|	ВТПраваНаЧтениеДобавление КАК ПраваНаЧтениеДобавление
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	ВТПраваНаИзменение КАК ПраваНаИзменение
			|		ПО ПраваНаЧтениеДобавление.ОбъектДоступа = ПраваНаИзменение.ОбъектДоступа
			|		И ПраваНаЧтениеДобавление.Сотрудник = ПраваНаИзменение.Сотрудник
			|		И ПраваНаЧтениеДобавление.Пользователь = ПраваНаИзменение.Пользователь
			|;
			|
			|// ПраваПоВсемИсточникам ////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПраваПоЗадаче.ОбъектДоступа КАК ОбъектДоступа,
			|	ПраваПоЗадаче.Сотрудник КАК Сотрудник,
			|	ПраваПоЗадаче.Пользователь КАК Пользователь,
			|	ПраваПоЗадаче.Изменение КАК Изменение,
			|	ПраваПоЗадаче.Добавление КАК Добавление,
			|	ПраваПоЗадаче.Удаление КАК Удаление,
			|	ПраваПоЗадаче.УправлениеПравами КАК УправлениеПравами,
			|	ПраваПоЗадаче.Чтение КАК Чтение
			|ПОМЕСТИТЬ ВТПраваПоВсемИсточникам
			|ИЗ
			|	ВТПраваПоЗадаче КАК ПраваПоЗадаче
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПраваПоДействиямЗадач.Задача,
			|	ПраваПоДействиямЗадач.Исполнитель,
			|	ПраваПоДействиямЗадач.Пользователь,
			|	ПраваПоДействиямЗадач.Изменение,
			|	ПраваПоДействиямЗадач.Добавление,
			|	ПраваПоДействиямЗадач.Удаление,
			|	ПраваПоДействиямЗадач.УправлениеПравами,
			|	ПраваПоДействиямЗадач.Чтение
			|ИЗ
			|	ВТПраваПоДействиямЗадач КАК ПраваПоДействиямЗадач
			|;
			|
			|// Финальный итог ///////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПраваПоВсемИсточникам.ОбъектДоступа КАК ОбъектДоступа,
			|	ПраваПоВсемИсточникам.Сотрудник КАК Сотрудник,
			|	ПраваПоВсемИсточникам.Пользователь КАК Пользователь,
			|	МАКСИМУМ(ПраваПоВсемИсточникам.Изменение) КАК Изменение,
			|	МАКСИМУМ(ПраваПоВсемИсточникам.Изменение) КАК Добавление,
			|	МАКСИМУМ(ПраваПоВсемИсточникам.Добавление) КАК Удаление,
			|	МАКСИМУМ(ПраваПоВсемИсточникам.Удаление) КАК УправлениеПравами,
			|	МАКСИМУМ(ПраваПоВсемИсточникам.Чтение) КАК Чтение
			|ИЗ
			|	ВТПраваПоВсемИсточникам КАК ПраваПоВсемИсточникам
			|СГРУППИРОВАТЬ ПО
			|	ПраваПоВсемИсточникам.ОбъектДоступа,
			|	ПраваПоВсемИсточникам.Сотрудник,
			|	ПраваПоВсемИсточникам.Пользователь";
	Запрос.УстановитьПараметр("ОбъектыДоступа", ОбъектыДоступа);
	
	ДокументооборотПраваДоступа.ПодменитьОтборПравообладателейВЗапросе(
		Запрос, "&СоставСубъектов_ОтборПравообладателей", "СоставСубъектов", СотрудникиОтбор, ПользователиОтбор);
	
	Запрос.УстановитьПараметр("ИдентификаторОМ", ИдентификаторОМ);
	Запрос.УстановитьПараметр(
		"ОбластиДелегирования",
		ЗамещающиеИПомощники.ИменаОбластейЗамещенияПоИдентификаторуОбъектаМетаданных(ИдентификаторОМ));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаПрав.Добавить(), Выборка);
	КонецЦикла;
	
	ДокументооборотПраваДоступа.РасширитьТаблицуПравНеограниченнымиПравами(
		ТаблицаПрав, ИдентификаторОМ, ОбъектыДоступа, СотрудникиОтбор, ПользователиОтбор);
	
	Возврат ТаблицаПрав;
	
КонецФункции

#Область УстаревшиеПроцедурыИФункции

// Устарела.
// См. ЕстьМетодПраваПравообладателейПоОбъектам
// 
// Возвращаемое значение:
//  Булево -
Функция ЕстьМетодПраваСотрудниковПоОбъектам() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Устарела. См. ПраваПравообладателейПоОбъектам
// 
// Параметры:
//  ОбъектыДоступа - Массив Из ЛюбаяСсылка - Типы - См. Объект в РегистрСведений.ДескрипторыДляОбъектов
//  СотрудникиОтбор - Массив Из СправочникСсылка.Сотрудники -
//  				- Неопределено -
// 
// Возвращаемое значение:
//  См. ДокументооборотПраваДоступа.ТаблицаПравСотрудниковПоОбъектам
Функция ПраваСотрудниковПоОбъектам(ОбъектыДоступа, СотрудникиОтбор = Неопределено) Экспорт
	
	Возврат ПраваПравообладателейПоОбъектам(ОбъектыДоступа, СотрудникиОтбор);
	
КонецФункции

#КонецОбласти // УстаревшиеПроцедурыИФункции

#КонецОбласти // ПраваДоступа

#КонецОбласти // ДляВызоваИзДругихПодсистем

#КонецОбласти // ПрограммныйИнтерфейс

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Наименование");
	Поля.Добавить("Дата")
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекстПредставления = НСтр("ru = '%1 от %2'");
	
	Представление = СтрШаблон(
		ТекстПредставления,
		Данные.Наименование,
		Данные.Дата);
	
КонецПроцедуры

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка) Экспорт
	
	Если РаботаСПроцессамиПоОбработкамОбъектовСобытия.ОбработатьПолучениеФормыЗадачи(
		ВидФормы, Параметры, ВыбраннаяФорма, СтандартнаяОбработка) Тогда
		
		Возврат;		
	КонецЕсли;
	
	Если ВидФормы = "ФормаОбъекта"
		И Параметры.Свойство("Ключ")
		И ЗначениеЗаполнено(Параметры.Ключ) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		Если Не ОбщегоНазначения.СсылкаСуществует(Параметры.Ключ) Тогда
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Не удалось перейти по ссылке ""%1"" по причине: Неверно задана навигационная ссылка.'"),
				ПолучитьНавигационнуюСсылку(Параметры.Ключ));
		КонецЕсли;
		
		Если ЭтаСлужебнаяЗадачаПроцесса(Параметры.Ключ) Тогда
			Возврат;
		КонецЕсли;
		
		// Найдём существующе действие задачи.
		ДействиеЗадачиПоИсточнику = Документы.ДействиеЗадачи.НайтиПоИсточнику(Параметры.Ключ);
		Если ЗначениеЗаполнено(ДействиеЗадачиПоИсточнику) Тогда
			
			Параметры.Ключ = ДействиеЗадачиПоИсточнику;
			
			Документы.ДействиеЗадачи.ОбработкаПолученияФормы(
				ВидФормы,
				Параметры,
				ВыбраннаяФорма,
				ДополнительнаяИнформация,
				СтандартнаяОбработка);
			
			Возврат;
			
		КонецЕсли;
		
		// Попробуем обновить задачу и найти снова действие задачи.
		БизнесПроцесс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Ключ, "БизнесПроцесс");
		ИнтеграцияЗадач.ОбновитьЗадачуПоИсточнику(БизнесПроцесс);
		ДействиеЗадачиПоИсточнику = Документы.ДействиеЗадачи.НайтиПоИсточнику(Параметры.Ключ);
		Если ЗначениеЗаполнено(ДействиеЗадачиПоИсточнику) Тогда
			
			Параметры.Ключ = ДействиеЗадачиПоИсточнику;
			
			Документы.ДействиеЗадачи.ОбработкаПолученияФормы(
				ВидФормы,
				Параметры,
				ВыбраннаяФорма,
				ДополнительнаяИнформация,
				СтандартнаяОбработка);
			
			Возврат;
			
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = "Задача.ЗадачаИсполнителя.Форма.ПодробнаяФормаЗадачи";
		
	ИначеЕсли ВидФормы = "ФормаВыбора" Тогда
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = "Задача.ЗадачаИсполнителя.Форма.ФормаСписка";
		Параметры.Вставить("РежимВыбора", Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли