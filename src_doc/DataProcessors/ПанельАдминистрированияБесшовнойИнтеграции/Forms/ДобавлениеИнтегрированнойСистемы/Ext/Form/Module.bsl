#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
#Если Не ВебКлиент Тогда
	// Добавим в список выбора имя пользователя ИС.
	ПользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь();
	Элементы.ИмяПользователя.СписокВыбора.Добавить(ПользовательИБ.Имя);
	Элементы.ИмяПользователя.КнопкаВыпадающегоСписка = Истина;
#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Добавить(Команда)
	
	Закрыть(ДобавитьНаСервере());
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодключение(Команда)
	
	ПроверитьПодключениеНаСервере();
	ПоказатьПредупреждение(,НСтр("ru = 'Подключение установлено успешно'"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДобавитьНаСервере()
	
	ДанныеДляПодключения = РаботаСИнтегрированнымиСистемами.ДанныеДляПодключения();
	ДанныеДляПодключения.Прокси = РаботаСИнтегрированнымиСистемами.ПолучитьПрокси(АдресСервиса, ИмяПользователя, Пароль);
	
	МетаданныеИС = РаботаСИнтегрированнымиСистемами.СтруктураМетаданныхСистемыИнтегрированнойБесшовно(ДанныеДляПодключения);
	
	ИдентификаторУзла = МетаданныеИС.dataBaseID;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ИнтегрированныеСистемы.Ссылка КАК Ссылка
		|ИЗ
		|	ПланОбмена.ИнтегрированныеСистемы КАК ИнтегрированныеСистемы
		|ГДЕ
		|	ИнтегрированныеСистемы.Идентификатор = &ИдентификаторУзла");
	Запрос.УстановитьПараметр("ИдентификаторУзла", ИдентификаторУзла);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НачатьТранзакцию();
	Попытка
		
		Если Выборка.Следующий() Тогда
			УзелОбъект = Выборка.Ссылка.ПолучитьОбъект();
			УзелОбъект.Заблокировать();
			УзелСсылка = Выборка.Ссылка;
		Иначе
			УзелОбъект = ПланыОбмена.ИнтегрированныеСистемы.СоздатьУзел();
			Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ИдентификаторУзла) Тогда
				УзелСсылка = ПланыОбмена.ИнтегрированныеСистемы.ПолучитьСсылку(
					Новый УникальныйИдентификатор(ИдентификаторУзла));
			Иначе
				УзелСсылка = ПланыОбмена.ИнтегрированныеСистемы.ПолучитьСсылку(Новый УникальныйИдентификатор);
			КонецЕсли;
			УзелОбъект.УстановитьСсылкуНового(УзелСсылка);
			УзелОбъект.УстановитьНовыйКод();
			УзелОбъект.Идентификатор = ИдентификаторУзла;
		КонецЕсли;
		
		РегистрыСведений.ДанныеИнтегрированныхСистем.ЗаписатьДанные(УзелСсылка, ИмяПользователя, Пароль);
		
		УзелОбъект.Наименование = ?(ЗначениеЗаполнено(ИмяИС), ИмяИС, МетаданныеИС.dataBaseName);
		УзелОбъект.АдресВебСервиса = АдресСервиса;
		УзелОбъект.ПоддерживаетПравилаЗагрузкиДанныхВДО = Истина;
		УзелОбъект.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(
			РаботаСИнтегрированнымиСистемами.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат УзелОбъект.Ссылка;
	
КонецФункции

&НаСервере
Процедура ПроверитьПодключениеНаСервере()
	
	ДанныеДляПодключения = РаботаСИнтегрированнымиСистемами.ДанныеДляПодключения();
	ДанныеДляПодключения.Прокси = РаботаСИнтегрированнымиСистемами.ПолучитьПрокси(АдресСервиса, ИмяПользователя, Пароль);
	
	МетаданныеИС = РаботаСИнтегрированнымиСистемами.СтруктураМетаданныхСистемыИнтегрированнойБесшовно(ДанныеДляПодключения);
	
	Если Не ЗначениеЗаполнено(ИмяИС) Тогда
		ИмяИС = МетаданныеИС.dataBaseName;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти