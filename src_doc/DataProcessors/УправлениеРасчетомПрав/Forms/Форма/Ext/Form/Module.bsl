#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ИспользоватьМетрики = ПолучитьФункциональнуюОпцию("ИспользоватьМетрики");
	
	ОтложенноеОбновлениеПрав = ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа();

	Если ОтложенноеОбновлениеПрав Тогда
		ОбновитьСведенияИОтчетОбОчереди();
	КонецЕсли;

	Элементы.ГруппаОчередь.Видимость = ОтложенноеОбновлениеПрав;
	Элементы.ОтчетыПоОчередиОбновления.Видимость = ОтложенноеОбновлениеПрав;

	ТекущийЭлемент = Элементы.НастройкиСсылка; 
	
	Элементы.Диаграмма.Видимость = ИспользоватьМетрики;
	Элементы.ДекорацияНетМетрик.Видимость = Не ИспользоватьМетрики;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ОтложенноеОбновлениеПрав Тогда
		ИнтервалОбновленияСведенийОчереди = 10;
		ПодключитьОбработчикОжидания("ОбновитьСведенияОбОчередиОбработчикОжидания", ИнтервалОбновленияСведенийОчереди);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбновитьСведенияОбОчередиКлиент(Элемент)

	ОбновитьСведенияИОтчетОбОчереди();

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОчередь(Элемент)

	ОткрытьФорму("РегистрСведений.ОчередьОбновленияПравДоступа.Форма.ФормаСписка");

КонецПроцедуры

&НаКлиенте
Процедура НастройкиНажатие(Элемент)

	ОткрытьФорму("Обработка.НастройкаПрограммы.Форма.ПраваДоступа", , ЭтотОбъект, , , , ,
		РежимОткрытияОкнаФормы.Независимый);

КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеВручнуюНажатие(Элемент)

	ОткрытьФорму("Обработка.ОбновлениеПравДоступа.Форма.Форма");

КонецПроцедуры

&НаКлиенте
Процедура ОтчетыДинамикаОчередиНажатие(Элемент)

	ПоказателиРазмераОчереди = Новый СписокЗначений;
	ПоказателиРазмераОчереди.Добавить(
		ПредопределенноеЗначение("Справочник.Метрики.ДолгаяОчередь"));
	ПоказателиРазмераОчереди.Добавить(
		ПредопределенноеЗначение("Справочник.Метрики.ОперативнаяОчередь"));

	ПараметрыОткрытия = Новый Структура("ВыбранныеПоказатели, Период", ПоказателиРазмераОчереди,
		Новый СтандартныйПериод(ВариантСтандартногоПериода.Последние7Дней));

	ОткрытьФорму("Отчет.Метрики.Форма", ПараметрыОткрытия);

КонецПроцедуры

&НаКлиенте
Процедура ОтчетыСоставОчередиНажатие(Элемент)

	ОткрытьФорму("Отчет.СоставОчередиОбновленияПрав.Форма");

КонецПроцедуры

&НаКлиенте
Процедура ДескрипторыДоступаНажатие(Элемент)

	ОткрытьФорму("Отчет.ДескрипторыДоступа.Форма");

КонецПроцедуры

&НаКлиенте
Процедура СведенияОбИБНажатие(Элемент)

	ОткрытьФорму("Обработка.СведенияОбИнформационнойБазе.Форма");

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьСведенияОбОчереди()

	Модуль = РегистрыСведений.ОчередьОбновленияПравДоступа;
	РазмерОчереди1 = Модуль.РазмерОчереди(Модуль.ПриоритетыОперативной());
	РазмерОчереди2 = Модуль.РазмерОчереди(Модуль.ПриоритетыДолгой());

	СведенияОбОперативнойОчереди = Новый ФорматированнаяСтрока("", Новый ФорматированнаяСтрока(НСтр(
		"ru = 'Оперативная'"), , Новый Цвет(60, 100, 190)), СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = ' очередь обновления: %1 заданий'"), РазмерОчереди1));

	СведенияОДолгойОчереди = Новый ФорматированнаяСтрока("", Новый ФорматированнаяСтрока(НСтр(
		"ru = 'Долгая'"), , Новый Цвет(70, 150, 70)), СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = ' очередь обновления: %1 заданий'"), РазмерОчереди2));

КонецПроцедуры

&НаСервере
Процедура ОбновитьОтчетОбОчереди()       
	
	Если Не ИспользоватьМетрики Тогда
		Возврат;
	КонецЕсли;	

	КоличествоТочек = 201;

	РазмерДолгойОчереди = Справочники.Метрики.ДолгаяОчередь;
	РазмерОперативнойОчереди = Справочники.Метрики.ОперативнаяОчередь;

	ЦветаСерий = Новый Соответствие;
	ЦветаСерий.Вставить(РазмерДолгойОчереди, Новый Цвет(70, 150, 70));
	ЦветаСерий.Вставить(РазмерОперативнойОчереди, Новый Цвет(60, 100, 190));

	ВыбранныеПоказатели = Новый Массив;
	ВыбранныеПоказатели.Добавить(РазмерДолгойОчереди);
	ВыбранныеПоказатели.Добавить(РазмерОперативнойОчереди);

	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	ЗначенияПоказателей = Метрики.ПолучитьДанныеДляВыводаОтчета(
		ТекущаяДатаСеанса - 24 * 3600, ТекущаяДатаСеанса, ВыбранныеПоказатели, КоличествоТочек,
		Ложь);

	Диаграмма.Очистить();
	Диаграмма.Обновление = Ложь;
	Диаграмма.ОтображатьЛегенду = Ложь;
	Диаграмма.ОбластьПостроения.ОтображатьЛинииЗначенийШкалы = Ложь;
	Диаграмма.ОбластьПостроения.ОриентацияМеток = ОриентацияПодписейДиаграммы.Горизонтально;

	Диаграмма.ОбластьПостроения.ОтображатьПодписиШкалыТочек = Ложь;

	Для Каждого Стр Из ЗначенияПоказателей Цикл

		Точка = Диаграмма.УстановитьТочку(Стр.ОтображаемаяДата);
		ИндексТочки = Диаграмма.Точки.Индекс(Точка);
		Точка.Текст = "";

		Серия = Диаграмма.УстановитьСерию(Стр.Показатель);
		Серия.Маркер = ТипМаркераДиаграммы.Нет;
		Серия.Цвет = ЦветаСерий.Получить(Стр.Показатель);

		Диаграмма.УстановитьЗначение(Точка, Серия, Окр(Стр.Результат), , "" + Стр.Результат
			* Стр.Коэффициент);

	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОбновитьСведенияИОтчетОбОчереди()
	ОбновитьСведенияОбОчереди();
	ОбновитьОтчетОбОчереди();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСведенияОбОчередиОбработчикОжидания()

	ОбновитьСведенияИОтчетОбОчереди();

КонецПроцедуры

#КонецОбласти