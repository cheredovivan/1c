#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ИспользоватьБизнесПроцессыИЗадачи.Доступность = НаборКонстант.ИспользоватьЗадачи;
	Элементы.ГруппаБизнесПроцессы.Доступность = НаборКонстант.ИспользоватьЗадачи
		И НаборКонстант.ИспользоватьБизнесПроцессыИЗадачи;
	
	ЭтоФайловаяБаза = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	
	Элементы.ИспользоватьФоновыйСтартПроцессов.Видимость = Не ЭтоФайловаяБаза;
	
	Если НаборКонстант.ИспользоватьСинхронизациюДанных Тогда
		
	КонецЕсли;
	
	Элементы.ГиперссылкаПравилаКоммуникаций.Доступность = НаборКонстант.ИспользоватьПравилаКоммуникаций;
	

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьДоступностьДляГруппаОтступОтНастройкиВыполнениеЗадачПоПочте();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Приглашение на мероприятие
	ТекущийОбъект.ИспользоватьПриглашениеНаМероприятие =
		ТекущийОбъект.ИспользоватьБизнесПроцессыИЗадачи И 
		ТекущийОбъект.ИспользоватьУправлениеМероприятиями;
		
	Если Не ЗначениеЗаполнено(ТекущийОбъект.СпособСогласованияВРежимеЗамечаний) Тогда
		ТекущийОбъект.СпособСогласованияВРежимеЗамечаний = Перечисления.СпособыСогласованияВРежимеЗамечаний.MicrosoftOfficeWord;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТекущийОбъект.ИспользоватьГрафикиРаботы И ЗначениеЗаполнено(ТекущийОбъект.ОсновнойГрафикРаботы) Тогда 
			
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Проекты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Проекты КАК Проекты
		|ГДЕ
		|	Проекты.ГрафикРаботы = ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ПроектОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ПроектОбъект.ГрафикРаботы = ТекущийОбъект.ОсновнойГрафикРаботы;
			ПроектОбъект.ОбменДанными.Загрузка = Истина;
			ПроектОбъект.Записать();
		КонецЦикла;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьПовторноИспользуемыеЗначения();
	ОбновитьИнтерфейс();
	
	Если ПараметрыЗаписи.Свойство("Закрыть") Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьЗадачиПриИзменении(Элемент)
	
	Элементы.ИспользоватьБизнесПроцессыИЗадачи.Доступность = НаборКонстант.ИспользоватьЗадачи;
	Элементы.ГруппаБизнесПроцессы.Доступность = НаборКонстант.ИспользоватьЗадачи
		И НаборКонстант.ИспользоватьБизнесПроцессыИЗадачи;
	
	Если Не НаборКонстант.ИспользоватьЗадачи Тогда
		НаборКонстант.ИспользоватьБизнесПроцессыИЗадачи = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьБизнесПроцессыИЗадачиПриИзменении(Элемент)
	
	Элементы.ГруппаБизнесПроцессы.Доступность = НаборКонстант.ИспользоватьЗадачи
		И НаборКонстант.ИспользоватьБизнесПроцессыИЗадачи;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьГрафикиРаботыПриИзменении(Элемент)
	
	СохранитьКонстантуИспользоватьГрафикиРаботы(НаборКонстант.ИспользоватьГрафикиРаботы);
	ОбновитьИнтерфейс();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьВыполнениеЗадачПоПочтеПриИзменении(Элемент)
	
	ОбновитьДоступностьДляГруппаОтступОтНастройкиВыполнениеЗадачПоПочте();
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПравилаКоммуникацийНажатие(Элемент)
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ПравилаКоммуникаций.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьПравилаКоммуникацийПриИзменении(Элемент)
	
	Элементы.ГиперссылкаПравилаКоммуникаций.Доступность = НаборКонстант.ИспользоватьПравилаКоммуникаций;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособСогласованияВРежимеЗамечанийОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("Закрыть", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьКонстантуИспользоватьГрафикиРаботы(ИспользоватьГрафикиРаботы)
	
	Константы.ИспользоватьГрафикиРаботы.Установить(ИспользоватьГрафикиРаботы);
	
КонецПроцедуры

// Обновляет доступность элемента ГруппаОтступОтНастройкиВыполнениеЗадачПоПочте,
// в зависимости от настройки ИспользоватьВыполнениеЗадачПоПочте.
//
&НаКлиенте
Процедура ОбновитьДоступностьДляГруппаОтступОтНастройкиВыполнениеЗадачПоПочте() Экспорт
	
	Элементы.ГруппаОтступОтНастройкиВыполнениеЗадачПоПочте.Доступность =
		НаборКонстант.ИспользоватьВыполнениеЗадачПоПочте;
	
КонецПроцедуры

#КонецОбласти

