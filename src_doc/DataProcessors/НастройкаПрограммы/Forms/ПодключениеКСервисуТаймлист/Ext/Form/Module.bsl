
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДанныеАвторизации = Таймлист.ДанныеАвторизацииВСервисе();
	
	Если ЗначениеЗаполнено(ДанныеАвторизации) Тогда
		Логин = ДанныеАвторизации.Логин;
		Пароль = ДанныеАвторизации.Пароль;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ДанныеАвторизации = Новый Структура("Логин, Пароль", Логин, Пароль);
	Таймлист.ЗаписатьДанныеАвторизации(ДанныеАвторизации);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияСервисНажатие(Элемент)
	
	ИнформационнаяСсылка = "https://timelist.ru/1CDO";
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(ИнформационнаяСсылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьПодключение(Команда)
	
	ПодключениеУстановлено = ТаймлистВызовСервера.СервисПоДаннымАвторизацииПодключен(Логин, Пароль,
		НаборКонстант.ТаймлистПорт, НаборКонстант.ТаймлистЗащищенноеСоединение);
	
	Если ПодключениеУстановлено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Подключение установлено успешно'"));
		Возврат;
	КонецЕсли;
	
	ПоказатьПредупреждение(, НСтр("ru = 'Подключение не установлено, проверьте корректность указанных данных.'"));
	
КонецПроцедуры

&НаКлиенте
Процедура АдресСервисаПоУмолчанию(Команда)
	
	НаборКонстант.ТаймлистАдресСервиса = "restapi.timelist.ru";
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РасширенияФайловПоУмолчанию(Команда)
	
	НаборКонстант.ТаймлистСписокРасширенийФайлов = ТаймлистКлиентСервер.РасширенияФайловТаймлистПоУмолчанию();
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти
