
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Элементы.ГруппаРаспознаваниеТекста.Видимость = Ложь;
		Элементы.ГруппаТаймлист.Видимость = Ложь;
		// Локализация
		Элементы.ГруппаРаспознаваниеРечи.Видимость = Ложь;
		// Локализация Конец
	Иначе
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
		
	УстановитьВидимостьЭлементовРаспознаванияТекста();
	ОбновитьДоступностьГруппыПрограммРаспознавания();
	ОбновитьДоступностьКомандНастройкиРаспознавания();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ВключеноИспользованиеРаспознаванияCuneiForm =
		ТекущийОбъект.ИспользоватьРаспознавание
		И НаборКонстант.ПрограммаРаспознавания = Перечисления.ПрограммыРаспознавания.CuneiForm;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	РаботаСФайламиВызовСервера.НормализоватьЯзыкРаспознавания();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПослеЗаписиОбработчик(ПараметрыЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьРаспознаваниеПриИзменении(Элемент)
	
	УстановитьВидимостьЭлементовРаспознаванияТекста();
	ОбновитьДоступностьГруппыПрограммРаспознавания();
	ОбновитьДоступностьКомандНастройкиРаспознавания();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрограммаРаспознаванияПриИзменении(Элемент)
	
	УстановитьВидимостьЭлементовРаспознаванияТекста();
	ОбновитьДоступностьКомандНастройкиРаспознавания();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСервисРаспознаванияДокументовДляЗаполненияКарточекПриИзменении(Элемент)
	
	Элементы.НастройкиЗаполненияКарточекДокументов.Доступность =
		НаборКонстант.ИспользоватьСервисРаспознаванияДокументовДляЗаполненияКарточек;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользуетсяРаспознаваниеТаймлистПриИзменении(Элемент)
	
	Элементы.НастройкиСервисаТаймлист.Доступность = НаборКонстант.ИспользуетсяРаспознаваниеТаймлист;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьНастройки(Команда)
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("Закрыть", Истина);
	Записать(ПараметрыЗаписи);
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиРаспознаванияТекста(Команда)
	
	Если НаборКонстант.ПрограммаРаспознавания =
		ПредопределенноеЗначение("Перечисление.ПрограммыРаспознавания.СервисРаспознавания") Тогда
		
		ОткрытьФорму("Справочник.Файлы.Форма.НастройкиСервисаРаспознаванияТекста",,,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ОткрытьФорму("Справочник.Файлы.Форма.НастройкиРаспознаванияCuneiForm",,,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗаполненияКарточекДокументов(Команда)
	
	ОткрытьФорму("ОбщаяФорма.НастройкиЗаполненияКарточекДокументов",,,,,,,
	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура НастройкиСервисаТаймлист(Команда)
	
	ОткрытьФорму("Обработка.НастройкаПрограммы.Форма.ПодключениеКСервисуТаймлист",,,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Локализация
&НаКлиенте
Процедура НастройкиРаботыСРечью(Команда)
	
	ПерейтиПоНавигационнойСсылке("e1cib/app/Обработка.ПанельАдминистрированияРаботыСРечью");
	
КонецПроцедуры
// Локализация Конец

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимостьЭлементовРаспознаванияТекста()
	
	Элементы.ГруппаПрограммыРаспознавания.Видимость = НаборКонстант.ИспользоватьРаспознавание;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступностьГруппыПрограммРаспознавания()
	
	Элементы.ГруппаПрограммыРаспознавания.Доступность = НаборКонстант.ИспользоватьРаспознавание;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступностьКомандНастройкиРаспознавания()
	
	Элементы.НастройкиЗаполненияКарточекДокументов.Доступность =
		НаборКонстант.ИспользоватьСервисРаспознаванияДокументовДляЗаполненияКарточек;
	
	Элементы.НастройкиСервисаТаймлист.Доступность = НаборКонстант.ИспользуетсяРаспознаваниеТаймлист;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиОбработчик(ПараметрыЗаписи)
	
	Если ВключеноИспользованиеРаспознаванияCuneiForm Тогда
		Если Не ПараметрыЗаписи.Свойство("ПоказанВопрос") Тогда
			Текст = НСтр("ru = 'Включено распознавание.
			|Для работы распознавания необходимо установить бесплатную программу CuneiForm.
			|Поставить в очередь на распознавание все существующие файлы изображений?'");
			
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ПослеЗаписиПродолжение",
				ЭтотОбъект,
				ПараметрыЗаписи);
			
			ПоказатьВопрос(ОписаниеОповещения, Текст, РежимДиалогаВопрос.ДаНет);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("Закрыть") Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиПродолжение(Результат, ПараметрыЗаписи) Экспорт
	
	ПараметрыЗаписи.Вставить("ПоказанВопрос", Истина);
	Если Результат = КодВозвратаДиалога.Да Тогда
		Состояние(НСтр("ru = 'Формируется очередь распознавания изображений. Пожалуйста подождите...'"));
		ЧислоКартинок = ПоставитьВсеКартинкиВОчередьРаспознавания();
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Очередь распознавания изображений сформирована (файлов: %1).'"), ЧислоКартинок);
		Состояние(ТекстСообщения);
	КонецЕсли;
	ПослеЗаписиОбработчик(ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Функция ПоставитьВсеКартинкиВОчередьРаспознавания()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЧислоКартинок = 0;
	ИспользоватьImageMagickДляРаспознаванияPDF =
		РаботаСФайламиВызовСервера.ПолучитьИспользоватьImageMagickДляРаспознаванияPDF();
		
	РасширенияКартинок = Новый Массив;
	РасширенияКартинок.Добавить("pdf");
	РасширенияКартинок.Добавить("tif");
	РасширенияКартинок.Добавить("tiff");
	РасширенияКартинок.Добавить("bmp");
	РасширенияКартинок.Добавить("jpg");
	РасширенияКартинок.Добавить("jpeg");
	РасширенияКартинок.Добавить("png");
	РасширенияКартинок.Добавить("gif");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Файлы.Ссылка КАК Ссылка,
		|	Файлы.ТекущаяВерсияРасширение КАК ТекущаяВерсияРасширение
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|ГДЕ
		|	Файлы.ТекущаяВерсияРасширение В(&РасширенияКартинок)";
	Запрос.УстановитьПараметр("РасширенияКартинок", РасширенияКартинок);	
	
	ПараметрыПроверки = РаботаСФайламиКлиентСервер.ОписаниеПараметровПроверкиВозможностиРаспознавания();
	ПараметрыПроверки.ИспользоватьImageMagick = ИспользоватьImageMagickДляРаспознаванияPDF;
	ПараметрыПроверки.ПрограммаРаспознавания = Перечисления.ПрограммыРаспознавания.CuneiForm;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		РасширениеПоддерживается = РаботаСФайламиКлиентСервер.ЭтотФайлМожноРаспознать(
			Выборка.ТекущаяВерсияРасширение, ПараметрыПроверки);
		
		Если РасширениеПоддерживается Тогда
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			Объект.СтратегияРаспознавания = Перечисления.СтратегииРаспознаванияТекста.ПоместитьТолькоВТекстовыйОбраз;
			Объект.ЯзыкРаспознавания = "7"; // русско-английский
			Объект.Записать();
			ЧислоКартинок = ЧислоКартинок + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВерсииФайлов.Ссылка КАК Ссылка,
		|	ВерсииФайлов.Расширение КАК Расширение
		|ИЗ
		|	Справочник.ВерсииФайлов КАК ВерсииФайлов
		|ГДЕ
		|	ВерсииФайлов.Расширение В(&РасширенияКартинок)"; 
	Запрос.УстановитьПараметр("РасширенияКартинок", РасширенияКартинок);	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		РасширениеПоддерживается = РаботаСФайламиКлиентСервер.ЭтотФайлМожноРаспознать(
			Выборка.Расширение, ПараметрыПроверки);
		
		Если РасширениеПоддерживается Тогда
			
			СтатусРаспознаванияТекста = Перечисления.СтатусыРаспознаванияТекста.НужноРаспознать;
			РегистрыСведений.ТекстыВерсийФайлов.ДобавитьСтатусРаспознавания(Выборка.Ссылка, СтатусРаспознаванияТекста);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЧислоКартинок;
	
КонецФункции

#КонецОбласти
