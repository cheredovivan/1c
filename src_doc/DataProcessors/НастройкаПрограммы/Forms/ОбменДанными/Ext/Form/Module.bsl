
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	

	ПрочитатьКонстантыСервер();
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		Элементы.ГруппаПрефиксИЗапретРедактированияСтатейДДС.Видимость = Ложь;
		
	Иначе
		
		Элементы.ПрефиксУзлаРаспределеннойИнформационнойБазы.Доступность = ИспользоватьСинхронизациюДанных;
		Элементы.ЗапретитьРедактированиеСтатейДвиженияДенежныхСредств.Доступность = ИспользоватьСинхронизациюДанных;
		Элементы.ЗапретитьРедактированиеТоваровИУслуг.Доступность = ИспользоватьСинхронизациюДанных;
		
	КонецЕсли;
	
	ЭтоФайловаяБаза = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	
	НастройкиПрограммыЛокализация.ПриСозданииНаСервере_ОбменДанными(ЭтотОбъект, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Локализация
	УстановитьВидимостьДоступностьМиграцииС21();
	// Локализация Конец
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И Не ЗавершениеРаботы Тогда
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
		СпроситьПередЗакрытием(Отказ, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьСинхронизациюДанныхПриИзменении(Элемент)

	Элементы.ПрефиксУзлаРаспределеннойИнформационнойБазы.Доступность = ИспользоватьСинхронизациюДанных;

	Элементы.ЗапретитьРедактированиеСтатейДвиженияДенежныхСредств.Доступность = ИспользоватьСинхронизациюДанных;
	Элементы.ЗапретитьРедактированиеТоваровИУслуг.Доступность = ИспользоватьСинхронизациюДанных;
	
	Если ИспользоватьСинхронизациюДанных Тогда
		ЗапретитьРедактированиеСтатейДвиженияДенежныхСредств = Истина;
		ЗапретитьРедактированиеТоваровИУслуг = Истина;
	Иначе
		ЗапретитьРедактированиеСтатейДвиженияДенежныхСредств = Ложь;
		ЗапретитьРедактированиеТоваровИУслуг = Ложь;
	КонецЕсли;
	
КонецПроцедуры


// Локализация
&НаКлиенте
Процедура ЗагружатьДанныеИз21ПриИзменении(Элемент)
	
	УстановитьКонстантуСервер("ЗагружатьДанныеИз21", ЗагружатьДанныеИз21);
	ОбновитьИнтерфейс();
	
	УстановитьВидимостьДоступностьМиграцииС21();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьМЭДОПриИзменении(Элемент)
	
	Элементы.ГруппаМЭДОНастройки.Доступность = ИспользоватьМЭДО;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОбменЭДПриИзменении(Элемент)

	Элементы.НастроитьЭДО.Доступность = ИспользоватьОбменЭД;

КонецПроцедуры
// Локализация Конец

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьКонстантыИЗакрыть(Команда)
	
	ЗаписатьНастройки();
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

// Локализация
&НаКлиенте
Процедура НастроитьМиграциюИзПредыдущейВерсии(Команда)
	
	ОткрытьФорму("Обработка.МиграцияДанныхИзВнешнихСистемМонитор.Форма.Форма");
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьЭДО(Команда, ПараметрыВыполненияКоманды)
	
	УстановитьКонстантуСервер("ИспользоватьОбменЭД", ИспользоватьОбменЭД);
	ОткрытьФорму("Обработка.ПанельАдминистрированияБЭД.Форма.ОбщиеНастройки",,
		ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.Независимый);

КонецПроцедуры


// Локализация Конец

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЧтениеЗаписьКонстантВРеквизитыФормы

&НаКлиенте
Асинх Процедура СпроситьПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Ответ = Ждать ВопросАсинх(
		НСтр("ru = 'Данные были изменены. Сохранить изменения?'"), РежимДиалогаВопрос.ДаНетОтмена);
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаписатьНастройки();
		Модифицированность = Ложь;
		Закрыть();
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНастройки()
	
	НастройкиПрограммыКлиентЛокализация.ПриЗаписиНастроекОбменаДанными(ЭтотОбъект);
	
	ЗаписатьКонстантыСервер();
	ОбновитьПовторноИспользуемыеЗначения();
	ОбновитьИнтерфейс();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьКонстантыСервер()
	
	Обработки.НастройкаПрограммы.ЗаписатьКонстантыВФорме(ЭтотОбъект, ИменаКонстант());
	

	НастройкиПрограммыЛокализация.ПриЗаписиНастроек_ОбменДанными(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьКонстантыСервер()
	
	Обработки.НастройкаПрограммы.ПрочитатьКонстантыВФорме(ЭтотОбъект, ИменаКонстант());
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИменаКонстант()
	
	Имена = Новый Массив(); // Массив Из Строка

	Имена.Добавить("ЗагружатьДанныеИз21");
	Имена.Добавить("ЗапретитьРедактированиеСтатейДвиженияДенежныхСредств");
	Имена.Добавить("ЗапретитьРедактированиеТоваровИУслуг");

	Имена.Добавить("ИспользоватьМобильныеПриложения");
	
	Имена.Добавить("ИспользоватьОтметкиВремени");
	Имена.Добавить("ИспользоватьСинхронизациюДанных");
	Имена.Добавить("ПрефиксУзлаРаспределеннойИнформационнойБазы");

	НастройкиПрограммыЛокализация.ДополнитьИменаКонстант_ОбменДанными(Имена);
	
	Возврат Имена;

КонецФункции

#КонецОбласти

&НаСервереБезКонтекста
Процедура УстановитьКонстантуСервер(Знач ИмяКонстанты, Знач Значение)
	
	Если Константы[ИмяКонстанты].Получить() <> Значение Тогда
		Константы[ИмяКонстанты].Установить(Значение);
	КонецЕсли;
	
КонецПроцедуры

// Локализация
&НаКлиенте
Процедура УстановитьВидимостьДоступностьМиграцииС21()
	
	Элементы.ГруппаОбменСВнешнимиРесурсамиПереход.Видимость = Ложь;
	
	Если ЗагружатьДанныеИз21 Тогда
		Элементы.ГруппаОбменСВнешнимиРесурсамиПереход.Видимость = Истина;
		Элементы.ГруппаВнешнийОбменВ21.Видимость = Ложь;
		Элементы.ГруппаВнешнийОбменВ30.Видимость = Ложь;
		
		Если ОбменСВнешнимиРесурсамиВ21 Тогда
			Элементы.ГруппаВнешнийОбменВ21.Видимость = Истина;
		Иначе
			Элементы.ГруппаВнешнийОбменВ30.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.НастроитьМиграциюИзПредыдущейВерсии.Доступность = ЗагружатьДанныеИз21;
	
КонецПроцедуры
// Локализация Конец


#КонецОбласти

