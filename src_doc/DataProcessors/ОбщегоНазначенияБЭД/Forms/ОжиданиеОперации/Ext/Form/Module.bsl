#Область ОписаниеПеременных
&НаКлиенте
Перем ПрограммноеЗакрытие;
#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыДлительнойОперации = ОжиданиеОперацийБЭДСлужебныйКлиентСервер.НовыеПараметры();
	ЗаполнитьЗначенияСвойств(ПараметрыДлительнойОперации, Параметры);
	ОбновитьФормуПоПараметрам(ПараметрыДлительнойОперации);
	
	Элементы.КартинкаУспешно.Видимость = Ложь;
	Элементы.ФормаЗакрыть.Видимость = Ложь;
	Элементы.ИндикаторПрогресса.Видимость = ПараметрыДлительнойОперации.ПроцентПрогресса <> Неопределено; 
	Элементы.ИндикаторПрогресса.ОтображатьПроценты = ПараметрыДлительнойОперации.ОтображатьПроценты;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, КонтекстДлительнойОперации, Источник)
	
	Если ИмяСобытия = ОжиданиеОперацийБЭДСлужебныйКлиентСервер.ИмяСобытияЗакрытияФормы()
		Или ИмяСобытия = ОжиданиеОперацийБЭДСлужебныйКлиентСервер.ИмяСобытияПередИнтерактивнымДействием() Тогда
		Если КонтекстДлительнойОперации.ИдентификаторОперации = Параметры.ИдентификаторОперации Тогда
			ЗакрытьФорму(Ложь);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = ОжиданиеОперацийБЭДСлужебныйКлиентСервер.ИмяСобытияОбновленияПрогресса() Тогда
		Если КонтекстДлительнойОперации.ИдентификаторОперации = Параметры.ИдентификаторОперации Тогда
			Если КонтекстДлительнойОперации.ПараметрыОжиданияОперации.ОперацияЗавершена Тогда 
				ОбновитьФормуПоПараметрам(КонтекстДлительнойОперации.ПараметрыОжиданияОперации);
				ПерейтиВСостояниеЗавершеннойОперации();
			Иначе 
				ОбновитьСостояниеПрогрессаДлительнойОперации(КонтекстДлительнойОперации.ПараметрыОжиданияОперации);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Или ПрограммноеЗакрытие Тогда
		Возврат;
	КонецЕсли;
	
	РезультатЗакрытия = Не ЗавершениеРаботы;
	
	ЗакрытьФорму(РезультатЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)
	
	ОтменитьВыполнениеЗадания(Параметры.ИдентификаторОперации);
	ЗакрытьФорму(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьБезОповещения(Команда)
	
	ЗакрытьФорму(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьФормуПоПараметрам(ПараметрыДлительнойОперации)

	Заголовок = ПараметрыДлительнойОперации.Заголовок;
	Если ЗначениеЗаполнено(ПараметрыДлительнойОперации.ТекстСообщения) Тогда
		Элементы.ДекорацияПоясняющийТекстДлительнойОперации.Заголовок = ПараметрыДлительнойОперации.ТекстСообщения;
	Иначе
		Элементы.ДекорацияПоясняющийТекстДлительнойОперации.Заголовок = НСтр("ru = 'Пожалуйста, подождите...'");
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПерейтиВСостояниеЗавершеннойОперации()
	
	Элементы.ИндикаторПрогресса.Видимость = Ложь;
	Элементы.ДекорацияДлительнаяОперация.Видимость = Ложь;
	Элементы.ФормаОтмена.Видимость = Ложь;
	Элементы.КартинкаУспешно.Видимость = Истина;
	Элементы.ФормаЗакрыть.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(РезультатЗакрытия)
	
	Если Открыта() Тогда
		ПрограммноеЗакрытие = Истина;
		Закрыть(РезультатЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояниеПрогрессаДлительнойОперации(ПараметрыДлительнойОперации)
    
	Если ПараметрыДлительнойОперации.ПроцентПрогресса <> Неопределено Тогда
		Элементы.ИндикаторПрогресса.Видимость = Истина;
		ИндикаторПрогресса = ПараметрыДлительнойОперации.ПроцентПрогресса;
	Иначе
		Элементы.ИндикаторПрогресса.Видимость = Ложь;
	КонецЕсли;
    
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьВыполнениеЗадания(Знач ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

ПрограммноеЗакрытие = Ложь;

#КонецОбласти