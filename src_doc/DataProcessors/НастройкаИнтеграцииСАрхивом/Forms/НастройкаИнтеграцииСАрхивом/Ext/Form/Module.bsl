#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокВыбора = Элементы.ВерсияФорматаОбменаПоддерживаемая1САрхивом.СписокВыбора;
	СписокВыбора.Очистить();
	ДоступныеЗначения = ОбменСАрхивом.ВерсииФорматаДоступные();
	Для Каждого Эл Из ДоступныеЗначения Цикл
		СписокВыбора.Добавить(Эл.Значение, Эл.Представление);
	КонецЦикла;
	ТекущееЗначениеФормата = НаборКонстант.ВерсияФорматаОбменаПоддерживаемая1САрхивом;
	Если ЗначениеЗаполнено(ТекущееЗначениеФормата)
			И СписокВыбора.НайтиПоЗначению(ТекущееЗначениеФормата) = Неопределено Тогда
		ПредставлениеВерсии = ОбменСАрхивом.ПредставлениеВерсииФормата(ТекущееЗначениеФормата);
		СписокВыбора.Вставить(0, ТекущееЗначениеФормата, ПредставлениеВерсии);
	КонецЕсли;
	
	ВладелецПароляFTPСервера = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Константы.ИнтеграцияС1САрхивомПользовательFTP);
	ВладелецПароляВебСервиса = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Константы.ИмяПользователя1САрхива);
	УстановитьПривилегированныйРежим(Истина);
	ПарольПользователяFTPСервера = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(ВладелецПароляFTPСервера);
	ПарольПользователяВебСервиса = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(ВладелецПароляВебСервиса);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ЗначениеЗаполнено(НаборКонстант.ИнтеграцияС1САрхивомПортFTP) Тогда
		НаборКонстант.ИнтеграцияС1САрхивомПортFTP = 21;
		НаборКонстант.ИнтеграцияС1САрхивомПассивныйРежимFTP = Истина;
	КонецЕсли;
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ЭтоСерверWindows = СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86 
		Или СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64;
	Если ЭтоСерверWindows Тогда
		Элементы.ИнтеграцияС1САрхивомКаталог.ПодсказкаВвода = "\\Servername\ExchangeFolder\";
	Иначе
		Элементы.ИнтеграцияС1САрхивомКаталог.ПодсказкаВвода = "/mnt/ExchangeFolder/";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не НаборКонстант.ИнтеграцияС1САрхивом Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НаборКонстант.ИнтеграцияС1САрхивомВидТранспорта) Тогда
		ТекстОшибки = НСтр("ru = 'Настройте один из способов обмена'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,, "НаборКонстант.ИнтеграцияС1САрхивомВидТранспорта", Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НаборКонстант.ИнтеграцияС1САрхивомКодБазы) Тогда
		ТекстОшибки = НСтр("ru = 'Не указан код базы'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,, "НаборКонстант.ИнтеграцияС1САрхивомКодБазы", Отказ);
	КонецЕсли;
	
	ВидыТранспорта = Перечисления.ВидыТранспортаДляОбменаС1САрхивом;
	Если НаборКонстант.ИнтеграцияС1САрхивомВидТранспорта = ВидыТранспорта.ОбщийКаталог Тогда
		
		ЕстьОшибкиКаталогаОбмена = Ложь;
		КаталогИнтеграции = НаборКонстант.ИнтеграцияС1САрхивомКаталог;
		Если Не ЗначениеЗаполнено(КаталогИнтеграции) Тогда
			ЕстьОшибкиКаталогаОбмена = Истина;
			ТекстОшибки = НСтр("ru = 'Не указан каталог обмена'");
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,, "НаборКонстант.ИнтеграцияС1САрхивомКаталог", Отказ);
		ИначеЕсли ЭтоСерверWindows 
			И (Лев(КаталогИнтеграции, 2) <> "\\"
				Или СтрНайти(КаталогИнтеграции, ":") <> 0) Тогда
			ЕстьОшибкиКаталогаОбмена = Истина;
			ТекстОшибки = НСтр("ru = 'Каталог обмена должен быть указан в формате UNC (\\Servername\Resource)'");
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,, "НаборКонстант.ИнтеграцияС1САрхивомКаталог", Отказ);
		КонецЕсли;
		
		Если Не ЕстьОшибкиКаталогаОбмена Тогда
			КаталогИнтеграцииФайл = Новый Файл(КаталогИнтеграции);
			Если Не КаталогИнтеграцииФайл.Существует() Тогда
				ТекстОшибки = НСтр("ru = 'Каталог не обнаружен'");
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,, "НаборКонстант.ИнтеграцияС1САрхивомКаталог", Отказ);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли НаборКонстант.ИнтеграцияС1САрхивомВидТранспорта = ВидыТранспорта.FTPСервер Тогда
		
		ПараметрыFTP = ОбменСАрхивомТранспорт.НовыеПараметрыПодключенияFTP();
		ПараметрыFTP.КаталогFTP = НаборКонстант.ИнтеграцияС1САрхивомАдресFTP;
		ПараметрыFTP.Логин = НаборКонстант.ИнтеграцияС1САрхивомПользовательFTP;
		ПараметрыFTP.Пароль = ПарольПользователяFTPСервера;
		ПараметрыFTP.Порт = НаборКонстант.ИнтеграцияС1САрхивомПортFTP;
		ПараметрыFTP.ПассивноеСоединение = НаборКонстант.ИнтеграцияС1САрхивомПассивныйРежимFTP;
		
		ТекстОшибки = ОбменСАрхивомТранспорт.ПроверитьОбменЧерезFTP(ПараметрыFTP);
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,, "НаборКонстант.ИнтеграцияС1САрхивомАдресFTP", Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ВладелецПароляFTPСервера = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Константы.ИнтеграцияС1САрхивомПользовательFTP);
	ВладелецПароляВебСервиса = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Константы.ИмяПользователя1САрхива);
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ВладелецПароляFTPСервера, ПарольПользователяFTPСервера);
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ВладелецПароляВебСервиса, ПарольПользователяВебСервиса);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("Закрыть") Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИнтеграцияС1САрхивомПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидТранспортаСетевойКаталогПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидТранспортаFTPСерверПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтеграцияС1САрхивомКаталогНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогОткрытия = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогОткрытия.МножественныйВыбор = Ложь;
	ДиалогОткрытия.Заголовок = НСтр("ru = 'Выберите каталог'");
	ДиалогОткрытия.Каталог = НаборКонстант.ИнтеграцияС1САрхивомКаталог;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИнтеграцияС1САрхивомКаталогПродолжениеВыбора", ЭтотОбъект);
	ДиалогОткрытия.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтеграцияС1САрхивомКаталогОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(НаборКонстант.ИнтеграцияС1САрхивомКаталог) Тогда
		ФайловаяСистемаКлиент.ОткрытьПроводник(НаборКонстант.ИнтеграцияС1САрхивомКаталог);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("Закрыть", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВебСервис(Команда)
	
	ЕстьНезаполненныеНастройки = Ложь;
	Если Не ЗначениеЗаполнено(НаборКонстант.АдресПубликации1САрхива) Тогда
		ЕстьНезаполненныеНастройки = Истина;
		ТекстОшибки = НСтр("ru = 'Не указан адрес публикации'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки,,, "НаборКонстант.АдресПубликации1САрхива");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НаборКонстант.ИмяПользователя1САрхива) Тогда
		ЕстьНезаполненныеНастройки = Истина;
		ТекстОшибки = НСтр("ru = 'Не указано имя пользователя'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки,,, "НаборКонстант.ИмяПользователя1САрхива");
	КонецЕсли;
	
	Если ЕстьНезаполненныеНастройки Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ПроверитьВебСервисНаСервере();
		ПоказатьПредупреждение(, НСтр("ru = 'Подключение выполнено'"));
	Исключение
		ТекстОшибки = НСтр("ru = 'Подключение не выполнено. Проверьте адрес, имя пользователя и пароль.'")
			+ Символы.ПС + Символы.ПС + ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИнтеграцияС1САрхивомКаталогПродолжениеВыбора(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Или Результат.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПапки = Результат[0];
	РазделительПути = ПолучитьРазделительПути();
	ИмяПапки = ИмяПапки + ?(Прав(ИмяПапки, 1) = РазделительПути, "", РазделительПути);
	
	НаборКонстант.ИнтеграцияС1САрхивомКаталог = ИмяПапки;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступность()
	
	Элементы.КодДляОбменаСАрхивом.Доступность = НаборКонстант.ИнтеграцияС1САрхивом;
	Элементы.ВерсияФорматаОбменаПоддерживаемая1САрхивом.Доступность = НаборКонстант.ИнтеграцияС1САрхивом;
	Элементы.ГруппаСтраницы.Доступность = НаборКонстант.ИнтеграцияС1САрхивом;
	
	ВидыТранспортаКаталог = ПредопределенноеЗначение("Перечисление.ВидыТранспортаДляОбменаС1САрхивом.ОбщийКаталог");
	ВидыТранспортаFTP = ПредопределенноеЗначение("Перечисление.ВидыТранспортаДляОбменаС1САрхивом.FTPСервер");
	
	Элементы.ИнтеграцияС1САрхивомКаталог.Доступность =
		НаборКонстант.ИнтеграцияС1САрхивомВидТранспорта = ВидыТранспортаКаталог;
	Элементы.ГруппаПараметрыПодключенияFTP.Доступность =	
		НаборКонстант.ИнтеграцияС1САрхивомВидТранспорта = ВидыТранспортаFTP;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВебСервисНаСервере()
	
	АдресПубликации = НаборКонстант.АдресПубликации1САрхива;
	ИмяПользователя = НаборКонстант.ИмяПользователя1САрхива;
	Пароль = ПарольПользователяВебСервиса;
	
	Таймаут = 10;
	Определение = Новый WSОпределения(АдресПубликации + "/ws/ArchiveDocuments.1cws?wsdl", ИмяПользователя, Пароль, Таймаут);
	Прокси = Новый WSПрокси(Определение, "http://www.1c.ru/docmng", "ArchiveDocuments", "ArchiveDocumentsSoap");
	
КонецПроцедуры

#КонецОбласти
