#Область ОписаниеПеременных

&НаКлиенте
Перем HTMLДокумент;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ПлюсСервис.Используется() Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Сервис 1С:Плюс недоступен для использования в данной конфигурации'"),,,, Отказ);
		Возврат;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	ЭтоУстановкаОбновлений = Параметры.ЭтоУстановкаОбновлений;
	ОжидаетсяОповещение = Параметры.ОжидаетсяОповещение;
	ЗаголовокПрограммы = Параметры.ЗаголовокПрограммы;
	
	АдресПрограммногоИнтерфейса = ПлюсСервис.АдресAPI();
	
	УстановкаРазрешена = Пользователи.ЭтоПолноправныйПользователь() 
		Или Пользователи.ЭтоПолноправныйПользователь(, Истина);
		
	Если ОбщегоНазначения.РазделениеВключено() 
		И Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		УстановкаРазрешена = Ложь;
	КонецЕсли;
	
	Если Не ЭтоУстановкаОбновлений Тогда
		ПараметрыПодключенияСервиса = ИнициализироватьСеансПодключения(УстановкаРазрешена, ЗаголовокПрограммы);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыПодключенияСервиса, "ИдентификаторСеанса, АдресИнтерфейса");
	КонецЕсли;
	
	Если УстановкаРазрешена Тогда
		ТекстДокумента = Обработки.ПлюсСервис.ПолучитьМакет("ОжиданиеУстановки").ПолучитьТекст();
		ОбновитьСостояниеРасширений();
	КонецЕсли;
	
	АдресХранилищаДляОбменаДанными = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если УстановкаРазрешена Тогда
		
		Если ЭтоУстановкаОбновлений Тогда
			ТребуетсяМонопольныйРежим = Истина;
			ПодключитьОбработчикОжидания("НачатьУстановку", 0.1, Истина);
			Возврат;
		КонецЕсли;
		
		ПодключитьОбработчикОжидания("ПодключитьОжиданиеУстановки", 0.1, Истина);
	КонецЕсли;
	
	Если Не ОжидаетсяОповещение Тогда
		ПерейтиПоНавигационнойСсылке(АдресИнтерфейса);
	КонецЕсли;
	
	Если Не УстановкаРазрешена Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если Не ИмяСобытия = ПлюсСервисСлужебныйКлиент.СобытиеОповещения()
		И Не ИмяСобытия = ПлюсСервисСлужебныйКлиент.СобытиеЗакрытияОкнаСостояния() Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяСобытия = ПлюсСервисСлужебныйКлиент.СобытиеЗакрытияОкнаСостояния() Тогда
		ОкноИнформацииОткрыто = Ложь;
		Возврат;
	КонецЕсли;
	
	Действие = Параметр; // см. ПлюсСервисСлужебныйКлиент.ДействиеРезультатаГлобальногоПоиска
	Если Действие.ДанныеРасширения.Установлено Тогда
		Поиск = Новый Структура;
		Поиск.Вставить("ИдВерсииРасширения",
			Новый УникальныйИдентификатор(Действие.ДанныеРасширения.ИдентификаторВерсии));
		РезультатПоиска = РасширенияКУстановке.НайтиСтроки(Поиск);
		Если РезультатПоиска.Количество() > 0 Тогда
			ИдентификаторСтроки = РезультатПоиска[0].ПолучитьИдентификатор();
			Элементы.РасширенияКУстановке.ТекущаяСтрока = ИдентификаторСтроки;
		КонецЕсли;
	КонецЕсли;

	Если Действие.Команда = ПлюсСервисКлиентСервер.КомандаПосмотретьОписание() Тогда
		АдресПерехода = СтрШаблон("%1&state=%2%3%4", АдресИнтерфейса, ПлюсСервисКлиентСервер.ПолеИдентификаторВерсии(),
			"%3D", Параметр.ДанныеРасширения.ИдентификаторВерсии);
		
		АдресПерехода = СтрШаблон("%1&utm_source=1Capp", АдресПерехода);
		Если Действие.Свойство("КаналUTM") Тогда
			АдресПерехода = СтрШаблон("%1&utm_medium=%2", АдресПерехода, Действие.КаналUTM);
		КонецЕсли;
			
		ПерейтиПоНавигационнойСсылке(АдресПерехода);
	ИначеЕсли Действие.Команда = ПлюсСервисКлиентСервер.КомандаУстановитьРасширение() Тогда
		ДобавитьРасширениеКУстановкеНаСервере(ИдентификаторСеанса, Параметр.ДанныеРасширения.ИдентификаторВерсии);
	ИначеЕсли Действие.Команда = ПлюсСервисКлиентСервер.КомандаОткрытьНастройки() Тогда
		ОткрытьФормуНастроек(
		 	Параметр.ДанныеРасширения.ПубличныйИдентификатор, Параметр.ДанныеРасширения.ИнтерфейсНастроек);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		ЗавершитьСеансПодключения();
		Возврат;
	КонецЕсли;
	
	Если ПодтверждениеЗакрытияПолучено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	Отказ = Истина;
	ПродолжитьЗакрытие();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Асинх Процедура ДокументПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ДанныеСобытия.Element.id <> "button1c" Тогда
		Возврат;
	КонецЕсли;
	
	Если HTMLДокумент.resultStatus = 499 Тогда
		Закрыть();
		Возврат;
	ИначеЕсли HTMLДокумент.resultStatus = 0 Тогда
		ПоказатьОкноИнформации(
			НСтр("ru = 'Сервер недоступен или не ответил вовремя. Повторить попытку?'"),
			БиблиотекаКартинок.СинхронизацияДанныхОтключение,
			Ложь,
			Истина);
		Возврат;	
	ИначеЕсли HTMLДокумент.resultStatus <> 200 Тогда
		ПоказатьОкноИнформации(
			НСтр("ru = 'Выполнение запроса завершилось с ошибкой. Повторить попытку?'"),
			БиблиотекаКартинок.СинхронизацияДанныхОтключение,
			Ложь,
			Истина);
		Возврат;
	КонецЕсли;
	
	НастройкиОткрытияФорм = Неопределено;
	ДанныеЗадания = Неопределено;
	
	ПрочитатьДанныеЗаданияJSON(HTMLДокумент.resultText, ДанныеЗадания, НастройкиОткрытияФорм);
	
	Если ЗначениеЗаполнено(НастройкиОткрытияФорм) Тогда
		ОткрытьФормыНастроек(НастройкиОткрытияФорм);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеЗадания.Extensions) Тогда
		ЗаписатьЗаданиеНаУстановку(ДанныеЗадания);
		НачатьУстановку();
	Иначе
		HTMLДокумент.subscribe();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументДокументСформирован(Элемент)
	
	Если ТипЗнч(Элемент.Документ.defaultView) = Тип("ВнешнийОбъект") Тогда
		HTMLДокумент = Элемент.Документ.defaultView;
	ИначеЕсли ТипЗнч(Элемент.Документ.parentWindow) = Тип("ВнешнийОбъект") Тогда
		HTMLДокумент = Элемент.Документ.parentWindow;
	Иначе
		ВызватьИсключение НСтр("ru = 'Не удалось определить тип браузера'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьИнформацияОбработкаНавигационнойСсылки(Элемент,
	НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ПустаяСтрока(ИдентификаторСеанса) Тогда
		ПараметрыПодключенияСервиса = ИнициализироватьСеансПодключения(УстановкаРазрешена, ЗаголовокПрограммы);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыПодключенияСервиса, "ИдентификаторСеанса, АдресИнтерфейса");
		
		ЭтоУстановкаОбновлений = Ложь;
		ПодключитьОжиданиеУстановки();
	КонецЕсли;
	
	ПерейтиПоНавигационнойСсылке(АдресИнтерфейса);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРасширенияКУстановке

&НаКлиенте
Процедура РасширенияКУстановкеАвтообновлениеОтключеноПриИзменении(Элемент)
	
	ИдРасширенияДляВосстановления = Элементы.РасширенияКУстановке.ТекущиеДанные.ИдРасширения;
	
	ПроверитьЗаданиеНаУстановку(ИдРасширенияДляВосстановления, ТребуетсяМонопольныйРежим);
	
	Элементы.РасширенияКУстановке.ТекущиеДанные.Автообновление =
		Не Элементы.РасширенияКУстановке.ТекущиеДанные.Автообновление;	
	
	ПараметрыНовойФормы = Новый Структура("ИдРасширения", ИдРасширенияДляВосстановления);
		
	ОткрытьФорму(
		"Обработка.ПлюсСервис.Форма.ФормаВключенияАвтообновления",
		ПараметрыНовойФормы,
		ЭтотОбъект,,,,
		Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыВключенияАвтообновления", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура РасширенияКУстановкеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РасширенияКУстановкеПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РасширенияКУстановкеПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.РасширенияКУстановке.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Элементы.РасширенияКУстановкеКонтекстноеМенюИзменитьРасписание.Видимость = Ложь;
	Иначе
		Элементы.РасширенияКУстановкеКонтекстноеМенюИзменитьРасписание.Видимость = ТекущиеДанные.ЕстьКомандыРасширения;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьСостояниеРасширений();
КонецПроцедуры

&НаКлиенте
Процедура ПовторитьПодключение(Команда)
	ПовторноеПодключение();
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВ1СПлюс(Команда)
	ПерейтиПоНавигационнойСсылке(АдресИнтерфейса);
КонецПроцедуры

&НаКлиенте
Асинх Процедура УдалитьРасширение(Команда)
	
	ТекущиеДанные = Элементы.РасширенияКУстановке.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Статус <> 1 
		И ТекущиеДанные.Статус <> 3 Тогда
		Возврат;
	КонецЕсли;
	
	Ответ = Ждать ВопросАсинх(
		СтрШаблон(НСтр("ru = 'Удалить расширение ""%1""?'"), ТекущиеДанные.Имя),
		РежимДиалогаВопрос.ДаНет,,,
		НСтр("ru = 'Удаление расширения'"), );
		
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеРасширения = Новый Структура();
	ДанныеРасширения.Вставить("ИдРасширения", ТекущиеДанные.ИдРасширения);
	ДанныеРасширения.Вставить("ИмяРасширения", ТекущиеДанные.Имя);
	ДанныеРасширения.Вставить("ВерсияРасширения", ТекущиеДанные.Версия);
	ДанныеРасширения.Вставить("ИдВерсииРасширения", ТекущиеДанные.ИдВерсииРасширения);
	
	Если ТребуетсяМонопольныйДоступ(ТекущиеДанные.ИдРасширения) Тогда
		ЭтоУстановка = Истина;
		ЭтоУдаление = Ложь;
		Если Не УстановитьМонопольныйРежимНаСервере(ЭтоУстановка, ЭтоУдаление) Тогда
			Оповещение = Новый ОписаниеОповещения(
				"ПродолжитьУдалениеРасширения",
				ЭтотОбъект,
				ДанныеРасширения);
			ОткрытьФорму(
				"Обработка.ПлюсСервис.Форма.ПредупреждениеМонопольныйРежим",
				Новый Структура("ЭтоУстановка, ЭтоУдаление", ЭтоУстановка, ЭтоУдаление),
				ЭтотОбъект,,,,
				Оповещение,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПродолжитьУдалениеРасширения(Истина, ДанныеРасширения);
	
КонецПроцедуры


&НаКлиенте
Процедура ИзменитьРасписание(Команда)
	
	ТекущиеДанные = Элементы.РасширенияКУстановке.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ИдРасширения", ТекущиеДанные.ИдРасширения);
	ОткрытьФорму("Обработка.ПлюсСервис.Форма.ФормаКомандыРасширения", 
		ПараметрыФормы, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.РасширенияКУстановкеСтатус.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РасширенияКУстановке.Статус");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;

	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Ожидание установки'"));

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.РасширенияКУстановкеСтатус.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РасширенияКУстановке.Статус");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 1;

	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Установлено'"));

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.РасширенияКУстановкеСтатус.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РасширенияКУстановке.Статус");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 2;

	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Требуется перезапуск'"));

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.РасширенияКУстановкеСтатус.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РасширенияКУстановке.Статус");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 3;

	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Ожидание установки обновления'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.РасширенияКУстановкеАвтообновление.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РасширенияКУстановке.Автообновление");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ИнициализироватьСеансПодключения(УстановкаРазрешена, ЗаголовокПрограммы)
	
	ПараметрыПодключенияСервиса = Новый Структура("ИдентификаторСеанса, АдресИнтерфейса");
	
	Попытка
		ПараметрыПодключенияСервиса.ИдентификаторСеанса = ПлюсСервис.СинхронизироватьСписокРасширенийССервисом(
			УстановкаРазрешена, ЗаголовокПрограммы);
		ПараметрыПодключенияСервиса.АдресИнтерфейса = ПлюсСервис.НавигационнаяСсылкаИнтерфейса(
			ПараметрыПодключенияСервиса.ИдентификаторСеанса);		 
	Исключение
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Расширения 1С:Плюс.Синхронизация списка расширений'"),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение НСтр("ru = 'Не удалось подключиться к серверу 1С:Плюс.'");
	КонецПопытки;
	
	Возврат ПараметрыПодключенияСервиса;

КонецФункции

&НаКлиенте
Процедура ЗавершитьСеансПодключения(ЗавершитьРаботуСФормойВзаимодействия = Ложь)
	Если Не ПустаяСтрока(ИдентификаторСеанса) Тогда
		HTMLДокумент.closeSession();
	ИначеЕсли ЗавершитьРаботуСФормойВзаимодействия Тогда
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьОжиданиеУстановки()
	Если Не УстановкаРазрешена Тогда
		Возврат;		
	КонецЕсли;
		
	Документ = ПолучитьТекстHTML();
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗакрытиеФормыВключенияАвтообновления(Знач РезультатРаботы, Знач ДопПараметры) Экспорт
	
	ОбновитьСостояниеРасширений();
	Если РезультатРаботы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НачатьВосстановлениеОригинальнойВерсииРасширения();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТребуетсяМонопольныйДоступ(Знач ИдРасширения)
	
	Возврат ПлюсСервис.РасширениеИзменяетСтруктуруДанных(ИдРасширения);
	
КонецФункции

&НаКлиенте
Процедура ОбработатьПродолжениеУдаленияРасширения()
	
	ДанныеРасширения = ДанныеУдаляемогоРасширения;
	УдалениеВыполненоУспешно = Истина;
	Попытка
		УдалитьРасширениеНаСервере(ДанныеРасширения);
		ОчиститьСообщения();
	Исключение
		УдалениеВыполненоУспешно = Ложь;
		ТекстОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ПоказатьОкноИнформации(
			НСтр("ru = 'Удаление завершилось ошибкой'"),
			БиблиотекаКартинок.ПлюсОшибка48,
			Ложь,
			Ложь,
			Ложь,
			ТекстОшибки);
		ОбновитьСостояниеРасширений();
	КонецПопытки;
			
	ЗавершитьУдалениеРасширения(УдалениеВыполненоУспешно);
		
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьРасширениеНаСервере(Знач ДанныеРасширения)
	
	ПлюсСервис.УдалитьРасширение(ДанныеРасширения); 
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПродолжитьЗакрытие()
	
	Результат = Ждать ВопросАсинх(
		НСтр("ru = 'Данное окно отвечает за взаимодействие с 1С:Плюс.
			|Если закрыть данное окно в процессе установки расширения, установка будет прервана.
			|Закрыть окно?'"),
		РежимДиалогаВопрос.ДаНет);
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПодтверждениеЗакрытияПолучено = Истина;
		ЗавершитьСеансПодключения(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОкноИнформации(ТекстОжидания, КартинкаДействия = Неопределено,
	ПоказатьКнопкиВопроса = Ложь, ПоказатьКнопкуПовторногоПодключения = Ложь,
	ЗакрытьФорму = Ложь, ТекстОшибки = "")
	
	Если КартинкаДействия = Неопределено Тогда
		КартинкаДействия = БиблиотекаКартинок.ПлюсПрогресс48;
	КонецЕсли;
	
	ПараметрыОтображения = Новый Структура();
	ПараметрыОтображения.Вставить("Картинка", КартинкаДействия);
	ПараметрыОтображения.Вставить("Текст", ТекстОжидания);
	ПараметрыОтображения.Вставить("ПоказатьКнопкиВопроса", ПоказатьКнопкиВопроса);
	ПараметрыОтображения.Вставить("ПоказатьКнопкуПовторногоПодключения", ПоказатьКнопкуПовторногоПодключения);
	ПараметрыОтображения.Вставить("ЗакрытьФорму", ЗакрытьФорму);
	ПараметрыОтображения.Вставить("ТекстОшибки", ТекстОшибки);
	
	ПараметрыНовойФормы = Новый Структура("АдресХранилищаДляОбменаДанными", АдресХранилищаДляОбменаДанными);
	ПараметрыНовойФормы.Вставить("АдресХранилищаДляОбменаДанными", АдресХранилищаДляОбменаДанными);
	ПараметрыНовойФормы.Вставить("НадписьОжидание", ТекстОжидания);
	ПоместитьВоВременноеХранилище(ПараметрыОтображения, АдресХранилищаДляОбменаДанными);
	
	Если ОкноИнформацииОткрыто Тогда
		Возврат;
	КонецЕсли;
	
	ОкноИнформацииОткрыто = Истина;
	ОткрытьФорму(
		"Обработка.ПлюсСервис.Форма.ФормаОжиданиеУстановки",
		ПараметрыНовойФормы,
		ЭтотОбъект,
		"УникальныйКлючДляОткрытияФормы",,,,
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		
КонецПроцедуры

&НаКлиенте
Процедура ПовторноеПодключение() Экспорт
	ПоказатьОкноИнформации("",,,, Истина);
	HTMLДокумент.subscribe();
КонецПроцедуры

&НаКлиенте
Процедура НачатьВосстановлениеОригинальнойВерсииРасширения()
	
	Если ТребуетсяМонопольныйРежим Тогда
		ЭтоУстановка = Истина;
		ЭтоУдаление = Ложь;
		Если Не УстановитьМонопольныйРежимНаСервере(ЭтоУстановка, ЭтоУдаление) Тогда
			Оповещение = Новый ОписаниеОповещения("ЗапуститьВосстановлениеОригинальнойВерсииРасширения", ЭтотОбъект);
			ОткрытьФорму(
				"Обработка.ПлюсСервис.Форма.ПредупреждениеМонопольныйРежим"
				,Новый Структура("ЭтоУстановка, ЭтоУдаление", ЭтоУстановка, ЭтоУдаление),
				ЭтотОбъект,,,,
				Оповещение,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗапуститьВосстановлениеОригинальнойВерсииРасширения(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьУстановку()
	
	Если ТребуетсяМонопольныйРежим Тогда
		ЭтоУстановка = Истина;
		ЭтоУдаление = Ложь;
		Если Не УстановитьМонопольныйРежимНаСервере(ЭтоУстановка, ЭтоУдаление) Тогда
			Оповещение = Новый ОписаниеОповещения("ПродолжитьУстановку", ЭтотОбъект);
			ОткрытьФорму(
				"Обработка.ПлюсСервис.Форма.ПредупреждениеМонопольныйРежим"
				,Новый Структура("ЭтоУстановка, ЭтоУдаление", ЭтоУстановка, ЭтоУдаление),
				ЭтотОбъект,,,,
				Оповещение,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗапуститьУстановкуРасширений();
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьУстановку(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Или Не Результат Тогда
		ПоказатьОкноИнформации(
			НСтр("ru = 'Расширение не установлено. Не удалось установить монопольный режим'"));
		ОтменитьУстановку();
	КонецЕсли;
		
	ЗапуститьУстановкуРасширений();
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьУстановку()
	
	УстановитьПривилегированныйРежим(Истина);
	ПлюсСервис.ОчиститьСписокРасширенийКУстановке(ИдентификаторСеанса);
	ОбновитьСостояниеРасширений();
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьУдалениеРасширения(Результат, ДанныеРасширения) Экспорт
	
	Если Результат = Неопределено Или Не Результат Тогда
		ПоказатьОкноИнформации(
			НСтр("ru = 'Расширение не удалено. Не удалось установить монопольный режим'"));
		Возврат;
	КонецЕсли;
	
	ПоказатьОкноИнформации(НСтр("ru = 'Удаление расширения'"),,,,);
	ДанныеУдаляемогоРасширения = ДанныеРасширения;
	ПодключитьОбработчикОжидания("ОбработатьПродолжениеУдаленияРасширения", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьУстановкуРасширений()
	
	ПоказатьОкноИнформации(НСтр("ru = 'Установка расширения'"));
	ПодключитьОбработчикОжидания("ПродолжитьУстановкуРасширений", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьВосстановлениеОригинальнойВерсииРасширения(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Или Не Результат Тогда
		ПоказатьОкноИнформации(
			НСтр("ru = 'Восстановление оригинальной версии расширения не выполнено. Не удалось установить монопольный режим'"));
		Возврат;
	КонецЕсли;
	
	ПоказатьОкноИнформации(НСтр("ru = 'Восстановление оригинальной версии расширения'"));
	ПодключитьОбработчикОжидания("ПродолжитьВосстановлениеОригинальнойВерсииРасширения", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьУстановкуРасширений()
	
	УстановкаВыполненаУспешно = Истина;
	Попытка
		ЗапуститьУстановкуРасширенийНаСервере();
		ОчиститьСообщения();
	Исключение
		УстановкаВыполненаУспешно = Ложь;
		ТекстОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ПоказатьОкноИнформации(
			НСтр("ru = 'Установка расширения завершилась ошибкой'"),
			БиблиотекаКартинок.ПлюсОшибка48,
			Ложь,
			Ложь,
			Ложь,
			ТекстОшибки);
		ОбновитьСостояниеРасширений();
	КонецПопытки;
			
	ЗавершитьУстановкуРасширений(УстановкаВыполненаУспешно);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьВосстановлениеОригинальнойВерсииРасширения()
	
	ВосстановлениеВыполненоУспешно = Истина;
	Попытка
		ЗапуститьВосстановлениеОригинальнойВерсииРасширенияНаСервере();
	Исключение
		ВосстановлениеВыполненоУспешно = Ложь;
		ТекстОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ПоказатьОкноИнформации(
			НСтр("ru = 'Восстановление оригинальной версии завершилось ошибкой'"),
			БиблиотекаКартинок.ПлюсОшибка48,
			Ложь,
			Ложь,
			Ложь,
			ТекстОшибки);
		ОбновитьСостояниеРасширений();
	КонецПопытки;
			
	ЗавершитьВосстановлениеОригинальнойВерсииРасширения(ВосстановлениеВыполненоУспешно);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСостояниеРасширений()
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ВложенныйЗапрос.ИдРасширения КАК ИдРасширения,
	|	МАКСИМУМ(ВложенныйЗапрос.ИдВерсииРасширения) КАК ИдВерсииРасширения,
	|	ВложенныйЗапрос.ИмяРасширения КАК Имя,
	|	МАКСИМУМ(ВложенныйЗапрос.ВерсияРасширения) КАК Версия,
	|	МАКСИМУМ(ВложенныйЗапрос.ДатаУстановкиОбновления) КАК ДатаУстановки,
	|	МИНИМУМ(ВложенныйЗапрос.АвтообновлениеОтключено) КАК АвтообновлениеОтключено,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ВложенныйЗапрос.Установлено) ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА МАКСИМУМ(ВложенныйЗапрос.КУстановке) ЕСТЬ NULL
	|			ТОГДА 1
	|		КОГДА МАКСИМУМ(ВложенныйЗапрос.Установлено)
	|		И МАКСИМУМ(ВложенныйЗапрос.КУстановке)
	|			ТОГДА 3
	|	КОНЕЦ КАК Статус
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПлюсУстановленныеРасширения.ИдРасширения КАК ИдРасширения,
	|		ПлюсУстановленныеРасширения.ИдВерсииРасширения КАК ИдВерсииРасширения,
	|		ПлюсУстановленныеРасширения.ДатаУстановкиОбновления КАК ДатаУстановкиОбновления,
	|		ПлюсУстановленныеРасширения.ИмяРасширения КАК ИмяРасширения,
	|		ПлюсУстановленныеРасширения.ВерсияРасширения КАК ВерсияРасширения,
	|		ПлюсУстановленныеРасширения.АвтообновлениеОтключено КАК АвтообновлениеОтключено,
	|		ИСТИНА КАК Установлено,
	|		NULL КАК КУстановке
	|	ИЗ
	|		РегистрСведений.ПлюсУстановленныеРасширения КАК ПлюсУстановленныеРасширения
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ПлюсРасширенияКУстановке.ИдРасширения,
	|		ПлюсРасширенияКУстановке.ИдВерсииРасширения,
	|		ДАТАВРЕМЯ(1, 1, 1),
	|		ПлюсРасширенияКУстановке.ИмяРасширения,
	|		ПлюсРасширенияКУстановке.ВерсияРасширения,
	|		ЛОЖЬ,
	|		NULL,
	|		ИСТИНА
	|	ИЗ
	|		РегистрСведений.ПлюсРасширенияКУстановке КАК ПлюсРасширенияКУстановке) КАК ВложенныйЗапрос
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ИдРасширения,
	|	ВложенныйЗапрос.ИмяРасширения");
	
	Отбор = Новый Структура;
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Отбор.Вставить("ИмяМетода", Метаданные.РегламентныеЗадания.ПлюсЗапускЗаданийРасширений.ИмяМетода);
	Иначе
		Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ПлюсЗапускЗаданийРасширений);
	КонецЕсли;
	
	Задания = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	
	РасширенияКУстановке.Очистить();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НовСтрока = РасширенияКУстановке.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрока, Выборка);
		НовСтрока.Автообновление = Не Выборка.АвтообновлениеОтключено;
		
		Если Выборка.Статус = 1 И УстановленныеВСеансеРасширения.Количество() <> 0 Тогда
			Отбор = Новый Структура("ИдВерсииРасширения", Выборка.ИдВерсииРасширения);
			Если УстановленныеВСеансеРасширения.НайтиСтроки(Отбор).Количество() > 0 Тогда
				Выборка.Статус = 2;
			КонецЕсли;
		КонецЕсли;
		
		Для Каждого Задание Из Задания Цикл
			Если Задание.Параметры.Количество() <> 0 И Задание.Параметры[0] = Строка(Выборка.ИдРасширения) Тогда
				НовСтрока.ЕстьКомандыРасширения = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗавершитьУстановкуРасширений(УстановкаЗавершенаУспешно = Истина) Экспорт
	
	Если УстановкаЗавершенаУспешно Тогда
		ОбновитьСостояниеРасширений();
		ПоказатьОкноИнформации(
			НСтр("ru = 'Установка расширения завершена.
			|Перезапустить приложение?'"),
			БиблиотекаКартинок.ПлюсУспех48,
			Истина);
	КонецЕсли;
	
	ПлюсСервисКлиент.СнятьМонопольныйРежим();
	ТребуетсяМонопольныйРежим = Ложь;
	
	Если Не ЭтоУстановкаОбновлений Тогда
		HTMLДокумент.subscribe();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВосстановлениеОригинальнойВерсииРасширения(ВосстановлениеВыполненоУспешно = Истина) Экспорт
	
	Если ВосстановлениеВыполненоУспешно Тогда
		ОбновитьСостояниеРасширений();
		ПоказатьОкноИнформации(
			НСтр("ru = 'Оригинальная версия расширения восстановлена.
			|Перезапустить приложение?'"),
			БиблиотекаКартинок.ПлюсУспех48,
			Истина);
	КонецЕсли;
	
	ПлюсСервисКлиент.СнятьМонопольныйРежим();
	ТребуетсяМонопольныйРежим = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьУдалениеРасширения(УдалениеВыполненоУспешно = Истина) Экспорт
	
	Если УдалениеВыполненоУспешно Тогда
		ОбновитьСостояниеРасширений();
		ПоказатьОкноИнформации(
			НСтр("ru = 'Расширение удалено.
			|Перезапустить приложение?'"),
			БиблиотекаКартинок.ПлюсУспех48,
			Истина);
	КонецЕсли;
	
	ПлюсСервисКлиент.СнятьМонопольныйРежим();
	ТребуетсяМонопольныйРежим = Ложь;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьТекстHTML()
	
	ДлительностьВызова = 30;
	ТаймаутВызова = (ДлительностьВызова + 10) * 1000;
	Путь = АдресПрограммногоИнтерфейса + СтрШаблон("/%1/WaitingInstall", ИдентификаторСеанса);
	ПутьКЗакрытию = АдресПрограммногоИнтерфейса + СтрШаблон("/%1/Close", ИдентификаторСеанса);
	
	Возврат СтрШаблон(ТекстДокумента, 
			Путь, 
			Формат(ДлительностьВызова, "ЧГ=0;"), 
			Формат(ТаймаутВызова, "ЧГ=0;"),
			ПутьКЗакрытию);
			
КонецФункции

&НаСервере
Процедура ЗаписатьЗаданиеНаУстановку(Знач ЗаданиеНаУстановку)
	
	НайденыРРД = Ложь;
	ПлюсСервис.ЗаписатьЗаданиеНаУстановку(ЗаданиеНаУстановку, ИдентификаторСеанса, НайденыРРД);
	ТребуетсяМонопольныйРежим = НайденыРРД;
	
	ОбновитьСостояниеРасширений();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьРасширениеКУстановкеНаСервере(ИдентификаторСеанса, ИдентификаторВерсии)
	
	ПлюсСервис.ДобавитьРасширениеКУстановке(ИдентификаторСеанса, ИдентификаторВерсии);
	
КонецПроцедуры
	
&НаСервере	
Процедура ПрочитатьДанныеЗаданияJSON(ТелоОтвета, ДанныеЗадания, НастройкиОткрытияФорм)
	
	Попытка
		ДанныеЗадания = ОбщегоНазначения.JSONВЗначение(ТелоОтвета,, Ложь);
		НастройкиОткрытияФорм = Новый Массив;
		Расширения = ДанныеЗадания.Extensions;

		КоличествоЭлементов = Расширения.Количество();
		Для ОбратныйИндекс = 1 По КоличествоЭлементов Цикл
			Элемент = Расширения[КоличествоЭлементов - ОбратныйИндекс];
			Если Элемент.OpenSettings Тогда
				НастройкиОткрытияФормы = НовыйНастройкиОткрытияФормы();
				НастройкиОткрытияФормы.ПубличныйИдентификатор = ОсновнойПубличныйИдентификаторРасширения(Элемент);
				НастройкиОткрытияФормы.ИнтерфейсНастроек = Элемент.SettingsInterface;
				НастройкиОткрытияФормы.ДанныеДляУдаления = Новый Структура("ExtId, ExtVersionId",
					Элемент.ExtId, Элемент.ExtVersionId);
				
				НастройкиОткрытияФорм.Добавить(НастройкиОткрытияФормы);
				Расширения.Удалить(КоличествоЭлементов - ОбратныйИндекс);
			КонецЕсли;
		КонецЦикла;
	Исключение
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Расширения 1С:Плюс.Чтение данных для установки'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка, , ,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция ОсновнойПубличныйИдентификаторРасширения(ДанныеРасширения)
	
	Если ДанныеРасширения.Свойство("PublicIds") И ДанныеРасширения.PublicIds.Количество() > 0 Тогда
		Возврат ДанныеРасширения.PublicIds[0];
	Иначе
		ВызватьИсключение НСтр("ru = 'Не заполнен публичный идентификатор'");
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗапуститьУстановкуРасширенийНаСервере()
	
	ПлюсСервис.УстановитьРасширения(ЭтоУстановкаОбновлений);

КонецПроцедуры

&НаСервере
Процедура ЗапуститьВосстановлениеОригинальнойВерсииРасширенияНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Менеджер = РегистрыСведений.ПлюсУстановленныеРасширения.СоздатьМенеджерЗаписи();
	Менеджер.ИдРасширения = ИдРасширенияДляВосстановления;
	Менеджер.Прочитать();
	Если Не Менеджер.Выбран() Тогда
		ВызватьИсключение НСтр("ru = 'Не удалось получить данные расширения.'");
	КонецЕсли;
	
	Задание = Менеджер.ЗаданиеНаУстановку.Получить();
	ПлюсСервис.УстановитьРасширение(Задание);
	Менеджер.АвтообновлениеОтключено = Ложь;
	Менеджер.Записать();	

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПроверитьЗаданиеНаУстановку(ИдРасширения, ТребуетсяМонопольныйРежим)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Менеджер = РегистрыСведений.ПлюсУстановленныеРасширения.СоздатьМенеджерЗаписи();
	Менеджер.ИдРасширения = ИдРасширения;
	Менеджер.Прочитать();
	Если Не Менеджер.Выбран() Тогда
		ВызватьИсключение НСтр("ru = 'Не удалось получить данные расширения.'");
	КонецЕсли;
	
	Задание = Менеджер.ЗаданиеНаУстановку.Получить();
	ТребуетсяМонопольныйРежим = Задание.ModifiesDataStructure;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормыНастроек(НастройкиОткрытияФорм)
	
	Для Каждого Настройка Из НастройкиОткрытияФорм Цикл

		ОткрытьФормуНастроек(Настройка.ПубличныйИдентификатор, Настройка.ИнтерфейсНастроек);
		УдалитьЗаданиеИзКорзины(Настройка.ДанныеДляУдаления, ИдентификаторСеанса);

	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастроек(ПубличныйИдентификатор, ИнтерфейсНастроек)

	СтандартнаяОбработка = Истина;
	ПлюсСервисКлиентПереопределяемый.ПриОткрытииФормыНастроек(ПубличныйИдентификатор, СтандартнаяОбработка);

	Если Не СтандартнаяОбработка Или Не ЗначениеЗаполнено(ИнтерфейсНастроек) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(ИнтерфейсНастроек, "/") Тогда
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(ИнтерфейсНастроек);
	Иначе
		//@skip-check module-unused-local-variable - особенность реализации
		Результат = Вычислить(ИнтерфейсНастроек);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция НовыйНастройкиОткрытияФормы()
	
	НастройкиОткрытияФормы = Новый Структура();
	НастройкиОткрытияФормы.Вставить("ПубличныйИдентификатор", "");
	НастройкиОткрытияФормы.Вставить("ИнтерфейсНастроек", "");
	НастройкиОткрытияФормы.Вставить("ДанныеДляУдаления", Новый Структура()); 
	
	Возврат НастройкиОткрытияФормы;
	 
КонецФункции

&НаСервереБезКонтекста
Процедура УдалитьЗаданиеИзКорзины(ДанныеДляУдаления, ИдентификаторСеанса)
	
	ПлюсСервис.УдалитьРасширениеИзКорзины(ДанныеДляУдаления, ИдентификаторСеанса);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УстановитьМонопольныйРежимНаСервере(ЭтоУстановка, ЭтоУдаление)
	
	Возврат Обработки.ПлюсСервис.УстановитьМонопольныйРежимНаСервере(ЭтоУстановка, ЭтоУдаление);
	
КонецФункции

#КонецОбласти


