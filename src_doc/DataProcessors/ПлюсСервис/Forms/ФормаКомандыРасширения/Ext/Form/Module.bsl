#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИдРасширения = Строка(Параметры.ИдРасширения);
	
	УстановитьПривилегированныйРежим(Истина);
	Отбор = Новый Структура;
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Отбор.Вставить("ИмяМетода", Метаданные.РегламентныеЗадания.ПлюсЗапускЗаданийРасширений.ИмяМетода);
	Иначе
		Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ПлюсЗапускЗаданийРасширений);
	КонецЕсли;
	
	Задания = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	
	Для Каждого Задание Из Задания Цикл
		Если Задание.Параметры.Количество() <> 0 И Задание.Параметры[0] = ИдРасширения Тогда
			НовСтрока = КомандыРасширения.Добавить();
			НовСтрока.Использование = Задание.Использование;
			НовСтрока.Расписание = Задание.Расписание;
			НовСтрока.Представление = Задание.Ключ;
			НовСтрока.Идентификатор = Задание.Параметры[1];
		КонецЕсли;
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
	КомандыРасширения.Сортировать("Представление");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКомандыРасширения

&НаКлиенте
Процедура КомандыРасширенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.КомандыРасширенияРасписание Тогда
		ТекущиеДанные = Элементы.КомандыРасширения.ТекущиеДанные;
		Расписание = ТекущиеДанные.Расписание;
		
		Если Расписание = Неопределено Тогда
			Расписание = Новый РасписаниеРегламентногоЗадания;
			Расписание.ПериодПовтораВТечениеДня = 600;
			Расписание.ПериодПовтораДней = 1;
		КонецЕсли;
		
		Диалог = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
		Диалог.Показать(Новый ОписаниеОповещения("ПослеРедактированияРасписания", ЭтотОбъект));
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КомандыРасширенияПередНачаломИзменения(Элемент, Отказ)

	Если Элемент.ТекущийЭлемент = Элементы.КомандыРасширенияИспользование Тогда
		ТекущиеДанные = Элементы.КомандыРасширения.ТекущиеДанные;
		Если ТекущиеДанные.Использование Тогда
			ОбновитьРегламентноеЗадание(ТекущиеДанные.Идентификатор, Неопределено);
			ТекущиеДанные.Расписание = Неопределено;
		Иначе
			Отказ = Истина;
			Расписание = ТекущиеДанные.Расписание;
			
			Если Расписание = Неопределено Тогда
				Расписание = Новый РасписаниеРегламентногоЗадания;
				Расписание.ПериодПовтораВТечениеДня = 600;
				Расписание.ПериодПовтораДней = 1;
			КонецЕсли;
			
			Диалог = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
			Диалог.Показать(Новый ОписаниеОповещения("ПослеРедактированияРасписания", ЭтотОбъект));
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеРедактированияРасписания(Расписание, ДополнительныеПараметры) Экспорт
	
	ТекущиеДанные = Элементы.КомандыРасширения.ТекущиеДанные;
	
	Если Расписание <> Неопределено Тогда
		ОбновитьРегламентноеЗадание(ТекущиеДанные.Идентификатор, Расписание);
		ТекущиеДанные.Расписание = Расписание;
	КонецЕсли;
	
	ТекущиеДанные.Использование = ТекущиеДанные.Расписание <> Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьРегламентноеЗадание(Идентификатор, Расписание)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Задание = Неопределено;
	Отбор = Новый Структура;
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Отбор.Вставить("ИмяМетода", Метаданные.РегламентныеЗадания.ПлюсЗапускЗаданийРасширений.ИмяМетода);
	Иначе
		Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ПлюсЗапускЗаданийРасширений);
	КонецЕсли;
	
	Задания = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	
	Для Каждого СтрокаЗадание Из Задания Цикл
		Если СтрокаЗадание.Параметры.Количество() <> 0 
			И СтрокаЗадание.Параметры[0] = ИдРасширения 
			И СтрокаЗадание.Ключ = Идентификатор Тогда
			
			Задание = СтрокаЗадание;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Расписание <> Неопределено Тогда
		
		Если ОбщегоНазначения.РазделениеВключено()
			И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда

			Если Не Константы.РазрешитьВыполнениеДООРегламентнымиЗаданиямиВМоделиСервиса.Получить() Тогда
				ВызватьИсключение НСтр(
					"ru = 'Периодическое выполнение команд расширений в качестве заданий запрещено администратором сервиса.'");
			КонецЕсли;

			МинимальныйИнтервал = Константы.МинимальныйИнтервалРегламентныхЗаданийДОИОВМоделиСервиса.Получить();
			ИсходнаяДата = ТекущаяДатаСеанса();
			ПроверяемаяДата = ИсходнаяДата + МинимальныйИнтервал - 1;

			Если Расписание.ТребуетсяВыполнение(ПроверяемаяДата, ИсходнаяДата) Тогда
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр(
					"ru = 'Расписание, задаваемое для выполнения команд расширения в качестве заданий, должно быть не чаще, чем 1 раз в %1 секунд.'"),
					МинимальныйИнтервал);
			КонецЕсли;

		КонецЕсли;
			
		Если Задание = Неопределено Тогда
			
			ПараметрыМетода = Новый Массив;
			ПараметрыМетода.Добавить(ИдРасширения);
			ПараметрыМетода.Добавить(Идентификатор);
			ПараметрыЗадания = Новый Структура;
			ПараметрыЗадания.Вставить("Использование", Истина);
			ПараметрыЗадания.Вставить("Ключ", Идентификатор);
			ПараметрыЗадания.Вставить("Параметры", ПараметрыМетода);
			ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ПлюсЗапускЗаданийРасширений);
			ПараметрыЗадания.Вставить("Расписание", Расписание);
			
			РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
		Иначе
			ПараметрыЗадания = Новый Структура;
			ПараметрыЗадания.Вставить("Расписание", Расписание);
			ПараметрыЗадания.Вставить("Использование", Истина);
			
			РегламентныеЗаданияСервер.ИзменитьЗадание(Задание, ПараметрыЗадания);
		КонецЕсли;
		
	ИначеЕсли Задание <> Неопределено Тогда
		ПараметрыЗадания = Новый Структура;
		ПараметрыЗадания.Вставить("Использование", Ложь);
		ПараметрыЗадания.Вставить("Расписание", Новый РасписаниеРегламентногоЗадания);
		
		РегламентныеЗаданияСервер.ИзменитьЗадание(Задание, ПараметрыЗадания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
