
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("АдресХранилищаДляОбменаДанными", АдресХранилищаДляОбменаДанными);
	
	Элементы.НадписьОжидание.Заголовок = Параметры.НадписьОжидание;
	
	Элементы.ГруппаКнопки.Видимость = Ложь;
	Элементы.ПовторитьПодключение.Видимость = Ложь;
	Элементы.ТекстОшибки.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ОбработатьДействиеСФормой", 1);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Оповестить(ПлюсСервисСлужебныйКлиент.СобытиеЗакрытияОкнаСостояния(), Истина, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерезапуститьПриложение(Команда)

	ЗавершитьРаботуСистемы(Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПовторитьПодключение(Команда)
	
	ВладелецФормы.ПовторноеПодключение();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьДействиеСФормой()
	
	ОчиститьСообщения();
	Данные = ПолучитьИзВременногоХранилища(АдресХранилищаДляОбменаДанными);
	Если Данные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Неопределено, АдресХранилищаДляОбменаДанными);
	ОтключитьОбработчикОжидания("ОбработатьДействиеСФормой");
	
	ОтобразитьИнформацию(Данные);
	
	Если Данные.ЗакрытьФорму Тогда
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ОбработатьДействиеСФормой", 1);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьИнформацию(Данные)
	
	Элементы.КартинкаОжидание.Картинка = Данные.Картинка;
	Элементы.НадписьОжидание.Заголовок = Данные.Текст;
	Элементы.ГруппаКнопки.Видимость = Данные.ПоказатьКнопкиВопроса;
	Элементы.ПовторитьПодключение.Видимость = Данные.ПоказатьКнопкуПовторногоПодключения;
	
	ТекстОшибки = Данные.ТекстОшибки;
	Элементы.ТекстОшибки.Видимость = Не ПустаяСтрока(ТекстОшибки);
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		Высота = 25;
		Ширина = 60;
	КонецЕсли;
	
	Если Данные.ЗакрытьФорму Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти