
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("ИдРасширения") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ИдРасширения = Параметры.ИдРасширения;
	
	УстановитьПривилегированныйРежим(Истина);
	Менеджер = РегистрыСведений.ПлюсУстановленныеРасширения.СоздатьМенеджерЗаписи();
	Менеджер.ИдРасширения = ИдРасширения;
	Менеджер.Прочитать();
	
	Если Не Менеджер.Выбран() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ВерсияРасширения = Менеджер.ВерсияРасширения;
	НаименованиеРасширения = Менеджер.ИмяРасширения;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
	
&НаКлиенте
Процедура СохранитьТекущуюВерсиюРасширенияНаПК(Команда)
		
	СкачатьФайлТекущейВерсии();
	
КонецПроцедуры
	
&НаКлиенте
Асинх Процедура ВключитьАвтообновление(Команда)
	
	Если Не ИзмененнаяВерсияСохранена Тогда
		Результат = Ждать ВопросАсинх(
			НСтр("ru = 'Файл используемой версии расширения не сохранен.
				|При восстановлении оригинальной версии расширения, используемая версия расширения будет заменена.
				|Сохранить файл используемой версии расширения?'"),
			РежимДиалогаВопрос.ДаНет,,,
			НСтр("ru = 'Сохранение используемой версии расширения'"));
		
		Если Результат = КодВозвратаДиалога.Да Тогда
			СкачатьФайлТекущейВерсии();
		КонецЕсли;
		
	КонецЕсли;
	
	НачатьВосстановлениеОригинальнойВерсииРасширения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СкачатьФайлТекущейВерсии()
		
	Оповещение = Новый ОписаниеОповещения("СохранитьТекущуюВерсиюРасширения", ЭтотОбъект);
	Если ЭтоВебКлиент() Тогда
		ТекстПредложения = НСтр("ru = 'Файл выгрузки может оказаться большим. В этом случае потребуется расширение для работы с 1С:Предприятием.
								|С этим расширением работа в веб-клиенте станет удобней не только при работе с большими файлами.'");
		ФайловаяСистемаКлиент.ПодключитьРасширениеДляРаботыСФайлами(Оповещение, ТекстПредложения);
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, Истина);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Асинх Процедура СохранитьТекущуюВерсиюРасширения(Подключено, ДополнительныеПараметры) Экспорт
		
	РасширениеРаботыСФайламиПодключено = Подключено;
	
	Если ЭтоВебКлиент() И Не РасширениеРаботыСФайламиПодключено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не подключено расширение работы с файлами. 
			|Сохранение текущей версии расширения не выполнено'"));
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанныеФайла = ПолучитьДанныеРасширения(ИдРасширения);
	Если ДвоичныеДанныеФайла = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не удалось получить данные расширения'"));
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Заголовок = НСтр("ru = 'Получение файла выгрузки'");
	Диалог.ПолноеИмяФайла = СтрШаблон("%1.cfe", НаименованиеРасширения);
	Диалог.Фильтр = СтрШаблон(НСтр("ru = 'Расширения %1'"), "(*.cfe)|*.cfe");
	Результат = Ждать Диалог.ВыбратьАсинх();
	Если Результат = Неопределено Или Результат.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайлаНаКлиенте = Результат[0]; 
	ПотокЗаписи = Ждать ФайловыеПотоки.ОткрытьАсинх(ИмяФайлаНаКлиенте, РежимОткрытияФайла.Создать, ДоступКФайлу.Запись);
	ЗаписьДанных = Новый ЗаписьДанных(ПотокЗаписи);
	ЗаписьДанных.ЗаписатьАсинх(ДвоичныеДанныеФайла);
	ЗаписьДанных.ЗакрытьАсинх();
	ПотокЗаписи.ЗакрытьАсинх();
	ИзмененнаяВерсияСохранена = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьВосстановлениеОригинальнойВерсииРасширения()
	
	Закрыть(Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеРасширения(Знач ИдРасширения)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	ПлюсУстановленныеРасширения.ИдРасширенияВИБ
	|ИЗ
	|	РегистрСведений.ПлюсУстановленныеРасширения КАК ПлюсУстановленныеРасширения
	|ГДЕ
	|	ПлюсУстановленныеРасширения.ИдРасширения = &ИдРасширения";
	
	Запрос.УстановитьПараметр("ИдРасширения", ИдРасширения);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	УстановитьПривилегированныйРежим(Истина);
	РасширенияБД = РасширенияКонфигурации.Получить(
		Новый Структура("УникальныйИдентификатор", Выборка.ИдРасширенияВИБ),
		ИсточникРасширенийКонфигурации.БазаДанных);
		
	Если РасширенияБД.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РасширенияБД[0].ПолучитьДанные();
	
КонецФункции

&НаКлиенте
Функция ЭтоВебКлиент()
	
	#Если ВебКлиент Тогда
		Возврат Истина;
	#КонецЕсли
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти
