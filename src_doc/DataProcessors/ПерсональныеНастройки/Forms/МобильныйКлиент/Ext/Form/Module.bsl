#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	//Вариант ОС по умолчанию
	ВариантОС = "iOS";
	ЗаполнитьСсылкиНаИсточникиРаспространения();

	СформироватьQRAndroid();

	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();

	Элементы.АдресПубликацииИБ.Видимость = ЭтоПолноПравныйПользователь;
	
	Если Не ЗначениеЗаполнено(АдресПубликацииИБ) Тогда
		
		СформироватьНадписьНеЗаполненИБ(ЭтоПолноправныйПользователь,
			Элементы.ДекорацияПодключениеНеЗаполненАдресИБ);
	
	КонецЕсли;
	
	СформироватьQRКодНастройкиПодключения();

КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)

	НастройкаМобильныйКлиентУстановлен = Настройки.Получить("МобильныйКлиентУстановлен");

	Если НастройкаМобильныйКлиентУстановлен = Истина Тогда
		Элементы.СтраницыОсновное.ТекущаяСтраница = Элементы.СтраницаПодключение;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти //ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантОСПриИзменении(Элемент)

	УстановитьТекущуюСтраницуОС(ЭтотОбъект);

	Элементы.AndroidStore.Видимость = ВариантОС = "Android";

КонецПроцедуры

&НаКлиенте
Процедура AndroidStoreОткрытиеМножественногоЗначения(Элемент, Идентификатор, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ВыбраннаяСтрока = AndroidStore.НайтиПоИдентификатору(Идентификатор);

	Для Каждого Строка Из AndroidStore Цикл
		Строка.Активно = (Строка.Значение = ВыбраннаяСтрока.Значение);
	КонецЦикла;

	СформироватьQRAndroid();

КонецПроцедуры

&НаКлиенте
Процедура МобильныйКлиентУжеУстановленПриИзменении(Элемент)

	Если МобильныйКлиентУстановлен Тогда
		Элементы.СтраницыОсновное.ТекущаяСтраница = Элементы.СтраницаПодключение;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура АдресПубликацииИБПриИзменении(Элемент)

	ИзменитьАдресПубликацииИБ(АдресПубликацииИБ);
	СформироватьQRКодНастройкиПодключения();

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияУстановкаiOSПодсказкаНажатие(Элемент)

	ЗапуститьПриложение(СсылкаAppStore);

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияУстановкаAndroidПодсказкаНажатие(Элемент)

	ЗапуститьПриложение(СсылкаAndroidStore);

КонецПроцедуры

#КонецОбласти //ОбработчикиСобытийЭлементовШапкиФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура КопироватьАдресСсылкиAppStoreВБуфер(Команда)
	
	Если СредстваБуфераОбмена.ИспользованиеДоступно() Тогда
		
		ЭлементБуфераОбмена = Новый ЭлементБуфераОбмена(СтандартныйФорматДанныхБуфераОбмена.Текст,
			СсылкаAppStore);

		ДанныеПомещены = Ждать СредстваБуфераОбмена.ПоместитьДанныеАсинх(ЭлементБуфераОбмена);
		
		Если ДанныеПомещены <> Неопределено Тогда
			ПоказатьОповещениеПользователя(НСтр("ru ='Ссылка скопирована'"));
		КонецЕсли;
	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Асинх Процедура КопироватьАдресСсылкиAndroidStoreВБуфер(Команда)

	Если СредстваБуфераОбмена.ИспользованиеДоступно() Тогда
		
		ЭлементБуфераОбмена = Новый ЭлементБуфераОбмена(СтандартныйФорматДанныхБуфераОбмена.Текст,
			СсылкаAndroidStore);

		ДанныеПомещены = Ждать СредстваБуфераОбмена.ПоместитьДанныеАсинх(ЭлементБуфераОбмена);
		
		Если ДанныеПомещены <> Неопределено Тогда
			ПоказатьОповещениеПользователя(НСтр("ru ='Ссылка скопирована'"));
		КонецЕсли;
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти //ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

//Генерирует картинку-QR код по строковому значению параметра "ДанныеКода"
// Параметры:
//  ДанныеКода - Строка - Данные для генерации QR кода
//
&НаСервереБезКонтекста
Функция QRКод(Знач ДанныеКода)

	QRКодДанные = ГенерацияШтрихкода.ДанныеQRКода(ДанныеКода, 0, 250);
	Возврат ПоместитьВоВременноеХранилище(QRКодДанные, Новый УникальныйИдентификатор);

КонецФункции

&НаСервере
Процедура ЗаполнитьСсылкиНаИсточникиРаспространения()

	//AppStore
	СсылкаAppStore = "https://apps.apple.com/ru/app/1с-документооборот-мк/id1512412622";
	QRiOS = QRКод(СсылкаAppStore);
	
	//Google Play - по умолчанию
	НоваяСтрокаStore = AndroidStore.Добавить();
	НоваяСтрокаStore.Значение =
		"https://play.google.com/store/apps/details?id=com.e1c.mobile.client.doc21";
	НоваяСтрокаStore.Представление = "Google Play";
	НоваяСтрокаStore.Активно = Истина;
	
	//RuStore
	НоваяСтрокаStore = AndroidStore.Добавить();
	НоваяСтрокаStore.Значение = "https://apps.rustore.ru/app/com.e1c.mobile.client.doc21";
	НоваяСтрокаStore.Представление = "RuStore";
	
	//NasheStore
	НоваяСтрокаStore = AndroidStore.Добавить();
	НоваяСтрокаStore.Значение = "https://store.nashstore.ru/store/62b42c420a39b21493a7c05d";
	НоваяСтрокаStore.Представление = "NasheStore";

КонецПроцедуры

&НаСервере
Процедура СформироватьQRAndroid()

	АктивныеСтроки = AndroidStore.НайтиСтроки(Новый Структура("Активно", Истина));

	Если АктивныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	АктивныйСтор = АктивныеСтроки[0];

	Элементы.ДекорацияУстановкаAndroidПодсказка.Заголовок = СтрШаблон(НСтр(
		"ru = 'Приложение в ""%1"" (Android)'"),
		АктивныйСтор.Представление);
	
	СсылкаAndroidStore = АктивныйСтор.Значение;
	
	QRAndroid = QRКод(СсылкаAndroidStore);

КонецПроцедуры

&НаСервере
Процедура СформироватьQRКодНастройкиПодключения()

	УстановитьПривилегированныйРежим(Истина);
	АдресПубликацииИБ = Константы.АдресПубликацииНаВебСервере.Получить();
	УстановитьПривилегированныйРежим(Ложь);

	Если Не ЗначениеЗаполнено(АдресПубликацииИБ) Тогда
		Элементы.СтраницыПодкючениеКИБ.ТекущаяСтраница = Элементы.СтраницаПодключениеНеЗаполненАдресПубликации;
		Возврат;
	Иначе
		Элементы.СтраницыПодкючениеКИБ.ТекущаяСтраница = Элементы.СтраницаПодключениеКонтент;
	КонецЕсли;

	ИмяКонфигурацииМобильногоКлиента = "1С:Документооборот 3.0";

	НастройкиПодключения = СтрШаблон("[%1]%2connect=ws=%3%4n=%5",
		ИмяКонфигурацииМобильногоКлиента,
		Символы.ПС,
		АдресПубликацииИБ,
		Символы.ПС,
		ИмяПользователя());

	QRНастройкиПодключения = QRКод(НастройкиПодключения);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьАдресПубликацииИБ(Знач АдресПубликацииИБ)

	Константы.АдресПубликацииНаВебСервере.Установить(АдресПубликацииИБ);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекущуюСтраницуОС(Форма)

	ВариантОС =Форма.ВариантОС;
	Элементы = Форма.Элементы;
	
	Если ВариантОС = "Android" Тогда
		Элементы.СтраницыОС.ТекущаяСтраница = Элементы.Android;
	ИначеЕсли ВариантОС = "iOS" Тогда
		Элементы.СтраницыОС.ТекущаяСтраница = Элементы.iOS;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура СформироватьНадписьНеЗаполненИБ(ЭтоПолноправныйПользователь, Надпись)
	
	ОсновнойТекст = НСтр("ru='Не заполнен адрес публикации.'");
	ДополнительныйТекст = НСтр("ru='Обратитесь к администратору.'");
	
	Если ЭтоПолноправныйПользователь Тогда
		Надпись.Заголовок = ОсновнойТекст;
	Иначе
		Надпись.Заголовок = СтрШаблон("%1%2%3", ОсновнойТекст, Символы.ПС, ДополнительныйТекст);
		Надпись.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти