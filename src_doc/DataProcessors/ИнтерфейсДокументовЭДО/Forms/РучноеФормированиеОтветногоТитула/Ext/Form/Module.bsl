#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОшибкиПриФормированииДокументов = ПолучитьИзВременногоХранилища(Параметры.АдресСведенийОбОшибках);
	ЭлектронныйДокумент = ОшибкиПриФормированииДокументов[0].ОшибкаФормированияВПрикладнойЧасти.ЭлектронныйДокумент;
	СвойстваДокумента = ЭлектронныеДокументыЭДО.СвойстваДокумента(ЭлектронныйДокумент, "Организация");
	Покупатель = СвойстваДокумента.Организация;
	СоставительДокумента = СвойстваДокумента.Организация;
	СоставительДокументаНаименование = СоставительДокументаНаименование(СоставительДокумента);
	Элементы.СоставительДокументаДоверенность.Видимость = Ложь;
	Элементы.ГруппаОснованиеСоставителя.Видимость = Ложь;
	
	Данные = ОбменСКонтрагентами.ДанныеЭлектронногоДокумента(ЭлектронныйДокумент);
	ДеревоДанных = Данные.ДанныеОтправителя.Содержание;
	ФорматОтвета = ФорматыЭДО.ФорматОтветногоТитула(Данные.ДанныеОтправителя.Формат);
	Форматы = ЭлектронныеДокументыЭДО.ПоддерживаемыеФорматы();
	ЭтоУПД = ФорматОтвета = Форматы.ФНС.УПД2019.ИнформацияПокупателя;
	ЭтоУПД_5_02 = (ФорматОтвета = Форматы.ФНС.УПД_5_02.ИнформацияПокупателя
		Или ФорматОтвета = Форматы.ФНС.УПД_5_03.ИнформацияПокупателя);
	ЭтоДополнительныеСведения = ФорматОтвета = Форматы.ФНС.АКтОРасхождениях.ДополнительныеСведения;
	ЭтоАктСверкиВзаиморасчетов = ФорматОтвета = Форматы.ФНС.АктСверкиВзаиморасчетов.ИнформацияПолучателя;
	
	Если ЭтоУПД Или ЭтоУПД_5_02 Тогда
		
		Элементы.ГруппаУПД2019.Видимость = Истина;
		Элементы.ГруппаУКД2020.Видимость = Ложь;
		Элементы.ГруппаДокументОРасхождениях.Видимость = Ложь;
		Элементы.ГруппаДополнительныеСведения.Видимость = Ложь;
		Элементы.ГруппаАктСверкиВзаиморасчетов.Видимость = Ложь;
		
		ПутьТовары = "СведенияОТоварах";
		ПутьГосКонтракт = "ДополнительныеСведенияОбУчастниках.ЗакупкаДляГосударственныхНужд.НомерГосКонтракта";
		ПутьИдентификаторГосКонтракт = "ДополнительныеСведенияОбУчастниках.ИдентификаторГосКонтракта";
		Если ЭтоУПД_5_02 Тогда
			ПутьТовары = "ТаблицаСчетаФактуры.СведенияОбОтгруженныхПозициях";
			ПутьГосКонтракт =
				"СведенияОСчетеФактуре.ДополнительныеСведенияОбУчастниках.СведенияОГосКонтракте.НомерКонтракта";
			ПутьИдентификаторГосКонтракт =
				"СведенияОСчетеФактуре.ДополнительныеСведенияОбУчастниках.ИдентификаторГосКонтракта";
		КонецЕсли;
		
		ТаблицаТоваровЭД = ЭлектронноеВзаимодействие.ДанныеЭлементаДереваЭлектронногоДокумента(
			ДеревоДанных, ПутьТовары);
		
		Для каждого Строка Из ТаблицаТоваровЭД Цикл
			СтрокаДенежныеОбязательства = ДенежныеОбязательства.Добавить();
			СтрокаДенежныеОбязательства.НомерСтрокиИнформацииПродавца = ТаблицаТоваровЭД.Индекс(Строка) + 1;	
		КонецЦикла;
		
		НомерГосКонтракта = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			ДеревоДанных, ПутьГосКонтракт);
		ИдентификаторГосКонтракта = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			ДеревоДанных, ПутьИдентификаторГосКонтракт);
		Если ЗначениеЗаполнено(НомерГосКонтракта) И ЗначениеЗаполнено(ИдентификаторГосКонтракта) Тогда
			ЗакупкаДляГосударственныхНужд = Истина;	
		КонецЕсли;
		
		Элементы.ГруппаЗакупкаДляГосударственныхНужд.Видимость = ЗакупкаДляГосударственныхНужд;
		Если ЭтоУПД_5_02 Тогда
			Элементы.ДокументОРасхожденияхВид.Видимость = Ложь;
		КонецЕсли;
		
		КодИтога = "1";
	
	ИначеЕсли ЭтоДополнительныеСведения Тогда	
		
		Элементы.ГруппаДополнительныеСведения.Видимость = Истина;
		Элементы.ГруппаУКД2020.Видимость = Ложь;
		Элементы.ГруппаУПД2019.Видимость = Ложь;
		Элементы.ГруппаАктСверкиВзаиморасчетов.Видимость = Ложь;
		
		ФормированиеДополнительныхСведений = ЭлектронноеВзаимодействие.ДанныеЭлементаДереваЭлектронногоДокумента(
			ДеревоДанных, "ФормированиеДополнительныхСведений");			
		ПредставленияВариантов =
			ИнтерфейсДокументовЭДОКлиентСервер.ПредставленияВариантовФормированияДополнительныхСведенийАктаОРасхождениях();
		ВариантФормированияДополнительныхСведений = ПредставленияВариантов[ФормированиеДополнительныхСведений];
		
		Элементы.СодержаниеДополнительныхСведений.Подсказка = СтрШаблон("%1 '%2'",
			Элементы.СодержаниеДополнительныхСведений.Подсказка,
			ВариантФормированияДополнительныхСведений);
	
	ИначеЕсли ЭтоАктСверкиВзаиморасчетов Тогда
		
		Элементы.ГруппаАктСверкиВзаиморасчетов.Видимость = Истина;
		Элементы.ГруппаУПД2019.Видимость = Ложь;
		Элементы.ГруппаУКД2020.Видимость = Ложь;
		Элементы.ГруппаДокументОРасхождениях.Видимость = Ложь;
		Элементы.ГруппаДополнительныеСведения.Видимость = Ложь;
		Элементы.ГруппаСоставительДокумента.Видимость = Ложь;
		
		Элементы.ГруппаКоманды.Доступность = Ложь;
	
	Иначе
		
		Элементы.ГруппаУКД2020.Видимость = Истина;
		Элементы.ГруппаУПД2019.Видимость = Ложь;
		Элементы.ГруппаДополнительныеСведения.Видимость = Ложь;
		Элементы.ГруппаАктСверкиВзаиморасчетов.Видимость = Ложь;
		
		СодержаниеОперации_УКД = "С изменением стоимости согласен";	
	
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Элементы.ПодписатьИОтправить.Заголовок = НСтр("ru='Подписать'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Элементы.ГруппаЗакупкаДляГосударственныхНужд.Видимость = Ложь;
	Элементы.ГруппаДокументОРасхождениях.Видимость = Ложь;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СоставительДокументаПриИзменении(Элемент)
	
	ЭлементОснования = Элементы.СоставительДокументаДоверенность;
	Если ЭтоУПД_5_02 Тогда
		ЭлементОснования = Элементы.ГруппаОснованиеСоставителя;
	КонецЕсли;	
	
	Если СоставительДокумента <> Покупатель Тогда
		ЭлементОснования.Видимость = Истина;	
	Иначе
		ЭлементОснования.Видимость = Ложь;	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СоставительДокумента) Тогда
		СоставительДокументаНаименование = СоставительДокументаНаименование(СоставительДокумента);
	Иначе
		СоставительДокументаНаименование = "";
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЕстьДокументОРасхожденияхПриИзменении(Элемент)
	
	ЕстьДокументОРасхожденияхПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КодИтогаПриИзменении(Элемент)
	
	Если КодИтога = "2" Тогда
		ЕстьДокументОРасхождениях = Истина;
		Элементы.ГруппаДокументОРасхождениях.Видимость = ЕстьДокументОРасхождениях;
	Иначе
		ЕстьДокументОРасхождениях = Ложь;
		Элементы.ГруппаДокументОРасхождениях.Видимость = ЕстьДокументОРасхождениях;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолноеСогласиеСДаннымиОтправителяПриИзменении(Элемент)
	
	Если ПолноеСогласиеСДаннымиОтправителя Тогда
		ЕстьРасхождения = Ложь;
		Элементы.ГруппаКоманды.Доступность = Истина;
	Иначе
		ЕстьРасхождения = Истина;
		Элементы.ГруппаКоманды.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодписатьИОтправить(Команда)
	
	Если Не ПроверкаЗаполнения() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураОтвета = ИнтерфейсДокументовЭДОКлиентСервер.ДанныеРучногоФормированияТитула(ЭтотОбъект);
	
	ИнтерфейсДокументовЭДОКлиент.ПодписатьИОтправитьДокументИзФормыРучногоФормированияОтветногоТитула(
		ЭтотОбъект, СтруктураОтвета);
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция СоставительДокументаНаименование(Знач СоставительДокумента)
	
	Возврат ИнтерфейсДокументовЭДО.НаименованиеСоставителяДокументаДляРучногоФормированияОтветногоТитула(
		СоставительДокумента);
		
КонецФункции

&НаСервере
Процедура ЕстьДокументОРасхожденияхПриИзмененииНаСервере()
	
	Элементы.ГруппаДокументОРасхождениях.Видимость = ЕстьДокументОРасхождениях; 
	
	Если ЕстьДокументОРасхождениях Тогда
		КодИтога = "2";
		Элементы.ЕстьДокументОРасхождениях.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветАкцента; 
	Иначе
		КодИтога = "1";
		Элементы.ЕстьДокументОРасхождениях.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветТекстаФормы;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверкаЗаполнения()
	
	СписокОшибок.Очистить();
	
	Если ЭтоУПД Тогда		
		ИнтерфейсДокументовЭДОКлиент.ПроверитьЗаполнениеУПД2019ВФормеРучногоФормированияОтветногоТитула(
			ЭтотОбъект, СписокОшибок);
	ИначеЕсли ЭтоУПД_5_02 Тогда
		ИнтерфейсДокументовЭДОКлиент.ПроверитьЗаполнениеУПД_5_02ВФормеРучногоФормированияОтветногоТитула(
			ЭтотОбъект, СписокОшибок);
	ИначеЕсли ЭтоДополнительныеСведения Тогда	
		ИнтерфейсДокументовЭДОКлиент.ПроверитьЗаполнениеДополнительныхСведенийВФормеРучногоФормированияОтветногоТитула(
			ЭтотОбъект, СписокОшибок);
	ИначеЕсли ЭтоАктСверкиВзаиморасчетов Тогда
		Возврат Истина;
	Иначе
		ИнтерфейсДокументовЭДОКлиент.ПроверитьЗаполнениеУКД2020ВФормеРучногоФормированияОтветногоТитула(
			ЭтотОбъект, СписокОшибок);
	КонецЕсли;
	
	Если СписокОшибок.Количество() > 0 Тогда
		Для Каждого Ошибка Из СписокОшибок Цикл
			ОбщегоНазначенияКлиент.СообщитьПользователю(Ошибка.Значение, ,
			Ошибка.Представление);	
		КонецЦикла;
		
		Возврат Ложь;
	Иначе
		Возврат Истина;	
	КонецЕсли;
	
КонецФункции

#КонецОбласти
