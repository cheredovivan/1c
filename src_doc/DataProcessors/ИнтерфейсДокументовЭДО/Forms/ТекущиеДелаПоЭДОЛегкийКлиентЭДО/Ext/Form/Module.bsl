

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ВариантыИнтерфейса = ИнтерфейсДокументовЭДОКлиентСервер.ВариантыИнтерфейсаДокументовЭДО();
	Если ИнтерфейсДокументовЭДО.ВариантИнтерфейсаДокументовЭДО() <> ВариантыИнтерфейса.ЛегкийКлиентЭДО Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЭтоМобильныйКлиент = ОбщегоНазначения.ЭтоМобильныйКлиент();
	
	ОтложеннаяОтправкаЭлектронныхДокументов = 
		ИнтерфейсДокументовЭДОВызовСервера.ОтложеннаяОтправкаЭлектронныхДокументов();
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(
		"РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам", ЭтотОбъект);
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(
		"РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами", ЭтотОбъект);
	
	НавигационнаяСсылка = "e1cib/app/" + ИмяФормы;
	
	РазделыТекущихДелЭДО = РазделыТекущихДелЭДО();
	
	ОтображатьОбластьПредпросмотра = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ИнтерфейсЭДО", 
		"ОтображатьОбластьПредпросмотра", Истина);
	ВключенОбменЭлектроннымиДокументами = НастройкиЭДО.ИспользуетсяОбменЭлектроннымиДокументами();
	Элементы.СтраницыПредпросмотр.Видимость = ВключенОбменЭлектроннымиДокументами И ОтображатьОбластьПредпросмотра;
	
	Если ЗначениеЗаполнено(Параметры.ТекущийРаздел) Тогда
		ВыбранныйРаздел = Параметры.ТекущийРаздел;
	Иначе
		ВыбранныйРаздел = РазделыТекущихДелЭДО.ВходящиеЭлектронныеДокументы;
	КонецЕсли;
	
	УстановитьТекущийРаздел(ЭтотОбъект, ВыбранныйРаздел);
	
	ЕстьОтборПоНепрочитанным = Ложь;
	
	ПакетноеОтображение = Истина;
	
	ПустойКонтрагент = Метаданные.ОпределяемыеТипы.КонтрагентБЭД.Тип.ПривестиЗначение();
	
	ОтображатьУдаленные = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ИнтерфейсЭДО", 
		"ОтображатьУдаленные", Ложь);
	Элементы.ОтображатьУдаленныеВходящие.Пометка = ОтображатьУдаленные; 
	Элементы.ОтображатьУдаленныеИсходящие.Пометка = ОтображатьУдаленные;
	Элементы.ОтображатьУдаленныеОшибки.Пометка = ОтображатьУдаленные;
	
	ЗаголовокТребуетВнимания = НСтр("ru = 'Требует внимания'");
	Элементы.Состояние.СписокВыбора.Вставить(0, 
		"ТребуетВнимания", ЗаголовокТребуетВнимания, , БиблиотекаКартинок.ТребуетВниманияЖелтыйШарБЭД);
	Элементы.СтатусПриглашения.СписокВыбора.Вставить(0, 
		"ТребуетВнимания", ЗаголовокТребуетВнимания, , БиблиотекаКартинок.ТребуетВниманияЖелтыйШарБЭД);
	
	Если Не НастройкиЭДО.ОтправлятьВходящиеДокументыНаУтверждение() Тогда
		ЗначениеУтвердить = Элементы.Состояние.СписокВыбора.НайтиПоЗначению("Утвердить");	
		Элементы.Состояние.СписокВыбора.Удалить(ЗначениеУтвердить);
	КонецЕсли;
		
	ОтображениеОрганизации();
	
	УстановитьПараметрыСписков();

	КоличествоДокументов();
	
	УстановитьВидимостьКомандФормыПоПравам();
	
	ЗаполнитьКэшФильтровВСписках();
	ЗаполнитьКоличествоДополнительныхФильтров();
	УстановитьФильтрыСписков();
	Задолженность = СведенияОЗадолженности();
	ПоказатьУведомленияПоЗадолженности(ЭтотОбъект, Задолженность);
	
	ЗаполнитьДанныеПоИспользованиюПоказаТрафикаЭлектронныхДокументов();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещенияКомандСозданияДокумента = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещенияКомандСозданияДокумента.КоманднаяПанель = Элементы.ГруппаКомандыСозданияДокумента;
	ПараметрыРазмещенияКомандСозданияДокумента.Источники = Новый Массив;
	ПараметрыРазмещенияКомандСозданияДокумента.Источники.Добавить(Метаданные.Документы.ЭлектронныйДокументИсходящийЭДО);
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещенияКомандСозданияДокумента);
	
	ПараметрыРазмещенияКомандКонтрагентов = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещенияКомандКонтрагентов.КоманднаяПанель = Элементы.ГруппаКомандыПриглашения;
	ПараметрыРазмещенияКомандКонтрагентов.Источники = Новый Массив;
	Для Каждого КонтрагентТип Из Метаданные.ОпределяемыеТипы.КонтрагентБЭД.Тип.Типы() Цикл
		ПараметрыРазмещенияКомандКонтрагентов.Источники.Добавить(Метаданные.НайтиПоТипу(КонтрагентТип));
	КонецЦикла;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещенияКомандКонтрагентов);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОбменСКонтрагентамиПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Параметры, Отказ, СтандартнаяОбработка);
	ИнтерфейсДокументовЭДО.ДобавитьПараметрыОбменаСКонтрагентами(ЭтотОбъект);
	ЛегкийИнтерфейсДокументовЭДО.УстановитьПризнакОтсутствияУчетныхЗаписейЭДОВФорме(ЭтотОбъект);
	
	ПараметрыПриСозданииНаСервере = АвтоматическоеПолучениеЭДО.НовыеПараметрыФормыСпискаПриСозданииНаСервере();
	ПараметрыПриСозданииНаСервере.ГруппаПредупреждение = Элементы.ГруппаПредупреждениеАвтоматическогоПолученияЭДО;
	АвтоматическоеПолучениеЭДО.ФормаСпискаПриСозданииНаСервере(ЭтотОбъект, ПараметрыПриСозданииНаСервере);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОтображениеОбластиПросмотра" Тогда			
		
		ОтображатьОбластьПредпросмотра = Параметр;
		Элементы.СтраницыПредпросмотр.Видимость = ОтображатьОбластьПредпросмотра;
		
	ИначеЕсли СобытиеОбновитьСписки(ИмяСобытия) Тогда
		
		ОбновитьДинамическийСписокТекущегоРаздела();
		
		ВидимостьЭлементовИКоманд();
	
	ИначеЕсли ИмяСобытия = "ВыполнитьСинхронизацию" Тогда
		
		Активизировать();
		ПолучитьОтправитьОбновить();
	
	ИначеЕсли ИмяСобытия = "УстановитьОтправкуПолучениеСОтборомПоОрганизации" Тогда
		
		Элементы.ВыборочныйОбменПоОрганизациямВходящие.Пометка = Параметр;
		Элементы.ВыборочныйОбменПоОрганизациямИсходящие.Пометка = Параметр;
	ИначеЕсли ИмяСобытия = "Запись_НаборКонстант" И Источник = "ИспользоватьРазделениеДокументовПоОтветственнымЭДО" Тогда

		ОбновитьОбластьПросмотра();
	
	ИначеЕсли ТекущийРаздел = "Приглашения"
		И (ИмяСобытия = "Запись_Контрагенты"
		Или ИмяСобытия = ОбработкаНеисправностейБЭДКлиент.ИмяСобытияИсправлениеВидаОшибки()
		Или ИмяСобытия = "ОбновитьСостояниеПриглашений"
		Или ИмяСобытия = "ОбновленСписокУчетныхЗаписей1СЭДО") Тогда
		
		ОбновитьДинамическийСписокТекущегоРаздела();
		
	КонецЕсли;
	
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОбработкаОповещенияДляПризнакаОтсутствияУчетныхЗаписейЭДО(ЭтотОбъект, ИмяСобытия);
	
	ОбменСКонтрагентамиКлиентПереопределяемый.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
	АвтоматическоеПолучениеЭДОКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
			
	ТекущиеДанные = Элементы.ВходящиеЭлектронныеДокументы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда 
		ТекущийЭлектронныйДокумент = Элементы.ВходящиеЭлектронныеДокументы.ТекущиеДанные.ЭлектронныйДокумент;
	Иначе
		ТекущийЭлектронныйДокумент = Неопределено;	
	КонецЕсли;
	
	Элементы.ГруппаБыстрыеОтборы.Видимость = Ложь;			
	Элементы.ГруппаОчиститьБыстрыеОтборы.Видимость = Ложь;
	
	Если Не ДанныеВосстановленыИзНастроек Тогда
		ЗаполнитьКоличествоДополнительныхФильтров();
	КонецЕсли;
	
	ОбновитьФильтры();
	ВидимостьЭлементовИКоманд();
	ПодключитьОбработчикОжидания("УстановитьАктивнымСписокВыбранногоРаздела", "0.1", Истина);
	
	ИмяПараметра = "ЭлектронноеВзаимодействие.ОбменСКонтрагентами.УчетныеЗаписи";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПараметра, Новый Структура("ОткрытаФормаПервогоПодключения", Ложь));
	КонецЕсли;
	ОтсутствуютУчетныеЗаписиЭДО = ИнтерфейсДокументовЭДОКлиент.ЗначениеРеквизитаОтсутствуютУчетныеЗаписиЭДО(ЭтотОбъект);
	Если ОтсутствуютУчетныеЗаписиЭДО И Не ПараметрыПриложения[ИмяПараметра].ОткрытаФормаПервогоПодключения Тогда
		ПараметрыПриложения[ИмяПараметра].ОткрытаФормаПервогоПодключения = Истина;
		ПодключитьОбработчикОжидания("ПодключитьУчетнуюЗапись", "1", Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяФормы, "Отборы", КэшФильтровВСписках);
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	ДанныеВосстановленыИзНастроек = Истина;
	ЗаполнитьКоличествоДополнительныхФильтров();
	УстановитьЗаголовокИВидимостьДополнительныхФильтров(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура БоковоеМенюПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РазделыТекущихДелЭДО = РазделыТекущихДелЭДО();
	
	Если ДанныеСобытия.Element.id = "inbox-button" Тогда
		ВыбранныйРаздел = РазделыТекущихДелЭДО.ВходящиеЭлектронныеДокументы;
	ИначеЕсли ДанныеСобытия.Element.id = "sent-button" Тогда 
		ВыбранныйРаздел = РазделыТекущихДелЭДО.ИсходящиеЭлектронныеДокументы;
	ИначеЕсли ДанныеСобытия.Element.id = "draft-button" Тогда
		ВыбранныйРаздел = РазделыТекущихДелЭДО.Черновики;
	ИначеЕсли ДанныеСобытия.Element.id = "invitation-button" Тогда
		ВыбранныйРаздел = РазделыТекущихДелЭДО.Приглашения;
	ИначеЕсли ДанныеСобытия.Element.id = "error-button" Тогда
		ВыбранныйРаздел = РазделыТекущихДелЭДО.Ошибки;
	Иначе
		Возврат;
	КонецЕсли; 
	Элементы.ПанельНавигации.Доступность = Ложь;
	ПереключитьРазделТекущихДел(ВыбранныйРаздел);
	Элементы.ПанельНавигации.Доступность = Истина;
КонецПроцедуры 

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПриИзмененииОтборов();
	ВидимостьЭлементовИКоманд();

КонецПроцедуры 

&НаКлиенте
Процедура ТипДокументаПриИзменении(Элемент)
	
	ПриИзмененииОтборов();
	ВидимостьЭлементовИКоманд();

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОтборПриИзменении(Элемент)
	
	ПриИзмененииОтборов();
	ВидимостьЭлементовИКоманд();

КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ПриИзмененииОтборов();
	ВидимостьЭлементовИКоманд();

КонецПроцедуры 

&НаКлиенте
Процедура ОтветственныйПриИзменении(Элемент)
	
	ПриИзмененииОтборов();
	ВидимостьЭлементовИКоманд();
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеПриИзменении(Элемент)
	
	ПриИзмененииОтборов();
	ВидимостьЭлементовИКоманд();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипРегламентаПриИзменении(Элемент)
		
	ПриИзмененииОтборов();
	ВидимостьЭлементовИКоманд();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриглашенияПриИзменении(Элемент)
		
	ПриИзмененииОтборов();
	ВидимостьЭлементовИКоманд();
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущийДокументТабличныйДокументПередПечатью(Элемент, ДиалогПечати, Отказ)
	
	ВывестиНаПечать();
	Отказ = Истина;	
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПредупреждениеАвтоматическогоПолученияЭДООбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка,
	СтандартнаяОбработка)
	АвтоматическоеПолучениеЭДОКлиент.ОбработкаНавигационнойСсылки(
		ЭтотОбъект, Элемент, НавигационнаяСсылка, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВходящиеЭлектронныеДокументы

&НаКлиенте
Процедура ВходящиеЭлектронныеДокументыПриАктивизацииСтроки(Элемент)
	
	ТекущийРаздел = РазделыТекущихДелЭДО().ВходящиеЭлектронныеДокументы;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено
		Или ВключенаГруппировкаСтрок(ТекущиеДанные) Тогда 
		ТекущийИдентификаторПакета = "";
		ТекущийЭлектронныйДокумент = Неопределено;
		ТекущийИдентификаторДокумента = "";
	ИначеЕсли ТекущиеДанные.ЭлектронныйДокумент = ТекущийЭлектронныйДокумент Тогда
		ТекущийИдентификаторПакета = ТекущиеДанные.КлючСортировкиПакета;
		Возврат;
	Иначе
		ТекущийИдентификаторПакета = ТекущиеДанные.КлючСортировкиПакета;
		ТекущийЭлектронныйДокумент = ТекущиеДанные.ЭлектронныйДокумент;
		ТекущийИдентификаторДокумента = ТекущиеДанные.КлючДокумента;
	КонецЕсли;
		
	ПодключитьОбработчикОжидания("ОбновитьОбластьПросмотра", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВходящиеЭлектронныеДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
		Или ВключенаГруппировкаСтрок(ТекущиеДанные) Тогда 
		Возврат;
	КонецЕсли;
	
	ОткрытьЭлектронныйДокументДляПросмотра(ТекущиеДанные.ЭлектронныйДокумент);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВходящиеЭлектронныеДокументыПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ПриПолученииДанныхСписков(ИмяЭлемента, Настройки, Строки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВходящиеЭлектронныеДокументыПриИзменении(Элемент)
	
	КоличествоДокументов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИсходящиеЭлектронныеДокументы

&НаКлиенте
Процедура ИсходящиеЭлектронныеДокументыПриАктивизацииСтроки(Элемент)
	
	ТекущийРаздел = РазделыТекущихДелЭДО().ИсходящиеЭлектронныеДокументы;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено
		Или ВключенаГруппировкаСтрок(ТекущиеДанные) Тогда 
		ТекущийИдентификаторПакета = "";
		ТекущийЭлектронныйДокумент = Неопределено;
		ТекущийИдентификаторДокумента = "";
	ИначеЕсли ТекущиеДанные.ЭлектронныйДокумент = ТекущийЭлектронныйДокумент Тогда
		Возврат;
	Иначе
		ТекущийИдентификаторПакета = ТекущиеДанные.КлючСортировкиПакета;
		ТекущийЭлектронныйДокумент = ТекущиеДанные.ЭлектронныйДокумент;
		ТекущийИдентификаторДокумента = ТекущиеДанные.КлючДокумента;
	КонецЕсли;
		
	ПодключитьОбработчикОжидания("ОбновитьОбластьПросмотра", 0.2, Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура ИсходящиеЭлектронныеДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
		Или ВключенаГруппировкаСтрок(ТекущиеДанные) Тогда 
		Возврат;
	КонецЕсли;
	
	ОткрытьЭлектронныйДокументДляПросмотра(ТекущиеДанные.ЭлектронныйДокумент);	

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИсходящиеЭлектронныеДокументыПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
		
	ПриПолученииДанныхСписков(ИмяЭлемента, Настройки, Строки);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсходящиеЭлектронныеДокументыПриИзменении(Элемент)
	
	КоличествоДокументов();

КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовТаблицыФормыЧерновики

&НаКлиенте
Процедура ЧерновикиПриАктивизацииСтроки(Элемент)
	
	ТекущийРаздел = РазделыТекущихДелЭДО().Черновики;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено
		Или ВключенаГруппировкаСтрок(ТекущиеДанные) Тогда 
		ТекущийИдентификаторПакета = "";
		ТекущийЭлектронныйДокумент = Неопределено;
		ТекущийИдентификаторДокумента = "";
		
		ТекущийДокументУчета = Неопределено;
	ИначеЕсли ТекущиеДанные.Документ = ТекущийДокументУчета Тогда
		Возврат;
	Иначе
		ТекущийИдентификаторПакета = ТекущиеДанные.КлючСортировкиПакета;
		ТекущийЭлектронныйДокумент = ТекущиеДанные.ЭлектронныйДокумент;
		ТекущийИдентификаторДокумента = ТекущиеДанные.КлючДокумента;
		
		ТекущийДокументУчета = ТекущиеДанные.Документ;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ЧерновикиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
		Или ВключенаГруппировкаСтрок(ТекущиеДанные) Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ЭлектронныйДокумент) Тогда
		ОткрытьЭлектронныйДокументДляПросмотра(ТекущиеДанные.ЭлектронныйДокумент);
	Иначе
		ПоказатьЗначение(, ТекущиеДанные.Документ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЧерновикиПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ПриПолученииДанныхСписков(ИмяЭлемента, Настройки, Строки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЧерновикиОбработкаПолученияНавигационнойСсылки(Элемент, Ключ, Представление, НавигационнаяСсылка,
	СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(Элемент.ТекущиеДанные.Документ);
	
КонецПроцедуры

#КонецОбласти
 
#Область ОбработчикиСобытийЭлементовТаблицыФормыПриглашения

&НаКлиенте
Процедура ПриглашенияПриАктивизацииСтроки(Элемент)
	УстановитьТекущиеДанныеПриглашения();
	УстановитьСтраницуДействийСПриглашением();
КонецПроцедуры 

&НаСервереБезКонтекста
Процедура ПриглашенияПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ПриглашенияКонтрагентов = Новый Соответствие;
	
	Для Каждого ТекущаяСтрока Из Строки Цикл
		
		ДанныеСтроки = ТекущаяСтрока.Значение.Данные;
		
		СписокПриглашений = ПриглашенияКонтрагентов[ДанныеСтроки.КонтрагентСортировка];
		
		Если СписокПриглашений = Неопределено Тогда
			
			СписокПриглашений = Новый ТаблицаЗначений;
			СписокПриглашений.Колонки.Добавить("ДатаИзмененияСтатуса");
			СписокПриглашений.Колонки.Добавить("ИдентификаторОрганизации");
			СписокПриглашений.Колонки.Добавить("КлючПриглашения");
			СписокПриглашений.Колонки.Добавить("ИндексСтатусаПодключения"); // 0 - самый высокий
			
		КонецЕсли;
		
		СтрокаПриглашения = СписокПриглашений.Добавить();
		СтрокаПриглашения.КлючПриглашения = ДанныеСтроки.КлючПриглашения;
		СтрокаПриглашения.ИдентификаторОрганизации = ДанныеСтроки.ИдентификаторОрганизации;
		СтрокаПриглашения.ДатаИзмененияСтатуса = ДанныеСтроки.ДатаИзмененияСтатуса;
		СтрокаПриглашения.ИндексСтатусаПодключения = ДанныеСтроки.ИндексСтатусаПодключения;
		
		ПриглашенияКонтрагентов.Вставить(ДанныеСтроки.КонтрагентСортировка, СписокПриглашений);
		
	КонецЦикла;
	
	КартинкиСостояний = СоответствиеИндексовКартинокСостоянийЭДО();
	
	Для Каждого ТекущаяСтрока Из Строки Цикл
		
		ДанныеСтроки = ТекущаяСтрока.Значение.Данные;
		
		Если СтатусПриглашенияТребуетВнимания(ДанныеСтроки.СтатусПодключения) Тогда
			ДанныеСтроки.ИндексКартинкиСостояния = КартинкиСостояний.Получить("ТребуетВнимания");
		Иначе
			ДанныеСтроки.ИндексКартинкиСостояния = КартинкиСостояний.Получить("НеТребуетВнимания");
		КонецЕсли;
		
		ПриглашенияКонтрагента = ПриглашенияКонтрагентов[ДанныеСтроки.КонтрагентСортировка];
		ПриглашенияКонтрагента.Сортировать("ДатаИзмененияСтатуса Убыв, ИндексСтатусаПодключения, КлючПриглашения");
		
		ПервыйЭлементПриглашенийПоСсылке = ДанныеСтроки.КлючПриглашения = ПриглашенияКонтрагента[0].КлючПриглашения
			И ДанныеСтроки.ИдентификаторОрганизации = ПриглашенияКонтрагента[0].ИдентификаторОрганизации;
		
		Если Не ПервыйЭлементПриглашенийПоСсылке Тогда
			
			ДанныеСтроки.Контрагент = ИнтеграцияЭДО.ПустаяСсылкаКонтрагента();
			ДанныеСтроки.ИНН = "";
			
			Если ДанныеСтроки.Свойство("КПП") Тогда
				ДанныеСтроки.КПП = "";
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриглашенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПоказатьЗначение(, ТекущиеДанные.КонтрагентСортировка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьСведенияПоТекущемуТрафику(Команда)
	
	ОткрытьФорму("Отчет.СведенияПоТекущемуТрафикуЭДО.Форма",,,,,,, РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользуетсяПоказТрафикаЭлектронныхДокументов(Команда)

	ИспользуетсяПоказТрафикаЭлектронныхДокументовНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСведенияОЗадолженности(Команда)
	
	ИнтерфейсДокументовЭДОКлиент.ПоказатьСведенияОЗадолженности(ЭтотОбъект);
	
КонецПроцедуры

#Область ВыполнениеДействийПоЭДО

&НаКлиенте
Асинх Процедура Аннулировать(Команда)
	
	ДанныеВыделенныхСтрокРаздела = ДанныеВыделенныхСтрокРаздела(ТекущийРаздел, 
		"ЭлектронныйДокумент, ИдентификаторПакета");
		
	Если Не ЗначениеЗаполнено(ДанныеВыделенныхСтрокРаздела) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектыДействий = ОбъектыДействий(ДанныеВыделенныхСтрокРаздела);

	ЭтоПакетнаяОбработка = ЗначениеЗаполнено(ОбъектыДействий.ПакетыДокументов);
	ЭлектронныеДокументы = Новый Массив;
	Если ЭтоПакетнаяОбработка И Не ВыполнениеДействийПоЭДОДоступноДляПакетов(ОбъектыДействий.ПакетыДокументов) Тогда
		ВидДействия = ЛегкийИнтерфейсДокументовЭДОКлиент.ВидыВыполняемыхДействийСДокументом().Аннулировать;
		ТекстВопроса = ЛегкийИнтерфейсДокументовЭДОКлиент.ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЭлектронныеДокументы.Добавить(ТекущийЭлектронныйДокумент);
			ЭтоПакетнаяОбработка = Ложь;
		Иначе
			Возврат;
		КонецЕсли;
	Иначе
		Для Каждого СтрокаРаздела Из ДанныеВыделенныхСтрокРаздела Цикл
			Если ЗначениеЗаполнено(СтрокаРаздела.ЭлектронныйДокумент) Тогда
				ЭлектронныеДокументы.Добавить(СтрокаРаздела.ЭлектронныйДокумент);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ВходящиеДокументы = ИнтерфейсДокументовЭДОКлиент.ОтобратьВходящиеДокументы(ЭлектронныеДокументы);
	Если ЗначениеЗаполнено(ВходящиеДокументы)
		И Не ЭлектронныеДокументыЭДОВызовСервера.КаждыйДокументБезПодтвержденияИлиОтветнаяПодписьОтправлена(ВходящиеДокументы,
		ЭтоПакетнаяОбработка) Тогда

		ПользовательСогласен = Ждать ИнтерфейсДокументовЭДОКлиент.ПредложитьОтклонение(ЭтоПакетнаяОбработка);
		Если ПользовательСогласен Тогда
			ОтказатьсяОтПодписания(Элементы.КомандаОтказатьсяОтПодписанияПодписать);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	
	ДополнительныеПараметры.Вставить("ЭлектронныеДокументы", ОбъектыДействий.ЭлектронныеДокументы);
	ДополнительныеПараметры.Вставить("ПакетыДокументов", ОбъектыДействий.ПакетыДокументов);
	
	НаборДействий = Новый Соответствие;	
	ДействияПоЭДОАннулировать = ПредопределенноеЗначение(
			"Перечисление.ДействияПоЭДО.Аннулировать");
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДОАннулировать);
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.Подписать"));
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.ПодготовитьКОтправке"));
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.Отправить"));
		
	ДополнительныеПараметры.Вставить("ОповещениеУспешногоЗавершения",
		Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект, НаборДействий));	
		
	ДополнительныеПараметры.Вставить("НаборДействий", НаборДействий);
	ДополнительныеПараметры.Вставить("ОсновноеДействие", ДействияПоЭДОАннулировать);
				
	Обработчик = Новый ОписаниеОповещения("ВыполнитьДействияПоЭДОПослеВводаСтроки", ИнтерфейсДокументовЭДОКлиент, 
		ДополнительныеПараметры);
	
	ДополнительныеПараметры = ОбщегоНазначенияБЭДКлиент.ПараметрыВводаСтроки();
	ДополнительныеПараметры.ЗаголовокФормы = НСтр("ru = 'Укажите причины аннулирования документа'");
	ДополнительныеПараметры.НазваниеКнопкиПоУмолчанию = НСтр("ru = 'Аннулировать'");
	ДополнительныеПараметры.Многострочность = Истина;
	ДополнительныеПараметры.Обязательность = Истина;
	ДополнительныеПараметры.КомментарийОбязательностиВвода =
		НСтр("ru = 'Для аннулирования документа необходимо указать причину.'");
	ОбщегоНазначенияБЭДКлиент.ПоказатьВводСтрокиБЭД(Обработчик, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура Утвердить(Команда)  
	
	ДанныеВыделенныхСтрокРаздела = ДанныеВыделенныхСтрокРаздела(ТекущийРаздел, 
		"ЭлектронныйДокумент, ИдентификаторПакета");
	
	Если Не ЗначениеЗаполнено(ДанныеВыделенныхСтрокРаздела) Тогда		
		Возврат;
	КонецЕсли;
	
	ОбъектыДействий = ОбъектыДействий(ДанныеВыделенныхСтрокРаздела);
	
	ЭтоПакетнаяОбработка = ЗначениеЗаполнено(ОбъектыДействий.ПакетыДокументов);
	Если ЭтоПакетнаяОбработка И Не ВыполнениеДействийПоЭДОДоступноДляПакетов(ОбъектыДействий.ПакетыДокументов) Тогда
		ВидДействия = ЛегкийИнтерфейсДокументовЭДОКлиент.ВидыВыполняемыхДействийСДокументом().Утвердить;
		ТекстВопроса = ЛегкийИнтерфейсДокументовЭДОКлиент.ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ОставитьДляОбработкиТолькоТекущийДокумент(ОбъектыДействий);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НаборДействий = Новый Соответствие;	
	ДействияПоЭДОУтвердить = ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.Утвердить");
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДОУтвердить);
	ДействияПоЭДОСформироватьОтвет = ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.СформироватьОтвет");
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДОСформироватьОтвет);
	ДействияПоЭДОПодготовитьКОтправке = ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.ПодготовитьКОтправке");
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДОПодготовитьКОтправке);
	ДействияПоЭДООтправить = ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.Отправить");
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДООтправить);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект);
	
	ПараметрыВыполненияДействийПоЭДО = ЭлектронныеДокументыЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	ПараметрыВыполненияДействийПоЭДО.НаборДействий = НаборДействий;
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ЭлектронныеДокументы = ОбъектыДействий.ЭлектронныеДокументы;
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ПакетыДокументов = ОбъектыДействий.ПакетыДокументов;
	
	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ПараметрыВыполненияДействийПоЭДО);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗакрытьПринудительно(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	
	Если ТекущийРаздел = РазделыТекущихДелЭДО().Черновики Тогда
		
		ОбъектыУчета = ДанныеВыделенныхСтрокРаздела(ТекущийРаздел, "Документ");
		
		Если Не ЗначениеЗаполнено(ОбъектыУчета) Тогда
			Возврат;
		КонецЕсли;
		
		ДополнительныеПараметры.Вставить("ОбъектыУчета", ОбъектыУчета);
		Обработчик = Новый ОписаниеОповещения("ВыполнитьОтменуФормированияДокументовПослеВводаСтроки", 
			ЭтотОбъект, ДополнительныеПараметры);
		
	Иначе
		
		Обработчик = Новый ОписаниеОповещения("ВыполнитьДействияПоЭДОПослеВводаСтроки", 
			ИнтерфейсДокументовЭДОКлиент, ДополнительныеПараметры);
		
		ДанныеВыделенныхСтрокРаздела = ДанныеВыделенныхСтрокРаздела(ТекущийРаздел, 
			"ЭлектронныйДокумент, ИдентификаторПакета");
		ОбъектыДействий = ОбъектыДействий(ДанныеВыделенныхСтрокРаздела);
		
		ЭтоПакетнаяОбработка = ЗначениеЗаполнено(ОбъектыДействий.ПакетыДокументов);
		Если ЭтоПакетнаяОбработка И Не ВыполнениеДействийПоЭДОДоступноДляПакетов(ОбъектыДействий.ПакетыДокументов) Тогда
			ВидДействия = ЛегкийИнтерфейсДокументовЭДОКлиент.ВидыВыполняемыхДействийСДокументом().ЗакрытьПринудительно;
			ТекстВопроса = ЛегкийИнтерфейсДокументовЭДОКлиент.ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидДействия);
			Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				ОставитьДляОбработкиТолькоТекущийДокумент(ОбъектыДействий);
			Иначе
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		ДополнительныеПараметры.Вставить("ЭлектронныеДокументы", ОбъектыДействий.ЭлектронныеДокументы);
		ДополнительныеПараметры.Вставить("ПакетыДокументов", ОбъектыДействий.ПакетыДокументов);
		
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект);
	ДополнительныеПараметры.Вставить("ОповещениеУспешногоЗавершения", ОписаниеОповещения);
	
	НаборДействий = Новый Соответствие;	
	ДействияПоЭДОЗакрытьПринудительно = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.ЗакрытьПринудительно");
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДОЗакрытьПринудительно);
	
	ДополнительныеПараметры.Вставить("НаборДействий", НаборДействий);	
	ДополнительныеПараметры.Вставить("ОсновноеДействие", ДействияПоЭДОЗакрытьПринудительно);
	
	ДополнительныеПараметры = ОбщегоНазначенияБЭДКлиент.ПараметрыВводаСтроки();
	ДополнительныеПараметры.ЗаголовокФормы = НСтр("ru = 'Укажите причину закрытия документооборота'");
	ДополнительныеПараметры.НазваниеКнопкиПоУмолчанию = НСтр("ru = 'Закрыть электронный документ'");
	ДополнительныеПараметры.Многострочность = Истина;
	ДополнительныеПараметры.Обязательность = Истина;
	ДополнительныеПараметры.КомментарийОбязательностиВвода =
		НСтр("ru = 'Для закрытия документооборота по выбранным ЭД необходимо указать причину закрытия.'");
	ОбщегоНазначенияБЭДКлиент.ПоказатьВводСтрокиБЭД(Обработчик, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ОтказатьсяОтПодписания(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект);
	
	ДанныеВыделенныхСтрокРаздела = ДанныеВыделенныхСтрокРаздела(ТекущийРаздел, 
		"ЭлектронныйДокумент, ИдентификаторПакета");
		
	Если Не ЗначениеЗаполнено(ДанныеВыделенныхСтрокРаздела) Тогда
		Возврат;
	КонецЕсли;
		
	ОбъектыДействий = ОбъектыДействий(ДанныеВыделенныхСтрокРаздела);	
	
	ЭтоПакетнаяОбработка = ЗначениеЗаполнено(ОбъектыДействий.ПакетыДокументов);
	Если ЭтоПакетнаяОбработка И Не ВыполнениеДействийПоЭДОДоступноДляПакетов(ОбъектыДействий.ПакетыДокументов) Тогда
		ВидДействия = ЛегкийИнтерфейсДокументовЭДОКлиент.ВидыВыполняемыхДействийСДокументом().Отклонить;
		ТекстВопроса = ЛегкийИнтерфейсДокументовЭДОКлиент.ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ОставитьДляОбработкиТолькоТекущийДокумент(ОбъектыДействий);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Элементы.СтраницыВходящиеИсходящие.ТекущаяСтраница = Элементы.СтраницаВходящиеЭлектронныеДокументы Тогда
		
		ИнтерфейсДокументовЭДОКлиент.ОтклонитьОбъекты(ОбъектыДействий, ОписаниеОповещения);
		
	Иначе
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ЭлектронныеДокументы", ОбъектыДействий.ЭлектронныеДокументы);
		ДополнительныеПараметры.Вставить("ПакетыДокументов", ОбъектыДействий.ПакетыДокументов);
		ДополнительныеПараметры.Вставить("ОповещениеУспешногоЗавершения", 
			ОписаниеОповещения);
		
		НаборДействий = Новый Соответствие;	
		
		ДействияПоЭДООтклонитьПодписание = ПредопределенноеЗначение(
			"Перечисление.ДействияПоЭДО.ОтклонитьПодписание");
		ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДООтклонитьПодписание);
		
		ДополнительныеПараметры.Вставить("НаборДействий", НаборДействий);	
		ДополнительныеПараметры.Вставить("ОсновноеДействие", ДействияПоЭДООтклонитьПодписание);
		
		Обработчик = Новый ОписаниеОповещения("ВыполнитьДействияПоЭДОПослеВводаСтроки", 
			ИнтерфейсДокументовЭДОКлиент, ДополнительныеПараметры);
		
		ДополнительныеПараметры = ОбщегоНазначенияБЭДКлиент.ПараметрыВводаСтроки();
		ДополнительныеПараметры.ЗаголовокФормы = НСтр("ru = 'Укажите причину отклонения документа'");
		ДополнительныеПараметры.НазваниеКнопкиПоУмолчанию = НСтр("ru = 'Отклонить'");
		ДополнительныеПараметры.Многострочность = Истина;
		ДополнительныеПараметры.Обязательность = Истина;
		ДополнительныеПараметры.КомментарийОбязательностиВвода =
		НСтр("ru = 'Для отказа от подписания по выбранным ЭД необходимо указать причину.'");
		ОбщегоНазначенияБЭДКлиент.ПоказатьВводСтрокиБЭД(Обработчик, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подписать(Команда)
	
	Если Элементы.СтраницыВходящиеИсходящие.ТекущаяСтраница = Элементы.СтраницаВходящиеЭлектронныеДокументы Тогда
		
		Если Элементы.ВходящиеЭлектронныеДокументы.ВыделенныеСтроки.Количество() Тогда
			ПодключитьОбработчикОжидания("ВыполнитьКомандаПодписать", 0.1, Истина);
		КонецЕсли;	
		
	Иначе
		
		Если Элементы.ИсходящиеЭлектронныеДокументы.ВыделенныеСтроки.Количество() Тогда
			
			ПодключитьОбработчикОжидания("ВыполнитьКомандаПодписать", 0.1, Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПринятьАннулирование(Команда)

	ДанныеВыделенныхСтрокРаздела = ДанныеВыделенныхСтрокРаздела(ТекущийРаздел, 
		"ЭлектронныйДокумент, ИдентификаторПакета");
		
	Если Не ЗначениеЗаполнено(ДанныеВыделенныхСтрокРаздела) Тогда		
		Возврат;
	КонецЕсли;
	
	ОбъектыДействий = ОбъектыДействий(ДанныеВыделенныхСтрокРаздела);
	
	ЭтоПакетнаяОбработка = ЗначениеЗаполнено(ОбъектыДействий.ПакетыДокументов);
	Если ЭтоПакетнаяОбработка И Не ВыполнениеДействийПоЭДОДоступноДляПакетов(ОбъектыДействий.ПакетыДокументов) Тогда
		ВидДействия = ЛегкийИнтерфейсДокументовЭДОКлиент.ВидыВыполняемыхДействийСДокументом().ПринятьАннулирование;
		ТекстВопроса = ЛегкийИнтерфейсДокументовЭДОКлиент.ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ОставитьДляОбработкиТолькоТекущийДокумент(ОбъектыДействий);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НаборДействий = Новый Соответствие;	
	
	ДействияПоЭДОПринятьАннулирование = ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.ПринятьАннулирование");
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДОПринятьАннулирование);
	
	ДействияПоЭДОПодписать = ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.Подписать");
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДОПодписать);
	
	ДействияПоЭДОПодготовитьКОтправке = ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.ПодготовитьКОтправке");
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДОПодготовитьКОтправке);
	
	ДействияПоЭДООтправить = ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.Отправить");
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДООтправить);
				
	ПараметрыВыполненияДействийПоЭДО = ЭлектронныеДокументыЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	ПараметрыВыполненияДействийПоЭДО.НаборДействий = НаборДействий;
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ЭлектронныеДокументы = ОбъектыДействий.ЭлектронныеДокументы;
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ПакетыДокументов = ОбъектыДействий.ПакетыДокументов;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеУспешногоЗавершения", 
		Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект, НаборДействий));
	ДополнительныеПараметры.Вставить("ПараметрыВыполненияДействийПоЭДО", ПараметрыВыполненияДействийПоЭДО);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект, ДополнительныеПараметры);

	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ПараметрыВыполненияДействийПоЭДО);

КонецПроцедуры

&НаКлиенте
Асинх Процедура ОтклонитьАннулирование(Команда)

	ДанныеВыделенныхСтрокРаздела = ДанныеВыделенныхСтрокРаздела(ТекущийРаздел, 
		"ЭлектронныйДокумент, ИдентификаторПакета");
		
	Если Не ЗначениеЗаполнено(ДанныеВыделенныхСтрокРаздела) Тогда		
		Возврат;
	КонецЕсли;
	
	ОбъектыДействий = ОбъектыДействий(ДанныеВыделенныхСтрокРаздела);

	ЭтоПакетнаяОбработка = ЗначениеЗаполнено(ОбъектыДействий.ПакетыДокументов);
	Если ЭтоПакетнаяОбработка И Не ВыполнениеДействийПоЭДОДоступноДляПакетов(ОбъектыДействий.ПакетыДокументов) Тогда
		ВидДействия = ЛегкийИнтерфейсДокументовЭДОКлиент.ВидыВыполняемыхДействийСДокументом().ОтклонитьАннулирование;
		ТекстВопроса = ЛегкийИнтерфейсДокументовЭДОКлиент.ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ОставитьДляОбработкиТолькоТекущийДокумент(ОбъектыДействий);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;

	ДополнительныеПараметры = Новый Структура;
	
	ДополнительныеПараметры.Вставить("ЭлектронныеДокументы", ОбъектыДействий.ЭлектронныеДокументы);
	ДополнительныеПараметры.Вставить("ПакетыДокументов", ОбъектыДействий.ПакетыДокументов);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект);
	ДополнительныеПараметры.Вставить("ОповещениеУспешногоЗавершения", 
		ОписаниеОповещения);
	
	НаборДействий = Новый Соответствие;
		
	ДействияПоЭДООтклонитьАннулирование = ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.ОтклонитьАннулирование");
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДООтклонитьАннулирование);
	
	ДействияПоЭДОПодписать = ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.Подписать");
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДОПодписать);
	
	ДействияПоЭДОПодготовитьКОтправке = ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.ПодготовитьКОтправке");
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДОПодготовитьКОтправке);
	
	ДействияПоЭДООтправить = ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.Отправить");
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДООтправить);
		
	ДополнительныеПараметры.Вставить("НаборДействий", НаборДействий);	
	ДополнительныеПараметры.Вставить("ОсновноеДействие", ДействияПоЭДООтклонитьАннулирование);
					
	Обработчик = Новый ОписаниеОповещения("ВыполнитьДействияПоЭДОПослеВводаСтроки", 
		ИнтерфейсДокументовЭДОКлиент, ДополнительныеПараметры);
	
	ДополнительныеПараметры = ОбщегоНазначенияБЭДКлиент.ПараметрыВводаСтроки();
	ДополнительныеПараметры.ЗаголовокФормы = НСтр("ru = 'Укажите причины отклонения аннулирования документа'");
	ДополнительныеПараметры.НазваниеКнопкиПоУмолчанию = НСтр("ru = 'Отклонить аннулирование'");
	ДополнительныеПараметры.Многострочность = Истина;
	ДополнительныеПараметры.Обязательность = Истина;
	ДополнительныеПараметры.КомментарийОбязательностиВвода =
		НСтр("ru = 'Для отклонения аннулирования документа необходимо указать причину.'");
	ОбщегоНазначенияБЭДКлиент.ПоказатьВводСтрокиБЭД(Обработчик, ДополнительныеПараметры);

КонецПроцедуры

&НаКлиенте
Асинх Процедура Отправить(Команда)
	
	ДанныеВыделенныхСтрокРаздела = ДанныеВыделенныхСтрокРаздела(ТекущийРаздел, 
		"ЭлектронныйДокумент, ИдентификаторПакета");
		
	Если Не ЗначениеЗаполнено(ДанныеВыделенныхСтрокРаздела) Тогда		
		Возврат;
	КонецЕсли;
		
	ОбъектыДействий = ОбъектыДействий(ДанныеВыделенныхСтрокРаздела);
	
	ЭтоПакетнаяОбработка = ЗначениеЗаполнено(ОбъектыДействий.ПакетыДокументов);
	Если ЭтоПакетнаяОбработка И Не ВыполнениеДействийПоЭДОДоступноДляПакетов(ОбъектыДействий.ПакетыДокументов) Тогда
		ВидДействия = ЛегкийИнтерфейсДокументовЭДОКлиент.ВидыВыполняемыхДействийСДокументом().Отправить;
		ТекстВопроса = ЛегкийИнтерфейсДокументовЭДОКлиент.ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ОставитьДляОбработкиТолькоТекущийДокумент(ОбъектыДействий);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;

	НаборДействий = Новый Соответствие;	
	ДействияПоЭДОПодготовитьКОтправке = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.ПодготовитьКОтправке");
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДОПодготовитьКОтправке);
	ДействияПоЭДООтправить = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Отправить");
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДООтправить);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект);
	
	ПараметрыВыполненияДействийПоЭДО = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	ПараметрыВыполненияДействийПоЭДО.НаборДействий = НаборДействий;
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ЭлектронныеДокументы = ОбъектыДействий.ЭлектронныеДокументы;
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ПакетыДокументов = ОбъектыДействий.ПакетыДокументов;

	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ПараметрыВыполненияДействийПоЭДО);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВернутьНаПодготовкуКОтправке(Команда)
	
	ДанныеВыделенныхСтрокРаздела = ДанныеВыделенныхСтрокРаздела(ТекущийРаздел, 
		"ЭлектронныйДокумент, ИдентификаторПакета");
		
	Если Не ЗначениеЗаполнено(ДанныеВыделенныхСтрокРаздела) Тогда		
		Возврат;
	КонецЕсли;
	
	ОбъектыДействий = ОбъектыДействий(ДанныеВыделенныхСтрокРаздела);
	
	ЭтоПакетнаяОбработка = ЗначениеЗаполнено(ОбъектыДействий.ПакетыДокументов);
	Если ЭтоПакетнаяОбработка И Не ВыполнениеДействийПоЭДОДоступноДляПакетов(ОбъектыДействий.ПакетыДокументов) Тогда
		ВидДействия = ЛегкийИнтерфейсДокументовЭДОКлиент.ВидыВыполняемыхДействийСДокументом().ВернутьНаПодготовкуКОтправке;
		ТекстВопроса = ЛегкийИнтерфейсДокументовЭДОКлиент.ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ОставитьДляОбработкиТолькоТекущийДокумент(ОбъектыДействий);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НаборДействий = Новый Соответствие;	
	ДействияПоЭДООтменитьОтправку = ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.ОтменитьОтправку");
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействияПоЭДООтменитьОтправку);
		
	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект);
	
	ПараметрыВыполненияДействийПоЭДО = ЭлектронныеДокументыЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	ПараметрыВыполненияДействийПоЭДО.НаборДействий = НаборДействий;
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ЭлектронныеДокументы = ОбъектыДействий.ЭлектронныеДокументы;
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ПакетыДокументов = ОбъектыДействий.ПакетыДокументов;

	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ПараметрыВыполненияДействийПоЭДО);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ПолучитьОтправитьОбновить();
	
КонецПроцедуры

#КонецОбласти // ВыполнениеДействийПоЭДО

&НаКлиенте
Процедура ЗагрузитьНовыйДокументСДиска(Команда)
	
	Шаблон = НСтр("ru = 'Для создания документа на основании файла необходимо завершить подключение к ЭДО и повторить действие.'");
	Операция = НСтр("ru = 'Создание документа на основании файла'");
	Если ИнтерфейсДокументовЭДОКлиент.ПрерватьВыполнениеОперацииДляПодключенияКЭДО(ЭтотОбъект, Шаблон, Операция) Тогда
		Возврат;
	КонецЕсли;

	ЛегкийИнтерфейсДокументовЭДОКлиент.СоздатьДокументыПоФайламКоманда(УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура РасширенныйПоиск(Команда)
	
	РасширенныйПоискРаскрыт = Не РасширенныйПоискРаскрыт;
	Элементы.ГруппаБыстрыеОтборы.Видимость = Не Элементы.ГруппаБыстрыеОтборы.Видимость;
	ВидимостьЭлементовИКоманд();
	
КонецПроцедуры 

&НаКлиенте
Процедура ОчиститьФильтры(Команда)
	
	ОчиститьНастройкиСписков();
	ОчиститьПользовательскиеОтборы();
	ВидимостьЭлементовИКоманд();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПодписатьИОтправить(Команда)
	
	Шаблон = НСтр("ru = 'Для продолжения отправки документа необходимо завершить подключение к ЭДО и повторить действие.'");
	Операция = НСтр("ru = 'Подписание и отправка документа'");
	Если ИнтерфейсДокументовЭДОКлиент.ПрерватьВыполнениеОперацииДляПодключенияКЭДО(ЭтотОбъект, Шаблон, Операция) Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Черновики.ВыделенныеСтроки.Количество() Тогда
		ПодключитьОбработчикОжидания("ВыполнитьСформироватьПодписатьИОтправить", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура РаспаковатьКонтейнеры(Команда)

	СинхронизацияЭДОКлиент.РаспаковатьТранспортныеКонтейнеры(Элементы.Ошибки.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусОтмененКонтейнера(Команда)
	
	УстановитьСтатусОтменен(Элементы.Ошибки.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПриглашение(Команда)
	
	ОчиститьСообщения();
	ДанныеПриглашений = ДанныеВыделенныхСтрокРазделаПриглашений(ДействиеСПриглашением_Отправить(),
		"КлючПриглашения, ИдентификаторОрганизации, ИдентификаторКонтрагента, Контрагент, Организация");

	ИсходящиеПриглашения = Новый Массив;

	Для Каждого ДанныеПриглашения Из ДанныеПриглашений Цикл	
		ИсходящееПриглашение = СинхронизацияЭДОКлиент.НовоеПриглашениеНаИдентификатор();
		ЗаполнитьЗначенияСвойств(ИсходящееПриглашение, ДанныеПриглашения);	
		ИсходящиеПриглашения.Добавить(ИсходящееПриглашение);
	КонецЦикла;
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ОтправкаПриглашенийОповещениеОЗавершении", ЭтотОбъект);
		
	СинхронизацияЭДОКлиент.ОтправитьПриглашения(ИсходящиеПриглашения, ЭтотОбъект, ОбработчикОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ПринятьПриглашения(Команда)
	
	Если Элементы.Приглашения.ВыделенныеСтроки.Количество() Тогда
		БлокируемыеЭлементы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Элементы.КомандаПринятьПриглашения.Имя);
		УстановитьБлокировкуЭлементов(БлокируемыеЭлементы);
		ПодключитьОбработчикОжидания("НачатьПринятиеПриглашений", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ОтклонитьПриглашения(Команда)
	
	Если Элементы.Приглашения.ВыделенныеСтроки.Количество() Тогда
		БлокируемыеЭлементы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Элементы.КомандаОтклонитьПриглашения.Имя);
		УстановитьБлокировкуЭлементов(БлокируемыеЭлементы);
		ПодключитьОбработчикОжидания("НачатьОтклонениеПриглашений", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПометитьКакПрочитанное(Команда)
	
	ТекущийСписок = Элементы[ТекущийРаздел];
	
	Если ТекущийСписок <> Неопределено Тогда
		
		ИнтерфейсДокументовЭДОКлиент.ПометитьКакПрочтенные(ТекущийСписок);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЭДО(Команда)
	
	Шаблон = НСтр("ru = 'Для открытия настроек необходимо завершить подключение к ЭДО и повторить действие.'");
	Операция = НСтр("ru = 'Настройки ЭДО'");
	Если ИнтерфейсДокументовЭДОКлиент.ПрерватьВыполнениеОперацииДляПодключенияКЭДО(ЭтотОбъект, Шаблон, Операция) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиЭДОКлиент.ОткрытьФормуНастроекЭДО();
	
КонецПроцедуры

&НаКлиенте
Процедура ЕстьИдея(Команда)
	
	ИнтерфейсДокументовЭДОКлиент.ОткрытьСтраницуОпроса();

КонецПроцедуры

&НаКлиенте
Процедура Техподдержка(Команда)
	
	ИнтерфейсДокументовЭДОКлиент.ОткрытьОбращениеВСлужбуТехническойПоддержки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОМодулеЭДО(Команда)
			
	ОткрытьФорму("Обработка.ИнтерфейсДокументовЭДО.Форма.ОМодулеЭДО", , , , , , ОписаниеОповещенияОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьУдаленные(Команда)
	
	ОтображатьУдаленные = Не ОтображатьУдаленные;
	
	ОтображатьУдаленныеСервер();
	ОбновитьДинамическиеСпискиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура НапечататьЭлектронныеДокументы(Команда)
		
	ВывестиНаПечать();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДокументооборотЦеликом(Команда)
	
	ЭлектронныеДокументы = Элементы[ТекущийРаздел].ВыделенныеСтроки;
	
	Если Не ЗначениеЗаполнено(ЭлектронныеДокументы) Тогда
		ТекстСообщения = НСтр("ru = 'Для выгрузки необходимо выбрать электронный документ'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ИнтерфейсДокументовЭДОКлиент.ВыгрузитьДокументооборотЦеликом(ЭлектронныеДокументы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЭлектронныеДокументыВФорматеПДФ(Команда)
	
	ЭлектронныеДокументы = Элементы[ТекущийРаздел].ВыделенныеСтроки;
	
	Если Не ЗначениеЗаполнено(ЭлектронныеДокументы) Тогда
		ТекстСообщения = НСтр("ru = 'Для выгрузки необходимо выбрать электронный документ'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ИнтерфейсДокументовЭДОКлиент.ВыгрузитьЭлектронныеДокументыВФорматеPDF(ЭлектронныеДокументы, 
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЭлектронныеДокументыДляФНС(Команда)
	
	ЭлектронныеДокументы = Элементы[ТекущийРаздел].ВыделенныеСтроки;
	
	Если Не ЗначениеЗаполнено(ЭлектронныеДокументы) Тогда
		ТекстСообщения = НСтр("ru = 'Для выгрузки необходимо выбрать электронный документ'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ИнтерфейсДокументовЭДОКлиент.ВыгрузитьЭлектронныеДокументыДляФНС(ЭлектронныеДокументы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьМашиночитаемыеДоверенности(Команда)

	МашиночитаемыеДоверенностиКлиент.ОткрытьОбщуюФормуСписковМЧД();
	
КонецПроцедуры

&НаКлиенте
Процедура НовоеПриглашение(Команда)
	
	Шаблон = НСтр("ru = 'Для продолжения отправки приглашений необходимо завершить подключение к ЭДО и повторить действие.'");
	Операция = НСтр("ru = 'Отправка приглашений'");
	Если ИнтерфейсДокументовЭДОКлиент.ПрерватьВыполнениеОперацииДляПодключенияКЭДО(ЭтотОбъект, Шаблон, Операция) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПриглашения = СинхронизацияЭДОКлиент.НовыеПараметрыОткрытияПомощникаОтправкиПриглашения();
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		ПараметрыПриглашения.Контрагент = Контрагент;
	Иначе
		ТекущиеДанные = Элементы.Приглашения.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.КонтрагентСортировка) Тогда
			ПараметрыПриглашения.Контрагент = ТекущиеДанные.КонтрагентСортировка;
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(Организация) Тогда
		ПараметрыПриглашения.Организация = Организация;
	КонецЕсли;
	
	СинхронизацияЭДОКлиент.ОткрытьПомощникОтправкиПриглашения(ПараметрыПриглашения);

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодписи(Команда)
	ОбщегоНазначенияБЭДКлиент.ОткрытьФормуБЭД(
		"Обработка.ИнтерфейсДокументовЭДО.Форма.ПроверкаПодписей");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПользовательскиеОтборы(Команда)
	
	ТекущийСписок = ЭтотОбъект[ТекущийРаздел];
	ОповещениеОЗавершении = Новый ОписаниеОповещения("НастройкаОтборовСпискаЗавершение", ЭтотОбъект);
	ИнтерфейсДокументовЭДОКлиент.ОткрытьНастройкиОтборовСписка(ОповещениеОЗавершении, ЭтотОбъект, 
		ТекущийСписок.КомпоновщикНастроек);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайлы(Команда)
	
	Шаблон = НСтр("ru = 'Для выполнения массовой отправки файлов необходимо завершить подключение к ЭДО и повторить действие.'");
	Операция = НСтр("ru = 'Массовая отправка файлов'");
	Если ИнтерфейсДокументовЭДОКлиент.ПрерватьВыполнениеОперацииДляПодключенияКЭДО(ЭтотОбъект, Шаблон, Операция) Тогда
		Возврат;
	КонецЕсли;
	
	ИнтерфейсДокументовЭДОКлиент.ОткрытьФормуМассовойЗагрузкиФайлов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОткрытьФайлИнформацииОтправителяДокумента(ТекущийЭлектронныйДокумент,
		ТекущийДокументОблачногоЭДО, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьОтветственного(Команда)
	
	ЭлектронныеДокументы = Элементы[ТекущийРаздел].ВыделенныеСтроки;
	ДополнитьДокументамиПакетов(ЭлектронныеДокументы);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеСменыОтветственного", ЭтотОбъект);

	ИнтерфейсДокументовЭДОКлиент.ПеренаправитьЭлектронныеДокументы(ЭлектронныеДокументы, ОповещениеОЗавершении);

КонецПроцедуры

&НаКлиенте
Процедура ВыборочныйОбменПоОрганизациям(Команда)
	ИнтерфейсДокументовЭДОВызовСервера.УстановитьОтправкуПолучениеСОтборомПоОрганизации(
		Не Элементы.ВыборочныйОбменПоОрганизациямВходящие.Пометка);
	Оповестить("УстановитьОтправкуПолучениеСОтборомПоОрганизации",
		Не Элементы.ВыборочныйОбменПоОрганизациямВходящие.Пометка);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРедакторДокумента(Команда)
	ПоказатьЗначение(, ТекущийДокументУчета);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПриглашение(Команда)
	
	Шаблон = НСтр("ru = 'Для продолжения отправки приглашений необходимо завершить подключение к ЭДО и повторить действие.'");
	Операция = НСтр("ru = 'Создание приглашений'");
	Если ИнтерфейсДокументовЭДОКлиент.ПрерватьВыполнениеОперацииДляПодключенияКЭДО(ЭтотОбъект, Шаблон, Операция) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанныеПриглашения = Элементы.Приглашения.ТекущиеДанные;
	
	Если ТекущиеДанныеПриглашения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанныеПриглашения.ПриглашениеСоздано Тогда
		
		СтруктураКлюча = Новый Структура;
		СтруктураКлюча.Вставить("Ключ",ТекущиеДанныеПриглашения.КлючПриглашения);
		СтруктураКлюча.Вставить("ИдентификаторОрганизации",ТекущиеДанныеПриглашения.ИдентификаторОрганизации);
		ПараметрыФормы = Новый Структура("Ключ", СтруктураКлюча);
		ОткрытьФорму("РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами.Форма.ФормаПриглашения",
			ПараметрыФормы);
	Иначе
		ПараметрыФормы = Новый Структура("Контрагент", ТекущиеДанныеПриглашения.КонтрагентСортировка);
		ОткрытьФорму("РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами.Форма.ПомощникОтправкиПриглашенияЛегкийКлиентЭДО",
			ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКонтрагента(Команда)
	
	ТекущиеДанные = Элементы.Приглашения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПоказатьЗначение(, ТекущиеДанные.КонтрагентСортировка);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЧерновик(Команда)
	
	ЧерновикиКУдалению = ДанныеВыделенныхСтрокРаздела(ТекущийРаздел, "Документ");
	
	Если Не ЗначениеЗаполнено(ЧерновикиКУдалению) Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьЧерновикНаСервере(ЧерновикиКУдалению);
	
	ЛегкийИнтерфейсДокументовЭДОКлиент.ОповеститьОбИзмененииСвязанныхДокументовОснованийЭлектронныхДокументов(
		ЧерновикиКУдалению);
	
	ВидимостьЭлементовИКоманд();
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	Если ТекущийРаздел = РазделыТекущихДелЭДО().Приглашения Тогда
		ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Приглашения);
	Иначе
		ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.ИсходящиеЭлектронныеДокументы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.ИсходящиеЭлектронныеДокументы);
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Приглашения);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти // ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСоСписками

&НаСервере
Процедура ОтображениеОрганизации()

	НесколькоОрганизаций = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ 2
		|	УчетныеЗаписиЭДО.Организация КАК Организация
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.Организация.ПометкаУдаления = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();
	
	Если ВыборкаДетальныеЗаписи.Количество() > 1 Тогда
		
		НесколькоОрганизаций = Истина;	
	
	КонецЕсли;
	
	Элементы.ВходящиеЭлектронныеДокументыОрганизация.Видимость = НесколькоОрганизаций;
	Элементы.ИсходящиеЭлектронныеДокументыОрганизация.Видимость = НесколькоОрганизаций;
	Элементы.ЧерновикиОрганизация.Видимость = НесколькоОрганизаций;
	Элементы.ПриглашенияОрганизация.Видимость = НесколькоОрганизаций;	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыСписков()
		
	РежимОтображения = "ВсеДокументы";
	
	СписокПользователей = Новый Массив;
	СписокПользователей.Добавить(Пользователи.АвторизованныйПользователь());
	Если РежимОтображения <> "МоиДокументы" Тогда
		СписокПользователей.Добавить(ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка"));
	КонецЕсли;
	
	НастройкиОтображения = Новый Структура;
	НастройкиОтображения.Вставить("РежимОтображения", РежимОтображения);
	НастройкиОтображения.Вставить("СписокПользователей", СписокПользователей);
	НастройкиОтображения.Вставить("ОтправлятьВходящиеДокументыНаУтверждение", Истина);
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ДатаПереходаНаВерсиюСПрочтенностью = РаботаСПрочтениямиВызовСервера.ПолучитьДатуПереходаНаВерсиюСПрочтенностью();
	
	СостоянияЭДОЗакрыт = Новый Массив;
	СостоянияЭДОЗакрыт.Добавить(Перечисления.СостоянияДокументовЭДО.ЗакрытПринудительно);	
	СостоянияЭДОЗакрыт.Добавить(Перечисления.СостоянияДокументовЭДО.ЗакрытСОтклонением);	
	СостоянияЭДОЗакрыт.Добавить(Перечисления.СостоянияДокументовЭДО.ЗакрытСОтклонениемПриглашения);	
	СостоянияЭДОЗакрыт.Добавить(Перечисления.СостоянияДокументовЭДО.ЗакрытСОшибкойПередачи);
	
	СостоянияЭДООжидается = Новый Массив;
	СостоянияЭДООжидается.Добавить(Перечисления.СостоянияДокументовЭДО.ОжидаетсяИзвещениеОПолучении);	
	СостоянияЭДООжидается.Добавить(Перечисления.СостоянияДокументовЭДО.ОжидаетсяИзвещениеПоОтклонению);	
	СостоянияЭДООжидается.Добавить(Перечисления.СостоянияДокументовЭДО.ОжидаетсяИсправление);	
	СостоянияЭДООжидается.Добавить(Перечисления.СостоянияДокументовЭДО.ОжидаетсяОтветНаПриглашение);
	СостоянияЭДООжидается.Добавить(Перечисления.СостоянияДокументовЭДО.ОжидаетсяПередачаОператору);	
	СостоянияЭДООжидается.Добавить(Перечисления.СостоянияДокументовЭДО.ОжидаетсяПодтверждение);
	СостоянияЭДООжидается.Добавить(Перечисления.СостоянияДокументовЭДО.ОжидаетсяПодтверждениеАннулирования);	
	СостоянияЭДООжидается.Добавить(Перечисления.СостоянияДокументовЭДО.ОжидаетсяПодтверждениеОператора);
	
	СостоянияЭДОТребуетсяДействие = Новый Массив;
	СостоянияЭДОТребуетсяДействие.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписание);	
	СостоянияЭДОТребуетсяДействие.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяУтверждение);	
	СостоянияЭДОТребуетсяДействие.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодтверждениеАннулирования);	
	СостоянияЭДОТребуетсяДействие.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяУточнение);
	СостоянияЭДОТребуетсяДействие.Добавить(Перечисления.СостоянияДокументовЭДО.НеСформирован);
	
	Для каждого ИмяСписка Из ИменаСписковФормы() Цикл
		
		Если ИмяСписка = РазделыТекущихДелЭДО().Приглашения
			Или ИмяСписка = РазделыТекущихДелЭДО().Черновики Тогда
			ИмяПоляДата = "Дата";
		ИначеЕсли ИмяСписка = РазделыТекущихДелЭДО().Ошибки Тогда
			Продолжить;
		Иначе
			ИмяПоляДата = "ДатаПолучения";
		КонецЕсли;
			
		УстановитьУсловноеОформлениеСписков(ИмяСписка, ИмяПоляДата);
			
		Если ИмяСписка <> РазделыТекущихДелЭДО().Приглашения Тогда
			УстановитьВариантОтображенияДокументовСписка(ИмяСписка);
		КонецЕсли;
		
		Если ИмяСписка = РазделыТекущихДелЭДО().Черновики Тогда
			Продолжить;
		ИначеЕсли ИмяСписка = РазделыТекущихДелЭДО().Приглашения Тогда
			УстановитьВариантОтображенияПриглашенийВСписке();
			Продолжить;
		КонецЕсли;
		
		Список = ЭтотОбъект[ИмяСписка];
		
		Список.Параметры.УстановитьЗначениеПараметра("ОтображатьУдаленные", ОтображатьУдаленные);
		Список.Параметры.УстановитьЗначениеПараметра("РежимОтображения", НастройкиОтображения.РежимОтображения);
		Список.Параметры.УстановитьЗначениеПараметра("СписокПользователей", НастройкиОтображения.СписокПользователей);
		Список.Параметры.УстановитьЗначениеПараметра("Пользователь", ТекущийПользователь);
		Список.Параметры.УстановитьЗначениеПараметра("ДатаПереходаНаВерсиюСПрочтенностью",
			ДатаПереходаНаВерсиюСПрочтенностью); 
		
	КонецЦикла;
	
	ИсходящиеЭлектронныеДокументы.Параметры.УстановитьЗначениеПараметра("СостоянияЭДОДляРазделаИсходящие",
		СостоянияЭДОДляРазделаИсходящие());
	
	Черновики.Параметры.УстановитьЗначениеПараметра("ПустойКонтрагент", ПустойКонтрагент);
	Черновики.Параметры.УстановитьЗначениеПараметра("СостоянияЭДОДляРазделаЧерновики",
		СостоянияЭДОДляРазделаЧерновики());
	
	Приглашения.Параметры.УстановитьЗначениеПараметра("ПустойКонтрагент", 
		ПустойКонтрагент);
	Приглашения.Параметры.УстановитьЗначениеПараметра("ТекстПриглашениеНеСформировано",
		НСтр("ru = 'Не сформировано'"));
	
	СтатусыНеРаспакованныхТранспортныхСообщенийБЭД = СтатусыНеРаспакованныхТранспортныхСообщенийБЭД();
	Ошибки.Параметры.УстановитьЗначениеПараметра("СтатусыНеРаспакованныхТранспортныхСообщенийБЭД",
		СтатусыНеРаспакованныхТранспортныхСообщенийБЭД);
	
	НаправлениеВходящий = ПредопределенноеЗначение("Перечисление.НаправленияЭДО.Входящий");
	Ошибки.Параметры.УстановитьЗначениеПараметра("Направление",
		НаправлениеВходящий);
	Ошибки.Параметры.УстановитьЗначениеПараметра("ОтображатьУдаленные", ОтображатьУдаленные);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СостоянияЭДОДляРазделаИсходящие()
	
	СостоянияЭДОДляРазделаИсходящие = Новый Массив; // Массив Из ПеречислениеСсылка.СостоянияДокументовЭДО
	Для Каждого ДанныеЗначения Из Метаданные.Перечисления.СостоянияДокументовЭДО.ЗначенияПеречисления Цикл
		ЗначениеПеречисления = Перечисления.СостоянияДокументовЭДО[ДанныеЗначения.Имя];
		Если ЗначениеПеречисления = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписание
			Или ЗначениеПеречисления = Перечисления.СостоянияДокументовЭДО.ТребуетсяУтверждение
			Или ЗначениеПеречисления = Перечисления.СостоянияДокументовЭДО.ТребуетсяОтправка
			Или ЗначениеПеречисления = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодготовкаКОтправке Тогда
			Продолжить;
		КонецЕсли;
		СостоянияЭДОДляРазделаИсходящие.Добавить(ЗначениеПеречисления);
	КонецЦикла;

	Возврат СостоянияЭДОДляРазделаИсходящие;
	
КонецФункции

&НаСервереБезКонтекста
Функция СостоянияЭДОДляРазделаЧерновики()
	
	Возврат РегистрыСведений.ЧерновикиЭлектронныхДокументовЛегкийКлиентЭДО.СостоянияЭДОДляЧерновиков();
	
КонецФункции

&НаСервере
Процедура УстановитьВариантОтображенияДокументовСписка(ИмяСписка)
		
	ПоляОграничения = Новый Массив;
	
	БазовыеНастройкиКомпоновки = Новый КомпоновщикНастроекКомпоновкиДанных;
	БазовыеНастройкиКомпоновки.ЗагрузитьНастройки(ЭтотОбъект[ИмяСписка].КомпоновщикНастроек.Настройки);
	БазовыеНастройкиКомпоновки.Настройки.Порядок.Элементы.Очистить();
	
	ЭтотОбъект[ИмяСписка].КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(
		БазовыеНастройкиКомпоновки.ПользовательскиеНастройки);
	ЭтотОбъект[ИмяСписка].КомпоновщикНастроек.Настройки.Порядок.Элементы.Очистить();
	
	ЭлементПорядка = ЭтотОбъект[ИмяСписка].КомпоновщикНастроек.Настройки.Порядок.Элементы.Добавить(
		Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных("ДатаПолучения");
	ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Убыв;
	ЭлементПорядка.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементПорядка.Использование = Истина;
	
	ЭлементПорядка = ЭтотОбъект[ИмяСписка].КомпоновщикНастроек.Настройки.Порядок.Элементы.Добавить(
		Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных("КлючСортировкиПакета");
	ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	ЭлементПорядка.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементПорядка.Использование = Истина;
	
	ЭлементПорядка = ЭтотОбъект[ИмяСписка].КомпоновщикНастроек.Настройки.Порядок.Элементы.Добавить(
		Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных("ПорядокСортировкиВПакете");
	ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	ЭлементПорядка.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементПорядка.Использование = Истина;
	
	ЭлементПорядка = ЭтотОбъект[ИмяСписка].КомпоновщикНастроек.Настройки.Порядок.Элементы.Добавить(
		Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных("НомерСлужебный");
	ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Убыв;
	ЭлементПорядка.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементПорядка.Использование = Истина; 
		
	СлужебныеПоляПорядка = Новый Массив;
	СлужебныеПоляПорядка.Добавить("ДатаПолучения");
	СлужебныеПоляПорядка.Добавить("КлючСортировкиПакета");
	СлужебныеПоляПорядка.Добавить("ПорядокСортировкиВПакете");
	СлужебныеПоляПорядка.Добавить("НомерСлужебный");
	
	Для Каждого ПолеПорядка Из 
		ЭтотОбъект[ИмяСписка].КомпоновщикНастроек.Настройки.Порядок.ДоступныеПоляПорядка.Элементы Цикл
		ИмяПоля = Строка(ПолеПорядка.Поле);
		Если СлужебныеПоляПорядка.Найти(ИмяПоля) <> Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		ПоляОграничения.Добавить(ИмяПоля);
	КонецЦикла;	
	
	Если ПоляОграничения.Количество() > 0 Тогда
		ЭтотОбъект[ИмяСписка].УстановитьОграниченияИспользованияВПорядке(ПоляОграничения);
	КонецЕсли;
		
	СлужебныеПоляОтбора = Новый Массив;
	СлужебныеПоляОтбора.Добавить("Контрагент"); 
	СлужебныеПоляОтбора.Добавить("Организация"); 
	СлужебныеПоляОтбора.Добавить("ДатаПолучения");
	СлужебныеПоляОтбора.Добавить("СуммаДокумента");
	СлужебныеПоляОтбора.Добавить("СостояниеЭДО");
	СлужебныеПоляОтбора.Добавить("ТипДокумента");
	СлужебныеПоляОтбора.Добавить("Ответственный");
	СлужебныеПоляОтбора.Добавить("ВидДокумента");
	СлужебныеПоляОтбора.Добавить("ИдентификаторПакета");
	СлужебныеПоляОтбора.Добавить("ТипРегламента");
	СлужебныеПоляОтбора.Добавить("ЭлектронныйДокумент");
		
	ПоляОграничения = Новый Массив;
	
	Для Каждого ПолеПорядка Из 
		ЭтотОбъект[ИмяСписка].КомпоновщикНастроек.Настройки.Отбор.ДоступныеПоляОтбора.Элементы Цикл
		ИмяПоля = Строка(ПолеПорядка.Поле);
		Если СлужебныеПоляОтбора.Найти(ИмяПоля) <> Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		ПоляОграничения.Добавить(ИмяПоля);
	КонецЦикла;	
	
	Если ПоляОграничения.Количество() > 0 Тогда
		ЭтотОбъект[ИмяСписка].УстановитьОграниченияИспользованияВОтборе(ПоляОграничения);
	КонецЕсли;
			
	Элементы[ИмяСписка].Обновить();
		
КонецПроцедуры 

&НаСервере
Процедура УстановитьВариантОтображенияПриглашенийВСписке()
	
	КомпоновщикНастроекСписка = Приглашения.КомпоновщикНастроек;
	
	ПоляОграничения = Новый Массив;
	
	БазовыеНастройкиКомпоновки = Новый КомпоновщикНастроекКомпоновкиДанных;
	БазовыеНастройкиКомпоновки.ЗагрузитьНастройки(КомпоновщикНастроекСписка.Настройки);
	БазовыеНастройкиКомпоновки.Настройки.Порядок.Элементы.Очистить();
	
	КомпоновщикНастроекСписка.ЗагрузитьПользовательскиеНастройки(
		БазовыеНастройкиКомпоновки.ПользовательскиеНастройки);
	КомпоновщикНастроекСписка.Настройки.Порядок.Элементы.Очистить();
	
	ЭлементПорядка = КомпоновщикНастроекСписка.Настройки.Порядок.Элементы.Добавить(
		Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных("ДатаИзмененияСтатусаПоКонтрагенту");
	ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Убыв;
	ЭлементПорядка.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементПорядка.Использование = Истина;
	
	ЭлементПорядка = КомпоновщикНастроекСписка.Настройки.Порядок.Элементы.Добавить(
		Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных("КонтрагентСортировка");
	ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	ЭлементПорядка.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементПорядка.Использование = Истина;
	
	ЭлементПорядка = КомпоновщикНастроекСписка.Настройки.Порядок.Элементы.Добавить(
		Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных("ДатаИзмененияСтатуса");
	ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Убыв;
	ЭлементПорядка.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементПорядка.Использование = Истина;
	
	ЭлементПорядка = КомпоновщикНастроекСписка.Настройки.Порядок.Элементы.Добавить(
		Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных("ИндексСтатусаПодключения");
	ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	ЭлементПорядка.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементПорядка.Использование = Истина;
	
	ЭлементПорядка = КомпоновщикНастроекСписка.Настройки.Порядок.Элементы.Добавить(
		Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных("КлючПриглашения");
	ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	ЭлементПорядка.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементПорядка.Использование = Истина;
	
	СлужебныеПоляПорядка = Новый Массив;
	СлужебныеПоляПорядка.Добавить("ДатаИзмененияСтатусаПоКонтрагенту");
	СлужебныеПоляПорядка.Добавить("КонтрагентСортировка");
	СлужебныеПоляПорядка.Добавить("ДатаИзмененияСтатуса");
	СлужебныеПоляПорядка.Добавить("ИндексСтатусаПодключения");
	СлужебныеПоляПорядка.Добавить("КлючПриглашения");
	СлужебныеПоляПорядка.Добавить("Контрагент");
	СлужебныеПоляПорядка.Добавить("ИдентификаторОрганизации");
	
	Для Каждого ПолеПорядка Из КомпоновщикНастроекСписка.Настройки.Порядок.ДоступныеПоляПорядка.Элементы Цикл
		ИмяПоля = Строка(ПолеПорядка.Поле);
		Если СлужебныеПоляПорядка.Найти(ИмяПоля) = Неопределено Тогда
			ПоляОграничения.Добавить(ИмяПоля);
		КонецЕсли;
	КонецЦикла;
	
	Если ПоляОграничения.Количество() > 0 Тогда
		Приглашения.УстановитьОграниченияИспользованияВПорядке(ПоляОграничения);
	КонецЕсли;
	
	Элементы.Приглашения.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеСписков(ИмяСписка, ИмяПоляДаты)
	
	Если ИмяСписка <> РазделыТекущихДелЭДО().Приглашения
		И ИмяСписка <> РазделыТекущихДелЭДО().Черновики
		И ПолучитьФункциональнуюОпцию("ИспользоватьПрочтениеДокументовЭДО") Тогда
			
		УстановитьУсловноеОформлениеСпискаДляОтображенияНепрочитанныхДокументов(ИмяСписка, ИмяПоляДаты);
		
	КонецЕсли;

	Если ИмяСписка <> РазделыТекущихДелЭДО().Приглашения
		И ИмяСписка <> РазделыТекущихДелЭДО().Черновики Тогда
			
		УстановитьУсловноеОформлениеСпискаДляОтображенияПомеченныхНаУдалениеДокументов(ИмяСписка, ИмяПоляДаты);
		
	КонецЕсли;
		
	Если ИмяСписка <> РазделыТекущихДелЭДО().Приглашения Тогда
			
		УстановитьУсловноеОформлениеСпискаДляПодсветкиДокументовПакета(ИмяСписка);
		
	Иначе
		
		УстановитьУсловноеОформлениеСпискаДляПодсветкиПриглашенийКонтрагента(ИмяСписка);
		
	КонецЕсли;
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[ИмяСписка + ИмяПоляДаты].Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяСписка + "." + ИмяПоляДаты);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ОтборЭлемента.ПравоеЗначение = Новый СтандартнаяДатаНачала(ВариантСтандартнойДатыНачала.НачалоЭтогоДня);
	ОтборЭлемента.Использование = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("Формат", "ДФ=ЧЧ:мм");
		
	Элемент.Представление = 
		НСтр("ru = 'Отображать дату сегодняшних писем только в виде времени ""09:46"" (стандартная настройка)'");

	Если ЕстьДублирующийсяЭлемент(УсловноеОформление.Элементы, Элемент) Тогда
		УсловноеОформление.Элементы.Удалить(Элемент);
	КонецЕсли;
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[ИмяСписка + ИмяПоляДаты].Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяСписка + "." + ИмяПоляДаты);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = Новый СтандартнаяДатаНачала(ВариантСтандартнойДатыНачала.НачалоЭтогоДня);
	ОтборЭлемента.Использование = Истина;

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяСписка + "." + ИмяПоляДаты);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ОтборЭлемента.ПравоеЗначение = Новый СтандартнаяДатаНачала(ВариантСтандартнойДатыНачала.НачалоЭтогоМесяца);
	ОтборЭлемента.Использование = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Формат", "ДФ='dd MMMM'");
			
	Элемент.Представление = 
		НСтр("ru = 'Отображать дату писем за эту неделю в виде ""Вт 10.06"" (стандартная настройка)'");
	
	Если ЕстьДублирующийсяЭлемент(УсловноеОформление.Элементы, Элемент) Тогда
		УсловноеОформление.Элементы.Удалить(Элемент);
	КонецЕсли;

	Элемент = УсловноеОформление.Элементы.Добавить(); 
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[ИмяСписка + ИмяПоляДаты].Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяСписка + "." + ИмяПоляДаты);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = Новый СтандартнаяДатаНачала(ВариантСтандартнойДатыНачала.НачалоЭтогоМесяца);
	ОтборЭлемента.Использование = Истина;

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяСписка + "." + ИмяПоляДаты);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ОтборЭлемента.ПравоеЗначение = Новый СтандартнаяДатаНачала(ВариантСтандартнойДатыНачала.НачалоЭтогоГода);
	ОтборЭлемента.Использование = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Формат", "ДФ='dd.MM'");
	
	Элемент.Представление = НСтр("ru = 'Отображать дату писем за этот год в виде ""10.06"" (стандартная настройка)'");
	
	Если ЕстьДублирующийсяЭлемент(УсловноеОформление.Элементы, Элемент) Тогда
		УсловноеОформление.Элементы.Удалить(Элемент);
	КонецЕсли;
		
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[ИмяСписка + ИмяПоляДаты].Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяСписка + "." + ИмяПоляДаты);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = Новый СтандартнаяДатаНачала(ВариантСтандартнойДатыНачала.НачалоЭтогоГода);
	ОтборЭлемента.Использование = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Формат", "ДФ='dd.MM.yy'");
			
	Элемент.Представление = НСтр("ru = 'Отображать дату писем за период более ранний, чем этот год в виде ""10.06.2022"" 
		|(стандартная настройка)'");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеСпискаДляОтображенияПомеченныхНаУдалениеДокументов(ИмяСписка, ИмяПоляДаты)

	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru = 'Отображение помеченных на удаление электронных документов'");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[ИмяСписка + "Контрагент"].Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[ИмяСписка + "СуммаДокумента"].Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[ИмяСписка + "ЭлектронныйДокумент"].Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[ИмяСписка + ИмяПоляДаты].Имя); 
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[ИмяСписка + "Состояние"].Имя);
	
	Если НесколькоОрганизаций Тогда
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[ИмяСписка + "Организация"].Имя);
	КонецЕсли;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяСписка + ".ПометкаУдаления");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Шрифт = Метаданные.ЭлементыСтиля.ЗачеркнутыйШрифтБЭД.Значение;
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Шрифт);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", 
		Метаданные.ЭлементыСтиля.ЦветРамкиПредпросмотраЭлектронногоДокумента.Значение);
		
	Если ЕстьДублирующийсяЭлемент(УсловноеОформление.Элементы, Элемент) Тогда
		УсловноеОформление.Элементы.Удалить(Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеСпискаДляОтображенияНепрочитанныхДокументов(ИмяСписка, ИмяПоляДаты)
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Представление = НСтр("ru = 'Отображение непрочитанных документов'");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[ИмяСписка + "Контрагент"].Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[ИмяСписка + "СуммаДокумента"].Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[ИмяСписка + "ЭлектронныйДокумент"].Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[ИмяСписка + ИмяПоляДаты].Имя);
	
	Если НесколькоОрганизаций Тогда
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[ИмяСписка + "Организация"].Имя);
	КонецЕсли;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяСписка + ".Прочтен");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Шрифт = Метаданные.ЭлементыСтиля.ЖирныйШрифтБЭД.Значение;
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Шрифт);
	
	Если ЕстьДублирующийсяЭлемент(УсловноеОформление.Элементы, Элемент) Тогда
		УсловноеОформление.Элементы.Удалить(Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеСпискаДляПодсветкиДокументовПакета(ИмяСписка)
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяСписка);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяСписка + ".КлючДокумента");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ТекущийИдентификаторДокумента");
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяСписка + ".КлючСортировкиПакета");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ТекущийИдентификаторПакета");
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяСписка + ".КлючСортировкиПакета");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.ЦветФонаПакетовДокументовБЭД);
	
	Если ЕстьДублирующийсяЭлемент(УсловноеОформление.Элементы, Элемент) Тогда
		УсловноеОформление.Элементы.Удалить(Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеСпискаДляПодсветкиПриглашенийКонтрагента(ИмяСписка)
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("Приглашения");
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Приглашения.КлючДокумента");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ИдентификаторТекущейОрганизации");
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Приглашения.КлючДокумента");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("КлючТекущегоПриглашения");
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Приглашения.КонтрагентСортировка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("КонтрагентТекущегоПриглашения");
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Приглашения.КонтрагентСортировка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.ЦветФонаПакетовДокументовБЭД);
	
	Если ЕстьДублирующийсяЭлемент(УсловноеОформление.Элементы, Элемент) Тогда
		УсловноеОформление.Элементы.Удалить(Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИменаСписковФормы()
	
	ИменаСписковФормы = Новый Массив;
	
	ИменаСписковФормы.Добавить("ВходящиеЭлектронныеДокументы");
	ИменаСписковФормы.Добавить("ИсходящиеЭлектронныеДокументы");
	ИменаСписковФормы.Добавить("Черновики");
	ИменаСписковФормы.Добавить("Приглашения");
	ИменаСписковФормы.Добавить("Ошибки");
	
	Возврат ИменаСписковФормы; 
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПоляФильтров()
	
	ПоляФильтров = Новый Массив;
	ПоляФильтров.Добавить("Организация");
	ПоляФильтров.Добавить("Контрагент"); 
	ПоляФильтров.Добавить("Состояние");
	ПоляФильтров.Добавить("Период");
	ПоляФильтров.Добавить("ТипДокумента");
	ПоляФильтров.Добавить("Ответственный");
	ПоляФильтров.Добавить("ТипРегламента");
	ПоляФильтров.Добавить("СтатусПриглашения");
	
	Возврат ПоляФильтров; 
	
КонецФункции

&НаКлиенте
Процедура ПриИзмененииОтборов()
	УстановитьОтборыНаСервере();
	УстановитьПараметрыПредпросмотра();
	ПодключитьОбработчикОжидания("ОбновитьОбластьПросмотра", 0.1, Истина);
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборыНаСервере(Раздел = Неопределено, ПолеОтбора = Неопределено,
	ПроверятьПересечениеОтборов = Истина)
	
	СохраняемыеВНастройкахДанныеМодифицированы = Истина;
	
	Если Раздел = Неопределено Тогда
		Раздел = ТекущийРаздел;
	КонецЕсли;
	Если ПолеОтбора = Неопределено Тогда
		ПолеОтбора = ТекущийЭлемент.Имя;
	КонецЕсли;
	Значение = ЭтотОбъект[ПолеОтбора];
	КэшФильтровВСписках[Раздел].Вставить(ПолеОтбора, Значение);
	
	Отбор = ЭтотОбъект[Раздел].Отбор;
	
	ИмяГруппыОтбора = ИмяГруппыФильтров();
	ГруппаБыстрыйОтбор = ОбщегоНазначенияКлиентСервер.НайтиЭлементОтбораПоПредставлению(Отбор.Элементы, ИмяГруппыОтбора);
	Если ГруппаБыстрыйОтбор = Неопределено Тогда
		ГруппаБыстрыйОтбор = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
			Отбор.Элементы, ИмяГруппыОтбора, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	КонецЕсли;
	
	Режим = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ГруппаБыстрыйОтбор.РежимОтображения = Режим;
	
	Если ПолеОтбора = "Организация" 
		Или ПолеОтбора = "Контрагент" 
		Или (ПолеОтбора = "Ответственный" 
			И ЭтоВходящиеИсходящие(Раздел)) Тогда
		
		Использование = ЗначениеЗаполнено(Значение);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаБыстрыйОтбор, ПолеОтбора, Значение,
			ВидСравненияКомпоновкиДанных.Равно, ПолеОтбора, Использование, Режим);
				
	ИначеЕсли ПолеОтбора = "Состояние" 
		И ЭтоВходящиеИсходящие(Раздел) Тогда
			
		Если Значение = "Все" Тогда
			
			Значение = "";
			ЭтотОбъект[ПолеОтбора] = Значение;
					
		КонецЕсли;
		
		УстановитьОтборПоСостоянию(Значение, ГруппаБыстрыйОтбор, Режим);
		
	ИначеЕсли ПолеОтбора = "Период" Тогда
		
		Использование = ЗначениеЗаполнено(Значение);
		ДатаНачала = НачалоДня(Период.ДатаНачала);
		ДатаОкончания = КонецДня(?(ЗначениеЗаполнено(Период.ДатаОкончания), Период.ДатаОкончания, Дата(3999, 12, 31)));
		
		Если Раздел <> РазделыТекущихДелЭДО().Приглашения Тогда
			Поле = "ДатаПолучения";
		Иначе
			Поле = "Дата";
		КонецЕсли;
		
		ЭлементыУжеДобавлены = 
			ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(ГруппаБыстрыйОтбор, Поле).Количество() <> 0;
		Если Не ЭлементыУжеДобавлены Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаБыстрыйОтбор, Поле,
				ВидСравненияКомпоновкиДанных.БольшеИлиРавно, ДатаНачала, "ОтборПериодДатаНачала");
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаБыстрыйОтбор, Поле,
				ВидСравненияКомпоновкиДанных.МеньшеИлиРавно, ДатаОкончания, "ОтборПериодДатаОкончания");
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаБыстрыйОтбор, Неопределено, ДатаНачала,
			ВидСравненияКомпоновкиДанных.БольшеИлиРавно, "ОтборПериодДатаНачала", Использование, Режим);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаБыстрыйОтбор, Неопределено, ДатаОкончания,
			ВидСравненияКомпоновкиДанных.МеньшеИлиРавно, "ОтборПериодДатаОкончания", Использование, Режим);
		
	ИначеЕсли ПолеОтбора = "ТипДокумента" 
		И Раздел <> "Черновики"
		И Раздел <> "Приглашения" Тогда
		
		Использование = ЗначениеЗаполнено(Значение);
		
		Если Раздел = "Черновики" Тогда
			Использование = Ложь;
		КонецЕсли;
		
		Поле = "ВидДокумента";
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаБыстрыйОтбор, Поле, Значение,
			ВидСравненияКомпоновкиДанных.Равно, ПолеОтбора, Использование, Режим);
		
	ИначеЕсли ПолеОтбора = "ВидДокумента" И Раздел = "Черновики" Тогда
		
		Использование = ЗначениеЗаполнено(Значение);
		Поле = "Документ.ВидДокумента";
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаБыстрыйОтбор, Поле, Значение,
			ВидСравненияКомпоновкиДанных.Равно, ПолеОтбора, Использование, Режим);
			
	ИначеЕсли ПолеОтбора = "СтатусПриглашения" И Раздел = "Приглашения" Тогда
		
		ЗначенияОтбора = Новый СписокЗначений;
		
		Если Значение = "Все" Тогда
			
			Значение = "";
			ЭтотОбъект[ПолеОтбора] = Значение;
		
		ИначеЕсли Значение = "ТребуетсяОтправить" Тогда
			ЗначенияОтбора.Добавить(Перечисления.СтатусыПриглашений.ТребуетсяОтправить);
		ИначеЕсли Значение = "Принято" Тогда
			ЗначенияОтбора.Добавить(Перечисления.СтатусыПриглашений.Принято);
		ИначеЕсли Значение = "Отклонено" Тогда
			ЗначенияОтбора.Добавить(Перечисления.СтатусыПриглашений.Отклонено);
		ИначеЕсли Значение = "ОжидаемСогласия" Тогда
			ЗначенияОтбора.Добавить(Перечисления.СтатусыПриглашений.ОжидаемСогласия);
		ИначеЕсли Значение = "ТребуетсяСогласие" Тогда
			ЗначенияОтбора.Добавить(Перечисления.СтатусыПриглашений.ТребуетсяСогласие);
		ИначеЕсли Значение = "НастройкаРоуминга" Тогда
			ЗначенияОтбора.Добавить(Перечисления.СтатусыПриглашений.НастройкаРоуминга);
		ИначеЕсли Значение = "Ошибка" Тогда
			ЗначенияОтбора.Добавить(Перечисления.СтатусыПриглашений.Ошибка);
		ИначеЕсли Значение = "НеТребуется" Тогда
			ЗначенияОтбора.Добавить(Перечисления.СтатусыПриглашений.НеТребуется);
		ИначеЕсли Значение = "ТребуетВнимания" Тогда
			ЗначенияОтбора.Добавить(Перечисления.СтатусыПриглашений.ТребуетсяОтправить);
			ЗначенияОтбора.Добавить(Перечисления.СтатусыПриглашений.ТребуетсяСогласие);
		ИначеЕсли Значение = "НеСформировано" Тогда
			ЗначенияОтбора.Добавить("Не сформировано");
		КонецЕсли;

		Поле = "СтатусПодключения";
		Использование = ЗначенияОтбора.Количество() > 0;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаБыстрыйОтбор, Поле, ЗначенияОтбора,
			ВидСравненияКомпоновкиДанных.ВСписке, ПолеОтбора, Использование, Режим);
	
	ИначеЕсли ПолеОтбора = "ТипРегламента" И ЭтоВходящиеИсходящие(Раздел) Тогда
		
		Если Значение = "Все" Тогда
			
			Значение = "";
			ЭтотОбъект[ПолеОтбора] = Значение;
			
		КонецЕсли; 
		
		Использование = ЗначениеЗаполнено(Значение); 
		
		Если Значение = "Только произвольные документы" Тогда // АПК:1297 Не проверять на локализацию
			ЗначениеОтбора = Перечисления.ТипыРегламентовЭДО.Неформализованный;	
		КонецЕсли;
						
		Поле = "ТипРегламента";
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаБыстрыйОтбор, Поле, ЗначениеОтбора,
			ВидСравненияКомпоновкиДанных.Равно, ПолеОтбора, Использование, Режим);
								
		СписокВидовДокументов = Новый СписокЗначений;
		Виды = ЭлектронныеДокументыЭДО.ВидыДокументовДляПроизвольногоФормата();
		
		Для каждого Вид Из Виды Цикл
			СписокВидовДокументов.Добавить(Вид);
		КонецЦикла;

		Поле = "ВидДокумента";
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаБыстрыйОтбор, Поле, СписокВидовДокументов,
			ВидСравненияКомпоновкиДанных.ВСписке, ПолеОтбора, Использование, Режим);

	КонецЕсли;
	
	Если ПроверятьПересечениеОтборов Тогда
		ОбработатьПересечениеОтборов(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоСостоянию(Знач Значение, ГруппаБыстрыйОтбор, Знач Режим)

	СписокСостояний = Новый СписокЗначений;
	
	Поле = "СостояниеЭДО";
	Если Значение = "Утвердить" Тогда
		
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяУтверждение);
		
	ИначеЕсли Значение = "Подписать" Тогда
		
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписание);
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеАннулирования);
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеОтклонения);
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеИзвещения);
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеИзвещенияПоОтклонению);
		
	ИначеЕсли Значение = "Аннулировать" Тогда
		
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодтверждениеАннулирования);
		
	ИначеЕсли Значение = "Исправить" Тогда
		
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяУточнение);
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодтверждениеАннулирования);
		
	ИначеЕсли Значение = "Завершен" Тогда
		
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ЗакрытПринудительно);
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ЗакрытСОтклонением); 
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ЗакрытСОтклонениемПриглашения);
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ЗакрытСОшибкойПередачи);
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ОбменЗавершен);
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ОбменЗавершенСИсправлением);
		
	ИначеЕсли Значение = "Отправлен" Тогда
		
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ОжидаетсяИзвещениеОПолучении);
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ОжидаетсяИзвещениеПоОтклонению); 
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ОжидаетсяИсправление);
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ОжидаетсяОтветНаПриглашение);
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ОжидаетсяПередачаОператору);
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ОжидаетсяПодтверждение);
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ОжидаетсяПодтверждениеАннулирования);
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ОжидаетсяПодтверждениеОператора); 
		
	ИначеЕсли Значение = "Отправить" Тогда
		
		СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодготовкаКОтправке);
		
	ИначеЕсли Значение = "ТребуетВнимания" Тогда
		
		СписокСостояний = СостоянияДокументовТребующиеВнимания();
		
	КонецЕсли;
	
	Использование = СписокСостояний.Количество();
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаБыстрыйОтбор, Поле, СписокСостояний,
		ВидСравненияКомпоновкиДанных.ВСписке, "Состояние", Использование, Режим);
		
КонецПроцедуры

&НаСервере
Функция ЭтоВходящиеИсходящие(Знач Список)
	
	Возврат Список <> "Черновики" И Список <> "Приглашения" И Список <> "Ошибки";
				
КонецФункции

&НаСервереБезКонтекста
Процедура ПредставлениеСостояния(ДанныеСтроки)
	
	Если ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяУтверждение 
		И ЗначениеЗаполнено(ДанныеСтроки.КлючСортировкиПакета) 
		Тогда
			
		ДанныеСтроки.Состояние = НСтр("ru = 'Утвердить пакет'");
		
	ИначеЕсли ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяУтверждение Тогда
		
		ДанныеСтроки.Состояние = НСтр("ru = 'Утвердить'");
		
	ИначеЕсли ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеАннулирования Тогда
		
		ДанныеСтроки.Состояние = НСтр("ru = 'Подписать аннулирование'");
		
	ИначеЕсли ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеОтклонения
		Или ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеИзвещенияПоОтклонению Тогда
		
		ДанныеСтроки.Состояние = НСтр("ru = 'Подписать отклонение'");
		
	ИначеЕсли ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяУточнение Тогда
		
		ДанныеСтроки.Состояние = НСтр("ru = 'Исправить'");
				
	ИначеЕсли ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодтверждениеАннулирования Тогда
		
		ДанныеСтроки.Состояние = НСтр("ru = 'Аннулировать'");
		
	ИначеЕсли ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодготовкаКОтправке
		Или ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяОтправка Тогда
		
		ДанныеСтроки.Состояние = НСтр("ru = 'Отправить'");
		
	ИначеЕсли ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписание
		Или ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеИзвещения Тогда
		
		ДанныеСтроки.Состояние = НСтр("ru = 'Подписать'");
		
	ИначеЕсли ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ОжидаетсяПодтверждениеАннулирования Тогда
		
		ДанныеСтроки.Состояние = НСтр("ru = 'Отправлен на аннулирование'");
		
	ИначеЕсли СтрНайти(ДанныеСтроки.СостояниеЭДО, "Ожидается") <> 0 Тогда
		
		ДанныеСтроки.Состояние = НСтр("ru = 'Отправлен'");
		
	ИначеЕсли ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.НеСформирован Тогда
		
		ДанныеСтроки.Состояние = НСтр("ru = 'Сформировать'");
		
	ИначеЕсли ДокументВСостоянииЗавершен(ДанныеСтроки) Тогда
		
		ДанныеСтроки.Состояние = НСтр("ru = 'Завершен'");
		
	ИначеЕсли ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.Аннулирован Тогда
		
		ДанныеСтроки.Состояние = НСтр("ru = 'Аннулирован'");
			
	ИначеЕсли ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ЗакрытСОтклонением Тогда
		
		ДанныеСтроки.Состояние = НСтр("ru = 'Завершен с отклонением'");
		
	Иначе
		
		ДанныеСтроки.Состояние = НСтр("ru = 'Завершен'");
		
	КонецЕсли;
		
	КартинкиСостояний = СоответствиеИндексовКартинокСостоянийЭДО();
	
	Если ДанныеСтроки.Свойство("КартинкаСтатуса") Тогда 
		
		ДанныеСтроки.КартинкаСтатуса = 0;
		
		Если ДанныеСтроки.Свойство("Контрагент") И ЗначениеЗаполнено(ДанныеСтроки.Контрагент) Тогда
			
			Если ДокументТребуетВнимания(ДанныеСтроки) Тогда
				
				ДанныеСтроки.КартинкаСтатуса = КартинкиСостояний.Получить("ТребуетВнимания");
							
			Иначе
				
				ДанныеСтроки.КартинкаСтатуса = КартинкиСостояний.Получить("НеТребуетВнимания");
			
			КонецЕсли;	
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоответствиеИндексовКартинокСостоянийЭДО()
	
	КартинкиСостояний = Новый Соответствие;
	КартинкиСостояний.Вставить("ТребуетВнимания", 1);
	КартинкиСостояний.Вставить("НеТребуетВнимания", 0);
	
	Возврат КартинкиСостояний;
	
КонецФункции

&НаСервереБезКонтекста
Функция СтатусПриглашенияТребуетВнимания(СтатусПодключения)
	Возврат СтатусПодключения = Перечисления.СтатусыПриглашений.ТребуетсяСогласие
		Или СтатусПодключения = Перечисления.СтатусыПриглашений.ТребуетсяОтправить;
КонецФункции

&НаСервереБезКонтекста
Функция ДокументТребуетВнимания(Знач ДанныеСтроки)
	
	Возврат ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяУтверждение 
		Или ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписание
		Или ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодтверждениеАннулирования
		Или ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяУточнение 
		Или ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеАннулирования
		Или ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеОтклонения
		Или ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодготовкаКОтправке
		Или ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеИзвещения
		Или ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеИзвещенияПоОтклонению
		Или ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.НеСформирован;
		
КонецФункции

&НаСервереБезКонтекста
Функция ДокументВСостоянииЗавершен(Знач ДанныеСтроки)
	
	Возврат ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ЗакрытПринудительно 
		Или ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ЗакрытСОтклонением
		Или ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ЗакрытСОтклонениемПриглашения
		Или ДанныеСтроки.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ЗакрытСОшибкойПередачи;
		
КонецФункции

&НаСервере
Процедура ОбновитьДинамическийСписокТекущегоРаздела(ПеречитыватьДанные = Истина)
		
	Если ПеречитыватьДанные Тогда
		Элементы[ТекущийРаздел].Обновить();
	КонецЕсли;
	
	КоличествоДокументов();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПриПолученииДанныхСписков(ИмяЭлемента, Настройки, Строки)
			
	ИдентификаторыДокументовПакетов = Новый Соответствие;
		
	Для Каждого ТекущаяСтрока Из Строки Цикл
		
		ДанныеСтроки = ТекущаяСтрока.Значение.Данные; 
				
		СписокДокументов = ИдентификаторыДокументовПакетов[ДанныеСтроки.КлючСортировкиПакета];
		
		Если СписокДокументов = Неопределено Тогда
			
			СписокДокументов = Новый ТаблицаЗначений;
			СписокДокументов.Колонки.Добавить("ПорядокСортировкиВПакете");
			СписокДокументов.Колонки.Добавить("НомерСлужебный");
			
		КонецЕсли;		
		
		Документ = СписокДокументов.Добавить();
		Документ.ПорядокСортировкиВПакете = ДанныеСтроки.ПорядокСортировкиВПакете;
		Документ.НомерСлужебный = ДанныеСтроки.НомерСлужебный;
		
		ИдентификаторыДокументовПакетов.Вставить(ДанныеСтроки.КлючСортировкиПакета, СписокДокументов);	
		
	КонецЦикла;	
		
	Для Каждого ТекущаяСтрока Из Строки Цикл
		
		ДанныеСтроки = ТекущаяСтрока.Значение.Данные;
		
		ПредставлениеСостояния(ДанныеСтроки);

		ДанныеСтроки.КлючДокумента = Строка(ДанныеСтроки.ЭлектронныйДокумент.УникальныйИдентификатор());
		Если ЗначениеЗаполнено(ДанныеСтроки.ИдентификаторПакета) Тогда
			ДанныеСтроки.КлючПакета = Строка(ДанныеСтроки.ИдентификаторПакета);
		Иначе
			ДанныеСтроки.КлючПакета = "";
		КонецЕсли;
		
		ТекущиеИдентификаторы = ИдентификаторыДокументовПакетов[ДанныеСтроки.КлючСортировкиПакета]; 
		
		Если ТекущиеИдентификаторы <> Неопределено И ДанныеСтроки.КлючСортировкиПакета <> "" Тогда
			
			НачалоДня = НачалоДня(ДанныеСтроки.ВремяПолучения);
			ДатаПолученияСоВременем = ДанныеСтроки.ДатаПолучения + (ДанныеСтроки.ВремяПолучения - НачалоДня);
			ДанныеСтроки.ДатаПолучения = ДатаПолученияСоВременем;
			
			ТекущиеИдентификаторыСвернута = Новый ТаблицаЗначений;
			ТекущиеИдентификаторыСвернута.Колонки.Добавить("ПорядокСортировкиВПакете");
			
			Для Индекс = 0 По ТекущиеИдентификаторы.Количество() - 1 Цикл 
				ТекущиеИдентификаторыСвернута.Добавить();	
			КонецЦикла;
			
			ЗначениеКолонки = ТекущиеИдентификаторы.ВыгрузитьКолонку("ПорядокСортировкиВПакете");
			ТекущиеИдентификаторыСвернута.ЗагрузитьКолонку(ЗначениеКолонки, "ПорядокСортировкиВПакете");
			
			ТекущиеИдентификаторыСвернута.Свернуть("ПорядокСортировкиВПакете");
			
			Если ТекущиеИдентификаторыСвернута.Количество() = 1 Тогда 
				ТекущиеИдентификаторы.Сортировать("НомерСлужебный Убыв");
			Иначе
				ТекущиеИдентификаторы.Сортировать("ПорядокСортировкиВПакете Возр, НомерСлужебный Убыв");
			КонецЕсли;
			
			ДанныеСтроки.ПервыйДокументПакета = ДанныеСтроки.НомерСлужебный = ТекущиеИдентификаторы[0].НомерСлужебный;

			ДанныеСтроки.ИндексКартинкиПакета = 0;
			
			Если ДанныеСтроки.ПервыйДокументПакета И ЗначениеЗаполнено(ДанныеСтроки.КлючСортировкиПакета) Тогда
				
				ДанныеСтроки.ИндексКартинкиПакета = 1;
				
			ИначеЕсли Не ДанныеСтроки.ПервыйДокументПакета И ЗначениеЗаполнено(ДанныеСтроки.КлючСортировкиПакета) Тогда
				
				Если ДанныеСтроки.Свойство("ДатаПолучения") Тогда
					ДанныеСтроки.ДатаПолучения = "";	
				КонецЕсли;
				Если ДанныеСтроки.Свойство("КартинкаСтатуса") Тогда
					ДанныеСтроки.КартинкаСтатуса = 0;	
				КонецЕсли;
				Если ДанныеСтроки.Свойство("Контрагент") Тогда
					ДанныеСтроки.Контрагент = ""; 
				КонецЕсли;
				Если ДанныеСтроки.Свойство("Организация") Тогда
					ДанныеСтроки.Организация = ""; 
				КонецЕсли;
				Если ДанныеСтроки.Свойство("Состояние") Тогда
					ДанныеСтроки.Состояние = "";
					ДанныеСтроки.КартинкаСтатуса = 0;
				КонецЕсли;
			
			КонецЕсли; 
				
		Иначе
			
			ДанныеСтроки.ПервыйДокументПакета = Истина;
			ДанныеСтроки.ИндексКартинкиПакета = 0;
			
		КонецЕсли;

	КонецЦикла;
	
	ОбменСКонтрагентамиПереопределяемый.ДинамическийСписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки);
	
КонецПроцедуры

&НаСервере
Процедура ОтображатьУдаленныеСервер()

	Элементы.ОтображатьУдаленныеВходящие.Пометка = ОтображатьУдаленные;
	Элементы.ОтображатьУдаленныеИсходящие.Пометка = ОтображатьУдаленные;
	Элементы.ОтображатьУдаленныеОшибки.Пометка = ОтображатьУдаленные;
	
	Для каждого ИмяСписка Из ИменаСписковФормы() Цикл
		Если ИмяСписка = "Черновики" Или ИмяСписка = "Приглашения" Или ИмяСписка = "Ошибки" Тогда
			Продолжить;
		КонецЕсли;
		ЭтотОбъект[ИмяСписка].Параметры.УстановитьЗначениеПараметра("ОтображатьУдаленные", ОтображатьУдаленные);
	КонецЦикла;
	
	Ошибки.Параметры.УстановитьЗначениеПараметра("ОтображатьУдаленные", ОтображатьУдаленные);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ИнтерфейсЭДО", "ОтображатьУдаленные", ОтображатьУдаленные);	
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДинамическиеСпискиФормы()
	
	Если Не НастройкиЭДО.ИспользуетсяОбменЭлектроннымиДокументами() Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ИмяСписка Из ИменаСписковФормы() Цикл

		Элементы[ИмяСписка].Обновить();

	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеФильтров()
	
	Если ИменаСписковФормы().Найти(ТекущийРаздел) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоФильтров = 0;
	КоличествоДополнительныхОтборов = КоличествоДополнительныхОтборовСписков[ТекущийРаздел];

	Для Каждого Отбор Из ПоляФильтров() Цикл
		Если ЗначениеЗаполнено(ЭтотОбъект[Отбор]) И Отбор <> "Новые" Тогда
			КоличествоФильтров = КоличествоФильтров + 1;
		КонецЕсли;
	КонецЦикла;
		
	Элементы.ГруппаОчиститьБыстрыеОтборы.Видимость = КоличествоФильтров > 0 Или КоличествоДополнительныхОтборов > 0;
	
	УстановитьЗаголовокИВидимостьДополнительныхФильтров(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФильтры()
	
	Для Каждого Фильтр Из ПоляФильтров() Цикл
		ЭтотОбъект[Фильтр] = Неопределено;
	КонецЦикла;
	
	СпискиСФильтрами = ИменаСписковФормы();
	Если СпискиСФильтрами.Найти(ТекущийРаздел) = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	ФильтрыСписка = КэшФильтровВСписках.Получить(ТекущийРаздел);
	Если ФильтрыСписка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Фильтр Из ФильтрыСписка Цикл
		ЭтотОбъект[Фильтр.Ключ] = Фильтр.Значение;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьПользовательскиеОтборы()
	
	ФильтрыСписка = КэшФильтровВСписках.Получить(ТекущийРаздел);
	Если ФильтрыСписка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Фильтр Из ФильтрыСписка Цикл
		ЭтотОбъект[Фильтр.Ключ] = Неопределено;
		УстановитьОтборыНаСервере(, Фильтр.Ключ);
	КонецЦикла;
	
	ЗаполнитьКоличествоДополнительныхФильтров();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьНастройкиСписков()
	
	Для Каждого ИмяСписка Из ИменаСписковФормы() Цикл
		Список = ЭтотОбъект[ИмяСписка];
		
		ИдентификаторНастройки = Список.КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки;
		Отбор = Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ИдентификаторНастройки);
		Отбор.Элементы.Очистить();
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФильтрыСписков()
	
	Для Каждого Раздел Из ИменаСписковФормы() Цикл
		Фильтры = КэшФильтровВСписках.Получить(Раздел);
		Если Фильтры = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого Фильтр Из Фильтры Цикл
			ЭтотОбъект[Фильтр.Ключ] = Фильтр.Значение;
			УстановитьОтборыНаСервере(Раздел, Фильтр.Ключ);
		КонецЦикла;
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКэшФильтровВСписках()
	
	КэшФильтров = Новый Соответствие;
	Для Каждого ИмяСписка Из ИменаСписковФормы() Цикл
		КэшФильтров.Вставить(ИмяСписка, Новый Соответствие);
	КонецЦикла;
	КэшФильтровВСписках = Новый ФиксированноеСоответствие(КэшФильтров);
	КэшФильтровВСписках = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИмяФормы, "Отборы", КэшФильтровВСписках);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПересечениеОтборов(ЭтоУстановкаФильтров = Ложь)
	
	ТекущийСписок = ЭтотОбъект[ТекущийРаздел];
	
	ГруппаБыстрыхФильтров = ОбщегоНазначенияКлиентСервер.НайтиЭлементОтбораПоПредставлению(ТекущийСписок.Отбор.Элементы,
		ИмяГруппыФильтров());
	Если ГруппаБыстрыхФильтров = Неопределено Тогда
		ЗаполнитьКоличествоДополнительныхФильтров();
		Возврат;
	КонецЕсли;
	ЭлементыБыстрыхФильтров = ИнтерфейсДокументовЭДО.ЭлементыОтбора(ГруппаБыстрыхФильтров);
		
	ПользовательскийОтбор = ИнтерфейсДокументовЭДО.ПользовательскийОтборСписка(ТекущийСписок);
	ЭлементыПользовательскогоОтбора = ИнтерфейсДокументовЭДО.ЭлементыОтбора(ПользовательскийОтбор);
	
	Для Каждого ЭлементПользовательскогоОтбора Из ЭлементыПользовательскогоОтбора Цикл
	
		Если Не ЭлементПользовательскогоОтбора.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ЭлементБыстрогоФильтра Из ЭлементыБыстрыхФильтров Цикл
			
			Если ЭлементПользовательскогоОтбора.ЛевоеЗначение <> ЭлементБыстрогоФильтра.ЛевоеЗначение 
				Или Не ЭлементБыстрогоФильтра.Использование Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЭтоУстановкаФильтров Тогда
				ЭлементПользовательскогоОтбора.Использование = Ложь;
			Иначе
				
				ЭлементБыстрогоФильтра.Использование = Ложь;
				РеквизитФормы = РеквизитФормыПоПредставлениюФильтра(ЭлементБыстрогоФильтра.Представление);
				Если ЗначениеЗаполнено(РеквизитФормы) Тогда
					ЭтотОбъект[РеквизитФормы] = Неопределено;
					УстановитьОтборыНаСервере(ТекущийРаздел, РеквизитФормы, Ложь);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ЗаполнитьКоличествоДополнительныхФильтров();
	УстановитьЗаголовокИВидимостьДополнительныхФильтров(ЭтотОбъект);
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяГруппыФильтров()
	Возврат "БыстрыйОтбор";
КонецФункции 
	
&НаКлиенте
Процедура НастройкаОтборовСпискаЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(Результат) <> Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
		
	ТекущийСписок = ЭтотОбъект[ТекущийРаздел];
	ТекущийСписок.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(Результат);
	
	ОбработатьПересечениеОтборов();
	ВидимостьЭлементовИКоманд();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РеквизитФормыПоПредставлениюФильтра(ПредставлениеФильтра)

	РеквизитыФормыПоПредставлениямФильтров = Новый Соответствие();
	РеквизитыФормыПоПредставлениямФильтров.Вставить("Организация", "Организация");
	РеквизитыФормыПоПредставлениямФильтров.Вставить("Контрагент", "Контрагент");
	РеквизитыФормыПоПредставлениямФильтров.Вставить("ОтборПериодДатаНачала", "Период");
	РеквизитыФормыПоПредставлениямФильтров.Вставить("ОтборПериодДатаОкончания", "Период");
	РеквизитыФормыПоПредставлениямФильтров.Вставить("Ответственный", "Ответственный");
	РеквизитыФормыПоПредставлениямФильтров.Вставить("Состояние", "Состояние");
	РеквизитыФормыПоПредставлениямФильтров.Вставить("ВидДокумента", "ТипДокумента");
	РеквизитыФормыПоПредставлениямФильтров.Вставить("ТипДокумента", "ТипДокумента");
	РеквизитыФормыПоПредставлениямФильтров.Вставить("ТипРегламента", "ТипРегламента");
	РеквизитыФормыПоПредставлениямФильтров.Вставить("СтатусПриглашения", "СтатусПриглашения");
	
	Возврат РеквизитыФормыПоПредставлениямФильтров[ПредставлениеФильтра];

КонецФункции

&НаСервереБезКонтекста
Функция СостоянияДокументовТребующиеВнимания()
	
	СписокСостояний = Новый СписокЗначений;
	
	СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодготовкаКОтправке);
	СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяУтверждение);
	СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписание);
	СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеАннулирования);
	СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеОтклонения);
	СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяОтправка);
	СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодтверждениеАннулирования);
	СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяУточнение);
	СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеИзвещения);
	СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеИзвещенияПоОтклонению);
	
	Возврат СписокСостояний;

КонецФункции

&НаСервереБезКонтекста
Функция СостоянияДокументовТребующиеВниманияДляРазделаИсходящие()
	
	СписокСостояний = Новый СписокЗначений;
	СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеАннулирования);
	СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеОтклонения);
	СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодтверждениеАннулирования);
	СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяУточнение);
	СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеИзвещения);
	СписокСостояний.Добавить(Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписаниеИзвещенияПоОтклонению);
	Возврат СписокСостояний;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокИВидимостьДополнительныхФильтров(Форма)
	
	Если ИменаСписковФормы().Найти(Форма.ТекущийРаздел) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоДополнительныхОтборов = Форма.КоличествоДополнительныхОтборовСписков[Форма.ТекущийРаздел];
	ШаблонЗаголовка = НСтр("ru='Дополнительные фильтры (%1)'");
	Форма.Элементы.КомандаДополнительныеФильтры.Заголовок = СтрШаблон(ШаблонЗаголовка, КоличествоДополнительныхОтборов);
	Форма.Элементы.КомандаДополнительныеФильтры.Видимость = КоличествоДополнительныхОтборов <> 0;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКоличествоДополнительныхФильтров()
	
	КоличествоДополнительныхОтборов = Новый Соответствие;
	
	Для Каждого ИмяСписка Из ИменаСписковФормы() Цикл
	
		ТекущийСписок = ЭтотОбъект[ИмяСписка];
		ПользовательскийОтбор = ИнтерфейсДокументовЭДО.ПользовательскийОтборСписка(ТекущийСписок);
		ЭлементыПользовательскогоОтбора = ИнтерфейсДокументовЭДО.ЭлементыОтбора(ПользовательскийОтбор);
		
		КоличествоЭлементов = 0;
	
		Для Каждого ЭлементПользовательскогоОтбора Из ЭлементыПользовательскогоОтбора Цикл
	
			Если ЭлементПользовательскогоОтбора.Использование Тогда
				КоличествоЭлементов = КоличествоЭлементов + 1;
			КонецЕсли;
		
		КонецЦикла;
		
		КоличествоДополнительныхОтборов.Вставить(ИмяСписка, КоличествоЭлементов);
		
	КонецЦикла;
		
	КоличествоДополнительныхОтборовСписков = Новый ФиксированноеСоответствие(КоличествоДополнительныхОтборов);
	
КонецПроцедуры

&НаКлиенте
Функция ТекущиеДанныеРазделаЭлектронныхДокументов()

	РазделыТекущихДелЭДО = РазделыТекущихДелЭДО();
	
	Если ТекущийРаздел = РазделыТекущихДелЭДО.ВходящиеЭлектронныеДокументы Тогда
		Результат = Элементы.ВходящиеЭлектронныеДокументы.ТекущиеДанные;
	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.ИсходящиеЭлектронныеДокументы Тогда
		Результат = Элементы.ИсходящиеЭлектронныеДокументы.ТекущиеДанные;
	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.Черновики Тогда
		Результат = Элементы.Черновики.ТекущиеДанные;
	Иначе
		Результат = Неопределено;
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Возвращаемое значение:
//  Структура:
// * ВходящиеЭлектронныеДокументы - Строка 
// * ИсходящиеЭлектронныеДокументы - Строка 
// * Черновики - Строка
// * Приглашения - Строка
// * Ошибки - Строка
&НаКлиентеНаСервереБезКонтекста
Функция РазделыТекущихДелЭДО()
	Разделы = Новый Структура;
	Разделы.Вставить("ВходящиеЭлектронныеДокументы", "ВходящиеЭлектронныеДокументы");
	Разделы.Вставить("ИсходящиеЭлектронныеДокументы", "ИсходящиеЭлектронныеДокументы");
	Разделы.Вставить("Черновики", "Черновики");
	Разделы.Вставить("Приглашения", "Приглашения");
	Разделы.Вставить("Ошибки", "Ошибки");
	Возврат Разделы;
КонецФункции

&НаКлиенте
Процедура ПереключитьРазделТекущихДел(ВыбранныйРаздел)
	
	ПредыдущийРаздел = ТекущийРаздел;
	
	УстановитьТекущийРаздел(ЭтотОбъект, ВыбранныйРаздел);
	УстановитьСтраницуОжиданияПредпросмотра(ПредыдущийРаздел);
	УстановитьПараметрыПредпросмотра();
	ОбновитьФильтры();
	ВидимостьЭлементовИКоманд();
	ПодключитьОбработчикОжидания("ОбновитьОбластьПросмотра", 0.1, Истина);
	ПодключитьОбработчикОжидания("УстановитьАктивнымСписокВыбранногоРаздела", "0.1", Истина);
	ТребуетсяОбновитьСчетчикиКоличестваДокументов = Истина;

КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекущийРаздел(Форма, ВыбранныйРаздел)
	
	Элементы             = Форма.Элементы;
	РазделыТекущихДелЭДО = РазделыТекущихДелЭДО();
	
	Если ВыбранныйРаздел = РазделыТекущихДелЭДО.ВходящиеЭлектронныеДокументы Тогда
		
		Форма.ТекущийЭлемент = Элементы.ВходящиеЭлектронныеДокументы;
		
		Форма.Заголовок = НСтр("ru = 'Входящие документы'");
		Форма.ТекущийРаздел = РазделыТекущихДелЭДО.ВходящиеЭлектронныеДокументы;
		
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВходящиеИсходящие; 
		Элементы.СтраницыВходящиеИсходящие.ТекущаяСтраница = Элементы.СтраницаВходящиеЭлектронныеДокументы;
	
	ИначеЕсли ВыбранныйРаздел = РазделыТекущихДелЭДО.ИсходящиеЭлектронныеДокументы Тогда 
		
		Форма.ТекущийЭлемент = Элементы.ИсходящиеЭлектронныеДокументы;
		
		Форма.Заголовок = НСтр("ru = 'Исходящие документы'");
		Форма.ТекущийРаздел = РазделыТекущихДелЭДО.ИсходящиеЭлектронныеДокументы;
		
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВходящиеИсходящие; 
		Элементы.СтраницыВходящиеИсходящие.ТекущаяСтраница = Элементы.СтраницаИсходящиеЭлектронныеДокументы;
		
	ИначеЕсли ВыбранныйРаздел = РазделыТекущихДелЭДО.Черновики Тогда
		
		Форма.ТекущийЭлемент = Элементы.Черновики;
		
		Форма.Заголовок = НСтр("ru = 'Черновики'");
		Форма.ТекущийРаздел = РазделыТекущихДелЭДО.Черновики;
		
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаЧерновики;
		
	ИначеЕсли ВыбранныйРаздел = РазделыТекущихДелЭДО.Приглашения Тогда
		
		Форма.ТекущийЭлемент = Элементы.Приглашения;
		
		Форма.Заголовок = НСтр("ru = 'Контрагенты'");
		Форма.ТекущийРаздел = РазделыТекущихДелЭДО.Приглашения;
		
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПриглашения;
		
	ИначеЕсли ВыбранныйРаздел = РазделыТекущихДелЭДО.Ошибки Тогда
		
		Форма.ТекущийЭлемент = Элементы.Ошибки;
		
		Форма.Заголовок = НСтр("ru = 'Ошибки'");
		Форма.ТекущийРаздел = РазделыТекущихДелЭДО.Ошибки;
		
		Форма.Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОшибки;
	
	КонецЕсли;
	
	Форма.Элементы[ВыбранныйРаздел].Обновить();
	
	УстановитьВидимостьПредпросмотра(Форма);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЧерновикНаСервере(ЧерновикиКУдалению)

	Для Каждого СсылкаНаЧерновик Из ЧерновикиКУдалению Цикл
		
		ИнтеграцияЭДО.ИзменитьПометкуУдаленияЧерновикаЭлектронногоДокумента(СсылкаНаЧерновик, Истина);
		
		Если ЭлектронныеДокументыЭДО.ЭтоЭлектронныйДокумент(СсылкаНаЧерновик) Тогда
		
			УчетныеДокументы = ИнтеграцияЭДО.ОбъектыУчетаАктуальногоЭлектронногоДокумента(СсылкаНаЧерновик);
			Для Каждого УчетныйДокумент Из УчетныеДокументы Цикл
				ИнтеграцияЭДО.ИзменитьПометкуУдаленияЧерновикаЭлектронногоДокумента(УчетныйДокумент, Истина);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновитьДинамическийСписокТекущегоРаздела();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьАктивнымСписокВыбранногоРаздела()
	
	РазделыТекущихДелЭДО = РазделыТекущихДелЭДО();
	
	Если ТекущийРаздел = РазделыТекущихДелЭДО.ВходящиеЭлектронныеДокументы Тогда
		ТекущийЭлемент = Элементы.ВходящиеЭлектронныеДокументы;
	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.ИсходящиеЭлектронныеДокументы Тогда 
		ТекущийЭлемент = Элементы.ИсходящиеЭлектронныеДокументы;
	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.Черновики Тогда
		ТекущийЭлемент = Элементы.Черновики;
	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.Приглашения Тогда
		ТекущийЭлемент = Элементы.Приглашения;
	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.Ошибки Тогда
		ТекущийЭлемент = Элементы.Ошибки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСПриглашениями

&НаКлиенте
Процедура ОтправкаПриглашенийОповещениеОЗавершении(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	КонтекстДиагностики = Результат.КонтекстДиагностики;

	Если Результат.Успех Тогда
	
		ТекстЗаголовка = НСтр("ru = 'Отправка приглашений получателям'");
		
		ОтправленоПриглашений = Результат.КоличествоОтправленных;
		ШаблонСообщения = НСтр("ru = 'Отправлено приглашений: %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения, ОтправленоПриглашений);

		ПоказатьОповещениеПользователя(ТекстЗаголовка, , ТекстСообщения);

		ОбновитьДинамическийСписокТекущегоРаздела();
		
	КонецЕсли;

	Если ОбработкаНеисправностейБЭДКлиентСервер.ЕстьОшибки(КонтекстДиагностики) Тогда
			ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(КонтекстДиагностики);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПринятиеПриглашений()

	ОтправитьОтветНаПриглашения(ОтветНаПриглашение_Принять());

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОтветНаПриглашения(Действие)

	ОчиститьСообщения();
	ДанныеПриглашений = ДанныеВыделенныхСтрокРазделаПриглашений(Действие,
		"КлючПриглашения, ИдентификаторОрганизации, Организация, ИдентификаторКонтрагента, СпособОбменаЭД, Контрагент, НовыйКонтрагент");
	
	Если Не ЗначениеЗаполнено(ДанныеПриглашений) Тогда
		СнятьБлокировкуСКомандСпискаПриглашений(Действие);
		Возврат;
	КонецЕсли;
	
	ВходящиеПриглашения = Новый Массив;

	Для Каждого ДанныеПриглашения Из ДанныеПриглашений Цикл
		ВходящееПриглашение = СинхронизацияЭДОКлиент.НовоеВходящееПриглашение();
		ЗаполнитьЗначенияСвойств(ВходящееПриглашение, ДанныеПриглашения, , "Контрагент");
		Если ТипЗнч(ДанныеПриглашения.Контрагент) = ТипЗнч(ПустойКонтрагент) Тогда
			ВходящееПриглашение.Контрагент = ДанныеПриглашения.Контрагент;
		Иначе
			ВходящееПриглашение.Контрагент = ПустойКонтрагент;
		КонецЕсли;
		ВходящееПриглашение.СпособОбмена = ДанныеПриглашения.СпособОбменаЭД;
		ВходящиеПриглашения.Добавить(ВходящееПриглашение);
	КонецЦикла;
	
	ДополнительныеПараметрыОповещения = Новый Структура("ОтветНаПриглашение", Действие);
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиОтветовНаПриглашения", ЭтотОбъект, 
		ДополнительныеПараметрыОповещения);

	Если Действие = ОтветНаПриглашение_Принять() Тогда
		СинхронизацияЭДОКлиент.ПринятьПриглашения(ВходящиеПриглашения, ЭтотОбъект, Оповещение);
	ИначеЕсли Действие = ОтветНаПриглашение_Отклонить() Тогда
		СинхронизацияЭДОКлиент.ОтклонитьПриглашения(ВходящиеПриглашения, ЭтотОбъект, Оповещение);
	ИначеЕсли Действие = ОтветНаПриглашение_Отозвать() Тогда
		СинхронизацияЭДОКлиент.ОтозватьПриглашения(ВходящиеПриглашения, ЭтотОбъект, Оповещение);
	Иначе
		СнятьБлокировкуСКомандСпискаПриглашений(Действие);
		Возврат;
	КонецЕсли;

КонецПроцедуры

// Возвращаемое значение:
//  Строка
&НаКлиентеНаСервереБезКонтекста
Функция ОтветНаПриглашение_Принять()
	Возврат "Принять";
КонецФункции

// Возвращаемое значение:
//  Строка
&НаКлиентеНаСервереБезКонтекста
Функция ОтветНаПриглашение_Отклонить()
	Возврат "Отклонить";
КонецФункции

// Возвращаемое значение:
//  Строка
&НаКлиентеНаСервереБезКонтекста
Функция ОтветНаПриглашение_Отозвать()
	Возврат "Отозвать";
КонецФункции

// Возвращаемое значение:
//  Строка
&НаКлиентеНаСервереБезКонтекста
Функция ДействиеСПриглашением_Отправить()
	Возврат "Отправить";
КонецФункции

//  Действие - см.ОтветНаПриглашение_Принять 
//           - см.ОтветНаПриглашение_Отклонить
//           - см.ОтветНаПриглашение_Отклонить
//           - см.ДействиеСПриглашением_Отправить .
&НаКлиенте
Процедура СнятьБлокировкуСКомандСпискаПриглашений(Действие)
	
	Если Действие = ОтветНаПриглашение_Принять() Тогда
		СнятьБлокировкуЭлементов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Элементы.КомандаПринятьПриглашения.Имя));
	ИначеЕсли Действие = ОтветНаПриглашение_Отклонить() Тогда
		СнятьБлокировкуЭлементов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Элементы.КомандаОтклонитьПриглашения.Имя));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиОтветовНаПриглашения(Результат, Контекст) Экспорт
	
	СнятьБлокировкуСКомандСпискаПриглашений(Контекст.ОтветНаПриглашение);
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	КонтекстДиагностики = Результат.КонтекстДиагностики;

	Если Результат.Успех Тогда
		
		КоличествоОбработанныхПриглашений = Результат.КоличествоОбработанных;
		
		Если Контекст.ОтветНаПриглашение = ОтветНаПриглашение_Принять() Тогда
			ТекстЗаголовка = НСтр("ru = 'Принимаются приглашения'");
			ШаблонСообщения = НСтр("ru = 'Принято приглашений: %1'");
			СнятьБлокировкуЭлементов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Элементы.КомандаПринятьПриглашения.Имя));
		ИначеЕсли Контекст.ОтветНаПриглашение = ОтветНаПриглашение_Отклонить() Тогда
			ТекстЗаголовка = НСтр("ru = 'Отклоняются приглашения'");
			ШаблонСообщения = НСтр("ru = 'Отклонено приглашений: %1'");
			СнятьБлокировкуЭлементов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Элементы.КомандаОтклонитьПриглашения.Имя));
		ИначеЕсли Контекст.ОтветНаПриглашение = ОтветНаПриглашение_Отозвать() Тогда
			ТекстЗаголовка = НСтр("ru = 'Отзываются приглашения'");
			ШаблонСообщения = НСтр("ru = 'Отозвано приглашений: %1'");
		Иначе
			Возврат;
		КонецЕсли;

		ТекстСообщения = СтрШаблон(ШаблонСообщения, КоличествоОбработанныхПриглашений);

		ПоказатьОповещениеПользователя(ТекстЗаголовка, , ТекстСообщения);

		ОбновитьДинамическийСписокТекущегоРаздела();
	    УстановитьСтраницуДействийСПриглашением();
	КонецЕсли;

	Если ОбработкаНеисправностейБЭДКлиентСервер.ЕстьОшибки(КонтекстДиагностики) Тогда
		ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(КонтекстДиагностики);			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтраницуДействийСПриглашением()
	
	Если ТекущийРаздел <> РазделыТекущихДелЭДО().Приглашения Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийСписок = Элементы[ТекущийРаздел];
	ТекущиеДанные = ТекущийСписок.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПриглашениеСоздано = ТекущиеДанные.ПриглашениеСоздано;
	Элементы.ПриглашенияКонтекстноеМенюСоздатьПриглашение.Видимость = Не ПриглашениеСоздано;
	Элементы.ПриглашенияКонтекстноеМенюОткрытьПриглашение.Видимость = ПриглашениеСоздано;
	
	Элементы.ГруппаКнопокПринятьПриглашения.Видимость = Ложь;
	Элементы.ГруппаКнопокОтправитьПриглашения.Видимость = Ложь;
	Элементы.ГруппаКнопокПринятьПриглашенияКонтекстноеМеню.Видимость = Ложь;
	Элементы.ГруппаКнопокОтправитьПриглашенияКонтекстноеМеню.Видимость = Ложь;
	
	Если ТекущиеДанные.СтатусПодключения =
		ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.ТребуетсяСогласие") Тогда
		
		Элементы.СтраницыКомандыСпискаПриглашения.ТекущаяСтраница = Элементы.СтраницаПринятьПриглашение;
		Элементы.ГруппаКнопокПринятьПриглашения.Видимость = Истина;
		Элементы.ГруппаКнопокПринятьПриглашенияКонтекстноеМеню.Видимость = Истина;
			
	ИначеЕсли ТекущиеДанные.СтатусПодключения = 
		ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.ТребуетсяОтправить") Тогда
		
		Элементы.СтраницыКомандыСпискаПриглашения.ТекущаяСтраница = Элементы.СтраницаОтправить;
		Элементы.ГруппаКнопокОтправитьПриглашения.Видимость = Истина;
		Элементы.ГруппаКнопокОтправитьПриглашенияКонтекстноеМеню.Видимость = Истина;
		
	Иначе
		
		Элементы.СтраницыКомандыСпискаПриглашения.ТекущаяСтраница = Элементы.СтраницаНетДействийСПриглашением;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОтклонениеПриглашений()

	ОтправитьОтветНаПриглашения(ОтветНаПриглашение_Отклонить());

КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущиеДанныеПриглашения()
	
	ТекущиеДанные = Элементы.Приглашения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		КонтрагентТекущегоПриглашения = ПустойКонтрагент;
	Иначе
		КонтрагентТекущегоПриглашения = ТекущиеДанные.КонтрагентСортировка;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ПредпросмотрЭлектронногоДокумента

&НаКлиенте
Процедура ОбновитьОбластьПросмотра()
	
	ТекущиеДанные = ТекущиеДанныеРазделаЭлектронныхДокументов();
	
	ОчиститьКэшРеквизитовТекущегоФайла();
	
	Если ТекущиеДанные = Неопределено Или Не ОтображатьОбластьПредпросмотра
		Или ВключенаГруппировкаСтрок(ТекущиеДанные) Тогда
		УстановитьСтраницуПредпросмотраДокументНеВыбран(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	НастроитьВидимостьКомандТекущейСтроки(ТекущиеДанные);
	
	ОбновитьОбластьПросмотраНаСервере(ТекущиеДанные);
	
	НастроитьПанелиПросмотра();

	ОжидатьПолученияАсинхронныхДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыПредпросмотра()
	
	ТекущиеДанные = ТекущиеДанныеРазделаЭлектронныхДокументов();
	
	Если ТекущиеДанные <> Неопределено И Не ВключенаГруппировкаСтрок(ТекущиеДанные) Тогда
		ТекущийЭлектронныйДокумент = ТекущиеДанные.ЭлектронныйДокумент;
	Иначе
		ТекущийЭлектронныйДокумент = Неопределено;
		Предпросмотр = Неопределено; 
		ТекущийДокументУчетаПредставление = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтраницуОжиданияПредпросмотра(ПредыдущийРаздел)
	
	ТекущиеДанные = ТекущиеДанныеРазделаЭлектронныхДокументов();
	
	Если ТекущиеДанные <> Неопределено И Не ВключенаГруппировкаСтрок(ТекущиеДанные) Тогда
		Если ТекущийЭлектронныйДокумент <> ТекущиеДанные.ЭлектронныйДокумент 
			И (ПредыдущийРаздел = "Черновики" Или ПредыдущийРаздел = "Приглашения") Тогда
			Элементы.СтраницыПредпросмотр.ТекущаяСтраница = Элементы.СтраницаОжидание;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьПредпросмотра(Форма)
	
	Разделы = РазделыТекущихДелЭДО();
	
	Если Форма.ТекущийРаздел = Разделы.ВходящиеЭлектронныеДокументы
		Или Форма.ТекущийРаздел = Разделы.ИсходящиеЭлектронныеДокументы Тогда
		Форма.Элементы.СтраницыПредпросмотр.Видимость = Истина;
	Иначе
		Форма.Элементы.СтраницыПредпросмотр.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСтраницуПредпросмотраДокументНеВыбран(Форма)
	Форма.Элементы.СтраницыПредпросмотр.ТекущаяСтраница = Форма.Элементы.СтраницаНеВыбранДокумент;
КонецПроцедуры

#Область ОбновлениеДанных

&НаСервере
Процедура ОбновитьОбластьПросмотраНаСервере(ТекущиеДанные)
	
	Если ТребуетсяОбновитьСчетчикиКоличестваДокументов Тогда
		КоличествоДокументов();
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоВебКлиент() Тогда
		ИнтерфейсДокументовЭДО.УдалитьРеквизитыВыводаPDFДокумента(ЭтотОбъект, СведенияОPdfДокументе);
	КонецЕсли;

	ЛегкийИнтерфейсДокументовЭДО.ПрекратитьОжиданиеПолученияДанныхКомпонентовПросмотраДокумента(ОперацияПолученияДанных);
	
	ЗаполнитьРеквизитыФормыНаОснованииТекущихДанных(ТекущиеДанные);
	
	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентовПросмотраДокумента(
		"ПанельВизуализацииТекущихДел, ПанельПредставленияФайла");
	
	ОбновитьДанныеКомпонентов(ДанныеКомпонентов);
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеКомпонентов(ДанныеКомпонентов)
	
	ПараметрыЗаполнения = ПараметрыЗаполненияДанныхКомпонентовПросмотра();
	
	ОперацияПолученияДанных = ЛегкийИнтерфейсДокументовЭДО.ЗаполнитьДанныеКомпонентовПросмотраДокумента(
		ДанныеКомпонентов, ПараметрыЗаполнения);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормыНаОснованииТекущихДанных(ТекущиеДанные)
	
	ОрганизацияТекущегоДокумента = ТекущиеДанные.Организация;
	КонтрагентТекущегоДокумента = ТекущиеДанные.Контрагент;
	СостояниеЭДОТекущегоДокумента = ТекущиеДанные.СостояниеЭДО;
	СуммаТекущегоДокумента = ТекущиеДанные.СуммаДокумента;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ЭлектронныйДокумент) Тогда
		ВидТекущегоДокумента = Неопределено;
		ОтветственныйТекущегоДокумента = Неопределено;
		ФорматОсновногоТитулаТекущегоДокумента = Неопределено;
		ИдентификаторОрганизацииТекущегоДокумента = Неопределено;
		ИдентификаторТекущегоДокумента = Неопределено;
		ТекущийДокументОблачногоЭДО = Неопределено;
		ТекущийДокументПомеченНаУдаление = Неопределено;
		ЕстьНоменклатураНаКонтролеТекущегоДокумента = Неопределено;
		Возврат;
	КонецЕсли;
	
	Если ТекущийРаздел = РазделыТекущихДелЭДО().Черновики Тогда
		РеквизитыДокумента = ЭлектронныеДокументыЭДО.СвойстваДокумента(ТекущиеДанные.ЭлектронныйДокумент,
			"ВидДокумента, ВидДокумента.ТипДокумента, Ответственный, ФорматОсновногоТитула, ИдентификаторОрганизации,
			|ИдентификаторДокумента, ЭтоОблачныйЭДО, ПометкаУдаления");
		ТипТекущегоДокумента = РеквизитыДокумента.ВидДокументаТипДокумента;
	Иначе
		РеквизитыДокумента = ТекущиеДанные;
		ТипТекущегоДокумента = ТекущиеДанные.ТипДокумента;
	КонецЕсли;
	
	ВидТекущегоДокумента = РеквизитыДокумента.ВидДокумента;
	ОтветственныйТекущегоДокумента = РеквизитыДокумента.Ответственный;
	ФорматОсновногоТитулаТекущегоДокумента = РеквизитыДокумента.ФорматОсновногоТитула;
	ИдентификаторОрганизацииТекущегоДокумента = РеквизитыДокумента.ИдентификаторОрганизации;
	ИдентификаторТекущегоДокумента = РеквизитыДокумента.ИдентификаторДокумента;
	ТекущийДокументОблачногоЭДО = РеквизитыДокумента.ЭтоОблачныйЭДО;
	ТекущийДокументПомеченНаУдаление = РеквизитыДокумента.ПометкаУдаления;
	
	НаКонтроле = ИнтеграцияЭДО.НоменклатураКонтрагентовНаКонтроле(
		Новый Структура("ЭлектронныйДокумент", ТекущиеДанные.ЭлектронныйДокумент));
	ЕстьНоменклатураНаКонтролеТекущегоДокумента = ЗначениеЗаполнено(НаКонтроле);
	
	ЭлектронныеДокументыПакета = Новый Массив;
	ЭлектронныеДокументыПакета.Добавить(ТекущиеДанные.ЭлектронныйДокумент);
	ПакетыДокументовЭДО.ДополнитьДокументамиПакетов(ЭлектронныеДокументыПакета);
	СоставПакетаТекущегоДокумента.Очистить();
	Для Каждого ЭлектронныйДокумент Из ЭлектронныеДокументыПакета Цикл
		НовыйДокументПакета = СоставПакетаТекущегоДокумента.Добавить();
		НовыйДокументПакета.ЭлектронныйДокумент = ЭлектронныйДокумент;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьКэшРеквизитовТекущегоФайла()
	
	ТекущийФайлАдресВХранилище = Неопределено;
	ТекущийФайлНомерИконки = Неопределено;
	ТекущийФайлПредставление = Неопределено;
	
КонецПроцедуры

#Область АсинхронноеОбновлениеДанных

&НаКлиенте
Процедура ОжидатьПолученияАсинхронныхДанных()

	ДлительнаяОперация = ОперацияПолученияДанных; // См. ДлительныеОперации.ВыполнитьФункцию

	Если ДлительнаяОперация = Неопределено Тогда
		Если ДанныеКомпонентов <> Неопределено
			И ЗначениеЗаполнено(ДанныеКомпонентов.ПанельПредставленияФайла) Тогда
			ВывестиPDFДокумент(ДанныеКомпонентов.ПанельПредставленияФайла.Данные);
		КонецЕсли;
		Возврат;
	КонецЕсли;

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаполнитьАсинхронныеДанныеПослеОжидания", ЭтотОбъект);

	Оповещение = Новый ОписаниеОповещения(
		"ОжидатьПолученияДанныхКомпонентовПросмотраПослеДлительнойОперации",
		ЛегкийИнтерфейсДокументовЭДОКлиент, ОповещениеОЗавершении);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАсинхронныеДанныеПослеОжидания(Результат, ДополнительныеПараметры) Экспорт

	Если ОперацияПолученияДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Результат.Успех Тогда
		// Для случаев, когда отмена предыдущей операции получения пройти не успела, но результат уже неактуален.
		Если Результат.ОперацияПолученияДанных.Свойство("ИдентификаторЗадания")
			И Не Результат.ОперацияПолученияДанных.ИдентификаторЗадания = ОперацияПолученияДанных.ИдентификаторЗадания Тогда
			Возврат;
		КонецЕсли;
		ОперацияПолученияДанных = Результат.ОперацияПолученияДанных;
		ОбновитьДанныеПослеЗаполненияАсинхронныхДанных();
		НастроитьПанелиПросмотра();
	Иначе
		Элементы.СтраницыПредпросмотр.ТекущаяСтраница = Элементы.СтраницаОшибка;
		ОперацияПолученияДанных = Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПослеЗаполненияАсинхронныхДанных()

	ДанныеКомпонентов = ЛегкийИнтерфейсДокументовЭДО.РезультатЗаполненияДанныхКомпонентовПросмотраДокументаАсинхронноВФоне(
		ОперацияПолученияДанных);
		
	ОперацияПолученияДанных = Неопределено;
	
	Если ДанныеКомпонентов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеКомпонентов.ПанельПредставленияФайла) Тогда
		ВывестиPDFДокумент(ДанныеКомпонентов.ПанельПредставленияФайла.Данные);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти // АсинхронноеОбновлениеДанных

#КонецОбласти // ОбновлениеДанных

#Область ПолучениеДанных

&НаСервере
Функция ПараметрыЗаполненияДанныхКомпонентовПросмотра()

	ПараметрыЗаполнения = ЛегкийИнтерфейсДокументовЭДО.НовыеПараметрыПолученияДанныхКомпонентов();

	ПараметрыЗаполнения.ИдентификаторФормы = УникальныйИдентификатор;
	ПараметрыЗаполнения.СвойстваДокумента = СвойстваТекущегоДокументаДляПолученияДанныхКомпонентовПросмотра();
	НастройкиФормы = ПараметрыЗаполнения.НастройкиФормы;
	НастройкиФормы.ВыводитьБанковскиеРеквизиты = Ложь;
	НастройкиФормы.ОтключитьВыводДопДанных = Истина;
	НастройкиФормы.ОтключитьВыводКопияВерна = Истина;	
	Возврат ПараметрыЗаполнения;

КонецФункции

&НаСервере
Функция СвойстваТекущегоДокументаДляПолученияДанныхКомпонентовПросмотра()
	
	СвойстваДокумента = ЛегкийИнтерфейсДокументовЭДО.НовыеСвойстваДокументаДляПолученияДанныхКомпонентовПросмотра();
	СвойстваДокумента.Ссылка = ТекущийЭлектронныйДокумент;
	СвойстваДокумента.ВидДокумента = ВидТекущегоДокумента;
	СвойстваДокумента.ТипДокумента = ТипТекущегоДокумента;
	СвойстваДокумента.СуммаДокумента = СуммаТекущегоДокумента;
	СвойстваДокумента.Организация = ОрганизацияТекущегоДокумента;
	СвойстваДокумента.Контрагент = КонтрагентТекущегоДокумента;
	СвойстваДокумента.Ответственный = ОтветственныйТекущегоДокумента;
	СвойстваДокумента.ЭтоОблачныйЭДО = ТекущийДокументОблачногоЭДО;
	СвойстваДокумента.ПометкаУдаления = ТекущийДокументПомеченНаУдаление;
	СвойстваДокумента.ФорматОсновногоТитула = ФорматОсновногоТитулаТекущегоДокумента;
	СвойстваДокумента.ИдентификаторОрганизации = ИдентификаторОрганизацииТекущегоДокумента;
	СвойстваДокумента.ИдентификаторДокумента = ИдентификаторТекущегоДокумента;
	Возврат СвойстваДокумента;
	
КонецФункции

#КонецОбласти // ПолучениеДанных

#Область ПредставлениеДанных

&НаКлиенте
Процедура НастроитьПанелиПросмотра()
	
	Если ДанныеКомпонентов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеКомпонентов.ПанельВизуализацииТекущихДел) Тогда
		НастроитьПанельВизуализации(ДанныеКомпонентов.ПанельВизуализацииТекущихДел);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеКомпонентов.ПанельПредставленияФайла) Тогда
		НастроитьПанельПредставленияФайла(ДанныеКомпонентов.ПанельПредставленияФайла);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ДанныеКомпонента - См. ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентаПанельВизуализацииТекущихДел
&НаКлиенте
Процедура НастроитьПанельВизуализации(ДанныеКомпонента)
	
	Если ДанныеКомпонента.АсинхронноеПолучение Тогда
		Элементы.СтраницыПредпросмотр.ТекущаяСтраница = Элементы.СтраницаОжидание;
		Возврат;
	КонецЕсли;
	
	Если ДанныеКомпонента.ФайлПомеченНаУдаление Тогда
		Элементы.СтраницыПредпросмотр.ТекущаяСтраница = Элементы.СтраницаПредставлениеПомеченногоНаУдаление;
		Возврат;
	КонецЕсли;
	
	Если ДанныеКомпонента.ЕстьОшибка Тогда
		Элементы.СтраницыПредпросмотр.ТекущаяСтраница = Элементы.СтраницаОшибка;
		Возврат;
	КонецЕсли;
	
	Если ДанныеКомпонента.ТипДанных = 
		ПредопределенноеЗначение("Перечисление.ТипыДанныхВизуализацииДокументаЭДО.PdfДокумент") Тогда
		
		ТекущийФайлПредставление = ?(ЗначениеЗаполнено(ДанныеКомпонента.НаименованиеФайла),
			ДанныеКомпонента.НаименованиеФайла,
			ИнтерфейсДокументовЭДОКлиентСервер.ПредставлениеОтсутствующегоФайла());
		ТекущийФайлНомерИконки = РаботаСФайламиБЭДКлиентСервер.ИндексКартинкиПоРасширениюФайла(
			ДанныеКомпонента.РасширениеФайла);
		
		Элементы.СтраницыПредпросмотр.ТекущаяСтраница = Элементы.СтраницаФайл;
		
		Если ДанныеКомпонента.ФайлМожноПроштамповать Тогда
			Элементы.СтраницыПредпросмотраФайла.ТекущаяСтраница = Элементы.СтраницаФайлСВизуализацией;
			Элементы.КнопкаОткрытьФайлСВизуализацией.Заголовок = ТекущийФайлПредставление;
			Элементы.СтраницыПредставленияФайла.ТекущаяСтраница = Элементы.СтраницаПредставлениеФайлаОжидание;
		Иначе
			Элементы.СтраницыПредпросмотраФайла.ТекущаяСтраница = Элементы.СтраницаФайлБезВизуализации;
			Элементы.КнопкаОткрытьФайлБезВизуализации.Заголовок = ТекущийФайлПредставление;
		КонецЕсли;
	
	ИначеЕсли ДанныеКомпонента.ТипДанных = 
		ПредопределенноеЗначение("Перечисление.ТипыДанныхВизуализацииДокументаЭДО.HTMLДокумент") Тогда
		
		ПредпросмотрHTML = ДанныеКомпонента.Данные;
		
		Элементы.СтраницыПредпросмотр.ТекущаяСтраница = Элементы.СтраницаHTMLДокумент;
	
	ИначеЕсли ДанныеКомпонента.ТипДанных = 
		ПредопределенноеЗначение("Перечисление.ТипыДанныхВизуализацииДокументаЭДО.ТабличныйДокумент") Тогда
		
		Предпросмотр = ДанныеКомпонента.Данные;
		
		Элементы.СтраницыПредпросмотр.ТекущаяСтраница = Элементы.СтраницаТабличныйДокументИнформацияОтправителя;
	
	Иначе
		
		УстановитьСтраницуПредпросмотраДокументНеВыбран(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ДанныеКомпонента - См. ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеКомпонентаПанельПредставленияФайла
&НаКлиенте
Процедура НастроитьПанельПредставленияФайла(ДанныеКомпонента)
		
	Если ЭтоМобильныйКлиент Тогда
		Элементы.ГруппаПанельПредставленияФайла.Видимость = Ложь;
		Возврат;
	КонецЕсли;
		
	Если ДанныеКомпонента.АсинхронноеПолучение Тогда
		Элементы.СтраницыПредставленияФайла.ТекущаяСтраница = Элементы.СтраницаПредставлениеФайлаОжидание;
		Возврат;
	КонецЕсли;
	
	Если ДанныеКомпонента.ЕстьОшибка Тогда
		Элементы.СтраницыПредставленияФайла.ТекущаяСтраница = Элементы.СтраницаПредставлениеФайлаОшибка;
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеКомпонента.Данные) Тогда
		Элементы.ГруппаПанельПредставленияФайла.Видимость = Ложь;
		Возврат;
	КонецЕсли;

	ИнтерфейсДокументовЭДОКлиент.ВывестиPDFДокументНаФорму(ЭтотОбъект, СведенияОPDFДокументе, ДанныеКомпонента.Данные);
	Элементы.ГруппаПанельПредставленияФайла.Видимость = Истина;
	Элементы.СтраницыПредставленияФайла.ТекущаяСтраница = Элементы.СтраницаПредставлениеФайлаЗагружено;
	ТекущийФайлАдресВХранилище = ПоместитьВоВременноеХранилище(ДанныеКомпонента.Данные, УникальныйИдентификатор);

КонецПроцедуры

&НаСервере
Процедура ВывестиPDFДокумент(ДанныеФайла)
	
	Если Не ЗначениеЗаполнено(ДанныеФайла) Тогда
		Возврат;
	КонецЕсли;
	
	Если СведенияОPDFДокументе = Неопределено Тогда
	
		СведенияОPdfДокументе =
			ИнтерфейсДокументовЭДО.СведенияОPDFДокументеНаФормеПоУмолчанию();
		СведенияОPdfДокументе.ИмяРодителяЭлемента = "СтраницаПредставлениеФайлаЗагружено";
	
	КонецЕсли;
	
	ИнтерфейсДокументовЭДО.НастроитьПанельДляВыводаPDFДокумента(ЭтотОбъект, СведенияОPdfДокументе);
	
КонецПроцедуры
	
#КонецОбласти // ПредставлениеДанных

#КонецОбласти 

 #Область ВидимостьЭлементовФормы
 
 &НаСервере
Процедура КоличествоДокументов()
	
	КоличествоНовыхВходящие = 0;
	КоличествоНовыхИсходящие = 0;
	КоличествоПриглашений = 0;
	КоличествоЧерновики = 0;
	
	СостоянияЭДО = СостоянияДокументовТребующиеВнимания();
		
	Запрос = Новый Запрос;
	ТекстЗапросаВходящиеТребуютВнимания(Запрос);
	Запрос.УстановитьПараметр("Состояние", СостоянияЭДО);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	КоличествоНовыхВходящие = ВыборкаДетальныеЗаписи.Количество();
	
	Запрос = Новый Запрос;
	ТекстЗапросаИсходящиеТребуютВнимания(Запрос);
	Запрос.УстановитьПараметр("Состояние", СостоянияДокументовТребующиеВниманияДляРазделаИсходящие());
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	КоличествоНовыхИсходящие = ВыборкаДетальныеЗаписи.Количество();
		
	Запрос = Новый Запрос;
	ТекстЗапросаЧерновики(Запрос);
	Запрос.УстановитьПараметр("Состояние", СостоянияЭДОДляРазделаЧерновики());	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	КоличествоЧерновики = ВыборкаДетальныеЗаписи.Количество();
	
	СтатусыПриглашений = Новый СписокЗначений; 
	СтатусыПриглашений.Добавить(Перечисления.СтатусыПриглашений.ТребуетсяСогласие);
	СтатусыПриглашений.Добавить(Перечисления.СтатусыПриглашений.ТребуетсяОтправить);
	ПустойКонтрагент = ИнтеграцияЭДО.ПустаяСсылкаКонтрагента();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОсновнаяТаблица.Ключ КАК КлючПриглашения,
		|	ОсновнаяТаблица.Статус КАК СтатусПодключения,
		|	ВЫБОР
		|		КОГДА ОсновнаяТаблица.Контрагент = &ПустойКонтрагент
		|				ИЛИ ОсновнаяТаблица.Контрагент = НЕОПРЕДЕЛЕНО
		|			ТОГДА ОсновнаяТаблица.Наименование + "" ("" + &ИНН + "": "" + ОсновнаяТаблица.ИНН + "")""
		|		ИНАЧЕ ОсновнаяТаблица.Контрагент
		|	КОНЕЦ КАК Контрагент,
		|	УчетныеЗаписиЭДО.Организация КАК Организация,
		|	ОсновнаяТаблица.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
		|	ОсновнаяТаблица.Ознакомиться КАК Ознакомиться,
		|	ОсновнаяТаблица.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
		|	ВЫБОР
		|		КОГДА ОсновнаяТаблица.Контрагент = &ПустойКонтрагент
		|				ИЛИ ОсновнаяТаблица.Контрагент = НЕОПРЕДЕЛЕНО
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК НовыйКонтрагент
		|ИЗ
		|	РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами КАК ОсновнаяТаблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|		ПО ОсновнаяТаблица.ИдентификаторОрганизации = УчетныеЗаписиЭДО.ИдентификаторЭДО
		|ГДЕ
		|	ОсновнаяТаблица.Контрагент <> &ПустойКонтрагент
		|	И ОсновнаяТаблица.ДатаИзмененияСтатуса <> ДАТАВРЕМЯ(1, 1, 1)
		|	И ОсновнаяТаблица.Статус В (&СтатусыПриглашений)";
	
	Запрос.УстановитьПараметр("ПустойКонтрагент", ПустойКонтрагент); 
	Запрос.УстановитьПараметр("СтатусыПриглашений", СтатусыПриглашений);
	Запрос.УстановитьПараметр("ИНН", НСтр("ru = 'ИНН'"));
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	КоличествоПриглашений = ВыборкаДетальныеЗаписи.Количество();
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТранспортныйКонтейнерЭДО.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ТранспортныйКонтейнерЭДО КАК ТранспортныйКонтейнерЭДО
		|ГДЕ
		|	(&ОтображатьУдаленные ИЛИ НЕ ТранспортныйКонтейнерЭДО.ПометкаУдаления)
		|	И НЕ ТранспортныйКонтейнерЭДО.СпособОбменаЭД = ЗНАЧЕНИЕ(Перечисление.СпособыОбменаЭД.УдалитьЧерезВебРесурсБанка)
		|	И ТранспортныйКонтейнерЭДО.Статус В(&СтатусыНеРаспакованныхТранспортныхСообщенийБЭД)
		|	И ТранспортныйКонтейнерЭДО.Направление = &Направление
		|{ГДЕ
		|	ТранспортныйКонтейнерЭДО.Направление}";
	
	СтатусыНеРаспакованныхТранспортныхСообщений = СтатусыНеРаспакованныхТранспортныхСообщенийБЭД();
	Запрос.УстановитьПараметр("СтатусыНеРаспакованныхТранспортныхСообщенийБЭД",
		СтатусыНеРаспакованныхТранспортныхСообщений); 
	
	ВходящееНаправление = ПредопределенноеЗначение("Перечисление.НаправленияЭДО.Входящий");
	Запрос.УстановитьПараметр("Направление",  
		ВходящееНаправление);
		
	Запрос.УстановитьПараметр("ОтображатьУдаленные", ОтображатьУдаленные);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать(); 
	
	КоличествоНераспакованныхКонтейнеров = ВыборкаДетальныеЗаписи.Количество();
	
	ПанельНавигации = Обработки.ИнтерфейсДокументовЭДО.ПолучитьМакет(
		"ПанельНавигацииЛегкийКлиентЭДО_ru").ПолучитьТекст();
	
	РазделыТекущихДелЭДО = РазделыТекущихДелЭДО();
	Если ТекущийРаздел = РазделыТекущихДелЭДО.ВходящиеЭлектронныеДокументы Тогда
		ПанельНавигации = СтрЗаменить(ПанельНавигации, "checkedbutton", "inbox");
	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.ИсходящиеЭлектронныеДокументы Тогда
		ПанельНавигации = СтрЗаменить(ПанельНавигации, "checkedbutton", "sent");
	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.Черновики Тогда 
		ПанельНавигации = СтрЗаменить(ПанельНавигации, "checkedbutton", "draft");
	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.Приглашения Тогда 
		ПанельНавигации = СтрЗаменить(ПанельНавигации, "checkedbutton", "invitation");
	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.Ошибки Тогда 
		ПанельНавигации = СтрЗаменить(ПанельНавигации, "checkedbutton", "error");
	КонецЕсли;
			
	Если КоличествоЧерновики = 0 Тогда
		ПанельНавигации = СтрЗаменить(ПанельНавигации, "КоличествоЧерновики", "");
	Иначе
		ПанельНавигации = СтрЗаменить(ПанельНавигации, "КоличествоЧерновики", КоличествоЧерновики);	
	КонецЕсли;
	
	Если КоличествоПриглашений = 0 Тогда
		ПанельНавигации = СтрЗаменить(ПанельНавигации, "КоличествоПриглашений", "");
	Иначе
		ПанельНавигации = СтрЗаменить(ПанельНавигации, "КоличествоПриглашений", КоличествоПриглашений);	
	КонецЕсли;
	
	Если КоличествоНовыхВходящие = 0 Тогда
		ПанельНавигации = СтрЗаменить(ПанельНавигации, "КоличествоВходящих", "");
	Иначе
		ПанельНавигации = СтрЗаменить(ПанельНавигации, "КоличествоВходящих", КоличествоНовыхВходящие);	
	КонецЕсли;
	
	Если КоличествоНовыхИсходящие = 0 Тогда
		ПанельНавигации = СтрЗаменить(ПанельНавигации, "КоличествоИсходящих", "");
	Иначе
		ПанельНавигации = СтрЗаменить(ПанельНавигации, "КоличествоИсходящих", КоличествоНовыхИсходящие);	
	КонецЕсли;

	Если КоличествоНераспакованныхКонтейнеров = 0 Тогда
		ПанельНавигации = СтрЗаменить(ПанельНавигации, "скрыть", "hidden");
		ПанельНавигации = СтрЗаменить(ПанельНавигации, "КоличествоНераспакованныхКонтейнеров", "");
	Иначе
		ПанельНавигации = СтрЗаменить(ПанельНавигации, "КоличествоНераспакованныхКонтейнеров", 
			КоличествоНераспакованныхКонтейнеров);	
	КонецЕсли;
	
	Задолженность = СведенияОЗадолженности();
	ПоказатьУведомленияПоЗадолженности(ЭтотОбъект, Задолженность);
	ПоказатьУведомлениеПоТекущемуТрафику();
	
	ТребуетсяОбновитьСчетчикиКоличестваДокументов = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ТекстЗапросаЧерновики(Запрос)
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СостоянияЭД.СсылкаНаОбъект КАК Документ
		|ИЗ
		|	РегистрСведений.ЧерновикиЭлектронныхДокументовЛегкийКлиентЭДО КАК СостоянияЭД
		|ГДЕ
		|	СостоянияЭД.СостояниеЭДО В (&Состояние)
		|	И НЕ СостоянияЭД.СсылкаНаОбъект ССЫЛКА Документ.УдалитьПроизвольныйЭД
		|	И НЕ СостоянияЭД.СсылкаНаОбъект ССЫЛКА Документ.ТранспортныйКонтейнерЭДО
		|	И НЕ СостоянияЭД.СсылкаНаОбъект ССЫЛКА Справочник.НастройкиЭДО
		|	И НЕ СостоянияЭД.СсылкаНаОбъект ССЫЛКА Справочник.УдалитьСоглашенияОбИспользованииЭД
		|	И СостоянияЭД.Вид <> """"";
		
КонецПроцедуры

&НаСервере
Процедура ТекстЗапросаИсходящиеТребуютВнимания(Запрос)
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЭлектронныеДокументыПереопределяемый.Ссылка КАК ЭлектронныйДокумент,
		|	ЭлектронныеДокументыПереопределяемый.Ответственный КАК Ответственный,
		|	СостоянияЭДОПереопределяемый.Состояние КАК СостояниеЭДО
		|ИЗ
		|	Документ.ЭлектронныйДокументИсходящийЭДО КАК ЭлектронныеДокументыПереопределяемый
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияДокументовЭДО КАК СостоянияЭДОПереопределяемый
		|		ПО (СостоянияЭДОПереопределяемый.ЭлектронныйДокумент = ЭлектронныеДокументыПереопределяемый.Ссылка)
		|ГДЕ
		|	НЕ ЭлектронныеДокументыПереопределяемый.ПометкаУдаления
		|	И СостоянияЭДОПереопределяемый.Состояние В(&Состояние)";
		
КонецПроцедуры

&НаСервере
Процедура ТекстЗапросаВходящиеТребуютВнимания(Запрос)
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЭлектронныеДокументыПереопределяемый.Ссылка КАК ЭлектронныйДокумент,
		|	ЭлектронныеДокументыПереопределяемый.Ответственный КАК Ответственный,
		|	СостоянияЭДОПереопределяемый.Состояние КАК СостояниеЭДО
		|ИЗ
		|	Документ.ЭлектронныйДокументВходящийЭДО КАК ЭлектронныеДокументыПереопределяемый
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияДокументовЭДО КАК СостоянияЭДОПереопределяемый
		|		ПО (СостоянияЭДОПереопределяемый.ЭлектронныйДокумент = ЭлектронныеДокументыПереопределяемый.Ссылка)
		|ГДЕ
		|	НЕ ЭлектронныеДокументыПереопределяемый.ПометкаУдаления
		|	И СостоянияЭДОПереопределяемый.Состояние В(&Состояние)";

КонецПроцедуры

&НаКлиенте
Процедура НастроитьВидимостьКомандТекущейСтроки(ТекущиеДанные)
		
	Элементы.ПометитьКакПрочитанноеВходящие.Видимость = Ложь;
	Элементы.ПометитьКакПрочитанноеИсходящие.Видимость = Ложь;
	Элементы.ВходящиеЭлектронныеДокументыКонтекстноеМенюПометитьКакПрочтенные.Видимость = Ложь;
	Элементы.ИсходящиеЭлектронныеДокументыКонтекстноеМенюПометитьКакПрочтенные.Видимость = Ложь;
	
	Элементы.ГруппаРаспечататьВыгрузитьВходящие.Видимость = Ложь;
	Элементы.ВходящиеЭлектронныеДокументыКонтекстноеМенюГруппаРаспечататьВыгрузить.Видимость = Ложь;
	Элементы.ГруппаРаспечататьВыгрузитьИсходящие.Видимость = Ложь;
	Элементы.ИсходящиеЭлектронныеДокументыКонтекстноеМенюГруппаРаспечататьВыгрузить.Видимость = Ложь;
		
	Элементы.ГруппаКнопокУтвердитьВходящие.Видимость = Ложь;
	Элементы.ГруппаКнопокПодписатьВходящие.Видимость = Ложь;
	Элементы.ГруппаКнопокАннулироватьВходящие.Видимость = Ложь;
	Элементы.ГруппаКнопокПодготовитьКОтправкеВходящие.Видимость = Ложь;
	Элементы.ГруппаКнопокПодписатьСлужебныеСообщенияВходящие.Видимость = Ложь;

	Элементы.ГруппаКнопокУтвердитьВходящиеКонтекстноеМеню.Видимость = Ложь;
	Элементы.ГруппаКнопокПодписатьВходящиеКонтекстноеМеню.Видимость = Ложь;
	Элементы.ГруппаКнопокАннулироватьВходящиеКонтекстноеМеню.Видимость = Ложь;
	Элементы.ГруппаКнопокПодготовитьКОтправкеВходящиеКонтекстноеМеню.Видимость = Ложь;
	Элементы.ГруппаКнопокПодписатьСлужебныеСообщенияВходящиеКонтекстноеМеню.Видимость = Ложь;

	Элементы.ГруппаКнопокУтвердитьИсходящие.Видимость = Ложь;
	Элементы.ГруппаКнопокПодписатьИсходящие.Видимость = Ложь;
	Элементы.ГруппаКнопокАннулироватьИсходящие.Видимость = Ложь;
	Элементы.ГруппаКнопокПодготовитьКОтправкеИсходящие.Видимость = Ложь;
	Элементы.ГруппаКнопокПодписатьСлужебныеСообщенияИсходящие.Видимость = Ложь;
	
	Элементы.ГруппаКнопокУтвердитьИсходящиеКонтекстноеМеню.Видимость = Ложь;
	Элементы.ГруппаКнопокПодписатьИсходящиеКонтекстноеМеню.Видимость = Ложь;
	Элементы.ГруппаКнопокАннулироватьИсходящиеКонтекстноеМеню.Видимость = Ложь;
	Элементы.ГруппаКнопокПодготовитьКОтправкеИсходящиеКонтекстноеМеню.Видимость = Ложь;
	Элементы.ГруппаКнопокПодписатьСлужебныеСообщенияИсходящиеКонтекстноеМеню.Видимость = Ложь;

	Элементы.АннулироватьВходящие.Видимость = Ложь;
	Элементы.АннулироватьИсходящие.Видимость = Ложь;
	Элементы.Аннулировать.Видимость = Ложь;
	Элементы.ИсходящиеЭлектронныеДокументыКонтекстноеМенюАннулировать.Видимость = Ложь;
	
	Элементы.ЗакрытьПринудительноВходящие.Видимость = Ложь;
	Элементы.ЗакрытьПринудительноИсходящие.Видимость = Ложь;
	Элементы.ВходящиеЭлектронныеДокументыЗакрытьПринудительно.Видимость = Ложь;
	Элементы.ИсходящиеЭлектронныеДокументыКонтекстноеМенюЗакрытьПринудительно.Видимость = Ложь;
	
	Элементы.КомандаПодписатьОтправитьЧерновики.Видимость = Ложь;
	Элементы.УдалитьЧерновикЧерновики.Видимость = Ложь;
	Элементы.ЧерновикиКонтекстноеМенюУдалитьЧерновик.Видимость = Ложь;

	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.КомандаПодписатьОтправитьЧерновики.Видимость = Истина;
	Элементы.УдалитьЧерновикЧерновики.Видимость = Истина;
	Элементы.ЧерновикиКонтекстноеМенюУдалитьЧерновик.Видимость = Истина;
	
	Если Не ТекущиеДанные.Свойство("СостояниеЭДО")
		Или Не ТекущиеДанные.Свойство("СпособОбмена") Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ПометитьКакПрочитанноеВходящие.Видимость = Истина;
	Элементы.ПометитьКакПрочитанноеИсходящие.Видимость = Истина;
	Элементы.ВходящиеЭлектронныеДокументыКонтекстноеМенюПометитьКакПрочтенные.Видимость = Истина;
	Элементы.ИсходящиеЭлектронныеДокументыКонтекстноеМенюПометитьКакПрочтенные.Видимость = Истина;
	
	Элементы.ГруппаРаспечататьВыгрузитьВходящие.Видимость = Истина;
	Элементы.ВходящиеЭлектронныеДокументыКонтекстноеМенюГруппаРаспечататьВыгрузить.Видимость = Истина;
	Элементы.ГруппаРаспечататьВыгрузитьИсходящие.Видимость = Истина;
	Элементы.ИсходящиеЭлектронныеДокументыКонтекстноеМенюГруппаРаспечататьВыгрузить.Видимость = Истина;

	СостояниеЭДО = ТекущиеДанные.СостояниеЭДО;
	СпособОбмена = ТекущиеДанные.СпособОбмена;
	ТипТекущегоДокумента = ТекущиеДанные.ТипДокумента;

	Если СпособОбмена <> ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.Внутренний") Тогда
		Элементы.ЗакрытьПринудительноВходящие.Видимость = Истина;
		Элементы.ЗакрытьПринудительноИсходящие.Видимость = Истина;
		Элементы.ВходящиеЭлектронныеДокументыЗакрытьПринудительно.Видимость = Истина;
		Элементы.ИсходящиеЭлектронныеДокументыКонтекстноеМенюЗакрытьПринудительно.Видимость = Истина;
	КонецЕсли;

	Если СостояниеЭДО = ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ТребуетсяПодписание") 
		И ТипТекущегоДокумента <> 
		ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.МашиночитаемаяДоверенность")
		Тогда
			
		Элементы.АннулироватьВходящие.Видимость = Истина;
		Элементы.Аннулировать.Видимость = Истина;
		
	ИначеЕсли ТипТекущегоДокумента <> 
		ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.МашиночитаемаяДоверенность")
		И СостояниеЭДО = 
		ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ТребуетсяУтверждение")
		Или СостояниеЭДО = 
		ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ТребуетсяИзвещениеОПолучении")
		Или СостояниеЭДО = 
		ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ТребуетсяПодписаниеИзвещения")
		Или СостояниеЭДО = 
		ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ТребуетсяПодготовкаКОтправкеИзвещения")
		Или СостояниеЭДО = 
		ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ТребуетсяОтправкаИзвещения")
		Или СостояниеЭДО = 
		ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ОжидаетсяПодтверждениеОператора")
		Или СостояниеЭДО = 
		ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ОжидаетсяИзвещениеОПолучении")
		Или СостояниеЭДО = 
		ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ОжидаетсяПодтверждение")
		Или СостояниеЭДО = 
		ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ОбменЗавершен")
		Тогда
			
		Элементы.АннулироватьВходящие.Видимость = Истина;
		Элементы.Аннулировать.Видимость = Истина;
		Элементы.АннулироватьИсходящие.Видимость = Истина;
		Элементы.ИсходящиеЭлектронныеДокументыКонтекстноеМенюАннулировать.Видимость = Истина;
		
	КонецЕсли;

	Если СостояниеЭДО = 
		ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ТребуетсяУтверждение") Тогда
		
		Элементы.СтраницыКомандыСписков.ТекущаяСтраница = Элементы.СтраницаУтвердить;
		Элементы.ГруппаКнопокУтвердитьВходящие.Видимость = Истина;
		Элементы.ГруппаКнопокУтвердитьИсходящие.Видимость = Истина;
		Элементы.ГруппаКнопокУтвердитьВходящиеКонтекстноеМеню.Видимость = Истина;
		Элементы.ГруппаКнопокУтвердитьИсходящиеКонтекстноеМеню.Видимость = Истина;
		
	ИначеЕсли СостояниеЭДО = 
			ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ТребуетсяПодписание") 
		Или СостояниеЭДО = 
			ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ТребуетсяПодписаниеАннулирования")
		Или СостояниеЭДО = 
			ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ТребуетсяПодписаниеОтклонения") Тогда
		
		Элементы.СтраницыКомандыСписков.ТекущаяСтраница = Элементы.СтраницаПодписать;
		Элементы.ГруппаКнопокПодписатьВходящие.Видимость = Истина;
		Элементы.ГруппаКнопокПодписатьИсходящие.Видимость = Истина;
		Элементы.ГруппаКнопокПодписатьВходящиеКонтекстноеМеню.Видимость = Истина;
		Элементы.ГруппаКнопокПодписатьИсходящиеКонтекстноеМеню.Видимость = Истина;
			
	ИначеЕсли СостояниеЭДО = 
		ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ТребуетсяПодтверждениеАннулирования") Тогда
		
		Элементы.СтраницыКомандыСписков.ТекущаяСтраница = Элементы.СтраницаАннулировать;
		Элементы.ГруппаКнопокАннулироватьВходящие.Видимость = Истина;
		Элементы.ГруппаКнопокАннулироватьИсходящие.Видимость = Истина;
		Элементы.ГруппаКнопокАннулироватьВходящиеКонтекстноеМеню.Видимость = Истина;
		Элементы.ГруппаКнопокАннулироватьИсходящиеКонтекстноеМеню.Видимость = Истина;
		
	ИначеЕсли СостояниеЭДО = 
		ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ТребуетсяПодготовкаКОтправке")
		Или СостояниеЭДО = 
		ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ТребуетсяОтправка") Тогда
		
		Элементы.СтраницыКомандыСписков.ТекущаяСтраница = Элементы.СтраницаПодготовитьКОтправке;
		Элементы.ГруппаКнопокПодготовитьКОтправкеВходящие.Видимость = Истина;
		Элементы.ГруппаКнопокПодготовитьКОтправкеИсходящие.Видимость = Истина;
		Элементы.ГруппаКнопокПодготовитьКОтправкеВходящиеКонтекстноеМеню.Видимость = Истина;
		Элементы.ГруппаКнопокПодготовитьКОтправкеИсходящиеКонтекстноеМеню.Видимость = Истина;
		
	ИначеЕсли СостояниеЭДО = 
			ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ТребуетсяПодписаниеИзвещения") 
		Или СостояниеЭДО = 
			ПредопределенноеЗначение("Перечисление.СостоянияДокументовЭДО.ТребуетсяПодписаниеИзвещенияПоОтклонению")
			Тогда
		
		Элементы.СтраницыКомандыСписков.ТекущаяСтраница = Элементы.СтраницаПодписатьСлужебныеСообщения;
		Элементы.ГруппаКнопокПодписатьСлужебныеСообщенияВходящие.Видимость = Истина;
		Элементы.ГруппаКнопокПодписатьСлужебныеСообщенияИсходящие.Видимость = Истина;
		Элементы.ГруппаКнопокПодписатьСлужебныеСообщенияВходящиеКонтекстноеМеню.Видимость = Истина;
		Элементы.ГруппаКнопокПодписатьСлужебныеСообщенияИсходящиеКонтекстноеМеню.Видимость = Истина;
		
	Иначе
		
		Элементы.СтраницыКомандыСписков.ТекущаяСтраница = Элементы.СтраницаНедоступныеКоманды;
		Элементы.КоманднаяПанельНедоступныеКоманды.Видимость =
			(ТекущийРаздел = РазделыТекущихДелЭДО().ВходящиеЭлектронныеДокументы);
			
	КонецЕсли;
	
	ЭтоПакетДокументов = ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторПакета);
	
	ТекстЗаголовкаУтвердить = 
		ЛегкийИнтерфейсДокументовЭДОКлиентСервер.ПредставлениеКомандыУтвердить(ЭтоПакетДокументов);
	ТекстЗаголовкаПодписать = 
		ЛегкийИнтерфейсДокументовЭДОКлиентСервер.ПредставлениеКомандыПодписать(ЭтоПакетДокументов);
	ТекстЗаголовкаОтклонить = 
		ЛегкийИнтерфейсДокументовЭДОКлиентСервер.ПредставлениеКомандыОтклонить(ЭтоПакетДокументов);
	ТекстЗаголовкаПринятьАннулирование = 
		ЛегкийИнтерфейсДокументовЭДОКлиентСервер.ПредставлениеКомандыПринятьАннулирование(ЭтоПакетДокументов);
	ТекстЗаголовкаОтклонитьАннулирование = 
		ЛегкийИнтерфейсДокументовЭДОКлиентСервер.ПредставлениеКомандыОтклонитьАннулирование(ЭтоПакетДокументов);
	ТекстЗаголовкаОтправить =
		ЛегкийИнтерфейсДокументовЭДОКлиентСервер.ПредставлениеКомандыОтправить(ЭтоПакетДокументов);
	ТекстЗаголовкаАннулировать = 
		ЛегкийИнтерфейсДокументовЭДОКлиентСервер.ПредставлениеКомандыАннулировать(ЭтоПакетДокументов,
			ТипТекущегоДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.Внутренний"));
	ТекстЗаголовкаЗакрытьПринудительно = 
		ЛегкийИнтерфейсДокументовЭДОКлиентСервер.ПредставлениеКомандыЗакрытьПринудительно(ЭтоПакетДокументов);
	
	Элементы.КомандаУтвердить.Заголовок = ТекстЗаголовкаУтвердить;
	Элементы.КомандаУтвердитьВходящие.Заголовок = ТекстЗаголовкаУтвердить;
	Элементы.КомандаУтвердитьИсходящие.Заголовок = ТекстЗаголовкаУтвердить;
	Элементы.КомандаУтвердитьВходящиеКонтекстноеМеню.Заголовок = ТекстЗаголовкаУтвердить;
	Элементы.КомандаУтвердитьИсходящиеКонтекстноеМеню.Заголовок = ТекстЗаголовкаУтвердить;
		
	Элементы.КомандаПодписать.Заголовок = ТекстЗаголовкаПодписать;
	Элементы.КомандаПодписатьНедоступная.Заголовок = ТекстЗаголовкаПодписать;
	Элементы.КомандаПодписатьВходящие.Заголовок = ТекстЗаголовкаПодписать;
	Элементы.КомандаПодписатьИсходящие.Заголовок = ТекстЗаголовкаПодписать;
	Элементы.КомандаПодписатьВходящиеКонтекстноеМеню.Заголовок = ТекстЗаголовкаПодписать;
	Элементы.КомандаПодписатьИсходящиеКонтекстноеМеню.Заголовок = ТекстЗаголовкаПодписать;
		
	Элементы.КомандаОтказатьсяОтПодписанияУтвердить.Заголовок = ТекстЗаголовкаОтклонить;
	Элементы.КомандаОтказатьсяОтПодписанияПодписать.Заголовок = ТекстЗаголовкаОтклонить;
	Элементы.КомандаОтказатьсяОтПодписанияНедоступная.Заголовок = ТекстЗаголовкаОтклонить;
	Элементы.КомандаОтказатьсяОтПодписанияУтвердитьВходящие.Заголовок = ТекстЗаголовкаОтклонить;
	Элементы.КомандаОтказатьсяОтПодписанияПодписатьВходящие.Заголовок = ТекстЗаголовкаОтклонить;
	Элементы.КомандаОтказатьсяОтПодписанияУтвердитьИсходящие.Заголовок = ТекстЗаголовкаОтклонить;
	Элементы.КомандаОтказатьсяОтПодписанияПодписатьИсходящие.Заголовок = ТекстЗаголовкаОтклонить;
	Элементы.КомандаОтказатьсяОтПодписанияУтвердитьВходящиеКонтекстноеМеню.Заголовок = ТекстЗаголовкаОтклонить;
	Элементы.КомандаОтказатьсяОтПодписанияПодписатьВходящиеКонтекстноеМеню.Заголовок = ТекстЗаголовкаОтклонить;
	Элементы.КомандаОтказатьсяОтПодписанияУтвердитьИсходящиеКонтекстноеМеню.Заголовок = ТекстЗаголовкаОтклонить;
	Элементы.КомандаОтказатьсяОтПодписанияПодписатьИсходящиеКонтекстноеМеню.Заголовок = ТекстЗаголовкаОтклонить;
		
	Элементы.ПринятьАннулирование.Заголовок = ТекстЗаголовкаПринятьАннулирование;
	Элементы.КомандаАннулироватьВходящие.Заголовок = ТекстЗаголовкаПринятьАннулирование;
	Элементы.КомандаАннулироватьИсходящие.Заголовок = ТекстЗаголовкаПринятьАннулирование;
	Элементы.КомандаАннулироватьВходящиеКонтекстноеМеню.Заголовок = ТекстЗаголовкаПринятьАннулирование;
	Элементы.КомандаАннулироватьИсходящиеКонтекстноеМеню.Заголовок = ТекстЗаголовкаПринятьАннулирование;
		
	Элементы.ОтклонитьАннулирование.Заголовок = ТекстЗаголовкаОтклонитьАннулирование;
	Элементы.ОтклонитьАннулированиеВходящие.Заголовок = ТекстЗаголовкаОтклонитьАннулирование;
	Элементы.ОтклонитьАннулированиеИсходящие.Заголовок = ТекстЗаголовкаОтклонитьАннулирование;
	Элементы.ОтклонитьАннулированиеВходящиеКонтекстноеМеню.Заголовок = ТекстЗаголовкаОтклонитьАннулирование;
	Элементы.ОтклонитьАннулированиеИсходящиеКонтекстноеМеню.Заголовок = ТекстЗаголовкаОтклонитьАннулирование;
	
	Элементы.Отправить.Заголовок = ТекстЗаголовкаОтправить;
	Элементы.ОтправитьВходящие.Заголовок = ТекстЗаголовкаОтправить;
	Элементы.ОтправитьИсходящие.Заголовок = ТекстЗаголовкаОтправить;
	Элементы.ОтправитьВходящиеКонтекстноеМеню.Заголовок = ТекстЗаголовкаОтправить;
	Элементы.ОтправитьИсходящиеКонтекстноеМеню.Заголовок = ТекстЗаголовкаОтправить;
	
	Элементы.АннулироватьВходящие.Заголовок = ТекстЗаголовкаАннулировать;
	Элементы.АннулироватьИсходящие.Заголовок = ТекстЗаголовкаАннулировать;
	Элементы.Аннулировать.Заголовок = ТекстЗаголовкаАннулировать;
	Элементы.ИсходящиеЭлектронныеДокументыКонтекстноеМенюАннулировать.Заголовок = ТекстЗаголовкаАннулировать;
	
	Элементы.ЗакрытьПринудительноВходящие.Заголовок = ТекстЗаголовкаЗакрытьПринудительно;
	Элементы.ЗакрытьПринудительноИсходящие.Заголовок = ТекстЗаголовкаЗакрытьПринудительно;
	Элементы.ВходящиеЭлектронныеДокументыЗакрытьПринудительно.Заголовок = ТекстЗаголовкаЗакрытьПринудительно;
	Элементы.ИсходящиеЭлектронныеДокументыКонтекстноеМенюЗакрытьПринудительно.Заголовок = 
		ТекстЗаголовкаЗакрытьПринудительно;

КонецПроцедуры

&НаКлиенте
Процедура ВидимостьЭлементовИКоманд()
	
	ТекущиеДанные = Неопределено;
			
	Элементы.ГруппаКомандыРасширенныйПоиск.Видимость = Ложь;
	
	Элементы.Ответственный.Видимость = Истина;
	Элементы.Состояние.Видимость = Истина;
	Элементы.ТипДокумента.Видимость = Истина;
	Элементы.ТипРегламента.Видимость = Истина;
	Элементы.СтатусПриглашения.Видимость = Ложь;
	Элементы.ГруппаБыстрыеОтборыЛево.Видимость = Истина;
	Элементы.ГруппаБыстрыеОтборыПраво.Видимость = Истина;

	Если ИнтерфейсДокументовЭДОВызовСервера.ИспользуетсяОтправкаПолучениеСОтборомПоОрганизации() = Истина Тогда
		Элементы.ВыборочныйОбменПоОрганизациямВходящие.Пометка = Истина;
	Иначе
		Элементы.ВыборочныйОбменПоОрганизациямВходящие.Пометка = Ложь;
	КонецЕсли;
		
	Элементы.ВыборочныйОбменПоОрганизациямИсходящие.Пометка = Элементы.ВыборочныйОбменПоОрганизациямВходящие.Пометка;
	
	РазделыТекущихДелЭДО = РазделыТекущихДелЭДО();
	
	Если ТекущийРаздел = РазделыТекущихДелЭДО.ВходящиеЭлектронныеДокументы Тогда
		
		ТекущиеДанные = Элементы.ВходящиеЭлектронныеДокументы.ТекущиеДанные;
		
		Элементы.СтраницыКомандыТекущегоСписка.ТекущаяСтраница = Элементы.СтраницаКомандыВходящиеИсходящие;
		
		Элементы.СтраницыДополнениеСтрокаПоиска.ТекущаяСтраница = Элементы.СтраницаДополнениеСтрокаПоискаВходящие;
				
		Элементы.ТипДокумента.Видимость = Истина;

		Элементы.ГруппаКомандыСписков.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	
		Элементы.ГруппаКомандыРасширенныйПоиск.Видимость = Истина;
		
		Элементы.ГруппаБыстрыеОтборы.Видимость = РасширенныйПоискРаскрыт;
		
		Элементы.ГруппаКомандыСозданияДокумента.Видимость = Истина;
		
	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.ИсходящиеЭлектронныеДокументы Тогда
		
		ТекущиеДанные = Элементы.ИсходящиеЭлектронныеДокументы.ТекущиеДанные;
		
		Элементы.СтраницыКомандыТекущегоСписка.ТекущаяСтраница = Элементы.СтраницаКомандыВходящиеИсходящие;
		
		Элементы.СтраницыДополнениеСтрокаПоиска.ТекущаяСтраница = Элементы.СтраницаДополнениеСтрокаПоискаИсходящие;
						
		Элементы.ТипДокумента.Видимость = Истина;

		Элементы.ГруппаКомандыСписков.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	
		Элементы.ГруппаКомандыРасширенныйПоиск.Видимость = Истина;
		
		Элементы.ГруппаБыстрыеОтборы.Видимость = РасширенныйПоискРаскрыт;
		
		Элементы.ГруппаКомандыСозданияДокумента.Видимость = Истина;

	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.Черновики Тогда
		
		ТекущиеДанные = Элементы.Черновики.ТекущиеДанные;
		
		Элементы.СтраницыКомандыТекущегоСписка.ТекущаяСтраница = Элементы.СтраницаКомандыЧерновики;
		
		Элементы.СтраницыДополнениеСтрокаПоиска.ТекущаяСтраница = Элементы.СтраницаДополнениеСтрокаПоискаЧерновики;
		
		Элементы.ГруппаКомандыСписков.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	
		Элементы.ГруппаКомандыРасширенныйПоиск.Видимость = Истина;
		
		Элементы.ГруппаБыстрыеОтборы.Видимость = РасширенныйПоискРаскрыт;
		
		Элементы.Ответственный.Видимость = Ложь;
		Элементы.Состояние.Видимость = Ложь;
		Элементы.ТипДокумента.Видимость = Ложь;
		Элементы.ТипРегламента.Видимость = Ложь;
		
		Элементы.ГруппаКомандыСозданияДокумента.Видимость = Истина;

	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.Приглашения Тогда
		
		ТекущиеДанные = Неопределено;
		
		Элементы.СтраницыКомандыТекущегоСписка.ТекущаяСтраница = Элементы.СтраницаКомандыПриглашения;
		
		Элементы.СтраницыДополнениеСтрокаПоиска.ТекущаяСтраница = Элементы.СтраницаДополнениеСтрокаПоискаПриглашения; 
		
		Элементы.ГруппаКомандыСписков.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
			
		Элементы.ГруппаКомандыРасширенныйПоиск.Видимость = Истина;
		
		Элементы.ГруппаБыстрыеОтборы.Видимость = РасширенныйПоискРаскрыт;
		
		Элементы.Ответственный.Видимость = Ложь;
		Элементы.Состояние.Видимость = Ложь;
		Элементы.ТипДокумента.Видимость = Ложь;
		Элементы.ТипРегламента.Видимость = Ложь;
		Элементы.СтатусПриглашения.Видимость = Истина;
		
		Элементы.ОткрытьПользовательскиеОтборыСпискаПриглашения.Видимость = Истина;
		Элементы.ГруппаКомандыСозданияДокумента.Видимость = Ложь;
			
	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.Ошибки Тогда
		
		ТекущиеДанные = Неопределено;
		
		Элементы.СтраницыКомандыТекущегоСписка.ТекущаяСтраница = Элементы.СтраницаКомандыОшибки;
		
		Элементы.СтраницыДополнениеСтрокаПоиска.ТекущаяСтраница = Элементы.СтраницаДополнениеСтрокаПоискаОшибки;
				 
		Элементы.ГруппаКомандыСписков.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	
		Элементы.ГруппаКомандыРасширенныйПоиск.Видимость = Ложь;
		
		Элементы.ОткрытьПользовательскиеОтборыСпискаПриглашения.Видимость = Ложь;
		
		Элементы.ГруппаКомандыСозданияДокумента.Видимость = Ложь;
		
		Элементы.ГруппаБыстрыеОтборы.Видимость = Истина;
		
		Элементы.ГруппаБыстрыеОтборыЛево.Видимость = Ложь;
		
		Элементы.ГруппаБыстрыеОтборыПраво.Видимость = Ложь;
		
		Элементы.ТипРегламента.Видимость = Ложь;
		
		Элементы.СтатусПриглашения.Видимость = Ложь;
	
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	НастроитьВидимостьКомандТекущейСтроки(ТекущиеДанные);
	ОбновитьОтображениеФильтров();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКомандФормыПоПравам()
	
	ЕстьПравоОбработкиДокументов = ЭлектронныеДокументыЭДО.ЕстьПравоОбработкиДокументов();
	ЕстьПравоНастройкиОбмена = НастройкиЭДО.ЕстьПравоНастройкиОбмена();
	ЕстьПравоАдминистрирования = ОбщегоНазначенияБЭД.ЕстьПравоАдминистрирования();
	ЕстьПравоВыполненияОбмена = СинхронизацияЭДО.ЕстьПравоВыполненияОбмена();
	
	Элементы.ГруппаДействияПоЭДОВходящие.Видимость = ЕстьПравоОбработкиДокументов;
	Элементы.ВходящиеЭлектронныеДокументыКонтекстноеМенюГруппаОсновныеКоманды.Видимость = ЕстьПравоОбработкиДокументов;
	
	Элементы.ГруппаДействияПоЭДОИсходящие.Видимость = ЕстьПравоОбработкиДокументов;
	Элементы.ИсходящиеЭлектронныеДокументыКонтекстноеМенюГруппаОсновныеКоманды.Видимость = ЕстьПравоОбработкиДокументов;
	
	Элементы.ГруппаДействияПоЭДОЧерновики.Видимость = ЕстьПравоОбработкиДокументов;
	Элементы.ЧерновикиКонтекстноеМенюГруппаДействияСДокументом.Видимость = ЕстьПравоОбработкиДокументов;
	
	Элементы.НовоеПриглашениеПриглашения.Видимость = ЕстьПравоОбработкиДокументов;
	Элементы.ГруппаКнопокПринятьПриглашения.Видимость = ЕстьПравоОбработкиДокументов;
	Элементы.ГруппаКнопокОтправитьПриглашения.Видимость = ЕстьПравоОбработкиДокументов;
	Элементы.ГруппаКнопокПринятьПриглашенияКонтекстноеМеню.Видимость = ЕстьПравоОбработкиДокументов;
	Элементы.ГруппаКнопокОтправитьПриглашенияКонтекстноеМеню.Видимость = ЕстьПравоОбработкиДокументов;
	
	Элементы.ГруппаКнопкиДействийСОшибками.Видимость = ЕстьПравоОбработкиДокументов;
	Элементы.ГруппаКнопкиДействийСОшибкамиКонтекстноеМеню.Видимость = ЕстьПравоОбработкиДокументов;
	
	Элементы.СтраницаКомандыВходящиеИсходящие.Видимость = ЕстьПравоОбработкиДокументов;
	Элементы.СтраницаКомандыЧерновики.Видимость = ЕстьПравоОбработкиДокументов;
	Элементы.СтраницаКомандыПриглашения.Видимость = ЕстьПравоОбработкиДокументов;
	Элементы.СтраницаКомандыОшибки.Видимость = ЕстьПравоОбработкиДокументов;
	
	Элементы.ОбновитьВходящие.Видимость = ЕстьПравоВыполненияОбмена;
	Элементы.ОбновитьИсходящие.Видимость = ЕстьПравоВыполненияОбмена;
	Элементы.ОбновитьЧерновики.Видимость = ЕстьПравоВыполненияОбмена;
	Элементы.ОбновитьПриглашения.Видимость = ЕстьПравоВыполненияОбмена;
	Элементы.ОбновитьОшибки.Видимость = ЕстьПравоВыполненияОбмена;
	Элементы.ВыборочныйОбменПоОрганизациямВходящие.Видимость = ЕстьПравоВыполненияОбмена И НесколькоОрганизаций;
	Элементы.ВыборочныйОбменПоОрганизациямИсходящие.Видимость = ЕстьПравоВыполненияОбмена И НесколькоОрганизаций;
	Элементы.ГруппаКнопокПринять.Видимость = ЕстьПравоНастройкиОбмена;
	Элементы.НовоеПриглашение.Видимость = ЕстьПравоНастройкиОбмена;
	Элементы.КомандаОтправитьПриглашение.Видимость = ЕстьПравоНастройкиОбмена;
	
	Элементы.ЗагрузитьФайлыВходящие.Видимость = ЕстьПравоОбработкиДокументов;
	Элементы.ЗагрузитьФайлыИсходящие.Видимость = ЕстьПравоОбработкиДокументов;
	Элементы.ЗагрузитьФайлыЧерновики.Видимость = ЕстьПравоОбработкиДокументов;
	
	Элементы.ПроверитьПодписиВходящие.Видимость = ЕстьПравоАдминистрирования;
	Элементы.ПроверитьПодписиИсходящие.Видимость = ЕстьПравоАдминистрирования;
	
КонецПроцедуры

#КонецОбласти

#Область ДействияСДокументами

 &НаКлиенте
Процедура ОткрытьЭлектронныйДокументДляПросмотра(ДанныеСтроки)
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИнтерфейсДокументовЭДОКлиент.ОткрытьЭлектронныйДокумент(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправкаПолучениеЭДЗавершение(Результат, Контекст) Экспорт
	
	ОбновитьДинамическиеСпискиФормы();
	
	Элементы.Обновить.Доступность = Истина;
	
	КоличествоДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьСформироватьПодписатьИОтправить()
	
	ВыделенныеСтроки = Элементы.Черновики.ВыделенныеСтроки;
	НаборОбъектовУчета = Новый Массив;
	НаборЭлектронныхДокументов = Новый Массив;
	
	Для Индекс = 0 По ВыделенныеСтроки.ВГраница() Цикл
		ДанныеСтроки = Элементы.Черновики.ДанныеСтроки(ВыделенныеСтроки[Индекс]);
		Если ЗначениеЗаполнено(ДанныеСтроки.ЭлектронныйДокумент) Тогда
			НаборЭлектронныхДокументов.Добавить(ДанныеСтроки.ЭлектронныйДокумент);
		Иначе
			НаборОбъектовУчета.Добавить(ДанныеСтроки.Документ);
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(НаборЭлектронныхДокументов) Тогда
		ВыполнитьКомандаПодписать();
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НаборОбъектовУчета) Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Источник", ЭтотОбъект);
	ДополнительныеПараметры.Вставить("ОбъектыУчета", НаборОбъектовУчета);
	ДополнительныеПараметры.Вставить("РежимПодписатьОтправить", Истина);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект, ДополнительныеПараметры);
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	
	Если ОтложеннаяОтправкаЭлектронныхДокументов Тогда
		ЭлектронныеДокументыЭДОКлиент.СформироватьПодписатьПодготовитьКОтправкеЭД(НаборОбъектовУчета,
			ДополнительныеПараметры);
	Иначе
		ЭлектронныеДокументыЭДОКлиент.СформироватьПодписатьОтправитьЭД(НаборОбъектовУчета, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыполнитьКомандаПодписать()	
	
	ДанныеВыделенныхСтрокРаздела = ДанныеВыделенныхСтрокРаздела(ТекущийРаздел, 
		"ЭлектронныйДокумент, ИдентификаторПакета");
	
	Если Не ЗначениеЗаполнено(ДанныеВыделенныхСтрокРаздела) Тогда		
		Возврат;
	КонецЕсли;
	
	ОбъектыДействий = ОбъектыДействий(ДанныеВыделенныхСтрокРаздела);
	
	ЭтоПакетнаяОбработка = ЗначениеЗаполнено(ОбъектыДействий.ПакетыДокументов);
	Если ЭтоПакетнаяОбработка И Не ВыполнениеДействийПоЭДОДоступноДляПакетов(ОбъектыДействий.ПакетыДокументов) Тогда
		ВидДействия = ЛегкийИнтерфейсДокументовЭДОКлиент.ВидыВыполняемыхДействийСДокументом().Подписать;
		ТекстВопроса = ЛегкийИнтерфейсДокументовЭДОКлиент.ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ОставитьДляОбработкиТолькоТекущийДокумент(ОбъектыДействий);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	РазделыТекущихДелЭДО = РазделыТекущихДелЭДО();
	НаборДействий = Новый Соответствие;	
	Если ТекущийРаздел <> РазделыТекущихДелЭДО.ИсходящиеЭлектронныеДокументы Тогда
		ДействияПоЭДОУтвердить = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Утвердить");
		ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, 
			ДействияПоЭДОУтвердить);
		ДействияПоЭДОСформироватьОтвет = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.СформироватьОтвет");
		ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, 
			ДействияПоЭДОСформироватьОтвет);
	КонецЕсли;
	ДействияПоЭДОПодписать = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Подписать");
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, 
		ДействияПоЭДОПодписать);
	ДействияПоЭДОПодготовитьКОтправке = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.ПодготовитьКОтправке");
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, 
		ДействияПоЭДОПодготовитьКОтправке);
	Если Не ОтложеннаяОтправкаЭлектронныхДокументов
		Или ТекущийРаздел = РазделыТекущихДелЭДО.ВходящиеЭлектронныеДокументы Тогда
		ДействияПоЭДООтправить = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Отправить");
		ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, 
			ДействияПоЭДООтправить);
	КонецЕсли;
	ПараметрыВыполненияДействийПоЭДО = ЭлектронныеДокументыЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	ПараметрыВыполненияДействийПоЭДО.НаборДействий = НаборДействий;
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ЭлектронныеДокументы = ОбъектыДействий.ЭлектронныеДокументы;
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ПакетыДокументов = ОбъектыДействий.ПакетыДокументов;

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеУспешногоЗавершения", 
		Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект, НаборДействий));
	ДополнительныеПараметры.Вставить("ПараметрыВыполненияДействийПоЭДО", ПараметрыВыполненияДействийПоЭДО);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект, ДополнительныеПараметры);
	
	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ПараметрыВыполненияДействийПоЭДО);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияДействийПоЭДО(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда

		Если Результат.Свойство("КонтекстДиагностики")
			И ОбработкаНеисправностейБЭДКлиентСервер.ЕстьОшибки(Результат.КонтекстДиагностики) Тогда

			ПрочиеОшибки = Результат.КонтекстДиагностики.Диагностика.Ошибки;
			Если ПрочиеОшибки.Количество() = 1
				И ОбработкаНеисправностейБЭДКлиентСервер.ЭтоОшибкаДанногоВида(ПрочиеОшибки[0],
					МашиночитаемыеДоверенностиКлиентСервер.ВидОшибкиЕстьПодписиСМЧДДоверенностьНеНайдена()) Тогда
						МашиночитаемыеДоверенностиКлиент.ОткрытьДиалогРучногоПодтвержденияПроверкиПодписиПоМЧД(
							Результат.КонтекстДиагностики, ДополнительныеПараметры);
			Иначе
				ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(Результат.КонтекстДиагностики);
			КонецЕсли;

		ИначеЕсли Результат.Свойство("ОшибкиФормирования") И Результат.ОшибкиФормирования.Количество() Тогда

			ПараметрыОбработки = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыПроблемПриОбработкеДокументов();
			ПараметрыОбработки.АдресСведенийОбОшибках = ПоместитьВоВременноеХранилище(Результат.ОшибкиФормирования,
				Новый УникальныйИдентификатор());
			Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
				И ДополнительныеПараметры.Свойство("ОбъектыУчета") Тогда
				ПараметрыОбработки.СписокДокументовКОтправке = ДополнительныеПараметры.ОбъектыУчета;
			КонецЕсли;
			Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
				И ДополнительныеПараметры.Свойство("РежимПодписатьОтправить") Тогда
				ПараметрыОбработки.РежимПодписатьОтправить = ДополнительныеПараметры.РежимПодписатьОтправить;				
			КонецЕсли;
			Если Результат.ОшибкиФормирования.Количество()
				И Результат.ОшибкиФормирования[0].Свойство("ОшибкаФормированияВПрикладнойЧасти") 
				И Результат.ОшибкиФормирования[0].ОшибкаФормированияВПрикладнойЧасти <> Неопределено Тогда
				ПараметрыОбработки.Вставить("ОткрытьФормуРучногоФормированияОтветногоТитула", Истина);
			КонецЕсли;
			Оповещение = Новый ОписаниеОповещения("ПослеЗавершенияРаботыПомощникаФормирования", 
				ЭтотОбъект, ДополнительныеПараметры);
			ИнтерфейсДокументовЭДОКлиент.ПоказатьПроблемыПриОбработкеДокументов(Оповещение, ПараметрыОбработки);
	
		КонецЕсли;

	КонецЕсли; 
	
	ОбновитьДинамическийСписокТекущегоРаздела();
	
	НастроитьВидимостьКомандТекущейСтроки(Элементы[ТекущийРаздел].ТекущиеДанные);
	
	ПодключитьОбработчикОжидания("ОбновитьОбластьПросмотра", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияРаботыПомощникаФормирования(Результат, ДополнительныеПараметры) Экспорт	
	
	Если Результат <> Неопределено Тогда
		ОповещениеЗавершения = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект, 
			ДополнительныеПараметры);
		ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(ОповещениеЗавершения, Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОтменуФормированияДокументовПослеВводаСтроки(Комментарий, ДополнительныеПараметры) Экспорт
	
	Если Комментарий = Неопределено Тогда
		Возврат; 
	КонецЕсли;
	
	ИнтерфейсДокументовЭДОВызовСервера.ОтменитьФормированиеДокументовПоОбъектамУчета(
		ДополнительныеПараметры.ОбъектыУчета, Комментарий);	
	
	Оповестить(ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияТекущихДелЭДО());
			
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДополнитьДокументамиПакетов(ЭлектронныеДокументы)
	ПакетыДокументовЭДО.ДополнитьДокументамиПакетов(ЭлектронныеДокументы);
КонецПроцедуры

&НаКлиенте
Процедура ПослеСменыОтветственного(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Или Не Результат.Успех Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = ТекущиеДанныеРазделаЭлектронныхДокументов();
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСНераспакованнымиКонтейнерами

&НаКлиенте
Процедура УстановитьСтатусОтменен(МассивЭД)

	КонтекстДиагностики = ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики();
	Количество = 0;
	СтатусыТранспортныхСообщенийБЭДОтменен = 
		ПредопределенноеЗначение("Перечисление.СтатусыТранспортныхСообщенийБЭД.Отменен");
	УстановитьСтатусТранспортныхКонтейнеров(МассивЭД, СтатусыТранспортныхСообщенийБЭДОтменен, Количество, 
		КонтекстДиагностики);
	ТекстОповещения = НСтр("ru = 'Изменен статус транспортного контейнера на ""Отменен"": (%1)'");
	ТекстОповещения = СтрЗаменить(ТекстОповещения, "%1", Количество);
	Текст = НСтр("ru = 'Обмен электронными документами'");
	ПоказатьОповещениеПользователя(Текст, , ТекстОповещения);
	ОбновитьДинамическийСписокТекущегоРаздела();
	ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(КонтекстДиагностики);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьСтатусТранспортныхКонтейнеров(Знач МассивКонтейнеров, Знач СтатусКонтейнера,
		КоличествоИзмененных, КонтекстДиагностики)

	КоличествоИзмененных = 0;
	Для Каждого СтрокаТаблицы Из МассивКонтейнеров Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Если СинхронизацияЭДО.ИзменитьСтатусТранспортногоКонтейнера(СтрокаТаблицы.Ссылка, СтатусКонтейнера) Тогда
				КоличествоИзмененных = КоличествоИзмененных + 1;
			КонецЕсли;
			КоличествоИзмененных = КоличествоИзмененных + 1;
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ТекстОперации = НСтр("ru = 'Изменение статуса транспортных контейнеров ЭД'");
			ДополнительныеПараметрыОшибки = Новый Структура;
			ДополнительныеПараметрыОшибки.Вставить("СсылкаНаОбъект", СтрокаТаблицы.Ссылка);
			ВидОшибкиНеизвестнаяОшибка = ОбработкаНеисправностейБЭДКлиентСервер.ВидОшибкиНеизвестнаяОшибка();
			ИнформацияОшибки = ИнформацияОбОшибке();
			ПодробноеОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(
				ИнформацияОшибки);
			КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОшибки);
			Ошибка = ОбработкаНеисправностейБЭД.НоваяОшибка(ТекстОперации, ВидОшибкиНеизвестнаяОшибка, 
				ПодробноеОписаниеОшибки, КраткоеПредставлениеОшибки, ДополнительныеПараметрыОшибки);
			ПодсистемаБЭД = ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД();
			ОбработкаНеисправностейБЭД.ДобавитьОшибку(КонтекстДиагностики, Ошибка,
				ПодсистемаБЭД.ОбменСКонтрагентами);
				
		КонецПопытки;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура ПодключитьУчетнуюЗапись()
	
	ПараметрыСоздания = УчетныеЗаписиЭДОКлиент.НовыеПараметрыСозданияУчетнойЗаписи();
	ПараметрыСоздания.РежимОткрытия = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
	ПараметрыСоздания.ВладелецФормы = ЭтотОбъект;
	УчетныеЗаписиЭДОКлиент.СоздатьУчетнуюЗапись(ПараметрыСоздания);
	
КонецПроцедуры

&НаКлиенте
Функция ДанныеВыделенныхСтрокРаздела(Знач Раздел, Знач ПутьКДанным, ПолучатьКлючСтроки = Ложь)
	
	МассивДанных = Новый Массив;
	ИспользоватьМассивРеквизитов = Ложь;
	
	Таблица = Элементы[Раздел];
	
	Реквизиты = СтрЗаменить(ПутьКДанным, " ", "");
	МассивРеквизитов = СтрРазделить(Реквизиты, ",", Ложь);
	
	Если МассивРеквизитов.Количество() > 1 Или ПолучатьКлючСтроки Тогда
		ИспользоватьМассивРеквизитов = Истина;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из Таблица.ВыделенныеСтроки Цикл
		
		Данные = Таблица.ДанныеСтроки(СтрокаТаблицы);
		Если Данные = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИспользоватьМассивРеквизитов Тогда
			
			Результат = Новый Структура;
			
			Для Каждого Реквизит Из МассивРеквизитов Цикл
				Результат.Вставить(Реквизит, Данные[Реквизит]);
			КонецЦикла;
			Результат.Вставить("КлючСтроки", СтрокаТаблицы);
			МассивДанных.Добавить(Результат);
			
		Иначе
			МассивДанных.Добавить(Данные[ПутьКДанным]);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивДанных;
	
КонецФункции

// Параметры:
//  Действие - см.ОтветНаПриглашение_Принять
//           - см.ОтветНаПриглашение_Отклонить
//           - см.ДействиеСПриглашением_Отправить
//  ПутьКДанным - Строка - Путь к данным
// 
// Возвращаемое значение:
//  Массив Из Структура - Данные реквизитов выделенных строк раздела приглашений
&НаКлиенте
Функция ДанныеВыделенныхСтрокРазделаПриглашений(Знач Действие, Знач ПутьКДанным)
	
	МассивДанных = Новый Массив;
	
	Таблица = Элементы["Приглашения"];
	
	Реквизиты = СтрЗаменить(ПутьКДанным, " ", "");
	МассивРеквизитов = СтрРазделить(Реквизиты, ",", Ложь);
	
	Для Каждого СтрокаТаблицы Из Таблица.ВыделенныеСтроки Цикл
		
		Данные = Таблица.ДанныеСтроки(СтрокаТаблицы);
		Если Данные = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Действие = ОтветНаПриглашение_Принять()
			И Данные.СтатусПодключения <> ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.ТребуетсяСогласие") Тогда
			Продолжить;
		ИначеЕсли Действие = ОтветНаПриглашение_Отклонить()
			И Данные.СтатусПодключения <> ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.ТребуетсяСогласие") Тогда
			Продолжить;
		ИначеЕсли Действие = ДействиеСПриглашением_Отправить()
			И Данные.СтатусПодключения <> ПредопределенноеЗначение("Перечисление.СтатусыПриглашений.ТребуетсяОтправить") Тогда
			Продолжить;
		КонецЕсли;
		
		Результат = Новый Структура;
		
		Для Каждого Реквизит Из МассивРеквизитов Цикл
			Если Реквизит = "Контрагент" Тогда
				Результат.Вставить("Контрагент", Данные["КонтрагентСортировка"]); //При группировке, поле контрагента может очищаться
			Иначе
				Результат.Вставить(Реквизит, Данные[Реквизит]);
			КонецЕсли;
		КонецЦикла;
		Результат.Вставить("КлючСтроки", СтрокаТаблицы);
		МассивДанных.Добавить(Результат);
		
	КонецЦикла;
	
	Возврат МассивДанных;
	
КонецФункции

&НаКлиенте
Функция ОбъектыДействий(СтрокиРаздела)
	
	Результат = ИнтерфейсДокументовЭДОКлиентСервер.НовыеОбъектыДействийПоЭДО();
	
	Для Каждого СтрокаРаздела Из СтрокиРаздела Цикл
		
		ИдентификаторПакета = СтрокаРаздела.ИдентификаторПакета;
		
		Если ЗначениеЗаполнено(ИдентификаторПакета) И ПакетноеОтображение
			И Результат.ПакетыДокументов.Найти(ИдентификаторПакета) = Неопределено Тогда
			
			Результат.ПакетыДокументов.Добавить(ИдентификаторПакета);
			
		Иначе
			Результат.ЭлектронныеДокументы.Добавить(СтрокаРаздела.ЭлектронныйДокумент);	
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЕстьДублирующийсяЭлемент(Элементы, ЭлементПоиска)
	
	Для Каждого Элемент Из Элементы Цикл
		Если Элемент <> ЭлементПоиска Тогда
			
			Если Элемент.Оформление.Элементы.Количество() <> ЭлементПоиска.Оформление.Элементы.Количество() Тогда
				Продолжить;
			КонецЕсли;
			
			НайденОтличающийсяЭлемент = Ложь;
			
			ЧислоЭлементов = Элемент.Оформление.Элементы.Количество();
			Для Индекс = 0 По ЧислоЭлементов - 1 Цикл
				Элемент1 = Элемент.Оформление.Элементы[Индекс];
				Элемент2 = ЭлементПоиска.Оформление.Элементы[Индекс];
				
				Если Элемент1.Использование И Элемент2.Использование Тогда
					Если Элемент1.Параметр <> Элемент2.Параметр ИЛИ Элемент1.Значение <> Элемент2.Значение Тогда
						НайденОтличающийсяЭлемент = Истина;
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Если НайденОтличающийсяЭлемент Тогда
				Продолжить;
			КонецЕсли;
			
			Если Элемент.Отбор.Элементы.Количество() <> ЭлементПоиска.Отбор.Элементы.Количество() Тогда
				Продолжить;
			КонецЕсли;
			
			ЧислоЭлементов = Элемент.Отбор.Элементы.Количество();
			Для Индекс = 0 По ЧислоЭлементов - 1 Цикл
				Элемент1 = Элемент.Отбор.Элементы[Индекс];
				Элемент2 = ЭлементПоиска.Отбор.Элементы[Индекс];
				
				ЭлементыРавны = СравнитьЭлементыОтбора(Элемент1, Элемент2);
				Если Не ЭлементыРавны Тогда
					НайденОтличающийсяЭлемент = Истина;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			Если НайденОтличающийсяЭлемент Тогда
				Продолжить;
			КонецЕсли;
			
			Возврат Истина;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция СравнитьЭлементыОтбора(Элемент1, Элемент2)
	
	Если Элемент1.Использование = Элемент2.Использование
		И ТипЗнч(Элемент1) = ТипЗнч(Элемент2) Тогда
		
		Если ТипЗнч(Элемент1) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Если Элемент1.ВидСравнения <> Элемент2.ВидСравнения
				ИЛИ Элемент1.ЛевоеЗначение <> Элемент2.ЛевоеЗначение
				ИЛИ Элемент1.ПравоеЗначение <> Элемент2.ПравоеЗначение Тогда
				Возврат Ложь;
			КонецЕсли;
		Иначе
			
			ЧислоЭлементов = Элемент1.Элементы.Количество();
			Если Элемент1.ТипГруппы <> Элемент2.ТипГруппы
				ИЛИ ЧислоЭлементов <> Элемент2.Элементы.Количество() Тогда
				Возврат Ложь;
			КонецЕсли;
			
			Для Индекс = 0 По ЧислоЭлементов - 1 Цикл
				ПодчиненныйЭлемент1 = Элемент1.Элементы[Индекс];
				ПодчиненныйЭлемент2 = Элемент2.Элементы[Индекс];
				ЭлементыРавны = СравнитьЭлементыОтбора(ПодчиненныйЭлемент1, ПодчиненныйЭлемент2);
				
				Если Не ЭлементыРавны Тогда
					Возврат ЭлементыРавны;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция СтатусыНеРаспакованныхТранспортныхСообщенийБЭД()
	
	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыТранспортныхСообщенийБЭД.Неизвестный"));
	Возврат МассивСтатусов;
		
КонецФункции

&НаКлиенте
Процедура ПолучитьОтправитьОбновить()
	Перем Оповещение;
	Элементы.Обновить.Доступность = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ОтправкаПолучениеЭДЗавершение", ЭтотОбъект);
	ИнтерфейсДокументовЭДОКлиент.ОтправитьПолучитьДокументыЭДО(ЭтотОбъект, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиНаПечать()
	
	ЭлектронныеДокументы = Элементы[ТекущийРаздел].ВыделенныеСтроки;
	
	Если Не ЗначениеЗаполнено(ЭлектронныеДокументы) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Для печати необходимо выбрать электронный документ'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыВизуализации = ИнтерфейсДокументовЭДОВызовСервера.НовыеПараметрыВизуализацииДокумента();
	ПараметрыВизуализации.ВыводитьБанковскиеРеквизиты = Ложь;
	ПараметрыВизуализации.ВыводитьДопДанные = Ложь;
	ПараметрыВизуализации.ВыводитьКопияВерна = Ложь;
	
	ИнтерфейсДокументовЭДОКлиент.ОткрытьФормуПечатиЭлектронныхДокументов(ЭлектронныеДокументы, ПараметрыВизуализации);
	
КонецПроцедуры

&НаКлиенте
Функция СобытиеОбновитьСписки(Знач ИмяСобытия)
	
	Возврат ИмяСобытия = ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияСостоянияЭДО()
		Или ИмяСобытия = "ОбновитьСостояниеПриглашений"
		Или ИмяСобытия = ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияТекущихДелЭДО()
		Или ИмяСобытия = "ПрочтениеОбновитьСписок";
		
КонецФункции

&НаКлиенте
Процедура УстановитьБлокировкуЭлементов(СписокНаименованийЭлементов)
	Для Каждого НаименованиеЭлемента Из СписокНаименованийЭлементов Цикл
		Элементы[НаименованиеЭлемента].Доступность = Ложь;
		ЗаблокированныеЭлементыФормы.Добавить(НаименованиеЭлемента);	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьБлокировкуЭлементов(СписокНаименованийЭлементов)
	Для Каждого НаименованиеЭлемента Из СписокНаименованийЭлементов Цикл
		Элементы[НаименованиеЭлемента].Доступность = Истина;
		ЗаблокированныйЭлемент = ЗаблокированныеЭлементыФормы.НайтиПоЗначению(НаименованиеЭлемента);
		Если ЗаблокированныйЭлемент <> Неопределено Тогда
			ЗаблокированныеЭлементыФормы.Удалить(ЗаблокированныйЭлемент);	
		КонецЕсли;			
	КонецЦикла;
КонецПроцедуры 

&НаСервереБезКонтекста
Функция ВыполнениеДействийПоЭДОДоступноДляПакетов(ПакетыДокументов)

	Результат = Истина;
	
	Для Каждого ИдентификаторПакета Из ПакетыДокументов Цикл
		СостоянияДокументовПакета = ПакетыДокументовЭДО.СостоянияДокументовПакета(ИдентификаторПакета);
		СостояниеПакета = ПакетыДокументовЭДО.СостояниеПакета(СостоянияДокументовПакета);
		Если Не ЗначениеЗаполнено(СостояниеПакета) Тогда
			Результат = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОставитьДляОбработкиТолькоТекущийДокумент(ОбъектыДействий)
	
	ОбъектыДействий.ПакетыДокументов.Очистить();
	ОбъектыДействий.ЭлектронныеДокументы.Добавить(ТекущийЭлектронныйДокумент);
	
КонецПроцедуры

&НаКлиенте
Функция ВключенаГруппировкаСтрок(ТекущиеДанные)
	Возврат ТекущиеДанные.Свойство("ГруппировкаСтроки") 
		И ЗначениеЗаполнено(ТекущиеДанные.ГруппировкаСтроки);
КонецФункции

#КонецОбласти

#Область ЗадолженностьЗаТрафикЭДО

&НаКлиентеНаСервереБезКонтекста
Процедура ПоказатьУведомленияПоЗадолженности(Форма, Задолженность)
	
	Форма.Элементы.ПоказатьСведенияОЗадолженности.Видимость = Задолженность.Количество() > 0;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СведенияОЗадолженности()
	
	Возврат ИнтерфейсДокументовЭДО.СведенияОЗадолженности();
	
КонецФункции

&НаСервере
Процедура ПоказатьУведомлениеПоТекущемуТрафику()
	
	Элементы.ГруппаОтправленоЗаМесяц.Видимость = ИспользуетсяПоказТрафикаЭлектронныхДокументов;
	
	Если Не ИспользуетсяПоказТрафикаЭлектронныхДокументов Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийТрафик = ИнтерфейсДокументовЭДО.ТекущийТрафик();
	Итог = ТекущийТрафик.Итог("РасчетныйТрафикОтправленныхЗаМесяц");
	
	ТекстЗаголовка = СтрШаблон("%1", Итог);
	Элементы.ДекорацияОтправленоМесяц.Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеПоИспользованиюПоказаТрафикаЭлектронныхДокументов()
	
	ИспользуетсяПоказТрафикаЭлектронныхДокументов = ИнтерфейсДокументовЭДО.ИспользуетсяПоказТрафикаЭлектронныхДокументов();
	Элементы.ИспользуетсяПоказТрафикаЭлектронныхДокументов.Пометка = ИспользуетсяПоказТрафикаЭлектронныхДокументов;
	ПоказатьУведомлениеПоТекущемуТрафику();
	
КонецПроцедуры

&НаСервере
Процедура ИспользуетсяПоказТрафикаЭлектронныхДокументовНаСервере()

	ИспользуетсяПоказТрафикаЭлектронныхДокументов = Не ИспользуетсяПоказТрафикаЭлектронныхДокументов;
	ИнтерфейсДокументовЭДОВызовСервера.СохранитьИспользованиеПоказаТрафикаЭлектронныхДокументовПоУмолчанию(
		ИспользуетсяПоказТрафикаЭлектронныхДокументов);
	Элементы.ИспользуетсяПоказТрафикаЭлектронныхДокументов.Пометка = ИспользуетсяПоказТрафикаЭлектронныхДокументов;
	ПоказатьУведомлениеПоТекущемуТрафику();

КонецПроцедуры

#КонецОбласти // ЗадолженностьЗаТрафикЭДО

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.ИсходящиеЭлектронныеДокументы);
КонецПроцедуры

&НаКлиенте
Процедура ПанельНавигацииДокументСформирован(Элемент)
	РазделыТекущихДелЭДО = РазделыТекущихДелЭДО();
	Если ТекущийРаздел = РазделыТекущихДелЭДО.ВходящиеЭлектронныеДокументы Тогда
		ОчиститьКнопки(Элемент.Документ);
		Элемент.Документ.getElementById("inbox-button").classList.add("checked");
	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.ИсходящиеЭлектронныеДокументы Тогда
		ОчиститьКнопки(Элемент.Документ);
		Элемент.Документ.getElementById("sent-button").classList.add("checked");
	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.Черновики Тогда 
		ОчиститьКнопки(Элемент.Документ);
		Элемент.Документ.getElementById("draft-button").classList.add("checked");
	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.Приглашения Тогда 
		ОчиститьКнопки(Элемент.Документ);
		Элемент.Документ.getElementById("invitation-button").classList.add("checked");
	ИначеЕсли ТекущийРаздел = РазделыТекущихДелЭДО.Ошибки Тогда 
		ОчиститьКнопки(Элемент.Документ);
		Элемент.Документ.getElementById("error-button").classList.add("checked");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьКнопки(Документ)
	Документ.getElementById("sent-button").classList.remove("checked");
      Документ.getElementById("inbox-button").classList.remove("checked");
      Документ.getElementById("draft-button").classList.remove("checked");
      Документ.getElementById("invitation-button").classList.remove("checked");
      Документ.getElementById("error-button").classList.remove("checked");
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти // ПодключаемыеКоманды

#КонецОбласти
