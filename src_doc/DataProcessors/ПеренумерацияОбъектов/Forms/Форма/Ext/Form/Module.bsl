
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КартинкаБизнесПроцесса = 23;
	КартинкаСправочника = 3;
	КартинкаДокумента = 7;
	
	ДлинаПрефикса = 3;
	
	// Перенумерация файлов и версий файлов
	
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Файлы'"),
		КартинкаСправочника, "Справочник.Файлы", "Код", 50);
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Версии файлов'"),
		КартинкаСправочника, "Справочник.ВерсииФайлов", "Код", 50);
		
	
	// Перенумерация шаблонов процессов
	
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны исполнений'"),
		КартинкаСправочника, "Справочник.ШаблоныИсполнения", "Код", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны комплексных бизнес процессов'"),
		КартинкаСправочника, "Справочник.ШаблоныКомплексныхБизнесПроцессов", "Код", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны ознакомлений'"),
		КартинкаСправочника, "Справочник.ШаблоныОзнакомления","Код", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны приглашений'"),
		КартинкаСправочника, "Справочник.ШаблоныПриглашения", "Код", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны рассмотрений'"),
		КартинкаСправочника, "Справочник.ШаблоныРассмотрения", "Код", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны регистраций'"),
		КартинкаСправочника, "Справочник.ШаблоныРегистрации", "Код", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны согласований'"),
		КартинкаСправочника, "Справочник.ШаблоныСогласования", "Код", 50);
	
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны подписания'"),
		КартинкаСправочника, "Справочник.ШаблоныПодписания", "Код", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Шаблоны утверждений'"),
		КартинкаСправочника, "Справочник.ШаблоныУтверждения", "Код", 50);
		
	// Перенумерация процессов
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Исполнения'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.Исполнение", "Номер", 45);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Комплексные процессы'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.КомплексныйПроцесс", "Номер", 45);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Ознакомления'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.Ознакомление", "Номер", 45);
	
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Приглашения'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.Приглашение", "Номер", 45);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Рассмотрения'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.Рассмотрение", "Номер", 45);
	
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Регистрации'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.Регистрация", "Номер", 45);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Решения вопросов выполнения задач'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.РешениеВопросовВыполненияЗадач", "Номер", 45);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Согласования'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.Согласование", "Номер", 45);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Утверждения'"),
		КартинкаБизнесПроцесса, "БизнесПроцесс.Утверждение", "Номер", 45);
		
	// Перенумерация писем
	
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Входящие письма'"),
		КартинкаДокумента, "Документ.ВходящееПисьмо", "Номер", 50);
		
	ДобавитьОбъектДляПеренумерации(НСтр("ru = 'Исходящие письма'"),
		КартинкаДокумента, "Документ.ИсходящееПисьмо", "Номер", 50);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьПометкуВсемОбъектам(Команда)
	
	Для Каждого Стр ИЗ СписокОбъектов Цикл
		Стр.Пометка = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометкуСоВсехОбъектов(Команда)
	
	Для Каждого Стр ИЗ СписокОбъектов Цикл
		Стр.Пометка = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПеренумерациюОбъектов(Команда)
	
	Если ПеренумероватьОбъекты() Тогда
		ТекстПредупреждения = НСтр("ru = 'Перенумерация объектов завершена.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Не выбрано ни одного объекта для перенумерации.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПеренумероватьОбъекты()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Попытка
		УстановитьМонопольныйРежим(Истина);
	Исключение
		
		ТекстИсключения = НСтр("ru = 'Не удалось установить монопольный режим.
			|С информационной базой работают другие пользователи.
			|Перенумерация объектов выполняется только в монопольном режиме!'");
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	ДокументооборотПраваДоступа.УстановитьИспользованиеПравДоступа(Ложь);
	
	Запрос = Новый Запрос;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Пометка", Истина);
	
	НайденныеСтроки = СписокОбъектов.НайтиСтроки(Отбор);
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого Стр ИЗ НайденныеСтроки Цикл
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Таблица.Ссылка,
			|	&ПолеНумерации
			|ИЗ
			|	&Таблица КАК Таблица";
			
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПолеНумерации", "Таблица." + Стр.ПолеНумерации);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Таблица", Стр.ТаблицаВЗапросе);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Номер = СокрЛП(Выборка[Стр.ПолеНумерации]);
			
			Если СтрДлина(Номер) = Стр.ДлинаНомераКода Тогда
				Продолжить;
			КонецЕсли;
			
			ОбъектДляИзменения = Выборка.Ссылка.ПолучитьОбъект();
			
			ПрефиксОбъекта = Лев(Номер, ДлинаПрефикса);
			
			ДлинаНомера = СтрДлина(Номер) - ДлинаПрефикса;
			
			НомерОбъекта = Прав(Номер, ДлинаНомера);
			
			НомерОбъекта = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(
				НомерОбъекта, Стр.ДлинаНомераКода - ДлинаПрефикса, "0", "Слева");
			
			ОбъектДляИзменения[Стр.ПолеНумерации] = ПрефиксОбъекта + НомерОбъекта;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ОбъектДляИзменения, Истина);
			
		КонецЦикла;
		
		СписокОбъектов.Удалить(Стр);
		
	КонецЦикла;
	
	ДокументооборотПраваДоступа.УстановитьИспользованиеПравДоступа(Истина);
	
	УстановитьМонопольныйРежим(Ложь);
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ДобавитьОбъектДляПеренумерации(Представление, Картинка, ТаблицаВЗапросе, ПолеНумерации, ДлинаНомераКода)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	&Поле1
		|ИЗ
		|	&Таблица КАК Таблица
		|ГДЕ
		|	НЕ &Поле1 ПОДОБНО &ШаблонНомера
		|	ИЛИ &Поле1 ПОДОБНО &ШаблонНомераСПробелом";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Поле1", "Таблица." + ПолеНумерации);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Таблица", ТаблицаВЗапросе);
	
	ШаблонНомера = "";
	Для Индекс = 1 По ДлинаНомераКода Цикл
		ШаблонНомера = ШаблонНомера + "_";
	КонецЦикла;
	
	ШаблонНомераСПробелом = Лев(ШаблонНомера, ДлинаНомераКода-1) + " ";
	
	Запрос.УстановитьПараметр("ШаблонНомера", ШаблонНомера);
	Запрос.УстановитьПараметр("ШаблонНомераСПробелом", ШаблонНомераСПробелом);
	
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	СтрОбъект = СписокОбъектов.Добавить();
	СтрОбъект.Представление = Представление;
	СтрОбъект.Картинка = Картинка;
	СтрОбъект.ТаблицаВЗапросе = ТаблицаВЗапросе;
	СтрОбъект.ПолеНумерации = ПолеНумерации;
	СтрОбъект.ДлинаНомераКода = ДлинаНомераКода;
	
КонецПроцедуры

#КонецОбласти
