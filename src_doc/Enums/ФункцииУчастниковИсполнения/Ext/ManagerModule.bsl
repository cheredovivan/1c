#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает успешный результат.
//
// Параметры:
//  ФункцияУчастника - ПеречислениеСсылка.ФункцииУчастниковИсполнения - Функция участника.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.РезультатыИсполнения, ПеречислениеСсылка.РезультатыИсполнения - Успешный результат.
//
Функция УспешныйРезультат(ФункцияУчастника) Экспорт
	
	Если ФункцияУчастника = Исполнитель Тогда
		УспешныйРезультат = Перечисления.РезультатыИсполнения.Исполнено;
	ИначеЕсли ФункцияУчастника = Рассматривающий Тогда
		УспешныйРезультат = Перечисления.РезультатыРассмотрения.Рассмотрено;
	Иначе
		УспешныйРезультат = Перечисления.РезультатыИсполнения.Исполнено;
	КонецЕсли;
	
	Возврат УспешныйРезультат;
	
КонецФункции

// Возвращает состояние выполняется.
//
// Параметры:
//  ФункцияУчастника - ПеречислениеСсылка.ФункцииУчастниковИсполнения - Функция участника.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументов - Состояние выполняется.
//
Функция СостоянияВыполняется(ФункцияУчастника) Экспорт
	
	Если ФункцияУчастника = Исполнитель Тогда
		СостоянияВыполняется = Перечисления.СостоянияДокументов.НаИсполнении;
	ИначеЕсли ФункцияУчастника = Рассматривающий Тогда
		СостоянияВыполняется = Перечисления.СостоянияДокументов.НаРассмотрении;
	Иначе
		СостоянияВыполняется = Перечисления.СостоянияДокументов.НаИсполнении;
	КонецЕсли;
	
	Возврат СостоянияВыполняется;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ДляПравилЗагрузки") И Параметры.ДляПравилЗагрузки Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.Добавить(Исполнитель);
		ДанныеВыбора.Добавить(Рассматривающий);
		ДанныеВыбора.Добавить(ОбрабатывающийРезультат);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ДляПравилЗагрузки") И Параметры.ДляПравилЗагрузки Тогда
		
		МассивДоступныхЗначений = Новый Массив;
		МассивДоступныхЗначений.Добавить(Исполнитель);
		МассивДоступныхЗначений.Добавить(Рассматривающий);
		МассивДоступныхЗначений.Добавить(ОбрабатывающийРезультат);
		Параметры.Отбор.Вставить("Ссылка", МассивДоступныхЗначений);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли