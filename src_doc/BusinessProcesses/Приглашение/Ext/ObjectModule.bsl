#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет бизнес-процесс на основании шаблона бизнес-процесса.
//
// Параметры
//  ШаблонБизнесПроцесса  - шаблон бизнес-процесса
//
Процедура ЗаполнитьПоШаблону(ШаблонБизнесПроцесса) Экспорт
	
	Если ШаблонБизнесПроцесса = Справочники.ШаблоныПриглашения.ПоУмолчанию Тогда
		// Не заполняем шаблон в процессе, если это шаблон по умолчанию.
		// Считаем, что процесс создан с нуля.
	ИначеЕсли ШаблонБизнесПроцесса.ШаблонВКомплексномПроцессе 
		И ЗначениеЗаполнено(ШаблонБизнесПроцесса.ИсходныйШаблон) Тогда
		Шаблон = ШаблонБизнесПроцесса.ИсходныйШаблон;
	ИначеЕсли НЕ ШаблонБизнесПроцесса.ШаблонВКомплексномПроцессе Тогда
		Шаблон = ШаблонБизнесПроцесса;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ШаблонБизнесПроцесса.НаименованиеБизнесПроцесса) Тогда 
		Наименование = ШаблонБизнесПроцесса.НаименованиеБизнесПроцесса;
		НаименованиеСПредметами = МультипредметностьКлиентСервер.ПолучитьНаименованиеСПредметами(СокрЛП(Наименование), Предметы);
		Если ЗначениеЗаполнено(НаименованиеСПредметами) И ШаблонБизнесПроцесса.ДобавлятьНаименованиеПредмета Тогда
			Наименование = НаименованиеСПредметами;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ШаблонБизнесПроцесса.Описание) Тогда 
		Описание = ШаблонБизнесПроцесса.Описание;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ШаблонБизнесПроцесса.Важность) Тогда 
		Важность = ШаблонБизнесПроцесса.Важность;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ШаблонБизнесПроцесса.Автор) Тогда 
		Автор = ШаблонБизнесПроцесса.Автор;
	КонецЕсли;
	
	// заполнение исполнителей
	Если ШаблонБизнесПроцесса.Исполнители.Количество() > 0 Тогда 
		Исполнители.Очистить();
	КонецЕсли;
	
	Для Каждого Строка Из ШаблонБизнесПроцесса.Исполнители Цикл
		
		Если ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.АвтоподстановкиДляПроцессов")
			И ЗначениеЗаполнено(Строка.Исполнитель) Тогда
			
			АвтоподстановкаИсполнитель = ШаблоныБизнесПроцессов.ПолучитьЗначениеАвтоподстановки(
				Строка.Исполнитель, ЭтотОбъект);
		
			Если ТипЗнч(АвтоподстановкаИсполнитель) = Тип("СправочникСсылка.Сотрудники") 
				Или ТипЗнч(АвтоподстановкаИсполнитель) = Тип("СправочникСсылка.Пользователи") Тогда 
				
				Если Исполнители.Найти(АвтоподстановкаИсполнитель, "Исполнитель") = Неопределено Тогда 
					НоваяСтрока = Исполнители.Добавить();
					НоваяСтрока.Исполнитель = АвтоподстановкаИсполнитель;
					НоваяСтрока.ЯвкаОбязательна = Строка.ЯвкаОбязательна;
					
					НоваяСтрока.ВариантУстановкиСрокаИсполнения = Строка.ВариантУстановкиСрокаИсполнения;
					НоваяСтрока.СрокИсполнения = Строка.СрокИсполнения;
					НоваяСтрока.СрокИсполненияДни = Строка.СрокИсполненияДни;
					НоваяСтрока.СрокИсполненияЧасы = Строка.СрокИсполненияЧасы;
					НоваяСтрока.СрокИсполненияМинуты = Строка.СрокИсполненияМинуты;
					
				КонецЕсли;	
				
			ИначеЕсли ТипЗнч(АвтоподстановкаИсполнитель) = Тип("СправочникСсылка.ПолныеРоли") Тогда 
				
				СтруктураОтбора = Новый Структура("Исполнитель",
					АвтоподстановкаИсполнитель);
					
				Если Исполнители.НайтиСтроки(СтруктураОтбора).Количество() = 0 Тогда 
					НоваяСтрока = Исполнители.Добавить();
					НоваяСтрока.Исполнитель = АвтоподстановкаИсполнитель;
					НоваяСтрока.ЯвкаОбязательна = Строка.ЯвкаОбязательна;
					
					НоваяСтрока.ВариантУстановкиСрокаИсполнения = Строка.ВариантУстановкиСрокаИсполнения;
					НоваяСтрока.СрокИсполнения = Строка.СрокИсполнения;
					НоваяСтрока.СрокИсполненияДни = Строка.СрокИсполненияДни;
					НоваяСтрока.СрокИсполненияЧасы = Строка.СрокИсполненияЧасы;
					НоваяСтрока.СрокИсполненияМинуты = Строка.СрокИсполненияМинуты;
					
				КонецЕсли;
				
			ИначеЕсли ТипЗнч(АвтоподстановкаИсполнитель) = Тип("Структура") Тогда 
				
				СтруктураОтбора = Новый Структура("Исполнитель",
					АвтоподстановкаИсполнитель.РольИсполнителя);
					
				Если Исполнители.НайтиСтроки(СтруктураОтбора).Количество() = 0 Тогда 
					НоваяСтрока = Исполнители.Добавить();
					НоваяСтрока.Исполнитель = АвтоподстановкаИсполнитель.РольИсполнителя;
					НоваяСтрока.ЯвкаОбязательна = Строка.ЯвкаОбязательна;
					
					НоваяСтрока.ВариантУстановкиСрокаИсполнения = Строка.ВариантУстановкиСрокаИсполнения;
					НоваяСтрока.СрокИсполнения = Строка.СрокИсполнения;
					НоваяСтрока.СрокИсполненияДни = Строка.СрокИсполненияДни;
					НоваяСтрока.СрокИсполненияЧасы = Строка.СрокИсполненияЧасы;
					НоваяСтрока.СрокИсполненияМинуты = Строка.СрокИсполненияМинуты;
					
				КонецЕсли;	
				
			ИначеЕсли ТипЗнч(АвтоподстановкаИсполнитель) = Тип("Массив") Тогда 
				
				Для Каждого ЭлементМассива Из АвтоподстановкаИсполнитель Цикл
					
					Если (ТипЗнч(ЭлементМассива) = Тип("СправочникСсылка.Сотрудники")
						Или ТипЗнч(ЭлементМассива) = Тип("СправочникСсылка.Пользователи"))
						И ЗначениеЗаполнено(ЭлементМассива) Тогда 
						
						Если Исполнители.Найти(ЭлементМассива, "Исполнитель") = Неопределено Тогда 
							НоваяСтрока = Исполнители.Добавить();
							НоваяСтрока.Исполнитель = ЭлементМассива;
							НоваяСтрока.ЯвкаОбязательна = Строка.ЯвкаОбязательна;
							
							НоваяСтрока.ВариантУстановкиСрокаИсполнения = Строка.ВариантУстановкиСрокаИсполнения;
							НоваяСтрока.СрокИсполнения = Строка.СрокИсполнения;
							НоваяСтрока.СрокИсполненияДни = Строка.СрокИсполненияДни;
							НоваяСтрока.СрокИсполненияЧасы = Строка.СрокИсполненияЧасы;
							НоваяСтрока.СрокИсполненияМинуты = Строка.СрокИсполненияМинуты;
							
						КонецЕсли;
						
					ИначеЕсли ТипЗнч(ЭлементМассива) = Тип("СправочникСсылка.ПолныеРоли") И ЗначениеЗаполнено(ЭлементМассива)	Тогда 
						
						ПараметрыОтбора = Новый Структура("Исполнитель",
							ЭлементМассива);
					
						Если Исполнители.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда 
							НоваяСтрока = Исполнители.Добавить();
							НоваяСтрока.Исполнитель = ЭлементМассива;
							НоваяСтрока.ЯвкаОбязательна = Строка.ЯвкаОбязательна;
							
							НоваяСтрока.ВариантУстановкиСрокаИсполнения = Строка.ВариантУстановкиСрокаИсполнения;
							НоваяСтрока.СрокИсполнения = Строка.СрокИсполнения;
							НоваяСтрока.СрокИсполненияДни = Строка.СрокИсполненияДни;
							НоваяСтрока.СрокИсполненияЧасы = Строка.СрокИсполненияЧасы;
							НоваяСтрока.СрокИсполненияМинуты = Строка.СрокИсполненияМинуты;
						КонецЕсли;
						
					ИначеЕсли ТипЗнч(ЭлементМассива) = Тип("Структура") Тогда 	
						
						ПараметрыОтбора = Новый Структура("Исполнитель",
							ЭлементМассива.РольИсполнителя);
					
						Если Исполнители.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда 
							НоваяСтрока = Исполнители.Добавить();
							НоваяСтрока.Исполнитель = ЭлементМассива.РольИсполнителя;
							НоваяСтрока.ЯвкаОбязательна = Строка.ЯвкаОбязательна;
							
							НоваяСтрока.ВариантУстановкиСрокаИсполнения = Строка.ВариантУстановкиСрокаИсполнения;
							НоваяСтрока.СрокИсполнения = Строка.СрокИсполнения;
							НоваяСтрока.СрокИсполненияДни = Строка.СрокИсполненияДни;
							НоваяСтрока.СрокИсполненияЧасы = Строка.СрокИсполненияЧасы;
							НоваяСтрока.СрокИсполненияМинуты = Строка.СрокИсполненияМинуты;
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;	
		Иначе
			СтруктураОтбора = Новый Структура("Исполнитель",
				Строка.Исполнитель);
		
			НайденныеСтроки = Исполнители.НайтиСтроки(СтруктураОтбора);
			Если НайденныеСтроки.Количество() = 0 Тогда
				НоваяСтрока = Исполнители.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;	
	
	// Срок обработки результатов
	Если ЗначениеЗаполнено(ШаблонБизнесПроцесса.ВариантУстановкиСрокаОбработкиРезультатов) Тогда
		ВариантУстановкиСрокаОбработкиРезультатов = ШаблонБизнесПроцесса.ВариантУстановкиСрокаОбработкиРезультатов;
	КонецЕсли;
	Если ЗначениеЗаполнено(ШаблонБизнесПроцесса.СрокОбработкиРезультатов) Тогда
		СрокОбработкиРезультатов = ШаблонБизнесПроцесса.СрокОбработкиРезультатов;
	КонецЕсли;
	Если ЗначениеЗаполнено(ШаблонБизнесПроцесса.СрокОбработкиРезультатовДни) Тогда
		СрокОбработкиРезультатовДни = ШаблонБизнесПроцесса.СрокОбработкиРезультатовДни;
	КонецЕсли;
	Если ЗначениеЗаполнено(ШаблонБизнесПроцесса.СрокОбработкиРезультатовЧасы) Тогда
		СрокОбработкиРезультатовЧасы = ШаблонБизнесПроцесса.СрокОбработкиРезультатовЧасы;
	КонецЕсли;
	Если ЗначениеЗаполнено(ШаблонБизнесПроцесса.СрокОбработкиРезультатовМинуты) Тогда
		СрокОбработкиРезультатовМинуты = ШаблонБизнесПроцесса.СрокОбработкиРезультатовМинуты;
	КонецЕсли;
	
	// Срок исполнения процесса
	СрокИсполненияПроцесса = ШаблонБизнесПроцесса.СрокИсполненияПроцесса;
	
	// Количество итераций
	Если ЗначениеЗаполнено(ШаблонБизнесПроцесса.КоличествоИтераций) Тогда
		КоличествоИтераций = ШаблонБизнесПроцесса.КоличествоИтераций;
	КонецЕсли;
		
	// трудозатраты
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		ТрудозатратыПланИсполнителя = ШаблонБизнесПроцесса.ТрудозатратыПланИсполнителя;
		ТрудозатратыПланАвтора = ШаблонБизнесПроцесса.ТрудозатратыПланАвтора;
	КонецЕсли;	
	
	РаботаСБизнесПроцессамиВызовСервера.СкопироватьЗначенияДопРеквизитов(ШаблонБизнесПроцесса, ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ШаблонДляОтложенногоСтарта", ШаблонБизнесПроцесса);
	
КонецПроцедуры	

// Заполняет бизнес-процесс на основании шаблона бизнес-процесса, предмета и автора.
//
// Параметры
//  ШаблонБизнесПроцесса  - шаблон бизнес-процесса
//  Предмет - предмет бизнес-процесса
//  Автор  - автор
//
Процедура ЗаполнитьПоШаблонуИПредмету(ШаблонБизнесПроцесса, ПредметСобытия, АвторСобытия) Экспорт
	
	Мультипредметность.ЗаполнитьПредметыПроцессаПоШаблону(ШаблонБизнесПроцесса, ЭтотОбъект);
	Мультипредметность.ПередатьПредметыПроцессу(ЭтотОбъект, ПредметСобытия, Ложь, Истина);
	ЗаполнитьПоШаблону(ШаблонБизнесПроцесса);
	
	Проект = МультипредметностьПереопределяемый.ПолучитьОсновнойПроектПоПредметам(ПредметСобытия);
	
	Дата = ТекущаяДатаСеанса();
	Автор = АвторСобытия;
	
КонецПроцедуры

// Заполняет бизнес-процесс на основании проектной задачи
//
Процедура ЗаполнитьПоПроектнойЗадаче(ДанныеЗаполнения) Экспорт 
	
	Проект = ДанныеЗаполнения.Владелец;
	ПроектнаяЗадача = ДанныеЗаполнения;
	
	ДобавлятьНаименованиеПредмета = РаботаСБизнесПроцессами.ДобавлятьНаименованиеПредмета(ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Наименование)
		Или Наименование = НаименованиеПроцессаПоУмолчанию(ДобавлятьНаименованиеПредмета) Тогда
		
		Наименование = ПроектнаяЗадача.Наименование;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Описание) Тогда 
		Описание = ПроектнаяЗадача.Описание;
	КонецЕсли;
	
	Если Предметы.Количество() = 0 Тогда 
		Предмет = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПроектнаяЗадача, "Предмет");
		
		Если Предмет <> Неопределено И Предметы.Найти(Предмет,"Предмет") = Неопределено Тогда
			СтрокаПредметов = Предметы.Добавить();
			СтрокаПредметов.РольПредмета = Перечисления.РолиПредметов.Основной;
			СтрокаПредметов.ИмяПредмета =  МультипредметностьВызовСервера.ПолучитьСсылкуНаИмяПредметаПоСсылкеНаПредмет(
				Предмет, Предметы.ВыгрузитьКолонку("ИмяПредмета"));
			СтрокаПредметов.Предмет = Предмет;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеПроектнойЗадачи = РаботаСПроектами.ПолучитьСрокиПроектнойЗадачи(ПроектнаяЗадача);
	СрокИсполненияУчастника = ДанныеПроектнойЗадачи.ТекущийПланОкончание;
	КоличествоИтераций = 1;
	
	Если СрокОбработкиРезультатов = Дата(1,1,1)
		И СрокОбработкиРезультатовДни = 0
		И СрокОбработкиРезультатовЧасы = 0
		И СрокОбработкиРезультатовМинуты = 0 Тогда
		
		СрокОбработкиРезультатов = СрокИсполненияУчастника;
		ВариантУстановкиСрокаОбработкиРезультатов = Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
	КонецЕсли;
	
	Если Исполнители.Количество() = 0 Тогда 
		Для Каждого СтрокаИсполнитель Из ПроектнаяЗадача.Исполнители Цикл 
			Если ТипЗнч(СтрокаИсполнитель.Исполнитель) = Тип("СправочникСсылка.Пользователи") 
			 Или ТипЗнч(СтрокаИсполнитель.Исполнитель) = Тип("СправочникСсылка.ПолныеРоли")
			 Или ТипЗнч(СтрокаИсполнитель.Исполнитель) = Тип("СправочникСсылка.Сотрудники") Тогда 
				НоваяСтрока = Исполнители.Добавить();
				НоваяСтрока.Исполнитель = СтрокаИсполнитель.Исполнитель;
				НоваяСтрока.СрокИсполнения = СрокИсполненияУчастника;
				НоваяСтрока.ВариантУстановкиСрокаИсполнения = Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
	
	ТрудозатратыПланИсполнителя = РаботаСПроектами.МинимальныеТрудозатратыИсполнителей(ПроектнаяЗадача.Исполнители);
	
КонецПроцедуры

// Возвращает признак наличия метода ИзменитьРеквизитыНевыполненныхЗадач
//
Функция ЕстьМетодИзменитьРеквизитыНевыполненныхЗадач() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Обновляет значения реквизитов невыполненных задач 
// при изменении реквизитов бизнес-процесса.
//
Процедура ИзменитьРеквизитыНевыполненныхЗадач(СтарыеУчастникиПроцесса, ПараметрыЗаписи) Экспорт 

	УстановитьПривилегированныйРежим(Истина);
	
	СтарыеИсполнители = СтарыеУчастникиПроцесса.Исполнители;
	
	ДобавлятьНаименованиеПредмета = РаботаСБизнесПроцессами.ДобавлятьНаименованиеПредмета(ЭтотОбъект);
	
	АвторИзменения = Неопределено;
	Если Не ПараметрыЗаписи.Свойство("АвторИзменения", АвторИзменения) Тогда
		АвторИзменения = ПользователиДокументооборот.ОлицетворяемыйПользователь();
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		ПараметрыСеанса.ОтключитьОбновлениеЗадач = Истина;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Задачи.Ссылка,
		|	Задачи.ТочкаМаршрута
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК Задачи
		|ГДЕ
		|	Задачи.БизнесПроцесс = &БизнесПроцесс
		|	И Задачи.ПометкаУдаления = ЛОЖЬ
		|	И Задачи.Выполнена = ЛОЖЬ";
		Запрос.УстановитьПараметр("БизнесПроцесс", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		
		ЕстьНевыполненныеЗадачиВТочкеМаршрутаПригласить = Ложь;
		
		Пока Выборка.Следующий() Цикл
			
			СрокИсполненияИзменен = Ложь;
			
			ЗадачаОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ЗадачаОбъект.ДополнительныеСвойства.Вставить("АвторИзменения", АвторИзменения);
			МассивПредметов = МультипредметностьКлиентСервер.ПолучитьМассивСтруктурПредметовОбъекта(ЭтотОбъект);
			СтрокаПредметов = МультипредметностьКлиентСервер.ПредметыСтрокой(МассивПредметов, Истина, Ложь);
			
			Если ЗадачаОбъект.ТочкаМаршрута = БизнесПроцессы.Приглашение.ТочкиМаршрута.Ознакомиться
				Или ЗадачаОбъект.ТочкаМаршрута = БизнесПроцессы.Приглашение.ТочкиМаршрута.Оповестить Тогда
				
				Если СтрНачинаетсяС(НаименованиеПроцессаПоУмолчанию(ДобавлятьНаименованиеПредмета), Наименование)
					И ЗначениеЗаполнено(СтрокаПредметов) Тогда
					
					НаименованиеЗадачи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Ознакомиться с результатом приглашения %1'",
							ЛокализацияКонфигурации.КодЯзыкаИсполнителяЗадачи(ЗадачаОбъект)),
						СтрокаПредметов);
				Иначе
					НаименованиеЗадачи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Ознакомиться с результатом приглашения: %1'",
							ЛокализацияКонфигурации.КодЯзыкаИсполнителяЗадачи(ЗадачаОбъект)),
						Наименование);
				КонецЕсли;
			Иначе
				НаименованиеЗадачи = Наименование;
				
				ЕстьНевыполненныеЗадачиВТочкеМаршрутаПригласить = Истина;
				
			КонецЕсли;
			
			Если ЗадачаОбъект.ТочкаМаршрута = БизнесПроцессы.Приглашение.ТочкиМаршрута.Пригласить Тогда
				
				СтрИсполнителя = Исполнители.Найти(ЗадачаОбъект.Ссылка, "ЗадачаИсполнителя");
				
				Если СтрИсполнителя <> Неопределено Тогда
					
					СтарыйСрокЗадачи = ЗадачаОбъект.СрокИсполнения;
					
					СрокиИсполненияПроцессов.ЗаполнитьСрокИсполненияЗадачи(
						ЗадачаОбъект,
						СтрИсполнителя.ВариантУстановкиСрокаИсполнения,
						СтрИсполнителя.СрокИсполнения,
						СтрИсполнителя.СрокИсполненияДни,
						СтрИсполнителя.СрокИсполненияЧасы,
						СтрИсполнителя.СрокИсполненияМинуты);
					
					Если СтарыйСрокЗадачи <> ЗадачаОбъект.СрокИсполнения Тогда
						СрокИсполненияИзменен = Истина;
					КонецЕсли;
					
				КонецЕсли;
				
			ИначеЕсли ЗадачаОбъект.ТочкаМаршрута = БизнесПроцессы.Приглашение.ТочкиМаршрута.Ознакомиться Тогда
				
				СтарыйСрокЗадачи = ЗадачаОбъект.СрокИсполнения;
				
				СрокиИсполненияПроцессов.ЗаполнитьСрокИсполненияЗадачи(
					ЗадачаОбъект,
					ВариантУстановкиСрокаОбработкиРезультатов,
					СрокОбработкиРезультатов,
					СрокОбработкиРезультатовДни,
					СрокОбработкиРезультатовЧасы,
					СрокОбработкиРезультатовМинуты);
				
				Если СтарыйСрокЗадачи <> ЗадачаОбъект.СрокИсполнения Тогда
					СрокИсполненияИзменен = Истина;
				КонецЕсли;
				
			Иначе
				ЗадачаОбъект.СрокИсполнения = Дата(1,1,1);
			КонецЕсли;
			
			Если ЗадачаОбъект.Наименование = НаименованиеЗадачи И
				 Не СрокИсполненияИзменен И
				 ЗадачаОбъект.Важность = Важность И
				 ЗадачаОбъект.Описание = Описание И
				 ЗадачаОбъект.Автор = Автор 
			Тогда 
				Продолжить;
			КонецЕсли;
			
			ЗаблокироватьДанныеДляРедактирования(ЗадачаОбъект.Ссылка);
			
			ЗадачаОбъект.Наименование 	= НаименованиеЗадачи;
			ЗадачаОбъект.Важность 		= Важность;
			ЗадачаОбъект.Описание 		= Описание;
			ЗадачаОбъект.Автор 	  		= Автор;
			
			ПереносСроковВыполненияЗадач.ПередатьПричинуИЗаявкуНаПереносаСрока(ЗадачаОбъект, ДополнительныеСвойства);
			
			ЗадачаОбъект.Записать();
		КонецЦикла;
		
		Если ЕстьНевыполненныеЗадачиВТочкеМаршрутаПригласить Тогда
		
			// обновление исполнителей
			ДобавленныеСтроки = Новый Массив;
			УдаленныеСтроки   = Новый Массив;
			ИзмененныеСтроки  = Новый Массив;
			
			КоличествоСтрок = СтарыеИсполнители.Количество();
			Для Инд = 1 По КоличествоСтрок Цикл
				Строка = СтарыеИсполнители[КоличествоСтрок-Инд];
				Если Строка.Пройден Или Не ЗначениеЗаполнено(Строка.ЗадачаИсполнителя) Тогда 
					СтарыеИсполнители.Удалить(Строка);
				КонецЕсли;	
			КонецЦикла;	
			
			НовыеИсполнители = ЭтотОбъект.Исполнители.Выгрузить();
			КоличествоСтрок = НовыеИсполнители.Количество();
			Для Инд = 1 По КоличествоСтрок Цикл
				Строка = НовыеИсполнители[КоличествоСтрок-Инд];
				Если Строка.Пройден Тогда
					НовыеИсполнители.Удалить(Строка);
				КонецЕсли;
			КонецЦикла;	
			
			
			КоличествоСтрок = НовыеИсполнители.Количество();
			Для Инд = 0 По КоличествоСтрок-1 Цикл
				Строка = НовыеИсполнители[Инд];
				
				Если Не ЗначениеЗаполнено(Строка.ЗадачаИсполнителя) Тогда 
					ДобавленныеСтроки.Добавить(Строка);
				КонецЕсли;
			КонецЦикла;	
			
			Для Каждого Строка Из НовыеИсполнители Цикл
				Если Не ЗначениеЗаполнено(Строка.ЗадачаИсполнителя) Тогда 
					Продолжить;
				КонецЕсли;	
				
				НайденнаяСтрока = СтарыеИсполнители.Найти(Строка.ЗадачаИсполнителя, "ЗадачаИсполнителя");
				Если НайденнаяСтрока = Неопределено Тогда 
					Продолжить;
				КонецЕсли;	
					
				Если Строка.Исполнитель <> НайденнаяСтрока.Исполнитель Тогда 
					ИзмененныеСтроки.Добавить(Строка);
				КонецЕсли;
			КонецЦикла;	
			
			Для Каждого Строка Из СтарыеИсполнители Цикл
				НайденнаяСтрока = НовыеИсполнители.Найти(Строка.ЗадачаИсполнителя, "ЗадачаИсполнителя");
			    Если НайденнаяСтрока = Неопределено Тогда 
					УдаленныеСтроки.Добавить(Строка);
				КонецЕсли;
			КонецЦикла;	
			
			// добавленные строки
			Для Каждого Строка Из ДобавленныеСтроки Цикл
				
				Задача = Задачи.ЗадачаИсполнителя.СоздатьЗадачу();
				Задача.ДополнительныеСвойства.Вставить("АвторИзменения", АвторИзменения);
				ЗаполнитьЗадачуПригласить(Задача, Строка);
				Задача.Записать();
				
				НайденнаяСтрока = ЭтотОбъект.Исполнители.Найти(Строка.НомерСтроки, "НомерСтроки");
				Если НайденнаяСтрока <> Неопределено Тогда 
					НайденнаяСтрока.ЗадачаИсполнителя = Задача.Ссылка;
				КонецЕсли;
				
				НоваяСтрока = РезультатыПриглашения.Добавить();
				НоваяСтрока.НомерИтерации = НомерИтерации;
				НоваяСтрока.ЗадачаИсполнителя = Задача.Ссылка;
				НоваяСтрока.ЯвкаОбязательна = Строка.ЯвкаОбязательна;
				
			КонецЦикла;
			
			Если ДобавленныеСтроки.Количество() > 0 Тогда 
				Записать();
			КонецЕсли;
			
			// измененные строки
			Для Каждого Строка Из ИзмененныеСтроки Цикл	
				Задача = Строка.ЗадачаИсполнителя.ПолучитьОбъект();
				Задача.ДополнительныеСвойства.Вставить("АвторИзменения", АвторИзменения);
				Если Задача.ПринятаКИсполнению Тогда
					Задача.ПринятаКИсполнению = Ложь;
					Задача.ДатаПринятияКИсполнению = '00010101';
				КонецЕсли;
				
				Если ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.Сотрудники")
					Или ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.Пользователи")Тогда
					Задача.Исполнитель = Строка.Исполнитель;
					Задача.РольИсполнителя = Неопределено;
				Иначе	
					Задача.Исполнитель = Неопределено;
					Задача.РольИсполнителя = Строка.Исполнитель;
				КонецЕсли;	
				
				Задача.Записать();
			КонецЦикла;

			// удаленные строки
			Если УдаленныеСтроки.Количество() > 0 Тогда 
			
				Для Каждого Строка Из УдаленныеСтроки Цикл	
					Задача = Строка.ЗадачаИсполнителя.ПолучитьОбъект();
					Задача.ДополнительныеСвойства.Вставить("АвторИзменения", АвторИзменения);
					Задача.ИсключитьИзПроцесса();
				КонецЦикла;	
				Прочитать();
				
				Для Каждого Строка Из УдаленныеСтроки Цикл	
					НайденнаяСтрока = РезультатыПриглашения.Найти(Строка.ЗадачаИсполнителя, "ЗадачаИсполнителя");
					Если НайденнаяСтрока <> Неопределено Тогда 
						РезультатыПриглашения.Удалить(НайденнаяСтрока);
					КонецЕсли;
				КонецЦикла;	
				Записать();
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не (ДополнительныеСвойства.Свойство("ОтключитьОбновлениеЗадач")
			И ДополнительныеСвойства.ОтключитьОбновлениеЗадач) Тогда
			ИнтеграцияЗадач.ОбновитьЗадачуПоИсточнику(Ссылка);
		КонецЕсли;
		ПараметрыСеанса.ОтключитьОбновлениеЗадач = Ложь;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ПараметрыСеанса.ОтключитьОбновлениеЗадач = Ложь;
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры 

// Формирует шаблон по процессу
// Параметры:
//	ВладелецШаблона - ссылка на шаблон комплексного процесса или комплексный процесс, который будет владельцем
//		создаваемого шаблона процесса
// Возвращает:
//	Ссылка на созданный шаблон
Функция СоздатьШаблонПоПроцессу(ВладелецШаблона = Неопределено) Экспорт
	
	ИмяТипа = БизнесПроцессы[ЭтотОбъект.Метаданные().Имя].ТипШаблона();	
	ШаблонОбъект = Справочники[СтрЗаменить(ИмяТипа, "Справочник.", "")].СоздатьЭлемент();
	
	// Перенос базовых реквизитов процесса
	ШаблонОбъект.Наименование = Наименование;
	ШаблонОбъект.НаименованиеБизнесПроцесса = Наименование;
	ШаблонОбъект.Описание = Описание;
	ШаблонОбъект.Важность = Важность;
	ШаблонОбъект.Автор = Сотрудники.ОсновнойСотрудник();
	ШаблонОбъект.ВладелецШаблона = ВладелецШаблона;
	
	ШаблонОбъект.Предметы.Загрузить(Предметы.Выгрузить());
	Для Каждого СтрокаПредмета Из ШаблонОбъект.Предметы Цикл
		Если ЗначениеЗаполнено(СтрокаПредмета.Предмет) Тогда
			ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(СтрокаПредмета.Предмет.Метаданные().ПолноеИмя()).ПустаяСсылка();
		КонецЕсли;
	КонецЦикла;
	ШаблонОбъект.ПредметыЗадач.Загрузить(ПредметыЗадач.Выгрузить());
	
	// Перенос срока обработки результатов
	ШаблонОбъект.СрокОбработкиРезультатов = СрокОбработкиРезультатов;
	ШаблонОбъект.СрокОбработкиРезультатовДни = СрокОбработкиРезультатовДни;
	ШаблонОбъект.СрокОбработкиРезультатовЧасы = СрокОбработкиРезультатовЧасы;
	ШаблонОбъект.СрокОбработкиРезультатовМинуты = СрокОбработкиРезультатовМинуты;
	ШаблонОбъект.ВариантУстановкиСрокаОбработкиРезультатов = 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
	
	// Перенос количества итераций
	ШаблонОбъект.КоличествоИтераций = КоличествоИтераций;
	
	// Перенос исполнителей
	ШаблонОбъект.Исполнители.Очистить();
	Для Каждого Исполнитель Из Исполнители Цикл
		ИсполнительШаблона = ШаблонОбъект.Исполнители.Добавить();
		ЗаполнитьЗначенияСвойств(
			ИсполнительШаблона,
			Исполнитель,
			"Исполнитель, ЯвкаОбязательна, СрокИсполненияДни, СрокИсполненияЧасы, СрокИсполненияМинуты");
			
		ИсполнительШаблона.ВариантУстановкиСрокаИсполнения =
			Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
	КонецЦикла;
	
	ШаблонОбъект.Ответственный = Сотрудники.ОсновнойСотрудник();
	ШаблонОбъект.Записать();
	Возврат ШаблонОбъект.Ссылка;
	
КонецФункции

// Заполнит структуру возврата вместо диалога настроек процесса
//
//Параметры
//  РезультатВыполнения
//
Процедура ЗаполнитьРезультатНастроекПроцесса(СтруктураРезультата) Экспорт
	
КонецПроцедуры	

// Возвращает пустую структуру данных для заполнения.
//
// Возвращаемое значение:
//  Структура
//   * Важность - ПеречислениеСсылка.ВариантыВажностиОбъектов
//   * Наименование - Строка
//   * Описание - Строка
//   * ДатаНачалаМероприятия - Дата
//   * ДатаОкончанияМероприятия - Дата
//   * МестоПроведения - Строка
//   * Исполнители - ТаблицаЗначений
//      ** ВариантУстановкиСрокаИсполнения - ПеречислениеСсылка.ВариантыУстановкиСрокаИсполнения
//      ** ИдентификаторИсполнителя - УникальныйИдентификатор
//      ** Исполнитель - СправочникСсылка.Сотрудники, СправочникСсылка.ПолныеРоли, СправочникСсылка.Пользователи
//      ** СрокИсполнения - Дата
//      ** СрокИсполненияДни - Число
//      ** СрокИсполненияЧасы - Число
//      ** СрокИсполненияМинуты - Число
//      ** ЯвкаОбязательна - Булево
//   * Автор - СправочникСсылка.Сотрудники
//   * КоличествоИтераций - Число
//   * ВариантУстановкиСрокаОбработкиРезультатов - ПеречислениеСсылка.ВариантыУстановкиСрокаИсполнения
//   * СрокОбработкиРезультатов - Дата
//   * СрокОбработкиРезультатовДни - Число
//   * СрокОбработкиРезультатовЧасы - Число
//   * СрокОбработкиРезультатовМинуты - Число
//   * ТрудозатратыПланАвтора - Число
//   * ТрудозатратыПланИсполнителя - Число
//   * ВедущаяЗадача - ЗадачаСсылка.ЗадачаИсполнителя
//   * Предметы - ОпределяемыйТип.ПредметДействия
//
Функция СтруктураДанныхДляЗаполнения() Экспорт

	СтруктураДанных = Новый Структура;

	СтруктураДанных.Вставить("Важность", Перечисления.ВариантыВажностиОбъектов.ПустаяСсылка());
	СтруктураДанных.Вставить("Наименование", "");
	СтруктураДанных.Вставить("Описание", "");
	
	СтруктураДанных.Вставить("ДатаНачалаМероприятия", Дата(1, 1, 1));
	СтруктураДанных.Вставить("ДатаОкончанияМероприятия", Дата(1, 1, 1));
	СтруктураДанных.Вставить("МестоПроведения", "");

	СтруктураДанных.Вставить("Исполнители",
		Исполнители.ВыгрузитьКолонки(
			"ИдентификаторИсполнителя,
			|Исполнитель,
			|СрокИсполнения,
			|СрокИсполненияДни,
			|СрокИсполненияЧасы,
			|СрокИсполненияМинуты,
			|ВариантУстановкиСрокаИсполнения,
			|ЯвкаОбязательна"));

	СтруктураДанных.Вставить("Автор", Справочники.Сотрудники.ПустаяСсылка());
	СтруктураДанных.Вставить("КоличествоИтераций", 0);
	
	СтруктураДанных.Вставить("ВариантУстановкиСрокаОбработкиРезультатов", 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ПустаяСсылка());
	СтруктураДанных.Вставить("СрокОбработкиРезультатов", Дата(1,1,1));
	СтруктураДанных.Вставить("СрокОбработкиРезультатовДни", 0);
	СтруктураДанных.Вставить("СрокОбработкиРезультатовЧасы", 0);
	СтруктураДанных.Вставить("СрокОбработкиРезультатовМинуты", 0);
	
	СтруктураДанных.Вставить("Предметы", Неопределено);
	
	СтруктураДанных.Вставить("ТрудозатратыПланАвтора", 0);
	СтруктураДанных.Вставить("ТрудозатратыПланИсполнителя", 0);
	
	СтруктураДанных.Вставить("ВедущаяЗадача", Задачи.ЗадачаИсполнителя.ПустаяСсылка());
	
	Возврат СтруктураДанных;

КонецФункции

// Обновляет реквизиты активного процесса по переданной структуре
// 
// Параметры:
//  ДанныеОбновления - Структура, см. СтруктураДанныхДляЗаполнения()
//
Процедура ОбновитьАктивныйПроцесс(ДанныеОбновления) Экспорт 
	
	КэшИсполнителей = Новый Соответствие;
	Для Каждого Исполнитель Из Исполнители Цикл
		КэшИсполнителей.Вставить(Исполнитель.ИдентификаторИсполнителя, Исполнитель);
	КонецЦикла;
	
	Если ДанныеОбновления.Важность <> Важность Тогда
		Важность = ДанныеОбновления.Важность;
	КонецЕсли;
	
	Если ДанныеОбновления.Наименование <> Наименование Тогда
		Наименование = ДанныеОбновления.Наименование;
	КонецЕсли;
	
	НовыеИсполнители = Исполнители.ВыгрузитьКолонки();
	Для Каждого Исполнитель Из ДанныеОбновления.Исполнители Цикл
		СтрокаИсполнителя = НовыеИсполнители.Добавить();
		СтрокаИсполнителя.Исполнитель = Исполнитель.Исполнитель;
		СтрокаИсполнителя.ИдентификаторИсполнителя = Исполнитель.ИдентификаторИсполнителя;
		СтрокаИсполнителя.ВариантУстановкиСрокаИсполнения = Исполнитель.ВариантУстановкиСрокаИсполнения;
		СтрокаИсполнителя.СрокИсполнения = Исполнитель.СрокИсполнения;
		СтрокаИсполнителя.СрокИсполненияДни = Исполнитель.СрокИсполненияДни;
		СтрокаИсполнителя.СрокИсполненияЧасы = Исполнитель.СрокИсполненияЧасы;
		СтрокаИсполнителя.СрокИсполненияМинуты = Исполнитель.СрокИсполненияМинуты;
		СтрокаИсполнителя.ЯвкаОбязательна = Исполнитель.ЯвкаОбязательна;
		
		//Если такой исполнитель уже был в таблице, воссстанавливаем для него этапы и задачи
		СтарыйИсполнитель = КэшИсполнителей.Получить(Исполнитель.ИдентификаторИсполнителя);
		Если СтарыйИсполнитель <> Неопределено Тогда 
			СтрокаИсполнителя.Пройден = СтарыйИсполнитель.Пройден;
			СтрокаИсполнителя.ЗадачаИсполнителя = СтарыйИсполнитель.ЗадачаИсполнителя;
		КонецЕсли;
	КонецЦикла;
	
	ЕстьОтличия = Ложь;
	
	//Если есть отличия исполнителей в старой и новой таблице, замещаем старую новой
	Если Исполнители.Количество() = ДанныеОбновления.Исполнители.Количество() Тогда
		Для Каждого Исполнитель Из ДанныеОбновления.Исполнители Цикл
			ИндексТекущего = ДанныеОбновления.Исполнители.Индекс(Исполнитель);
			СтарыйИсполнитель = Исполнители[ИндексТекущего];
			
			Если СтарыйИсполнитель.Исполнитель <> Исполнитель.Исполнитель 
				Или СтарыйИсполнитель.ИдентификаторИсполнителя <> Исполнитель.ИдентификаторИсполнителя 
				Или СтрокаИсполнителя.ВариантУстановкиСрокаИсполнения <> Исполнитель.ВариантУстановкиСрокаИсполнения
				Или СтрокаИсполнителя.СрокИсполнения <> Исполнитель.СрокИсполнения 
				Или СтрокаИсполнителя.СрокИсполненияДни <> Исполнитель.СрокИсполненияДни 
				Или СтрокаИсполнителя.СрокИсполненияЧасы <> Исполнитель.СрокИсполненияЧасы 
				Или СтрокаИсполнителя.СрокИсполненияМинуты <> Исполнитель.СрокИсполненияМинуты 
				Или СтрокаИсполнителя.ПорядокСогласования <> Исполнитель.ПорядокСогласования
				Или СтрокаИсполнителя.ЯвкаОбязательна <> Исполнитель.ЯвкаОбязательна Тогда 
				
				ЕстьОтличия = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	Иначе
		ЕстьОтличия = Истина;
	КонецЕсли;
	
	Если ЕстьОтличия Тогда
		Исполнители.Очистить();
		Исполнители.Загрузить(НовыеИсполнители);
	КонецЕсли;
	
	Если ДанныеОбновления.Автор <> Автор Тогда
		Автор = ДанныеОбновления.Автор;
	КонецЕсли;
	
	Если ДанныеОбновления.ИдентификаторОбрабатывающегоРезультат <>
			ИдентификаторОбрабатывающегоРезультат Тогда
		ИдентификаторОбрабатывающегоРезультат = ДанныеОбновления.ИдентификаторОбрабатывающегоРезультат;
	КонецЕсли;
	
	Если ДанныеОбновления.ВариантУстановкиСрокаОбработкиРезультатов <>
		ВариантУстановкиСрокаОбработкиРезультатов Тогда
		ВариантУстановкиСрокаОбработкиРезультатов = 
			ДанныеОбновления.ВариантУстановкиСрокаОбработкиРезультатов;
	КонецЕсли;
	
	Если ДанныеОбновления.СрокОбработкиРезультатов <> СрокОбработкиРезультатов Тогда
		СрокОбработкиРезультатов = ДанныеОбновления.СрокОбработкиРезультатов;
	КонецЕсли;
	
	Если ДанныеОбновления.СрокОбработкиРезультатовДни <> СрокОбработкиРезультатовДни Тогда
		СрокОбработкиРезультатовДни = ДанныеОбновления.СрокОбработкиРезультатовДни;
	КонецЕсли;
	
	Если ДанныеОбновления.СрокОбработкиРезультатовЧасы <> СрокОбработкиРезультатовЧасы Тогда
		СрокОбработкиРезультатовЧасы = ДанныеОбновления.СрокОбработкиРезультатовЧасы;
	КонецЕсли;
	
	Если ДанныеОбновления.СрокОбработкиРезультатовМинуты <> СрокОбработкиРезультатовМинуты Тогда
		СрокОбработкиРезультатовМинуты = ДанныеОбновления.СрокОбработкиРезультатовМинуты;
	КонецЕсли;
	
	Если ДанныеОбновления.КоличествоИтераций <> КоличествоИтераций Тогда
		КоличествоИтераций = ДанныеОбновления.КоличествоИтераций;
	КонецЕсли;
	
	Если ДанныеОбновления.ТрудозатратыПланИсполнителя <> ТрудозатратыПланИсполнителя Тогда
		ТрудозатратыПланИсполнителя = ДанныеОбновления.ТрудозатратыПланИсполнителя;
	КонецЕсли;
	
	Если ДанныеОбновления.ТрудозатратыПланАвтора <> ТрудозатратыПланАвтора Тогда
		ТрудозатратыПланАвтора = ДанныеОбновления.ТрудозатратыПланАвтора;
	КонецЕсли;
	
	Если ДанныеОбновления.ВедущаяЗадача <> ВедущаяЗадача Тогда
		ВедущаяЗадача = ДанныеОбновления.ВедущаяЗадача;
	КонецЕсли;
	
	Если Модифицированность() Тогда
		СтарыеУчастникиПроцесса = БизнесПроцессыИЗадачиВызовСервера.ТекущиеУчастникиПроцесса(Ссылка);
		
		НачатьТранзакцию();
		Попытка
			
			ПараметрыСеанса.ОтключитьОбновлениеЗадач = Истина;
			
			Записать();
			ИзменитьРеквизитыНевыполненныхЗадач(СтарыеУчастникиПроцесса, Новый Структура);
			
			ИнтеграцияЗадач.ОбновитьЗадачуПоИсточнику(Ссылка);
			ПараметрыСеанса.ОтключитьОбновлениеЗадач = Ложь;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ПараметрыСеанса.ОтключитьОбновлениеЗадач = Ложь;
			ВызватьИсключение;
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_Предметы

// Проверяет права участников процесса на предметы этого процесса.
// Если у участников процесса отсутствуют права на предметы, то выводятся сообщения с привязкой
// к карточке процесса.
//
// Параметры
//  ПроцессОбъект - БизнесПроцессОбъект - процесс.
//  Отказ - Булево - в этот параметр помещается значение Истина, если кто-то из участников не имеет
//                   прав на предметы.
//  ПроверятьПриИзменении - Булево - если указано значение Истина, то проверка выполняется только если
//                          изменены участники или предметы процесса, иначе проверка выполняется всегда.
//
Процедура ПроверитьПраваУчастниковПроцессаНаПредметы(
	ПроцессОбъект, Отказ, ПроверятьПриИзменении) Экспорт
	
	Мультипредметность.ПроверитьПраваУчастниковПроцессаНаПредметы(
		ПроцессОбъект, Отказ, ПроверятьПриИзменении);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если ЭтоНовый() Тогда
		Дата = ТекущаяДатаСеанса();
		Если Не ЗначениеЗаполнено(Автор) Тогда
			Автор = Сотрудники.ОсновнойСотрудник();
		КонецЕсли;
		Важность = Перечисления.ВариантыВажностиОбъектов.Обычная;
		НомерИтерации = 0;
		
		Если Не ЗначениеЗаполнено(Проект) Тогда 
			Проект = РаботаСПроектами.ПолучитьПроектПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеЗаполнения <> Неопределено И ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Тогда
		Мультипредметность.ПередатьПредметыПроцессу(ЭтотОбъект, ДанныеЗаполнения, Ложь, Истина);
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		ЗадачаСсылка = ДанныеЗаполнения;
		ЗаполнитьБизнесПроцессПоЗадаче(ЗадачаСсылка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("ВедущаяЗадача") Тогда
			ВедущаяЗадача = ДанныеЗаполнения.ВедущаяЗадача;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Важность") Тогда
			Важность = ДанныеЗаполнения.Важность;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Шаблон") Тогда
			Мультипредметность.ЗаполнитьПредметыПроцессаПоШаблону(ДанныеЗаполнения.Шаблон, ЭтотОбъект);
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Наименование") Тогда
			Наименование = ДанныеЗаполнения.Наименование;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Предметы") Тогда
			Мультипредметность.ПередатьПредметыПроцессу(ЭтотОбъект, ДанныеЗаполнения.Предметы, Ложь, Истина);
			Проект = МультипредметностьПереопределяемый.ПолучитьОсновнойПроектПоПредметам(ДанныеЗаполнения.Предметы);
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("АвторСобытия") Тогда
			Автор = ДанныеЗаполнения.АвторСобытия;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Автор") Тогда
			Автор = ДанныеЗаполнения.Автор;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("ИдентификаторОбрабатывающегоРезультат") Тогда
			ИдентификаторОбрабатывающегоРезультат = ДанныеЗаполнения.ИдентификаторОбрабатывающегоРезультат;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Шаблон") Тогда
			ЗаполнитьПоШаблону(ДанныеЗаполнения.Шаблон);
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("ПроектнаяЗадача") Тогда
			ЗаполнитьПоПроектнойЗадаче(ДанныеЗаполнения.ПроектнаяЗадача);
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Проект") Тогда
			Проект = ДанныеЗаполнения.Проект;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Исполнители") Тогда
			Если ТипЗнч(ДанныеЗаполнения.Исполнители) = Тип("ТаблицаЗначений") Тогда
				Для Каждого Исполнитель ИЗ ДанныеЗаполнения.Исполнители Цикл
					Строка = Исполнители.Добавить();
					Строка.Исполнитель = Исполнитель.Исполнитель;
					Строка.ИдентификаторИсполнителя = Исполнитель.ИдентификаторИсполнителя;
					Строка.ВариантУстановкиСрокаИсполнения = Исполнитель.ВариантУстановкиСрокаИсполнения;
					Строка.СрокИсполнения = Исполнитель.СрокИсполнения;
					Строка.СрокИсполненияДни = Исполнитель.СрокИсполненияДни;
					Строка.СрокИсполненияЧасы = Исполнитель.СрокИсполненияЧасы;
					Строка.СрокИсполненияМинуты = Исполнитель.СрокИсполненияМинуты;
					Строка.ЯвкаОбязательна = Исполнитель.ЯвкаОбязательна;
				КонецЦикла;
			ИначеЕсли ТипЗнч(ДанныеЗаполнения.Исполнители)= Тип("Массив") Тогда
				Для Каждого Исполнитель Из ДанныеЗаполнения.Исполнители Цикл
					Если ТипЗнч(Исполнитель) = Тип("Структура") Тогда
						Строка = Исполнители.Добавить();
						ЗаполнитьЗначенияСвойств(Строка, Исполнитель);
					Иначе
						Строка = Исполнители.Добавить();
						Строка.Исполнитель = Исполнитель;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("ВариантУстановкиСрокаОбработкиРезультатов") Тогда
			ВариантУстановкиСрокаОбработкиРезультатов = 
				ДанныеЗаполнения.ВариантУстановкиСрокаОбработкиРезультатов;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("СрокОбработкиРезультатов") Тогда
			СрокОбработкиРезультатов = ДанныеЗаполнения.СрокОбработкиРезультатов;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("СрокОбработкиРезультатовДни") Тогда
			СрокОбработкиРезультатовДни = ДанныеЗаполнения.СрокОбработкиРезультатовДни;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("СрокОбработкиРезультатовЧасы") Тогда
			СрокОбработкиРезультатовЧасы = ДанныеЗаполнения.СрокОбработкиРезультатовЧасы;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("СрокОбработкиРезультатовМинуты") Тогда
			СрокОбработкиРезультатовМинуты = ДанныеЗаполнения.СрокОбработкиРезультатовМинуты;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("КоличествоИтераций") Тогда
			КоличествоИтераций = ДанныеЗаполнения.КоличествоИтераций;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("ТрудозатратыПланАвтора") Тогда
			ТрудозатратыПланАвтора = ДанныеЗаполнения.ТрудозатратыПланАвтора;
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("ТрудозатратыПланИсполнителя") Тогда
			ТрудозатратыПланИсполнителя = ДанныеЗаполнения.ТрудозатратыПланИсполнителя;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("ЗадачаИсполнителя") Тогда
			ЗадачаСсылка = ДанныеЗаполнения.ЗадачаИсполнителя;
			ЗаполнитьБизнесПроцессПоЗадаче(ЗадачаСсылка);
		КонецЕсли;
		
		ТипыПисем = МультипредметностьПереопределяемый.ПолучитьТипыПисем();
		ОсновныеПисьма = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(ЭтотОбъект, ТипыПисем, Истина);
		Для Каждого Письмо Из ОсновныеПисьма Цикл
			Тема = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Письмо, "Тема");
			Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Письмо, "Проект");
			Если Не ЗначениеЗаполнено(Наименование) Тогда
				Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Принять участие в ""%1""'"),
					Тема);
			КонецЕсли;
			Прервать;
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда 	
		
		ЗаполнитьПоПроектнойЗадаче(ДанныеЗаполнения);
		
	КонецЕсли;
	
	Мероприятие = Неопределено;
	Для Каждого СтрокаПредмета Из Предметы Цикл
		
		Если СтрокаПредмета.РольПредмета = Перечисления.РолиПредметов.Основной
			И (УправлениеМероприятиями.ЭтоМероприятие(СтрокаПредмета.Предмет, Мероприятие)
			Или УправлениеМероприятиями.ЭтоМатериалМероприятия(СтрокаПредмета.Предмет, Мероприятие)) Тогда 
			
			Если Не ЗначениеЗаполнено(Наименование) Тогда
				Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Принять участие в ""%1""'"), 
						Строка(Мероприятие));
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДатаНачалаМероприятия) Тогда
				ДатаНачалаМероприятия = Мероприятие.ДатаНачала;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДатаОкончанияМероприятия) Тогда
				ДатаОкончанияМероприятия = Мероприятие.ДатаОкончания;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(МестоПроведения) Тогда
				МестоПроведения = Мероприятие.МестоПроведения;
			КонецЕсли;
			
			Если Исполнители.Количество() = 0 Тогда
				
				Участники = ПолучитьУчастниковМероприятия(Мероприятие);
				Исполнители.Загрузить(Участники);
				
				Для Каждого СтрИсполнитель Из Исполнители Цикл
					СтрИсполнитель.СрокИсполнения = ДатаНачалаМероприятия;
					
					СтрИсполнитель.ВариантУстановкиСрокаИсполнения = 
						Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
				КонецЦикла;
				
				СрокОбработкиРезультатов = ДатаНачалаМероприятия;
				ВариантУстановкиСрокаОбработкиРезультатов = 
					Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(КоличествоИтераций) Тогда
				КоличествоИтераций = 1;
			КонецЕсли;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(Наименование) И Предметы.Количество() > 0 Тогда
		МультипредметностьКлиентСервер.ЗаполнитьНаименованиеПроцесса(
			ЭтотОбъект, НСтр("ru = 'Принять участие в'") + " ");
	КонецЕсли;
	
	БизнесПроцессыИЗадачиСервер.ЗаполнитьГлавнуюЗадачу(ЭтотОбъект, ДанныеЗаполнения);	
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Мультипредметность.ПроверитьКорректностьТиповОсновныхПредметов(ЭтотОбъект, Отказ);
	
	КоличествоИсполнителей = Исполнители.Количество();
	Для Инд1 = 0 По КоличествоИсполнителей-2 Цикл
		Строка1 = Исполнители[Инд1];
		
		Для Инд2 = Инд1+1 По КоличествоИсполнителей-1 Цикл
			Строка2 = Исполнители[Инд2];
			
			Если Строка1.Исполнитель = Строка2.Исполнитель Тогда 
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Участник ""%1"" указан дважды в списке участников!'"),
					Строка(Строка1.Исполнитель));
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					"Исполнители[" + Формат(Строка1.НомерСтроки-1, "ЧГ=0") + "].Исполнитель",, 
					Отказ);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	РаботаСПроцессамиПоОбработкамОбъектов.ПроверкаЗапретаНаЗапускПоПредметуИзДругойСистемы(ЭтотОбъект, Отказ);
	
	// Проверка прав участников процесса на предметы
	Если Не РаботаСБизнесПроцессами.ЭтоФоновоеВыполнениеПроцесса() Тогда
		
		ПроверитьПраваУчастниковПроцессаНаПредметы(ЭтотОбъект, Отказ, Стартован);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбычнаяЗапись = Истина;
	ТолькоОбработкаВыполненияЗадачи = Ложь;
	ТолькоОбновлениеРабочейГруппыПроцессов = Ложь;
	Если ДополнительныеСвойства.Свойство("ВидЗаписи") Тогда
		
		ОбычнаяЗапись = Ложь;
		
		ТолькоОбработкаВыполненияЗадачи = (ДополнительныеСвойства.ВидЗаписи =
			"ЗаписьСОбработкойВыполненияЗадачи");
			
		ТолькоОбновлениеРабочейГруппыПроцессов = 
			(ДополнительныеСвойства.ВидЗаписи =
			"ЗаписьСОбновлением_Предметов_ПредметовЗадач_Проекта_ОбщегоСпискаПроцессов_РабочихГруппПредметов_РабочихГруппПроцессов_ДопРеквизитовПоПредметам");
		
		Если Не ТолькоОбработкаВыполненияЗадачи
			И Не ТолькоОбновлениеРабочейГруппыПроцессов Тогда
			
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбычнаяЗапись Или ТолькоОбработкаВыполненияЗадачи Тогда
	
		// Обработка выполнения задачи
		Если ДополнительныеСвойства.Свойство("ТекущаяЗадача") Тогда
			
			Если ДополнительныеСвойства.ТекущаяЗадача.ТочкаМаршрута = БизнесПроцессы.Приглашение.ТочкиМаршрута.Пригласить Тогда
				НайденнаяСтрока = РезультатыПриглашения.Найти(ДополнительныеСвойства.ТекущаяЗадача, "ЗадачаИсполнителя");
				НайденнаяСтрока.РезультатПриглашения = ДополнительныеСвойства.РезультатПриглашения;
				
				ПредметыДляУстановки = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(ДополнительныеСвойства.ТекущаяЗадача, , Истина);
				Для Каждого Предмет Из ПредметыДляУстановки Цикл
					Мероприятие = Неопределено;
					
					Если УправлениеМероприятиями.ЭтоМероприятие(Предмет, Мероприятие) 
						Или УправлениеМероприятиями.ЭтоМатериалМероприятия(Предмет, Мероприятие) Тогда 
						
						Участники = УправлениеМероприятиями.ПолучитьУчастниковМероприятия(Мероприятие, Истина);
						
						ИсполнительЗадачи = ОбщегоНазначенияДокументооборот.ЗначениеРеквизитаОбъектаВПривилегированномРежиме(
							ДополнительныеСвойства.ТекущаяЗадача, "ТекущийИсполнитель");
						
						СотрудникИсполнителяЗадачи = 
							Сотрудники.ОсновнойСотрудникПользователя(
							ИсполнительЗадачи);
						
						СтруктураПоиска = Новый Структура;
						СтруктураПоиска.Вставить("Исполнитель", СотрудникИсполнителяЗадачи);
						НайденныеСтроки = Участники.НайтиСтроки(СтруктураПоиска);
						Если НайденныеСтроки.Количество() = 0 Тогда
							СтруктураПоиска.Исполнитель = ИсполнительЗадачи;
							НайденныеСтроки = Участники.НайтиСтроки(СтруктураПоиска);
						КонецЕсли;
						
						Если НайденныеСтроки.Количество() > 0 Тогда 
							НайденнаяСтрока = НайденныеСтроки[0];
							
							ЗначенияКлюча = Новый Структура;
							ЗначенияКлюча.Вставить("Мероприятие", Мероприятие);
							ЗначенияКлюча.Вставить("Исполнитель", НайденнаяСтрока.Исполнитель);
							
							КлючЗаписи = РегистрыСведений.УчастникиМероприятия.СоздатьКлючЗаписи(ЗначенияКлюча);
							ЗаблокироватьДанныеДляРедактирования(КлючЗаписи);
							
							НаборЗаписей = РегистрыСведений.УчастникиМероприятия.СоздатьНаборЗаписей();
							НаборЗаписей.Отбор.Мероприятие.Установить(ЗначенияКлюча.Мероприятие);
							НаборЗаписей.Отбор.Исполнитель.Установить(ЗначенияКлюча.Исполнитель);
							
							ЗаписьНабора = НаборЗаписей.Добавить();
							ЗаполнитьЗначенияСвойств(ЗаписьНабора, НайденнаяСтрока);
							Если ДополнительныеСвойства.РезультатПриглашения = Перечисления.РезультатыПриглашения.Принято Тогда 
								ЗаписьНабора.СостояниеПриглашения = Перечисления.СостоянияПриглашения.Принято;
							ИначеЕсли ДополнительныеСвойства.РезультатПриглашения = Перечисления.РезультатыПриглашения.НеПринято Тогда 
								ЗаписьНабора.СостояниеПриглашения = Перечисления.СостоянияПриглашения.НеПринято;
							КонецЕсли;
							
							НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьОпределениеЗависимыхПрав", Истина);
							НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьОбновлениеДанныхМероприятия", Истина);
							НаборЗаписей.Записать();
							
							КлючЗаписи = РегистрыСведений.УчастникиМероприятия.СоздатьКлючЗаписи(ЗначенияКлюча);
							РазблокироватьДанныеДляРедактирования(КлючЗаписи);
							
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
			ИначеЕсли ДополнительныеСвойства.ТекущаяЗадача.ТочкаМаршрута = БизнесПроцессы.Приглашение.ТочкиМаршрута.Ознакомиться Тогда
				Если ДополнительныеСвойства.Свойство("ПовторитьПриглашение") И ДополнительныеСвойства.ПовторитьПриглашение Тогда
					ПовторитьПриглашение = Истина;
					
					Если ДополнительныеСвойства.Свойство("Исполнители") Тогда
						Исполнители.Очистить();
						Для Каждого СтрИсполнитель Из ДополнительныеСвойства.Исполнители Цикл
							ЗаполнитьЗначенияСвойств(Исполнители.Добавить(), СтрИсполнитель);
						КонецЦикла;
					КонецЕсли;
					Если ДополнительныеСвойства.Свойство("МестоПроведения") Тогда
						МестоПроведения = ДополнительныеСвойства.МестоПроведения;
					КонецЕсли;
					Если ДополнительныеСвойства.Свойство("ДатаНачалаМероприятия") Тогда
						ДатаНачалаМероприятия = ДополнительныеСвойства.ДатаНачалаМероприятия;
					КонецЕсли;
					Если ДополнительныеСвойства.Свойство("ДатаОкончанияМероприятия") Тогда
						ДатаОкончанияМероприятия = ДополнительныеСвойства.ДатаОкончанияМероприятия;
					КонецЕсли;
					
					Если ДополнительныеСвойства.Свойство("СрокИсполненияПроцесса") Тогда
						СрокИсполненияПроцесса = ДополнительныеСвойства.СрокИсполненияПроцесса;
					КонецЕсли;
					
					Если ДополнительныеСвойства.Свойство("СрокОбработкиРезультатовДни") Тогда
						СрокОбработкиРезультатовДни = ДополнительныеСвойства.СрокОбработкиРезультатовДни;
					КонецЕсли;
					Если ДополнительныеСвойства.Свойство("СрокОбработкиРезультатовЧасы") Тогда
						СрокОбработкиРезультатовЧасы = ДополнительныеСвойства.СрокОбработкиРезультатовЧасы;
					КонецЕсли;
					Если ДополнительныеСвойства.Свойство("СрокОбработкиРезультатовМинуты") Тогда
						СрокОбработкиРезультатовМинуты = ДополнительныеСвойства.СрокОбработкиРезультатовМинуты;
					КонецЕсли;
					Если ДополнительныеСвойства.Свойство("ВариантУстановкиСрокаОбработкиРезультатов") Тогда
						ВариантУстановкиСрокаОбработкиРезультатов = ДополнительныеСвойства.ВариантУстановкиСрокаОбработкиРезультатов;
					КонецЕсли;
					
					Если ДополнительныеСвойства.Свойство("КоличествоИтераций") Тогда
						КоличествоИтераций = ДополнительныеСвойства.КоличествоИтераций;
					КонецЕсли;
					
					НайденнаяСтрока = РезультатыОзнакомлений.Найти(ДополнительныеСвойства.ТекущаяЗадача, "ЗадачаИсполнителя");
					НайденнаяСтрока.ОтправленоПовторноеПриглашение = Истина;
					
				ИначеЕсли ДополнительныеСвойства.Свойство("ПодтвердитьПриглашение") И ДополнительныеСвойства.ПодтвердитьПриглашение Тогда
					НайденнаяСтрока = РезультатыОзнакомлений.Найти(ДополнительныеСвойства.ТекущаяЗадача.Ссылка, "ЗадачаИсполнителя");
					НомерИтерации = НайденнаяСтрока.НомерИтерации;
					
					УдаленныеСтрокиИсполнителей = Новый Массив;
					Для Каждого Строка Из РезультатыПриглашения Цикл
						Если Строка.НомерИтерации <> НомерИтерации Тогда 
							Продолжить;
						КонецЕсли;	
						
						Если Строка.РезультатПриглашения <> Перечисления.РезультатыПриглашения.НеПринято Тогда 
							Продолжить;
						КонецЕсли;	
						
						Если Строка.ЯвкаОбязательна Тогда 
							Продолжить;
						КонецЕсли;	
						
						Строка.НеУчитывать = Истина;
						
						НайденнаяСтрока = Исполнители.Найти(Строка.ЗадачаИсполнителя, "ЗадачаИсполнителя");
						Если НайденнаяСтрока <> Неопределено Тогда 
							СтруктураИсполнитель = Новый Структура;
							СтруктураИсполнитель.Вставить("Исполнитель", НайденнаяСтрока.Исполнитель);
							УдаленныеСтрокиИсполнителей.Добавить(СтруктураИсполнитель);
							Исполнители.Удалить(НайденнаяСтрока);
						КонецЕсли;
						
					КонецЦикла;
					
					ПредметыДляУстановки = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(ДополнительныеСвойства.ТекущаяЗадача, , Истина);
					Для Каждого Предмет Из ПредметыДляУстановки Цикл
						Мероприятие = Неопределено;
						УдаленныеЗаписи = Новый Массив;
						Если УправлениеМероприятиями.ЭтоМероприятие(Предмет, Мероприятие) 
							Или УправлениеМероприятиями.ЭтоМатериалМероприятия(Предмет, Мероприятие) Тогда 
							
							НаборЗаписей = РегистрыСведений.УчастникиМероприятия.СоздатьНаборЗаписей();
							НаборЗаписей.Отбор.Мероприятие.Установить(Мероприятие);
							НаборЗаписей.Прочитать();
							Участники = НаборЗаписей.Выгрузить();
							
							Для Каждого СтрокаУчастник Из Участники Цикл
								ЗначенияКлюча = Новый Структура;
								ЗначенияКлюча.Вставить("Мероприятие", СтрокаУчастник.Мероприятие);
								ЗначенияКлюча.Вставить("Исполнитель", СтрокаУчастник.Исполнитель);
								КлючЗаписи = РегистрыСведений.УчастникиМероприятия.СоздатьКлючЗаписи(ЗначенияКлюча);
								ЗаблокироватьДанныеДляРедактирования(КлючЗаписи);
							КонецЦикла;
							
							Для Каждого Строка Из УдаленныеСтрокиИсполнителей Цикл
								
								Исполнитель = Строка.Исполнитель;
								СотрудникИсполнителя =
									Сотрудники.ОсновнойСотрудникПользователя(
									Исполнитель);
								
								Строка.Исполнитель = СотрудникИсполнителя;
								НайденныеСтроки = Участники.НайтиСтроки(Строка);
								Если НайденныеСтроки.Количество() = 0 Тогда
									Строка.Исполнитель = Исполнитель;
									НайденныеСтроки = Участники.НайтиСтроки(Строка);
								КонецЕсли;
								Если НайденныеСтроки.Количество() = 0 Тогда
									Продолжить;
								КонецЕсли;
								
								Для Каждого Запись Из НаборЗаписей Цикл
									Если Запись.Исполнитель = СотрудникИсполнителя
										Или Запись.Исполнитель = Исполнитель Тогда
										
										УдаленныеЗаписи.Добавить(Запись);
										Прервать;
									КонецЕсли;
								КонецЦикла;
								
							КонецЦикла;
							
							Для Каждого УдаленнаяЗапись Из УдаленныеЗаписи Цикл
								НаборЗаписей.Удалить(УдаленнаяЗапись);
							КонецЦикла;
							
							НаборЗаписей.Записать();
							
							Для Каждого СтрокаУчастник Из Участники Цикл
								ЗначенияКлюча = Новый Структура;
								ЗначенияКлюча.Вставить("Мероприятие", СтрокаУчастник.Мероприятие);
								ЗначенияКлюча.Вставить("Исполнитель", СтрокаУчастник.Исполнитель);
								КлючЗаписи = РегистрыСведений.УчастникиМероприятия.СоздатьКлючЗаписи(ЗначенияКлюча);
								РазблокироватьДанныеДляРедактирования(КлючЗаписи);
							КонецЦикла;
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
			ИначеЕсли ДополнительныеСвойства.ТекущаяЗадача.ТочкаМаршрута = БизнесПроцессы.Приглашение.ТочкиМаршрута.Оповестить Тогда
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбычнаяЗапись Тогда
		
		Если Не РаботаСБизнесПроцессамиВызовСервера.ПроверитьПередЗаписью(ЭтотОбъект) Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;	
		
		ПредыдущаяПометкаУдаления = Ложь;
		Если Не Ссылка.Пустая() Тогда
			ПредыдущаяПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления");
		КонецЕсли;
		ДополнительныеСвойства.Вставить("ПредыдущаяПометкаУдаления", ПредыдущаяПометкаУдаления);
		
		Если Не Ссылка.Пустая() Тогда
			ПредыдущееСостояние = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Состояние");
			ДополнительныеСвойства.Вставить("ПредыдущееСостояние", ПредыдущееСостояние);
		КонецЕсли;
		
		Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда
			
			РаботаСФайламиВызовСервера.ПометитьНаУдалениеПриложенныеФайлы(Ссылка, ПометкаУдаления);
				
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбычнаяЗапись Или ТолькоОбновлениеРабочейГруппыПроцессов Тогда
		// Обработка рабочей группы	
		РаботаСБизнесПроцессамиВызовСервера.СформироватьРабочуюГруппу(ЭтотОбъект);
	КонецЕсли;
	
	Если ОбычнаяЗапись Тогда
		ЗаполнитьПустыеИдентификаторыУчастников();
		ПроверитьУникальностьИдентификаторовУчастников();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияЗадач.ПриЗаписиИсточникаЗадачи(ЭтотОбъект);
	
	Если ДополнительныеСвойства.Свойство("ВидЗаписи") Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаОчередиЗаданийСервер.ПриЗаписиПроцесса(ЭтотОбъект);
	
	ПредыдущаяПометкаУдаления = Ложь;
	Если ДополнительныеСвойства.Свойство("ПредыдущаяПометкаУдаления") Тогда
		ПредыдущаяПометкаУдаления = ДополнительныеСвойства.ПредыдущаяПометкаУдаления;
	КонецЕсли;
	
	Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда
		ПротоколированиеРаботыСотрудников.ЗаписатьПометкуУдаления(Ссылка, ПометкаУдаления);
	КонецЕсли;
	
	СтартПроцессовСервер.ПроцессПриЗаписи(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	РаботаСБизнесПроцессами.ПриКопированииБизнесПроцесса(ОбъектКопирования);
	
	Дата = ТекущаяДатаСеанса();
	Автор = Сотрудники.ОсновнойСотрудник();
	НомерИтерации = 0;
	РезультатыПриглашения.Очистить();
	РезультатыОзнакомлений.Очистить();
	ДатаНачала = '00010101';
	ДатаЗавершения = '00010101';
	РезультатПриглашения = Неопределено;
	
	Для Каждого Строка Из Исполнители Цикл     
		Строка.Пройден = Ложь;
		Строка.ЗадачаИсполнителя = Неопределено;
	КонецЦикла;	
	
КонецПроцедуры

/////////////////////////////
// Карта маршрута

Процедура СтартПередСтартом(ТочкаМаршрутаБизнесПроцесса, Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	ДатаНачала = ТекущаяДатаСеанса();
	
	РаботаСПроектами.ОтметитьНачалоВыполненияПроектнойЗадачи(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПодготовкаОбработка(ТочкаМаршрутаБизнесПроцесса)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПредметыДляУстановки = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(ЭтотОбъект, , Истина);
	
	Для Каждого Предмет Из ПредметыДляУстановки Цикл
		Мероприятие = Неопределено;
		Если УправлениеМероприятиями.ЭтоМероприятие(Предмет, Мероприятие) 
			Или УправлениеМероприятиями.ЭтоМатериалМероприятия(Предмет, Мероприятие) Тогда 
			
			УправлениеМероприятиями.ЗаписатьСостояниеМероприятия(
				Мероприятие, 
				ТекущаяДатаСеанса(), 
				Перечисления.СостоянияМероприятий.ПриглашенияОтправлены, 
				Ссылка);
			
			НаборЗаписей = РегистрыСведений.УчастникиМероприятия.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Мероприятие.Установить(Мероприятие);
			НаборЗаписей.Прочитать();
			Участники = НаборЗаписей.Выгрузить();
			
			Для Каждого СтрокаУчастник Из Участники Цикл
				ЗначенияКлюча = Новый Структура;
				ЗначенияКлюча.Вставить("Мероприятие", СтрокаУчастник.Мероприятие);
				ЗначенияКлюча.Вставить("Исполнитель", СтрокаУчастник.Исполнитель);
				
				КлючЗаписи = РегистрыСведений.УчастникиМероприятия.СоздатьКлючЗаписи(ЗначенияКлюча);
				ЗаблокироватьДанныеДляРедактирования(КлючЗаписи);
			КонецЦикла;
			
			Для Каждого Строка Из Исполнители Цикл
				
				СтруктураПоиска = Новый Структура;
				СтруктураПоиска.Вставить("Исполнитель", Строка.Исполнитель);
				НайденныеСтроки = Участники.НайтиСтроки(СтруктураПоиска);
				Если НайденныеСтроки.Количество() = 0 Тогда
					СтруктураПоиска.Исполнитель = Строка.Исполнитель;
					НайденныеСтроки = Участники.НайтиСтроки(СтруктураПоиска);
				КонецЕсли;
				
				Если НайденныеСтроки.Количество() > 0 Тогда 
					
					Для Каждого Запись Из НаборЗаписей Цикл
						
						Если Запись.Исполнитель = Строка.Исполнитель
							Или Запись.Исполнитель = Строка.Исполнитель Тогда
							
							Запись.СостояниеПриглашения = Перечисления.СостоянияПриглашения.Отправлено;
							Прервать;
							
						КонецЕсли;
						
					КонецЦикла;
					
				Иначе
					
					НоваяЗапись = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, Строка);
					НоваяЗапись.Исполнитель = Строка.Исполнитель;
					НоваяЗапись.Мероприятие = Мероприятие;
					НоваяЗапись.СостояниеПриглашения = Перечисления.СостоянияПриглашения.Отправлено;
					НоваяЗапись.Порядок = Строка.НомерСтроки;
					
				КонецЕсли;
				
			КонецЦикла;
			
			НаборЗаписей.Записать();
			
			Для Каждого СтрокаУчастник Из Участники Цикл
				ЗначенияКлюча = Новый Структура;
				ЗначенияКлюча.Вставить("Мероприятие", СтрокаУчастник.Мероприятие);
				ЗначенияКлюча.Вставить("Исполнитель", СтрокаУчастник.Исполнитель);
				
				КлючЗаписи = РегистрыСведений.УчастникиМероприятия.СоздатьКлючЗаписи(ЗначенияКлюча);
				РазблокироватьДанныеДляРедактирования(КлючЗаписи);
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;
	
	НомерИтерации = НомерИтерации + 1;
	ПовторитьПриглашение = Ложь;
	Для Каждого Строка Из Исполнители Цикл     
		Строка.Пройден = Ложь;
		Строка.ЗадачаИсполнителя = Неопределено;
	КонецЦикла;	
	
	РаботаСБизнесПроцессами.ЗаписатьПроцесс(ЭтотОбъект, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
	
КонецПроцедуры

Процедура ПригласитьПередСозданиемЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Для Каждого Строка Из Исполнители Цикл
		
		Задача = Задачи.ЗадачаИсполнителя.СоздатьЗадачу();
		ЗаполнитьЗадачуПригласить(Задача, Строка);
	
		ЗадачаСсылка = Задачи.ЗадачаИсполнителя.ПолучитьСсылку();
		Задача.УстановитьСсылкуНового(ЗадачаСсылка);
	
		ФормируемыеЗадачи.Добавить(Задача);
		
		Контроль.УстановитьНовыйИсточникВСтрокеКонтроля(Ссылка, Строка.ЗадачаИсполнителя, ЗадачаСсылка);
		Строка.ЗадачаИсполнителя = ЗадачаСсылка;
		
		НоваяСтрока = РезультатыПриглашения.Добавить();
		НоваяСтрока.НомерИтерации = НомерИтерации;
		НоваяСтрока.ЗадачаИсполнителя = ЗадачаСсылка;
		НоваяСтрока.ЯвкаОбязательна = Строка.ЯвкаОбязательна;
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	
	РаботаСБизнесПроцессами.ЗаписатьПроцесс(ЭтотОбъект, "ПростаяЗапись");
	
КонецПроцедуры

Процедура ОбработкаПроверкиВыполнения(ТочкаМаршрутаБизнесПроцесса, Задача, Результат)
	
	Результат = Истина;
	
КонецПроцедуры

Процедура ПригласитьПередВыполнением(ТочкаМаршрутаБизнесПроцесса, Задача, Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	НайденнаяСтрока = Исполнители.Найти(Задача, "ЗадачаИсполнителя");
	Если НайденнаяСтрока <> Неопределено Тогда 
		НайденнаяСтрока.Пройден = Истина;
		
		РаботаСБизнесПроцессами.ЗаписатьПроцесс(ЭтотОбъект, "ПростаяЗапись");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаРезультатаОбработка(ТочкаМаршрутаБизнесПроцесса)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// результат приглашения
	Если РезультатыПриглашения.Количество() > 0 Тогда
		РезультатПриглашения = Перечисления.ОбщиеРезультатыПриглашения.ПринятоВсемиУчастниками;
		СтрокиИтерации = РезультатыПриглашения.НайтиСтроки(Новый Структура("НомерИтерации", НомерИтерации));
		
		ЕстьПринятые = Ложь;
		Для Каждого Строка Из СтрокиИтерации Цикл
			Если Строка.РезультатПриглашения = Перечисления.РезультатыПриглашения.Принято Тогда 
				ЕстьПринятые = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;	
		
		Если Не ЕстьПринятые Тогда 
			РезультатПриглашения = Перечисления.ОбщиеРезультатыПриглашения.НеПринятоВсемиУчастниками;
		Иначе	
			Для Каждого Строка Из СтрокиИтерации Цикл
				Если Строка.РезультатПриглашения = Перечисления.РезультатыПриглашения.НеПринято Тогда 
					Если Строка.ЯвкаОбязательна Тогда 
						РезультатПриглашения = Перечисления.ОбщиеРезультатыПриглашения.НеПринятоОбязательнымиУчастниками;
						Прервать;
					Иначе
						РезультатПриглашения = Перечисления.ОбщиеРезультатыПриглашения.ПринятоОбязательнымиУчастниками;
					КонецЕсли;	
				КонецЕсли;	
			КонецЦикла;	
		КонецЕсли;
		
		РаботаСБизнесПроцессами.ЗаписатьПроцесс(ЭтотОбъект, "ПростаяЗапись");
	КонецЕсли;	
	
	ОсновныеПредметы = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(ЭтотОбъект,, Истина);
	
	Для Каждого Предмет Из ОсновныеПредметы Цикл
		
		Мероприятие = Неопределено;
	
		Если УправлениеМероприятиями.ЭтоМероприятие(Предмет, Мероприятие) 
		 Или УправлениеМероприятиями.ЭтоМатериалМероприятия(Предмет, Мероприятие) Тогда 
		 
			Если РезультатПриглашения = Перечисления.ОбщиеРезультатыПриглашения.ПринятоВсемиУчастниками
			 Или РезультатПриглашения = Перечисления.ОбщиеРезультатыПриглашения.ПринятоОбязательнымиУчастниками Тогда 
				СостояниеМероприятия = Перечисления.СостоянияМероприятий.ПриглашенияПриняты;
			Иначе	
				СостояниеМероприятия = Перечисления.СостоянияМероприятий.ПриглашенияНеПриняты;
			КонецЕсли;	
			
			УправлениеМероприятиями.ЗаписатьСостояниеМероприятия(
				Мероприятие, 
				ТекущаяДатаСеанса(), 
				СостояниеМероприятия, 
				Ссылка);
			КонецЕсли;
			
		КонецЦикла;
	
КонецПроцедуры

Процедура ОзнакомитьсяПередСозданиемЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	// Если процесс завершился положительно, то задачу ознакомления не формируем.
	Если РезультатПриглашения = Перечисления.ОбщиеРезультатыПриглашения.ПринятоВсемиУчастниками Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьЗадачуОзнакомиться(ФормируемыеЗадачи);
	
КонецПроцедуры

Процедура ПовторитьПриглашениеПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	Результат = ПовторитьПриглашение;
	
КонецПроцедуры

Процедура ОповеститьПередСозданиемЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	// Задачи оповещения всегда создаем после завершения процесса.
	// Это нужно, чтобы не срывать сроки исполнения процесса и
	// не затягивать со стартом следующих действий комплексных процессов.
	
КонецПроцедуры

Процедура ЗавершениеПриЗавершении(ТочкаМаршрутаБизнесПроцесса, Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	ДатаЗавершения = ТекущаяДатаСеанса();
	
	РаботаСПроектами.ОтметитьОкончаниеВыполненияПроектнойЗадачи(ЭтотОбъект);
		
	// Если нет задач оповещения (они могли быть созданы до перехода на редакцию 2.1)
	// то создаем их.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЗадачаИсполнителя.Ссылка
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
		|ГДЕ
		|	ЗадачаИсполнителя.ТочкаМаршрута = ЗНАЧЕНИЕ(БизнесПроцесс.Приглашение.ТочкаМаршрута.Оповестить)
		|	И ЗадачаИсполнителя.БизнесПроцесс = &БизнесПроцесс";
	Запрос.УстановитьПараметр("БизнесПроцесс", Ссылка);
	Если Запрос.Выполнить().Пустой() Тогда
		СоздатьЗадачиОповестить(, Истина);
	КонецЕсли;
	
	РаботаСБизнесПроцессами.ЗаписатьПроцесс(ЭтотОбъект, "ЗаписьСОбновлениемОбщегоСпискаПроцессов");
	
	РаботаСПроцессамиПоДействиямСобытия.ПриЗавершенииПроцесса(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьБизнесПроцессПоЗадаче(ЗадачаСсылка)
	
	РаботаСБизнесПроцессами.ЗаполнитьБизнесПроцессПоЗадаче(ЭтотОбъект, ЗадачаСсылка);
	
КонецПроцедуры

Процедура ЗаполнитьЗадачуПригласить(Задача, Строка)
	
	Задача.Дата 	= ТекущаяДатаСеанса();
	Задача.Автор 	= Автор;
	Задача.Описание = Описание;
	Задача.Важность = Важность;
	Мультипредметность.ЗадачаПередСозданием(ЭтотОбъект, Задача, БизнесПроцессы.Приглашение.ТочкиМаршрута.Пригласить);
	
	Задача.Наименование = Наименование;
	Если Строка.ЯвкаОбязательна Тогда 
		Задача.Наименование = Задача.Наименование + НСтр("ru = ' (явка обязательна)'");
	КонецЕсли;
	
	Задача.БизнесПроцесс = ЭтотОбъект.Ссылка;
	Задача.ТочкаМаршрута = БизнесПроцессы.Приглашение.ТочкиМаршрута.Пригласить;
	
	Задача.Проект = Проект;
	Задача.ПроектнаяЗадача = ПроектнаяЗадача;
	
	Если ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.Сотрудники") 
		Или ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.Пользователи") Тогда
		Задача.Исполнитель = Строка.Исполнитель;
		
	ИначеЕсли ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.ПолныеРоли") Тогда 
		Задача.РольИсполнителя = Строка.Исполнитель;
		
	КонецЕсли;
	Задача.ИдентификаторИсполнителя = Строка.ИдентификаторИсполнителя;
	
	СрокиИсполненияПроцессов.ЗаполнитьСрокИсполненияЗадачи(
		Задача,
		Строка.ВариантУстановкиСрокаИсполнения,
		Строка.СрокИсполнения,
		Строка.СрокИсполненияДни,
		Строка.СрокИсполненияЧасы,
		Строка.СрокИсполненияМинуты);
	
	РаботаСБизнесПроцессамиВызовСервера.СкопироватьЗначенияДопРеквизитов(Задача.БизнесПроцесс, Задача);
	
КонецПроцедуры

Процедура СоздатьЗадачуОзнакомиться(ФормируемыеЗадачи = Неопределено, ЗаписатьЗадачу = Ложь)
	
	Если ФормируемыеЗадачи = Неопределено Тогда
		ФормируемыеЗадачи = Новый Массив;
	КонецЕсли;
	
	Задача = Задачи.ЗадачаИсполнителя.СоздатьЗадачу();
	Если ЗначениеЗаполнено(Состояние) Тогда
		Задача.СостояниеБизнесПроцесса = Состояние;
	КонецЕсли;
	ЗаполнитьЗадачуОзнакомиться(Задача);
	
	ЗадачаСсылка = Задачи.ЗадачаИсполнителя.ПолучитьСсылку();
	Задача.УстановитьСсылкуНового(ЗадачаСсылка);
	
	Если ЗаписатьЗадачу Тогда
		Задача.Записать();
	КонецЕсли;
	
	ФормируемыеЗадачи.Добавить(Задача);
	
	УстановитьПривилегированныйРежим(Истина);
	НоваяСтрока = РезультатыОзнакомлений.Добавить();
	НоваяСтрока.НомерИтерации 	  = НомерИтерации;
	НоваяСтрока.ЗадачаИсполнителя = ЗадачаСсылка;
	
	РаботаСБизнесПроцессами.ЗаписатьПроцесс(ЭтотОбъект, "ПростаяЗапись");
	
КонецПроцедуры

Процедура ЗаполнитьЗадачуОзнакомиться(Задача)
	
	Задача.Дата  	= ТекущаяДатаСеанса();
	Задача.Автор 	= Автор;
	Задача.Описание = Описание;
	Задача.Важность = Важность;
	
	Мультипредметность.ЗадачаПередСозданием(ЭтотОбъект, Задача, БизнесПроцессы.Приглашение.ТочкиМаршрута.Ознакомиться);
	
	Задача.Исполнитель 	  = Автор;
	Задача.ИдентификаторИсполнителя = ИдентификаторОбрабатывающегоРезультат;
	
	// Если указана дата завершения, то создается после завершения с пустым сроком,
	// т.к. она формируется для уведомления автора процесса о положительном
	// завершении и не влияет на срок исполнения всего процесса.
	Если ЗначениеЗаполнено(ДатаЗавершения) Тогда
		Задача.СрокИсполнения = Дата(1,1,1);
	Иначе
		
		СрокиИсполненияПроцессов.ЗаполнитьСрокИсполненияЗадачи(
			Задача,
			ВариантУстановкиСрокаОбработкиРезультатов,
			СрокОбработкиРезультатов,
			СрокОбработкиРезультатовДни,
			СрокОбработкиРезультатовЧасы,
			СрокОбработкиРезультатовМинуты);
		
	КонецЕсли;
	
	Задача.БизнесПроцесс  = ЭтотОбъект.Ссылка;
	Задача.ТочкаМаршрута  = БизнесПроцессы.Приглашение.ТочкиМаршрута.Ознакомиться;
	
	Задача.Проект = Проект;
	Задача.ПроектнаяЗадача = ПроектнаяЗадача;
	
	МассивПредметов = МультипредметностьКлиентСервер.ПолучитьМассивСтруктурПредметовОбъекта(ЭтотОбъект);
	СтрокаПредметов = МультипредметностьКлиентСервер.ПредметыСтрокой(МассивПредметов, Истина, Ложь);
	
	ДобавлятьНаименованиеПредмета = РаботаСБизнесПроцессами.ДобавлятьНаименованиеПредмета(ЭтотОбъект);
	
	Если СтрНачинаетсяС(НаименованиеПроцессаПоУмолчанию(ДобавлятьНаименованиеПредмета), Наименование)
		И ЗначениеЗаполнено(СтрокаПредметов) Тогда
		
		Задача.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ознакомиться с результатом приглашения %1'",
				ЛокализацияКонфигурации.КодЯзыкаИсполнителяЗадачи(Задача)),
			СтрокаПредметов);
	Иначе
		Задача.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ознакомиться с результатом приглашения: %1'",
				ЛокализацияКонфигурации.КодЯзыкаИсполнителяЗадачи(Задача)), 
			Наименование);
	КонецЕсли;	
	
	РаботаСБизнесПроцессамиВызовСервера.СкопироватьЗначенияДопРеквизитов(Задача.БизнесПроцесс, Задача);
	
КонецПроцедуры

// Создает задачи для точки маршрута Оповестить и помещает задачи параметр ФормируемыеЗадачи.
//
// Параметры:
//   ФормируемыеЗадачи - массив созданных задач
//          - ЗадачаОбъекта
//   ЗаписатьЗадачи - Булево - если передается Истина, то задачи будут записаны при создании.
//
Процедура СоздатьЗадачиОповестить(ФормируемыеЗадачи = Неопределено, ЗаписатьЗадачи = Ложь)
	
	Если ФормируемыеЗадачи = Неопределено Тогда
		ФормируемыеЗадачи = Новый Массив;
	КонецЕсли;
	
	Если РезультатПриглашения = Перечисления.ОбщиеРезультатыПриглашения.НеПринятоВсемиУчастниками Или
		РезультатПриглашения = Перечисления.ОбщиеРезультатыПриглашения.НеПринятоОбязательнымиУчастниками Тогда 
		
		ДобавлятьНаименованиеПредмета = РаботаСБизнесПроцессами.ДобавлятьНаименованиеПредмета(ЭтотОбъект);
		
		Для Каждого Строка Из РезультатыПриглашения Цикл
			Если Строка.НомерИтерации = НомерИтерации Тогда 
				Если Строка.НеУчитывать Тогда 
					Продолжить;
				КонецЕсли;	
				
				Если Строка.РезультатПриглашения = Перечисления.РезультатыПриглашения.НеПринято Тогда 
					Продолжить;
				КонецЕсли;	
				
				СтрокаИсполнителя = Исполнители.Найти(Строка.ЗадачаИсполнителя, "ЗадачаИсполнителя");
				Если СтрокаИсполнителя = Неопределено Тогда 
					СтрокаИсполнителя = Строка.ЗадачаИсполнителя;
				КонецЕсли;	
				
				Задача = Задачи.ЗадачаИсполнителя.СоздатьЗадачу();
				Если ЗначениеЗаполнено(Состояние) Тогда
					Задача.СостояниеБизнесПроцесса = Состояние;
				КонецЕсли;
				ЗаполнитьЗадачуОповестить(Задача, СтрокаИсполнителя, ДобавлятьНаименованиеПредмета);
				
				Если ЗаписатьЗадачи Тогда
					Задача.Записать();
				КонецЕсли;
				
				ФормируемыеЗадачи.Добавить(Задача);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьЗадачуОповестить(Задача, Строка, ДобавлятьНаименованиеПредмета)
	
	Задача.Дата  	= ТекущаяДатаСеанса();
	Задача.Автор 	= Автор;
	Задача.Описание = Описание;
	Задача.Важность = Важность;
	
	Мультипредметность.ЗадачаПередСозданием(ЭтотОбъект, Задача, БизнесПроцессы.Приглашение.ТочкиМаршрута.Оповестить);
	
	Задача.Проект = Проект;
	Задача.ПроектнаяЗадача = ПроектнаяЗадача;
	
	Если ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.Сотрудники")
		Или ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.Пользователи") Тогда
		Задача.Исполнитель = Строка.Исполнитель;
		
	ИначеЕсли ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.ПолныеРоли") Тогда 
		Задача.РольИсполнителя = Строка.Исполнитель;
		
	КонецЕсли;
	Задача.ИдентификаторИсполнителя = Строка.ИдентификаторИсполнителя;
	
	Задача.СрокИсполнения = '00010101'; 
	Задача.БизнесПроцесс  = ЭтотОбъект.Ссылка;
	Задача.ТочкаМаршрута  = БизнесПроцессы.Приглашение.ТочкиМаршрута.Оповестить;
	
	МассивПредметов = МультипредметностьКлиентСервер.ПолучитьМассивСтруктурПредметовОбъекта(ЭтотОбъект);
	СтрокаПредметов = МультипредметностьКлиентСервер.ПредметыСтрокой(МассивПредметов, Истина, Ложь);
	
	Если СтрНачинаетсяС(НаименованиеПроцессаПоУмолчанию(ДобавлятьНаименованиеПредмета), Наименование)
		И ЗначениеЗаполнено(СтрокаПредметов) Тогда
		
		Задача.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ознакомиться с результатом приглашения %1'",
				ЛокализацияКонфигурации.КодЯзыкаИсполнителяЗадачи(Задача)),
			СтрокаПредметов);
	Иначе
		Задача.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ознакомиться с результатом приглашения: %1'",
				ЛокализацияКонфигурации.КодЯзыкаИсполнителяЗадачи(Задача)), 
			Наименование);
	КонецЕсли;	
	
	РаботаСБизнесПроцессамиВызовСервера.СкопироватьЗначенияДопРеквизитов(Задача.БизнесПроцесс, Задача);
	
КонецПроцедуры

// Заполняет пустые идентификаторы участников в процессе.
//
Процедура ЗаполнитьПустыеИдентификаторыУчастников()
	
	Для Каждого Строка Из Исполнители Цикл
		Если Строка.ИдентификаторИсполнителя = УникальныйИдентификаторПустой() Тогда
			Строка.ИдентификаторИсполнителя = Новый УникальныйИдентификатор;
		КонецЕсли;
	КонецЦикла;
	
	Если ИдентификаторОбрабатывающегоРезультат = УникальныйИдентификаторПустой() Тогда
		ИдентификаторОбрабатывающегоРезультат = Новый УникальныйИдентификатор;
	КонецЕсли;
	
КонецПроцедуры

// Проверяет уникальность идентификаторов участников процесса.
// Если есть не уникальные значения, то вызывается исключение. 
//
Процедура ПроверитьУникальностьИдентификаторовУчастников()
	
	ПройденныеИдентификаторы = Новый Соответствие();
	ПройденныеИдентификаторы[ИдентификаторОбрабатывающегоРезультат] = Истина;
	
	Для Каждого СтрокаТаблицы Из Исполнители Цикл
		Если ПройденныеИдентификаторы[СтрокаТаблицы.ИдентификаторИсполнителя] <> Неопределено Тогда
			ВызватьИсключение НСтр("ru = 'Идентификаторы участников не уникальны.'",
				ОбщегоНазначения.КодОсновногоЯзыка());
		КонецЕсли;
		ПройденныеИдентификаторы[СтрокаТаблицы.ИдентификаторИсполнителя] = Истина; 
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры

// Возвращает описание задачи, специфичное для бизнес-процесса
Функция ПолучитьОписаниеУведомленияЗадачи(ЗадачаПроцесса, КодЯзыкаПолучателя) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Задача = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ЗадачаПроцесса,
		"Ссылка, БизнесПроцесс, ТочкаМаршрута, Выполнена, Исполнитель");
		
	Если Задача.БизнесПроцесс <> Ссылка Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Неопределено;
	
	Если Задача.ТочкаМаршрута = БизнесПроцессы.Приглашение.ТочкиМаршрута.Пригласить Тогда
		
		// Результат предыдущего приглашения
		НомерИтерации = Неопределено;
		НайденнаяСтрока = РезультатыПриглашения.Найти(Задача.Ссылка, "ЗадачаИсполнителя");
		Если НайденнаяСтрока <> Неопределено Тогда 
			РезультатПриглашенияЗадачи = НайденнаяСтрока.РезультатПриглашения;
			НомерИтерации = НайденнаяСтрока.НомерИтерации - 1;
		КонецЕсли;
		
		СтрокиИтерации =
			РезультатыПриглашения.НайтиСтроки(Новый Структура("НомерИтерации", НомерИтерации));
		Для Каждого Строка Из СтрокиИтерации Цикл
			
			Если Строка.ЗадачаИсполнителя.Исполнитель = Задача.Исполнитель Тогда
				Результат = НСтр("ru = 'Приглашение было прислано повторно'", КодЯзыкаПолучателя);
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		// Результат приглашения
		Если РезультатПриглашенияЗадачи = Перечисления.РезультатыПриглашения.Принято Тогда
			ДобавитьЗначениеКСтрокеЧерезРазделитель(
				Результат,
				Символы.ПС,
				НСтр("ru = 'Результат приглашения: Принято'", КодЯзыкаПолучателя));
		ИначеЕсли РезультатПриглашенияЗадачи = Перечисления.РезультатыПриглашения.НеПринято Тогда
			ДобавитьЗначениеКСтрокеЧерезРазделитель(
				Результат,
				Символы.ПС,
				НСтр("ru = 'Результат приглашения: Не принято'", КодЯзыкаПолучателя));
		КонецЕсли;
		
	ИначеЕсли Задача.ТочкаМаршрута = БизнесПроцессы.Приглашение.ТочкиМаршрута.Ознакомиться Тогда
		
		// Результат принятия приглашений
		НомерИтерации = Неопределено;
		НайденнаяСтрока = РезультатыОзнакомлений.Найти(Задача.Ссылка, "ЗадачаИсполнителя");
		Если НайденнаяСтрока <> Неопределено Тогда 
			НомерИтерации = НайденнаяСтрока.НомерИтерации;
		КонецЕсли;
		
		ЕстьПринятые = Ложь;
		РезультатПриглашенияПоЗадачам = Перечисления.ОбщиеРезультатыПриглашения.ПринятоВсемиУчастниками;
		СтрокиИтерации =
			РезультатыПриглашения.НайтиСтроки(Новый Структура("НомерИтерации", НомерИтерации));
		Для Каждого Строка Из СтрокиИтерации Цикл
			Если Строка.РезультатПриглашения = Перечисления.РезультатыПриглашения.Принято Тогда 
				ЕстьПринятые = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ЕстьПринятые Тогда 
			РезультатПриглашенияПоЗадачам = Перечисления.ОбщиеРезультатыПриглашения.НеПринятоВсемиУчастниками;
		Иначе	
			Для Каждого Строка Из СтрокиИтерации Цикл
				Если Строка.РезультатПриглашения = Перечисления.РезультатыПриглашения.НеПринято Тогда 
					Если Строка.ЯвкаОбязательна Тогда 
						РезультатПриглашенияПоЗадачам = Перечисления.ОбщиеРезультатыПриглашения.НеПринятоОбязательнымиУчастниками;
						Прервать;
					Иначе
						РезультатПриглашенияПоЗадачам = Перечисления.ОбщиеРезультатыПриглашения.ПринятоОбязательнымиУчастниками;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Результат = НСтр("ru = 'Результат принятия приглашений:'", КодЯзыкаПолучателя) + " ";
		Если РезультатПриглашенияПоЗадачам = Перечисления.ОбщиеРезультатыПриглашения.ПринятоВсемиУчастниками Тогда
			Результат = Результат + НСтр("ru = 'Все приглашения приняты'",
				КодЯзыкаПолучателя);
		ИначеЕсли РезультатПриглашенияПоЗадачам = Перечисления.ОбщиеРезультатыПриглашения.ПринятоОбязательнымиУчастниками Тогда
			Результат = Результат + НСтр("ru = 'Есть приглашения, не принятые участниками без обязательной явки'",
				КодЯзыкаПолучателя);
		ИначеЕсли РезультатПриглашенияПоЗадачам = Перечисления.ОбщиеРезультатыПриглашения.НеПринятоОбязательнымиУчастниками Тогда
			Результат = Результат + НСтр("ru = 'Есть приглашения, не принятые участниками с обязательной явкой'",
				КодЯзыкаПолучателя);
		ИначеЕсли РезультатПриглашенияПоЗадачам = Перечисления.ОбщиеРезультатыПриглашения.НеПринятоВсемиУчастниками Тогда
			Результат = Результат + НСтр("ru = 'Приглашения не приняты всеми участниками'",
				КодЯзыкаПолучателя);
		КонецЕсли;
		
		// Результат рассылки приглашений
		Если Задача.Выполнена Тогда
			
			НайденнаяСтрока = РезультатыОзнакомлений.Найти(Задача.Ссылка, "ЗадачаИсполнителя");
			Если НайденнаяСтрока <> Неопределено Тогда 
				
				Результат = Результат + Символы.ПС + НСтр("ru = 'Результат рассылки приглашений:'", КодЯзыкаПолучателя) + " ";
				
				Если НайденнаяСтрока.ОтправленоПовторноеПриглашение Тогда
					Результат = Результат  + НСтр("ru = 'Приглашения разосланы повторно'", КодЯзыкаПолучателя);
				ИначеЕсли РезультатПриглашенияПоЗадачам = Перечисления.ОбщиеРезультатыПриглашения.ПринятоВсемиУчастниками
					ИЛИ РезультатПриглашенияПоЗадачам = Перечисления.ОбщиеРезультатыПриглашения.ПринятоОбязательнымиУчастниками Тогда
					Результат = Результат + НСтр("ru = 'Приглашения подтверждены'", КодЯзыкаПолучателя);
				Иначе
					Результат = Результат + НСтр("ru = 'Приглашения отменены'", КодЯзыкаПолучателя);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли Задача.ТочкаМаршрута = БизнесПроцессы.Приглашение.ТочкиМаршрута.Оповестить Тогда
		
		Если РезультатПриглашения = Перечисления.ОбщиеРезультатыПриглашения.ПринятоВсемиУчастниками
			Или РезультатПриглашения = Перечисления.ОбщиеРезультатыПриглашения.ПринятоОбязательнымиУчастниками Тогда 
			Результат = НСтр("ru = 'Результат рассылки приглашений: Приглашения подтверждены'", КодЯзыкаПолучателя);
		Иначе
			Результат = НСтр("ru = 'Результат рассылки приглашений: Приглашения отменены'", КодЯзыкаПолучателя);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает участников, для которых следует доформировать приглашение на мероприятие.
//
// Параметры:
//  Мероприятие - СправочникСсылка.Мероприятия - Мероприятие.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Участники мероприятия.
//
Функция ПолучитьУчастниковМероприятия(Мероприятие)
	
	// Получаем участников мероприятия.
	Участники = УправлениеМероприятиями.ПолучитьУчастниковМероприятия(Мероприятие, Истина);
	
	// Проверяем есть ли неприглашенные участники. Если таких нет - приглашем всех участников.
	УчастникБезПриглашения =
		Участники.Найти(Перечисления.СостоянияПриглашения.ПустаяСсылка(), "СостояниеПриглашения");
	ЕстьНеприглашенныеУчастники = УчастникБезПриглашения <> Неопределено;
	Если Не ЕстьНеприглашенныеУчастники Тогда
		Возврат Участники;
	КонецЕсли;
	
	// Не приглашем участников, которые уже приглашены. Приглашаем только неприглашенных участников.
	КоличествоУчастников = Участники.Количество();
	Для Инд = 1 По КоличествоУчастников Цикл
		Участник = Участники[КоличествоУчастников-Инд];
		Участник.Исполнитель = Участник.Исполнитель;
		Если Участник.СостояниеПриглашения = Перечисления.СостоянияПриглашения.ПустаяСсылка() Тогда
			Продолжить;
		КонецЕсли;
		Участники.Удалить(Участник);
	КонецЦикла;
	
	Возврат Участники;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для работы со стартом процесса

Процедура ОтложенныйСтарт() Экспорт
	
	СтартПроцессовСервер.СтартоватьПроцессОтложенно(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОтключитьОтложенныйСтарт() Экспорт
	
	СтартПроцессовСервер.ОтключитьОтложенныйСтарт(ЭтотОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для поддержки комплексных процессов

// Дополняет описание процесса общим описанием
Процедура ДополнитьОписание(ОбщееОписание) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ОбщееОписание) Тогда
		Возврат;
	КонецЕсли;
	Описание = ОбщееОписание + Символы.ПС + Описание;
	
КонецПроцедуры

// Проверяет что заполнены поля шаблона
Функция ПолучитьСписокНезаполненныхПолейНеобходимыхДляСтарта() Экспорт
	
	МассивПолей = Новый Массив;
	
	Если Исполнители.Количество() = 0 Тогда
		МассивПолей.Добавить("Исполнители");
	КонецЕсли;	
	
	Возврат МассивПолей;
	
КонецФункции	

Функция НаименованиеПроцессаПоУмолчанию(ДобавлятьНаименованиеПредмета)
	
	НаименованиеПоУмолчанию = НСтр("ru = 'Принять участие в '");
	
	Если ДобавлятьНаименованиеПредмета Тогда
		НаименованиеПоУмолчанию = МультипредметностьКлиентСервер.ПолучитьНаименованиеСПредметами(
			СокрЛП(НаименованиеПоУмолчанию), Предметы);
	КонецЕсли;
	
	Возврат НаименованиеПоУмолчанию;
	
КонецФункции


#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли