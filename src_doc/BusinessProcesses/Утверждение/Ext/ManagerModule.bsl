#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает текущих участников процесса в виде структуры
//
// Параметры:
//   Процесс
//      БизнесПроцессОбъект
//      БизнесПроцессСсылка
//
// Возвращаемое значение:
//   Структура
//
Функция ТекущиеУчастникиПроцесса(Процесс) Экспорт
	
	РеквизитыПроцессаСтрокой = 
		"Автор,
		|ОбрабатывающийРезультат,
		|СрокОбработкиРезультатов,
		|СрокОбработкиРезультатовДни,
		|СрокОбработкиРезультатовЧасы,
		|СрокОбработкиРезультатовМинуты,
		|Исполнитель,
		|СрокИсполнения,
		|СрокИсполненияДни,
		|СрокИсполненияЧасы,
		|СрокИсполненияМинуты";
	
	Участники = Новый Структура(РеквизитыПроцессаСтрокой);
		
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Процесс)) Тогда
		РеквизитыПроцессаПоСсылке = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Процесс, РеквизитыПроцессаСтрокой);
		ЗаполнитьЗначенияСвойств(Участники, РеквизитыПроцессаПоСсылке);
	Иначе
		ЗаполнитьЗначенияСвойств(Участники, Процесс);
	КонецЕсли;
	
	Возврат Участники;
	
КонецФункции

// Возвращает идентификаторы текущих участников процесса.
//
// Параметры:
//   Процесс
//      БизнесПроцессОбъект
//      БизнесПроцессСсылка
//
// Возвращаемое значение:
//   Массив
//    * УникальныйИдентификатор
//
Функция ИдентификаторыТекущихУчастниковПроцесса(Процесс) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторыУчастников = Новый Массив;
	
	РеквизитыПроцессаСтрокой = 
		"ИдентификаторИсполнителя,
		|ИдентификаторОбрабатывающегоРезультат";
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Процесс)) Тогда
		РеквизитыПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Процесс, РеквизитыПроцессаСтрокой);
	Иначе
		РеквизитыПроцесса = Процесс;
	КонецЕсли;
	
	ИдентификаторыУчастников.Добавить(РеквизитыПроцесса.ИдентификаторИсполнителя);
	ИдентификаторыУчастников.Добавить(РеквизитыПроцесса.ИдентификаторОбрабатывающегоРезультат);
	
	Возврат ИдентификаторыУчастников;
	
КонецФункции

Функция ПлановыеТрудозатратыИсполнителяЗадачи(БизнесПроцесс, Задача, ТочкаМаршрута) Экспорт
	
	РеквизитыПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(БизнесПроцесс,
		"ТрудозатратыПланИсполнителя, ТрудозатратыПланОбрабатывающегоРезультат");
		
	Трудозатраты = 0;
		
	Если ТочкаМаршрута = ТочкиМаршрута.Утвердить Тогда
		Трудозатраты = РеквизитыПроцесса.ТрудозатратыПланИсполнителя;
	ИначеЕсли ТочкаМаршрута = ТочкиМаршрута.Ознакомиться Тогда
		Трудозатраты = РеквизитыПроцесса.ТрудозатратыПланОбрабатывающегоРезультат;
	КонецЕсли;
	
	Возврат Трудозатраты;
	
КонецФункции

Функция ПредставлениеРезультатаЗадачи(ЗадачаОбъект) Экспорт
	
	Представление = "";
	
	Если ЗадачаОбъект.ТочкаМаршрута = ТочкиМаршрута.Утвердить Тогда
		
		РезультатыУтверждения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ЗадачаОбъект.БизнесПроцесс, "РезультатыУтверждения").Выгрузить();
			
		НайденнаяСтрока = РезультатыУтверждения.Найти(ЗадачаОбъект.Ссылка, "ЗадачаИсполнителя");
		Если НайденнаяСтрока <> Неопределено Тогда
			Представление = Строка(НайденнаяСтрока.РезультатУтверждения) + ".";
		КонецЕсли;
		
	ИначеЕсли ЗадачаОбъект.ТочкаМаршрута = ТочкиМаршрута.Ознакомиться Тогда
		
		РезультатыОзнакомлений = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ЗадачаОбъект.БизнесПроцесс, "РезультатыОзнакомлений").Выгрузить();
			
		НайденнаяСтрока = РезультатыОзнакомлений.Найти(ЗадачаОбъект.Ссылка, "ЗадачаИсполнителя");
		Если НайденнаяСтрока <> Неопределено Тогда
			Если НайденнаяСтрока.ОтправленоНаПовторноеУтверждение Тогда
				Представление = НСтр("ru = 'Отправлено на повторное утверждение.'");
			Иначе	
				Представление = НСтр("ru = 'Ознакомился.'");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

// Заполняет html обзор данными процесса.
//
// Параметры:
//   HTMLТекст - Строка
//   Шаблон - БизнесПроцессСсылка.Утверждение - ссылка на процесс
//
Процедура ЗаполнитьОбзорПроцесса(HTMLТекст, Процесс) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	РеквизитыПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Процесс,
		"Исполнитель,
		|СрокИсполненияПроцесса,
		|ОбрабатывающийРезультат,
		|СрокОбработкиРезультатов,
		|СрокОбработкиРезультатовДни,
		|СрокОбработкиРезультатовМинуты,
		|СрокОбработкиРезультатовЧасы,
		|СрокИсполнения,
		|СрокИсполненияДни,
		|СрокИсполненияЧасы,
		|СрокИсполненияМинуты,
		|СрокИсполненияПроцесса,
		|ВариантУстановкиСрокаИсполнения,
		|ВариантУстановкиСрокаОбработкиРезультатов,
		|КоличествоИтераций,
		|СрокИсполненияПроцесса,
		|ДатаЗавершения,
		|РезультатУтверждения,
		|Завершен");
		
	Исполнитель = РеквизитыПроцесса.Исполнитель;
	СрокИсполненияПроцесса = РеквизитыПроцесса.СрокИсполненияПроцесса;
	ДатаЗавершения = РеквизитыПроцесса.ДатаЗавершения;
	ОбрабатывающийРезультат = РеквизитыПроцесса.ОбрабатывающийРезультат;
	
	ДатыИсполненияУчастников = ДатыИсполненияУчастников(Процесс);
	
	ЦветЗакрытыеНеактуальныеЗаписи = ОбзорПроцессовВызовСервера.ЦветЗакрытыеНеактуальныеЗаписи();
	
	ЦветПросроченныеДанные = ОбзорПроцессовВызовСервера.ЦветПросроченныеДанные();
	
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	ДатаИсполненияПоУмолчанию = ТекущаяДатаСеанса;
	Если ЗначениеЗаполнено(ДатаЗавершения) Тогда
		ДатаИсполненияПоУмолчанию = ДатаЗавершения;
	КонецЕсли;
	
	ИспользоватьДатуИВремяВСрокахЗадач = ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач");
	Если ИспользоватьДатуИВремяВСрокахЗадач Тогда
		ФорматСрока = "ДФ='dd.MM.yyyy HH:mm'";
	Иначе
		ФорматСрока = "ДФ='dd.MM.yyyy'";
	КонецЕсли;
	
	// Исполнитель
	Если ЗначениеЗаполнено(Исполнитель) Тогда
		HTMLТекст = HTMLТекст + "<p>";
		ОбзорОбъектовКлиентСервер.ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Исполнитель: '"));
		
		ЗадачаИсполнителя = ОбзорПроцессовВызовСервера.ПолучитьЗадачуПроцесса(
			Процесс, БизнесПроцессы.Утверждение.ТочкиМаршрута.Утвердить);
		Если ЗначениеЗаполнено(ЗадачаИсполнителя) Тогда
			
			ЗадачаМассив = Новый Массив;
			ЗадачаМассив.Добавить(ЗадачаИсполнителя);
			РезультатСоответствие 
				= ОбзорПроцессовВызовСервера.РезультатыВыполненияПоОбъектам(ЗадачаМассив, "Задача");
			Если РезультатСоответствие.Количество() <> 0 Тогда	
					
				РезультатВыполненияЗадачи = РезультатСоответствие.Получить(ЗадачаИсполнителя);
				Если РезультатВыполненияЗадачи <> Неопределено Тогда
					
					Картинка = ОбзорПроцессовВызовСервера.ПолучитьКартинкуПоСтатусуВыполнения(
						РезультатВыполненияЗадачи);
					
					ОбзорОбъектовКлиентСервер.ДобавитьКартинку(HTMLТекст, Картинка);
					
				КонецЕсли;
					
			КонецЕсли;		
			
		КонецЕсли;	
		
		ОбзорОбъектовКлиентСервер.ДобавитьЗначение(HTMLТекст, Исполнитель, "");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыПроцесса.СрокИсполнения) 
		Или ЗначениеЗаполнено(РеквизитыПроцесса.СрокИсполненияДни) 
		Или ЗначениеЗаполнено(РеквизитыПроцесса.СрокИсполненияМинуты) 
		Или ЗначениеЗаполнено(РеквизитыПроцесса.СрокИсполненияЧасы) Тогда
		
		HTMLТекст = HTMLТекст + "<br>";
		
		ОбзорОбъектовКлиентСервер.ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Срок утверждения:'"));
		
		ПредставлениеСрока = ОбзорПроцессовВызовСервера.ПредставлениеСрокаИсполнения(
			РеквизитыПроцесса.СрокИсполнения, 
			РеквизитыПроцесса.СрокИсполненияДни, 
			РеквизитыПроцесса.СрокИсполненияЧасы, 
			РеквизитыПроцесса.СрокИсполненияМинуты, 
			ИспользоватьДатуИВремяВСрокахЗадач, 
			РеквизитыПроцесса.ВариантУстановкиСрокаИсполнения);
			
		ДатаИсполнения = ДатыИсполненияУчастников.Получить("Исполнитель");
		Если ЗначениеЗаполнено(ДатаИсполнения) Тогда
			ДатаИсполнения = Мин(ДатаИсполненияПоУмолчанию, ДатаИсполнения);
		Иначе
			ДатаИсполнения = ДатаИсполненияПоУмолчанию;
		КонецЕсли;
		
		ДатаИсполнения = ДатаИсполнения - Секунда(ДатаИсполнения);
		
		ЦветПредставленияСрока = "";
		Если РеквизитыПроцесса.СрокИсполнения < ДатаИсполнения Тогда
			ЦветПредставленияСрока = ЦветПросроченныеДанные;
		КонецЕсли;
		
		ОбзорОбъектовКлиентСервер.ДобавитьЗначение(HTMLТекст, ПредставлениеСрока, ЦветПредставленияСрока);
		
	КонецЕсли;
		
	HTMLТекст = HTMLТекст + "<p>";
	HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<A href=v8doc:%1>%2</A>",
		"Подзадачи_" + Процесс.УникальныйИдентификатор() + "_Утверждение",
		НСтр("ru = 'Все задачи'"));
	
	Если ЗначениеЗаполнено(ОбрабатывающийРезультат) Тогда
		HTMLТекст = HTMLТекст + "<p>";
		ОбзорОбъектовКлиентСервер.ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Обрабатывающий результат: '"));
		ОбзорОбъектовКлиентСервер.ДобавитьЗначение(HTMLТекст, ОбрабатывающийРезультат, "");
	КонецЕсли;
		
	Если ЗначениеЗаполнено(РеквизитыПроцесса.СрокОбработкиРезультатов) 
		Или ЗначениеЗаполнено(РеквизитыПроцесса.СрокОбработкиРезультатовДни) 
		Или ЗначениеЗаполнено(РеквизитыПроцесса.СрокОбработкиРезультатовМинуты) 
		Или ЗначениеЗаполнено(РеквизитыПроцесса.СрокОбработкиРезультатовЧасы) Тогда
		
		HTMLТекст = HTMLТекст + "<p>";
		
		ОбзорОбъектовКлиентСервер.ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Срок обработки результата:'"));
		
		ПредставлениеСрока = ОбзорПроцессовВызовСервера.ПредставлениеСрокаИсполнения(
			РеквизитыПроцесса.СрокОбработкиРезультатов, 
			РеквизитыПроцесса.СрокОбработкиРезультатовДни, 
			РеквизитыПроцесса.СрокОбработкиРезультатовЧасы, 
			РеквизитыПроцесса.СрокОбработкиРезультатовМинуты, 
			ИспользоватьДатуИВремяВСрокахЗадач, 
			РеквизитыПроцесса.ВариантУстановкиСрокаОбработкиРезультатов);
			
		ЦветПредставленияСрока = "";
		Если Не РеквизитыПроцесса.Завершен
			Или РеквизитыПроцесса.РезультатУтверждения = Перечисления.РезультатыУтверждения.НеУтверждено Тогда
			
			ДатаИсполнения = ДатыИсполненияУчастников.Получить("ОбрабатывающийРезультат");
			Если ЗначениеЗаполнено(ДатаИсполнения) Тогда
				ДатаИсполнения = Мин(ДатаИсполненияПоУмолчанию, ДатаИсполнения);
			Иначе
				ДатаИсполнения = ДатаИсполненияПоУмолчанию;
			КонецЕсли;
			
			ДатаИсполнения = ДатаИсполнения - Секунда(ДатаИсполнения);
			
			Если РеквизитыПроцесса.СрокОбработкиРезультатов < ДатаИсполнения Тогда
				ЦветПредставленияСрока = ЦветПросроченныеДанные;
			КонецЕсли;
			
		КонецЕсли;
		
		ОбзорОбъектовКлиентСервер.ДобавитьЗначение(HTMLТекст, ПредставлениеСрока, ЦветПредставленияСрока);
		
	КонецЕсли;
		
	Если ЗначениеЗаполнено(СрокИсполненияПроцесса) Тогда
		
		HTMLТекст = HTMLТекст + "<p>";
		
		Если Не ЗначениеЗаполнено(ДатаЗавершения) Тогда
			ДатаЗавершения = ТекущаяДатаСеанса;
		КонецЕсли;
		
		ДатаЗавершения = ДатаЗавершения - Секунда(ДатаЗавершения);
		
		ЦветПредставленияСрока = "";
		Если СрокИсполненияПроцесса < ДатаЗавершения Тогда
			ЦветПредставленияСрока = ЦветПросроченныеДанные;
		КонецЕсли;
		
		ОбзорОбъектовКлиентСервер.ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Срок процесса:'"));
		ОбзорОбъектовКлиентСервер.ДобавитьЗначение(
			HTMLТекст, Формат(СрокИсполненияПроцесса, ФорматСрока), ЦветПредставленияСрока);
		
		Если РеквизитыПроцесса.КоличествоИтераций <> 0 Тогда
			
			HTMLТекст = HTMLТекст + " (";
			
			ОбзорОбъектовКлиентСервер.ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Кол. циклов:'"));
			ОбзорОбъектовКлиентСервер.ДобавитьЗначение(HTMLТекст, 
				Формат(РеквизитыПроцесса.КоличествоИтераций, "ЧЦ=2"), "");
			
			HTMLТекст = HTMLТекст + ")";
			
		КонецЕсли;		
		
	КонецЕсли;
		
КонецПроцедуры

// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - ТаблицаЗначений - Таблица с командами заполнения. Для изменения.
//       См. описание 1 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
КонецПроцедуры

// Возвращает признак того что процесс использует условия выполнения задач.
//
// Параметры:
//  ТочкаМаршрута - ТочкаМаршрутаСсылка - Точка маршрута.
//  Параметры - Структура - Параметры.
// 
// Возвращаемое значение:
//  Булево - Использует условия выполнения задач.
//
Функция ИспользуетУсловияЗапретаВыполненияЗадач(ТочкаМаршрута = Неопределено, Параметры = Неопределено) Экспорт
	
	Если Параметры = Неопределено Тогда
		Параметры = Новый Структура;
	КонецЕсли;
	
	ИспользуетУсловияЗапретаВыполненияЗадач = Истина;
	
	Если ТочкаМаршрута = ТочкиМаршрута.Утвердить Тогда
		
		Если Параметры.Свойство("ВариантВыполнения") Тогда
			ИспользуетУсловияЗапретаВыполненияЗадач =
				Параметры.ВариантВыполнения <>
					Перечисления.ВариантыВыполненияПроцессовИЗадач.Отрицательно;
		ИначеЕсли Параметры.Свойство("РезультатУтверждения") Тогда
			ИспользуетУсловияЗапретаВыполненияЗадач =
				Параметры.РезультатУтверждения <>
					Перечисления.РезультатыУтверждения.НеУтверждено;
		КонецЕсли;
		
	ИначеЕсли ТочкаМаршрута = ТочкиМаршрута.Ознакомиться Тогда
		
		Если Параметры.Свойство("ВариантВыполнения") Тогда
			ИспользуетУсловияЗапретаВыполненияЗадач =
				Параметры.ВариантВыполнения <>
					Перечисления.ВариантыВыполненияПроцессовИЗадач.Отрицательно;
		ИначеЕсли Параметры.Свойство("ПовторитьУтверждение") Тогда
			ИспользуетУсловияЗапретаВыполненияЗадач = Не Параметры.ПовторитьУтверждение;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИспользуетУсловияЗапретаВыполненияЗадач;
	
КонецФункции

// Формирует комментарий автоматического выполнения задачи.
//
// Параметры:
//  ТочкаМаршрута - ТочкаМаршрута - Точка маршрута.
//  ВариантВыполнения - Булево - Вариант выполнения.
//
// Возвращаемое значение:
//  Строка - Комментарий автоматического выполнения задачи.
//
Функция КомментарийВыполненаАвтоматически(ТочкаМаршрута, ВариантВыполнения) Экспорт
	
	Комментарий = "";
	
	Если ТочкаМаршрута = ТочкиМаршрута.Утвердить Тогда
		Если ВариантВыполнения Тогда
			Комментарий = НСтр("ru = 'Утверждено автоматически'");
		Иначе
			Комментарий = НСтр("ru = 'Не утверждено автоматически'");
		КонецЕсли;
	ИначеЕсли ТочкаМаршрута = ТочкиМаршрута.Ознакомиться Тогда
		Комментарий = НСтр("ru = 'Ознакомился автоматически'");
	КонецЕсли;
	
	Возврат Комментарий;
	
КонецФункции

// Заполняет html обзор задачи данными процесса.
//
// Параметры:
//   HTMLТекст - Строка
//   Процесс - БизнесПроцессСсылка
//   ТочкаМаршрута - ТочкаМаршрутаБизнесПроцессаСсылка
//   ЗадачаПроцесса - ЗадачаСсылка.ЗадачаИсполнителя
//   ВключатьНавигационныеСсылки - Булево
//   КодЯзыка - Строка
//   КешДанных - см. ОбработкаЗапросовXDTOБизнесПроцессыИЗадачи.ДополнитьДанныеЗадачИСформироватьКешДанных
//
Процедура ЗаполнитьОбзорЗадачи(HTMLТекст, Процесс, ТочкаМаршрута, ЗадачаПроцесса, ВключатьНавигационныеСсылки, КодЯзыка,
		КешДанных = Неопределено) Экспорт
	
	// Нет специального заполнения.
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_РабочиеГруппы

// Возвращает признак наличия метода ДобавитьУчастниковВТаблицу у менеджера объекта
//
Функция ЕстьМетодДобавитьУчастниковВТаблицу() Экспорт
	Возврат Истина;
КонецФункции

// Добавляет участников бизнес-процесса в переданную таблицу
//
Процедура ДобавитьУчастниковВТаблицу(ТаблицаНабора, БизнесПроцесс) Экспорт
	
	РаботаСБизнесПроцессами.ДобавитьУчастниковУтвержденияВТаблицу(ТаблицаНабора, БизнесПроцесс);
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_ВыполнениеЗадач

// Обработчик, вызываемый перед выполнением задачи из процедуры
// ВыполнениеЗадачСервер.ВыполнитьЗадачуСПараметрами
//
// Параметры
//   ЗадачаСсылка  - ЗадачаСсылка.ЗадачаИсполнителя
//   БизнесПроцессСсылка - БизнесПроцессСсылка
//   ТочкаМаршрутаСсылка - точка маршрута
//   ПараметрыВыполнения - Структура
//
Процедура ОбработкаПередВыполнениемЗадачи(
	ЗадачаСсылка, БизнесПроцессСсылка, ТочкаМаршрутаБизнесПроцесса, ПараметрыВыполнения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПараметрыВыполнения.Свойство("ИдентификаторБлокировкиПроцесса") Тогда
		ПараметрыВыполнения.Вставить("ИдентификаторБлокировкиПроцесса", Новый УникальныйИдентификатор());
	КонецЕсли;
	
	Если ТочкаМаршрутаБизнесПроцесса = БизнесПроцессы.Утверждение.ТочкиМаршрута.Утвердить Тогда
		
		ЗаписатьПроцесс = ПараметрыВыполнения.Свойство("РезультатУтверждения")
			Или ПараметрыВыполнения.Свойство("ИсполнительЗадачи");
		
		Если ЗаписатьПроцесс Тогда
			ЗаблокироватьДанныеДляРедактирования(БизнесПроцессСсылка,, ПараметрыВыполнения.ИдентификаторБлокировкиПроцесса);
			УтверждениеОбъект = БизнесПроцессСсылка.ПолучитьОбъект();
		КонецЕсли;
		
		Если ПараметрыВыполнения.Свойство("РезультатУтверждения") Тогда
			УтверждениеОбъект.ДополнительныеСвойства.Вставить("ТекущаяЗадача", ЗадачаСсылка);
			УтверждениеОбъект.ДополнительныеСвойства.Вставить(
				"РезультатУтверждения", ПараметрыВыполнения.РезультатУтверждения);
		КонецЕсли;
		
		Если ПараметрыВыполнения.Свойство("ИсполнительЗадачи")
			И ТипЗнч(УтверждениеОбъект.Исполнитель) <> Тип("СправочникСсылка.ПолныеРоли")
			И УтверждениеОбъект.Исполнитель <> ПараметрыВыполнения.ИсполнительЗадачи Тогда
			
			УтверждениеОбъект.Исполнитель = ПараметрыВыполнения.ИсполнительЗадачи;
			
		КонецЕсли;
		
		Если ЗаписатьПроцесс Тогда
			РаботаСБизнесПроцессами.ЗаписатьПроцесс(УтверждениеОбъект, "ЗаписьСОбработкойВыполненияЗадачи");
			РазблокироватьДанныеДляРедактирования(БизнесПроцессСсылка, ПараметрыВыполнения.ИдентификаторБлокировкиПроцесса);
		КонецЕсли;
			
		Если ПараметрыВыполнения.Свойство("ПодписиЭПКПредметам")
			И Тип("Соответствие") = ТипЗнч(ПараметрыВыполнения.ПодписиЭПКПредметам) Тогда
			
			ДействиеЗадачи = Документы.ДействиеЗадачи.НайтиПоИсточнику(ЗадачаСсылка);
			
			ПодписиПоДействию = ПараметрыВыполнения.ПодписиЭПКПредметам.Получить(ДействиеЗадачи);
			Если ТипЗнч(ПодписиПоДействию) = Тип("Массив") Тогда
				РаботаСЭП.ЗанестиИнформациюОПодписях(ПодписиПоДействию);
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТочкаМаршрутаБизнесПроцесса = БизнесПроцессы.Утверждение.ТочкиМаршрута.Ознакомиться Тогда
		
		ЗаписатьПроцесс = ПараметрыВыполнения.Свойство("ПовторитьУтверждение")
			Или ПараметрыВыполнения.Свойство("ИсполнительЗадачи");
			
		Если ЗаписатьПроцесс Тогда
			ЗаблокироватьДанныеДляРедактирования(БизнесПроцессСсылка,, ПараметрыВыполнения.ИдентификаторБлокировкиПроцесса);
			УтверждениеОбъект = БизнесПроцессСсылка.ПолучитьОбъект();
		КонецЕсли;
			
		Если ПараметрыВыполнения.Свойство("ПовторитьУтверждение") Тогда
			
			ДействиеПроцесса = РегистрыСведений.ПроцессыДействий.ДействиеПоПроцессу(БизнесПроцессСсылка);
			ЕстьДействие = ЗначениеЗаполнено(ДействиеПроцесса);
			
			УтверждениеОбъект.ДополнительныеСвойства.Вставить("ТекущаяЗадача", ЗадачаСсылка);
			УтверждениеОбъект.ДополнительныеСвойства.Вставить("ПовторитьУтверждение", ПараметрыВыполнения.ПовторитьУтверждение);
			Если Не ЕстьДействие И ПараметрыВыполнения.Свойство("Исполнитель") Тогда
				УтверждениеОбъект.ДополнительныеСвойства.Вставить("Исполнитель", ПараметрыВыполнения.Исполнитель);
			КонецЕсли;
			
			Если Не ЕстьДействие И ПараметрыВыполнения.Свойство("СрокИсполненияПроцесса") Тогда
				УтверждениеОбъект.ДополнительныеСвойства.Вставить("СрокИсполненияПроцесса", ПараметрыВыполнения.СрокИсполненияПроцесса);
			КонецЕсли;
			
			Если Не ЕстьДействие И ПараметрыВыполнения.Свойство("СрокИсполнения") Тогда
				УтверждениеОбъект.ДополнительныеСвойства.Вставить("СрокИсполнения", ПараметрыВыполнения.СрокИсполнения);
			КонецЕсли;
			Если Не ЕстьДействие И ПараметрыВыполнения.Свойство("СрокИсполненияДни") Тогда
				УтверждениеОбъект.ДополнительныеСвойства.Вставить("СрокИсполненияДни", ПараметрыВыполнения.СрокИсполненияДни);
			КонецЕсли;
			Если Не ЕстьДействие И ПараметрыВыполнения.Свойство("СрокИсполненияЧасы") Тогда
				УтверждениеОбъект.ДополнительныеСвойства.Вставить("СрокИсполненияЧасы", ПараметрыВыполнения.СрокИсполненияЧасы);
			КонецЕсли;
			Если Не ЕстьДействие И ПараметрыВыполнения.Свойство("СрокИсполненияМинуты") Тогда
				УтверждениеОбъект.ДополнительныеСвойства.Вставить("СрокИсполненияМинуты", ПараметрыВыполнения.СрокИсполненияМинуты);
			КонецЕсли;
			Если Не ЕстьДействие И ПараметрыВыполнения.Свойство("ВариантУстановкиСрокаИсполнения") Тогда
				УтверждениеОбъект.ДополнительныеСвойства.Вставить("ВариантУстановкиСрокаИсполнения", ПараметрыВыполнения.ВариантУстановкиСрокаИсполнения);
			КонецЕсли;
			
			Если Не ЕстьДействие И ПараметрыВыполнения.Свойство("СрокОбработкиРезультатов") Тогда
				УтверждениеОбъект.ДополнительныеСвойства.Вставить("СрокОбработкиРезультатов", ПараметрыВыполнения.СрокОбработкиРезультатов);
			КонецЕсли;
			Если Не ЕстьДействие И ПараметрыВыполнения.Свойство("СрокОбработкиРезультатовДни") Тогда
				УтверждениеОбъект.ДополнительныеСвойства.Вставить("СрокОбработкиРезультатовДни", ПараметрыВыполнения.СрокОбработкиРезультатовДни);
			КонецЕсли;
			Если Не ЕстьДействие И ПараметрыВыполнения.Свойство("СрокОбработкиРезультатовЧасы") Тогда
				УтверждениеОбъект.ДополнительныеСвойства.Вставить("СрокОбработкиРезультатовЧасы", ПараметрыВыполнения.СрокОбработкиРезультатовЧасы);
			КонецЕсли;
			Если Не ЕстьДействие И ПараметрыВыполнения.Свойство("СрокОбработкиРезультатовМинуты") Тогда
				УтверждениеОбъект.ДополнительныеСвойства.Вставить("СрокОбработкиРезультатовМинуты", ПараметрыВыполнения.СрокОбработкиРезультатовМинуты);
			КонецЕсли;
			Если Не ЕстьДействие И ПараметрыВыполнения.Свойство("ВариантУстановкиСрокаОбработкиРезультатов") Тогда
				УтверждениеОбъект.ДополнительныеСвойства.Вставить("ВариантУстановкиСрокаОбработкиРезультатов", ПараметрыВыполнения.ВариантУстановкиСрокаОбработкиРезультатов);
			КонецЕсли;
			
			Если ПараметрыВыполнения.Свойство("ПричинаПереносаСрока") Тогда
				УтверждениеОбъект.ДополнительныеСвойства.Вставить("ПричинаПереносаСрока", ПараметрыВыполнения.ПричинаПереносаСрока);
			КонецЕсли;
			
			Если Не ЕстьДействие И ПараметрыВыполнения.Свойство("КоличествоИтераций") Тогда
				УтверждениеОбъект.ДополнительныеСвойства.Вставить("КоличествоИтераций", ПараметрыВыполнения.КоличествоИтераций);
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПараметрыВыполнения.Свойство("ИсполнительЗадачи")
			И УтверждениеОбъект.ОбрабатывающийРезультат <> ПараметрыВыполнения.ИсполнительЗадачи Тогда
			
			УтверждениеОбъект.ОбрабатывающийРезультат = ПараметрыВыполнения.ИсполнительЗадачи;
		КонецЕсли;
		
		Если ЗаписатьПроцесс Тогда
			РаботаСБизнесПроцессами.ЗаписатьПроцесс(УтверждениеОбъект, "ЗаписьСОбработкойВыполненияЗадачи");
			РазблокироватьДанныеДляРедактирования(БизнесПроцессСсылка, ПараметрыВыполнения.ИдентификаторБлокировкиПроцесса);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик, вызываемый после выполнением задачи из процедуры
// ВыполнениеЗадачСервер.ВыполнитьЗадачуСПараметрами
//
// Параметры
//   ЗадачаСсылка  - ЗадачаСсылка.ЗадачаИсполнителя
//   БизнесПроцессСсылка - БизнесПроцессСсылка
//   ТочкаМаршрутаСсылка - точка маршрута
//   ПараметрыВыполнения - Структура
//
Процедура ОбработкаПослеВыполненияЗадачи(
	ЗадачаСсылка, БизнесПроцессСсылка, ТочкаМаршрутаБизнесПроцесса, ПараметрыВыполнения) Экспорт

КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_ПоддержкаКомплексныхПроцессов

#Область КэшДанныхДействий

// Возвращает выборку данных действий.
//
// Параметры:
//  ТаблицаДействий - ТаблицаЗначений
//   * Действие - ОпределяемыйТип.ДействиеКомплексногоПроцесса
//
// Возвращаемое значение:
//  ВыборкаДанных
//
Функция ВыборкаДанныхДействий(ТаблицаДействий) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаДействий.Действие
		|ПОМЕСТИТЬ ТаблицаДействий
		|ИЗ
		|	&ТаблицаДействий КАК ТаблицаДействий
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Утверждение.Ссылка,
		|	Утверждение.Наименование,
		|	Утверждение.Исполнитель,
		|	Утверждение.СрокИсполненияПроцесса,
		|	Утверждение.ДатаЗавершения,
		|	Утверждение.Состояние,
		|	Утверждение.Завершен
		|ИЗ
		|	ТаблицаДействий КАК ТаблицаДействий
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.Утверждение КАК Утверждение
		|		ПО ТаблицаДействий.Действие = Утверждение.Ссылка";
		
	Запрос.УстановитьПараметр("ТаблицаДействий", ТаблицаДействий);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

// Возвращает данные процесса, являющегося действием комплексного процесса.
//
// Параметры:
//  Объект - БизнесПроцессСсылка.Согласование, ВыборкаДанных
//
// Возвращаемое значение:
//  Структура - см. функцию РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураДанныхДействия
//
Функция ДанныеДействия(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеПроцесса = РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураДанныхДействия();
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Объект)) Тогда
		РеквизитыОбъекта = 
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				Объект, "Наименование, Исполнитель, СрокИсполненияПроцесса,
						|Состояние, Завершен, ДатаЗавершения");
	Иначе
		РеквизитыОбъекта = Объект;
	КонецЕсли;
	
	ДанныеПроцесса.Описание = НСтр("ru = 'Утверждение: '") + РеквизитыОбъекта.Наименование;
	
	Если ЗначениеЗаполнено(РеквизитыОбъекта.СрокИсполненияПроцесса) Тогда
		ФорматДаты = СрокиИсполненияПроцессовКлиентСервер.ФорматДатыСроковПроцессовИЗадач(
			Константы.ИспользоватьДатуИВремяВСрокахЗадач.Получить());
		ДанныеПроцесса.Описание = ДанныеПроцесса.Описание
			+ Символы.ПС
			+ НСтр("ru = 'Срок: '")
			+ Формат(РеквизитыОбъекта.СрокИсполненияПроцесса, ФорматДаты);
	КонецЕсли;
	
	МассивИсполнителей = Новый Массив;
	РаботаСКомплекснымиБизнесПроцессамиСервер.ДобавитьИсполнителяПроцессаВМассив(
		МассивИсполнителей, РеквизитыОбъекта.Исполнитель);
		
	ДанныеПроцесса.Исполнители = 
		РаботаСКомплекснымиБизнесПроцессамиСервер.ИсполнителиСтрокой(МассивИсполнителей);
		
	ДанныеПроцесса.СостояниеПроцесса = РеквизитыОбъекта.Состояние;
	
	ДанныеПроцесса.ПроцессЗавершен = РеквизитыОбъекта.Завершен;
	
	ДанныеПроцесса.ДатаЗавершения = РеквизитыОбъекта.ДатаЗавершения;
	
	Возврат ДанныеПроцесса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ПрограммныйИнтерфейс_Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс_СрокиИсполненияПроцессов

// Возвращает даты исполнения участников процесса.
//
// Параметры:
//  Процесс - БизнесПроцессСсылка.Утверждение - ссылка на процесс
//
// Возвращаемое значение:
//  Соответствие
//   * Ключ - Строка, ЗадачаСсылка.ЗадачаИсполнителя - имя реквизита с участником процесса или ссылка на его задачу.
//
Функция ДатыИсполненияУчастников(Процесс) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДатыИсполнения = Новый Соответствие;
	
	НомерИтерации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		Процесс, "НомерИтерации");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Процесс", Процесс);
	Запрос.УстановитьПараметр("НомерИтерации", НомерИтерации);
	
	// Получение дата исполнения исполнителями.
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Задачи.ДатаИсполнения КАК ДатаИсполнения
		|ИЗ
		|	БизнесПроцесс.Утверждение.РезультатыУтверждения КАК РезультатыУтверждения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Задача.ЗадачаИсполнителя КАК Задачи
		|		ПО РезультатыУтверждения.ЗадачаИсполнителя = Задачи.Ссылка
		|ГДЕ
		|	РезультатыУтверждения.Ссылка = &Процесс
		|	И РезультатыУтверждения.НомерИтерации = &НомерИтерации";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДатыИсполнения.Вставить("Исполнитель", Выборка.ДатаИсполнения);
	КонецЕсли;
	
	// Получение даты исполнения обработки результов.
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Задачи.ДатаИсполнения КАК ДатаИсполнения
		|ИЗ
		|	БизнесПроцесс.Утверждение.РезультатыОзнакомлений КАК РезультатыОзнакомлений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Задача.ЗадачаИсполнителя КАК Задачи
		|		ПО РезультатыОзнакомлений.ЗадачаИсполнителя = Задачи.Ссылка
		|ГДЕ
		|	РезультатыОзнакомлений.Ссылка = &Процесс
		|	И РезультатыОзнакомлений.НомерИтерации = &НомерИтерации";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДатыИсполнения.Вставить("ОбрабатывающийРезультат", Выборка.ДатаИсполнения);
	КонецЕсли;
	
	Возврат ДатыИсполнения;
	
КонецФункции

#КонецОбласти

#Область ПрограммныйИнтерфейс_УчетПереносовСроковИсполнения

// Устанавливает срок исполнителю задачи в процессе.
//
// Параметры:
//  Срок - Дата - новый срок исполнителя.
//  Задача - ЗадачаСсылка.ЗадачаИсполнителя - задача исполнителя.
//  Процесс - БизнесПроцессОбъект - изменяемый процесс.
//  ПараметрыИзмененногоРеквизита - Структура - параметры измененного реквизита процесса.
//   * РеквизитТаблицаСИзмененнымСроком - Строка - имя реквизита или табличной части процесса.
//   * ИндексСтроки - Число - индекс строки табличной части. Присутствует если изменяется табличная часть.
//
Процедура УстановитьСрокИсполнителяЗадачиВПроцессе(
	Срок, Задача, Процесс, ПараметрыИзмененногоРеквизита) Экспорт
	
	ПараметрыИзмененногоРеквизита = Новый Структура;
	ПараметрыИзмененногоРеквизита.Вставить("РеквизитТаблицаСИзмененнымСроком", "");
	ПараметрыИзмененногоРеквизита.Вставить("ИндексСтроки", 0);
	
	ТочкаМаршрутаЗадачи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "ТочкаМаршрута");
	
	Если ТочкаМаршрутаЗадачи = ТочкиМаршрута.Утвердить Тогда
		Процесс.СрокИсполнения = Срок;
		
		Процесс.ВариантУстановкиСрокаИсполнения = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
		
		ПараметрыИзмененногоРеквизита.РеквизитТаблицаСИзмененнымСроком = "СрокИсполнения";
		
	ИначеЕсли ТочкаМаршрутаЗадачи = ТочкиМаршрута.Ознакомиться Тогда
		Процесс.СрокОбработкиРезультатов = Срок;
		
		Процесс.ВариантУстановкиСрокаОбработкиРезультатов = 
			Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
			
		ПараметрыИзмененногоРеквизита.РеквизитТаблицаСИзмененнымСроком = "СрокОбработкиРезультатов";
	КонецЕсли;
	
КонецПроцедуры

// Переносит срок исполнения задачи процесса по заявке.
// При переносе обновляются сроки в текущем и зависимых процессах.
//
// Параметры:
//	Задача - ЗадачаСсылка.ЗадачаИсполнителя - Ссылка на задачу.
//	ПараметрыВыполнения - Структура:
//		* НовыйСрок - Дата - Новый срок задачи.
//		* Процесс - БизнесПроцессСсылка - Ссылка на процесс задачи.
//		* ПричинаПереносаСрока - Строка - Причина, по которой переносится срок задачи.
//		* ЗаявкаНаПереносСрока - БизнесПроцессСсылка.РешениеВопросовВыполненияЗадач - Заявка на перенос срока.
//
Процедура ИзменитьСрокИсполненияЗадачи(Задача, ПараметрыВыполнения) Экспорт
	
	Процесс = ПараметрыВыполнения.Процесс;
	НовыйСрок = ПараметрыВыполнения.НовыйСрок;
	ЗаявкаНаПеренос = ПараметрыВыполнения.ЗаявкаНаПереносСрока;
	ПричинаПереносаСрока = ПараметрыВыполнения.ПричинаПереносаСрока;
	
	Если РаботаСПроцессамиПоДействиямСобытия.ОбработатьИзмениеСрокаИсполненияЗадачи(
		НовыйСрок, Задача, ПричинаПереносаСрока, ЗаявкаНаПеренос) Тогда
		
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	ЗаблокироватьДанныеДляРедактирования(Процесс);
	
	ПроцессОбъект = Процесс.ПолучитьОбъект();
	
	СтарыеУчастникиПроцесса = ТекущиеУчастникиПроцесса(ПроцессОбъект);
	
	ПараметрыИзмененногоРеквизита = Новый Структура;
	
	УстановитьСрокИсполнителяЗадачиВПроцессе(
		НовыйСрок, Задача, ПроцессОбъект, ПараметрыИзмененногоРеквизита);
	
	Если ПараметрыИзмененногоРеквизита.Количество() > 0 Тогда
		
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("ПричинаПереносаСрока", ПричинаПереносаСрока);
		ПараметрыЗаписи.Вставить("БизнесПроцессПереноса", ЗаявкаНаПеренос);
		
		ПереносСроковВыполненияЗадач.ПередатьПричинуИЗаявкуНаПереносаСрока(
			ПроцессОбъект, ПараметрыЗаписи);
		
		ПараметрыДляРасчетаСроков = СрокиИсполненияПроцессов.ПараметрыДляРасчетаСроков();
		
		ПараметрыДляРасчетаСроков.ДатаОтсчета = 
			СрокиИсполненияПроцессов.ДатаОтсчетаДляРасчетаСроковПроцесса(ПроцессОбъект);
			
		ПараметрыДляРасчетаСроков.РеквизитТаблицаСИзмененнымСроком = 
			ПараметрыИзмененногоРеквизита.РеквизитТаблицаСИзмененнымСроком;
			
		ПараметрыДляРасчетаСроков.ТекущаяИтерация = ПроцессОбъект.НомерИтерации;
		
		СрокиИсполненияПроцессов.РассчитатьСрокиУтверждения(ПроцессОбъект, ПараметрыДляРасчетаСроков);
		
		РаботаСБизнесПроцессами.ЗаписатьПроцесс(
			ПроцессОбъект, "ЗаписьСОбновлениемОбщегоСпискаИПереносомСроковВИерархииПроцессов");
		
		ПроцессОбъект.ИзменитьРеквизитыНевыполненныхЗадач(СтарыеУчастникиПроцесса, ПараметрыЗаписи);
	КонецЕсли;
	
	РазблокироватьДанныеДляРедактирования(Процесс);
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ЗадачиПроцессов

// Проверяет является ли переданная задача процесса служебной.
//
// Параметры:
//  Задача - ЗадачаОбъект.ЗадачаИсполнителя,
//           ЗадачаСсылка.ЗадачаИсполнителя,
//           Структура со свойствами Исполнитель, РольИсполнителя, ТочкаМаршрута, Наименование
//
// Возвращаемое значение:
//  Булево
//
Функция ЭтаСлужебнаяЗадачаПроцесса(ЗадачаПроцесса) Экспорт
	
	// Служебных задач не предусмотрено.
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область Предметы

// Возвращает участников для проверки прав на предметы.
//
// Параметры:
//  Процесс - БизнесПроцессОбъект, БизнесПроцессСсылка - процесс
//
// Возвращаемое значение:
//  ТаблицаЗначений
//   * Участник
//   * Изменение
//
Функция УчастникиДляПроверкиПрав(Процесс) Экспорт
	
	ТаблицаУчастников = РаботаСРабочимиГруппами.ПолучитьПустуюТаблицуУчастников();
	
	РаботаСБизнесПроцессами.ДобавитьУчастниковУтвержденияВТаблицу(ТаблицаУчастников, Процесс);
	
	Возврат ТаблицаУчастников;
	
КонецФункции

// Возвращает массив доступных типов основных предметов
Функция ТипыОсновныхПредметов() Экспорт
	
	ТипыПредметов = Новый Массив;
	
	ТипыПредметов.Добавить(Тип("СправочникСсылка.ДокументыПредприятия"));  
	ТипыПредметов.Добавить(Тип("СправочникСсылка.Контрагенты")); 
	ТипыПредметов.Добавить(Тип("СправочникСсылка.Мероприятия")); 
	ТипыПредметов.Добавить(Тип("СправочникСсылка.Проекты")); 
	ТипыПредметов.Добавить(Тип("СправочникСсылка.ПроектныеЗадачи"));
	ТипыПредметов.Добавить(Тип("СправочникСсылка.СообщенияОбсуждений")); 
	ТипыПредметов.Добавить(Тип("СправочникСсылка.ТемыОбсуждений")); 
	ТипыПредметов.Добавить(Тип("СправочникСсылка.Файлы")); 
	
	ТипыПредметов.Добавить(Тип("ДокументСсылка.ВходящееПисьмо")); 
	ТипыПредметов.Добавить(Тип("ДокументСсылка.ИсходящееПисьмо")); 
	
	Возврат ТипыПредметов;
	
КонецФункции

// Возвращает доступные для процесса роли предметов
Функция ПолучитьДоступныеРолиПредметов() Экспорт
	
	РолиПредметов = Новый Массив;
	
	РолиПредметов.Добавить(Перечисления.РолиПредметов.Основной);
	РолиПредметов.Добавить(Перечисления.РолиПредметов.Вспомогательный);
	
	Возврат РолиПредметов;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка) Экспорт
	
	ПолучениеФормыОбработано = РаботаСПроцессамиПоДействиямСобытия.ОбработатьПолучениеФормыПроцесса(
		ВидФормы, Параметры, ВыбраннаяФорма, СтандартнаяОбработка);
	ПолучениеФормыОбработано = Ложь;
	
	Если Не ПолучениеФормыОбработано Тогда
	
		Если ВидФормы = "ФормаСписка" Тогда
			
			СтандартнаяОбработка = Ложь;
			Параметры.Вставить("ТипПроцесса", "Утверждение");
			ВыбраннаяФорма = Метаданные.ОбщиеФормы.СписокПроцессов;
			
		ИначеЕсли ВидФормы = "ФормаВыбора" Тогда
			
			СтандартнаяОбработка = Ложь;
			Параметры.Вставить("Заголовок", НСтр("ru = 'Утверждения'"));
			Параметры.Вставить("ТипПроцесса", Тип("БизнесПроцессСсылка.Утверждение"));
			ВыбраннаяФорма = Метаданные.ОбщиеФормы.ВыборБизнесПроцесса;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

#Область ПраваДоступа

Функция ПолучитьПоляДоступа() Экспорт
	
	Возврат "Ссылка";
	
КонецФункции

// Заполняет переданный дескриптор доступа 
Процедура ЗаполнитьОсновнойДескриптор(ОбъектДоступа, ДескрипторДоступа) Экспорт
	
	ДескрипторДоступа.ОбъектДоступа = ОбъектДоступа.Ссылка;
	
КонецПроцедуры

Функция ЕстьМетодЗаполнитьДескрипторыОбъекта() Экспорт
	
	Возврат Истина;
	
КонецФункции

Процедура ЗаполнитьДескрипторыОбъекта(ОбъектДоступа, ТаблицаДескрипторов, ПротоколРасчетаПрав = Неопределено) Экспорт
	
	// Только основной дескриптор, без рабочей группы.
	ДокументооборотПраваДоступа.ЗаполнитьДескрипторОбъектаОсновной(ОбъектДоступа, ТаблицаДескрипторов);
	
КонецПроцедуры

// Возвращает признак того, что менеджер содержит метод ЗапросДляРасчетаПрав()
// 
Функция ЕстьМетодЗапросДляРасчетаПрав() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает запрос для расчета прав доступа по дескрипторам объекта
// 
// Параметры:
//  
//  Дескрипторы - Массив - массив дескрипторов, чьи права нужно рассчитать
//  ИдОбъекта - Ссылка - идентификатор объекта метаданных, назначенный переданным дескрипторам
//  МенеджерОбъектаДоступа - СправочникМенеджер, ДокументМенеджер - менеджер объекта доступа
// 
// Возвращаемое значение - Запрос - запрос, который выберет права доступа для переданного массива дескрипторов
// 
Функция ЗапросДляРасчетаПрав(Дескрипторы, ИдОбъекта, МенеджерОбъектаДоступа) Экспорт
	
	Возврат БизнесПроцессыИЗадачиСервер.ЗапросДляРасчетаПрав(Дескрипторы, ИдОбъекта, МенеджерОбъектаДоступа);
	
КонецФункции

// Заполняет протокол расчета прав дескрипторов
// 
// Параметры:
//  
//  ПротоколРасчетаПрав - Массив - протокол для заполнения
//  ЗапросПоПравам - Запрос - запрос, который использовался для расчета прав дескрипторов
//  Дескрипторы - Массив - массив дескрипторов, чьи права были рассчитаны
//  
Процедура ЗаполнитьПротоколРасчетаПрав(ПротоколРасчетаПрав, ЗапросПоПравам) Экспорт
	
	БизнесПроцессыИЗадачиСервер.ЗаполнитьПротоколРасчетаПрав(ПротоколРасчетаПрав, ЗапросПоПравам);
	
КонецПроцедуры

// Проверяет наличие метода.
// 
// Возвращаемое значение:
//  Булево -
Функция ЕстьМетодПраваПравообладателейПоФайлам() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает права доступа правообладателей, т.е. сотрудников или пользователей, к переданным файлам.
// 
// Параметры:
//	Файлы - Массив Из СправочникСсылка.Файлы -
//	СотрудникиОтбор - Массив Из СправочникСсылка.Сотрудники - Сотрудники для отбора.
//					- Неопределено -
//	ПользователиОтбор - Массив Из СправочникСсылка.Пользователи - Пользователи для отбора.
//					  - Неопределено -
//
// Возвращаемое значение:
//  См. ДокументооборотПраваДоступа.ТаблицаПравСотрудниковПоОбъектам
Функция ПраваПравообладателейПоФайлам(Файлы, СотрудникиОтбор = Неопределено, ПользователиОтбор = Неопределено) Экспорт
	
	Возврат БизнесПроцессыИЗадачиСервер.ПраваПравообладателейПоФайлам(Файлы, СотрудникиОтбор, ПользователиОтбор);
	
КонецФункции

#Область УстаревшиеПроцедурыИФункции

// Устарела. См. ЕстьМетодПраваПравообладателейПоФайлам
// 
// Возвращаемое значение:
//  Булево -
Функция ЕстьМетодПраваСотрудниковПоФайлам() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Устарела. См. ПраваПравообладателейПоФайлам
// 
// Параметры:
//	Файлы - Массив Из СправочникСсылка.Файлы -
//	СотрудникиОтбор - Массив Из СправочникСсылка.Сотрудники -
//					- Неопределено -
//
// Возвращаемое значение:
//  См. ДокументооборотПраваДоступа.ТаблицаПравСотрудниковПоОбъектам
Функция ПраваСотрудниковПоФайлам(Файлы, СотрудникиОтбор = Неопределено) Экспорт
	
	Возврат ПраваПравообладателейПоФайлам(Файлы, СотрудникиОтбор, Неопределено);
	
КонецФункции

#КонецОбласти // УстаревшиеПроцедурыИФункции

#КонецОбласти // ПраваДоступа

#КонецОбласти // ДляВызоваИзДругихПодсистем

// Получить структуру с описанием формы выполнения задачи.
// Вызывается при открытии формы выполнения задачи.
//
// Параметры
//   ЗадачаСсылка  - ЗадачаСсылка.ЗадачаИсполнителя - задача 
//   ТочкаМаршрутаСсылка - точка маршрута 
//
// Возвращаемое значение:
//   Структура   - структуру с описанием формы выполнения задачи.
//                 Ключ "ИмяФормы" содержит имя формы, передаваемое в метод контекста ОткрытьФорму(). 
//                 Ключ "ПараметрыФормы" содержит параметры формы. 
//
Функция ФормаВыполненияЗадачи(ЗадачаСсылка, ТочкаМаршрутаСсылка) Экспорт
	
	Если ТочкаМаршрутаСсылка = БизнесПроцессы.Утверждение.ТочкиМаршрута.Утвердить Тогда 
		ИмяФормы = "БизнесПроцесс.Утверждение.Форма.ФормаЗадачиИсполнителя";
		
	ИначеЕсли ТочкаМаршрутаСсылка = БизнесПроцессы.Утверждение.ТочкиМаршрута.Ознакомиться Тогда 
		ИмяФормы = "БизнесПроцесс.Утверждение.Форма.ФормаЗадачиОзнакомиться";
		
	КонецЕсли;	
		
	Результат = Новый Структура;
	Результат.Вставить("ПараметрыФормы", Новый Структура("Ключ", ЗадачаСсылка));
	Результат.Вставить("ИмяФормы", ИмяФормы);
	Возврат Результат;	
	
КонецФункции

// Вызывается при перенаправлении задачи.
//
// Параметры
//   ЗадачаСсылка  - ЗадачаСсылка.ЗадачаИсполнителя - перенаправляемая задача.
//   ОтключитьОбновлениеЗадач - Булево.
//
Процедура ПриПеренаправленииЗадачи(ЗадачаСсылка, ОтключитьОбновлениеЗадач = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗадачаСсылка, 
		"ТочкаМаршрута, БизнесПроцесс, Исполнитель, РольИсполнителя");
		
	ТочкаМаршрута = Реквизиты.ТочкаМаршрута;
	БизнесПроцесс = Реквизиты.БизнесПроцесс;
	
	Если ТочкаМаршрута = ТочкиМаршрута.Утвердить Тогда 
		
		БизнесПроцессОбъект = БизнесПроцесс.ПолучитьОбъект();
		БизнесПроцессОбъект.Заблокировать();
		
		Если ЗначениеЗаполнено(Реквизиты.Исполнитель) Тогда 
			БизнесПроцессОбъект.Исполнитель = Реквизиты.Исполнитель;
		Иначе
			БизнесПроцессОбъект.Исполнитель = Реквизиты.РольИсполнителя;
		КонецЕсли;	
		
		Если ОтключитьОбновлениеЗадач Тогда
			БизнесПроцессОбъект.ДополнительныеСвойства.Вставить("ОтключитьОбновлениеЗадач", Истина);
		КонецЕсли;
		
		БизнесПроцессОбъект.Записать();
		
	ИначеЕсли ТочкаМаршрута = ТочкиМаршрута.Ознакомиться Тогда 
		
		БизнесПроцессОбъект = БизнесПроцесс.ПолучитьОбъект();
		БизнесПроцессОбъект.Заблокировать();
		
		Если ЗначениеЗаполнено(Реквизиты.Исполнитель) Тогда
			БизнесПроцессОбъект.ОбрабатывающийРезультат = Реквизиты.Исполнитель;
		Иначе
			БизнесПроцессОбъект.ОбрабатывающийРезультат = Реквизиты.РольИсполнителя;
		КонецЕсли;
		
		Если ОтключитьОбновлениеЗадач Тогда
			БизнесПроцессОбъект.ДополнительныеСвойства.Вставить("ОтключитьОбновлениеЗадач", Истина);
		КонецЕсли;
		
		БизнесПроцессОбъект.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает признак наличия метода объекта ПриПеренаправленииЗадачи
// 
Функция ЕстьМетодПриПеренаправленииЗадачи() Экспорт
	Возврат Истина;
КонецФункции

// Возвращает массив пользователей переданного бизнес-процесса,
// которые должны иметь иметь права на другие бизнес-процессы, 
// для которых данный бизнес-процесс является ведущим
Функция ПользователиВедущегоБизнесПроцесса(ВедущийБизнесПроцесс) Экспорт
	
	МассивПользователей = Новый Массив;
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВедущийБизнесПроцесс, "Автор, ОбрабатывающийРезультат");
	
	Если ЗначениеЗаполнено(Реквизиты.Автор) Тогда
		МассивПользователей.Добавить(Реквизиты.Автор);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Реквизиты.ОбрабатывающийРезультат) Тогда
		МассивПользователей.Добавить(Реквизиты.ОбрабатывающийРезультат);
	КонецЕсли;

	Возврат МассивПользователей;
	
КонецФункции

// Возвращает тип шаблона бизнес-процесса, соответствующего данному процессу
Функция ТипШаблона() Экспорт
	
	Возврат "Справочник.ШаблоныУтверждения";
	
КонецФункции

// Показывает, может ли процесс запускаться через привычные интерфейсы
Функция МожетЗапускатьсяИнтерактивно() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает текстовое описание назначения процесса
Функция ПолучитьОписаниеПроцесса() Экспорт
	
	Возврат НСтр("ru = 'Используется для передачи документа на утверждение руководителю или другому сотруднику.'");
	
КонецФункции

// Проверяет, что процесс завершился удачно
Функция ПроцессЗавершилсяУдачно(Ссылка) Экспорт
	
	РеквизитыПроцесса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Ссылка,
		"Завершен, РезультатУтверждения");
	Возврат РеквизитыПроцесса.Завершен 
		И РеквизитыПроцесса.РезультатУтверждения = Перечисления.РезультатыУтверждения.Утверждено;
	
КонецФункции

// Возвращает признак наличия метода РезультатВыполненияПроцесса
Функция ЕстьМетодРезультатВыполненияПроцесса() Экспорт
	Возврат Истина;
КонецФункции

// Возвращает результат выполнения - значение перечисления ВариантыВыполненияПроцессовИЗадач
Функция РезультатВыполненияПроцесса(Ссылка) Экспорт
	
	РезультатУтверждения = 
		ОбщегоНазначенияДокументооборот.ЗначениеРеквизитаОбъектаВПривилегированномРежиме(
		Ссылка, "РезультатУтверждения");
	
	Если РезультатУтверждения = Перечисления.РезультатыУтверждения.Утверждено Тогда
		Возврат Перечисления.ВариантыВыполненияПроцессовИЗадач.Положительно;
	ИначеЕсли РезультатУтверждения = Перечисления.РезультатыУтверждения.НеУтверждено Тогда
		Возврат Перечисления.ВариантыВыполненияПроцессовИЗадач.Отрицательно;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает результат выполнения в контексте процесса.
// 
// Параметры:
//  Ссылка Ссылка
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.РезультатыУтверждения
//
Функция КонтекстныйРезультатВыполненияПроцесса(Ссылка) Экспорт
	
	Возврат 
		ОбщегоНазначенияДокументооборот.ЗначениеРеквизитаОбъектаВПривилегированномРежиме(
		Ссылка, "РезультатУтверждения");
	
КонецФункции

// Возвращает массив пользователей переданного бизнес-процесса,
// которые должны иметь иметь права на другие бизнес-процессы, 
// для которых данный бизнес-процесс является ведущим
Функция УчастникиПроцессаВлияющиеНаДоступКПодчиненнымОбъектам(Процесс) Экспорт
	
	МассивПользователей = Новый Массив;
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Процесс, "Автор, Проект, ОбрабатывающийРезультат");
	
	Если ЗначениеЗаполнено(Реквизиты.Автор) Тогда
		ДанныеУчастника = Новый Структура(
			"Участник");
		ДанныеУчастника.Участник = Реквизиты.Автор;
		МассивПользователей.Добавить(ДанныеУчастника);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Реквизиты.ОбрабатывающийРезультат) Тогда
		ДанныеУчастника = Новый Структура(
			"Участник");
		ДанныеУчастника.Участник = Реквизиты.ОбрабатывающийРезультат;
		МассивПользователей.Добавить(ДанныеУчастника);
	КонецЕсли;
	
	// Добавление руководителя проекта
	Если ЗначениеЗаполнено(Реквизиты.Проект) Тогда
		
		РуководительПроекта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Реквизиты.Проект, "Руководитель");
		Если ЗначениеЗаполнено(РуководительПроекта) Тогда
			ДанныеУчастника = Новый Структура(
				"Участник");
			ДанныеУчастника.Участник = РуководительПроекта;
			МассивПользователей.Добавить(ДанныеУчастника);
		КонецЕсли;
			
	КонецЕсли;
	
	Возврат МассивПользователей;
	
КонецФункции

// Возвращает массив всех участников процесса 
Функция ВсеУчастникиПроцесса(ПроцессСсылка) Экспорт
	
	ВсеУчастники = Новый Массив;
	
	КлючевыеУчастники = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПроцессСсылка, 
		"Автор, Исполнитель, ОбрабатывающийРезультат");
	
	// Автор
	ДанныеУчастника = Новый Структура;
	ДанныеУчастника.Вставить("Участник", КлючевыеУчастники.Автор);
	ВсеУчастники.Добавить(ДанныеУчастника);
	
	// Обрабатывающий результат
	ДанныеУчастника = Новый Структура;
	ДанныеУчастника.Вставить("Участник", КлючевыеУчастники.ОбрабатывающийРезультат);
	ВсеУчастники.Добавить(ДанныеУчастника);

	// Исполнитель
	ДанныеУчастника = Новый Структура;
	ДанныеУчастника.Вставить("Участник", КлючевыеУчастники.Исполнитель);
	ВсеУчастники.Добавить(ДанныеУчастника);
	
	Возврат ВсеУчастники;
	
КонецФункции

// Проверяет, подходит ли объект к шаблону бизнес-процесса
Функция ШаблонПодходитДляАвтозапускаБизнесПроцессаПоОбъекту(ШаблонСсылка, ПредметСсылка, Подписчик, ВидСобытия, Условие) Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает признак наличия метода ДополнительныеДанныеПоЗадаче
Функция ЕстьМетодДополнительныеДанныеПоЗадаче() Экспорт
	Возврат Истина;
КонецФункции

// Возвращает структуру дополнительных данных переданной задачи:
//	РезультатВыполнения - результат выполнения задачи
//  КонтекстВыполненияЗадачи - Контекст выполнения задачи задачи для истории
Функция ДополнительныеДанныеПоЗадаче(Задача) Экспорт
	
	СтруктураВозврата = Новый Структура("РезультатВыполнения, КонтекстВыполненияЗадачи");
		
	Если Не Задача.Выполнена Тогда
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	РезультатВыполнения = Неопределено;
	
	ТочкаМаршрута = Задача.ТочкаМаршрута;
	ТочкиМаршрутаПроцесса = БизнесПроцессы.Утверждение.ТочкиМаршрута;
	
	РезультатДействия = Справочники.РезультатыДействийЗадач.ПустаяСсылка();
	ТекстРезультатаВыполнения = Задача.РезультатВыполнения;
	
	Если ТочкаМаршрута = ТочкиМаршрутаПроцесса.Утвердить Тогда
		
		РезультатВыполнения = РезультатВыполненияЗадачи(Задача);
		Если РезультатВыполнения = Перечисления.ВариантыВыполненияПроцессовИЗадач.Положительно Тогда
			РезультатДействия = Справочники.РезультатыДействийЗадач.Утверждено;
		ИначеЕсли РезультатВыполнения = Перечисления.ВариантыВыполненияПроцессовИЗадач.Отрицательно Тогда
			РезультатДействия = Справочники.РезультатыДействийЗадач.НеУтверждено;
		КонецЕсли;
		
	ИначеЕсли ТочкаМаршрута = ТочкиМаршрутаПроцесса.Ознакомиться Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	УтверждениеРезультатыОзнакомлений.ОтправленоНаПовторноеУтверждение
			|ИЗ
			|	БизнесПроцесс.Утверждение.РезультатыОзнакомлений КАК УтверждениеРезультатыОзнакомлений
			|ГДЕ
			|	УтверждениеРезультатыОзнакомлений.ЗадачаИсполнителя = &ЗадачаИсполнителя");
			
		Запрос.УстановитьПараметр("ЗадачаИсполнителя", Задача.Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			Если Выборка.ОтправленоНаПовторноеУтверждение Тогда
				РезультатДействия = Справочники.РезультатыДействийЗадач.ОтправленоПовторно;
			Иначе
				Если Задача.БизнесПроцесс.РезультатУтверждения = Перечисления.РезультатыУтверждения.НеУтверждено Тогда
					РезультатДействия = Справочники.РезультатыДействийЗадач.Завершил;
				Иначе
					РезультатДействия = Справочники.РезультатыДействийЗадач.Ознакомился;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеИсполнителяДляИсторииВыполнения = Задача.ДанныеИсполнителяДляИсторииВыполнения();
	
	КонтекстВыполненияЗадачи = РегистрыСведений.ИсторияЗадач.КонтекстВыполнения();
	КонтекстВыполненияЗадачи.Исполнитель = ДанныеИсполнителяДляИсторииВыполнения.Исполнитель;
	КонтекстВыполненияЗадачи.ПлановыйИсполнитель = ДанныеИсполнителяДляИсторииВыполнения.ПлановыйИсполнитель;
	КонтекстВыполненияЗадачи.ОснованиеФактическогоИсполнителя =
		ДанныеИсполнителяДляИсторииВыполнения.ОснованиеФактическогоИсполнителя;
	КонтекстВыполненияЗадачи.ТекстРезультатаВыполнения = ТекстРезультатаВыполнения;
	КонтекстВыполненияЗадачи.ПриложенияРезультатаВыполнения = 
		РаботаСЗадачами.СлепокПриложенийРезультатаПоИсточнику(Задача.Ссылка);
	КонтекстВыполненияЗадачи.РезультатДействия = РезультатДействия;
	КонтекстВыполненияЗадачи.ФактическийИсполнитель = 
		ДанныеИсполнителяДляИсторииВыполнения.ФактическийИсполнитель;
	
	СтруктураВозврата.РезультатВыполнения = РезультатВыполнения;
	СтруктураВозврата.КонтекстВыполненияЗадачи = КонтекстВыполненияЗадачи;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Возвращает признак наличия метода РезультатВыполненияЗадачи
//
// Возвращаемое значение:
//  Булево
//
Функция ЕстьМетодРезультатВыполненияЗадачи() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает результат выполнения задачи.
//
// Параметры:
//  Задача - ЗадачаОбъект.ЗадачаИсполнителя,
//           ЗадачаСсылка.ЗадачаИсполнителя
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ВариантыВыполненияПроцессовИЗадач,
//  Неопределено
//
Функция РезультатВыполненияЗадачи(Задача) Экспорт
	
	РезультатВыполнения = Неопределено;
	
	Если ТипЗнч(Задача) = Тип("ЗадачаОбъект.ЗадачаИсполнителя") Тогда
		РеквизитыЗадачи = Задача;
	Иначе
		РеквизитыЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Задача, "Выполнена, ТочкаМаршрута, БизнесПроцесс, Ссылка");
	КонецЕсли;
	
	Если РеквизитыЗадачи.Выполнена = Истина
		И РеквизитыЗадачи.ТочкаМаршрута = ТочкиМаршрута.Утвердить Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	УтверждениеРезультатыУтверждения.РезультатУтверждения КАК РезультатУтверждения
			|ИЗ
			|	БизнесПроцесс.Утверждение.РезультатыУтверждения КАК УтверждениеРезультатыУтверждения
			|ГДЕ
			|	УтверждениеРезультатыУтверждения.Ссылка = &Процесс
			|	И УтверждениеРезультатыУтверждения.ЗадачаИсполнителя = &ЗадачаПроцесса";
		Запрос.УстановитьПараметр("Процесс", РеквизитыЗадачи.БизнесПроцесс);
		Запрос.УстановитьПараметр("ЗадачаПроцесса", РеквизитыЗадачи.Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.РезультатУтверждения = Перечисления.РезультатыУтверждения.Утверждено Тогда
				РезультатВыполнения = Перечисления.ВариантыВыполненияПроцессовИЗадач.Положительно;
			ИначеЕсли Выборка.РезультатУтверждения = Перечисления.РезультатыУтверждения.НеУтверждено Тогда
				РезультатВыполнения = Перечисления.ВариантыВыполненияПроцессовИЗадач.Отрицательно;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РезультатВыполнения;
	
КонецФункции

// Возвращает массив структур, содержащих описания участников.
// Состав структуры:
//   ТабличнаяЧасть - имя ТЧ, в которой хранятся данные участников. Если данные хранятся в шапке, этот ключ отсутствует.
//   ИмяУчастника - имя реквизита шапки или ТЧ, в котором хранится ссылка на участника.
//   ВлияетНаДоступКПодчиненнымОбъектам - признак, указывающий на необходимость пересчета прав 
//   задач и дочерних процессов при изменении данного участника.
//
Функция ЗаполнитьОписанияУчастников() Экспорт
	
	МассивОписанийУчастников = Новый Массив;
	
	// Автор
	МассивОписанийУчастников.Добавить(Новый Структура(
		"ИмяУчастника,
		|ВлияетНаДоступКПодчиненнымОбъектам", 
		"Автор",
		Истина));
	
	// Исполнитель
	МассивОписанийУчастников.Добавить(Новый Структура(
		"ИмяУчастника,
		|ВлияетНаДоступКПодчиненнымОбъектам", 
		"Исполнитель",
		Ложь));
	
	// Обрабатывающий результат
	МассивОписанийУчастников.Добавить(Новый Структура(
		"ИмяУчастника,
		|ВлияетНаДоступКПодчиненнымОбъектам", 
		"ОбрабатывающийРезультат",
		Истина));
		
	// Проект
	МассивОписанийУчастников.Добавить(Новый Структура(
		"ИмяУчастника,
		|ВлияетНаДоступКПодчиненнымОбъектам",
		"Проект",
		Ложь));
		
	Возврат МассивОписанийУчастников;
	
КонецФункции

// Возвращает возможность выполнения задачи процесса по почте.
// 
// Параметры:
//  РеквизитыЗадачи - Структура
//    * БизнесПроцесс - БизнесПроцессСсылка.Утверждение
//    * ТочкаМаршрута - ТочкаМаршрутаБизнесПроцессаСсылка.Утверждение
//    * Ссылка - ЗадачаИсполнителя - ссылка на задачу процесса.
//
// Возвращаемое значение:
//  Булево
//
Функция ВозможноВыполнениеЗадачиПроцессаПоПочте(РеквизитыЗадачи) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если РеквизитыЗадачи.ТочкаМаршрута = ТочкиМаршрута.Утвердить
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыЗадачи.БизнесПроцесс, "ПодписыватьЭП") Тогда
		
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Возвращает структуру с вариантами ответов для формирования уведомлений
// с возможностью исполнения задач по почте. Варианты ответов определяются в
// зависимости от точки маршрута.
//
// Параметры:
//	ЗадачаСсылка - ЗадачаСсылка.ЗадачаИсполнителя - задача для которой определяются
//				   варианты ответов.
//	БизнесПроцессСсылка - БизнесПроцессСсылка.Утверждение - бизнес процесс по которому
//						  назначена задача.
//	ТочкаМаршрута - ТочкаМаршрутаБизнесПроцессаСсылка - точка маршрута в которой находится
//					БизнесПроцесс
//	КодЯзыкаПолучателя - Строка.
//
// Возвращаемое значение:
//  Структура
//	 * СписокВариантовОтветов - СписокЗначений - список значений типа
//								ПеречисленияСсылка.ВариантыВыполненияПроцессовИЗадач,
//								с заполненным представлением; в нем содержатся варианты
//								ответов.
//	 * ИспользоватьКомментарий - Булево - Принимает значение Истина, если для текущей задачи
//								 ввод комментария обязателен.
//
Функция ВариантыОтветовДляВыполненияЗадачиПоПочте(
	ЗадачаСсылка,
	БизнесПроцессСсылка,
	ТочкаМаршрута,
	КодЯзыкаПолучателя) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("СписокВариантовОтветов", Новый СписокЗначений);
	Результат.Вставить("ИспользоватьКомментарий", Ложь);
	
	Если ТочкаМаршрута = ТочкиМаршрута.Утвердить Тогда
		
		Результат.СписокВариантовОтветов.Добавить(
			Перечисления.ВариантыВыполненияПроцессовИЗадач.Положительно,
			НСтр("ru = 'Утверждено'", КодЯзыкаПолучателя));
		Результат.СписокВариантовОтветов.Добавить(
			Перечисления.ВариантыВыполненияПроцессовИЗадач.Отрицательно,
			НСтр("ru = 'Не утверждено'", КодЯзыкаПолучателя));
			
		Результат.ИспользоватьКомментарий = Истина;
		
	ИначеЕсли ТочкаМаршрута = ТочкиМаршрута.Ознакомиться Тогда
		
		НаименованиеПоложительногоРезультата = НСтр("ru = 'Ознакомился'", КодЯзыкаПолучателя);
		НаименованиеОтрицательногоРезультата = "";
		
		РезультатУтверждения = 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БизнесПроцессСсылка, "РезультатУтверждения");
		
		Если РезультатУтверждения = Перечисления.РезультатыУтверждения.НеУтверждено Тогда
			НаименованиеПоложительногоРезультата = НСтр("ru = 'Завершить утверждение'", КодЯзыкаПолучателя);
			НаименованиеОтрицательногоРезультата = НСтр("ru = 'Повторить утверждение'", КодЯзыкаПолучателя);
		КонецЕсли;
		
		Результат.СписокВариантовОтветов.Добавить(
			Перечисления.ВариантыВыполненияПроцессовИЗадач.Положительно,
			НаименованиеПоложительногоРезультата);
		
		Если ЗначениеЗаполнено(НаименованиеОтрицательногоРезультата) Тогда
			
			Результат.СписокВариантовОтветов.Добавить(
				Перечисления.ВариантыВыполненияПроцессовИЗадач.Отрицательно,
				НаименованиеОтрицательногоРезультата);
				
			Результат.ИспользоватьКомментарий = Истина;
			
		КонецЕсли;
	Иначе
		ВызватьИсключение 
			НСтр("ru = 'Неожиданная точка маршрута задачи.'", КодЯзыкаПолучателя);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает участника процесса, которого необходимо ознакомить с результатом завершения процесса.
//
// Параметры:
//	Процесс - БизнесПроцессСсылка, БизнесПроцессОбъект - Процесс, для которого необходимо получить ознакамливаемого c
//														 результатом завершения процесса.
//
// Возвращаемое значение:
//	СправочникСсылка.Сотрудники, СправочникСсылка.ПолныеРоли
//
Функция ОзнакамливаемыйСРезультатом(Процесс) Экспорт

	Если ТипЗнч(Процесс) = Тип("БизнесПроцессОбъект.Утверждение") Тогда
		Возврат Процесс.ОбрабатывающийРезультат;
	Иначе
		Возврат ОбщегоНазначенияДокументооборот.ЗначениеРеквизитаОбъектаВПривилегированномРежиме(Процесс, "ОбрабатывающийРезультат");
	КонецЕсли;
		
КонецФункции

// Возвращает описание результата завершения процесса.
//
// Параметры:
//	Процесс - БизнесПроцессСсылка - Процесс, для которого необходимо получить описание результата завершения.
//	КодЯзыка - Строка - Код языка пользователя
//
// Возвращаемое значение:
//	Строка - Описание результата завершения процесса.
//
Функция ОписаниеРезультатаЗавершенияПроцесса(Процесс, КодЯзыка) Экспорт

	ОписаниеРезультата = "";
	
	РезультатУтверждения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Процесс, "РезультатУтверждения");
	Если РезультатУтверждения = Перечисления.РезультатыУтверждения.Утверждено Тогда 
		ОписаниеРезультата = НСтр("ru = 'Результат утверждения: Утверждено'", КодЯзыка);
	Иначе
		ОписаниеРезультата = НСтр("ru = 'Результат утверждения: Не утверждено'", КодЯзыка);
	КонецЕсли;
	
	Возврат ОписаниеРезультата;
	
КонецФункции

// ВерсионированиеОбъектов
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры
// Конец ВерсионированиеОбъектов

#КонецЕсли
